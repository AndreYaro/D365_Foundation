<?xml version="1.0" encoding="utf-8"?>
<AxMap xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesATPSettingsMap</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class SalesATPSettingsMap extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initDefault</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes all ATP parameters with their default values.
    /// </summary>
    public void initDefault()
    {
        this.atpTimeFence                   = 0;
        this.atpBackwardDemandTimeFence     = 0;
        this.atpBackwardSupplyTimeFence     = 0;
        this.atpApplyDemandTimeFence        = 0;
        this.atpApplySupplyTimeFence        = 0;
        this.DeliveryDateControlType        = SalesDeliveryDateControlType::None;
        this.atpInclPlannedOrders           = false;
        this.IsItemSpecific                 = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInventSetup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes all ATP parameters from a given <c>InventItemInventSetup</c> table buffer.
    /// </summary>
    /// <param name="_inventSetup">
    ///    The <c>InventItemInventSetup</c> table buffer from which ATP parameters will be read.
    /// </param>
    public void initFromInventSetup(InventItemInventSetup _inventSetup)
    {
        this.data(_inventSetup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromSalesParameters</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes all ATP parameters from a given <c>SalesParameters</c> table buffer.
    /// </summary>
    /// <param name="_salesParameters">
    ///    <c>SalesParameters</c> table buffer from which ATP parameters will be read; optional.
    /// </param>
    public void initFromSalesParameters(SalesParameters _salesParameters = SalesParameters::find())
    {
        this.data(_salesParameters);
        this.IsItemSpecific = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromSalesSetup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes all ATP parameters from a given <c>InventItemSalesSetup</c> table buffer.
    /// </summary>
    /// <param name="_salesSetup">
    ///    <c>InventItemSalesSetup</c> table buffer from which ATP parameters will be read.
    /// </param>
    public void initFromSalesSetup(InventItemSalesSetup _salesSetup)
    {
        this.data(_salesSetup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromTransferLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the current <c>SalesATPSettingsMap</c> map with settings from the specified
    ///    <c>InventTransferLine</c> record.
    /// </summary>
    /// <param name="_inventTransferLine">
    ///    A <c>InventTransferLine</c> record.
    /// </param>
    public void initFromTransferLine(InventTransferLine _inventTransferLine)
    {
        this.data(_inventTransferLine);
        this.IsItemSpecific = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateFieldDeliveryDateControlType</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks the delivery date control type.
    /// </summary>
    /// <param name="_inventTable">
    ///    A <c>InventTable</c> record that represents the item and is related to the
    ///    <c>SalesATPSettingsMap</c> map.
    /// </param>
    /// <returns>
    ///    true if the delivery date control value is valid; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    The service items and non-inventoried lines can not have a delivery date control value of 'CTP'.
    /// </remarks>
    public boolean validateFieldDeliveryDateControlType(
        InventTable     _inventTable = null)
    {
        Common      actualTable = this;
        boolean     ok          = true;

        switch (true)
        {
            case   _inventTable
                && _inventTable.inventItemType().isServiceItem()
                && (this.DeliveryDateControlType == SalesDeliveryDateControlType::CTP
                || (this.DeliveryDateControlType == SalesDeliveryDateControlType::FullRunCTP && MpsDeliveryDateControlTypeCTPHelper::isMpsOnlineCTPFeatureEnabledWithPlanningService())):

                ok = checkFailed(strfmt("@SYS123070",this.DeliveryDateControlType));
                break;

            case     _inventTable
                && (InventModelGroupItem::findByItemIdLegalEntity(_inventTable.ItemId, _inventTable.dataAreaId)
                && ! _inventTable.isStocked())
                && (this.DeliveryDateControlType == SalesDeliveryDateControlType::CTP
                || (this.DeliveryDateControlType == SalesDeliveryDateControlType::FullRunCTP && MpsDeliveryDateControlTypeCTPHelper::isMpsOnlineCTPFeatureEnabledWithPlanningService())):

                ok = checkFailed(strfmt("@SYS190860",this.DeliveryDateControlType));
                break;

            case    actualTable is InventItemInventSetup
                && (this.DeliveryDateControlType == SalesDeliveryDateControlType::CTP
                || (this.DeliveryDateControlType == SalesDeliveryDateControlType::FullRunCTP && MpsDeliveryDateControlTypeCTPHelper::isMpsOnlineCTPFeatureEnabledWithPlanningService()))
                &&  this.IsItemSpecific:

                ok = checkFailed(strfmt("@SYS321137",this.DeliveryDateControlType));
                break;

            case    actualTable is InventTransferLine
                && (this.DeliveryDateControlType == SalesDeliveryDateControlType::CTP
                || (this.DeliveryDateControlType == SalesDeliveryDateControlType::FullRunCTP && MpsDeliveryDateControlTypeCTPHelper::isMpsOnlineCTPFeatureEnabledWithPlanningService())):

                ok = checkFailed(strfmt("@SYS321137",this.DeliveryDateControlType));
                break;
        }

        return ok;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<DeveloperDocumentation>@SYS321139</DeveloperDocumentation>
	<Label>@SYS119195</Label>
	<FieldGroups />
	<Fields>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldInt">
			<Name>ATPApplyDemandTimeFence</Name>
			<ExtendedDataType>SalesATPApplyDemandTimeFence</ExtendedDataType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldInt">
			<Name>ATPApplySupplyTimeFence</Name>
			<ExtendedDataType>SalesATPApplySupplyTimeFence</ExtendedDataType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldInt">
			<Name>ATPBackwardDemandTimeFence</Name>
			<ExtendedDataType>SalesATPBackwardDemandTimeFence</ExtendedDataType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldInt">
			<Name>ATPBackwardSupplyTimeFence</Name>
			<ExtendedDataType>SalesATPBackwardSupplyTimeFence</ExtendedDataType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldEnum">
			<Name>ATPInclPlannedOrders</Name>
			<ExtendedDataType>SalesATPInclPlannedOrders</ExtendedDataType>
			<EnumType>boolean</EnumType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldInt">
			<Name>ATPTimeFence</Name>
			<ExtendedDataType>SalesATPTimeFence</ExtendedDataType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldEnum">
			<Name>DeliveryDateControlType</Name>
			<EnumType>SalesDeliveryDateControlType</EnumType>
		</AxMapBaseField>
		<AxMapBaseField xmlns=""
			i:type="AxMapFieldEnum">
			<Name>IsItemSpecific</Name>
			<Label>@SYS128998</Label>
			<EnumType>NoYes</EnumType>
		</AxMapBaseField>
	</Fields>
	<Mappings />
</AxMap>