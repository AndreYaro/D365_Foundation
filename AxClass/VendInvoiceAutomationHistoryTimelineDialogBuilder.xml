<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceAutomationHistoryTimelineDialogBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Vendor invoice implementation of the document event history timeline dialog builder.
/// </summary>
[Export(identifierStr(Dynamics.AX.Application.ITimelineDialogBuilder))]
[ExportMetadata(classStr(ITimelineDialogBuilder), menuItemDisplayStr(VendInvoiceAutomationHistoryTimelineDialog))]
internal final class VendInvoiceAutomationHistoryTimelineDialogBuilder extends DocumentEventHistoryBaseTimelineDialogBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDocument</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected boolean validateDocument(Common _document)
    {
        // Ensure the given document is a vendor invoice or, if posted, a vendor invoice journal
        return super(_document) && (_document is VendInvoiceInfoTable || _document is VendInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildTimelineDialogConfigurationForDocument</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected TimelineDialogConfiguration buildTimelineDialogConfigurationForDocument(Common _document)
    {
        VendInvoiceInfoTable vendorInvoice = 
            VendInvoiceAutomationHistoryTimelineHelper::getVendorInvoiceFromAutomationHistoryDocument(_document);
        
        TimelineDialogConfiguration timelineDialogConfiguration = super(_document);
        if (VendInvoiceIncludeManualCreatedInvoiceIntoAutomationFeature::isEnabled())
        {
            timelineDialogConfiguration.parmFormCaption("@AccountsPayable:VendInvoiceDocumentEventHistoryTimelineDialogCaptionV2");
        }
        else
        {
            timelineDialogConfiguration.parmFormCaption("@AccountsPayable:VendInvoiceDocumentEventHistoryTimelineDialogCaption");
        }

        this.addVendorInvoiceNumberToDialogFormContext(timelineDialogConfiguration, vendorInvoice);
        this.addLinkToVendorInvoiceWorkflowToDialogFormContext(timelineDialogConfiguration, vendorInvoice);

        return timelineDialogConfiguration;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildTimelineContractForDocument</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected TimelineContract buildTimelineContractForDocument(Common _document)
    {
        TimelineContract timelineContractForDocument = super(_document);

        // Ensure that timeline entry bodies are not rendered
        timelineContractForDocument.parmTimelineConfiguration().parmShouldShowEntryBodies(false);

        // Attempt to open a CES survey for the AP Automation feature
        SysCESClient::PostEligibility(
            VendInvoiceAutomationConstants::SurveyTeamName, 
            VendInvoiceAutomationConstants::SurveyName,
            VendInvoiceAutomationConstants::SurveyEventName,
            VendInvoiceAutomationConstants::SurveyMessage);

        return timelineContractForDocument;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addVendorInvoiceNumberToDialogFormContext</Name>
				<Source><![CDATA[
    private void addVendorInvoiceNumberToDialogFormContext(
        TimelineDialogConfiguration _timelineDialogConfiguration,
        VendInvoiceInfoTable _vendorInvoice)
    {
        _timelineDialogConfiguration.getDialogFormContext()
                                    .clearContextElements()
                                    .addText(strFmt("@AccountsPayable:VendInvoiceTimelineDialogInvoiceContext", _vendorInvoice.Num));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addLinkToVendorInvoiceWorkflowToDialogFormContext</Name>
				<Source><![CDATA[
    private void addLinkToVendorInvoiceWorkflowToDialogFormContext(
        TimelineDialogConfiguration _timelineDialogConfiguration,
        VendInvoiceInfoTable _vendorInvoice)
    {
        // If the vendor invoice is in workflow, a link to that workflow should be added to the
        // automation history timeline dialog's form context
        if (_vendorInvoice.RequestStatus != VendInvoiceRequestStatus::Draft 
         && VendInvoiceAutomationStateManagement::isInvoiceInAutomatedWorkflow(_vendorInvoice))
        {
            WorkflowTrackingStatusTable vendorInvoiceTrackingStatus = Workflow::findTrackingStatusForDocument(_vendorInvoice);

            if (vendorInvoiceTrackingStatus)
            {
                // Build arguments expected by the workflow history timeline dialog
                Args workflowHistoryTimelineDialogArguments = new Args();
                workflowHistoryTimelineDialogArguments.record(_vendorInvoice);
                workflowHistoryTimelineDialogArguments.menuItemName(menuItemDisplayStr(VendInvoiceWorkflowHistoryTimelineDialog));
                workflowHistoryTimelineDialogArguments.menuItemType(MenuItemType::Display);

                // Add the workflow history link to the form context
                _timelineDialogConfiguration.getDialogFormContext()
                                            .addMenuItemLink(
                                                vendorInvoiceTrackingStatus.InstanceNumber,
                                                "@AccountsPayable:VendInvoiceAutomationHistoryWorkflowContextLinkCaption",
                                                menuItemDisplayStr(VendInvoiceWorkflowHistoryTimelineDialog),
                                                MenuItemType::Display,
                                                workflowHistoryTimelineDialogArguments);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>