<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailTransactionTableInventoryAdjustmentOffsetHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class RetailTransactionTableInventoryAdjustmentOffsetHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>RetailTransactionTable_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(RetailTransactionTable), DataEventType::Updated), SuppressBPWarningAttribute('BPParameterNotUsed', 'No use for DataEventArgs')]
    public static void RetailTransactionTable_onUpdated(Common sender, DataEventArgs e)
    {
        RetailTransactionTable transactionTable = sender as RetailTransactionTable;
        if (!(transactionTable.salesOrderId && !transactionTable.orig().salesOrderId))
        {
            return;
        }

        if (!RetailInventoryServiceIntegrationHelper::isUsingInventoryServiceAsDataSource())
        {
            return;
        }

        boolean skip = true;
        if (transactionTable.type == RetailTransactionType::AsyncCustomerOrder)
        {
            RetailFunctionalityProfile functionalityProfile;
            RetailStoreTable storeTable;
            select firstonly profileId, CreateCustomerOrdersWithAsyncFallback
                from functionalityProfile
                exists join storeTable
                where storeTable.functionalityProfile == functionalityProfile.profileId
                    && storeTable.RecId == transactionTable.Channel;

            skip = functionalityProfile.CreateCustomerOrdersWithAsyncFallback == NoYes::Yes;
        }
        else if (transactionTable.type == RetailTransactionType::PendingSalesOrder)
        {
            skip = RetailParameters::find().UseRTSForOnlineOrderCreation == NoYes::Yes;
        }

        if (skip)
        {
            return;
        }

        RetailInventoryAdjustmentOffsetHelper::ProcessInventoryAdjustmentOffset(transactionTable);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>