<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetLocationTransfer_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Asset location transfer for China
/// </summary>
class AssetLocationTransfer_CN
{
    private const str FeatureReference = 'CN-00017';

    AssetLocationId previousLocation;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initial instance of class <c>AssetLocationTransfer_CN</c>
    /// </summary>
    /// <param name="_origLocation">
    /// Asset location ID
    /// </param>
    public void new(AssetLocationId _origLocation)
    {
        previousLocation = _origLocation;
        GlobalizationInstrumentationHelper::featureRun(FeatureReference, funcName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfer a fixed asset to a new location
    /// </summary>
    /// <param name="_assetId">
    /// Fixed asset ID
    /// </param>
    /// <param name="_locationId">
    /// Asset location ID
    /// </param>
    /// <param name="_transferDate">
    /// Transfer date
    /// </param>
    /// <param name="_comment">
    /// Transfer comment
    /// </param>
    /// <param name="_confirmPopup">
    /// True to pop up confirm dialog
    /// </param>
    /// <returns>
    /// Return true when the transfer operation succeeds
    /// </returns>
    public boolean transferLocation(AssetId _assetId, AssetLocationId _locationId,
        TransferDate _transferDate, TransferComment _comment, boolean _confirmPopup = false)
    {
        boolean         ret;
        boolean         confirm = true;
        int             lastId;
        Args            args;
        FormRun         formRun;
        Object          formRunObj;
        MenuFunction    menuFunction;
        AssetTable      assetTable;
        AssetLocationTransferHistory_CN     locationTransferHistory, locationHistory;

        if (_locationId != previousLocation)
        {
            assetTable = AssetTable::find(_assetId, true);
            if (assetTable)
            {
                locationTransferHistory.initValue();
                locationTransferHistory.AssetId = _assetId;
                locationTransferHistory.TransferDate = _transferDate;
                locationTransferHistory.FromLocation = previousLocation;
                locationTransferHistory.ToLocation = _locationId;
                locationTransferHistory.TransferComment = _comment;

                if (_confirmPopup)
                {
                    args = new Args();
                    args.record(locationTransferHistory);
                    args.caller(this);

                    menuFunction = new MenuFunction(menuitemDisplayStr(AssetLocationTransferConfirm_CN), MenuItemType::Display);

                    formRun = menuFunction.create(args);
                    formRun.run();
                    formRun.wait(true);
                    formRunObj = formRun;

                    if (SysFormRun::hasMethod(formRun, identifierstr(paramIsTransferConfirmed)))
                    {
                        confirm = formRunObj.paramIsTransferConfirmed();
                    }
                }

                if (confirm)
                {
                    select maxof(Id) from locationHistory where locationHistory.AssetId == _assetId;
                    if (locationHistory)
                    {
                        lastId = locationHistory.Id;
                    }
                    locationTransferHistory.Id = lastId + 1;

                    ttsBegin;
                    assetTable.Location = _locationId;
                    assetTable.update();
                    locationTransferHistory.insert();
                    ttsCommit;
                    ret = true;
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>