<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestOrigin</Name>
	<SourceCode>
		<Declaration><![CDATA[
public abstract class CFMPaymentRequestOrigin extends RunBase
{
    CFMPaymentRequestTable requestTable;
    str menuFunctionName;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CFMPaymentRequestOrigin construct(CFMPaymentRequestTable _paymentRequest)
    {
        CFMPaymentRequestOrigin requestOrigin;

        if (_paymentRequest)
        {
            CFMPaymentRequestSourceTypeAttribute extensionAttribute = new CFMPaymentRequestSourceTypeAttribute(_paymentRequest.SourceType);
            requestOrigin = SysExtensionAppClassFactory::getClassFromSysAttribute(classStr(CFMPaymentRequestOrigin), extensionAttribute);

            if (!requestOrigin)
            {
                throw error(Error::wrongUseOfFunction(funcName()));
            }

            requestOrigin.parmPaymentRequestTable(_paymentRequest);
        }

        return requestOrigin;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (!_args || !_args.record() || _args.record().TableId != tableNum(CFMPaymentRequestTable))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        CFMPaymentRequestOrigin requestOrigin = CFMPaymentRequestOrigin::construct(_args.record());

        if (requestOrigin)
        {
            requestOrigin.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>menuFunctionName</Name>
				<Source><![CDATA[
    abstract protected str menuFunctionName()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymentRequestTable</Name>
				<Source><![CDATA[
    public CFMPaymentRequestTable parmPaymentRequestTable(CFMPaymentRequestTable _requestTable = requestTable)
    {
        requestTable = _requestTable;

        return requestTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        Args args = new Args();

        args.name(this.menuFunctionName());
        args.record(requestTable);

        changeCompany(requestTable.SourceDataAreaId)
        {
            FormRun formRun = ClassFactory.formRunClass(args);
            formRun.init();
            formRun.run();
            formRun.wait();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>