<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxIntegrationVendInvoiceJourDataRetrieval</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxIntegrationVendInvoiceJourDataRetrieval</c> class retrieves data from <c>VendInvoiceJour</c>, etc.
/// </summary>
[TaxIntegrationDataRetrieval(tableStr(VendInvoiceJour))]
public class TaxIntegrationVendInvoiceJourDataRetrieval
    extends TaxIntegrationAbstractDataRetrievalTemplate
{
    protected VendInvoiceJour vendInvoiceJour;
    protected TransportationDocument transportationDocument;
    protected PurchTable purchTable;
    protected VendTable vendTable;
    protected VendTable vendTableInvoice;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDocumentQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the document.
    /// </summary>
    /// <returns>The query for the document.</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentQueryObject()
    {
        return SysDaQueryObjectBuilder::from(this.vendInvoiceJour)
            .where(this.vendInvoiceJour, fieldStr(VendInvoiceJour, RecId))
                .isEqualToLiteral(this.document.getLocalRecId())
            .outerJoin(this.transportationDocument) // primary key
                .where(this.transportationDocument, fieldStr(TransportationDocument, RecId))
                    .isEqualTo(this.vendInvoiceJour, fieldStr(VendInvoiceJour, TransportationDocument))
            .outerJoin(this.vendTable) // alternate key
                .where(this.vendTable, fieldStr(VendTable, AccountNum))
                    .isEqualTo(this.vendInvoiceJour, fieldStr(VendInvoiceJour, OrderAccount))
            .outerJoin(this.vendTableInvoice) // alternate key
                .where(this.vendTableInvoice, fieldStr(VendTable, AccountNum))
                    .isEqualTo(this.vendInvoiceJour, fieldStr(VendInvoiceJour, InvoiceAccount))
            .outerJoin(this.purchTable)
                .where(this.purchTable, fieldStr(PurchTable, PurchId))
                    .isEqualTo(this.vendInvoiceJour, fieldStr(VendInvoiceJour, PurchId))
            .toSysDaQueryObject();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the lines of the document.
    /// </summary>
    /// <returns>The query for the lines of the document.</returns>
    [Replaceable]
    protected SysDaQueryObject getLineQueryObject()
    {
        // Here return null since only need header info to determine some info (e.g. tax-related cash discount parameters).
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of the document.
    /// </summary>
    /// <returns>The query for the charges of the document.</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentChargeQueryObject()
    {
        // Here return null since only need header info to determine some info (e.g. tax-related cash discount parameters).
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of each line.
    /// </summary>
    /// <returns>The query for the charges of each line.</returns>
    [Replaceable]
    protected SysDaQueryObject getLineChargeQueryObject()
    {
        // Here return null since only need header info to determine some info (e.g. tax-related cash discount parameters).
        return null;

    }

]]></Source>
			</Method>
			<Method>
				<Name>constructLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a line of the document.
    /// </summary>
    /// <returns>The constructed line of the document.</returns>
    protected TaxIntegrationLineObject constructLine()
    {
        // Here return null since only need header info to determine some info (e.g. tax-related cash discount parameters).
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyToDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies to the document.
    /// </summary>
    protected void copyToDocument()
    {
        // For now, only header info is needed for Vendor invouce journal, so skip document charge and line.
        document.setShouldSkipDocumentCharge(true);
        document.setShouldSkipLine(true);

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyToDocumentByDefault</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies defaults to the document.
    /// </summary>
    protected void copyToDocumentByDefault()
    {
        super();

        document.setDirection(TaxIntegrationDirection::Purchase);
        document.setInvoiceId(this.vendInvoiceJour.InvoiceId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyToDocumentFromHeaderTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies to the document from <c>SalesTable</c>.
    /// </summary>
    protected void copyToDocumentFromHeaderTable()
    {
        super();

        document.setIncludingTax(this.vendInvoiceJour.InclTax);
        document.setOrderAccount(this.vendInvoiceJour.OrderAccount);
        document.setOrderAccountGroup(this.vendInvoiceJour.VendGroup);
        document.setInvoiceAccount(this.vendInvoiceJour.InvoiceAccount);
        document.setInvoiceAccountGroup(this.vendTableInvoice.VendGroup);
        document.setTransactionCurrencyCode(this.vendInvoiceJour.CurrencyCode);
        document.setDeliveryTerm(this.vendInvoiceJour.DlvTerm);
        document.setListCode(this.vendInvoiceJour.Listcode);
        document.setPurchaseType(this.vendInvoiceJour.PurchaseType);
        document.setDirectDelivery(this.purchTable.MCRDropShipment);
        document.setIntercompanyDirectDelivery(this.purchTable.InterCompanyDirectDelivery);
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies to the document's address.
    /// </summary>
    protected void copyAddressToDocument()
    {
        super();

        LogisticsPostalAddress shipmentOriginLogisticsPostalAddress = TaxIntegrationLogisticsPostalAddressUtility::retrieveAddressFromLadingAddressOrVendor(
            this.transportationDocument,
            this.vendTable,
            LogisticsLocationRoleType::Delivery);
        LogisticsPostalAddress shipmentDestinationLogisticsPostalAddress = LogisticsPostalAddress::findRecId(this.vendInvoiceJour.DeliveryPostalAddress);

        this.copyToDocumentFromShipmentAddressWithCountryRegionType(
            shipmentOriginLogisticsPostalAddress,
            shipmentDestinationLogisticsPostalAddress);

        LogisticsPostalAddress invoiceOriginLogisticsPostalAddress = TaxIntegrationLogisticsPostalAddressUtility::retrieveAddressFromVendor(
            this.vendTableInvoice,
            LogisticsLocationRoleType::Invoice);
        LogisticsPostalAddress invoiceDestinationLogisticsPostalAddress = TaxIntegrationLogisticsPostalAddressUtility::retrieveAddressFromCompanyInformation(
            CompanyInfo::find(),
            LogisticsLocationRoleType::Invoice);

        this.copyToDocumentFromInvoiceAddressWithCountryRegionType(
            invoiceOriginLogisticsPostalAddress,
            invoiceDestinationLogisticsPostalAddress);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>