<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SettlementPair_CustPaym</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Represents a single debit-credit relationship during settlement. Debit is usually an invoice while credit is usually a payment.
/// Relationship between debits and credits is many-to-many, therefore single invoice or single payment can be partially present in multiple SettlementPair relationships.
/// </summary>
/// <remarks>
/// This class should be instantiated and used only from inside <c>CustVendSettle</c> class.
/// Additional functionality currently present on <c>CustVendSettle</c> class will be moved to <c>SettlementPair</c> class in further releases.
/// Get and set methods on this class exposing individual table buffers are going to be removed in subsequent releases in favor of more coarse operations.
/// </remarks>
public class SettlementPair_CustPaym extends SettlementPair_Cust
{
    private boolean isAutoSettled;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setIsAutoSettled</Name>
				<Source><![CDATA[
    public void setIsAutoSettled(boolean _isAutoSettled)
    {
        isAutoSettled = _isAutoSettled;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetchInvoice</Name>
				<Source><![CDATA[
    public boolean fetchInvoice(dataAreaId _company, tableId _tableId, RecId _recId)
    {
        boolean recordFound;

        if (!specTransDebit)
        {
            if (isAutoSettled)
            {
                select forupdate crossCompany specTransDebit
                    where specTransDebit.SpecCompany == _company
                       && specTransDebit.SpecTableId == _tableId
                       && specTransDebit.SpecRecId   == _recId
                       && !specTransDebit.Payment
                join forupdate custVendTransOpenDebit
                    where custVendTransOpenDebit.DataAreaId == specTransDebit.RefCompany
                       && custVendTransOpenDebit.TableId    == specTransDebit.RefTableId
                       && custVendTransOpenDebit.RecId      == specTransDebit.RefRecId;
            }
            else
            {
                select forupdate crossCompany specTransDebit
                    where specTransDebit.SpecCompany == _company
                       && specTransDebit.SpecTableId == _tableId
                       && specTransDebit.SpecRecId   == _recId
                       && !specTransDebit.Payment
                join forupdate custVendTransOpenDebit
                    order by AmountCur, CashDiscDate, DueDate, RecId
                    where custVendTransOpenDebit.DataAreaId == specTransDebit.RefCompany
                       && custVendTransOpenDebit.TableId    == specTransDebit.RefTableId
                       && custVendTransOpenDebit.RecId      == specTransDebit.RefRecId;
            }
        }
        else
        {
            next specTransDebit;
        }

        CashDiscDateSettlementCache::restoreCustVendTransOpenCashDiscDate(custVendTransOpenDebit);

        // This can't be part of join because it causes problems if several CustVendTransOpen records
        // have a RefRecId to the same CustVendTrans (this happens if there is a payment schedule and
        // are settling two or more transactions on the payment schedule).  The problems will be an
        // update conflict error message and the CustVendTrans not having the updated fields.
        changecompany(custVendTransOpenDebit.company())
        {
            // Check if both CustVendTransOpenDebit and CustVendTransOpenCredit records point to the
            // same CustVendTrans record
            if (custVendTransOpenDebit.RefRecId == custVendTransOpenCredit.RefRecId)
            {
                custVendTransDebit = custVendTransCredit;
            }
            else
            {
                custVendTransDebit = CustTrans::find(custVendTransOpenDebit.RefRecId, true);
            }
        }

        if (specTransDebit.RecId != 0)
        {
            recordFound = true;
            this.initDebitCompanyCurrency();
            SettlementUtility::roundTransactionAmounts(custVendTransDebit, specTransDebit, debitCompanyCurrency);
        }

        this.forceExchRateCalculation();

        return recordFound;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetchPayment</Name>
				<Source><![CDATA[
    public boolean fetchPayment(dataAreaId _company, tableId _tableId, RecId _recId)
    {
        boolean recordFound;

        if (!specTransCredit)
        {
            select forupdate crossCompany specTransCredit
                where specTransCredit.SpecCompany == _company
                   && specTransCredit.SpecTableId == _tableId
                   && specTransCredit.SpecRecId   == _recId
                   && specTransCredit.Payment
            join forupdate custVendTransOpenCredit
                order by TransDate, RecId
                where custVendTransOpenCredit.DataAreaId == specTransCredit.RefCompany
                   && custVendTransOpenCredit.TableId   == specTransCredit.RefTableId
                   && custVendTransOpenCredit.RecId     == specTransCredit.RefRecId;
        }
        else
        {
            next specTransCredit;
        }

        CashDiscDateSettlementCache::restoreCustVendTransOpenCashDiscDate(custVendTransOpenCredit);

        // This can't be part of join because it causes problems if several CustVendTransOpen records
        // have a RefRecId to the same CustVendTrans (this happens if there is a payment schedule and
        // are settling two or more transactions on the payment schedule).  The problems will be an
        // update conflict error message and the CustVendTrans not having the updated fields.
        changecompany(custVendTransOpenCredit.company())
        {
            // Check if both custVendTransOpenCredit and CustVendTransOpenDebit records point to the
            // same CustVendTrans record
            if (custVendTransOpenCredit.RefRecId == custVendTransOpenDebit.RefRecId)
            {
                custVendTransCredit = custVendTransDebit;
            }
            else
            {
                custVendTransCredit = CustTrans::find(custVendTransOpenCredit.RefRecId, true);
            }
        }

        if (specTransCredit.RecId == 0)
        {
            this.throwIfOversettled();
        }
        else
        {
            recordFound = true;
            this.initCreditCompanyCurrency();
            SettlementUtility::roundTransactionAmounts(custVendTransCredit, specTransCredit, creditCompanyCurrency);
        }

        this.forceExchRateCalculation();

        return recordFound;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>