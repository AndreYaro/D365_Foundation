<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmBusRelTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'BusinessRelation'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class smmBusRelTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        smmBusRelTable smmBusRelTable;
        select crosscompany DataAreaId from smmBusRelTable where smmBusRelTable.RecId == _parentRecId;

        return smmBusRelTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        smmBusRelTable smmBusRelTable;
        select crosscompany DataAreaId from smmBusRelTable where smmBusRelTable.RecId == _parentRecId;

        return smmBusRelTable.BusRelAccount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        smmBusRelTable smmBusRelTable;
        select RecId from smmBusRelTable where smmBusRelTable.BusRelAccount == _naturalKey;

        return smmBusRelTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        smmBusRelTable smmBusRelTable;
        select crosscompany * from smmBusRelTable where smmBusRelTable.RecId == _parentRecId;

        return smmBusRelTable.name();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        smmBusRelTable smmBusRelTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        smmBusRelTable smmBusRelTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from smmBusRelTableCrossCompany
            where smmBusRelTableCrossCompany.RecId == _association.RefRecId;

        if (smmBusRelTableCrossCompany.Recid)
        {
            changecompany (smmBusRelTableCrossCompany.DataAreaId)
            {
                select firstonly smmBusRelTable where smmBusRelTable.RecId == _association.RefRecId;
            }
        }

        _args.record(smmBusRelTable);
        return menuitemdisplaystr(smmBusRelTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(smmBusRelTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(smmBusRelTable));

        oSysTableLookup.addLookupfield(fieldnum(smmBusRelTable, BusRelAccount));
        oSysTableLookup.addLookupfield(fieldnum(smmBusRelTable, BusRelTypeId));
        oSysTableLookup.addLookupfield(fieldnum(smmBusRelTable, Party));

        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(smmBusRelTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::BusinessRelation);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>