<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_TmpPurchTbl</Name>
	<SourceCode>
		<Declaration><![CDATA[
class LogisticsLocationSelectForm_TmpPurchTbl
    extends LogisticsLocationSelectForm
{
    TmpPurchTable tmpPurchTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies a selected address to a calling purchase order.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The record ID of the selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        tmpPurchTable.DeliveryPostalAddress = _addressRecId;
        tmpPurchTable.AddressRefRecId = _transferFromTable.RecId;
        tmpPurchTable.AddressRefTableId = _transferFromTable.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// The initial source that is selected for the addresses on a form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (tmpPurchTable.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                tmpPurchTable.AddressRefTableId,
                tmpPurchTable.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    tmpPurchTable.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of a <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public recId getLogisticsPostalAddressRecId()
    {
        return tmpPurchTable.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of the target form selects upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (tmpPurchTable.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                tmpPurchTable.AddressRefTableId,
                tmpPurchTable.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            addressOwner = CompanyInfo::find();
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(TmpPurchTable _tmpPurchTable)
    {
        tmpPurchTable = _tmpPurchTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>LogisticsLocationSelectForm_TmpPurchTbl</c> class.
    /// </summary>
    /// <param name="_buffer">
    /// A record that is used to initialize the <c>LogisticsLocationSelectForm_TmpPurchTbl</c> object.
    /// </param>
    /// <returns>
    /// A new instance of the <c>LogisticsLocationSelectForm_TmpPurchTbl</c> class.
    /// </returns>
    public static LogisticsLocationSelectForm_TmpPurchTbl construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_TmpPurchTbl(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>