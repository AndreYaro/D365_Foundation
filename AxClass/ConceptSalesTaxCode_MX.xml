<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ConceptSalesTaxCode_MX</Name>
	<SourceCode>
		<Declaration><![CDATA[
class ConceptSalesTaxCode_MX
{
    DIOTDeclarationTaxCode_MX   diotDeclarationTaxCode;
    boolean                     alreadyMarked;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkMarked</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks whether the sales tax code is marked for another concept ID.
    /// </summary>
    /// <param name="_taxCode">
    ///    The tax code to check.
    /// </param>
    /// <param name="_diotConceptRecID">
    ///    A record ID of the <c>DIOTDeclarationTaxCode_MX</c> table.
    /// </param>
    /// <returns>
    ///    true if any sales tax code is not marked for another concept ID; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    If the sales tax code is already marked for another concept ID this method will create a warning
    ///    message
    /// </remarks>
    public boolean checkMarked(
        TaxCode     _taxCode,
        RefRecID    _diotConceptRecID)
    {
        alreadyMarked = true;

        alreadyMarked = diotDeclarationTaxCode.checkIfTaxCodeIsNotLInkedToAnyone(_taxCode, _diotConceptRecID);

        return alreadyMarked;
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkAllOrUnLinkAllSalesTaxCode</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks or unchecks all the sales tax codes.
    /// </summary>
    /// <param name="_tmpFrmVirtual">
    ///    A <c>TmpFrmVirtual</c> table buffer that contains the sales tax codes to mark.
    /// </param>
    /// <param name="_check">
    ///    A <c>NoYes</c> enumeration value that indicates whether to check or uncheck all the sales tax codes
    /// </param>
    /// <param name="_diotRecID">
    ///    A record ID of the <c>DIOTDeclarationTaxCode_MX</c> table.
    /// </param>
    /// <returns>
    ///    true if any net changes were made; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    If the <paramref name="_check" /> parameter is set to the <c>No</c> enumeration value, all sales
    ///    tax codes will be unchecked. If the <paramref name="_check" /> parameter is set to the <c>Yes</c>
    ///    enumeration value, all sales tax codes will be checked.This method only reports a change if any of
    ///    the sales tax codes have had their checked status changed. For example, if all the sales tax codes
    ///    are already checked and the <paramref name="_check" /> parameter is set to the <c>Yes</c>
    ///    enumeration value, this method will return false.
    /// </remarks>
    boolean linkAllOrUnLinkAllSalesTaxCode(
        TmpFrmVirtual   _tmpFrmVirtual,
        NoYes           _check,
        RefRecID        _diotRecID)
    {
        boolean onceMarked = true;
        NoYes   inverseCheck = (_check == NoYes::No ? NoYes::Yes : NoYes::No);

        // Only process if there are actually records that need to be changed
        while select _tmpFrmVirtual
        {
            if (_check == NoYes::Yes)
            {
                alreadyMarked = this.checkMarked(_tmpFrmVirtual.Id, _diotRecID);
            }

            if (!alreadyMarked && _check == NoYes::Yes)
            {
                onceMarked = false;
            }
        }

        select count(RecId) from _tmpFrmVirtual
            where _tmpFrmVirtual.NoYes == inverseCheck;

        if (onceMarked && _tmpFrmVirtual.RecId)
        {
            // Update only the rows that need to be changed (Ignores any filters set)
            update_recordset _tmpFrmVirtual
                setting NoYes = _check
                where _tmpFrmVirtual.NoYes == inverseCheck;

            select _tmpFrmVirtual;
            return true;
        }

        // Remove restriction
        select _tmpFrmVirtual;
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkOrUnlinkSalesTaxCode</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks or unchecks one or more accounts.
    /// </summary>
    /// <param name="_tmpFrmVirtual">
    ///    A <c>TmpFrmVirtual</c> table buffer that contains accounts to mark.
    /// </param>
    /// <param name="_diotConceptRecId">
    ///    A record ID of the <c>DIOTDeclarationTaxCode_MX</c> table.
    /// </param>
    /// <remarks>
    ///    If the <c>TmpFrmVirutual</c> table buffer does not have a sales tax code marked this method delete
    ///    records in the <c>DIOTDeclarationTaxCode_MX</c> table. If a sales tax code is marked this method
    ///    will create records in the <c>DIOTDeclarationTaxCode_MX</c> table.
    /// </remarks>
    public void linkOrUnlinkSalesTaxCode(
        TmpFrmVirtual   _tmpFrmVirtual,
        RecId           _diotConceptRecId)
    {
        DIOTDeclarationTaxCode_MX   diotDeclarationTaxCode1;
        while select diotDeclarationTaxCode
            where diotDeclarationTaxCode.diotDeclarationConcept == _diotConceptRecId
        {
            select firstonly _tmpFrmVirtual
                where _tmpFrmVirtual.Id != diotDeclarationTaxCode.TaxCode;

            if (_tmpFrmVirtual)
            {
                delete_from diotDeclarationTaxCode1
                    where diotDeclarationTaxCode1.diotDeclarationConcept == _diotConceptRecId
                       && diotDeclarationTaxCode1.TaxCode                == diotDeclarationTaxCode.TaxCode;
            }
        }

        while select _tmpFrmVirtual
            where _tmpFrmVirtual.NoYes == NoYes::Yes
        {
            diotDeclarationTaxCode = DIOTDeclarationTaxCode_MX::findBySalesTaxCodeAndConcept(_tmpFrmVirtual.Id, _diotConceptRecId);

            if (!diotDeclarationTaxCode)
            {
                diotDeclarationTaxCode.diotDeclarationConcept = _diotConceptRecId;
                diotDeclarationTaxCode.TaxCode                = _tmpFrmVirtual.Id;
                diotDeclarationTaxCode.insert();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>