<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EinvoiceInMemoryDestination_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// Destination class for <c>EInvoiceDestinationFactory_IT</c> that signs result of generated report depending on <c>EInvoiceParameters_IT</c>.
/// </summary>
public class EinvoiceInMemoryDestination_IT implements ERIFileDestination
{
    private ERIFileDestination originDestination;
    private EInvoice_IT eInvoice;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFileStream</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a stream for new file.
    /// </summary>
    /// <param name = "_filePath">Name of new file.</param>
    /// <returns>Stream for new file.</returns>
    public System.IO.Stream newFileStream(System.String _filePath)
    {
        return originDestination.newFileStream(eInvoice.getFileName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveFile</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves file in destination.
    /// </summary>
    /// <param name = "_stream">A stream to save.</param>
    /// <param name = "_filePath">A file path.</param>
    /// <returns>Saved stream.</returns>
    public System.IO.Stream saveFile(System.IO.Stream _stream, System.String _filePath)
    {
        XML generatedXML;
        System.Xml.XmlDocument xmlDoc = new System.Xml.XmlDocument();
        if (_stream.Length > 0)
        {
            _stream.Seek(0, System.IO.SeekOrigin::Begin);
            xmlDoc.Load(_stream);
            generatedXML = xmlDoc.OuterXml;
        }

        if (!generatedXML)
        {
            throw error("@SYS345627");
        }

        eInvoice.attachOutgoingXML(generatedXML);
        return _stream;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs destination for italian Einvoice.
    /// </summary>
    /// <param name = "_originDestination">The original destination.</param>
    /// <param name = "_einvoiceIt">The <c>EInvoice_IT</c> record corresponding to the report, that's being created.</param>
    /// <returns>The italian Einvoice destination.</returns>
    public static EinvoiceInMemoryDestination_IT construct(ERIFileDestination _originDestination, EInvoice_IT _einvoiceIt)
    {
        return new EinvoiceInMemoryDestination_IT(_originDestination, _einvoiceIt);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(ERIFileDestination _originDestination, EInvoice_IT _einvoiceIt)
    {
        originDestination = _originDestination;
        eInvoice = _einvoiceIt;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>