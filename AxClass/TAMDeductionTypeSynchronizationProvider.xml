<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMDeductionTypeSynchronizationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>TAMDeductionTypeSynchronizationProvider</c> is the dimension structure synchronization provider for TAM Deduction Type data.
/// </summary>
[ExportAttribute(identifierStr(Dynamics.AX.Application.DimensionStructureISynchronizationTransactionProvider))]
[DimensionStructureSynchronizationProviderCaption("@Tax:TAMDeductionTypeSynchronizationProviderCaption")]
[DimensionStructureSynchronizationProviderInterface(true)]
[DimensionStructureSynchronizationProviderV2Flight(classStr(TAMDeductionTypeSynchronizationProviderV2Flight))]
public class TAMDeductionTypeSynchronizationProvider implements DimensionStructureISynchronizationTransactionProvider, DimensionStructureISynchronizationTransactionProviderV2
{
    private const str InstrumentationNamespace = classStr(TAMDeductionTypeSynchronizationProvider);
    private SysInstrumentationLogger instrumentationLogger = SysInstrumentationLoggerFactory::CreateLogger(InstrumentationNamespace);
    private const int MaxRetryCount = 5;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkIfStuctureRemovalFromLedgerIsPrevented</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the account structure is prevented from being removed from the ledger.
    /// </summary>
    /// <param name="_dimensionHierarchy">
    /// The account structure being removed.
    /// </param>
    /// <param name="_ledger">
    /// The <c>Ledger</c> record from which the structure is being removed.
    /// </param>
    /// <param name = "_ret">
    /// The event handler result.
    /// </param>
    /// <returns>
    /// true if the account structure cannot be removed; otherwise, false.
    /// </returns>
    [SubscribesTo(classStr(LedgerStructureDelegates), delegateStr(LedgerStructureDelegates, checkIfStuctureRemovalFromLedgerIsPrevented))]
    public static void checkIfStuctureRemovalFromLedgerIsPrevented(DimensionHierarchy _dimensionHierarchy, Ledger _ledger, EventHandlerResult _ret)
    {
        SysInstrumentationActivity activity = SysInstrumentationActivity::construct(classStr(TAMDeductionTypeSynchronizationProvider),
            staticMethodStr(TAMDeductionTypeSynchronizationProvider, checkIfStuctureRemovalFromLedgerIsPrevented));
        SysInstrumentationLogger instrumentationLogger = SysInstrumentationLoggerFactory::CreateLogger(classStr(TAMDeductionTypeSynchronizationProvider));

        using (SysInstrumentationActivityContext activityContext = instrumentationLogger.activityContextWithCustomProperties(activity))
        {
            activityContext.addCustomProperty('MethodName', staticMethodStr(TAMDeductionTypeSynchronizationProvider, checkIfStuctureRemovalFromLedgerIsPrevented));

            TAMDeductionTypeTable tamDeductionTypeTable;
            DimensionAttributeValueCombination dimAttrValueCombination;
            DimensionAttributeValueGroupCombination dimAttrValueGroupCombination;
            DimensionAttributeValueGroup dimAttrValueGroup;

            unchecked (Uncheck::XDS)
            {
                CompanyInfo companyInfo = CompanyInfo::find(_ledger.PrimaryForLegalEntity);

                changecompany (companyInfo.DataArea)
                {
                    // Company tables must be nulled to work properly when changecompany is invoked multiple times.
                    tamDeductionTypeTable.clear();

                    // Locate any LedgerDimension from unposted customer invoice distribution templates
                    select firstonly RecId from tamDeductionTypeTable
                        exists join dimAttrValueCombination
                            where dimAttrValueCombination.RecId == tamDeductionTypeTable.DeductOffLedgerDimension
                                && dimAttrValueCombination.LedgerDimensionType == LedgerDimensionType::Account
                        exists join dimAttrValueGroupCombination
                            where dimAttrValueGroupCombination.DimensionAttributeValueCombination == dimAttrValueCombination.RecId
                        exists join dimAttrValueGroup
                            where dimAttrValueGroup.RecId == dimAttrValueGroupCombination.DimensionAttributeValueGroup
                                && dimAttrValueGroup.DimensionHierarchy == _dimensionHierarchy.RecId;
                }
            }

            if (tamDeductionTypeTable.RecId)
            {
                // The account structure is referenced by an unposted Deduction Type distribution template
                checkFailed(strfmt("@SalesAndMarketing:AccountStructureReferencedByUnpostedDeductionType", _dimensionHierarchy.Name, _ledger.Name));
                _ret.booleanResult(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDimensionSynchronize</Name>
				<Source><![CDATA[
    public void insertDimensionSynchronize(DimensionStructureISynchronizationRequest _synchronizationRequest)
    {
        DimensionStructureSynchronizationRequest synchronizationRequest = _synchronizationRequest as DimensionStructureSynchronizationRequest;

        if (synchronizationRequest)
        {
            DimensionSynchronize                dimSynchronize;
            DimensionSynchronize                dimSynchronizeNotExists;
            DimensionSynchronizeLedger          dimSynchronizeLedger;

            dimSynchronize.linkPhysicalTableInstance(synchronizationRequest.getDimensionSynchronize());
            dimSynchronizeNotExists.linkPhysicalTableInstance(synchronizationRequest.getDimensionSynchronize());
            dimSynchronizeLedger.linkPhysicalTableInstance(synchronizationRequest.getDimensionSynchronizeLedger());
            
            TAMDeductionTypeTable               tamDeductionTypeTable;
            DimensionAttributeValueCombination  dimAttrValueCombination;
            DataArea                            company;
            DataAreaId                          companyId;

            SysInstrumentationActivity activity = SysInstrumentationActivity::construct(InstrumentationNamespace, methodStr(TAMDeductionTypeSynchronizationProvider, insertDimensionSynchronize));

            using (SysInstrumentationActivityContext activityContext = instrumentationLogger.activityContextWithCustomProperties(activity))
            {
                unchecked (Uncheck::XDS)
                {
                    while select Id from company
                        where company.IsVirtual == NoYes::No
                    {
                        changecompany (company.Id)
                        {
                            companyId = company.Id;
                            // Company tables must be nulled to work properly when changecompany is invoked multiple times.
                            tamDeductionTypeTable.clear();

                            // Extract LedgerDimension's from Deduction Type distribution templates
                            insert_recordset dimSynchronize (OriginalLedgerDimension, DataArea)
                                select DeductOffLedgerDimension, companyId from tamDeductionTypeTable
                                    group by tamDeductionTypeTable.DeductOffLedgerDimension
                                    notexists join dimSynchronizeNotExists
                                        where dimSynchronizeNotExists.OriginalLedgerDimension == tamDeductionTypeTable.DeductOffLedgerDimension;

                            activityContext.addCustomProperty('MethodName', methodStr(TAMDeductionTypeSynchronizationProvider, insertDimensionSynchronize));
                            activityContext.addCustomProperty('WereRecordsInserted', int2str(dimSynchronize.rowCount() > 0));
                        }
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDimensionSynchronizeV2</Name>
				<Source><![CDATA[
    public RefRecId insertDimensionSynchronizeV2(DimensionStructureSynchronizationRequestV2 _synchronizationRequest)
    {
        RefRecId correlationId;

        SysInstrumentationActivity activity = SysInstrumentationActivity::construct(InstrumentationNamespace, methodStr(TAMDeductionTypeSynchronizationProvider, insertDimensionSynchronizeV2));

        using (SysInstrumentationActivityContext activityContext = instrumentationLogger.activityContextWithCustomProperties(activity))
        {
            RecId batchJobId = BatchHeader::getCurrentBatchHeader().parmBatchHeaderId();

            unchecked (Uncheck::XDS)
            {
                DimensionStructureSyncProviderStatusTracking dimensionStructureSyncProviderStatusTracking;

                select forupdate firstonly RecId from dimensionStructureSyncProviderStatusTracking
                    where dimensionStructureSyncProviderStatusTracking.BatchJobId == batchJobId &&
                        dimensionStructureSyncProviderStatusTracking.ProviderName == _synchronizationRequest.parmProviderName();

                if (!dimensionStructureSyncProviderStatusTracking)
                {
                    ttsbegin;

                    dimensionStructureSyncProviderStatusTracking.BatchJobId = batchJobId;
                    dimensionStructureSyncProviderStatusTracking.ProviderName = _synchronizationRequest.parmProviderName();
                    dimensionStructureSyncProviderStatusTracking.insert();

                    correlationId = dimensionStructureSyncProviderStatusTracking.RecId;

                    DataArea company;

                    while select Id from company
                        where company.IsVirtual == NoYes::No
                    {
                        changecompany (company.Id)
                        {
                            DataAreaId companyId = company.Id;

                            DimensionSynchronizeDetail dimSynchronizeDetail;
                            DimensionSynchronizeDetail dimSynchronizeDetailNotExists;
                            DimensionSynchronizeLedgerDetail dimSynchronizeLedgerDetail;
                            TAMDeductionTypeTable tamDeductionTypeTable;

                            // Company tables must be nulled to work properly when changecompany is invoked multiple times.
                            tamDeductionTypeTable.clear();

                            // Extract LedgerDimension's from Deduction Type distribution templates
                            insert_recordset dimSynchronizeDetail (ProviderStatusTrackingId, OriginalLedgerDimension, DataArea)
                                select correlationId, DeductOffLedgerDimension, companyId from tamDeductionTypeTable
                                    group by tamDeductionTypeTable.DeductOffLedgerDimension
                                    exists join dimSynchronizeLedgerDetail
                                        where dimSynchronizeLedgerDetail.DataArea == tamDeductionTypeTable.DataAreaId
                                    notexists join dimSynchronizeDetailNotExists
                                        where dimSynchronizeDetailNotExists.OriginalLedgerDimension == tamDeductionTypeTable.DeductOffLedgerDimension
                                            && dimSynchronizeDetailNotExists.ProviderStatusTrackingId == correlationId;

                            activityContext.addCustomProperty('MethodName', methodStr(TAMDeductionTypeSynchronizationProvider, insertDimensionSynchronizeV2));
                            activityContext.addCustomProperty('WereRecordsInserted', int2str(dimSynchronizeDetail.rowCount() > 0));
                        }
                    }

                    dimensionStructureSyncProviderStatusTracking.Status = DimensionStructureSyncProviderStatus::Inserted;
                    dimensionStructureSyncProviderStatusTracking.update();

                    ttscommit;
                }
                else
                {
                    correlationId = dimensionStructureSyncProviderStatusTracking.RecId;
                }
            }
        }

        return correlationId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>synchronizeStructureChanges</Name>
				<Source><![CDATA[
    public LedgerDimensionBase synchronizeStructureChanges(DimensionSynchronize _dimensionSynchronize)
    {
        return DimensionStructureChangeSynchronize::synchronizeStructureChanges(_dimensionSynchronize.OriginalLedgerDimension, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>synchronizeStructureChangesV2</Name>
				<Source><![CDATA[
    public LedgerDimensionBase synchronizeStructureChangesV2(DimensionSynchronizeDetail _dimensionSynchronize)
    {
        return DimensionStructureChangeSynchronize::synchronizeStructureChanges(_dimensionSynchronize.OriginalLedgerDimension, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateProviderReferences</Name>
				<Source><![CDATA[
    public void updateProviderReferences(DimensionStructureISynchronizationRequest _synchronizationRequest)
    {
        DimensionStructureSynchronizationRequest synchronizationRequest = _synchronizationRequest as DimensionStructureSynchronizationRequest;
        
        if (synchronizationRequest)
        {
            DimensionSynchronize            dimSynchronize;
            DimensionSynchronizeLedger      dimSynchronizeLedger;

            dimSynchronize.linkPhysicalTableInstance(synchronizationRequest.getDimensionSynchronize());
            dimSynchronizeLedger.linkPhysicalTableInstance(synchronizationRequest.getDimensionSynchronizeLedger());
            
            TAMDeductionTypeTable           tamDeductionTypeTable;
            DataArea                        company;

            SysInstrumentationActivity activity = SysInstrumentationActivity::construct(InstrumentationNamespace, methodStr(TAMDeductionTypeSynchronizationProvider, updateProviderReferences));

            using (SysInstrumentationActivityContext activityContext = instrumentationLogger.activityContextWithCustomProperties(activity))
            {
                unchecked (Uncheck::XDS)
                {               
                    while select Id from company
                        where company.IsVirtual == NoYes::No
                    {
                        changecompany (company.Id)
                        {
                            // Company tables must be nulled to work properly when changecompany is invoked multiple times.
                            tamDeductionTypeTable.clear();

                            // Update Deduction Type templates with the new ledger dimension values
                            update_recordset tamDeductionTypeTable
                                setting DeductOffLedgerDimension = dimSynchronize.UpdatedLedgerDimension
                                join UpdatedLedgerDimension from dimSynchronize
                                    where dimSynchronize.OriginalLedgerDimension == tamDeductionTypeTable.DeductOffLedgerDimension
                                        && dimSynchronize.OriginalLedgerDimension != dimSynchronize.UpdatedLedgerDimension;

                            info(strFmt("@Dimension:StructureSynchronizationProviderLogPerCompany", tamDeductionTypeTable.rowCount(), tableId2PName(tamDeductionTypeTable.TableId), company.Id));

                            activityContext.addCustomProperty('MethodName', methodStr(TAMDeductionTypeSynchronizationProvider, updateProviderReferences));
                            activityContext.addCustomProperty('WereRecordsUpdated', int2str(tamDeductionTypeTable.rowCount() > 0));
                        }
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateProviderReferencesV2</Name>
				<Source><![CDATA[
    public void updateProviderReferencesV2(DimensionStructureSynchronizationRequestV2 _request)
    {
        SysInstrumentationActivity activity = SysInstrumentationActivity::construct(InstrumentationNamespace, methodStr(TAMDeductionTypeSynchronizationProvider, updateProviderReferencesV2));

        try
        {
            ttsbegin;

            using (SysInstrumentationActivityContext activityContext = instrumentationLogger.activityContextWithCustomProperties(activity))
            {
                unchecked (Uncheck::XDS)
                {
                    DataArea company;
                    RecId batchJobId = BatchHeader::getCurrentBatchHeader().parmBatchHeaderId();

                    while select Id from company
                        where company.IsVirtual == NoYes::No
                    {
                        changecompany (company.Id)
                        {
                            DimensionSynchronizeDetail dimSynchronizeDetail;
                            DimensionSynchronizeLedgerDetail dimSynchronizeLedgerDetail;
                            TAMDeductionTypeTable tamDeductionTypeTable;

                            // Company tables must be nulled to work properly when changecompany is invoked multiple times.
                            tamDeductionTypeTable.clear();

                            // Update Deduction Type templates with the new ledger dimension values
                            update_recordset tamDeductionTypeTable
                                setting DeductOffLedgerDimension = dimSynchronizeDetail.UpdatedLedgerDimension
                                join UpdatedLedgerDimension from dimSynchronizeDetail
                                    where dimSynchronizeDetail.OriginalLedgerDimension == tamDeductionTypeTable.DeductOffLedgerDimension
                                        && dimSynchronizeDetail.OriginalLedgerDimension != dimSynchronizeDetail.UpdatedLedgerDimension
                                        && dimSynchronizeDetail.ProviderStatusTrackingId == _request.parmProviderStatusTrackingId()
                                exists join dimSynchronizeLedgerDetail
                                    where dimSynchronizeLedgerDetail.DataArea == tamDeductionTypeTable.DataAreaId
                                        && dimSynchronizeLedgerDetail.BatchJobId == batchJobId;

                            info(strFmt("@Dimension:StructureSynchronizationProviderLogPerCompany", tamDeductionTypeTable.rowCount(), tableId2PName(tamDeductionTypeTable.TableId), company.Id));

                            activityContext.addCustomProperty('MethodName', methodStr(TAMDeductionTypeSynchronizationProvider, updateProviderReferencesV2));
                            activityContext.addCustomProperty('WereRecordsUpdated', int2str(tamDeductionTypeTable.rowCount() > 0));
                        }
                    }
                }
            }

            ttscommit;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() > MaxRetryCount)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>