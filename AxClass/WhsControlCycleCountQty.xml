<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsControlCycleCountQty</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WhsControlCycleCountQty</c> class is responsible for handling the cycle count quantity controls on the mobile device.
/// </summary>
#WHSRF
[WhsControlFactory(#CycleCountQty1)]
[WhsControlFactory(#CycleCountQty2)]
[WhsControlFactory(#CycleCountQty3)]
[WhsControlFactory(#CycleCountQty4)]
class WhsControlCycleCountQty extends WhsControl
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>process</Name>
				<Source><![CDATA[
    public boolean process()
    {
        if (!super())
        {
            return false;
        }

        Qty cycleCountQty = WHSWorkExecuteDisplay::str2numDisplay(data);
        if (cycleCountQty < 0)
        {
            return this.fail("@WAX1172");
        }

        if (!this.validateQuantityForSerialControlledItem(cycleCountQty))
        {
            return this.fail(strFmt("@SYS4050070", cycleCountQty, fieldValues.parmSerialId()));
        }

        fieldValues.insert(this.parmName(), cycleCountQty);

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultValueToBlank</Name>
				<Source><![CDATA[
    protected boolean defaultValueToBlank()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateQuantityForSerialControlledItem</Name>
				<Source><![CDATA[
    private boolean validateQuantityForSerialControlledItem(Qty _cycleCountQty)
    {
        if (_cycleCountQty > 1
            && fieldValues.parmSerialId())
        {
            InventTable inventTable = InventTable::find(fieldValues.parmItemId());

            if (this.isSerialNumberControlled(inventTable))
            {
                return false;
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isSerialNumberControlled</Name>
				<Source><![CDATA[
    private boolean isSerialNumberControlled(InventTable _inventTable)
    {
        return InventDimGroupSetup::newItemId(_inventTable.ItemId).getFieldSetup(fieldNum(InventDim, InventSerialId)).isSerialNumberControlEnabled();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>