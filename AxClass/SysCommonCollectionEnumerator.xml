<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysCommonCollectionEnumerator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SysCommonCollectionEnumerator</c> class enables iteration through the elements in a
/// <c>SysICommonCollectionEnumerable</c> object.
/// </summary>
public class SysCommonCollectionEnumerator implements Enumerator
{
    private SysICommonCollectionEnumerable enumerable;

    private boolean atBeginning;
    private boolean atEnd;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>current</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the current <c>SysICommonCollection</c> object.
    /// </summary>
    /// <returns>
    /// A <c>SysICommonCollection</c> object.
    /// </returns>
    [Hookable(false)]
    final public anytype current()
    {
        if (atBeginning)
        {
            throw error("@SYS125456");
        }

        return enumerable.currentCommonCollection();
    }

]]></Source>
			</Method>
			<Method>
				<Name>definitionString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a description of the enumerator.
    /// </summary>
    /// <returns>
    /// The description of the enumerator.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public str definitionString()
    {
        return new List(Types::Class).getEnumerator().definitionString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>moveNext</Name>
				<Source><![CDATA[
    /// <summary>
    /// Advances the enumerator to the next element of the collection.
    /// </summary>
    /// <returns>
    /// true if the enumerator was successfully advanced to the next element; 
    /// otherwise, false if the enumerator has passed the end of the collection.
    /// </returns>
    /// <remarks>
    /// <c>SysICommonCollectionEnumerable</c> enumerators start before the first buffer in the
    /// <c>SysICommonCollectionEnumerable</c> object. You must call the <c>SysCommonCollectionEnumerator.moveNext</c> method
    /// to make it point to the first element in the list.
    /// </remarks>
    [Hookable(false)]
    final public boolean moveNext()
    {
        boolean moveNext;

        if (atEnd)
        {
            throw error("@SYS125456");
        }

        if (atBeginning)
        {
            enumerable.initiateEnumeration();
            atBeginning = false;
        }

        moveNext = enumerable.moveEnumerableNext();

        if (!moveNext)
        {
            atEnd = true;
        }

        return moveNext;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reset</Name>
				<Source><![CDATA[
    /// <summary>
    /// Moves the enumerator to the start of the <c>SysICommonCollectionEnumerable</c> object.
    /// </summary>
    /// <remarks>
    /// The reset method moves the enumerator to the start of the <c>SysICommonCollectionEnumerable</c> object,
    /// before the first element in the <c>SysICommonCollectionEnumerable</c> object.You must call the
    /// <c>SysCommonCollectionEnumerator.moveNext</c> method to make it point to the first element in the
    /// <c>SysICommonCollectionEnumerable</c> object.
    /// </remarks>
    [Hookable(false), Wrappable(false)]
    public void reset()
    {
        if (!atBeginning && !enumerable.canEnumerableBeReset())
        {
            throw error("@SYS344540");
        }

        atBeginning = true;
        atEnd       = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>toString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a description of the content of the element in the collection, that the enumerator is currently pointing to.
    /// </summary>
    /// <returns>
    /// A description of the current element.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public str toString()
    {
        str     toString;

        if (atBeginning)
        {
            toString = "@SYS344541";
        }
        else
        {
            toString = classStr(SysCommonCollection);

            if (atEnd)
            {
                toString += "@SYS344542";
            }
        }
        return toString;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        atBeginning = true;
        atEnd = false;
        this.reset();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeEnumerable</Name>
				<Source><![CDATA[
    private void initializeEnumerable(SysICommonCollectionEnumerable _enumerable)
    {
        enumerable = _enumerable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromEnumerable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of <c>SysCommonCollectionEnumerator</c> class.
    /// </summary>
    /// <param name="_SysICommonCollectionEnumerable">
    /// A <c>SysICommonCollectionEnumerable</c> object that is enumerated.
    /// </param>
    /// <returns>
    /// An instance of the <c>SysCommonCollectionEnumerator</c> class.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    static public SysCommonCollectionEnumerator newFromEnumerable(SysICommonCollectionEnumerable _enumerable)
    {
        SysCommonCollectionEnumerator   enumerator;

        enumerator = new SysCommonCollectionEnumerator();
        enumerator.initializeEnumerable(_enumerable);

        return enumerator;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>