<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BusinessProcessGenericHeaderEditorDialog_AppSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of the <c>BusinessProcessGenericHeaderEditorDialog</c> form, which is used for retail business process.
/// </summary>
[ExtensionOf(formStr(BusinessProcessGenericHeaderEditorDialog))]
final class BusinessProcessGenericHeaderEditorDialog_AppSuite_Extension
{    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();
        
        BusinessProcessIGenericSubtypeFilterable form = this.args().caller() as BusinessProcessIGenericSubtypeFilterable;
        if (form && form.getGenericSubtype() == BusinessProcessGenericSubtype::Retail)
        {
            FormReferenceGroupControl ownerControl = this.design(0).controlName(formControlStr(BusinessProcessGenericHeaderEditorDialog, BusinessProcessHeader_Owner));
            ownerControl.visible(false);

            FormComboBoxControl originalStatusControl = this.design(0).controlName(formControlStr(BusinessProcessGenericHeaderEditorDialog, BusinessProcessHeader_Status));
            originalStatusControl.visible(false);
            
            FormComboBoxControl statusControl = this.design(0).controlName(formControlStr(BusinessProcessGenericHeaderEditorDialog, RetailBusinessProcessHeader_Status));
            statusControl.visible(true);
            
            BusinessProcessHeader header = this.args().record();
            if (header)
            {
                statusControl.selection(header.Status);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>RetailBusinessProcessHeader_Status_OnSelectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    /// set the status of <c>BusinessProcessHeader</c> based on the value of RetailBusinessProcessHeader_Status control
    /// </summary>
    /// <param name="sender">the sender representing the FormComboBoxControl</param>
    /// <param name="e">event args</param>
    [FormControlEventHandler(formControlStr(BusinessProcessGenericHeaderEditorDialog, RetailBusinessProcessHeader_Status), FormControlEventType::SelectionChanged)]
    public static void RetailBusinessProcessHeader_Status_OnSelectionChanged(FormControl sender, FormControlEventArgs e)
    {
        FormComboBoxControl comboBoxControl = sender;
        FormRun form = sender.formRun();
        FormDataSource dataSource = form.dataSource(formDataSourceStr(BusinessProcessGenericHeaderEditorDialog, BusinessProcessHeader));
        BusinessProcessHeader header = dataSource.cursor();
        
        if (header.GenericSubtype == BusinessProcessGenericSubtype::Retail)
        {
            header.Status = comboBoxControl.selection();
            header.modifiedField(fieldNum(BusinessProcessHeader, Status));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>