<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInterestPost_ChronologicalNumber_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustInterestPost_ChronologicalNumber_Extension</c> class extends the logic of interest note posting 
/// in order to support chronological numbering functionality.
/// </summary>
[ExtensionOf(classStr(CustInterestPost))]
public final class CustInterestPost_ChronologicalNumber_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allocateVoucher</Name>
				<Source><![CDATA[
    protected NumberSeq allocateVoucher(CustInterestJour _custInterestJour)
    {
        NumberSeq numberSeqGroup;

        if (_custInterestJour.NumberSequenceGroup != ''
            && FeatureStateProvider::isFeatureEnabled(ChronologicalNumberingFeature::instance()))
        {
            NumberSequenceDatatype voucherDatatype = NumberSequenceDatatype::find(CustParameters::numRefInterestNoteVoucher().NumberSequenceDatatype);
            RefRecId scopeId = NumberSeqScopeFactory::createDefaultScope().getId();
            RefRecId voucherSeqId = NumberSequenceGroupRef::findNaturalKey(voucherDatatype.RecId, scopeId, _custInterestJour.NumberSequenceGroup).NumberSequenceId;

            if (voucherSeqId != 0)
            {
                numberSeqGroup = NumberSeq::newGetVoucherFromId(voucherSeqId);
            }
        }

        NumberSeq numberSeqRef = next allocateVoucher(_custInterestJour);

        return numberSeqGroup ? numberSeqGroup : numberSeqRef;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePostCustInterest</Name>
				<Source><![CDATA[
    protected void prePostCustInterest(CustInterestJour _custInterestJour)
    {
        next prePostCustInterest(_custInterestJour);

        if (FeatureStateProvider::isFeatureEnabled(ChronologicalNumberingFeature::instance()))
        {
            ChronologicalNumberSequenceGroupEngine::applyChronologicalNumberSequenceGroup(_custInterestJour, this.postingDate());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>