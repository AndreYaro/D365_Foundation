<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsRebateProcessDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class that serves to processed selected rebates.
/// </summary>
class PdsRebateProcessDialog extends RunBaseBatch implements BatchRetryable
{
    CustAccount                 custAccount;
    PdsCustRebateGroupId        pdsCustRebateGroupId;
    PdsRebateProgramTMAGroup    pdsRebateProgramTMAGroup;

    DialogField                 diagCustAccount;
    DialogField                 diagpdsCustRebateGroupId;
    DialogField                 dialogpdsRebateProgramTMAGroup;

    #DEFINE.CurrentVersion(4)
    #LOCALMACRO.CurrentList
        custAccount,
        pdsCustRebateGroupId,
        pdsRebateProgramTMAGroup
    #ENDMACRO

    #DEFINE.Version3(3)
    #LOCALMACRO.Version3List
        custAccount,
        pdsCustRebateGroupId
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds customer related fields to dialog box.
    /// </summary>
    /// <returns>Any object.</returns>
    public Object dialog()
    {
        DialogRunbase dialogRunbase = super();

        diagCustAccount = dialogRunbase.addFieldValue(
                                extendedtypestr(CustAccount),
                                custAccount,
                                "@SYS24704",
                                "@SYS7149");
        diagpdsCustRebateGroupId = dialogRunbase.addFieldValue(
                                        extendedtypestr(PdsCustRebateGroupId),
                                        pdsCustRebateGroupId,
                                        "@PRO1314",
                                        "@PRO1314");

        dialogpdsRebateProgramTMAGroup = dialogRunbase.addFieldValue(
                                        extendedtypestr(PdsRebateProgramTMAGroup),
                                        pdsRebateProgramTMAGroup,
                                        "@PRO1235",
                                        "@PRO1236");

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets dialog customer fields value.
    /// </summary>
    /// <returns>Return flag.</returns>
    public boolean getFromDialog()
    {
        custAccount = diagCustAccount.value();
        pdsCustRebateGroupId = diagpdsCustRebateGroupId.value();
        pdsRebateProgramTMAGroup = dialogpdsRebateProgramTMAGroup.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets application module value.
    /// </summary>
    /// <returns>Returns vendor application module value.</returns>
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// substantiates the object
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// stores the dialog object parameters.
    /// </summary>
    /// <returns>Returns list of stored parameters.</returns>
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches process rebate funtionality.
    /// </summary>
    public void run()
    {
        PdsRebateTable::processRebates(custAccount, pdsCustRebateGroupId, pdsRebateProgramTMAGroup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// restore the last parameters saved.
    /// </summary>
    /// <param name="_packedClass">list of stored parameters.</param>
    /// <returns>Returns if restore succeded or failed.</returns>
    public boolean unpack(container _packedClass)
    {
        Integer     version     = conpeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case #CurrentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            case #Version3:
                [version, #Version3List] = _packedClass;
                break;

            default :
                ret = false;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns an instance of the class
    /// </summary>
    /// <returns>returns instance</returns>
    public static PdsRebateProcessDialog construct()
    {
        return new PdsRebateProcessDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns class description.
    /// </summary>
    /// <returns>class description.</returns>
    public static ClassDescription description()
    {
        return "@PRO1121";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method constructs an instance and runs the class
    /// </summary>
    /// <param name="_args">Any arguments passed</param>
    public static void main(Args _args)
    {
        PdsRebateProcessDialog pdsRebateProcessDialog = PdsRebateProcessDialog::construct();

        try
        {
            if (pdsRebateProcessDialog.prompt())
            {
                pdsRebateProcessDialog.runOperation();

                Common record = _args.record();
                if (record && FormDataUtil::getFormDataSource(record))
                {
                    FormDataSource pdsRebateTable_ds = FormDataUtil::getFormDataSource(record);
                    pdsRebateTable_ds.research();
                }
            }
        }
        catch (Exception::Error)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>