<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymAdviceReportGeneratorVendPosted</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Generates the report data for the <c>BankPaymAdviceVend</c> report for posted payments.
/// </summary>
public class BankPaymAdviceReportGeneratorVendPosted extends BankPaymAdviceReportGeneratorVend
{
    private VendTrans vendTransPayment;
    private VendSettlement vendSettlementPayment;
    private VendSettlement vendSettlementInvoice;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findInvoicesAndCreateData</Name>
				<Source><![CDATA[
    protected void findInvoicesAndCreateData()
    {
        Set vouchers;

        if (ledgerJournalTrans.Company != ledgerJournalTrans.DataAreaId)
        {
            vouchers = this.findIntercompanyRelatedPaymentVouchers();
        }

        if (vouchers == null)
        {
            vouchers = new Set(Types::String);
            vouchers.add(ledgerJournalTrans.Voucher);
        }

        SetEnumerator vouchersEnumerator = vouchers.getEnumerator();

        while(vouchersEnumerator.moveNext())
        {
            Voucher voucher = vouchersEnumerator.current();
            this.insertData(voucher);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertData</Name>
				<Source><![CDATA[
    private void insertData(Voucher _voucher)
    {
        while select crosscompany vendTransPayment
            where vendTransPayment.Voucher == _voucher
                && vendTransPayment.AccountNum == vendTable.AccountNum
                && vendTransPayment.TransDate == ledgerJournalTrans.TransDate
                && vendTransPayment.DataAreaId == ledgerJournalTrans.Company
            join vendSettlementPayment
                where vendSettlementPayment.TransRecId == vendTransPayment.RecId
                    && vendSettlementPayment.CanBeReversed == NoYes::Yes
        {
            select firstonly crosscompany vendTransInvoice
                where vendTransInvoice.RecId == vendSettlementPayment.OffsetRecid
                    && vendTransInvoice.DataAreaId == vendSettlementPayment.OffsetCompany
                join vendSettlementInvoice
                    where vendSettlementInvoice.TransRecId == vendTransInvoice.RecId
                        && vendSettlementInvoice.CanBeReversed == NoYes::Yes
                        && vendSettlementInvoice.OffsetTransVoucher == _voucher;

            this.insertBankPaymAdviceTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateBankPaymAdviceTempPaymentInfo</Name>
				<Source><![CDATA[
    protected void populateBankPaymAdviceTempPaymentInfo()
    {
        bankPaymAdviceTmp.PaymRefLabel = this.getPaymRefLabel();
        bankPaymAdviceTmp.PaymentReference = this.getPaymRef();
        bankPaymAdviceTmp.SettledCurrencyCode = this.getPaymentCurrencyCode();
        bankPaymAdviceTmp.CashDisc = this.getCashDiscAmount();
        bankPaymAdviceTmp.TransDate = ledgerJournalTrans.TransDate;
        bankPaymAdviceTmp.Balance01 = vendSettlementPayment.SettleAmountCur;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCashDiscAmount</Name>
				<Source><![CDATA[
    protected Amount getCashDiscAmount()
    {
        return this.calculateAmountInPaymentCurrency(vendSettlementInvoice.TransDate, vendTransInvoice.CurrencyCode, vendSettlementInvoice.UtilizedCashDisc);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>