<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventDimCtrl_Frm_Mov_OwnershipChange</c> class controls inventory dimensions on the inventory ownership change journal form lines.
/// </summary>
public class InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine extends InventDimCtrl_Frm_Mov
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine construct()
    {
        return new InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromFormMovement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes an instance of this class to be used in forms.
    /// </summary>
    /// <param name = "_formRun">The form containing the inventory dimension fields to be controlled.</param>
    /// <param name = "_movement">Inventory movement whose inventory dimension fields are to be controlled.</param>
    /// <returns>An instance of the <c>InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine</c> class.</returns>
    public static InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine newFromFormMovement(
        FormRun         _formRun,
        InventMovement  _movement)
    {
        InventDimCtrl_Frm_Mov inventDimCtrl = InventDimCtrl_Frm_Mov_OwnershipChangeJournalLine::construct();
        InventDimAxFormAdapter adapter = InventDimAxFormAdapter::newFromForm(_formRun);

        inventDimCtrl.parmCallingElement(adapter);
        inventDimCtrl.parmMovement(_movement);
        inventDimCtrl.init();

        return inventDimCtrl;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canEnableDimensionInCurrentCountryContext</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the specified inventory dimension can be enabled in the current country context.
    /// </summary>
    /// <param name = "_fieldId">The ID of the inventory dimension field.</param>
    /// <returns>Will always return true.</returns>
    /// <remarks>The base class only allows to enable Owner in the Russian context. We need it globally in this journal.</remarks>
    protected boolean canEnableDimensionInCurrentCountryContext(FieldId _fieldId)
    {
        if (_fieldId == fieldNum(InventDim, InventOwnerId_RU))
        {
            return true;
        }

        return super(_fieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustEnableControl</Name>
				<Source><![CDATA[
    protected boolean mustEnableControl(
        InventDimFormControlInterface   _fc,
        InventDimParm                   _dimParmEnabled = dimParmEnabled)
    {
        if (this.isInventDimToField(_fc))
        {
            return false;
        }

        return super(_fc, _dimParmEnabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustMarkControlAsMandatory</Name>
				<Source><![CDATA[
    public boolean mustMarkControlAsMandatory(InventDimFormControlInterface _fc, InventDimParm _dimParmEnabled = dimParmEnabled)
    {
        if (this.isInventDimToField(_fc))
        {
            return false;
        }
        else if (inventDimControls.fieldId(_fc) == fieldnum(InventDim, InventOwnerId_RU))
        {
            return true;
        }

        return super(_fc, _dimParmEnabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustShowGridField</Name>
				<Source><![CDATA[
    protected NoYes mustShowGridField(FieldId _dimFieldId)
    {
        if (_dimFieldId == fieldnum(InventDim, InventOwnerId_RU))
        {
            return NoYes::Yes;
        }

        return super(_dimFieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInventDimToField</Name>
				<Source><![CDATA[
    private boolean isInventDimToField(InventDimFormControlInterface _fc)
    {
        return inventDimControls.datasourceName(_fc) == identifierstr(InventDimReceipt);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>