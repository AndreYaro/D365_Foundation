<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAutomationLookupBusinessDocument</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>CustAutomationLookupBusinessDocument</c> class is the implementation of business document lookup for the current state of a strategy line.
/// </summary>
[ExportMetadataAttribute(enumstr(CustAutomationLookupField), CustAutomationLookupField::BusinessDocument)]
[ExportAttribute(identifierstr(Dynamics.AX.Application.CustAutomationILookup))]
internal final class CustAutomationLookupBusinessDocument implements CustAutomationILookup
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resolveLookup</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void resolveLookup(CustAutomationStrategyLine _line, FormControl _control)
    {
        switch (_line.ActionType)
        {
            case CustAutomationActionType::Activity:
                this.lookupActivity(_control);
                break;
            case CustAutomationActionType::Email:
                this.lookupEmailTemplate(_control);
                break;
            case CustAutomationActionType::CollectionLetter:
                this.lookupCollectionLetter(_control);
                break;
            default:
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupActivity</Name>
				<Source><![CDATA[
    private void lookupActivity(FormControl _control)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(CustAutomationActivityTemplate), _control);
        Query q = new Query();
        q.addDataSource(tableNum(CustAutomationActivityTemplate));

        sysTableLookup.parmQuery(q);
        sysTableLookup.addLookupfield(fieldNum(CustAutomationActivityTemplate, TemplateName), true);
        sysTableLookup.addLookupfield(fieldNum(CustAutomationActivityTemplate, TypeId));

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupEmailTemplate</Name>
				<Source><![CDATA[
    private void lookupEmailTemplate(FormControl _control)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(SysEmailSystemTable), _control);
        Query q = new Query();
        q.addDataSource(tableNum(SysEmailSystemTable));

        sysTableLookup.parmQuery(q);
        sysTableLookup.addLookupfield(fieldNum(SysEmailSystemTable, EmailId), true);
        sysTableLookup.addLookupfield(fieldNum(SysEmailSystemTable, Description));

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupCollectionLetter</Name>
				<Source><![CDATA[
    private void lookupCollectionLetter(FormControl _control)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(CustCollectionLetterLine), _control);
        Query q = new Query();
        QueryBuildDataSource qbds = q.addDataSource(tableNum(CustCollectionLetterLine));
        qbds.addRange(fieldNum(CustCollectionLetterLine, CollectionLetterCode)).value(strFmt("%1", CustCollectionLetterCode::CollectionPerCust));

        sysTableLookup.parmQuery(q);
        sysTableLookup.addLookupfield(fieldNum(CustCollectionLetterLine, CollectionLetterCode), true);
        sysTableLookup.addLookupfield(fieldNum(CustCollectionLetterLine, CollectionLetterCourse));

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>