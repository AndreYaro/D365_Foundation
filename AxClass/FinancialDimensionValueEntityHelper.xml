<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FinancialDimensionValueEntityHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FinancialDimensionValueEntityHelper</c> class has helper methods for working with the <c>FinancialDimensionValueEntity</c> entity
/// and subscriber methods to delegates on the <c>FinancialDimensionValueEntity</c> table.
/// </summary>
class FinancialDimensionValueEntityHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setDimensionAttributeValueExtensionFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets additional entity extension fields.
    /// </summary>
    /// <param name = "_financialDimensionValueEntity">The <c>FinancialDimensionValueEntity</c> record from which to get the values.</param>
    /// <param name = "_dimensionAttributeValue">The <c>DimensionAttributeValue</c> record to set the values.</param>
    /// <param name = "_ret">The EventHandlerResult will return a <c>DimensionAttributeValue</c> record with the extension values set.</param>
    [SubscribesTo(classstr(FinancialDimensionValueEntityDelegates), delegatestr(FinancialDimensionValueEntityDelegates, setDimensionAttributeValueExtensionFields))]
    public static void setDimensionAttributeValueExtensionFields(FinancialDimensionValueEntity _financialDimensionValueEntity, DimensionAttributeValue _dimensionAttributeValue, EventHandlerResult _ret)
    {
        DimensionAttributeValue dimensionAttributeValue = _dimensionAttributeValue;

        if (_dimensionAttributeValue)
        {
            if (_financialDimensionValueEntity.isFieldSet(fieldNum(FinancialDimensionValueEntity, IsBalancing_PSN)))
            {
                dimensionAttributeValue.IsBalancing_PSN = _financialDimensionValueEntity.IsBalancing_PSN;
            }

            if (_financialDimensionValueEntity.isFieldSet(fieldNum(FinancialDimensionValueEntity, Owner)))
            {
                HCMWorker hcmWorker = HCMWorker::findByPersonnelNumber(_financialDimensionValueEntity.Owner);
                dimensionAttributeValue.Owner = hcmWorker.RecId;
            }
        }

        _ret.result(dimensionAttributeValue);
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkDimensionAttributeValueExtensionFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks additional entity extension fields.
    /// </summary>
    /// <param name = "_financialDimensionValueEntity">The <c>FinancialDimensionValueEntity</c> record from which to check the values.</param>
    /// <param name = "_ret">The EventHandlerResult will return a boolean value indicating if there are values to set.</param>
    [SubscribesTo(classstr(FinancialDimensionValueEntityDelegates), delegatestr(FinancialDimensionValueEntityDelegates, checkDimensionAttributeValueExtensionFields))]
    public static void checkDimensionAttributeValueExtensionFields(FinancialDimensionValueEntity _financialDimensionValueEntity, EventHandlerResult _ret)
    {
        if (!_ret.hasResult())
        {
            // Only process if another subscriber hasn't already indicated fields need to be set
            if ((_financialDimensionValueEntity.isFieldSet(fieldNum(FinancialDimensionValueEntity, IsBalancing_PSN)) && _financialDimensionValueEntity.IsBalancing_PSN) ||
                (_financialDimensionValueEntity.isFieldSet(fieldNum(FinancialDimensionValueEntity, Owner)) && _financialDimensionValueEntity.Owner != ''))
            {
                _ret.result(true);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>