<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSReturnItemPolicyRetriever</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSReturnItemPolicyRetriever</c> class is used to retrieve return item policies.
/// </summary>
internal final class WHSReturnItemPolicyRetriever
{
    private ItemId itemId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromItemId</Name>
				<Source><![CDATA[
    internal static WHSReturnItemPolicyRetriever newFromItemId(ItemId _itemId)
    {
        WHSReturnItemPolicyRetriever retriever = new WHSReturnItemPolicyRetriever();
        retriever.itemId = _itemId;

        return retriever;
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveMostSpecificPolicy</Name>
				<Source><![CDATA[
    internal WHSReturnItemPolicy retrieveMostSpecificPolicy()
    {
        WHSReturnItemPolicy policyItemAll;

        select firstonly policyItemAll
            order by policyItemAll.ItemCode asc // First get the Table and then All.
            where (policyItemAll.ItemCode == TableGroupAll::Table && policyItemAll.ItemRelation == itemId)
                || policyItemAll.ItemCode == TableGroupAll::All;

        // If no policy was found or the policy for All was found, try to find a more specific one, which would be a Group.
        if (!policyItemAll
            || policyItemAll.ItemCode == TableGroupAll::All)
        {
            WHSReturnItemPolicy policyGroup;
            InventItemGroupItem itemGroupItem;

            select firstonly policyGroup
                where policyGroup.ItemCode == TableGroupAll::GroupId
                exists join itemGroupItem
                    where itemGroupItem.ItemGroupDataAreaId == curext()
                        && itemGroupItem.ItemGroupId == policyGroup.ItemRelation
                        && itemGroupItem.ItemId == itemId;

            if (policyGroup)
            {
                return policyGroup;
            }
        }

        return policyItemAll;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>