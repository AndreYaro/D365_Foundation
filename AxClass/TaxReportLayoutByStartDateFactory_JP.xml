<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxReportLayoutByStartDateFactory_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class TaxReportLayoutByStartDateFactory_JP implements TaxReportILayoutFactory_JP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createTaxReportLayout</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public TaxReportILayout_JP createTaxReportLayout(TaxReportIParameters_JP _taxReportParameters, TaxReportILayout_JP _defaultLayout = TaxReportLayout_JP::construct())
    {
        if (!_taxReportParameters)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        TaxReportILayout_JP layout = _defaultLayout;
        StartDate layoutStartDate = dateNull();
        FromDate reportFromDate = _taxReportParameters.parmFromDate();

        Array layouts = SysPluginFactory::Instances(
            identifierStr(Dynamics.AX.Application),
            classStr(TaxReportILayout_JP),
            new SysPluginMetadataCollection());

        for (int idx = layouts.lastIndex(); idx; idx--)
        {
            TaxReportILayout_JP currentLayout = layouts.value(idx);
            if (!currentLayout || !currentLayout.isEnabled(_taxReportParameters))
            {
                continue;
            }

            TaxReportLayoutStartDateAttribute_JP startDateAttribute = new DictClass(classIdGet(currentLayout)).getAttribute(attributeStr(TaxReportLayoutStartDateAttribute_JP));
            if (!startDateAttribute)
            {
                continue;
            }

            StartDate currentLayoutStartDate = startDateAttribute.parmStartDate();

            if (currentLayoutStartDate && currentLayoutStartDate <= reportFromDate && currentLayoutStartDate > layoutStartDate)
            {
                layoutStartDate = currentLayoutStartDate;
                layout = currentLayout;
            }
        }

        return layout;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static TaxReportILayoutFactory_JP construct()
    {
        return new TaxReportLayoutByStartDateFactory_JP();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>