<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBInventoryAndProductionController_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FBInventoryAndProductionController_BR</c> class shows the FBInventoryAndProduction_BR SSRS report.
/// </summary>
public class FBInventoryAndProductionController_BR extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        FiscalEstablishment_BR fiscalEstablishment;

        FBHeaderContract_BR contract = this.parmReportContract().parmRdpContract() as FBHeaderContract_BR;

        select firstOnly crossCompany FiscalEstablishmentId, DataAreaId, CNPJCPFNum_BR, IENum_BR, Name from fiscalEstablishment
            where fiscalEstablishment.FiscalEstablishmentId == contract.parmFiscalEstablishmentId()
                && fiscalEstablishment.dataAreaId == contract.parmFiscalEstablishmentDataArea();

        if (this.parmArgs().record())
        {
            contract.parmFBBookingPeriodRecId(this.parmArgs().record().RecId);
        }
        else
        {
            FBBookingPeriod_BR bookingPeriod;

            select firstOnly RecId from bookingPeriod
                where bookingPeriod.FiscalEstablishmentId == fiscalEstablishment.FiscalEstablishmentId
                    && bookingPeriod.FiscalEstablishmentDataArea == fiscalEstablishment.dataAreaId
                    && bookingPeriod.PeriodMonth == contract.parmMonthPeriod()
                    && bookingPeriod.PeriodYear == contract.parmYearPeriod();

            contract.parmFBBookingPeriodRecId(bookingPeriod.RecId);
        }

        contract.parmCnpj(fiscalEstablishment.CNPJCPFNum_BR);
        contract.parmIeNum(fiscalEstablishment.IENum_BR);
        contract.parmFiscalEstablishmentName(fiscalEstablishment.Name);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a <c>FBInventoryAndProductionController_BR</c> class instance.
    /// </summary>
    /// <param name="_args">
    /// The collection of arguments in an instance of the <c>Args</c> class.
    /// </param>
    /// <returns>
    /// An instance of the <c>FBInventoryAndProductionController_BR</c> class.
    /// </returns>
    public static FBInventoryAndProductionController_BR construct(Args _args)
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR10019, funcName());

        FBInventoryAndProductionController_BR controller = new FBInventoryAndProductionController_BR();
        controller.parmReportName(ssrsReportStr(FBInventoryAndProduction_BR, Report));
        controller.parmArgs(_args);

        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        FBInventoryAndProductionController_BR controller = FBInventoryAndProductionController_BR::construct(_args);

        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns true.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>