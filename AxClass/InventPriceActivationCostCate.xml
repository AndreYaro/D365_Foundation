<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventPriceActivationCostCate</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventPriceActivationCostCate extends InventPriceActivation
{
    RouteCostCategoryId costCategoryId;
    InventSiteId        inventSiteId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>activate</Name>
				<Source><![CDATA[
    public void activate()
    {
        #OCCRetryCount

        try
        {
            ttsbegin;
            super();
            RouteCostCategoryPrice routeCostCategoryPriceCurrent = RouteCostCategoryPrice::findCurrent(costCategoryId,
                                                                                                       inventSiteId,
                                                                                                       true);
            routeCostCategoryPriceCurrent.moveCurrentToPrevious();

            RouteCostCategoryPrice routeCostCategoryPricePending = RouteCostCategoryPrice::find(costCategoryId,
                                                                                                  versionId,
                                                                                                  fromDate,
                                                                                                  0,
                                                                                                  inventSiteId,
                                                                                                  true);
            this.checkFromDate();
            routeCostCategoryPricePending.movePendingToCurrent();

            RouteCostCategory routeCostCategory = RouteCostCategory::find(costCategoryId,
                                                                          true);
            routeCostCategory.initRouteCostCategoryPrice(routeCostCategoryPricePending);
            routeCostCategory.update();
            ttscommit;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dateCheckFailedMessage</Name>
				<Source><![CDATA[
    protected str dateCheckFailedMessage()
    {
        return strFmt("@SYS104964", costCategoryId, date2StrUsr(fromDate, DateFlags::FormatAll), date2StrUsr(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), DateFlags::FormatAll));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCostCategoryId</Name>
				<Source><![CDATA[
    public RouteCostCategoryId parmCostCategoryId(RouteCostCategoryId _costCategoryId = costCategoryId)
    {
        costCategoryId = _costCategoryId;
        return costCategoryId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventSiteId</Name>
				<Source><![CDATA[
    public InventSiteId parmInventSiteId(InventSiteId _inventSiteId = inventSiteId)
    {
        inventSiteId = _inventSiteId;

        return inventSiteId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    protected static InventPriceActivationCostCate construct()
    {
        return new InventPriceActivationCostCate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newRouteCostCategory</Name>
				<Source><![CDATA[
    public static InventPriceActivationCostCate newRouteCostCategory(
        RouteCostCategoryId _costCategoryId,
        CostingVersionId    _versionId,
        FromDate            _fromDate,
        InventSiteId        _inventSiteId
        )
    {
        InventPriceActivationCostCate inventPriceActivationCostCate = InventPriceActivationCostCate::construct();

        inventPriceActivationCostCate.parmCostCategoryId(_costCategoryId);
        inventPriceActivationCostCate.parmVersionId(_versionId);
        inventPriceActivationCostCate.parmFromDate(_fromDate);
        inventPriceActivationCostCate.parmInventSiteId(_inventSiteId);

        return inventPriceActivationCostCate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
