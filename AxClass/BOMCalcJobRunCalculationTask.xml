<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BomCalcJobRunCalculationTask</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BOMCalcJobRunCalculationTask</c> class is used to run BOM calculation in batch using multiple batch tasks.
/// </summary>
class BomCalcJobRunCalculationTask extends RunBaseBatch implements BatchRetryable
{
    ItemBOMLevel      bomLevelFilter;
    container         packedBomCalcJobClass;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        bomLevelFilter,
        packedBomCalcJobClass
    #Endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatchJournal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies whether the batch task can be seen in the batch journal.
    /// </summary>
    /// <returns>
    /// true in order to see the results in the batch journal.
    /// </returns>
    public boolean canGoBatchJournal()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>caption</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a description of the class instance.
    /// </summary>
    /// <returns>
    /// A description of the class instance
    /// </returns>
    public ClassDescription caption()
    {
        return strfmt("@SYS53184",BOMCalcJobRunCalculationTask::description(),strfmt("@SYS335841",this.parmBOMLevelFilter()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>createBOMCalcJob_All</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a new instance of the <c>BOMCalcJob_All</c> class which is initialized with the packed class.
    /// </summary>
    /// <returns>
    /// A new instance of the <c>BOMCalcJob_All</c> class which is initialized with the packed class.
    /// </returns>
    protected BOMCalcJob_All createBOMCalcJob_All()
    {
        BOMCalcJob_All bomCalcJob;

        bomCalcJob = BOMCalcJob_All::construct();

        bomCalcJob.unpack(this.parmPackedBomCalcJobClass());

        return bomCalcJob;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a <c>container</c> with a packed version of the class.
    /// </summary>
    /// <returns>
    /// A packed version of the class.
    /// </returns>
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmBOMLevelFilter</Name>
				<Source><![CDATA[
    public ItemBOMLevel parmBOMLevelFilter(ItemBOMLevel _bomLevelFilter = bomLevelFilter)
    {
        bomLevelFilter = _bomLevelFilter;

        return bomLevelFilter;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPackedBomCalcJobClass</Name>
				<Source><![CDATA[
    public container parmPackedBomCalcJobClass(container _packedBomCalcJobClass = packedBomCalcJobClass)
    {
        packedBomCalcJobClass = _packedBomCalcJobClass;

        return packedBomCalcJobClass;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delegates the operation of doing the BOM calculation to a new instance of the <c>BOMCalcJob_All</c> class.
    /// </summary>
    public void run()
    {
        BOMCalcJob_All bomCalcJob = this.createBOMCalcJob_All();

        bomCalcJob.processTasks(this.parmBOMLevelFilter());
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies whether the batch task should run in an impersonated session.
    /// </summary>
    /// <returns>
    /// true to run in server side impersonated session and to get the job picked up by the
    /// batch AOS automatically
    /// </returns>
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the values in the <c>Container</c> parameter.
    /// </summary>
    /// <param name="_packedClass">
    /// A <c>container</c> with a packed version of the class.
    /// </param>
    /// <returns>
    /// true if the class was unpacked using the container; otherwise, false.
    /// </returns>
    public boolean unpack(container _packedClass)
    {
        Integer version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedClass;
                return true;

            default:
                return false;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static BOMCalcJobRunCalculationTask construct()
    {
        return new BOMCalcJobRunCalculationTask();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a description of what the class does.
    /// </summary>
    /// <returns>
    /// A description of what the class does.
    /// </returns>
    public static ClassDescription description()
    {
        return "@SYS343262";
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a new instance of the <c>BOMCalcJobRunCalculationTask</c> class.
    /// </summary>
    /// <param name="_bomLevelFilter">
    /// A <c>ItemBOMLevel</c> level which us used as a filter for the level to do BOM  calculation for.
    /// </param>
    /// <param name="_bomCalcJob_All">
    /// An instance of the <c>BOMCalcJob_All</c> class which is used for the BOM calculation.
    /// </param>
    /// <returns>
    /// A new instance of the <c>BOMCalcJobRunCalculationTask</c> class.
    /// </returns>
    public static BOMCalcJobRunCalculationTask newFromParameters(
        ItemBOMLevel   _bomLevelFilter,
        BOMCalcJob_All _bomCalcJob_All)
    {
        BOMCalcJobRunCalculationTask bomCalcJobRunCalculationTask;

        if (!_bomCalcJob_All)
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        bomCalcJobRunCalculationTask = BOMCalcJobRunCalculationTask::construct();

        bomCalcJobRunCalculationTask.parmPackedBomCalcJobClass(_bomCalcJob_All.pack());
        bomCalcJobRunCalculationTask.parmBOMLevelFilter(_bomLevelFilter);

        return bomCalcJobRunCalculationTask;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>