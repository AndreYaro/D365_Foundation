<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLGActionTypeExeLiquidate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLGActionTypeExeLiquidate</c> class is used for the liquidate execution action for the
///    letter of guarantee.
/// </summary>
class BankLGActionTypeExeLiquidate extends BankLGActionType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doAction</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Does the execution action "Liquidate" based on the parameters.
    /// </summary>
    protected void doAction()
    {
        BankLGLedgerJournalPost                 bankLGLedgerJournalPost;
        BankDocumentFacilityAgreementLineType   bankDocumentFacilityAgreementLineType;

        ttsbegin;

        // Update LG
        bankLGGuarantee.updateStatus(actionParms.getActionType());
        bankLGGuarantee.update();

        // Update facility agreement
        bankDocumentFacilityAgreementLineType = BankDocumentFacilityAgreementLineType::construct(
            bankLGGuarantee.FacilityAgreementLine,
            true,
            true);
        bankDocumentFacilityAgreementLineType.releaseAmount(bankLGGuarantee.BankAccountCurrencyGuaranteeAmount);

        // Post
        if (bankLGGuarantee.BankAccountCurrencyMarginAmount         != 0.0
            || bankLGGuarantee.BankAccountCurrencyGuaranteeAmount   != 0.0)
        {
            bankLGLedgerJournalPost = BankLGLedgerJournalPost::constructFromLG(bankLGGuarantee);

            // insert financial tag value
            if (FinTagConfiguration::isFinTagConfigurationSet(bankLGGuarantee.DataAreaId))
            {
                bankLGLedgerJournalPost.parmFinTag(bankLGGuarantee.FinTag);
            }

            // Margin
            bankLGLedgerJournalPost.addMarginTrans(
                -bankLGGuarantee.BankAccountCurrencyMarginAmount,
                "@SYS5446");

            // Liquidate
            bankLGLedgerJournalPost.addLiquidationTrans(
                bankLGGuarantee.BankAccountCurrencyGuaranteeAmount,
                "@SYS317972");

            bankLGLedgerJournalPost.run();
        }

        // Update LG action
        bankLGAction.ExecutionAction                    = BankLGExecutionActionType::Liquidate;
        bankLGAction.ExecutionDateTime                  = DateTimeUtil::getSystemDateTime();
        bankLGAction.BankAccountCurrencyGuaranteeAmount = bankLGGuarantee.BankAccountCurrencyGuaranteeAmount;
        bankLGAction.BankAccountCurrencyExpenseAmount   = bankLGGuarantee.BankAccountCurrencyExpenseAmount;
        bankLGAction.BankAccountCurrencyMarginAmount    = bankLGGuarantee.BankAccountCurrencyMarginAmount;
        if (bankLGLedgerJournalPost)
        {
            bankLGAction.LedgerJournalTable = bankLGLedgerJournalPost.getLedgerJournalTable().JournalNum;
        }
        bankLGAction.update();

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>