<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetReportingCurrencyAmountsService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The service to add reporting currency amounts on the table <c>AssetTrans</c>.
/// </summary>
public class AssetReportingCurrencyAmountsService extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateReportingCurrencyAmounts</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates reporting currency amounts on the table <c>AssetTrans</c> and update reporting currency updated field in <c>AssetParameter</c> table.
    /// </summary>
    /// <param name = "_contract">
    /// The value of the contract class.
    /// </param>    
    public void updateReportingCurrencyAmounts(AssetReportingCurrencyAmountsContract _contract)
    {
        ttsbegin;

        this.updateAssetTrans();
        this.updateReportingCurrecyFlag(_contract);
        
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateAssetTrans</Name>
				<Source><![CDATA[
    private void updateAssetTrans()
    {
        AssetTrans assetTrans;
        AssetTransReportingCurrencyAmounts assetTransReportingCurrencyAmounts;        

        assetTrans.skipDataMethods(true);
        assetTrans.skipEvents(true);
        assetTrans.skipDatabaseLog(true);

        update_recordset assetTrans
            setting ReportingCurrencyAmount = assetTransReportingCurrencyAmounts.AmountReportingCurrency,
                IsReportingCurrencyAmountUpdated = true
            join AmountReportingCurrency from assetTransReportingCurrencyAmounts
                where assetTransReportingCurrencyAmounts.AssetTrans == assetTrans.RecId;

        AssetCache::clearScope(AssetCacheScope::AssetTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateReportingCurrecyFlag</Name>
				<Source><![CDATA[
    private void updateReportingCurrecyFlag(AssetReportingCurrencyAmountsContract _contract)
    {
        NoYes reportingCurrencyAmountsUpdated = _contract.parmIsUpdatedAllReportingAmounts();

        if (reportingCurrencyAmountsUpdated)
        {
            AssetParameters assetParameter = AssetParameters::find(true);
            assetParameter.ReportingCurrencyAmountUpdatesComplete = reportingCurrencyAmountsUpdated;
            assetParameter.update();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>