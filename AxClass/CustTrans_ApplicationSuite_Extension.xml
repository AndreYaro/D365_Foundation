<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTrans_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(CustTrans))]
internal final class CustTrans_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    public void insert()
    {
        next insert();
        CustInvoiceJour custInvoiceJour = this.custInvoiceJour_RU();

        if (custInvoiceJour)
        {
            UnknownNoYes isCreditNote = custInvoiceJour.creditNote()
            ? UnknownNoYes::Yes
            : UnknownNoYes::No;

            TaxTrans taxTrans;
            TaxTrans_Reporting taxTrans_Reporting;
            taxTrans_Reporting.skipDataMethods(true);

            if (TaxTransReportingRowByRowUpdateFlight::instance().isEnabled())
            {
                while select RecId from taxTrans
                        where taxTrans.Voucher == this.Voucher
                            && taxTrans.TransDate == this.TransDate
                {
                    taxTrans_Reporting = TaxTrans_Reporting::findByTaxTrans(taxTrans.RecId);

                    if (taxTrans_Reporting && taxTrans_Reporting.IsOriginalDocumentCreditNote != isCreditNote)
                    {
                        taxTrans_Reporting.selectForUpdate(true);
                        taxTrans_Reporting.IsOriginalDocumentCreditNote = isCreditNote;
                        taxTrans_Reporting.update();
                    }
                }
            }
            else
            {
                update_recordset taxTrans_Reporting
                    setting
                        IsOriginalDocumentCreditNote = isCreditNote
                    exists join taxTrans
                        where taxTrans.RecId == taxTrans_Reporting.TaxTrans
                            && taxTrans.Voucher == this.Voucher
                            && taxTrans.TransDate == this.TransDate;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>