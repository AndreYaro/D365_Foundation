<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CreditCardProviderActivationResult</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CreditCardProviderActivationResult</c> class stores result from a credit card activation operation.
/// </summary>
internal class CreditCardProviderActivationResult extends CreditCardProviderResult
{
    private CreditCardActivationResult creditCardActivationResult;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the object from an array of properties.
    /// </summary>
    /// <param name="_properties">
    /// An array of properties.
    /// </param>
    protected void init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty[] _properties)
    {
        str xml = CreditCardPaymentProperties::convertPropertyArrayToXML(_properties);
        str ns = Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.GenericNamespace::get_ActivateGiftCardResponse();
        CreditCardPaymentProperty property;

        super(_properties);

        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_GiftCardNumber());
        this.parmCreditCardNumber(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_CardType());
        this.parmCreditCardTypeName(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_ProviderTransactionId());
        this.parmAuthorization(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_AvailableBalance());
        this.parmTransactionAmount(property ? property.parmDecimalValue() : 0.0);
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_ActivateGiftCardResult());
        this.parmActivationResult(property ? CreditCardProviderActivationResult::getActivationResult(property.parmStringValue()) : CreditCardActivationResult::None);

        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_ExpirationMonth());
        str expirationResult = '';
        
        if (property)
        {
            expirationResult = int2Str(property.parmDecimalValue());
            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.ActivateGiftCardResponseProperties::get_ExpirationYear());
            if (property)
            {
                expirationResult += '/' + int2Str(property.parmDecimalValue());
                this.parmExpiration(expirationResult);
            }
        }

        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.PaymentCardProperties::get_CardVerificationValue());
        
        if (property)
        {
            this.parmGiftCardPin(property.parmStringValue());
        }

        this.parmAvsResult(CreditCardAddressVerificationResult::None);
        this.parmAvsDetail(CreditCardMSAVSDetailCode::None);
        this.parmCvcResult(CreditCardMSCVCResultCode::Unknown);
        this.parmResultOK(this.parmActivationResult() == CreditCardActivationResult::Success);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmActivationResult</Name>
				<Source><![CDATA[
    public CreditCardActivationResult parmActivationResult(CreditCardActivationResult _value = creditCardActivationResult)
    {
        creditCardActivationResult = _value;
        return creditCardActivationResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromResponse</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a response object.
    /// </summary>
    /// <param name="_response">
    /// A response.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderActivationResult constructFromResponse(Microsoft.Dynamics.Retail.PaymentSDK.Portable.Response _response)
    {
        CreditCardProviderActivationResult result = new CreditCardProviderActivationResult();
        result.init(_response.get_Properties());
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromXmlString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a XML string.
    /// </summary>
    /// <param name="_xmlString">
    /// A XML string.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderActivationResult constructFromXmlString(str _xmlString)
    {
        CreditCardProviderActivationResult result = new CreditCardProviderActivationResult();
        result.init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty::ConvertXMLToPropertyArray(_xmlString));
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getActivationResult</Name>
				<Source><![CDATA[
    private static CreditCardActivationResult getActivationResult(str _code)
    {
        CreditCardActivationResult activationResult = CreditCardActivationResult::None;

        if (_code == CreditCardProviderActivationResult::activationGiftCardResultToString(Microsoft.Dynamics.Retail.PaymentSDK.Portable.ActivateGiftCardResult::Success))
        {
            return CreditCardActivationResult::Success;
        }
        else if (_code == CreditCardProviderActivationResult::activationGiftCardResultToString(Microsoft.Dynamics.Retail.PaymentSDK.Portable.ActivateGiftCardResult::Failure))
        {
            return CreditCardActivationResult::Failure;
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return activationResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>activationGiftCardResultToString</Name>
				<Source><![CDATA[
    private static str activationGiftCardResultToString(Microsoft.Dynamics.Retail.PaymentSDK.Portable.ActivateGiftCardResult _value)
    {
        return System.Convert::ToString(_value);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>