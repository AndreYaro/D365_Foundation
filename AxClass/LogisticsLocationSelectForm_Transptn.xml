<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_Transptn</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LogisticsLocationSelectForm_Transptn</c> class extends the <c>LogisticsLocationSelectForm</c> class
///    to manage the loaded postal address selection for transportation documents.
/// </summary>
public class LogisticsLocationSelectForm_Transptn extends LogisticsLocationSelectForm
{
    TransportationDocument      transportationDocument;
    List                        sourceTypes;
    LogisticsLocationRoleType   roleType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    public Set allowedSources()
    {
        Set allowedItems = new Set(typeName2Type(enumStr(LogisticsLocationSelectSourceType)));

        ListEnumerator sourceTypesEnumerator = sourceTypes.getEnumerator();

        while (sourceTypesEnumerator.moveNext())
        {
            allowedItems.add(sourceTypesEnumerator.current());
        }

        return allowedItems;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        if (roleType == LogisticsLocationRoleType::Unlading_W)
        {
            transportationDocument.UnloadedPostalAddress = _addressRecId;
        }
        else
        {
            transportationDocument.LoadedPostalAddress = _addressRecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   defaultSourceType;

        // The first source type in the sourceTypes is the default.
        ListEnumerator sourceTypesEnumerator = sourceTypes.getEnumerator();

        if (sourceTypesEnumerator.moveNext())
        {
            defaultSourceType = sourceTypesEnumerator.current();
        }

        return defaultSourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    public RecId getLogisticsPostalAddressRecId()
    {
        RecId postalAddressRecId;

        if (roleType == LogisticsLocationRoleType::Unlading_W)
        {
            postalAddressRecId = transportationDocument.UnloadedPostalAddress;
        }
        else
        {
            postalAddressRecId = transportationDocument.LoadedPostalAddress;
        }

        return postalAddressRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    public Common getLookupRecord()
    {
        return transportationDocument.getLoadedPostalAddressLookupRecord();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates new <c>LogisticsLocationSelectForm_Transptn</c> class based on transportation document and role type.
    /// </summary>
    /// <param name="_transportationDocument">
    /// The transportation document.
    /// </param>
    /// <param name="_roleType">
    /// The role type.
    /// </param>
    protected void new(TransportationDocument _transportationDocument, LogisticsLocationRoleType _roleType)
    {
        roleType                = _roleType;
        transportationDocument  = _transportationDocument;
        sourceTypes             = transportationDocument.getLoadedPostalAddressSourceTypes();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new instance of the <c>LogisticsLocationSelectForm_Transptn</c> class.
    /// </summary>
    /// <param name="_buffer">
    ///    A record to use to initialize the <c>LogisticsLocationSelectForm_Transptn</c> class instance.
    /// </param>
    /// <param name="_roleType">
    ///    The <c>LogisticsLocationRoleType</c> value to use to initialize the <c>LogisticsLocationSelectForm_Transptn</c> class instance.
    /// </param>
    /// <returns>
    ///    A new instance of the <c>LogisticsLocationSelectForm_Transptn</c> class.
    /// </returns>
    public static LogisticsLocationSelectForm_Transptn construct(Common _buffer, LogisticsLocationRoleType _roleType)
    {
        return new LogisticsLocationSelectForm_Transptn(_buffer, _roleType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateAddressName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates Loaded or Unloaded address name from <c>LogisticsPostalAddress</c> table
    /// </summary>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void updateAddressName(LogisticsPostalAddressRecId _addressRecId)
    {
        Description addressName = LogisticsPostalAddress::locationNameFromRecId(_addressRecId);

        if (roleType == LogisticsLocationRoleType::Unlading_W)
        {
            transportationDocument.UnloadedAddressName = addressName;
        }
        else
        {
            transportationDocument.LoadedAddressName = addressName;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>