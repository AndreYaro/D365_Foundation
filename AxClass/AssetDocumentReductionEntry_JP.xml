<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentReductionEntry_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Asset reduction entry document instance.
/// </summary>
public class AssetDocumentReductionEntry_JP extends AssetDocument_JP
{
    AssetDocumentReductionEntryTable_JP   reductionEntryTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteAssetDocumentEntry</Name>
				<Source><![CDATA[
        public void deleteAssetDocumentEntry()
    {
        AssetDocumentReductionEntryTable_JP   reductionEntryTableLoc;

        if (this.vaildateReductionEntryStatus())
        {
            reductionEntryTableLoc = AssetDocumentReductionEntryTable_JP::find(
            this.parmReductionEntryTable().RecId,
            true);

            ttsBegin;

            super();
            if (reductionEntryTableLoc)
            {
                reductionEntryTableLoc.delete();
            }

            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>increaseReductionEntryAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Increase PostAmount of specific reduction entry document entry.
    /// </summary>
    /// <param name="_preAmount">
    /// Reduction entry pre-adjustment amount.
    /// </param>
    /// <param name="_postAmount">
    /// Reduction entry post amount.
    /// </param>
    public void increaseReductionEntryAmount(
        AmountMST    _preAmount,
        AmountMST    _postAmount
    )
    {
        AssetDocumentReductionEntryTable_JP    reductionEntryTableLoc;

        if (this.parmReductionEntryTable().RecId)
        {
            ttsBegin;

            reductionEntryTableLoc = AssetDocumentReductionEntryTable_JP::find(
                this.parmReductionEntryTable().RecId,
                true);

            reductionEntryTableLoc.PostAmountMST += _postAmount;
            reductionEntryTableLoc.PreAmountMST  += _preAmount;
            reductionEntryTableLoc.update();

            ttsCommit;

            this.parmReductionEntryTable(reductionEntryTableLoc);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
        protected void initialize()
    {
        super();

        this.parmReductionEntryTable(
            AssetDocumentReductionEntryTable_JP::findByDocumentEntry(
                this.parmAssetDocumentEntry().RecId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryDocumentDate</Name>
				<Source><![CDATA[
        public AssetReductionEntryDocumentDate_JP parmReductionEntryDocumentDate(AssetReductionEntryDocumentDate_JP _documentDate = reductionEntryTable.DocumentDate)
    {
        reductionEntryTable.DocumentDate = _documentDate;

        return  reductionEntryTable.DocumentDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryPostAmount</Name>
				<Source><![CDATA[
        public AmountMST parmReductionEntryPostAmount(AmountMST _amount = reductionEntryTable.PostAmountMST)
    {
        reductionEntryTable.PostAmountMST = _amount;

        return reductionEntryTable.PostAmountMST;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryTable</Name>
				<Source><![CDATA[
        public AssetDocumentReductionEntryTable_JP parmReductionEntryTable(AssetDocumentReductionEntryTable_JP _reductionEntryTable = reductionEntryTable)
    {
        reductionEntryTable = _reductionEntryTable;

        return reductionEntryTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateAssetDocumentEntry</Name>
				<Source><![CDATA[
        public void updateAssetDocumentEntry()
    {
        if (this.vaildateReductionEntryStatus())
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateReductionEntryDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update reduction entry document.
    /// </summary>
    public void updateReductionEntryDocument()
    {
        AssetDocumentReductionEntryTable_JP    reductionEntryTableLoc;

        if (this.parmReductionEntryTable().RecId)
        {
            ttsBegin;

            reductionEntryTableLoc = AssetDocumentReductionEntryTable_JP::find(
                this.parmReductionEntryTable().RecId,
                true);

            reductionEntryTableLoc.DocumentDate = this.parmReductionEntryTable().DocumentDate;
            reductionEntryTableLoc.update();

            ttsCommit;

            this.parmReductionEntryTable(reductionEntryTableLoc);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateReductionEntryStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update reduction entry status.
    /// </summary>
    /// <param name="_status">
    /// The instance of <c>AssetDocumentReductionEntryStatus_JP</c>
    /// </param>
    public void updateReductionEntryStatus(AssetDocumentReductionEntryStatus_JP _status)
    {
        AssetDocumentReductionEntryTable_JP   redutionEntryTableLoc  = AssetDocumentReductionEntryTable_JP::find(this.parmReductionEntryTable().RecId, true);

        if (redutionEntryTableLoc && redutionEntryTableLoc.Status != _status)
        {
            ttsBegin;
            redutionEntryTableLoc.Status = _status;
            redutionEntryTableLoc.update();
            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>vaildateReductionEntryStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validate the reduction entry status. If the reduction entry has already been posted, update is not allowed.
    /// </summary>
    /// <returns>
    /// True if the reduction entry is not posted. Otherwise false;
    /// </returns>
    protected boolean vaildateReductionEntryStatus()
    {
        boolean ret = true;

        if (reductionEntryTable.Status == AssetDocumentReductionEntryStatus_JP::Posted)
        {
            ret = checkFailed("@SYS4006140");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createAssetDocumentEntry</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates specific asset reduction entry document instance by asset id, value mode/depbook id and selected asset document template id.
    /// </summary>
    /// <param name="_assetId">
    /// The asset id.
    /// </param>
    /// <param name="_bookId">
    /// The value mode/depbook id.
    /// </param>
    /// <param name="_templateId">
    /// Asset document tempate id.
    /// </param>
    /// <returns>
    /// Asset reduction entry document instance.
    /// </returns>
    public static AssetDocumentReductionEntry_JP createAssetDocumentEntry(
        AssetId                                  _assetId,
        AssetBookId                              _bookId,
        AssetDocumentReductionEntryTemplateId_JP _templateId)
    {
        AssetDocumentReductionEntry_JP         document;

        ttsBegin;
        document = AssetDocument_JP::createAssetDocumentEntry(_assetId, _bookId, AssetDocumentMainType_JP::ReductionEntry, _templateId);
        if (document != null)
        {
            document.parmReductionEntryTable(
                AssetDocumentReductionEntryTable_JP::createByAssetDocumentEntry(
                    document.parmAssetDocumentEntry()));
        }
        ttsCommit;

        return document;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>