<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Upgrade script for HRMInjuryIncidentCostType and HRMInjuryIncidentTreatment schema changes.
/// </summary>
class ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum extends ReleaseUpdateDB
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>moduleName</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the module name of an upgrade class.
    /// </summary>
    /// <returns>
    ///    The module name.
    /// </returns>
    /// <remarks>
    ///    This method must be implemented by sub classes.The module name is displayed on the cockpit form for
    ///    every script.
    /// </remarks>
    public ReleaseUpdateModule moduleName()
    {
        return "@SYS6720";
    }

]]></Source>
			</Method>
			<Method>
				<Name>myVersion</Name>
				<Source><![CDATA[
    ///<summary>
    ///The script runs in any system having version R1Update3.
    ///</summary>
    ///<returns>Script version.</returns>
    public sysReleasedVersion myVersion()
    {
        return sysReleasedVersion::R1Update3;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preSyncHRMInjuryIncidentCostType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Disables the unique index for the <c>HRMInjuryIncidentCostType</c> table.
    /// </summary>
    [UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum)),
    UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PreSyncUpdate),
    UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::SharedScript),
    UpgradeScriptConfigKeyAttribute(configurationKeyStr(HRMAdministration)),
    UpgradeScriptTableAttribute('HRMInjuryIncidentCostType', false, false, false, false), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void preSyncHRMInjuryIncidentCostType()
    {
        ReleaseUpdateDBSyncManager syncManager = ReleaseUpdateDBSyncManager::Construct();

        DictTable injuryIncidentCostType = new DictTable(tableNum(HRMInjuryIncidentCostType));
        DictIndex lineNumIndex = new DictIndex(tableNum(HRMInjuryIncidentCostType), indexNum(HRMInjuryIncidentCostType, LineNumIdx));

        syncManager.IgnoreIndexSynchronization(injuryIncidentCostType, lineNumIndex);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postSyncHRMInjuryIncidentCostType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the values of the <c>LineNum</c> field in the <c>HRMInjuryIncidentCostType</c> table.
    /// </summary>
    [UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum)),
    UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PostSyncUpdate),
    UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::SharedScript),
    UpgradeScriptConfigKeyAttribute(configurationKeyStr(HRMAdministration)),
    UpgradeScriptTableAttribute(tableStr(HRMInjuryIncidentCostType), false, true, true, false), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void postSyncHRMInjuryIncidentCostType()
    {
        SysDictTable injuryIncidentCostTypeTable = new SysDictTable(tableNum(HRMInjuryIncidentCostType));

        str injuryIncidentCostTypeTableName = injuryIncidentCostTypeTable.name(DbBackend::Sql);
        str injuryIncidentFieldName = injuryIncidentCostTypeTable.fieldName(fieldNum(HRMInjuryIncidentCostType, InjuryIncident), DbBackend::Sql);
        str lineNumFieldName = injuryIncidentCostTypeTable.fieldName(fieldNum(HRMInjuryIncidentCostType, LineNum), DbBackend::Sql);
        str updateLineNumSql = @"UPDATE [dbo].[%1]
                                SET [%2] = INJURYINCIDENTCOSTTYPETEMP.[ROWNUMBER]
                             FROM [dbo].[%1]
                             INNER JOIN
                                (SELECT RECID, ROW_NUMBER() OVER(PARTITION BY [%3]
                                    ORDER BY [%3]) AS ROWNUMBER
                                FROM [dbo].[%1] )
                             AS INJURYINCIDENTCOSTTYPETEMP
                                on [dbo].[%1].[RECID] = INJURYINCIDENTCOSTTYPETEMP.[RECID]";

        str sqlStatement = strFmt(updateLineNumSql, injuryIncidentCostTypeTableName, lineNumFieldName, injuryIncidentFieldName);

        this.executeSqlStatement(sqlStatement);
    }

]]></Source>
			</Method>
			<Method>
				<Name>preSyncHRMInjuryIncidentTreatment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Disables the unique index for the <c>HRMInjuryIncidentTreatment</c> table.
    /// </summary>
    [UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum)),
    UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PreSyncUpdate),
    UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::SharedScript),
    UpgradeScriptConfigKeyAttribute(configurationKeyStr(HRMAdministration)),
    UpgradeScriptTableAttribute('HRMInjuryIncidentTreatment', false, false, false, false), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void preSyncHRMInjuryIncidentTreatment()
    {
        ReleaseUpdateDBSyncManager syncManager = ReleaseUpdateDBSyncManager::Construct();

        DictTable injuryIncidentTreatment = new DictTable(tableNum(HRMInjuryIncidentTreatment));
        DictIndex lineNumIndex = new DictIndex(tableNum(HRMInjuryIncidentTreatment), indexNum(HRMInjuryIncidentTreatment, LineNumIdx));

        syncManager.IgnoreIndexSynchronization(injuryIncidentTreatment, lineNumIndex);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postSyncHRMInjuryIncidentTreatment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the values of the <c>LineNum</c> field in the <c>HRMInjuryIncidentTreatment</c> table.
    /// </summary>
    [UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_HcmInjuryIncidentAddLineNum)),
    UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PostSyncUpdate),
    UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::SharedScript),
    UpgradeScriptConfigKeyAttribute(configurationKeyStr(HRMAdministration)),
    UpgradeScriptTableAttribute(tableStr(HRMInjuryIncidentTreatment), false, true, true, false), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void postSyncHRMInjuryIncidentTreatment()
    {
        SysDictTable injuryIncidentTreatmentTable = new SysDictTable(tableNum(HRMInjuryIncidentTreatment));

        str injuryIncidentCostTypeTableName = injuryIncidentTreatmentTable.name(DbBackend::Sql);
        str injuryIncidentFieldName = injuryIncidentTreatmentTable.fieldName(fieldNum(HRMInjuryIncidentTreatment, InjuryIncident), DbBackend::Sql);
        str lineNumFieldName = injuryIncidentTreatmentTable.fieldName(fieldNum(HRMInjuryIncidentTreatment, LineNum), DbBackend::Sql);
        str updateLineNumSql = @"UPDATE [dbo].[%1]
                                SET [%2] = INJURYINCIDENTTREATMENTTEMP.[ROWNUMBER]
                             FROM [dbo].[%1]
                             INNER JOIN
                                (SELECT RECID, ROW_NUMBER() OVER(PARTITION BY [%3]
                                    ORDER BY [%3]) AS ROWNUMBER
                                FROM [dbo].[%1] )
                             AS INJURYINCIDENTTREATMENTTEMP
                                on [dbo].[%1].[RECID] = INJURYINCIDENTTREATMENTTEMP.[RECID]";

        str sqlStatement = strFmt(updateLineNumSql, injuryIncidentCostTypeTableName, lineNumFieldName, injuryIncidentFieldName);

        this.executeSqlStatement(sqlStatement);
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeSqlStatement</Name>
				<Source><![CDATA[
    private void executeSqlStatement(str _sqlStatement)
    {
        Connection sessionConn = new Connection();

        Statement statement = sessionConn.createStatement();

        new SqlStatementExecutePermission(_sqlStatement).assert();
        statement.executeUpdate(_sqlStatement);

        CodeAccessPermission::revertAssert();

        statement.close();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>