<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BpaDataverseQueryHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Xrm.Sdk.Query;
using System.Linq;

internal static class BpaDataverseQueryHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isBpaInstalled</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static boolean isBpaInstalled()
    {
        boolean doesSolutionExist = false;

        try
        {
            doesSolutionExist = SysDataverseUtility::CheckSolutionExistence(SysDataverseUtility::GetOrganizationServiceForCurrentUser(BpaConstants::OrgServiceName), BpaConstants::AnchorSolutionName);
        }
        catch
        {
            SysInstrumentationLoggerFactory::CreateLogger(classStr(BpaDataverseQueryHelper)).logInformation('User does not have permissions to query solutions');
        }

        return doesSolutionExist;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getModelDrivenAppId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static str getModelDrivenAppId()
    {
        try
        {
            var query = new QueryExpression('appmodule');
            query.TopCount = 1;
            query.ColumnSet = new ColumnSet('appmoduleid');

            var criteria = query.Criteria;
            criteria.AddCondition('uniquename', ConditionOperator::Equal, BpaConstants::ModelDrivenAppName);

            var result = SysDataverseUtility::GetOrganizationServiceForCurrentUser(BpaConstants::OrgServiceName).RetrieveMultiple(query).Entities;

            if (result.Count == 1)
            {
                return result.get_Item(0).get_Item('appmoduleid');
            }
        }
        catch
        {
            SysInstrumentationLoggerFactory::CreateLogger(classStr(BpaDataverseQueryHelper)).logInformation('User does not have permissions to query apps');
        }

        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>isUserBpaUser</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static boolean isUserBpaUser()
    {
        boolean isBpaUser = false;

        try
        {
            var query = new QueryExpression('msdyn_frhreport');
            query.TopCount = 1;

            SysDataverseUtility::GetOrganizationServiceForCurrentUser(BpaConstants::OrgServiceName).RetrieveMultiple(query);
        
            isBpaUser = true;
        }
        catch
        {
            SysInstrumentationLoggerFactory::CreateLogger(classStr(BpaDataverseQueryHelper)).logInformation('Current user is not a bpa user');
        }

        return isBpaUser;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>