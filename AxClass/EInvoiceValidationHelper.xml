<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EInvoiceValidationHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains validations for e-Invoice.
/// </summary>
class EInvoiceValidationHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkCompanyInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks <c>CompanyInfo</c> fields are filled correctly when uploading XML.
    /// </summary>
    /// <returns>
    /// True, if all fields are filled correctly; otherwise, false.
    /// </returns>
    public static boolean checkCompanyInfo()
    {
        CompanyInfo companyInfo = CompanyInfo::find();
        LogisticsPostalAddress postalAddress = LogisticsPostalAddress::findRecId(companyInfo.postalAddress().RecId);

        if (!companyInfo.Name)
        {
            warning(strfmt("@SYS75684", fieldpname(CompanyInfo, Name), tablepname(CompanyInfo)));
        }

        if (!companyInfo.RegNum)
        {
            warning(strfmt("@SYS75684", fieldpname(CompanyInfo, RegNum), tablepname(CompanyInfo)));
        }

        return EInvoiceValidationHelper::checkAddressInfo(tablepname(CompanyInfo), companyInfo.postalAddress().RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkAddressInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks that address fields are filled correctly when uploading XML.
    /// </summary>
    /// <param name="_tableName">
    /// Name of the table to which address fields are related.
    /// </param>
    /// <param name="_postalAddressRecId">
    /// Record RecId with address fields which will be unloaded.
    /// </param>
    /// <returns>
    /// True, if all fields are filled correctly; otherwise, false.
    /// </returns>
    public static boolean checkAddressInfo(TableName _tableName, RecId _postalAddressRecId)
    {
        LogisticsPostalAddress postalAddress = LogisticsPostalAddress::findRecId(_postalAddressRecId);
        LogisticsAddressCountryRegion countryRegion;

        if (!postalAddress.Street)
        {
            warning(strfmt("@SYS75684", "@SYS23438", _tableName));
        }

        if (!postalAddress.ZipCode)
        {
            warning(strfmt("@SYS75684", "@SYS12463", _tableName));
        }

        if (!postalAddress.City)
        {
            warning(strfmt("@SYS75684", "@SYS22249", _tableName));
        }

        if (!postalAddress.CountryRegionId)
        {
            warning(strfmt("@SYS75684", "@SYS7437", _tableName));
        }

        if (postalAddress.CountryRegionId)
        {
            countryRegion = LogisticsAddressCountryRegion::find(postalAddress.CountryRegionId);
            if (countryRegion.ISOcode)
            {
                if (strlen(countryRegion.ISOcode) != 2)
                {
                    warning(strfmt("@SYS101033", countryRegion.CountryRegionId));
                }
            }
            else
            {
                warning(strfmt("@SYS101034", countryRegion.CountryRegionId));
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkCurrencyInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks <c>Currency</c> fields are filled correctly when uploading XML.
    /// </summary>
    /// <param name="_currencyCode">
    /// Code of record for currency which will be checked.
    /// </param>
    /// <returns>
    /// True, if all fields are filled correctly; otherwise, false.
    /// </returns>
    public static boolean checkCurrencyInfo(CurrencyCode _currencyCode)
    {
        Currency currency = Currency::find(_currencyCode);

        if (currency.CurrencyCodeISO)
        {
            if (strlen(currency.CurrencyCodeISO) != 3)
            {
                warning(strfmt("@SYS344733", _currencyCode));
            }
        }
        else
        {
            warning(strfmt("@SYS101036", _currencyCode));
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkCustTableInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks <c>CustTable</c> fields are filled correctly when uploading XML.
    /// </summary>
    /// <param name="_custAccount">
    /// Code of the client which will be checked.
    /// </param>
    /// <returns>
    /// True, if all fields are filled correctly; otherwise, false.
    /// </returns>
    public static boolean checkCustTableInfo(CustAccount _custAccount)
    {
        #ISOCountryRegionCodes

        CustTable custTable = CustTable::find(_custAccount);

        if (!custTable.ContactPersonId)
        {
            warning(strfmt("@SYS75684", fieldpname(CustTable, ContactPersonId), tablepname(CustTable)));
        }

        if (!custTable.editContactPersonName(false, ''))
        {
            warning(strfmt("@SYS26332", "@SYS100792"));
        }

        if (!SysCountryRegionCode::isLegalEntityInCountryRegion([#isoFR]) && !custTable.getPrimaryRegistrationNumber(TaxRegistrationTypesList::TAXID))
        {
            warning(strfmt("@SYS56499", _custAccount));
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>