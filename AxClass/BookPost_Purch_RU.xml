<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BookPost_Purch_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
class BookPost_Purch_RU extends RunBaseBatch implements BatchRetryable
{
    PurchBookVATProcessLogTable_RU        logTable;
    TmpPurchBookVATProcessLogTrans_RU     tmpProcessLogTrans;
    TmpPurchBookVATProcessLogTransOper_RU tmpProcessLogTransOper;
    PurchBookVATProcessCancelType_RU      processMode;
    PurchBookVATProcessLogTrans_RU        processLogTrans;
    LedgerVoucher                         ledgerVoucher, ledgerVoucherCorrect;
    CurrencyCode                          currencyCode;
    DialogField                           transDateCtrl;
    TransDate                             transDate;
    RecordSortedList                      processLogTransList;

    #DEFINE.CurrentVersion(1)

    #LOCALMACRO.CurrentList
         processMode, transDate
    #ENDMACRO

    #MacroLib.AviFiles
    #VATTransitionPeriod_RU
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>cancelBlockedTrans</Name>
				<Source><![CDATA[
    protected void cancelBlockedTrans(RefRecId _recId)
    {
        PurchBookVATProcessLogTransOper_RU   processLogTransOper, reverseProcessLogTransOper;
        ;

        while select forupdate processLogTransOper
            where processLogTransOper.RefRecId          == _recId     &&
                  processLogTransOper.Blocked           == NoYes::Yes &&
                  processLogTransOper.CanceledRefRecId  == 0
        {
            reverseProcessLogTransOper.clear();
            reverseProcessLogTransOper.data(processLogTransOper);
            reverseProcessLogTransOper.AmountLiableToVAT = -processLogTransOper.AmountLiableToVAT;
            reverseProcessLogTransOper.TaxAmount         = -processLogTransOper.TaxAmount;
            reverseProcessLogTransOper.TransDate         = transDate;
            reverseProcessLogTransOper.CanceledRefRecId  = processLogTransOper.RecId;
            reverseProcessLogTransOper.LogTableRefRecId  = processLogTransOper.LogTableRefRecId;
            reverseProcessLogTransOper.Correct           = NoYes::Yes;
            reverseProcessLogTransOper.insert();

            processLogTransOper.CanceledRefRecId        = reverseProcessLogTransOper.RecId;
            processLogTransOper.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>check</Name>
				<Source><![CDATA[
    #localmacro.operationKey
        tmpProcessLogTransOper.RefRecId,
        tmpProcessLogTransOper.LogTableRefRecId,
        tmpProcessLogTransOper.TaxCode,
        tmpProcessLogTransOper.DefaultDimension,
        tmpProcessLogTransOper.InventTransId,
        tmpProcessLogTransOper.DocumentType
    #endmacro
    protected boolean check()
    {
        boolean     ret = true;
        BookStructSmallVATAmounts_RU    selectedAmounts, settledAmounts, usedAmounts;
        Map                             map = new Map(Types::Container, Types::Class);
        MapEnumerator                   mapEnumerator;
        RefRecId                        refRecId,logTableRefRecId;
        InventTransId                   inventTransId;
        TaxCode                         taxCode;
        DimensionDefault                defaultDimension;
        PurchBookVATDocumentType_RU     documentType;
        boolean                         useDocumentType = true;
        ;

        while select forupdate tmpProcessLogTrans
                where tmpProcessLogTrans.Marked  == NoYes::Yes
        {
            if (transDate > #TransitionEndDate && processMode == PurchBookVATProcessCancelType_RU::Process)
            {
                select sum(AmountLiableToVAT),sum(TaxAmount) from tmpProcessLogTransOper
                    group by TaxCode, DefaultDimension, RefRecId, LogTableRefRecId, InventTransId
                    where tmpProcessLogTransOper.Marked             == NoYes::Yes &&
                          tmpProcessLogTransOper.RefRecId           == tmpProcessLogTrans.RecId &&
                          tmpProcessLogTransOper.LogTableRefRecId   == tmpProcessLogTrans.LogTableRefRecId;

                useDocumentType = false;
            }

            else
            {
                select sum(AmountLiableToVAT),sum(TaxAmount) from tmpProcessLogTransOper
                    group by TaxCode, DefaultDimension, RefRecId, LogTableRefRecId, InventTransId, DocumentType
                    where tmpProcessLogTransOper.Marked             == NoYes::Yes &&
                          tmpProcessLogTransOper.RefRecId           == tmpProcessLogTrans.RecId &&
                          tmpProcessLogTransOper.LogTableRefRecId   == tmpProcessLogTrans.LogTableRefRecId;
            }

            while (tmpProcessLogTransOper)
            {
                selectedAmounts = new BookStructSmallVATAmounts_RU();
                selectedAmounts.addAmounts(tmpProcessLogTransOper.AmountLiableToVAT + tmpProcessLogTransOper.TaxAmount,
                                           tmpProcessLogTransOper.AmountLiableToVAT,
                                           tmpProcessLogTransOper.TaxAmount);
                map.insert([#operationKey], selectedAmounts);
                next tmpProcessLogTransOper;
            }
        }

        mapEnumerator = map.getEnumerator();

        while (mapEnumerator.moveNext())
        {
            [refRecId,
             logTableRefRecId,
             taxCode,
             defaultDimension,
             inventTransId,
             documentType] = mapEnumerator.currentKey();

            selectedAmounts = mapEnumerator.currentValue();

            select firstonly tmpProcessLogTransOper
                where tmpProcessLogTransOper.RefRecId           == refRecId         &&
                      tmpProcessLogTransOper.LogTableRefRecId   == logTableRefRecId &&
                      tmpProcessLogTransOper.TaxCode            == taxCode          &&
                      tmpProcessLogTransOper.DefaultDimension   == defaultDimension &&
                      tmpProcessLogTransOper.InventTransId      == inventTransId    &&
                      (! useDocumentType || tmpProcessLogTransOper.DocumentType == documentType);

            if (tmpProcessLogTransOper.rDeferralsTransRecId || tmpProcessLogTransOper.Blocked)
            {
                continue;
            }

            settledAmounts = BookStructSmallVATAmounts_RU::create(tmpProcessLogTransOper.SettledAmounts);
            usedAmounts    = BookStructSmallVATAmounts_RU::create(tmpProcessLogTransOper.UsedAmounts);

            settledAmounts.subAmounts2(usedAmounts);

            if ((settledAmounts.amountLiableToVAT() != selectedAmounts.amountLiableToVAT()) ||
                (settledAmounts.taxAmountVAT()      != selectedAmounts.taxAmountVAT()))
            {
                ret = false;

                select forupdate firstonly tmpProcessLogTrans
                    where tmpProcessLogTrans.RecId              == refRecId &&
                          tmpProcessLogTrans.LogTableRefRecId   == logTableRefRecId;

                tmpProcessLogTrans.Error = NoYes::Yes;
                tmpProcessLogTrans.update();
            }
        }

        if ( ! ret)
        {
            error("@GLS105051");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearErrors</Name>
				<Source><![CDATA[
    private void clearErrors()
    {
        while select forupdate tmpProcessLogTrans
            where tmpProcessLogTrans.Error == NoYes::Yes
        {
            tmpProcessLogTrans.Error = NoYes::No;
            tmpProcessLogTrans.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        Dialog dialog = super();
        ;

        dialog.addText("@GLS101158");
        dialog.addText("@SYS60031");

        transDateCtrl = dialog.addFieldValue(extendedTypeStr(TransDate), transDate, "@SYS67");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>endLedgerVoucher</Name>
				<Source><![CDATA[
    protected void endLedgerVoucher()
    {
        ledgerVoucherCorrect.end();
        ledgerVoucher.end();
    }

]]></Source>
			</Method>
			<Method>
				<Name>endLog</Name>
				<Source><![CDATA[
    protected void endLog()
    {
        logTable.EndTime = DateTimeUtil::getTimeNow(DateTimeUtil::getUserPreferredTimeZone());
        logTable.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = super();

        if (ret)
        {
            transDate = transDateCtrl.value();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public boolean init()
    {
        ;
        currencyCode = CompanyInfoHelper::standardCurrency();

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initLedgerVoucher</Name>
				<Source><![CDATA[
    protected void initLedgerVoucher()
    {
        NumberSeq           numberSeq;
        Voucher             voucher;
        LedgerVoucherObject ledgerVoucherObject;
        boolean             cancelMode = (processMode == PurchBookVATProcessCancelType_RU::Process) ? false : true;

        numberSeq = NumberSeq::newGetNum(VendParameters::numRefPurchBookVATProcessVoucherId_RU());
        voucher   = numberSeq.num();

        ledgerVoucherObject = LedgerVoucherObject::newVoucher(voucher,
                                                              transDate,
                                                              SysModule::Tax,
                                                              LedgerTransType::Tax,
                                                              cancelMode);
        ledgerVoucherObject.parmVoucherCheck(false);

        ledgerVoucher = LedgerVoucher::newLedgerPost(DetailSummary::Summary, SysModule::Tax, voucher);
        ledgerVoucher.addVoucher(ledgerVoucherObject);

        ledgerVoucherObject = LedgerVoucherObject::newVoucher(voucher,
                                                              transDate,
                                                              SysModule::Tax,
                                                              LedgerTransType::Tax,
                                                              !cancelMode);
        ledgerVoucherObject.parmVoucherCheck(false);

        ledgerVoucherCorrect = LedgerVoucher::newLedgerPost(DetailSummary::Summary, SysModule::Tax, voucher);
        ledgerVoucherCorrect.addVoucher(ledgerVoucherObject);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initLog</Name>
				<Source><![CDATA[
    protected void initLog()
    {
        logTable.clear();
        logTable.StartTime  = DateTimeUtil::getTimeNow(DateTimeUtil::getUserPreferredTimeZone());
        logTable.Executed   = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        logTable.Performer  = curUserId();

        logTable.Voucher    = ledgerVoucher.findLedgerVoucherObject().parmVoucher();

        logTable.ActionType = processMode;
        logTable.TransDate  = transDate;
        logTable.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>logOperation</Name>
				<Source><![CDATA[
    protected PurchBookVATProcessLogTransOper_RU logOperation(AmountMST _taxAmount,
                                                              AmountMST _amountLiableToVAT)
    {
        PurchBookVATProcessLogTransOper_RU processLogTransOper;
        ;

        processLogTransOper.clear();

        processLogTransOper.initFromTmpProcessLogTransOper(tmpProcessLogTransOper);

        processLogTransOper.LogTableRefRecId    = logTable.RecId;
        processLogTransOper.TransDate           = transDate;
        processLogTransOper.RefRecId            = processLogTrans.RecId;
        processLogTransOper.TaxAmount           = _taxAmount;
        processLogTransOper.AmountLiableToVAT   = _amountLiableToVAT;

        if (tmpProcessLogTransOper.Blocked || tmpProcessLogTransOper.BlockedAuto)
        {
            processLogTransOper.rDeferralsTransRecId    = 0;
            processLogTransOper.rDeferralsClosedAmount  = 0;
        }

        if (processMode == PurchBookVATProcessCancelType_RU::Cancel)
        {
            processLogTransOper.rDeferralsClosedAmount = - processLogTransOper.rDeferralsClosedAmount;
        }

        processLogTransOper.insert();

        return processLogTransOper;
    }

]]></Source>
			</Method>
			<Method>
				<Name>logTable</Name>
				<Source><![CDATA[
    public PurchBookVATProcessLogTable_RU logTable()
    {
        return logTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>logTrans</Name>
				<Source><![CDATA[
    protected void logTrans()
    {
        boolean          useSettlementInfo = true;
        RecordSortedList processLogTransListExisted;
        FactureJour_RU   factureJour;

        void createNewRecord()
        {
            processLogTrans.clear();
            processLogTrans.initFromTmpPurchBookVATProcessLogTrans(tmpProcessLogTrans);
            processLogTrans.insert();
        }
        ;

        // after transition period system should use settlement information only in case 'on payment' facture
        if (transDate > #TransitionEndDate)
        {
            factureJour       = FactureJour_RU::find(tmpProcessLogTrans.FactureId, tmpProcessLogTrans.factureModule());
            useSettlementInfo = factureJour.vatOnPayment;
        }

        processLogTransList = PurchBookVATProcessLogTrans_RU::findFromSettlement(tmpProcessLogTrans.FactureId,
                                                                                 tmpProcessLogTrans.InvoiceRecIdRef,
                                                                                 tmpProcessLogTrans.PaymentRecIdRef,
                                                                                 tmpProcessLogTrans.InvoiceCompany,
                                                                                 tmpProcessLogTrans.PaymentCompany,
                                                                                 tmpProcessLogTrans.SettlementRecId,
                                                                                 tmpProcessLogTrans.SettlementDate,
                                                                                 false,
                                                                                 useSettlementInfo,
                                                                                 tmpProcessLogTrans.ProcessingType,
                                                                                 tmpProcessLogTrans.CorrectedPeriod,
                                                                                 tmpProcessLogTrans.CorrectedFactureId);

        if (processLogTransList.len())
        {
            if (processMode == PurchBookVATProcessCancelType_RU::Process)
            {
                while (processLogTransList.next(processLogTrans))
                {
                    this.cancelBlockedTrans(processLogTrans.RecId);
                }
            }

            if (! useSettlementInfo)
            {
                processLogTransListExisted = PurchBookVATProcessLogTrans_RU::findFromSettlement(tmpProcessLogTrans.FactureId,
                                                                                                tmpProcessLogTrans.InvoiceRecIdRef,
                                                                                                tmpProcessLogTrans.PaymentRecIdRef,
                                                                                                tmpProcessLogTrans.InvoiceCompany,
                                                                                                tmpProcessLogTrans.PaymentCompany,
                                                                                                tmpProcessLogTrans.SettlementRecId,
                                                                                                tmpProcessLogTrans.SettlementDate,
                                                                                                false,
                                                                                                false,
                                                                                                tmpProcessLogTrans.ProcessingType,
                                                                                                tmpProcessLogTrans.CorrectedPeriod,
                                                                                                tmpProcessLogTrans.CorrectedFactureId);

                if (! processLogTransListExisted.len())
                {
                    createNewRecord();
                }
                else
                {
                    processLogTransListExisted.first(processLogTrans);
                }
            }
            else
            {
                processLogTransList.first(processLogTrans);
            }
        }
        else
        {
            createNewRecord();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>logTransReverse</Name>
				<Source><![CDATA[
    protected void logTransReverse()
    {
        boolean          useSettlementInfo = true;
        FactureJour_RU   factureJour;

        // after transition period system should use settlement information only in case 'on payment' facture
        if (transDate > #TransitionEndDate)
        {
            factureJour       = FactureJour_RU::find(tmpProcessLogTrans.FactureId, tmpProcessLogTrans.factureModule());
            useSettlementInfo = factureJour.vatOnPayment;
        }

        processLogTransList = PurchBookVATProcessLogTrans_RU::findFromSettlement(tmpProcessLogTrans.FactureId,
                                                                                 tmpProcessLogTrans.InvoiceRecIdRef,
                                                                                 tmpProcessLogTrans.PaymentRecIdRef,
                                                                                 tmpProcessLogTrans.InvoiceCompany,
                                                                                 tmpProcessLogTrans.PaymentCompany,
                                                                                 tmpProcessLogTrans.ReverseSettlementRecId,
                                                                                 tmpProcessLogTrans.ReverseDate,
                                                                                 true,
                                                                                 useSettlementInfo,
                                                                                 tmpProcessLogTrans.ProcessingType,
                                                                                 tmpProcessLogTrans.CorrectedPeriod,
                                                                                 tmpProcessLogTrans.CorrectedFactureId);

        processLogTransList.first(processLogTrans);
        if (processLogTrans.RecId)
        {
            processLogTrans.Reversed = (processMode == PurchBookVATProcessCancelType_RU::Process);
            processLogTrans.ReverseDate = (processLogTrans.Reversed ? tmpProcessLogTrans.SettlementDate : dateNull());
            processLogTrans.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        container   packedProcessLogTrans,
                    packedProcessLogTransOper;

        // don't save parameters if we are not in batch mode
        if ( ! this.isInBatch())
        {
            return [#CurrentVersion, #CurrentList, conNull(), conNull()];
        }

        while select tmpProcessLogTrans
        {
            packedProcessLogTrans     += [tmpProcessLogTrans];
        }
        while select tmpProcessLogTransOper
        {
            packedProcessLogTransOper += [tmpProcessLogTransOper];
        }

        return [#CurrentVersion, #CurrentList, packedProcessLogTrans, packedProcessLogTransOper];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProcessLogTrans</Name>
				<Source><![CDATA[
    private TmpPurchBookVATProcessLogTrans_RU parmProcessLogTrans(TmpPurchBookVATProcessLogTrans_RU _processLogTrans = tmpProcessLogTrans)
    {
        tmpProcessLogTrans = _processLogTrans;

        return tmpProcessLogTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProcessLogTransOper</Name>
				<Source><![CDATA[
    private TmpPurchBookVATProcessLogTransOper_RU parmProcessLogTransOper(TmpPurchBookVATProcessLogTransOper_RU _processLogTransOper = tmpProcessLogTransOper)
    {
        tmpProcessLogTransOper = _processLogTransOper;

        return tmpProcessLogTransOper;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProcessMode</Name>
				<Source><![CDATA[
    private PurchBookVATProcessCancelType_RU parmProcessMode(PurchBookVATProcessCancelType_RU _processMode = processMode)
    {
        processMode = _processMode;

        return processMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTransDate</Name>
				<Source><![CDATA[
    public TransDate parmTransDate(TransDate _transDate = transDate)
    {
        transDate = _transDate;
        return transDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postTaxAmount</Name>
				<Source><![CDATA[
    protected void postTaxAmount(LedgerDimensionAccount _fromLedgerDimension,
                                 LedgerDimensionAccount _toLedgerDimension,
                                 TransTxt               _transTxt,
                                 AmountMST              _taxAmount,
                                 RefTableId             _refTableId,
                                 RefRecId               _refRecId)
    {
        LedgerVoucherTransObject    ledgerVoucherTransObject;
        LedgerVoucher               localLedgerVoucher;
        LedgerBondClient_RU         ledgerBondClient;
        AmountMST                   amountMST = CurrencyExchangeHelper::amount(_taxAmount);
        CurrencyExchangeHelper      currencyExchangeHelper = CurrencyExchangeHelper::newLedger(Ledger::primaryLedger(CompanyInfo::findDataArea(curext()).RecId));
        ;

        if ((amountMST < 0 && processMode == PurchBookVATProcessCancelType_RU::Process) ||
            (amountMST > 0 && processMode == PurchBookVATProcessCancelType_RU::Cancel))
        {
            localLedgerVoucher = ledgerVoucher;
        }
        else
        {
            localLedgerVoucher = ledgerVoucherCorrect;
        }

        ledgerBondClient = localLedgerVoucher.findLedgerVoucherObject().ledgerBondClient_RU();

        if (ledgerBondClient)
        {
            ledgerBondClient.addNewLogObject();
        }

        ledgerVoucherTransObject = LedgerVoucherTransObject::newTransactionAmountDefault(localLedgerVoucher.findLedgerVoucherObject(),
                                                                            LedgerPostingType::TaxReport,
                                                                            _fromLedgerDimension,
                                                                            currencyCode,
                                                                            amountMST,
                                                                            currencyExchangeHelper);

        ledgerVoucherTransObject.parmTaxParentReferenceTableId(_refTableId);
        ledgerVoucherTransObject.parmTaxParentReferenceRecId(_refRecId);
        ledgerVoucherTransObject.parmTransTxt(_transTxt);

        localLedgerVoucher.addTrans(ledgerVoucherTransObject);

        ledgerVoucherTransObject = LedgerVoucherTransObject::newTransactionAmountDefault(localLedgerVoucher.findLedgerVoucherObject(),
                                                                            LedgerPostingType::TaxReport,
                                                                            _toLedgerDimension,
                                                                            currencyCode,
                                                                            -amountMST,
                                                                            currencyExchangeHelper);

        ledgerVoucherTransObject.parmTaxParentReferenceTableId(_refTableId);
        ledgerVoucherTransObject.parmTaxParentReferenceRecId(_refRecId);
        ledgerVoucherTransObject.parmTransTxt(_transTxt);

        localLedgerVoucher.addTrans(ledgerVoucherTransObject);

        if (ledgerBondClient)
        {
            ledgerBondClient.bondLastVRef2CurrentLog();
            ledgerBondClient.removeCurrentLogObject();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        int                                 sign = (processMode == PurchBookVATProcessCancelType_RU::Process) ? 1 : -1;
        Set                                 processedTrans = new Set(typeName2Type(extendedTypeStr(recId)));
        SetEnumerator                       setEnumerator;
        Counter                             counter;
        Voucher                             voucher;
        PurchBookVATProcessLogTransOper_RU  localLogTransOper;
        BookPostTax_RU                      bookPostTax;

        select count(RecId) from tmpProcessLogTrans
            exists join tmpProcessLogTransOper
            where tmpProcessLogTrans.Marked                 == NoYes::Yes                           &&
                  tmpProcessLogTransOper.RefRecId           == tmpProcessLogTrans.RecId             &&
                  tmpProcessLogTransOper.LogTableRefRecId   == tmpProcessLogTrans.LogTableRefRecId  &&
                  tmpProcessLogTransOper.Marked             == NoYes::Yes                           &&
                  (tmpProcessLogTransOper.AmountLiableToVAT || tmpProcessLogTrans.TransType == SalesPurchBookTransType_RU::TaxCorrection);

        counter = any2int(tmpProcessLogTrans.RecId);

        if ( ! counter)
        {
            throw error("@GLS111372");
        }

        this.clearErrors();

        if ( ! this.check())
        {
            throw error("@GLS101992");
        }

        this.progressInit("@GLS101145", counter, #AviUpdate);
        progress.setText("@SYS26577");

        try
        {
            ttsbegin;

            this.init();
            this.initLedgerVoucher();
            this.initLog();

            RestoredVATLogTable_RU::setRecalcAll(transDate);

            while select tmpProcessLogTrans
                where tmpProcessLogTrans.Marked == NoYes::Yes
            {
                progress.setText(strFmt("@GLS111360", tmpProcessLogTrans.AccountNum, tmpProcessLogTrans.FactureExternalId));

                if (tmpProcessLogTrans.Reversed)
                {
                    this.logTransReverse();
                }

                this.logTrans();

                processedTrans.add(tmpProcessLogTrans.RecId);
                while select tmpProcessLogTransOper
                     where tmpProcessLogTransOper.RefRecId          == tmpProcessLogTrans.RecId             &&
                           tmpProcessLogTransOper.LogTableRefRecId  == tmpProcessLogTrans.LogTableRefRecId  &&
                           tmpProcessLogTransOper.Marked            == NoYes::Yes                           &&
                           (tmpProcessLogTransOper.AmountLiableToVAT || tmpProcessLogTrans.TransType == SalesPurchBookTransType_RU::TaxCorrection) &&
                           tmpProcessLogTransOper.CanceledRefRecId  == 0
                {
                    voucher = ledgerVoucher.findLedgerVoucherObject().parmVoucher();

                    localLogTransOper = this.logOperation(sign * tmpProcessLogTransOper.TaxAmount,
                                                          sign * tmpProcessLogTransOper.AmountLiableToVAT);

                    if (voucher && tmpProcessLogTransOper.LedgerDimension && tmpProcessLogTransOper.OffsetLedgerDimension && tmpProcessLogTransOper.Blocked == NoYes::No)
                    {
                        bookPostTax = BookPostTax_RU::construct(ledgerVoucher,
                                                                ledgerVoucherCorrect,
                                                                voucher,
                                                                transDate,
                                                                localLogTransOper.TransTxt,
                                                                processMode ? true : false,
                                                                localLogTransOper);

                        bookPostTax.calcAndPost(LedgerPostingController::newForLedgerPostingJournal(bookPostTax.parmLedgerVoucher()), '', dateNull());
                    }
                }

                progress.incCount();
            }

            this.endLedgerVoucher();
            this.endLog();

            setEnumerator = processedTrans.getEnumerator();

            while (setEnumerator.moveNext())
            {
                select RecId, LogTableRefRecId from tmpProcessLogTrans
                    where tmpProcessLogTrans.RecId == setEnumerator.current();

                delete_from tmpProcessLogTransOper
                    where tmpProcessLogTransOper.RefRecId           == tmpProcessLogTrans.RecId            &&
                          tmpProcessLogTransOper.LogTableRefRecId   == tmpProcessLogTrans.LogTableRefRecId &&
                          tmpProcessLogTransOper.Marked             == NoYes::Yes;

                select count(RecId) from tmpProcessLogTransOper
                    where tmpProcessLogTransOper.RefRecId           == tmpProcessLogTrans.RecId       &&
                          tmpProcessLogTransOper.LogTableRefRecId   == tmpProcessLogTrans.LogTableRefRecId;

                if (! tmpProcessLogTransOper.RecId)
                {
                    tmpProcessLogTrans.delete();
                }
            }

            info(strFmt("@SYS9799", ledgerVoucher.findLedgerVoucherObject().parmVoucher()));

            ttscommit;
        }

        catch(Exception::Deadlock)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        boolean                                 ret;
        Counter                                 x;
        Counter                                 maxCount;
        int                                     version     = conPeek(_packedClass,1);
        container                               packedProcessLogTrans,
                                                packedProcessLogTransOper;
        TmpPurchBookVATProcessLogTrans_RU       tmpPurchBookVATProcessLogTrans;
        TmpPurchBookVATProcessLogTransOper_RU   tmpPurchBookVATProcessLogTransOper;
        ;

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedProcessLogTrans, packedProcessLogTransOper] = _packedClass;

                if (this.isInBatch())
                {
                    delete_from tmpProcessLogTrans;
                    delete_from tmpProcessLogTransOper;

                    maxCount = conLen(packedProcessLogTrans);

                    for (x = 1; x <= maxCount; x++)
                    {
                        [tmpPurchBookVATProcessLogTrans] = conPeek(packedProcessLogTrans, x);
                        tmpProcessLogTrans.clear();
                        tmpProcessLogTrans.data(tmpPurchBookVATProcessLogTrans);
                        tmpProcessLogTrans.insert();
                    }

                    maxCount = conLen(packedProcessLogTransOper);

                    for (x = 1; x <= maxCount; x++)
                    {
                        [tmpPurchBookVATProcessLogTransOper] = conPeek(packedProcessLogTransOper, x);
                        tmpProcessLogTransOper.clear();
                        tmpProcessLogTransOper.data(tmpPurchBookVATProcessLogTransOper);
                        tmpProcessLogTransOper.insert();
                    }
                }

                ret = true;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object calledFrom = null)
    {
        boolean ret = super(calledFrom);

        ret = PurchBookTable_RU::canProcessDate(transDate);

        if ( ! ret)
        {
            ret = checkFailed("@GLS104874");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@GLS101145";
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromVATProcess</Name>
				<Source><![CDATA[
    public static BookPost_Purch_RU newFromVATProcess(PurchBookVATProcessCancelType_RU      _processMode,
                                                      TmpPurchBookVATProcessLogTrans_RU     _processLogTrans,
                                                      TmpPurchBookVATProcessLogTransOper_RU _processLogTransOper)
    {
        BookPost_Purch_RU bookPost;

        switch (_processMode)
        {
            case PurchBookVATProcessCancelType_RU::Process :
                bookPost = new BookPost_Purch_Process_RU();
                break;

            case PurchBookVATProcessCancelType_RU::Cancel  :
                bookPost = new BookPost_Purch_Cancel_RU();
                break;

            default :
                throw error(Error::wrongUseOfFunction(funcName()));
        }

        bookPost.parmProcessLogTrans(_processLogTrans);
        bookPost.parmProcessLogTransOper(_processLogTransOper);
        bookPost.parmProcessMode(_processMode);

        return bookPost;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns false.
    /// </returns>
    [Hookable(false)]
    public boolean isRetryable() 
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>