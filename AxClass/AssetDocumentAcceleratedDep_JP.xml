<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentAcceleratedDep_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Asset accelerated depreciation document instance.
/// </summary>
public class AssetDocumentAcceleratedDep_JP extends AssetDocument_JP
{
    AssetDocumentAcceleratedDepEntry_JP      acceleratedeDepEntry;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    protected void initialize()
    {
        super();
        this.parmAcceleratedeDepEntry(
            AssetDocumentAcceleratedDepEntry_JP::findByAssetDocumentEntry(this.parmAssetDocumentEntry().RecId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAcceleratedeDepEntry</Name>
				<Source><![CDATA[
    public AssetDocumentAcceleratedDepEntry_JP parmAcceleratedeDepEntry(AssetDocumentAcceleratedDepEntry_JP _acceleratedeDepEntry = acceleratedeDepEntry)
    {
        acceleratedeDepEntry = _acceleratedeDepEntry;
        return acceleratedeDepEntry;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAcceleratedDocumentEntryList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get <c>AssetDocumentEntry_JP</c> with AssetId and BookId after specific transaction date.
    /// </summary>
    /// <param name="_assetId">
    /// The instance of <c>AssetId</c>.
    /// </param>
    /// <param name="_bookId">
    /// The instance of <c>AssetBookId</c>.
    /// </param>
    /// <param name="_transDate">
    /// The start date of valid asset document entry.
    /// </param>
    /// <returns>
    /// The container of valid document entry record after specific transaction date.
    /// </returns>
    public static container getAcceleratedDocumentEntryList(
        AssetId                        _assetId,
        AssetBookId                    _bookId,
        AssetTransDate                 _transDate)
    {
        container                           conEntry                 = conNull();
        AssetDocumentEntry_JP               assetDocumentEntry_JP;
        AssetDocumentTable_JP               assetDocumentTable_JP;
        AssetDocumentAcceleratedDepTable_JP assetDocumentAcceleratedDepTable;

        while select *
            from assetDocumentEntry_JP
            order by assetDocumentAcceleratedDepTable.ValidTo
            where assetDocumentEntry_JP.AssetId  == _assetId
                && assetDocumentEntry_JP.BookId  == _bookId
            join RecId from assetDocumentTable_JP
            where assetDocumentTable_JP.MainType == AssetDocumentMainType_JP::AssetAcceleratedDepreciation
                && assetDocumentTable_JP.RecId   == assetDocumentEntry_JP.AssetDocumentTable_JP
            join ValidFrom, ValidTo from assetDocumentAcceleratedDepTable
            where assetDocumentAcceleratedDepTable.AssetDocumentTable_JP == assetDocumentTable_JP.RecId
                && (assetDocumentAcceleratedDepTable.Status == AssetAcceleratedDepDocumentStatus_JP::Confirmed
                    || assetDocumentAcceleratedDepTable.Status == AssetAcceleratedDepDocumentStatus_JP::PartiallyPosted)
                && assetDocumentAcceleratedDepTable.ValidTo >= _transDate
        {
            conEntry += [[assetDocumentAcceleratedDepTable.ValidFrom, assetDocumentAcceleratedDepTable.ValidTo, assetDocumentEntry_JP]];
        }

        return conEntry;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>