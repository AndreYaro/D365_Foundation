<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSOutboundWorkLoadChartDrillThrough</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>WHSOutboundWorkLoadChartDrillThrough</c> is used to navigate the user to the
/// work details form when user perform a drills down action.
/// </summary>
internal final class WHSOutboundWorkLoadChartDrillThrough
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Ensure that the user is routed to the work details forms, when the user clicks on a data point in a chart.
    /// </summary>
    /// <param name = "_args">The serialized context object passed from chart with name value pairs.</param>
    [Hookable(false)]
    internal static void main(Args _args)
    {
        SysChartDrillThruParameters parameters = _args.parmObject();

        if (parameters)
        {
            WHSInstrumentationLogger instrumentationLogger = WHSOutboundWorkLoadChartDrillThrough::instrumentationLogger();

            using (var activityContext = instrumentationLogger.outboundWorkLoadChartActivities().drillThrough())
            {
                instrumentationLogger.outboundWorkLoadChartActivities().drillThroughFilters(activityContext, parameters);
            
                WHSOutboundWorkLoadChartFilter filter = WHSOutboundWorkLoadChartFilter::find(parameters.warehouseWorkLoadVisualizationFilterName());
                MenuFunction menuFunction = WHSOutboundWorkLoadChartDrillThrough::drilldownNavigationMenu(filter.DisplayLevel);

                instrumentationLogger.outboundWorkLoadChartActivities().drillThroughMenuName(activityContext, menuFunction.name());
                       
                var args = new Args(menuFunction.object());
                args.caller(_args.caller());
                args.parmObject(_args.parmObject());
                args.record(filter);
                menuFunction.run(args);            
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>drilldownNavigationMenu</Name>
				<Source><![CDATA[
    private static MenuFunction drilldownNavigationMenu(WHSChartDisplayLevel _displayLevel)
    {
        if (_displayLevel == WHSChartDisplayLevel::OpenLines)
        {
            return new MenuFunction(menuitemDisplayStr(WHSWorkLineDetails), MenuItemType::Display);
        }
        
        return new MenuFunction(menuitemDisplayStr(WHSWorkTableListPage), MenuItemType::Display);        
    }

]]></Source>
			</Method>
			<Method>
				<Name>instrumentationLogger</Name>
				<Source><![CDATA[
    private static WHSInstrumentationLogger instrumentationLogger()
    {
        return WHSInstrumentationLogger::createLogger(classStr(WHSOutboundWorkLoadChartDrillThrough));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>