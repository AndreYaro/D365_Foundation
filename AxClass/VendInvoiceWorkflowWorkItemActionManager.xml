<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceWorkflowWorkItemActionManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendInvoiceWorkflowWorkItemActionManager</c> class manages the workitem actions for vendor invoices workflows.
/// </summary>
public class VendInvoiceWorkflowWorkItemActionManager extends WorkflowWorkItemActionManager
{
    private VendInvoiceInfoTable vendInvoiceInfoTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmVendInvoiceInfoTable</Name>
				<Source><![CDATA[
    internal VendInvoiceInfoTable parmVendInvoiceInfoTable(VendInvoiceInfoTable _vendInvoiceInfoTable = vendInvoiceInfoTable)
    {
        vendInvoiceInfoTable = _vendInvoiceInfoTable;

        return vendInvoiceInfoTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static VendInvoiceWorkflowWorkItemActionManager construct()
    {
        return new VendInvoiceWorkflowWorkItemActionManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is primary main method to be invoked via the selection of a menu item.
    /// </summary>
    /// <param name="_args">
    /// Menu item arguments.
    /// </param>
    [SysObsolete('This method has been deprecated. Use the main from a child class.', false, 31\07\2020)]
    public static void main(Args _args)
    {
        VendInvoiceWorkflowWorkItemActionManager workflowWorkItemActionManager = new VendInvoiceWorkflowWorkItemActionManager();

        workflowWorkItemActionManager.parmArgs(_args);
        workflowWorkItemActionManager.parmCaller(_args.caller());
        workflowWorkItemActionManager.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>preActionDialogRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the budget funds are available when the action dialog run event occurs for workflow work item actions.
    /// </summary>
    protected void preActionDialogRun()
    {
        super();

        // Perform budget check before confirming the action dialog.
        // This gives the reviewer a chance to cancel the action if there are any budget check errors or warnings.
        this.checkBudget();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkBudget</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether budget funds are available for the budget transaction.
    /// </summary>
    protected void checkBudget()
    {
        // Submit to source document processor to perform GL budget check before confirming the comment dialog.
    }

]]></Source>
			</Method>
			<Method>
				<Name>takeWorkItemAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the workflow action that was triggered. Detects if the action was triggered through a user interaction and if so, logs the event.
    /// </summary>
    /// <remarks>Performing the action and logging of the event is wrapped in a transaction scope to enable a rollback.</remarks>
    protected void takeWorkItemAction()
    {
        ttsbegin;

        super();

        this.logTouchEvent();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>logTouchEvent</Name>
				<Source><![CDATA[
    internal void logTouchEvent()
    {
        if (this.isInvoiceTouched())
        {
            this.insertDocumentEventLog(this.parmArgs().menuItemName());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInvoiceTouched</Name>
				<Source><![CDATA[
    private boolean isInvoiceTouched()
    {
        return this.parmArgs().menuItemName() != strMin()
            && this.parmIsActionDialogClosedOK()
            && VendInvoiceAutomationStateManagement::isInvoiceInAutomatedWorkflow(vendInvoiceInfoTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDocumentEventLog</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected void insertDocumentEventLog(str _menuItemName)
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>