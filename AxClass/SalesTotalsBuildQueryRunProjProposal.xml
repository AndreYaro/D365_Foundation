<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesTotalsBuildQueryRunProjProposal</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesTotalsBuildQueryRunProjProposal</c> class provides <c>QueryRun</c> objects to be used
/// by the <c>SalesTotals_Proj</c> class and the <c>SalesCalcTax_ProjProposal</c> class for iterating
/// <c>SalesLine</c> records.
/// </summary>
class SalesTotalsBuildQueryRunProjProposal extends TradeTotalsBuildQueryRun implements TradeTotalsIBuildQueryRun
{
    QueryRun                projProposalItemQueryRun;
    QueryBuildDataSource    projProposalItemDataSource;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildLineQueryRun</Name>
				<Source><![CDATA[
        public QueryRun buildLineQueryRun()
    {
        if (!projProposalItemQueryRun)
        {
            projProposalItemQueryRun = this.createProjProposalItemQueryRun();
        }

        return projProposalItemQueryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createProjProposalItemQueryRun</Name>
				<Source><![CDATA[
        protected QueryRun createProjProposalItemQueryRun()
    {
        QueryRun queryRun = new QueryRun(queryStr(ProjProposalItem));

        queryRun.query().dataSourceTable(tableNum(ProjProposalJour)).rangeField(fieldNum(ProjProposalJour, ProposalId)).value(queryValue(this.parmProjProposalJour().ProposalId));
        queryRun.query().dataSourceTable(tableNum(ProjProposalItem)).rangeField(fieldNum(ProjProposalItem, ProposalId)).value(queryValue(this.parmProjProposalJour().ProposalId));
        if (FeatureStateProvider::isFeatureEnabled(ProjSalesOrderAddFilterToSalesTotalQueryFlight::instance()))
        {
            queryRun.query().dataSourceTable(tableNum(ProjProposalItem)).rangeField(fieldNum(ProjProposalItem, ItemType)).value(queryValue(ProjItemType::SalesOrder));
        }

        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mainLineDataSourceNumber</Name>
				<Source><![CDATA[
        public DataSourceNumber   mainLineDataSourceNumber()
    {
        if (!projProposalItemDataSource)
        {
            projProposalItemDataSource = this.buildLineQueryRun().query().dataSourceTable(tableNum(ProjProposalItem));
        }

        return projProposalItemDataSource.id();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProjProposalContract</Name>
				<Source><![CDATA[
        protected SalesTotalsProjProposalContract parmProjProposalContract()
    {
        return this.parmContract() as SalesTotalsProjProposalContract;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProjProposalJour</Name>
				<Source><![CDATA[
        protected ProjProposalJour parmProjProposalJour()
    {
        return this.parmProjProposalContract().parmProjProposalJour();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        protected static SalesTotalsBuildQueryRunProjProposal construct()
    {
        return new SalesTotalsBuildQueryRunProjProposal();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromProjProposalContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>SalesTotalsBuildQueryRunProjProposal</c> class.
    /// </summary>
    /// <param name="_contract">
    /// The <c>SalesTotalsProjProposalContract</c> object that is used to filter the
    /// <c>ProjProposalItem</c> records.
    /// </param>
    /// <returns>
    /// An instance of the <c>SalesTotalsBuildQueryRunProjProposal</c> class.
    /// </returns>
    public static SalesTotalsBuildQueryRunProjProposal newFromProjProposalContract(
        SalesTotalsProjProposalContract     _contract)
    {
        SalesTotalsBuildQueryRunProjProposal salesTotalsBuildQueryRunProjProposal = SalesTotalsBuildQueryRunProjProposal::construct();

        salesTotalsBuildQueryRunProjProposal.parmContract(_contract);

        return salesTotalsBuildQueryRunProjProposal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromProjProposalJour</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>SalesTotalsBuildQueryRunProjProposal</c> class.
    /// </summary>
    /// <param name="_projProposalJour">
    /// The <c>ProjProposalJour</c> record that is used to filter the <c>ProjProposalItem</c> records.
    /// </param>
    /// <returns>
    /// An instance of the <c>SalesTotalsBuildQueryRunProjProposal</c> class.
    /// </returns>
    public static SalesTotalsBuildQueryRunProjProposal newFromProjProposalJour(
        ProjProposalJour    _projProposalJour)
    {
        SalesTotalsBuildQueryRunProjProposal salesTotalsBuildQueryRunProjProposal = SalesTotalsBuildQueryRunProjProposal::newFromProjProposalContract(
            SalesTotalsProjProposalContract::newFromProjProposalJour(_projProposalJour));

        return salesTotalsBuildQueryRunProjProposal;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>