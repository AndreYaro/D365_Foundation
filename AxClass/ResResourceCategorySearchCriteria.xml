<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResResourceCategorySearchCriteria</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Resource category or role search criteria.
/// </summary>
public class ResResourceCategorySearchCriteria extends ResSearchCriteriaItem
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applySearch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies resource category search criteria in the specified query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>Updated query applied with resource category search criteria.</returns>
    public Query applySearch(Query _query)
    {
        if (_query)
        {
            if (!this.getSearchQuery(_query))
            {
                warning("@Res:SearchCriteria_ResourceCategoryNotApplicable");
            }
        }

        return _query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSearchQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get resource category search criteria query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>true if query is updated; otherwise false.</returns>
    private boolean getSearchQuery(Query _query)
    {
        boolean queryUpdated;

        if (_query)
        {
            QueryBuildDataSource mainDS = _query.dataSourceNo(1);
            QueryBuildDataSource dataSource;
            QueryBuildRange range;

            if (ResUtil::isPerformanceEnhancementFeatureEnabled(ResEnableByDefaultPerfEnhancementFeatureFlight::instance().isEnabled()))
            {
                switch (mainDS.table())
                {
                    case tableNum(ResAssignmentResourcesViewV2):
                        dataSource = mainDS.addDataSource(tableNum(ResActivityResourceAndResourceRoleView));
                        this.linkTo_ActivityResourceAndResourceRoleView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResActivityResourceAndResourceRoleView, ResourceCategoryId));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;

                    case tableNum(ResProjectResourceSchedulableDistinctView):
                        dataSource = mainDS.addDataSource(tableNum(ResActivityResourceAndResourceRoleView));
                        this.linkTo_ResourceRoleView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResActivityResourceAndResourceRoleView, ActivityResource));
                        range.value(queryValue(0));

                        range = dataSource.addRange(fieldNum(ResActivityResourceAndResourceRoleView, ResourceCategoryId));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;
                }
            }
            else
            {
                switch (mainDS.table())
                {
                    case tableNum(ResCompanyResourceSchedulableDetailDistinctView):
                        dataSource = mainDS.addDataSource(tableNum(ResResourceResourceCategoryView));
                        this.linkTo_CompanyResourceDetailView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResResourceResourceCategoryView, CharacteristicId));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;

                    case tableNum(ResAssignmentResourcesDistinctView):
                        dataSource = mainDS.addDataSource(tableNum(ResAssignmentResourceCategoriesView));

                        this.linkTo_AssignmentResourcesView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResAssignmentResourceCategoriesView, ResourceCategoryId));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;

                    case tableNum(ResActivityResourceDetailView):
                        dataSource = mainDS.addDataSource(tableNum(ResActivityResourceRolesView));

                        this.linkTo_ActivityResourceDetailView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResActivityResourceRolesView, ResourceCategoryId));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;
                }
            }
        }

        return queryUpdated;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addResourceCategory</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a criteria value to the criteria item.
    /// </summary>
    /// <param name = "_value">Resource category value.</param>
    public void addResourceCategory(ResourceCategoryId _value)
    {
        this.addValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeResourceCategory</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes the criteria value from the criteria item.
    /// </summary>
    /// <param name = "_value">Resource category value.</param>
    public void removeResourceCategory(ResourceCategoryId _value)
    {
        this.removeValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of resource category search criteria.
    /// </summary>
    protected void new ()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of Resource category search criteria.
    /// </summary>
    /// <returns>Instance of <c>ResResourceCategorySearchCriteria</c>.</returns>
    public static ResResourceCategorySearchCriteria construct()
    {
        ResResourceCategorySearchCriteria searchCriteriaItem = new ResResourceCategorySearchCriteria();
        return searchCriteriaItem;
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_CompanyResourceDetailView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Company resources view.
    /// </summary>
    /// <param name = "_dataSource">Company resource categories data source.</param>
    private void linkTo_CompanyResourceDetailView(QueryBuildDataSource _dataSource)
    {
        FieldId resourceField = fieldNum(ResCompanyResourceSchedulableDetailDistinctView, Resource);
        _dataSource.addLink(resourceField, fieldNum(ResResourceResourceCategoryView, Resource));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_AssignmentResourcesView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Assignment resources view.
    /// </summary>
    /// <param name = "_dataSource">Assignment resource categories data source.</param>
    private void linkTo_AssignmentResourcesView(QueryBuildDataSource _dataSource)
    {
        FieldId activityField = fieldNum(ResAssignmentResourcesDistinctView, Activity);
        _dataSource.addLink(activityField,
            fieldNum(ResAssignmentResourceCategoriesView, Activity));

        FieldId resourceField = fieldNum(ResAssignmentResourcesDistinctView, Resource);
        _dataSource.addLink(resourceField,
            fieldNum(ResAssignmentResourceCategoriesView, Resource));

        FieldId activityResourceField = fieldNum(ResAssignmentResourcesDistinctView, ActivityResource);
        _dataSource.addLink(activityResourceField,
            fieldNum(ResAssignmentResourceCategoriesView, ActivityResource));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_ActivityResourceAndResourceRoleView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links activity resource and resource role view.
    /// </summary>
    /// <param name = "_dataSource">Activity resource and resource role data source.</param>
    private void linkTo_ActivityResourceAndResourceRoleView(QueryBuildDataSource _dataSource)
    {         
        FieldId activityResourceField = fieldNum(ResAssignmentResourcesViewV2, ActivityResource);
        _dataSource.addLink(activityResourceField, fieldNum(ResActivityResourceAndResourceRoleView, ActivityResource));
        
        FieldId resourceField = fieldNum(ResAssignmentResourcesViewV2, Resource);
        _dataSource.addLink(resourceField, fieldNum(ResActivityResourceAndResourceRoleView, Resource));

        FieldId receipientCompanyField = fieldNum(ResAssignmentResourcesViewV2, RecipientCompany);
        _dataSource.addLink(receipientCompanyField, fieldNum(ResActivityResourceAndResourceRoleView, RecipientCompany));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_ResourceRoleView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links resource role view.
    /// </summary>
    /// <param name = "_dataSource">Resource role data source.</param>
    private void linkTo_ResourceRoleView(QueryBuildDataSource _dataSource)
    {
        FieldId resourceField = fieldNum(ResProjectResourceSchedulableDistinctView, Resource);
        _dataSource.addLink(resourceField, fieldNum(ResActivityResourceAndResourceRoleView, Resource));

        FieldId receipientCompanyField = fieldNum(ResProjectResourceSchedulableDistinctView, RecipientCompany);
        _dataSource.addLink(receipientCompanyField, fieldNum(ResActivityResourceAndResourceRoleView, RecipientCompany));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_ActivityResourceDetailView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Activity resources detail view.
    /// </summary>
    /// <param name = "_dataSource">Assignment resource categories data source.</param>
    private void linkTo_ActivityResourceDetailView(QueryBuildDataSource _dataSource)
    {
        FieldId activityResourceField = fieldNum(ResActivityResourceDetailView, ActivityResource);
        _dataSource.addLink(activityResourceField, fieldNum(ResActivityResourceRolesView, RecId));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>