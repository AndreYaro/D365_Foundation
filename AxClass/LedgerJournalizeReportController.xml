<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalizeReportController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerJournalizeReportController</c> class is the controller class for
///    <c>LedgerJournalizeReport</c> SSRS report.
/// </summary>
class LedgerJournalizeReportController extends SrsReportRunController implements BatchRetryable
{
    // <GEEPL>
    #define.sysVersion('SysVersion')
    #define.userName('UserName')
    #define.displayDescription('DisplayDescription')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    // </GEEPL>

    /// <summary>
    ///    Initializes the report objects.
    /// </summary>
    /// <param name="_query">
    ///    The report query to use.
    /// </param>
    public void init(Query _query)
    {
        LedgerJournalizeReport ledgerJournalizeReport;
        QueryBuildDataSource queryBuildDataSource;
        QueryBuildRange queryBuildRangeJournalizeNum;

        // Getting the args record
        ledgerJournalizeReport = this.parmArgs().record() as LedgerJournalizeReport;

        if (ledgerJournalizeReport)
        {
            // Get the current Query from the report
            queryBuildDataSource = _query.dataSourceTable(tableNum(LedgerEntryJournalizing));

            queryBuildRangeJournalizeNum = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(LedgerEntryJournalizing, Journal));

            // Set the range status to hidden if not already
            queryBuildRangeJournalizeNum.status(RangeStatus::Hidden);

            // Set the range value
            queryBuildRangeJournalizeNum.value(ledgerJournalizeReport.JournalizeNum);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Modifies the report contract before a dialog box is shown to the user.
    /// </summary>
    /// <remarks>
    /// Use this method to change the report contract. This method is called only during interactive
    /// scenario, before the UI is rendered to the user.For batch scenario, this method is not called
    /// because there is no user interaction and contract values are already saved in batch.Do not
    /// instantiate any instance variables in this code if they are used during the lifetime of the
    /// controller because these will fail in batch cases.
    /// </remarks>
    protected void prePromptModifyContract()
    {
        Query query;

        query = this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey());
        this.init(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Changes the report contract before it runs the report.
    /// </summary>
    protected void preRunModifyContract()
    {
        #ISOCountryRegionCodes

        SrsReportDataContract  contract;

        this.parmReportContract().parmReportName(ssrsReportStr(LedgerJournalizeReport,Report));

        contract = this.parmReportContract() as SrsReportDataContract;

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoPL]))
        {
            contract.parmRdlContract().setValue(#sysVersion, SysReportRun::sysVersion());
            contract.parmRdlContract().setValue(#userName, SysReportRun::userName());
            contract.parmRdlContract().setValue(#displayDescription, True);
        }
        else
        {
            contract.parmRdlContract().setValue(#displayDescription, False);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>reportViewerRefreshComplete</Name>
				<Source><![CDATA[
    public void reportViewerRefreshComplete(SRSReportExecutionInfo _executionInfo, SrsReportDataContract _reportContract = null)
    {
        int page;
        LedgerJournalizeReport              tmpLedgerJournalizeReport;

        if (!_reportContract)
        {
            _reportContract = this.parmReportContract();
        }

        super(_executionInfo, _reportContract);

        page = _reportContract.parmReportExecutionInfo().parmPrintedPhysicalPages();
        tmpLedgerJournalizeReport = this.parmArgs().record() as LedgerJournalizeReport;
        LedgerJournalizeReport::updatePageNum(tmpLedgerJournalizeReport.JournalizeNum, page);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        LedgerJournalizeReportController controller = new LedgerJournalizeReportController();
        controller.parmReportName(ssrsReportStr(LedgerJournalizeReport, Report));
        controller.parmArgs(_args);

        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>