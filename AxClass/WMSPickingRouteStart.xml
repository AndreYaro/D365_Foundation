<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsPickingRouteStart</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class WmsPickingRouteStart extends RunBase
{
    WMSPickingRouteID       wmsPickingRouteID;
    HcmWorkerRecId          worker;

    DialogField             workerField;
    DialogField             pickingRouteIdField;

    #DEFINE.CurrentVersion(2)
    #LOCALMACRO.CurrentList
         wMSPickingRouteId,
         worker
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialog = super();

        pickingRouteIdField = dialog.addField(extendedTypeStr(WMSPickingRouteID));
        pickingRouteIdField.value(wmsPickingRouteID);

        workerField = dialog.addField(extendedTypeStr(HcmWorkerRecId));
        workerField.value(worker);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        worker              = workerField.value();
        wmsPickingRouteID   = pickingRouteIdField.value();

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWMSPickingRouteId</Name>
				<Source><![CDATA[
    public WMSPickingRouteID parmWMSPickingRouteId(WMSPickingRouteID _wmsPickingRouteID = wmsPickingRouteID)
    {
        wmsPickingRouteID = _wmsPickingRouteID;
        return wmsPickingRouteID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorker</Name>
				<Source><![CDATA[
    public HcmWorkerRecId parmWorker(HcmWorkerRecId _worker = worker)
    {
        worker = _worker;
        return worker;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount
        WMSPickingRoute wmsPickingRoute;

        try
        {
            ttsbegin;

            select forupdate wmsPickingRoute
                where wmsPickingRoute.PickingRouteID == wmsPickingRouteID;

            if (!wmsPickingRoute)
            {
                throw(error(strFmt("@SYS51116", wmsPickingRouteID)));
            }

            if (wmsPickingRoute.ExpeditionStatus != WMSExpeditionStatus::Activated)
            {
                throw(error(strFmt("@SYS50574", wmsPickingRoute.PickingRouteID)));
            }

            wmsPickingRoute.start(worker);

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer     version     = conPeek(packedClass,1);

        switch (version)
        {
            case #CurrentVersion   :
                [version,#CurrentList] = packedClass;
                break;
            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS51115";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        WMSPickingRouteStart    wmsPickingRouteStart = new WMSPickingRouteStart();
        WMSPickingRoute         wmsPickingRoute;

        wmsPickingRouteStart.getLast();

        if (args && args.record())
        {
            switch (args.record().TableId)
            {
                case (tableNum(WMSPickingRoute)) :
                    wmsPickingRoute= args.record();

                    wmsPickingRouteStart.parmWMSPickingRouteId(wmsPickingRoute.PickingRouteID);
                    wmsPickingRouteStart.parmWorker(DirPersonUserEx::findUserWorkerReference());
                    if (wmsPickingRouteStart.prompt())
                    {
                        wmsPickingRouteStart.runOperation();

                        wmsPickingRoute= args.record();
                        if (FormDataUtil::isFormDataSource(wmsPickingRoute))
                        {
                            FormDataUtil::getFormDataSource(wmsPickingRoute).reread();
                            FormDataUtil::getFormDataSource(wmsPickingRoute).refresh();
                        }
                    }

                    break;

                default:
                    throw error(strFmt("@SYS19306",funcName()));
            }
        }
        else
        {
            wmsPickingRouteStart.parmWorker(DirPersonUserEx::findUserWorkerReference());
            if (wmsPickingRouteStart.prompt())
            {
                wmsPickingRouteStart.runOperation();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>