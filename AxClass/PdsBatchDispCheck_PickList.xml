<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsBatchDispCheck_PickList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class handles the disposition check for picking lists.
/// </summary>
class PdsBatchDispCheck_PickList extends PdsBatchDispCheck
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkNotRestrict</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if shipping is blocked for the module type _moduleType.
    /// </summary>
    /// <param name="_moduleType">
    /// Module type used during disposition check.
    /// </param>
    /// <returns>
    /// True if batch is not restricted, false if it is restricted.
    /// </returns>
    protected boolean checkNotRestrict(PdsDispModuleType _moduleType)
    {
        boolean                 ret = true;
        PdsDispositionMaster    dispMaster;

        dispMaster = PdsDispositionMaster::find(
                            inventBatch.PdsDispositionCode);

        switch (_moduleType)
        {
            case PdsDispModuleType::Production :
                if (dispMaster.PdsProdBlockPick)
                {
                    ret = this.setReturn();
                }
                break;

            case PdsDispModuleType::Sales:
                if (dispMaster.PdsSalesBlockPick)
                {
                    ret = this.setReturn();
                }
                break;
            case PdsDispModuleType::Transfer:
                if (dispMaster.PdsTransBlockPick)
                {
                    ret = this.setReturn();
                }
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs standard new functionality, overriden to make protected.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReturn</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns false and displays the error for the blocked <c>InventBatchId</c>.
    /// </summary>
    /// <returns>
    /// Always returns false.
    /// </returns>
    protected PdsDispBatchCheck setReturn()
    {
        if (!suppressMessage)
        {
            checkFailed(strFmt("@PRO2123",inventBatch.InventBatchId));
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCheck</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method performs the disposition check on the _common record.
    /// </summary>
    /// <param name="_common">
    /// Record to perform the disposition chekc on.
    /// </param>
    /// <param name="_batchId">
    /// Batch to perform the disposition chekc on.
    /// </param>
    /// <returns>
    /// Result of disposition check.
    /// </returns>
    public boolean validateCheck(Common _common,InventBatchId _batchId)
    {
        boolean                         ret = true;
        SalesLine                       salesLine;
        ProdBOM                         prodBOM;
        ProdJournalBOM                  prodJourBom;
        WMSOrderTrans                   wmsOrderTrans;
        WMSOrder                        wmsOrder;
        InventTransferJourLine          transferLine;
        ReqTrans                        reqTrans;
        PdsDispModuleType               modType;

        if (_batchId)
        {
            switch (_common.TableId)
            {
                case tableNum(SalesLine):
                    salesLine = _common;
                    this.parmBatch(InventBatch::find(_batchId,salesLine.ItemId));
                    modType = PdsDispModuleType::Sales;
                    break;
                case tableNum(ProdBOM):
                    prodBOM = _common;
                    this.parmBatch(InventBatch::find(_batchId, prodBOM.ItemId));
                    modType = PdsDispModuleType::Production;
                    break;
                case tableNum(WMSOrderTrans):
                    wmsOrderTrans = _common;
                    if (_batchId)
                    {
                        this.parmBatch(InventBatch
                            ::find(_batchId,wmsOrderTrans.ItemId));
                    }
                    else
                    {
                        this.parmBatch(InventBatch::find(
                            wmsOrderTrans.inventDim().InventBatchId,
                            wmsOrderTrans.ItemId));
                    }
                    switch (wmsOrderTrans.InventTransType)
                    {
                        case InventTransType::Sales :
                            modType = PdsDispModuleType::Sales;
                            break;
                        case InventTransType::ProdLine :
                            modType = PdsDispModuleType::Production;
                            break;
                        case InventTransType::TransferOrderShip :
                            modType = PdsDispModuleType::Transfer;
                            break;
                    }
                    break;
                case tableNum(WMSOrder):
                    wmsOrder = _common;
                    if (_batchId)
                    {
                        this.parmBatch(InventBatch
                            ::find(_batchId,wmsOrder.ItemId));
                    }
                    else
                    {
                        this.parmBatch(InventBatch::find(
                            wmsOrder.inventDim().InventBatchId,
                            wmsOrder.ItemId));
                    }
                    switch (wmsOrder.InventTransType)
                    {
                        case InventTransType::Sales :
                            modType = PdsDispModuleType::Sales;
                            break;
                        case InventTransType::ProdLine :
                            modType = PdsDispModuleType::Production;
                            break;
                        case InventTransType::TransferOrderShip :
                            modType = PdsDispModuleType::Transfer;
                            break;
                    }
                    break;
                case tableNum(ProdJournalBOM):
                    prodJourBom = _common;
                    this.parmBatch(InventBatch::find(_batchId, prodJourBom.ItemId));
                    modType = PdsDispModuleType::Production;
                    break;
                case tableNum(InventTransferJourLine):
                    transferLine = _common;
                    this.parmBatch(InventBatch::find(_batchId,
                        transferLine.ItemId));
                    modType = PdsDispModuleType::Transfer;
                    break;
                case tableNum(ReqTrans):
                    reqTrans = _common;
                    this.parmBatch(InventBatch::find(_batchId,reqTrans.ItemId));
                    modType = PdsDispModuleType::Planning;
                    break;
            }
        }

        ret = this.checkNotRestrict(modType);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateTransCheck</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs disposition check on inventTrans record, and either _batchId
    /// parameter or the batch related to the transaction.
    /// </summary>
    /// <param name="_inventTrans">
    /// InventTrans record to perform check on.
    /// </param>
    /// <param name="_refRecord">
    /// refrence record buffer
    /// </param>
    /// <param name="_batchId">
    /// Option batchId to perform check on.  If left blank the check is performed
    /// on the batch on the InventTrans record.
    /// </param>
    /// <returns>
    /// Result of disposition check.
    /// </returns>
    public boolean validateTransCheck(InventTrans _inventTrans,
        Common          _refRecord,
        InventBatchId   _batchId = '')
    {
        boolean                 ret = false;
        PdsDispModuleType       modType;

        if (_inventTrans)
        {
            if (!_batchId)
            {
                this.parmBatch(InventBatch::find(_inventTrans.inventBatchId(),
                                                 _inventTrans.ItemId));
            }
            else
            {
                this.parmBatch(InventBatch::find(_batchId,_inventTrans.ItemId));
            }

            switch (_refRecord.TableId)
            {
                case tableNum(SalesLine),
                     tableNum(SalesParmLine):
                    modType = PdsDispModuleType::Sales;
                    break;

                case tableNum(ProdBOM):
                    modType = PdsDispModuleType::Production;
                    break;

                case tableNum(InventTransferJourLine),
                     tableNum(InventTransferLine),
                     tableNum(InventTransferParmLine):
                    modType = PdsDispModuleType::Transfer;
                    break;

                case tableNum(ReqTrans):
                    modType = PdsDispModuleType::Planning;
                    break;

                case tableNum(InventJournalTrans):
                    if (_refRecord.(fieldNum(InventJournalTrans, JournalType)) == InventJournalType::Transfer)
                    {
                        modType = PdsDispModuleType::Transfer;
                    }
                    else
                    {
                        ret = true;
                    }
                    break;

                case tableNum(WMSOrderTrans):
                    switch (_refRecord.(fieldNum(WMSOrderTrans, InventTransType)))
                    {
                        case InventTransType::Sales :
                            modType = PdsDispModuleType::Sales;
                            break;
                        case InventTransType::ProdLine :
                            modType = PdsDispModuleType::Production;
                            break;
                        case InventTransType::TransferOrderShip :
                            modType = PdsDispModuleType::Transfer;
                            break;
                    }
                    break;

                case tableNum(WMSOrder):
                    switch (_refRecord.(fieldNum(WMSOrder, InventTransType)))
                    {
                        case InventTransType::Sales :
                            modType = PdsDispModuleType::Sales;
                            break;
                        case InventTransType::ProdLine :
                            modType = PdsDispModuleType::Production;
                            break;
                        case InventTransType::TransferOrderShip :
                            modType = PdsDispModuleType::Transfer;
                            break;
                    }
                    break;

                default:
                    ret = true;
            }
        }

        if (!ret)
        {
            ret = this.checkNotRestrict(modType);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns a new instance of the PdsBatchDispCheck_PickList class.
    /// </summary>
    /// <returns>
    /// New instance of the PdsBatchDispCheck_PickList class.
    /// </returns>
    public static PdsBatchDispCheck_PickList construct()
    {
        return new PdsBatchDispCheck_PickList();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>