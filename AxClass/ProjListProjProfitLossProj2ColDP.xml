<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjProfitLossProj2ColDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjProfitLossProj2ColDP</c> class is the report data provider class for the
///    associated <c>ProjListProjProfitLossProj2Column</c> reports.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjProfitLossProjContract))
]
class ProjListProjProfitLossProj2ColDP extends SrsReportDataProviderPreProcessTempDB
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    ProjOptionRevenue       revenue;
    ProjOptionOnAcc         onAcc;
    TransDate               projFromDate;
    TransDate               projBreakDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerBreakDate;
    TransDate               ledgerToDate;
    projActualVsBudget      actualVsBudget2Col;
    ForecastModelId         forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;

    ProjListProjTrans_ProfitLoss    listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjProfitLossTmp       projListProjProfitLossTmp;

    ProjListProjHelpClass   actualProjHelpClass;
    ProjListProjHelpClass   budgetProjHelpClass;

    AmountMST               actualTotalCost;
    AmountMST               budgetTotalCost;
    AmountMST               actualRevenue;
    AmountMST               budgetRevenue;
    AmountMST               actualMargin;
    AmountMST               budgetMargin;

    ProjTable               projTable;

    QueryRun                queryRun;

    ProjId                  projIdFound, projId;
    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    int                     showTotalCost;
    int                     showMargin;
    int                     hourQtyDecimals;
    int                     amountDecimals;
    int                     grossMarginDecimals;
    int                     valueAddedDecimals;
    str                     totalCostLabel;
    str                     revenueLabel;
    str                     marginLabel;
    str                     headerLabel;
    str                     devianceLabel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the amounts required for the report.
    /// </summary>
    void calculate()
    {
        actualProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        actualProjHelpClass.setParmField();

        actualTotalCost   += actualProjHelpClass.parmProjStatement().a300_Total_Cost(false,ProjListStateType::ProfitLoss);
        actualRevenue     += actualProjHelpClass.parmProjStatement().a100_Total_Revenue();
        actualMargin      += actualProjHelpClass.parmProjStatement().a500_Gross_Margin();

        budgetProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        budgetProjHelpClass.setParmField();

        budgetTotalCost   += budgetProjHelpClass.parmProjStatement().a300_Total_Cost(false,ProjListStateType::ProfitLoss);
        budgetRevenue     += budgetProjHelpClass.parmProjStatement().a100_Total_Revenue();
        budgetMargin      += budgetProjHelpClass.parmProjStatement().a500_Gross_Margin();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the line should print.
    /// </summary>
    /// <returns>
    ///    true if line should print; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(actualProjHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should print and calls the insert to temporary table method.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record that contains information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (listProjTransLayout.parmStateProjType() == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= listProjTransLayout.parmLevel()) ||
                (projTableLocal.levelFormat(listProjTransLayout.parmLevel())   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    actualProjHelpClass.setName();
                    this.insertProjListProjProfitLossTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjProfitLossTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the data from the <c>ProjListProjProfitLossTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>ProjListProjProfitLossTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('projListProjProfitLossTmp')]
    public ProjListProjProfitLossTmp getProjListProjProfitLossTmp()
    {
        select * from projListProjProfitLossTmp;

        return projListProjProfitLossTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>ProjListProjTrans</c> class.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans   = ProjListProjTrans::construct(ProjListStateType::ProfitLoss, ProjListStateId::Project);
        listProjTransLayout     = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);
        listProjTrans.parmTime(time);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);
        listProjTrans.parmRevenue(revenue);
        listProjTrans.parmOnAcc(onAcc);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjBreakDate(projBreakDate);
        listProjTrans.parmProjToDate(projToDate);

        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerBreakDate(ledgerBreakDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmActualVsBudget(actualVsBudget2Col);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjProfitLossTmp()
    {
        projListProjProfitLossTmp.clear();

        projListProjProfitLossTmp.Id            = actualProjHelpClass.id();
        projListProjProfitLossTmp.IdLabel       = strfmt('%1',stateProjType);
        projListProjProfitLossTmp.Name          = actualProjHelpClass.name();

        projListProjProfitLossTmp.TotalCost     = listProjTransLayout.showAmountReport(actualTotalCost, showAmount);
        projListProjProfitLossTmp.TotalCost1    = listProjTransLayout.showAmountReport(budgetTotalCost, showAmount);
        projListProjProfitLossTmp.TotalCost2    = ProjListProjHelpClass::calcActualVsBudget(projListProjProfitLossTmp.TotalCost,
                                                    projListProjProfitLossTmp.TotalCost1, actualVsBudget2Col, true);

        projListProjProfitLossTmp.Revenue       = listProjTransLayout.showAmountReport(actualRevenue, showAmount);
        projListProjProfitLossTmp.Revenue1      = listProjTransLayout.showAmountReport(budgetRevenue, showAmount);
        projListProjProfitLossTmp.Revenue2      = ProjListProjHelpClass::calcActualVsBudget(projListProjProfitLossTmp.Revenue,
                                                    projListProjProfitLossTmp.Revenue1, actualVsBudget2Col, false);

        projListProjProfitLossTmp.GrossMargin   = listProjTransLayout.showAmountReport(actualMargin, showAmount);
        projListProjProfitLossTmp.GrossMargin1  = listProjTransLayout.showAmountReport(budgetMargin, showAmount);
        projListProjProfitLossTmp.GrossMargin2  = ProjListProjHelpClass::calcActualVsBudget(projListProjProfitLossTmp.GrossMargin,
                                                    projListProjProfitLossTmp.GrossMargin1, actualVsBudget2Col, false);

        // set labels
        projListProjProfitLossTmp.HeaderLabel     = headerLabel;
        projListProjProfitLossTmp.TotalCostLabel  = totalCostLabel;
        projListProjProfitLossTmp.RevenueLabel    = revenueLabel;
        projListProjProfitLossTmp.GrossMarginLabel= marginLabel;
        projListProjProfitLossTmp.DevianceLabel   = devianceLabel;

        // set decimal values
        projListProjProfitLossTmp.DecimalHourQty     = hourQtyDecimals;
        projListProjProfitLossTmp.DecimalValueAdded  = valueAddedDecimals;
        projListProjProfitLossTmp.DecimalGrossMargin = grossMarginDecimals;
        projListProjProfitLossTmp.DecimalAmount      = amountDecimals;

        // set field visibility
        projListProjProfitLossTmp.ShowGrossMargin        = showMargin;
        projListProjProfitLossTmp.ShowTotalCost          = showTotalCost;

        projListProjProfitLossTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the amounts are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise, false.
    /// </returns>
    boolean lineValue()
    {
        return (actualTotalCost != 0 || actualRevenue != 0 ||
                budgetTotalCost != 0 || budgetRevenue != 0 ||
                actualMargin    != 0 || budgetMargin  != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = queryRun.query().dataSourceTable(tableNum(ProjTable));
        qbdsProjTable.addSortField(fieldNum(ProjTable, ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldNum(ProjTable, Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = queryRun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
            PSAContractLineItemRangesHelper::addTransactionTypeRangesToDS(
                qbdsPSAContractLineItems,
                hour,
                expenses,
                revenue,
                item,
                false);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Internal));
            }

            if (time)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Time));
            }

            if (!(timeMaterial || fixedPrice || investment || internalCost || time))
            {
                QueryBuildRange rangeDummy = qbdsProjTable.addRange(fieldNum(ProjTable, Type));
                rangeDummy.value(queryValue(SysQuery::valueEmptyString()));
            }
        }

        if (skipZero)
        {
            QueryBuildDataSource qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransPostingBudgetView)));
            qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
            qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransPostingBudgetView, ProjId));
            qbdsProjectTrans.addRange(fieldNum(ProjTransPostingBudgetView, ProjTransDate)).value(queryValue(listProjTrans.parmProjFromDate()) + '..' + queryValue(listProjTrans.parmProjToDate()));
            qbdsProjectTrans.addRange(fieldNum(ProjTransPostingBudgetView, LedgerTransDate)).value(queryValue(listProjTrans.parmLedgerFromDate()) + '..' + queryValue(listProjTrans.parmLedgerToDate()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSOurceLocal;

        projTableLocal  = queryRun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSOurceLocal = queryRun.get(tablenum(ProjFundingSource));
        }
        actualProjHelpClass.setId(projTableLocal, projId, projFundingSOurceLocal);

        if (! listProjTransLayout.canPrintId(actualProjHelpClass.idNew()))
        {
            actualProjHelpClass.setOld();

            return;
        }

        if (! firstRun && actualProjHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        actualProjHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(listProjTransLayout.parmLevel());
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SSRS at
    ///    runtime. The method should compute data and populate the data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjProfitLossProjContract contract = this.parmDataContract() as ProjListProjProfitLossProjContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();
        time = contract.parmTime();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();
        revenue = contract.parmRevenue();
        onAcc = contract.parmOnAcc();

        projFromDate = contract.parmProjFromDate();
        projToDate = contract.parmProjToDate();
        projBreakDate = contract.parmProjBreakDate();
        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();
        ledgerBreakDate = contract.parmLedgerBreakDate();

        actualVsBudget2Col = contract.parmActualVsBudget2Col();
        forecastModelId = contract.parmForecastModelIdV2();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();

        firstRun = true;

        this.initProjListTrans();

        [showTotalCost, showMargin] = listProjTrans.setDisplayFlags2Column(hour, expenses, item);
        [hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        headerLabel = listProjTrans.formatSelection(listProjTransLayout.parmShowAmount(),false);

        [totalCostLabel, revenueLabel, marginLabel, devianceLabel] = listProjTrans.setDisplayLabel2Column(hour, expenses, item);

        actualProjHelpClass       = ProjListProjHelpClass::newListProjConsumption(
                                        projStatement::newProjListProfitLoss(
                                                listProjTrans,
                                                stateProjType,
                                                ProjActualBudget::Actual,
                                                listProjTransLayout.parmAmountQty()), stateProjType);

        budgetProjHelpClass       = ProjListProjHelpClass::newListProjConsumption(
                                        projStatement::newProjListProfitLoss(
                                                listProjTrans,
                                                stateProjType,
                                                ProjActualBudget::Budget,
                                                listProjTransLayout.parmAmountQty(),
                                                forecastModelId),stateProjType);

        queryRun = new SysQueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (queryRun.next())
        {
            this.printProject();
            firstRun    = false;
        }

        if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjProfitLossTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the fields to zero.
    /// </summary>
    void setFieldZero()
    {
        actualTotalCost   = 0;
        actualRevenue     = 0;
        actualMargin      = 0;

        budgetTotalCost   = 0;
        budgetRevenue     = 0;
        budgetMargin      = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        queryRun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>