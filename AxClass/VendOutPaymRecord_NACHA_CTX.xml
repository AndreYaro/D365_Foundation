<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendOutPaymRecord_NACHA_CTX</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class VendOutPaymRecord_NACHA_CTX extends VendOutPaymRecord_NACHA
{
    //***** NACHA CTX Record Layout *****
    //
    //      Field                           Offset      Default
    //--    ------------------------------- -------     -----------
    //01    recordTypeCode                  01 - 01     6
    //02    transactionCode                 02 - 03
    //03    receivingDFI_Identification     04 - 11
    //04    checkDigit                      12 - 12
    //05    DFI_AccountNumber               13 - 29
    //06    amount                          30 - 39
    //07    identificationNumber            40 - 54
    //08    numberOfAddendaRecords          55 - 58     0 <ZERO>
    //09    receivingCompanyName            59 - 74
    //10    reserved                        75 - 76     <BLANK>
    //11    discretionaryData               77 - 78     <BLANK>
    //12    addendaRecordIndicator          79 - 79     0 <ZERO>
    //13    traceNumber                     80 - 94

    #define.interfaceName('Standard NACHA - CTX')
    #define.formatType('CTX')

    //Offsets:
    #define.numberOfAddendaRecordsOffset(55)
    #define.receivingCompanyNameOffset(59)
    #define.reservedOffset(75)
    #define.discretionaryDataOffset(77)
    #define.addendaRecordIndicatorOffset(79)
    #define.traceNumberOffset(80)

    //Field Lengths:
    #define.receivingCompanyNameLength(16)
    #define.reservedLength(2)
    #define.discretionaryDataLength(2)

    container               nacha_ctx_Record;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addCTXFieldPositions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds debug field information to the output file.
    /// </summary>
    // Add field positions and descriptions to the output file -- Debug only
    private void addCTXFieldPositions()
    {
        #define.cTXRecordLayout         ('ABBCCCCCCCCDEEEEEEEEEEEEEEEEEFFFFFFFFFFGGGGGGGGGGGGGGGHHHHIIIIIIIIIIIIIIIIJJKKLMMMMMMMMMMMMMMM - CTX PAYABLES RECORD')
        #define.cTXTraceNumber          ('                                                                               MMMMMMMMMMMMMMM - Trace Number [Journal ID + Line Number]')
        #define.cTXAddendaIndicator     ('                                                                              L - Addenda Record Indicator = "0"')
        #define.cTXDiscretionaryData    ('                                                                            KK - Discretionary Data <BLANK>')
        #define.cTXReserved             ('                                                                          JJ - Reserved <BLANK>')
        #define.cTXReceivingCompanyName ('                                                          IIIIIIIIIIIIIIII - Receiving Company Name/ID')
        #define.cTXNumAddendaRecords    ('                                                      HHHH - Number Of Addenda Records = "0000"')
        #define.cTXIdentificationNumber ('                                       GGGGGGGGGGGGGGG - Identification Number')
        #define.cTXAmount               ('                             FFFFFFFFFF - Amount [$$$$$$$$cc]')
        #define.cTXDFIAccountNumber     ('            EEEEEEEEEEEEEEEEE - DFI Account Number')
        #define.cTXCheckDigit           ('           D - Check Digit')
        #define.cTXReceivingDFI_ID      ('   CCCCCCCC - Receiving DFI Identification')
        #define.cTXTransactionCode      (' BB - Transaction Code = "22" for AP Checking, "32" for AP Savings')
        #define.cTXRecordTypeCode       ('A - Record Type Code = "6"')

        file.write(#cTXRecordLayout);
        file.write(#cTXTraceNumber);
        file.write(#cTXAddendaIndicator);
        file.write(#cTXDiscretionaryData);
        file.write(#cTXReserved);
        file.write(#cTXReceivingCompanyName);
        file.write(#cTXNumAddendaRecords);
        file.write(#cTXIdentificationNumber);
        file.write(#cTXAmount);
        file.write(#cTXDFIAccountNumber);
        file.write(#cTXCheckDigit);
        file.write(#cTXReceivingDFI_ID);
        file.write(#cTXTransactionCode);
        file.write(#cTXRecordTypeCode);
        file.write();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkValues</Name>
				<Source><![CDATA[
    public boolean checkValues(LedgerJournalTrans _ledgerJournalTrans = null,
        RunbaseProgress _progress = null)
    {
        boolean ok = true;

        ok = super(_ledgerJournalTrans, _progress);

        changecompany(custVendPaym.ledgerJournalTrans().Company)
        {
            if (!this.parmIsPrenote())
            {
                if (VendPaymMethodVal::exist(_ledgerJournalTrans.PaymMode, VendPaymentValidate::PaymReference)
                    && !_ledgerJournalTrans.PaymReference)
                {
                    ok = checkFailed("@SYS24523");
                }
            }

            if (ok)
            {
                // Only transactions with Debit Amounts can be used from Accounts Payable
                if (custVendPaym.paymAmountInSendersCurrency() <= 0)
                {
                    // Payment  must be a debit amount greater than zero.
                    if (!this.parmIsPrenote())
                    {
                        ok = checkFailed(strfmt("@SYS98765", custVendPaym.ledgerJournalTrans().parmAccount()));
                    }
                }

                vendBankAccount = this.getVendBankAccount();
                if (!vendBankAccount)
                {
                    // A bank account has not been setup for the vendor '%1'.
                    ok = checkFailed(strfmt("@SYS98116", custVendPaym.ledgerJournalTrans().parmAccount()));
                }
                else
                {
                    if (!vendBankAccount.AccountNum)
                    {
                        // Bank account number on account '%1' for vendor '%2' is missing.
                        ok = checkFailed(strfmt("@SYS82529", vendBankAccount.AccountID, custVendPaym.ledgerJournalTrans().parmAccount()));
                    }
                }

                if (vendBankAccount.bankAccountStatus(DateTimeUtil::utcNow()) == BankAccountStatus::Inactive)
                {
                    if (BankParameters::find().AllowInactiveBankPrenote == NoYes::Yes)
                    {
                        warning(strfmt("@SYS128746",vendBankAccount.AccountID,vendBankAccount.VendAccount));
                    }
                    else
                    {
                        ok = checkFailed(strfmt("@SYS127527",VendTable::find(vendBankAccount.VendAccount).Name(),vendBankAccount.AccountID));
                    }
                }
            }
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField08</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Number Of Addenda Records field in the NACHA CTX detail record. Will always be 0000 (that
    ///    is, zeros).
    /// </summary>
    // Number Of Addenda Records
    protected void fillField08()
    {
        #define.numAddendaRecords('0000')

        this.strInsert(#numAddendaRecords, #numberOfAddendaRecordsOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField09</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Receiving Company Name/ID field in the NACHA CTX detail record. Will contain the
    ///    receiver's company name.
    /// </summary>
    // Receiving Company Name/Id
    protected void fillField09()
    {
        this.strInsert(substr(custVendPaym.recieversCompanyName(), 1, #receivingCompanyNameLength), #receivingCompanyNameOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField10</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills field 10 which is reserved, and will always be blank.
    /// </summary>
    // Reserved       <BLANK>
    protected void fillField10()
    {
        #define.blank(' ')

        this.strInsert(strrep(#blank, #reservedLength), #reservedOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField11</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Discretionary Data field in the NACHA CTX detail record. Will always be blank.
    /// </summary>
    // Discretionary Data       <BLANK>
    protected void fillField11()
    {
        #define.blank(' ')

        this.strInsert(strrep(#blank, #discretionaryDataLength), #discretionaryDataOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField12</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Addenda Record Indicator field in the NACHA CTX detail record. Will always be 0 (zero).
    /// </summary>
    // Addenda Record Indicator -- Always '0' <ZERO>
    //
    // A value of '1' would indicate that one or more addenda records follow, '0' indicates no such record(s) is/are present.
    protected void fillField12()
    {
        #define.addendaIndicator('0')

        this.strInsert(#addendaIndicator, #addendaRecordIndicatorOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField13</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Trace Number field in the NACHA CTX detail record.
    /// </summary>
    // Trace Number
    //
    // This is a number that we can use to attempt to uniquely identify the transaction.
    // Ideally, this would consist of an EFT Number, which would function like a Check Number.
    //
    // But because of time constraints, the Trace Number will be generated by concatinating
    // the Journal Numer & Line Number with the following limitations to create a number that
    // will be unique for most users:
    //
    //  The Line Number portion of the Trace Number will be at least four characters,
    //  allowing for a minimum of 10000 unique lines per journal.  If the Journal Number
    //  is shorter than 11 characters, the number of characters the line number uses will
    //  grow to fill the 15 characters allowed for the Trace Number.
    //  If the Journal Number is longer than 11 characters, this method will use the
    //  right-most 11 characters from the Journal Number when generating the Trace Number.
    //  If the Line Number is greater than 9999, the right-most three characters of the
    //  Line Number will be used.
    //  So in the situation where there are only three characters for the line number, the
    //  numbers will wrap-around to 0000 each 10000 lines and icrement from there, which will
    //  make this trace number non-unique and not suitable for electronic reconcile, but it
    //  should be unique enough for most manual needs.
    //
    //  EXAMPLES:
    //      Journal Number: 000097_003
    //      Line Number:    1
    //      Trace Number: "000097_00300001"
    //
    //      Journal Number: 1234567890_003
    //      Line Number:    11036
    //      Trace Number: "4567890_0031036"
    //

    protected void fillField13()
    {
        this.strInsert(this.generateTraceNumber(), #traceNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calls each fillFieldXX method to populate the complete CTX payment record.
    /// </summary>
    protected void fillRecord()
    {
        this.fillField01();
        this.fillField02();
        this.fillField03();
        this.fillField04();
        this.fillField05();
        this.fillField06();
        this.fillField07();
        this.fillField08();
        this.fillField09();
        this.fillField10();
        this.fillField11();
        this.fillField12();
        this.fillField13();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the CTX payment output record.
    /// </summary>
    protected void initRecord()
    {
        #define.blankRoutingNumber('0')
        #define.blank(' ')

        nacha_ctx_Record = connull();
        transactionAmount = 0;
        bankRoutingNumber = strrep(#blankRoutingNumber, 8);
        outputRecord = strrep(#blank, 94);

        vendBankAccount = VendBankAccount::find(custVendPaym.ledgerJournalTrans().parmAccount(), VendTable::findByCompany(custVendPaym.ledgerJournalTrans().Company, custVendPaym.ledgerJournalTrans().parmAccount()).BankAccount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>interfaceName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the name of the specification that is to be displayed in the user interface (UI).
    /// </summary>
    /// <returns>
    ///    The name of the specification that is to be displayed in the UI.
    /// </returns>
    public PaymInterfaceName interfaceName()
    {
        return #interfaceName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>output</Name>
				<Source><![CDATA[
    /// <summary>
    /// Writes the payment detail to the electronic funds transfer output file and increments the totals
    /// for the transaction.
    /// </summary>
    public void output()
    {
        this.initRecord();
        this.fillRecord();
        custVendOutPaym.addDetailRecord(#formatType, bankRoutingNumber, 0, transactionAmount);

        nacha_ctx_Record = conins(nacha_ctx_Record, 1, outputRecord);

        file.writeExp(nacha_ctx_Record);

        if (custVendOutPaym.isDebug())
        {
            this.addCTXFieldPositions();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>