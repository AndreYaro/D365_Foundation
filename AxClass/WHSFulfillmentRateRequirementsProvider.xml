<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSFulfillmentRateRequirementsProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSFulfillmentRateRequirementsProvider</c> class is responsible
/// for providing fulfillment requirements.
/// </summary>
public class WHSFulfillmentRateRequirementsProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getErrorToleranceForSalesFormRelease</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides fulfillment rate error tolerance for releasing the sales order to warehouse
    /// using the sales order form.
    /// </summary>
    /// <param name="_salesTable">
    /// The sales order.
    /// </param>
    /// <returns>
    /// An <c>ErrorTolerance</c> value that determines how the application should react
    /// if the fulfillment rate validation failed.
    /// </returns>
    public static ErrorTolerance getErrorToleranceForSalesFormRelease(SalesTable _salesTable)
    {
        WHSFulfillmentPolicy fulfillmentPolicy = WHSFulfillmentRateRequirementsProvider::getFulfillmentPolicyForSalesOrder(_salesTable);

        return fulfillmentPolicy.FulfillmentErrorTolerance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>salesOrderFulfillmentPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method returns sales order fulfillment policy.
    /// </summary>    
    /// <returns>
    /// Sales order fulfillment policy record.
    /// </returns>
    public static WHSFulfillmentPolicy salesOrderFulfillmentPolicy()
    {
        return WHSFulfillmentPolicy::find(WHSParameters::find().SalesOrderFulFillmentPolicy);
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferOrderFulfillmentPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method returns transfer order fulfillment policy.
    /// </summary>
    /// <returns>
    /// Transfer order fulfillment policy record.
    /// </returns>
    public static WHSFulfillmentPolicy transferOrderFulfillmentPolicy()
    {
        return WHSFulfillmentPolicy::find(WHSParameters::find().TransferOrderFulfillmentPolicy);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFulfillmentTypeAndRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the type and rate of fulfillment for the specified sales order.
    /// </summary>
    /// <param name="_salesTable">
    /// The sales order.
    /// </param>
    /// <returns>
    /// A container with the fulfillment type and rate.
    /// </returns>
    public static container getFulfillmentTypeAndRate(SalesTable _salesTable)
    {
        WHSFulfillmentPolicy fulfillmentPolicy = WHSFulfillmentRateRequirementsProvider::getFulfillmentPolicyForSalesOrder(_salesTable);

        return [fulfillmentPolicy.FulfillmentType, fulfillmentPolicy.FulfillmentRate];
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFulfillmentPolicyForSalesOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the fulfillment policy that should be used when evaluating the sales order.
    /// </summary>
    /// <param name="_salesTable">
    /// The sales order.
    /// </param>
    /// <returns>
    /// The fulfillment policy that should be used when evaluating the sales order.
    /// </returns>
    public static WHSFulfillmentPolicy getFulfillmentPolicyForSalesOrder(SalesTable _salesTable)
    {
        WHSSourceOrderFullfillmentPolicyCache whsSourceOrderFullfillmentPolicyCache = WHSSourceOrderFullfillmentPolicyCache::construct();

        if (whsSourceOrderFullfillmentPolicyCache.isFulfillmentPolicyUsedForSalesOrders())
        {
            WHSSourceOrderFullfillmentPolicy whsSourceOrderFullfillmentPolicy = WHSSourceOrderFullfillmentPolicy::find(_salesTable.SalesId, WHSSourceOrderDocumentType::SalesOrder);
            WHSFulfillmentPolicy salesFulfillmentPolicy = whsSourceOrderFullfillmentPolicy.fulfillmentPolicy();
            if (salesFulfillmentPolicy)
            {
                return salesFulfillmentPolicy;
            }
        }

        return WHSFulfillmentRateRequirementsProvider::getDefaultFulfillmentPolicyForSalesOrder(_salesTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDefaultFulfillmentPolicyForSalesOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method returns sales order fulfillment policy.
    /// </summary>
    /// <param name="_salesTable">
    /// The sales order.
    /// </param>
    /// <returns>
    /// Sales order fulfillment policy record.
    /// </returns>
    public static WHSFulfillmentPolicy getDefaultFulfillmentPolicyForSalesOrder(SalesTable _salesTable)
    {
        WHSCustTable whsCustTable = WHSCustTable::find(_salesTable.CustAccount);
        WHSFulfillmentPolicy fulfillmentPolicy = whsCustTable.fulfillmentPolicy();

        if (fulfillmentPolicy)
        {
            return fulfillmentPolicy;
        }
           
        return WHSFulfillmentRateRequirementsProvider::salesOrderFulfillmentPolicy();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>