<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepreciationScheduleController_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This class is a controller class for <c>AssetDepreciationSchedule_IN</c> report.
/// </summary>
public class AssetDepreciationScheduleController_IN extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check parameters
    /// </summary>
    /// <exception cref="Exception::Error">
    /// Depreciation as per Companies Act parameter or Depreciation as per Income tax Act parameter
    /// must be selected in general ledger parameters to activate the Depreciation schedule feature.
    /// </exception>
    /// <exception cref="Exception::Error">
    /// Depreciation as per Companies Act parameter must be selected in Fixed assets parameters to
    /// activate the Component depreciation schedule feature.
    /// </exception>
    protected void checkParameters()
    {
        boolean             isDepreciationIncomeTaxActEnable_IN;
        boolean             isDepreciationCompaniesActEnable_IN;

        isDepreciationIncomeTaxActEnable_IN = AssetParameters::isDepreciationIncomeTaxActEnable_IN();
        isDepreciationCompaniesActEnable_IN = AssetParameters::isDepreciationCompaniesActEnable_IN();

        switch (args.parmEnum())
        {
            case AssetDepreciationScheduleReportType_IN::DepreciationSchedule:
                if (!isDepreciationIncomeTaxActEnable_IN && !isDepreciationCompaniesActEnable_IN)
                {
                    throw error(strfmt("@GLS6323", "@GLS6634", "@GLS6635", "@GLS5175"));
                }
                break;
            case AssetDepreciationScheduleReportType_IN::ComponentDepreciationSchedule:
                if (!isDepreciationCompaniesActEnable_IN)
                {
                    throw error(strfmt("@GLS60241", "@GLS6634", "@SYS19633", "@FixedAssets_ShiftDepreciation:SYP4882908"));
                }
                break;
            default:
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Assigns the parameter values of controller and validates the parameters.
    /// </summary>
    public void init()
    {
        this.checkParameters();
        this.setReportName();
        this.setReportType();
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        super();

        this.setDialogCaption();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDialogCaption</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the dialog caption.
    /// </summary>
    public void setDialogCaption()
    {
        switch (args.parmEnum())
        {
            case AssetDepreciationScheduleReportType_IN::DepreciationSchedule:
                this.parmDialogCaption("@GLS5175");
                break;

            case AssetDepreciationScheduleReportType_IN::ComponentDepreciationSchedule:
                this.parmDialogCaption("@FixedAssets_ShiftDepreciation:SYP4882908");
                break;

            default:
                throw error(Error::wrongUseOfFunction(funcName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReportName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Selects report
    /// </summary>
    protected void setReportName()
    {
        SRSCatalogItemName  sRSCatalogItemName;

        switch (args.parmEnum())
        {
            case AssetDepreciationScheduleReportType_IN::DepreciationSchedule:
                sRSCatalogItemName = ssrsReportStr(AssetDepreciationSchedule_IN, Report);
                break;

            case AssetDepreciationScheduleReportType_IN::ComponentDepreciationSchedule:
                sRSCatalogItemName = ssrsReportStr(AssetDepreciationSchedule_IN, ReportII);
                break;

            default:
                throw error(Error::wrongUseOfFunction(funcName()));
        }

        this.parmReportName(sRSCatalogItemName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReportType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set report type.
    /// </summary>
    protected void setReportType()
    {
        AssetDepScheduleContract_IN contract;

        contract = this.parmReportContract().parmRdpContract() as AssetDepScheduleContract_IN;
        contract.parmReportType(args.parmEnum());
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Constructs a <c>AssetDepreciationScheduleController_IN</c> class.
    /// </summary>
    /// <returns>
    ///     Returns <c>AssetDepreciationScheduleController_IN</c> class.
    /// </returns>
    public static AssetDepreciationScheduleController_IN construct()
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceIN00093, funcName());

        return new AssetDepreciationScheduleController_IN();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes the controller, validates the parameters and run <c>AssetDepreciationSchedule_IN</c> SSRS Report.
    /// </summary>
    /// <param name="_args">
    ///     The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        AssetDepreciationScheduleController_IN  controller;

        controller = AssetDepreciationScheduleController_IN::construct();
        controller.parmArgs(_args);
        controller.init();
        controller.startOperation();
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>