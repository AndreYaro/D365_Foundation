<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AccDistConstraintExtendedPrice</Name>
	<SourceCode>
		<Declaration><![CDATA[
[
SourceDocumentExtensionAttribute(MonetaryAmount::ExtendedPrice)
]
class AccDistConstraintExtendedPrice extends AccountingDistributionConstraint
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>activeDistributionIsReferenced</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the active accounting distribution is referenced.
    /// </summary>
    /// <returns>
    /// true if the active accounting distribution is referenced; otherwise, false.
    /// </returns>
    protected boolean activeDistributionIsReferenced()
    {
        boolean isReferenced;

        isReferenced = super();

        // If the accounting distribution is for a ProjAdvancedJournalLine, then ignore that it is referenced.
        if (isReferenced && isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            ProjAdvancedJournalLine projAdvancedJournalLine = ProjAdvancedJournalLine::findBySourceDocumentLine(activeAccountingDistribution.SourceDocumentLine);

            if (projAdvancedJournalLine)
            {
                AccountingDistribution  referenceAccountingDistribution;

                select firstonly ReferenceDistribution from referenceAccountingDistribution
                    where referenceAccountingDistribution.ReferenceDistribution == activeAccountingDistribution.RecId
                    && referenceAccountingDistribution.ReferenceRole != AccountingDistributionReferenceRole::WorkInProcess;

                if (!referenceAccountingDistribution.ReferenceDistribution)
                {
                    isReferenced = false;
                }
            }
        }

        return isReferenced;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canDeleteAccountingDistribution</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the active accounting distribution can be deleted.
    /// </summary>
    /// <returns>
    /// true if the active accounting distribution can be deleted; otherwise, false.
    /// </returns>
    public boolean canDeleteAccountingDistribution()
    {    
        if (isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            ProjAdvancedJournalLine projAdvancedJournalLine = ProjAdvancedJournalLine::findBySourceDocumentLine(activeAccountingDistribution.SourceDocumentLine);

            if (projAdvancedJournalLine)
            {
                AccountingDistribution  accountingDistribution;
                AccountingDistribution  referenceAccountingDistribution;

                select firstonly RecId from accountingDistribution
                    where accountingDistribution.RecId != activeAccountingDistribution.RecId
                    && accountingDistribution.SourceDocumentLine == activeAccountingDistribution.SourceDocumentLine
                    && accountingDistribution.MonetaryAmount == activeAccountingDistribution.MonetaryAmount
                    // Need to check the distribution reference. For example, the vendor invoice distribution matched to PO.
                    // We can't delete the last distribution which has a reference to a specific PO distribution.
                    && accountingDistribution.ReferenceDistribution == activeAccountingDistribution.ReferenceDistribution
                    && accountingDistribution.ReferenceRole == activeAccountingDistribution.ReferenceRole
                // To exclude the original (corrected) distribution if the document is allowed for correction.
                notexists join referenceAccountingDistribution
                    where referenceAccountingDistribution.ReferenceDistribution == accountingDistribution.RecId
                    && referenceAccountingDistribution.ReferenceRole != AccountingDistributionReferenceRole::WorkInProcess;

                if(accountingDistribution.RecId != 0)
                {
                    return true;
                }
            }
        }

        return super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>