<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AmountAdjustEngineCustInvoice4Paym_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class AmountAdjustEngineCustInvoice4Paym_W extends AmountAdjustEngine_W
{
    CustInvoice4PaymJour_RU custInvoiceJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcLine_CustInvoice4PaymTrans</Name>
				<Source><![CDATA[
    protected container calcLine_CustInvoice4PaymTrans(CustInvoice4PaymTrans_RU _line)
    {
        AmountMST amountCalculated, amountLocked;

        if (custInvoiceJour.CurrencyCode == CompanyInfoHelper::standardCurrency())
        {
            amountLocked     = _line.LineAmount;
        }
        else
        {
            amountCalculated = custInvoiceJour.amountMST(_line.LineAmount);
        }

        return [ amountCalculated, amountLocked ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcLine_MarkupTrans</Name>
				<Source><![CDATA[
    protected container calcLine_MarkupTrans(MarkupTrans _line)
    {
        AmountMST amountCalculated, amountLocked;

        if (_line.CurrencyCode == CompanyInfoHelper::standardCurrency())
        {
            amountLocked     = _line.CalculatedAmount;
        }
        else
        {
            amountCalculated = CurrencyExchangeHelper::mstAmount(_line.CalculatedAmount, _line.CurrencyCode, custInvoiceJour.Invoice4PaymDate, Currency::noYes2UnknownNoYes(custInvoiceJour.Triangulation), _line.CurrencyCode == custInvoiceJour.CurrencyCode ? custInvoiceJour.ExchRate : 0, _line.CurrencyCode == custInvoiceJour.CurrencyCode ? custInvoiceJour.ExchRateSecondary : 0);
        }

        return [ amountCalculated, amountLocked ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustInvoice4PaymJour</Name>
				<Source><![CDATA[
    protected void parmCustInvoice4PaymJour(CustInvoice4PaymJour_RU _custInvoiceJour)
    {
        ;
        custInvoiceJour = _custInvoiceJour;
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustInvoiceJour</Name>
				<Source><![CDATA[
    #ISOCountryRegionCodes
    public static void adjustInvoiceJour(CustInvoice4PaymJour_RU _invoiceJour)
    {
        AmountAdjustEngineCustInvoice4Paym_W adjustEngine;
        MarkupTrans                          markupTrans;
        CustInvoice4PaymTrans_RU             invoiceTrans;

        if (! SysCountryRegionCode::isLegalEntityInCountryRegion([ #isoRU ]) &&
            ! isConfigurationkeyEnabled(configurationkeynum(LogisticsBasic)))
            return;

        adjustEngine = new AmountAdjustEngineCustInvoice4Paym_W();
        adjustEngine.parmCustInvoice4PaymJour(_invoiceJour);
        adjustEngine.parmTotalMST(_invoiceJour.amountMST(_invoiceJour.Invoice4PaymAmount) -
                                  _invoiceJour.amountMST(_invoiceJour.SumTax)             -
                                  _invoiceJour.amountMST(_invoiceJour.RoundOff)           -
                                  _invoiceJour.amountMST(_invoiceJour.EndDisc));

        ttsbegin;

        while select forupdate markupTrans
            where markupTrans.TransTableId == _invoiceJour.TableId &&
                  markupTrans.TransRecId   == _invoiceJour.RecId
        {
            adjustEngine.addLine(markupTrans);
        }

        while select forupdate invoiceTrans
            where invoiceTrans.SalesId          == _invoiceJour.SalesId        &&
                  invoiceTrans.Invoice4PaymId   == _invoiceJour.Invoice4PaymId &&
                  invoiceTrans.Invoice4PaymDate == _invoiceJour.Invoice4PaymDate
        {
            adjustEngine.addLine(invoiceTrans);

            while select forupdate markupTrans
                where markupTrans.TransTableId == invoiceTrans.TableId &&
                      markupTrans.TransRecId   == invoiceTrans.RecId
            {
                adjustEngine.addLine(markupTrans);
            }
        }

        adjustEngine.run();

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
