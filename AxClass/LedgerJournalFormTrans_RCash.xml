<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalFormTrans_RCash</Name>
	<SourceCode>
		<Declaration><![CDATA[
class LedgerJournalFormTrans_RCash extends LedgerJournalFormTrans
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>minCashStatus</Name>
				<Source><![CDATA[
    private RCashTransStatus minCashStatus()
    {
        LedgerJournalTrans_RCash ledgerJournalTrans_RCash;
        LedgerJournalTrans       localLedgerJournalTrans;
        ;

        select minof(TransStatus) from ledgerJournalTrans_RCash
        exists join localLedgerJournalTrans
            where localLedgerJournalTrans.RecId      == ledgerJournalTrans_RCash.RefRecId &&
                  localLedgerJournalTrans.JournalNum == journalTableData.journalTable().JournalId;

        return ledgerJournalTrans_RCash.TransStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setPostMenuButtonEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables the post button on the lines form.
    /// </summary>
    protected void setPostMenuButtonEnabled()
    {
        boolean enabled;

        enabled = this.isJournalEnabled();
        enabled = enabled && ledgerJournalEngine.approved();
        enabled = enabled && this.isStatusAfterOrEqualToApproved(this.minCashStatus());
        enabled = enabled && ledgerJournalTrans.AccountType == LedgerJournalACType::RCash;

        if (ctrlPostJournalMenu)
        {
            ctrlPostJournalMenu.enabled(enabled);
        }
        else
        {
            if (ctrlPostJournalButton)
            {
                ctrlPostJournalButton.enabled(enabled);
            }

            if (ctrlPostAndTransferJournalButton)
            {
                ctrlPostAndTransferJournalButton.enabled(enabled);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSettlementButtonEnable</Name>
				<Source><![CDATA[
    protected void setSettlementButtonEnable()
    {
        boolean enabled;

        if (ctrlSettlementButton)
        {
            enabled = this.isJournalEnabled();

            enabled = enabled && !ledgerJournalTrans.isOffsetCompanyIntercompany();
            enabled = enabled && (!ledgerJournalEngine.approved() || !ledgerJournalEngine.approveActive());
            enabled = enabled && (ledgerJournalTrans.OffsetAccountType == LedgerJournalACType::Cust   ||
                                  ledgerJournalTrans.OffsetAccountType == LedgerJournalACType::Vend);

            ctrlSettlementButton.enabled(enabled);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isStatusAfterOrEqualToApproved</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the status of an <c>RCashTrans</c> is after or equal to Approved.
    /// </summary>
    /// <param name = "_status">
    ///    The status to be compared.
    /// </param>
    /// <returns>
    ///    true if the status is after or equal to the <c>RCashTransStatus::Approved</c>
    /// </returns>
    protected boolean isStatusAfterOrEqualToApproved(RCashTransStatus _status)
    {
        return _status == RCashTransStatus::Approved || _status == RCashTransStatus::Canceled;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSplitPostingButtonEnable</Name>
				<Source><![CDATA[
    protected void setSplitPostingButtonEnable()
    {
        boolean enabled;
        ;

        if (ctrlSplitPostingButton)
        {
            enabled = this.isJournalEnabled();

            enabled = enabled && LedgerJournalTrans_RCash::find(ledgerJournalTrans.RecId).TransStatus == RCashTransStatus::None;
            enabled = enabled && ledgerJournalTrans.AccountType == LedgerJournalACType::RCash;

            ctrlSplitPostingButton.enabled(enabled);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>