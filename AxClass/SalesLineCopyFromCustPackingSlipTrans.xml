<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesLineCopyFromCustPackingSlipTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesLineCopyFromSource</c> class is responsible for copying <c>SalesLine</c> from a <c>CustPackingSlipTrans</c>.
/// </summary>
[SalesLineCopyFromSourceFactory(tableStr(CustPackingSlipTrans))]
class SalesLineCopyFromCustPackingSlipTrans extends SalesLineCopyFromSource
{
    #ISOCountryRegionCodes

    CustPackingSlipTrans sourceCustPackingSlipTrans;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateTaxInformation_IN</Name>
				<Source><![CDATA[
    public void updateTaxInformation_IN()
    {
        if (TaxIntegrationUtils::isTaxInformationEnabled())
        {
            TransTaxInformationHelper::copyTransTaxInformation(sourceCustPackingSlipTrans, salesLine);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updatePriceDiscLineChangePolicy</Name>
				<Source><![CDATA[
    protected void updatePriceDiscLineChangePolicy()
    {
        salesLine.setPriceDiscChangePolicy(PriceDiscSystemSource::CopyFromSalesOrder);
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyFromSource</Name>
				<Source><![CDATA[
    protected void copyFromSource()
    {
        super();

        salesLine.initFromCustPackingSlipTrans(sourceCustPackingSlipTrans, copyPrecisely, !searchPrice, copyQty, tmpFrmVirtualLine.Qty,
                        #PdsCatchWeightEnabled ? tmpFrmVirtualLine.PdsCWQty : 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveSourceInventTransId</Name>
				<Source><![CDATA[
    public InventTransId retrieveSourceInventTransId()
    {
        return sourceCustPackingSlipTrans.InventTransId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveSourceQty</Name>
				<Source><![CDATA[
    public Qty retrieveSourceQty()
    {
        return sourceCustPackingSlipTrans.Qty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveSalesLineLoc</Name>
				<Source><![CDATA[
    public SalesLine retrieveSalesLineLoc()
    {
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoTH, #isoSA, #isoGB, #isoIE]))
        {
            return SalesLine::findInventTransId(CustPackingSlipTrans::findRecId(
                            tmpFrmVirtualLine.RecordNo).InventTransId);
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyCategoryRelatedData</Name>
				<Source><![CDATA[
    protected void copyCategoryRelatedData()
    {

        if (salesLine.isCategoryBased())
        {
            salesLineForeignTradeCategory.initFromCustPackingSlipTrans(sourceCustPackingSlipTrans);
            copyForeignTradeRecord = true;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveSource</Name>
				<Source><![CDATA[
    public Common retrieveSource()
    {
        return sourceCustPackingSlipTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    protected void initialize(SalesLine _salesLine, SalesTable _salesTable, TmpFrmVirtual _tmpFrmVirtualLines, SalesCopying _salesCopying)
    {
        super(_salesLine, _salesTable, _tmpFrmVirtualLines, _salesCopying);
        sourceCustPackingSlipTrans = CustPackingSlipTrans::findRecId(_tmpFrmVirtualLines.RecordNo);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>