<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRSalesTableForm_TM</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>MCRSalesTableForm_TM</c> class supports viewing and creating sales orders from a telemarketing call list.
/// </summary>
class MCRSalesTableForm_TM extends SalesTableForm
{
    smmCampaignTable        smmCampaignTable;
    ContactPerson           contactPerson;
    smmTMCallListTable      smmTMCallListTable;
    smmTMCallListGroup      smmTMCallListGroup;
    smmTMCallListID         smmTMCallListId_Campaign;

    CustTable           custTable;
    smmBusRelTable      smmBusRelTable;

    smmCampaignId       smmCampaignId_Campaign;

    MCRSourceID         mcrSourceCodeId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initValues</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes values for a new sales order.
    /// </summary>
    public void initValues()
    {
        MCRSalesTable       mcrSalesTable = salesTable.mcrSalesTable();

        super();

        if (custAccount)
        {
            salesTable.initFromCustTable();
        }

        salesTable.ContactPersonId = ContactPersonId;
        salesTable.initFromContactInfo();

        mcrSalesTable.SourceId = mcrSourceCodeId;
        salesTable.SmmCampaignId = smmCampaignId_Campaign;
        mcrSalesTable.SmmTMCallListID = smmTMCallListTable.CallListId;
        SalesTable.mcrPackMCRSalesTable(mcrSalesTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes vaious buffers and creates a new instance of the
    /// <c>MCRSalesTableForm_TM</c> class.
    /// </summary>
    /// <param name="_source">
    /// The passed buffer that dictates which buffers to create.
    /// </param>
    public void new(Common _source)
    {
        if (_source.TableId == tableNum(smmTMCallListTable))
        {
            smmTMCallListTable = _source;
            smmTMCallListId_Campaign       = smmTMCallListTable.CallListId;
            contactPerson   = ContactPerson::find(smmTMCallListTable.ContactPersonId);

            smmBusRelTable    = smmBusRelTable::findByParty(contactPerson.Party);
            custTable      = CustTable::findByPartyRecId(smmBusRelTable.Party);
            smmCampaignTable  = smmCampaignTable::findTMCallListId(smmTMCallListTable.CallListId);

            custAccount     = custTable.AccountNum;
            invoiceAccount  = custTable.InvoiceAccount ? custTable.InvoiceAccount: custTable.AccountNum;
            smmCampaignId_Campaign      = smmCampaignTable.CampaignId;
            contactPersonId = smmTMCallListTable.ContactPersonId;
            mcrSourceCodeId      = smmCampaignTable.MCRSourceID;
        }
        else if (_source.TableId == tableNum(smmTMCallListGroup))
        {
            smmTMCallListGroup = _source;
            smmTMCallListId_Campaign = smmTMCallListGroup.CallListID;
        }

        [advancedStartup] = [true];

        super(_source);
    }

]]></Source>
			</Method>
			<Method>
				<Name>querySalesTable</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Queries sales orders related to passed telemarketing if one was passed.
    /// </summary>
    /// <param name="querySalesTable">
    ///     The sales order query to be used as a base for sales order initialization.
    /// </param>
    /// <returns>
    ///    An updated query initialized to a telemarketing customer account.
    /// </returns>
    public Query querySalesTable(Query querySalesTable)
    {
        Query                   ret;
        QueryBuildDataSource    queryBuildDataSourceMCRSalesTable;
        QueryBuildDataSource    queryBuildDataSourceSalesTable;

        ret = super(querySalesTable);

        queryBuildDataSourceSalesTable = querySalesTable.dataSourceTable(tableNum(SalesTable));
        queryBuildDataSourceSalesTable.clearRange(fieldNum(SalesTable, CustAccount));
        queryBuildDataSourceSalesTable.clearRange(fieldNum(SalesTable, ContactPersonId));

        queryBuildDataSourceMCRSalesTable = queryBuildDataSourceSalesTable.addDataSource(tableNum(MCRSalesTable));
        queryBuildDataSourceMCRSalesTable.clearRange(fieldNum(MCRSalesTable, SmmTMCallListID));

        if (smmTMCallListId_Campaign) // record passed
        {
            queryBuildDataSourceMCRSalesTable.addRange(fieldNum(MCRSalesTable, SmmTMCallListID)).value(smmTMCallListId_Campaign);

            if (smmTMCallListID)
            {
                queryBuildDataSourceSalesTable.addRange(fieldNum(SalesTable, CustAccount)).value(custAccount);
            }
        }
        else // no record passed - called off of menu
        {
            queryBuildDataSourceMCRSalesTable.addRange(fieldNum(MCRSalesTable, SmmTMCallListID)).value(SysQuery::valueNotEmptyString());
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>