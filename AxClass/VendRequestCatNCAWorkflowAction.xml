<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendRequestCatNCAWorkflowAction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>VendRequestCatNCAWorkflowAction</c> class provides services to the submit command of the new
///    vendor request.
/// </summary>
class VendRequestCatNCAWorkflowAction
{
    VendRequest                     vendRequest;
    WorkflowVersionTable            workflowConfigurationTable;
    WorkflowComment                 workflowComment;
    boolean                         submit;
    WorkflowWorkItemTable           workflowWorkItemTable;
    menuItemName                    menuItemName;
    workflowTypeName                workflowTemplateName;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogOk</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the workflow was submitted by using the <c>EPVendRequestCategoryNCASubmit</c> Web
    ///    command.
    /// </summary>
    /// <returns>
    ///    true if the action was called by the <c>EPVendRequestCategoryNCASubmit</c> Web command; otherwise,
    ///    false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean dialogOk()
    {
        //TODO: Add new action menu. Update the 'Submit to workflow menu item' with the one added
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the workflow objects.
    /// </summary>
    /// <param name="_documentRecord">
    ///    The workflow document record that is used in the workflow.
    /// </param>
    /// <param name="_menuItemName">
    ///    The submit command to use for the new vendor request workflow.
    /// </param>
    /// <param name="_workflowConfigurationTable">
    ///    The workflow configuration table to use for the workflow.
    /// </param>
    /// <param name="_workflowWorkItemTable">
    ///    The workflow item table to use for the workflow.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void init(   Common                      _documentRecord,
                        menuItemName                _menuItemName,
                        WorkflowVersionTable        _workflowConfigurationTable,
                        WorkflowWorkItemTable       _workflowWorkItemTable
                        )
    {
        this.parmVendRequest(_documentRecord as VendRequest);
        //TODO: Valid menu item needed.
        //this.parmSubmit(_menuItemName == menuitemactionstr(VendRequestCategoryNCASubmit));
        this.parmMenuItemName(_menuItemName);

        this.parmWorkflowConfigurationTable(_workflowConfigurationTable);
        this.parmWorkflowWorkItemtable(_workflowWorkItemTable);
        this.parmWorkflowTemplateName(this.parmWorkflowConfigurationTable().workflowTable().TemplateName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMenuItemName</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public menuItemName parmMenuItemName(menuItemName _menuItemName = menuItemName)
    {
        menuItemName = _menuItemName;

        return menuItemName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSubmit</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean parmSubmit(boolean _submit = submit)
    {
        submit = _submit;
        return submit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendRequest</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or sets the vendor request record.
    /// </summary>
    /// <param name="_vendRequest">
    ///    The <c>VendRequest</c> record to set; optional.
    /// </param>
    /// <returns>
    ///    The current <c>VendRequest</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public VendRequest parmVendRequest(VendRequest _vendRequest = vendRequest)
    {
        vendRequest = _vendRequest;
        return vendRequest;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowComment</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or sets the <c>WorkflowComment</c> record.
    /// </summary>
    /// <param name="_workflowComment">
    ///    The <c>WorkflowComment</c> record to set; optional.
    /// </param>
    /// <returns>
    ///    The current <c>WorkflowComment</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowComment parmWorkflowComment(WorkflowComment _workflowComment = workflowComment)
    {
        workflowComment = _workflowComment;
        return workflowComment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowConfigurationTable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or sets the <c>WorkflowConfigurationTable</c> record.
    /// </summary>
    /// <param name="_workflowConfigurationTable">
    ///    The <c>WorkflowConfigurationTable</c> record to set; optional.
    /// </param>
    /// <returns>
    ///    The current <c>WorkflowConfigurationTable</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowVersionTable parmWorkflowConfigurationTable(WorkflowVersionTable _workflowConfigurationTable = workflowConfigurationTable)
    {
        workflowConfigurationTable = _workflowConfigurationTable;
        return workflowConfigurationTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowTemplateName</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public workflowTypeName parmWorkflowTemplateName(workflowTypeName _workflowTemplateName = workflowTemplateName)
    {
        workflowTemplateName = _workflowTemplateName;
        return workflowTemplateName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowWorkItemtable</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowWorkItemTable parmWorkflowWorkItemtable(WorkflowWorkItemTable _workflowWorkItemTable = workflowWorkItemTable)
    {
        workflowWorkItemTable = _workflowWorkItemTable;
        return workflowWorkItemTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[
    /// <summary>
    ///    The submit method is called from the <c>main</c> static method.
    /// </summary>
    /// <remarks>
    ///    This is used to start the new vendor category request workflow template.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void submit()
    {
        boolean                         checkVendor = false;
        VendRequestCategoryExtension    vendRequestCategoryExtension;

        Workflow::activateFromWorkflowType(
            this.parmWorkflowTemplateName(),
            this.parmVendRequest().RecId,
            this.parmWorkflowComment(),
            NoYes::No);

        ttsbegin;
        select  forupdate vendRequestCategoryExtension
        where   vendRequestCategoryExtension.RecId == this.parmVendRequest().RecId;

        vendRequestCategoryExtension.VendRequestWorkflowState = VendRequestWorkflowState::Submitted;
        vendRequestCategoryExtension.CategoryExtensionStatus = VendRequestCategoryStatus::CategoryRequestSubmitted;
        vendRequestCategoryExtension.update();
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateSubmit</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates the submission to workflow by the user.
    /// </summary>
    /// <returns>
    ///    true if the workflow can be submitted by the user; otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean validateSubmit()
    {
        boolean             ret = this.parmVendRequest().canSubmitToWorkflow(workFlowTypeStr(VendRequestCategoryNCATemplate));
        VendRequestProfile  vendRequestProfile;

        ret = ret && VendRequest::checkVendorQuestionnaireStatus(this.parmVendRequest().RecId, this.parmVendRequest().VendParty);
        ret = ret && VendRequestCategory::checkAllConfirmed(this.parmVendRequest().RecId);

        vendRequestProfile = this.parmVendRequest().configuration();

        if (vendRequestProfile.ShowAcceptAgreement == NoYes::Yes && this.parmVendRequest().AcceptChecked == NoYes::No)
        {
            // Terms and conditions must be accepted
            ret = checkFailed("@SYS135278");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static VendRequestCatNCAWorkflowAction construct()
    {
        return new VendRequestCatNCAWorkflowAction();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        VendRequestCatNCAWorkflowAction vendRequestCatNCAWorkflowAction = VendRequestCatNCAWorkflowAction::construct();
        vendRequestCatNCAWorkflowAction.init(args.record(), args.menuItemName(), null, null);

        if (vendRequestCatNCAWorkflowAction.validateSubmit())
        {
            if (vendRequestCatNCAWorkflowAction.dialogOk())
            {
                if (vendRequestCatNCAWorkflowAction.parmSubmit())
                {
                    vendRequestCatNCAWorkflowAction.submit();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>