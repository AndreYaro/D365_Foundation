<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResResourceNameSearchCriteria</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Resource name search criteria.
/// </summary>
public class ResResourceNameSearchCriteria extends ResSearchCriteriaItem
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applySearch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies resource names search criteria in the specified query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>Updated query applied with resource name search criteria.</returns>
    public Query applySearch(Query _query)
    {
        if (_query)
        {
            QueryBuildDataSource mainDS = _query.dataSourceNo(1);
            FieldId nameField  = this.getNameFieldID(mainDS.table());

            if (!nameField)
            {
                warning("@Res:SearchCriteria_ResourceNameNotApplicable");
            }

            // process search for resource names
            Enumerator enumerator = this.getCriteriaItemValuesEnumerator();
            QueryBuildRange range;

            while (enumerator.moveNext())
            {
                range = mainDS.addRange(nameField);
                range.value(SysQuery::valueLike(enumerator.current()));
            }
        }

        return _query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNameFieldID</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get name field ID by table ID.
    /// </summary>
    /// <param name = "_tableId">Table ID.</param>
    /// <returns>Name field ID.</returns>
    private FieldId getNameFieldID(TableId _tableId)
    {
        FieldId nameField;

        if (ResUtil::isPerformanceEnhancementFeatureEnabled(ResEnableByDefaultPerfEnhancementFeatureFlight::instance().isEnabled()))
        {
            switch (_tableId)
            {
                case tableNum(ResProjectResourceSchedulableDistinctView):
                    nameField = fieldNum(ResProjectResourceSchedulableDistinctView, Name);
                    break;

                case tableNum(ResAssignmentResourcesViewV2):
                    nameField = fieldNum(ResAssignmentResourcesViewV2, Name);
                    break;
            }
        }
        else
        {
            switch (_tableId)
            {
                case tableNum(ResCompanyResourceSchedulableDetailDistinctView):
                    nameField = fieldNum(ResCompanyResourceSchedulableDetailDistinctView, Name);
                    break;

                case tableNum(ResAssignmentResourcesDistinctView):
                    nameField = fieldNum(ResAssignmentResourcesDistinctView, Name);
                    break;
            }
        }

        return nameField;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addResourceName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a criteria value to the criteria item.
    /// </summary>
    /// <param name = "_value">Resource name value.</param>
    public void addResourceName(str _value)
    {
        this.addValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeResourceName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes the criteria value from the criteria item.
    /// </summary>
    /// <param name = "_value">Resource name value.</param>
    public void removeResourceName(str _value)
    {
        this.removeValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of resource names search criteria.
    /// </summary>
    protected void new ()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of Resource name search criteria.
    /// </summary>
    /// <returns>Instance of <c>ResResourceNameSearchCriteria</c>.</returns>
    public static ResResourceNameSearchCriteria construct()
    {
        ResResourceNameSearchCriteria searchCriteriaItem = new ResResourceNameSearchCriteria();
        return searchCriteriaItem;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>