<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBankPrenoteGeneration</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustBankPrenote</c> class generates customer specific prenotes.
/// </summary>
class CustBankPrenoteGeneration extends BankPrenoteBase
{
    private CustSumForPaymPrenote custSumForPaymPrenote;

    private DialogField dialogCustType;
    private DialogField dialogCust;
    private DialogField dialogPrenote;
    private DialogGroup dialogGroup;

    private QueryRun queryRun;

    #DEFINE.dialogPrenoteFieldNo(900)
    #DEFINE.dialogPrenoteCtrlName("Fld900_1")

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildCustomerBankAccountsQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the customer bank account information.
    /// </summary>
    /// <param name="_custAccount">
    /// The customer account ID for which to retrieve bank account information.
    /// </param>
    /// <returns>
    /// A <c>Query</c> object.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public Query buildCustomerBankAccountsQuery(CustAccount _custAccount = '')
    {
        Query query = new Query();

        QueryBuildDataSource qbds = query.addDataSource(tablenum(CustBankAccount));

        if (_custAccount != '')
        {
            QueryBuildRange qbr = qbds.addRange(fieldnum(CustBankAccount, CustAccount));
            qbr.value(queryValue(_custAccount));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createPrenoteTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>BankPrenoteJournalList</c> class and adds it to the list.
    /// </summary>
    /// <param name="_custAccount">
    /// A <c>CustAcount</c> enumeration value.
    /// </param>
    /// <param name="_custBankAccountId">
    /// A customer bank account ID to generate prenotes for.
    /// </param>
    /// <param name="_lineNum">
    /// The line number of the transaction.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void createPrenoteTrans(
        CustAccount _custAccount,
        CustBankAccountId _custBankAccountId,
        int _lineNum)
    {
        BankPrenoteJournalList prenoteJournalList = new BankPrenoteJournalList();

        prenoteJournalList.parmAccountNum(_custAccount);
        prenoteJournalList.parmBankAccountId(_custBankAccountId);
        prenoteJournalList.parmLineNum(_lineNum);

        prenoteTrans.addEnd(prenoteJournalList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds fields to the dialog box.
    /// </summary>
    /// <returns>
    /// An instance of the <c>DialogRunBase</c> class.
    /// </returns>
    public Object dialog()
    {
        Set enumSet = new Set(Types::Enum);

        dialog = super();

        dialogGroup = dialog.addGroup('');
        dialogGroup.columns(2);
        dialogGroup.frameType(FormFrameType::None);

        dialogCustType = new DialogField(dialog, enumstr(PrenoteAccountType), #dialogPrenoteFieldNo);
        dialog.addCtrlDialogField(dialogCustType.name());
        dialogCustType.init(dialog);
        dialogCustType.label("@SYS111239");
        dialogCustType.helpText("@SYS128487");
        dialogCustType.value();

        // assert if resulting field name was not set as expected as this must match the method which processes the event, e.g. Account_1_validate
        // Debug::assert(dialogCustType.fieldname() == #dialogPrenoteCtrlName);

        dialogCust = dialog.addField(extendedtypestr(CustAccount), "@SYS24500", "@SYS128526");
        dialogCust.fieldControl().enabled(false);

        // Fill in acceptable values in the ComboBox
        enumSet.add(PrenoteAccountType::AllCustomers);
        enumSet.add(PrenoteAccountType::Customers);

        // Initialize the comboBox and fill it with values
        // The first argument is null, because formRun is not available at this moment
        // The last argument provides for a way to handle the control before the form is opened
        dialogEnumComboBox = DialogEnumComboBox::newParameters(null, dialogCustType.control().id(), enumnum(PrenoteAccountType), enumSet, dialog.form());

        this.addEffectiveEntryDateToDialog(dialog);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogPostRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets a <c>FormRun</c> object of the dialog box on the enumeration combo box and selects an item in
    /// it.
    /// </summary>
    /// <param name="_dialog">
    /// An instance of the <c>DialogRunbase</c> class.
    /// </param>
    public void dialogPostRun(DialogRunbase _dialog)
    {
        super(dialog);

        if (dialogEnumComboBox)
        {
            // Specify the formRun (at this point the formRun is already available)
            // This is needed to track selection in the comboBox
            dialogEnumComboBox.parmFormRun(dialog.dialogForm().formRun());

            // Select a specific entry in the comboBox, if needed
            dialogEnumComboBox.select(PrenoteAccountType::AllCustomers);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogSelectCtrl</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called whenever a control is selected on a dialog box.
    /// </summary>
    /// <remarks>
    /// To enable this callback, the <c>allowUpdateOnSelectCtrl</c> method must return true.
    /// </remarks>
    public void dialogSelectCtrl()
    {
        // super() is not called deliberately.
    }

]]></Source>
			</Method>
			<Method>
				<Name>execute</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the prenote generation.
    /// </summary>
    public void execute()
    {
        int lineNum = 0;
        CustPrenote custPrenote;
        CustBankAccount custBankAccount;

        bankAccountTable = this.findBankAccountTable();
        prenoteTrans = new List(Types::Class);

        if (bankAccountTable.IsBankPrenote == NoYes::Yes)
        {
            queryRun = new QueryRun(this.buildCustomerBankAccountsQuery(accountNum));

            if (queryRun)
            {
                while (queryRun.next())
                {
                    custBankAccount = queryRun.get(tablenum(CustBankAccount));
                    if (!CustPrenote::exist(companyBankAccount, custBankAccount.CustAccount, custBankAccount.AccountID))
                    {
                        lineNum++;
                        this.createPrenoteTrans(custBankAccount.CustAccount, custBankAccount.AccountID, lineNum);
                    }
                }

                if (lineNum > 0)
                {
                    custSumForPaymPrenote = CustSumForPaymPrenote::construct(prenoteTrans, prenoteFilename, prenoteExportFormats, companyBankAccount);
                    custSumForPaymPrenote.parmFile(prenoteFile);
                    custSumForPaymPrenote.parmEffectiveEntryDate(effectiveEntryDate);
                    custSumForPaymPrenote.runOperation();
                    prenoteFile = custSumForPaymPrenote.parmFile();

                    queryRun.reset();

                    while (queryRun.next())
                    {
                        custBankAccount = queryRun.get(tablenum(CustBankAccount));
                        if (!CustPrenote::exist(companyBankAccount, custBankAccount.CustAccount, custBankAccount.AccountID))
                        {
                            custPrenote.BankAccountId = companyBankAccount;
                            custPrenote.CustAccount = custBankAccount.CustAccount;
                            custPrenote.CustAccountId = custBankAccount.AccountID;
                            custPrenote.PrenoteApprovedDate = DateTimeUtil::addDays(DateTimeUtil::utcNow(), bankAccountTable.PrenoteResponseDays);
                            custPrenote.insert();
                        }
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fld900_1_selectionChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables and disables the customer account field on selection of the customer type field.
    /// </summary>
    private void fld900_1_selectionChange()
    {
        Object control;
        boolean valueWasModified;

        control = dialog.formRun().controlCallingMethod();
        valueWasModified = control.modified();

        if (valueWasModified)
        {
            if (dialogCustType.value() == 0)
            {
                dialogCust.fieldControl().enabled(false);
            }
            else if (dialogCustType.value() == 1)
            {
                dialogCust.fieldControl().enabled(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the fields from the dialog box.
    /// </summary>
    /// <returns>
    /// Always returns <c>true</c>.
    /// </returns>
    public boolean getFromDialog()
    {
        boolean ret;

        ret = super();

        if (dialogEnumComboBox)
        {
            prenoteAccountType = dialogEnumComboBox.selection();
            if (prenoteAccountType == PrenoteAccountType::Customers)
            {
                accountNum = dialogCust.value();
            }
            else
            {
                accountNum = '';
            }
        }

        effectiveEntryDate = dialogEffectiveEntryDate.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </summary>
    /// <returns>
    /// The instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </returns>
    /// <remarks>
    /// This method is used if a query prompt is the dialog, and if a Select menu item is added to a dialog
    /// box. Do not create the instance of the <c>QueryRun</c> class when this method is called. Do it in
    /// the <c>unpack</c> method and the <c>initParmDefault</c> method, or in the <c>init</c> method.
    /// </remarks>
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates prenotes that qualify the selection in the dialog box.
    /// </summary>
    public void run()
    {
        super();

        this.execute();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the combination of account type and account number.
    /// </summary>
    /// <param name="_calledFrom">
    /// The object that calls this method.
    /// </param>
    /// <returns>
    /// <c>true</c> if the combination of account type and account number is valid; otherwise, <c>false</c>.
    /// </returns>
    public boolean validate(Object _calledFrom = null)
    {
        boolean isValid;

        isValid = super(_calledFrom);

        if (!this.validateAccount())
        {
            isValid = checkFailed(strfmt("@SYS116347", "@SYS24500"));
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateAccount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the combination of account type and account number.
    /// </summary>
    /// <returns>
    /// <c>true</c> if the combination of account type and account number is valid; otherwise, <c>false</c>.
    /// </returns>
    private boolean validateAccount()
    {
        boolean isValid = true;

        if (prenoteAccountType == PrenoteAccountType::Customers && accountNum == '')
        {
            isValid = false;
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ClassDescription description()
    {
        return "@SYS128497";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        CustBankPrenoteGeneration custBankPrenoteGeneration;

        custBankPrenoteGeneration = new CustBankPrenoteGeneration();

        if (custBankPrenoteGeneration.prompt())
        {
            custBankPrenoteGeneration.runOperation();

            custBankPrenoteGeneration.sendFileToUser();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>