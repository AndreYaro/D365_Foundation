<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqTransOnHandInsertBuffer</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// A 'buffer' class to collapse OnHand if it matches certain criteria.
/// </summary>
internal class ReqTransOnHandInsertBuffer 
{
    private Map reqTransBuffer = new Map(Types::Container, Types::Record);
    private ReqPlanData reqPlanData;
    private ReqTransCache reqTransCache;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    final void new(ReqPlanData _reqPlanData, ReqTransCache _reqTransCache)
    {
        reqPlanData = _reqPlanData;
        reqTransCache = _reqTransCache;
	}

]]></Source>
			</Method>
			<Method>
				<Name>flush</Name>
				<Source><![CDATA[
	/// <summary>
    /// Flush added ReqTrans to Database. 
    /// </summary>
    internal void flush()
    {
        var recordInsertList =  reqPlanData.useInsertList() ? new RecordInsertList(tableNum(ReqTrans), true) : null;
       
        MapEnumerator enumerator = reqTransBuffer.getEnumerator();
        while (enumerator.moveNext())
        {
            ReqTrans current = enumerator.currentValue();
            current.insertUsingInsertList(recordInsertList, reqPlanData);
        }

        if (recordInsertList)
        {
            recordInsertList.insertDatabase();
            reqTransCache.createReqProcessTransFilterRecords();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>add</Name>
				<Source><![CDATA[
	/// <summary>
    /// Add a ReqTrans and try to collapse if it matches certain criteria.
    /// i.e another previously added record has same InventDimId, SellableDays and ExpiryDate.
    /// </summary>
    /// <param name = "_reqTrans">The ReqTrans to add to the buffer.</param>
    internal void add(ReqTrans _reqTrans)
    {
        container key = [_reqTrans.CovInventDimId, _reqTrans.PdsSellableDays, _reqTrans.PdsExpiryDate];
        if (reqTransBuffer.exists(key))
        {
            ReqTrans existingReqTrans = reqTransBuffer.lookup(key);
            existingReqTrans.Qty += _reqTrans.Qty;
            reqTransBuffer.insert(key, existingReqTrans);
        }
        else
        {
            reqTransBuffer.insert(key, _reqTrans);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>