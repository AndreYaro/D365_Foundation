<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransferTable_BR_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTransferTable_BR_Extension</c> class is an extension class for the InventTransferTable table.
/// </summary>
[ExtensionOf(tableStr(InventTransferTable))]
internal final class InventTransferTable_BR_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>defaultToAddressName</Name>
				<Source><![CDATA[
    protected void defaultToAddressName()
    {
        next defaultToAddressName();

        DirPartyRecId party = this.getThirdPartyInventLocationParty(InventLocation::find(this.InventLocationIdTo));

        if (party)
        {
            this.ToAddressName = DirPartyTable::getName(party);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultToPostalAddress</Name>
				<Source><![CDATA[
    protected void defaultToPostalAddress()
    {
        next defaultToPostalAddress();

        DirPartyRecId party = this.getThirdPartyInventLocationParty(InventLocation::find(this.InventLocationIdTo));

        if (party)
        {
            LogisticsPostalAddress postalAddress = DirParty::primaryPostalAddress(party);
            this.ToPostalAddress = postalAddress ? postalAddress.RecId : 0;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getThirdPartyInventLocationParty</Name>
				<Source><![CDATA[
    private DirPartyRecId getThirdPartyInventLocationParty(InventLocation _inventLocation)
    {
        DirPartyRecId party;

        if (BrazilParameters::isEnabled())
        {
            if (_inventLocation.VendAccount)
            {
                party = VendTable::find(_inventLocation.VendAccount).Party;
            }
            else if (_inventLocation.CustAccount_BR)
            {
                party = CustTable::find(_inventLocation.CustAccount_BR).Party;
            }
        }

        return party;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>