<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchCORRejectsWorkflowCancelManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PurchCORRejectsWorkflowCancelManager</c> class is used to cancel a rejected lines workflow.
/// </summary>
/// <remarks>
///    This class inherits from the <c>WorkflowCancelManager</c> class and handles the update of the
///    rejected line record when the Cancel button has been pressed. The rejected line is set into a
///    pending cancelation status and is presented to the user immediately.The default dialog box and
///    cancel functionality from the <c>WorkflowCancelManager</c> class is used.
/// </remarks>
public class PurchCORRejectsWorkflowCancelManager extends WorkflowCancelManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateDocument</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the status to pending cancellation on the rejected line that is canceled.
    /// </summary>
    /// <param name="_args">
    ///    The record to cancel that should be canceled.
    /// </param>
    /// <remarks>
    ///    This method rereads the record from the data source because it is changed when submitting a
    ///    workflow. These changes may not yet be refreshed in the client.The state is set to pending
    ///    cancellation and the record is updated and refreshed in the client.
    /// </remarks>
    public void updateDocument(Args _args)
    {
        PurchCORRejectsTable purchCORRejectsTable;

        if (_args.record() is PurchCORRejectsTable)
        {
            purchCORRejectsTable = _args.record() as PurchCORRejectsTable;
            purchCORRejectsTable.reread();
        }
        else if (_args.record() is WorkflowTrackingStatusTable)
        {
            WorkflowTrackingStatusTable workflowTrackingStatusTable = _args.record() as WorkflowTrackingStatusTable;
            workflowTrackingStatusTable.reread();

            if (workflowTrackingStatusTable.ContextTableId == tableNum(PurchCORRejectsTable))
            {
                purchCORRejectsTable = PurchCORRejectsTable::find(workflowTrackingStatusTable.ContextRecId);
            }
        }

        if (purchCORRejectsTable)
        {
            ttsbegin;
            purchCORRejectsTable.selectForUpdate(true);
            purchCORRejectsTable.RejectStatus = PurchCORRejectStatus::Rejected;
            purchCORRejectsTable.WorkflowState = TradeWorkflowState::Returned;
            purchCORRejectsTable.update();
            ttscommit;

            FormDataSource purchCORRejectsTable_ds = FormDataUtil::getFormDataSource(purchCORRejectsTable);

            if (purchCORRejectsTable_ds)
            {
                purchCORRejectsTable_ds.research(true);
            }
        }
        else
        {
            throw error(strFmt("@SCM:WorkflowMissingRecordError", tableStr(PurchCORRejectsTable), tableId2Name(_args.record().TableId)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PurchCORRejectsWorkflowCancelManager construct()
    {
        return new PurchCORRejectsWorkflowCancelManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        PurchCORRejectsWorkflowCancelManager   workflowCancelManager = PurchCORRejectsWorkflowCancelManager::construct();

        workflowCancelManager.run(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>