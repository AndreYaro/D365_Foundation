<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetReservationSourceDocumentStateInProcess_PSN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetReservationSourceDocumentStateInProcess_PSN</c> class is an in-process source document state for
/// a budget reservation.
/// </summary>
[SourceDocumentClassExtensionAttribute(tableStr(BudgetReservationHeader_PSN))]
public class BudgetReservationSourceDocumentStateInProcess_PSN extends SourceDocumentStateInProcess
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSourceDocumentLineQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a query to obtain a <c>SourceDocumentLine</c> and associated <c>BudgetReservationLine_PSN</c> table records.
    /// </summary>
    /// <returns>
    /// The initialized query.
    /// </returns>    
    protected Query getSourceDocumentLineQuery(SourceDocumentHeaderRecId _sourceDocumentHeaderRecId)
    {
        QueryBuildDataSource    retrieveSourceDocumentLineQbds;
        QueryBuildDataSource    budgetReservationLine_PSNQbds;
        QueryBuildDataSource    budgetReservationHeader_PSNQbds;
        Query                   query;
    
        query = super(_sourceDocumentHeaderRecId);
    
        retrieveSourceDocumentLineQbds = query.dataSourceNo(query.dataSourceCount());
        budgetReservationLine_PSNQbds = retrieveSourceDocumentLineQbds.addDataSource(tableNum(BudgetReservationLine_PSN));
        budgetReservationLine_PSNQbds.joinMode(JoinMode::OuterJoin);
        budgetReservationLine_PSNQbds.addLink(fieldNum(SourceDocumentLine, RecId),fieldNum(BudgetReservationLine_PSN, SourceDocumentLine));
    
        budgetReservationHeader_PSNQbds = budgetReservationLine_PSNQbds.addDataSource(tableNum(BudgetReservationHeader_PSN));
        budgetReservationHeader_PSNQbds.joinMode(JoinMode::InnerJoin);
        budgetReservationHeader_PSNQbds.addLink(fieldNum(BudgetReservationLine_PSN, BudgetReservationHeader_PSN), fieldNum(BudgetReservationHeader_PSN, RecId));
    
        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSourceDocumentLineQueryUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the getSourceDocumentLineQuery() implementation using the SysDa framework
    /// </summary>
    /// <param name = "_sourceDocumentHeaderRecId">Source document header record</param>
    /// <returns>The initialized query.</returns>
    protected SysDaQueryObject getSourceDocumentLineQueryUsingSysDa(SourceDocumentHeaderRecId _sourceDocumentHeaderRecId)
    {
        SysDaQueryObject    ret = super(_sourceDocumentHeaderRecId);

        SourceDocumentLine  sourceDocumentLine = ret.table();

        BudgetReservationLine_PSN  budgetReservationLine_PSN;
        SysDaQueryObject  sysDaQueryLineObject = new SysDaQueryObject(budgetReservationLine_PSN);

        ret.joinClause(SysDaJoinKind::OuterJoin, sysDaQueryLineObject);
        sysDaQueryLineObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(budgetReservationLine_PSN, fieldStr(BudgetReservationLine_PSN, SourceDocumentLine)),
                new SysDaFieldExpression(sourceDocumentLine, fieldStr(SourceDocumentLine, RecId))));

        BudgetReservationHeader_PSN budgetReservationHeader_PSN;
        SysDaQueryObject sysDaQueryTableObject = new SysDaQueryObject(budgetReservationHeader_PSN);

        sysDaQueryLineObject.joinClause(SysDaJoinKind::InnerJoin, sysDaQueryTableObject);
        sysDaQueryTableObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(budgetReservationHeader_PSN, fieldStr(BudgetReservationHeader_PSN, RecId)),
                new SysDaFieldExpression(budgetReservationLine_PSN, fieldStr(BudgetReservationLine_PSN, BudgetReservationHeader_PSN))));

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAdditionalInformationMap</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a map holding the <c>BudgetReservationHeader_PSN</c> record associated with the specific source document line.
    /// </summary>
    /// <param name="_queryRun">
    /// The query run object holding the source document line implementation records.
    /// </param>
    /// <returns>
    /// The initialized map.
    /// </returns>    
    protected Map initAdditionalInformationMap(QueryRun _queryRun)
    {
        List tableList = new List(Types::Integer);
        tableList.addEnd(tableNum(BudgetReservationHeader_PSN));

        Map additionalInformationMap = this.fillMap(_queryRun, tableList);
    
        return additionalInformationMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAdditionalInformationMapUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the implementation of initAdditionalInformationMap() using the SysDa framework
    /// </summary>
    /// <param name = "_sdso">SysDaSearchObject instead of QueryRun</param>
    /// <returns>
    /// The initialized map.
    /// </returns>
    protected Map initAdditionalInformationMapUsingSysDa(SysDaSearchObject _sdso)
    {
        List tableList = new List(Types::Integer);
        tableList.addEnd(tableNum(BudgetReservationHeader_PSN));

        Map additionalInformation = this.fillMapUsingSysDa(_sdso, tableList);

        return additionalInformation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSourceDocumentLineImplementation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the <c>BudgetReservationLine_PSN</c> record associated with the <c>SourceDocumentLine</c> instance.
    /// </summary>
    /// <param name="_queryRun">
    /// The query run object holding the <c>BudgetReservationLine_PSN</c> records.
    /// </param>
    /// <returns>
    /// The <c>SourceDocumentLineImplementation</c> instance of a <c>BudgetReservationLine_PSN</c> record.
    /// </returns>    
    protected SourceDocumentLineImplementation initSourceDocumentLineImplementation(QueryRun _queryRun)
    {
        BudgetReservationLine_PSN   budgetReservationLine_PSN = _queryRun.get(tableNum(BudgetReservationLine_PSN));
    
        return budgetReservationLine_PSN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSDLImplementationUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the implementation of initSDLImplementation(), using the SysDa framework.
    /// </summary>
    /// <param name = "_sdso">
    /// The SysDa Search object containing the query to search in.
    /// </param>
    /// <returns>
    /// The CustInvoiceLine record as <c>SourceDocumentLineImplementation</c>.
    /// </returns>
    protected SourceDocumentLineImplementation initSDLImplementationUsingSysDa(SysDaSearchObject _sdso)
    {
        BudgetReservationLine_PSN   budgetReservationLine_PSN = this.sysDaGetRecordByTableId(_sdso.query(), tableNum(BudgetReservationLine_PSN));

        return budgetReservationLine_PSN;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>