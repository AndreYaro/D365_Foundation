<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResPlannedResourcesCollection</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Resource assignment collection controller.
/// </summary>
public class ResPlannedResourcesCollection extends ResActivityResourcesCollection implements IAVCollectionController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getCollectionQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for all planned resources of the activity.
    /// </summary>
    /// <returns>Instance of query object.</returns>
    public Query getCollectionQuery()
    {
        ResAvailabilityViewContext context = this.getContext();
        ResourceRecId resource = context.resource();

        Query query = new Query();
        QueryBuildDataSource dataSource = query.addDataSource(tableNum(ResPlannedResourcesView));

        // set activityResource
        dataSource.addRange(fieldNum(ResPlannedResourcesView, ActivityResource)).value(queryValue(resource));

        // set Name sorting
        dataSource.addSortField(fieldNum(ResPlannedResourcesView, Name));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolveEntity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Serializes common record to data contract instance.
    /// </summary>
    /// <param name = "_entity">Record instance.</param>
    /// <returns>Instance of <c>CollectionItemDataContract</c>.</returns>
    public CollectionItemDataContract resolveEntity(Common _entity)
    {
        List properties = new List(Types::Class);

        EntityFieldDataContract kvPair = EntityFieldDataContract::construct("Name", _entity.getFieldValue("Name"));
        properties.addEnd(kvPair);

        kvPair = EntityFieldDataContract::construct("Role", _entity.getFieldValue("ResourceCategoryId"));
        properties.addEnd(kvPair);

        str collectionKey = this.buildCollectionItemKey(
            _entity.getFieldValue("Activity"),
            _entity.getFieldValue("Resource"),
            _entity.getFieldValue("ActivityResource"));

        CollectionItemDataContract resource = CollectionItemDataContract::construct();
        resource.CollectionKey(collectionKey);
        resource.Properties(properties);
        return resource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of the planned resources collection controller.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the planned resources collection controller.
    /// </summary>
    /// <returns>Instance of <c>ResPlannedResourcesCollection</c>.</returns>
    public static ResPlannedResourcesCollection construct()
    {
        ResPlannedResourcesCollection controller = new ResPlannedResourcesCollection();
        return controller;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>