<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBalanceDP_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustBalanceDP_CN</c> class is report data provider class for customer balance report.
/// </summary>
class CustBalanceDP_CN
{
    CustVendLedgerBalanceTmp_CN custBalanceTmp_CN;

    Map openingBalanceMap;
    Map periodBalanceMap;
    Map defaultDimensionMap;
    Set keySet;

    TransDate fromDate;
    TransDate toDate;

    private const int DebitAmountPosition = 1;
    private const int CreditAmountPosition = 2;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>prepareData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fetches the required data for the report.
    /// </summary>
    /// <param name = "_contract">
    /// The instance of <c>CustBalanceContract_CN</c> .
    /// </param>
    /// <param name = "_finDimDataContract">
    /// The instance of <c>ERFinDimensionsRunDataContract</c> .
    /// </param>
    public CustVendLedgerBalanceTmp_CN prepareData(CustBalanceContract_CN _contract, ERFinDimensionsRunDataContract _finDimDataContract = null)
    {
        openingBalanceMap = new Map(Types::Container, Types::Real);
        periodBalanceMap = new Map(Types::Container, Types::Container);
        defaultDimensionMap = new Map(Types::Container, Types::Int64);
        keySet = new Set(Types::Container);

        fromDate = _contract.parmLedgerFiscalPeriodNameContractER_CN().parmFromDate();
        toDate = _contract.parmLedgerFiscalPeriodNameContractER_CN().parmToDate();

        Query q = new Query(queryStr(CustBalance_CN));

        QueryBuildDataSource qbdsCustTrans = q.dataSourceTable(tableNum(CustTrans));
        QueryBuildDataSource qbdsCustTable = q.dataSourceTable(tableNum(CustTable));
        SysQuery::findOrCreateRange(qbdsCustTrans, fieldNum(CustTrans, TransDate)).value(SysQuery::range(dateNull(), toDate));
        SysQuery::findOrCreateRange(qbdsCustTrans, fieldNum(CustTrans, PostingProfile)).value(SysQuery::range(_contract.parmFromProfile(), _contract.parmToProfile()));
        SysQuery::findOrCreateRange(qbdsCustTable, fieldNum(CustTable, CustGroup)).value(SysQuery::range(_contract.parmFromCustGroup(), _contract.parmToCustGroup()));
        SysQuery::findOrCreateRange(qbdsCustTable, fieldNum(CustTable, SalesGroup)).value(SysQuery::range(_contract.parmFromSalesGroup(), _contract.parmToSalesGroup()));

        // Get dimesion attributes from the dimension set
        container dimensionAttributeNameCon;
        if (_finDimDataContract)
        {
            ERFinancialDimensionsHelper eRFinancialDimensionsHelper = ERFinancialDimensionsHelper::construct();
            List dimensionAttributeNameList = eRFinancialDimensionsHelper.getValidDimensionSequenceFromFinDimDetailsRunContract(_finDimDataContract);
            if (dimensionAttributeNameList)
            {
                ListEnumerator enumerator = dimensionAttributeNameList.getEnumerator();
                while (enumerator.moveNext())
                {
                    dimensionAttributeNameCon += enumerator.current();
                }
            }
        }

        CustTrans custTrans, lastCustTrans;
        CustTable custTable, lastCustTable;
        DirPartyTable dirPartyTable;
        CustGroup custGroup;
        CommissionSalesGroup salesGroup;
        DirPartyName lastDirPartyName;
        Description lastCustGroupName;
        Name lastSalesGroupName;
        container dimenionValueCon;

        QueryRun qr = new QueryRun(q);

        while (qr.next())
        {
            DimensionAttributeValueSetItem dimensionAttributeValueSetItem = qr.get(tableNum(DimensionAttributeValueSetItem));
            DimensionAttribute dimensionAttribute = qr.get(tableNum(DimensionAttribute));

            // Go through the dimension value
            if (!qr.changed(tableNum(CustTrans)))
            {
                int dimensionValuePosition = conFind(dimensionAttributeNameCon, dimensionAttribute.Name);
                if (dimensionValuePosition)
                {
                    dimenionValueCon = conPoke(dimenionValueCon, dimensionValuePosition, dimensionAttributeValueSetItem.DisplayValue);
                }
            }
            // New CustTrans
            else
            {
                // Calculate the begin amount, credit amount, and debit amount for last Cust trans
                this.calculateAmountsForCustTrans(lastCustTrans, lastCustTable, lastCustGroupName, lastSalesGroupName, lastDirPartyName, dimenionValueCon);

                dimenionValueCon = conNull();
                for (int i = 1; i <= conLen(dimensionAttributeNameCon); i++)
                {
                    dimenionValueCon += '';
                }

                int dimensionValuePosition = conFind(dimensionAttributeNameCon, dimensionAttribute.Name);
                if (dimensionValuePosition)
                {
                    dimenionValueCon = conPoke(dimenionValueCon, dimensionValuePosition, dimensionAttributeValueSetItem.DisplayValue);
                }

                custTrans = qr.get(tableNum(CustTrans));
                custTable = qr.get(tableNum(CustTable));
                dirPartyTable = qr.get(tableNum(DirPartyTable));
                custGroup = qr.get(tableNum(CustGroup));
                salesGroup = qr.get(tableNum(CommissionSalesGroup));

                lastCustTrans = custTrans.data();
                lastCustTable = custTable.data();
                lastDirPartyName = dirPartyTable.Name;
                lastCustGroupName = custGroup.Name;
                lastSalesGroupName = salesGroup.Name;
            }
        }

        // calculate last record
        this.calculateAmountsForCustTrans(lastCustTrans, lastCustTable, lastCustGroupName, lastSalesGroupName, lastDirPartyName, dimenionValueCon);
        // Insert records
        this.insertCustBalanceTmpTable();

        return custBalanceTmp_CN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateAmountsForCustTrans</Name>
				<Source><![CDATA[
    private void calculateAmountsForCustTrans(
        CustTrans _custTrans,
        CustTable _custTable,
        Description _custGroupName,
        Name _salesGroupName,
        DirPartyName _dirPartyName,
        container _dimensionValueCon)
    {
        AmountMST beginBalance, debitAmount, creditAmount;
        container keyContainerLocal;

        if (!_custTrans.AccountNum)
        {
            return;
        }

        keyContainerLocal = [
            _custTrans.AccountNum,
            _custTrans.PostingProfile,
            _dirPartyName,
            _custTable.CreditMax,
            _custTable.CustGroup,
            _custGroupName,
            _custTable.SalesGroup,
            _salesGroupName,
            _dimensionValueCon];

        if (!keySet.in(keyContainerLocal))
        {
            keySet.add(keyContainerLocal);
            defaultDimensionMap.insert(keyContainerLocal, _custTrans.DefaultDimension);
        }

        if (_custTrans.TransDate < fromDate)
        {
            if (openingBalanceMap.exists(keyContainerLocal))
            {
                openingBalanceMap.insert(
                    keyContainerLocal,
                    openingBalanceMap.lookup(keyContainerLocal) + _custTrans.AmountMST);
            }
            else
            {
                openingBalanceMap.insert(keyContainerLocal, _custTrans.AmountMST);
            }
        }
        else
        {
            creditAmount += (_custTrans.AmountMST < 0 && !_custTrans.Correct) ?
                -_custTrans.AmountMST : 0;
            debitAmount  += (_custTrans.AmountMST >= 0 || (_custTrans.AmountMST < 0 && _custTrans.Correct)) ?
                _custTrans.AmountMST : 0;

            container periodAmountContainerLocal = [0, 0];
            if (periodBalanceMap.exists(keyContainerLocal))
            {
                periodAmountContainerLocal = periodBalanceMap.lookup(keyContainerLocal);
                creditAmount += conPeek(periodAmountContainerLocal, CreditAmountPosition);
                debitAmount  += conPeek(periodAmountContainerLocal, DebitAmountPosition);
            }
            periodAmountContainerLocal = conPoke(periodAmountContainerLocal, CreditAmountPosition, creditAmount);
            periodAmountContainerLocal = conPoke(periodAmountContainerLocal, DebitAmountPosition, debitAmount);

            periodBalanceMap.insert(keyContainerLocal, periodAmountContainerLocal);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertCustBalanceTmpTable</Name>
				<Source><![CDATA[
    private void insertCustBalanceTmpTable()
    {
        Amount              amountDebitLocal;
        Amount              amountCreditLocal;
        Amount              beginningBalanceAmountLocal;
        Amount              endingBalanceAmountLocal;
        CustPostingProfile  custPostingProfileLocal;
        SetEnumerator       setEnumerator = keySet.getEnumerator();
        container           keyContainerLocal;
        container           dimensionValueConLocal;

        while (setEnumerator.moveNext())
        {
            amountDebitLocal            = 0;
            amountCreditLocal           = 0;
            BeginningBalanceAmountLocal = 0;
            endingBalanceAmountLocal    = 0;

            keyContainerLocal = setEnumerator.current();
            [
                custBalanceTmp_CN.AccountNum,
                custPostingProfileLocal,
                custBalanceTmp_CN.AccountName,
                custBalanceTmp_CN.CreditLimit,
                custBalanceTmp_CN.AccountGroup,
                custBalanceTmp_CN.AccountGroupName,
                custBalanceTmp_CN.OperatorGroup,
                custBalanceTmp_CN.OperatorGroupName,
                dimensionValueConLocal
            ] = keyContainerLocal;

            if (custPostingProfileLocal)
            {
                LedgerDimensionDefaultAccount summaryAccount = CustLedgerAccounts::summaryLedgerDimension(custBalanceTmp_CN.AccountNum, custPostingProfileLocal);
                MainAccount mainAccount = MainAccount::find(DimensionAttributeValueCombination::find(summaryAccount).MainAccount);
                custBalanceTmp_CN.MainAccount = mainAccount.MainAccountId;
                custBalanceTmp_CN.MainAccountName = mainAccount.Name;
            }

            if (openingBalanceMap.exists(keyContainerLocal))
            {
                BeginningBalanceAmountLocal     = openingBalanceMap.lookup(keyContainerLocal);
            }

            if (periodBalanceMap.exists(keyContainerLocal))
            {
                [amountDebitLocal, amountCreditLocal] = periodBalanceMap.lookup(keyContainerLocal);
            }

            endingBalanceAmountLocal = BeginningBalanceAmountLocal + amountDebitLocal - amountCreditLocal;

            custBalanceTmp_CN.DefaultDimension = defaultDimensionMap.lookup(keyContainerLocal);
            custBalanceTmp_CN.BeginBalance  = BeginningBalanceAmountLocal;
            custBalanceTmp_CN.DebitAmount   = amountDebitLocal;
            custBalanceTmp_CN.CreditAmount  = amountCreditLocal;
            custBalanceTmp_CN.EndBalance    = endingBalanceAmountLocal;
            custBalanceTmp_CN.doInsert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>