<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSSlotDemandGenerateUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class builds the dialog box that shows when running the slotting process from the menu item.
/// </summary>
internal final class WHSSlotDemandGenerateUIBuilder extends SysOperationAutomaticUIBuilder
{
    private WHSSlotDemandGenerateContract   slotDemandContract;
    private DialogField                     dialogFieldGenerateDemand;
    private DialogField                     dialogFieldLocateDemand;
    private DialogField                     dialogFieldCreateReplen;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateDemandModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// A method used to replace the modified method on the generate demand check box.
    /// </summary>
    /// <param name="_checkBoxControl">
    /// The modified checkbox control.
    /// </param>
    /// <returns>
    /// Returns true to show the check box has been modified.
    /// </returns>
    [Hookable(false)]
    internal boolean generateDemandModified(FormCheckBoxControl _checkBoxControl)
    {
        dialogFieldLocateDemand.enabled(dialogFieldGenerateDemand.value());

        if (!dialogFieldGenerateDemand.value())
        {
            dialogFieldLocateDemand.value(false);

            this.locateDemandModified(_checkBoxControl);
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>locateDemandModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// A method used to override the modified method of the locate demand check box.
    /// </summary>
    /// <param name="_checkBoxControl">
    /// The modified check box control.
    /// </param>
    /// <returns>
    /// Returns true to show the check box has been modified.
    /// </returns>
    [Hookable(false)]
    internal boolean locateDemandModified(FormCheckBoxControl _checkBoxControl)
    {
        dialogFieldCreateReplen.enabled(dialogFieldLocateDemand.value());

        if (!dialogFieldLocateDemand.value())
        {
            dialogFieldCreateReplen.value(false);
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Called after the dialog is setup, overrides the modified methods for some check boxes to make them dependant.
    /// </summary>
    [Hookable(false)]
    final public void postBuild()
    {
        super();

        slotDemandContract = this.dataContractObject();

        dialogFieldGenerateDemand   = this.bindInfo().getDialogField(slotDemandContract, methodStr(WHSSlotDemandGenerateContract, parmGenerateDemand));
        dialogFieldLocateDemand     = this.bindInfo().getDialogField(slotDemandContract, methodStr(WHSSlotDemandGenerateContract, parmLocateDemand));
        dialogFieldCreateReplen     = this.bindInfo().getDialogField(slotDemandContract, methodStr(WHSSlotDemandGenerateContract, parmCreateReplen));

        dialogFieldGenerateDemand.registerOverrideMethod(methodStr(FormCheckBoxControl, modified), methodStr(WHSSlotDemandGenerateUIBuilder, generateDemandModified), this);
        dialogFieldLocateDemand.registerOverrideMethod(methodStr(FormCheckBoxControl, modified), methodStr(WHSSlotDemandGenerateUIBuilder, locateDemandModified), this);

        dialogFieldLocateDemand.enabled(dialogFieldGenerateDemand.value());
        dialogFieldCreateReplen.enabled(dialogFieldLocateDemand.value());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>