<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventAgingCmdCalcAmounts</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventAgingCmdCalcAmounts</c> class implements a command to calculate the amounts on
/// a line in the aging report.
/// </summary>
class InventAgingCmdCalcAmounts implements InventAgingICommand
{
    InventAgingReportLines          inventAgingReportLines;
    InventAgingCmdAggregateCost     inventAgingCmdAggregateCost;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>execute</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the amount for each period and the on hand.
    /// </summary>
    public void execute()
    {
        InventAgingTmp line = inventAgingReportLines.parmLine();

        inventAgingCmdAggregateCost.setResultOnLine(line);

        line.AmountOnHand = line.QtyOnHand * line.UnitPrice;
        line.AmountofAgingPeriod1 = line.QtyofAgingPeriod1 * line.UnitPrice;
        line.AmountofAgingPeriod2 = line.QtyofAgingPeriod2 * line.UnitPrice;
        line.AmountofAgingPeriod3 = line.QtyofAgingPeriod3 * line.UnitPrice;
        line.AmountofAgingPeriod4 = line.QtyofAgingPeriod4 * line.UnitPrice;
        line.AmountofAgingPeriod5 = line.QtyofAgingPeriod5 * line.UnitPrice;

        inventAgingReportLines.insert(line);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>InventAgingCmdCalcAmounts</c> class.
    /// </summary>
    /// <param name="_inventAgingReportLines">
    /// The instance of the <c>InventAgingReportLines</c> that calls execute.
    /// </param>
    /// <param name="_inventAgingCmdAggregateCost">
    /// The instance of the <c>InventAgingCmdAggregateCost</c> with the inventory cost.
    /// </param>
    protected void initialize(
        InventAgingReportLines          _inventAgingReportLines,
        InventAgingCmdAggregateCost     _inventAgingCmdAggregateCost)
    {
        inventAgingCmdAggregateCost = _inventAgingCmdAggregateCost;
        inventAgingReportLines = _inventAgingReportLines;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        protected static InventAgingCmdCalcAmounts construct()
    {
        return new InventAgingCmdCalcAmounts();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>InventAgingCmdCalcAmounts</c> class.
    /// </summary>
    /// <param name="_inventAgingReportLines">
    /// The instance of the <c>InventAgingReportLines</c> that calls execute.
    /// </param>
    /// <param name="_inventAgingCmdAggregateCost">
    /// The instance of the <c>InventAgingCmdAggregateCost</c> with the inventory cost.
    /// </param>
    /// <returns>
    /// An instance of the <c>InventAgingCmdCalcAmounts</c> class.
    /// </returns>
    public static InventAgingCmdCalcAmounts newFromParameters(
        InventAgingReportLines          _inventAgingReportLines,
        InventAgingCmdAggregateCost     _inventAgingCmdAggregateCost)
    {
        InventAgingCmdCalcAmounts inventAgingCmdCalcAmounts = InventAgingCmdCalcAmounts::construct();
        inventAgingCmdCalcAmounts.initialize(_inventAgingReportLines, _inventAgingCmdAggregateCost);

        return inventAgingCmdCalcAmounts;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
