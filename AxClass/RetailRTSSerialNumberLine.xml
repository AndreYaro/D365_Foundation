<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailRTSSerialNumberLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailRTSSerialNumberLine</c> class represents the serial number line retrieved from an xml containing serial number line data.
/// </summary>
public class RetailRTSSerialNumberLine
{
    internal const str SerialNumberLineName            = 'SerialNumberLine';
    internal const str LineIdAttribute                 = 'LineId';
    internal const str SourceInventTransRecIdAttribute = 'SourceInventTransRecId';
    internal const str InventSerialIdAttribute         = 'SerialNumber';
    internal const str QtyAttribute                    = 'Quantity';

    private str            lineId;
    private RecId          sourceInventTransRecId;
    private InventSerialId inventSerialId;
    private InventQty      qty;
    private XmlElement     xmlElement;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fromXml</Name>
				<Source><![CDATA[
    /// <summary>
    /// Converts the xml formatted serial number lines to <c>RetailRTSSerialNumberLine</c> objects.
    /// </summary>
    /// <param name = "serialNumberLineNodes">The xml nodes containing the serial number lines.</param>
    /// <returns>A list of <c>RetailRTSSerialNumberLine</c> objects.</returns>
    public static List fromXml(XmlNodeList serialNumberLineNodes)
    {
        List serialNumberLines = new List(Types::Class);

        // create the serial number line object representations.
        XmlElement serialNumberLineNode = serialNumberLineNodes.nextNode();
        while (serialNumberLineNode)
        {
            RetailRTSSerialNumberLine serialNumberLine = new RetailRTSSerialNumberLine();

            serialNumberLine.xmlElement             = serialNumberLineNode;
            serialNumberLine.lineId                 = serialNumberLineNode.getAttribute(LineIdAttribute);
            serialNumberLine.sourceInventTransRecId = str2int64(serialNumberLineNode.getAttribute(SourceInventTransRecIdAttribute));
            serialNumberLine.inventSerialId         = serialNumberLineNode.getAttribute(InventSerialIdAttribute);
            serialNumberLine.qty                    = str2Num((serialNumberLineNode.getAttribute(QtyAttribute)));

            serialNumberLines.addEnd(serialNumberLine);

            serialNumberLineNode = serialNumberLineNodes.nextNode();
        }

        return serialNumberLines;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLineId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the serial number line id.
    /// </summary>
    /// <param name = "_lineId">The serial number line id.</param>
    /// <returns>The serial number line id.</returns>
    public str parmLineId(str _lineId = lineId)
    {
        this.lineId = _lineId;
        return this.lineId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceInventTransRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the source invent transaction RecId.
    /// </summary>
    /// <param name = "_sourceInventTransRecId">The source invent transaction RecId.</param>
    /// <returns>The source invent transaction RecId.</returns>
    public RecId parmSourceInventTransRecId(RecId _sourceInventTransRecId = sourceInventTransRecId)
    {
        this.sourceInventTransRecId = _sourceInventTransRecId;
        return sourceInventTransRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventSerialId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the invent serial id.
    /// </summary>
    /// <param name = "_inventSerialId">The invent serial Id.</param>
    /// <returns>The <c>InventSerialId</c>.</returns>
    public InventSerialId parmInventSerialId(InventSerialId _inventSerialId = inventSerialId)
    {
        this.inventSerialId = _inventSerialId;
        return inventSerialId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the quantity.
    /// </summary>
    /// <param name = "_qty">The quantity.</param>
    /// <returns>The quantity.</returns>
    public InventQty parmQty(InventQty _qty = qty)
    {
        this.qty = _qty;
        return this.qty;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>