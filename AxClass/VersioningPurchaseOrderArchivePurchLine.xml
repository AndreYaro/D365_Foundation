<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VersioningPurchaseOrderArchivePurchLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>VersioningPurchaseOrderArchivePurchLine</c> class archive <c>PurchLine</c> records.
/// </summary>
class VersioningPurchaseOrderArchivePurchLine extends SysQueryInsertRecordSet
{
    protected QueryBuildDataSource  purchLineDataSource;
    protected QueryBuildDataSource  purchTableValidPeriodVersionDataSource;

    protected PurchTableVersion     purchTableVersion;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializePurchTableVersionFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies filters to the selection query based on the <c>PurchTableVersion</c> record.
    /// </summary>
    /// <param name = "_purchTableVersion">
    /// A <c>PurchTableVersion</c> record used to filter by.
    /// </param>
    protected void initializePurchTableVersionFilter(PurchTableVersion _purchTableVersion)
    {
        purchTableVersion.data(_purchTableVersion);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    protected Query initQuery()
    {
        Query query = new Query();

        purchLineDataSource = query.addDataSource(tableNum(PurchLine));

        purchLineDataSource.addRange(fieldNum(PurchLine, PurchId)).value(queryValue(purchTableVersion.PurchId));

        purchLineDataSource.addRange(fieldNum(PurchLine, IsModified)).value(queryValue(NoYes::Yes));
        purchLineDataSource.addRange(fieldNum(PurchLine, IsDeleted)).value(queryValue(NoYes::No));

        purchTableValidPeriodVersionDataSource = purchLineDataSource.addDataSource(tableNum(PurchTableValidPeriodVersion));
        purchTableValidPeriodVersionDataSource.relations(false);

        purchTableValidPeriodVersionDataSource.addRange(fieldNum(PurchTableValidPeriodVersion, RecId)).value(queryValue(purchTableVersion.RecId));

        query.clearAllFields();

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFieldMapping</Name>
				<Source><![CDATA[
    protected void initFieldMapping()
    {
        SysDictField selectDictField;
        SysDictField insertDictField;

        TableId purchLineTableId = tableNum(PurchLine);
        TableId insertTableId = tableNum(PurchLineHistory);

        PurchLineDictVersioning purchLineDictVersioning = PurchLineDictVersioning::createInstance();

        Enumerator mapFieldEnumerator = purchLineDictVersioning.versionedDictFieldsEnumerator();

        while (mapFieldEnumerator.moveNext())
        {
            SysDictField mapDictField = mapFieldEnumerator.current();

            selectDictField = new SysDictField(purchLineTableId, purchLineDictVersioning.versionedFieldId(mapDictField, purchLineTableId));
            insertDictField = new SysDictField(insertTableId, purchLineDictVersioning.versionedFieldId(mapDictField, insertTableId));

            this.mapField(purchLineDataSource.uniqueId(), selectDictField.name(), insertDictField.name());
        }

        selectDictField = new SysDictField(tableNum(PurchTableValidPeriodVersion), fieldNum(PurchTableValidPeriodVersion, ValidFromDateTime));
        insertDictField = new SysDictField(insertTableId, fieldNum(PurchLineHistory, ValidFrom));

        this.mapField(purchTableValidPeriodVersionDataSource.uniqueId(), selectDictField.name(), insertDictField.name());

        selectDictField = new SysDictField(tableNum(PurchTableValidPeriodVersion), fieldNum(PurchTableValidPeriodVersion, ValidToDateTime));
        insertDictField = new SysDictField(insertTableId, fieldNum(PurchLineHistory, ValidTo));

        this.mapField(purchTableValidPeriodVersionDataSource.uniqueId(), selectDictField.name(), insertDictField.name());
    }

]]></Source>
			</Method>
			<Method>
				<Name>initTargetCursor</Name>
				<Source><![CDATA[
    protected Common initTargetCursor()
    {
        PurchLineHistory purchLineHistoryInsert;

        return purchLineHistoryInsert;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromPurchTableVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a <c>VersioningPurchaseOrderArchivePurchLine</c> used to archive <c>PurchLine</c> record associated with a purchase order version.
    /// </summary>
    /// <param name = "_purchTableVersion">
    /// A <c>PurchTableVersion</c> record for which the <c>PurchLine</c> records should be archived.
    /// </param>
    /// <returns>
    /// The instantiated <c>VersioningPurchaseOrderArchivePurchLine</c> object.
    /// </returns>
    public static VersioningPurchaseOrderArchivePurchLine newFromPurchTableVersion(PurchTableVersion _purchTableVersion)
    {
        VersioningPurchaseOrderArchivePurchLine instance = new VersioningPurchaseOrderArchivePurchLine();

        instance.initializePurchTableVersionFilter(_purchTableVersion);

        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>