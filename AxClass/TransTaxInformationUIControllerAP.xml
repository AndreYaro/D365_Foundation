<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TransTaxInformationUIControllerAP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The UI controller class for controlling all Accounts Payable on new tax information UI and transaction update.
/// </summary>
public class TransTaxInformationUIControllerAP extends TransTaxInformationUIController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>modifiedVATNonRecoverablePercent</Name>
				<Source><![CDATA[
    /// <summary>
    /// Override the modified method to VAT non recoverable percent.
    /// </summary>
    /// <param name="_vatNonRecoverablePercent">
    /// The form data source field VAT non recoverable percent.
    /// </param>
    protected void modifiedVATNonRecoverablePercent(FormDataObject _vatNonRecoverablePercent)
    {
        transTaxInformation.VATNonRecoverablePercent = min(100, max(0, transTaxInformation.VATNonRecoverablePercent));
    }

]]></Source>
			</Method>
			<Method>
				<Name>setVisibleToCustTaxInfoCtrls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set visable or invisible to all customer tax information controls on Tax information form.
    /// </summary>
    protected void setVisibleToCustTaxInfoCtrls()
    {
        super();

        if (sourceTable.TableId == tablenum(LedgerJournalTrans))
        {
            LedgerJournalTrans ledgerJournalTrans = LedgerJournalTrans::findRecId(sourceTable.RecId, false);

            if ((ledgerJournalTrans.AccountType == LedgerJournalACType::Cust && ledgerJournalTrans.LedgerDimension != 0)
                || (ledgerJournalTrans.OffsetAccountType == LedgerJournalACType::Cust && ledgerJournalTrans.OffsetLedgerDimension != 0))
            {
                transTaxInformationCustTaxInfoCtrl.parmCustomerTaxInformation().visible(true);
            }
            else
            {
                transTaxInformationCustTaxInfoCtrl.parmCustomerTaxInformation().visible(false);
            }
        }
        else
        {
            transTaxInformationCustTaxInfoCtrl.parmCustomerTaxInformation().visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setVisibleToVendTaxInfoCtrls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set visable or invisible to all vendor tax information controls on Tax information form.
    /// </summary>
    protected void setVisibleToVendTaxInfoCtrls()
    {
        super();

        if (sourceTable.TableId == tablenum(LedgerJournalTrans))
        {
            if (LedgerJournalTrans::findRecId(sourceTable.RecId, false).AccountType == LedgerJournalACType::Cust
            || LedgerJournalTrans::findRecId(sourceTable.RecId, false).OffsetAccountType == LedgerJournalACType::Cust)
            {
                transTaxInformationVendorTaxInfoCtrl.parmVendorTaxInformation().visible(false);
            }
            else
            {
                transTaxInformationVendorTaxInfoCtrl.parmVendorTaxInformation().visible(true);
            }
        }
        else
        {
            transTaxInformationVendorTaxInfoCtrl.parmVendorTaxInformation().visible(true);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>