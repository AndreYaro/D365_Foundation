<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsShipmentLineUnload</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>WMSShipmentLineUnload</c> class handles the unload operation by updating shipment lines to
///    status Staged.
/// </summary>
class WmsShipmentLineUnload implements WMSOrderTransMultiUpdateHandleable
{
    Counter     linesToUpdate;
    Map         wmsOrderTransMoveQtyMap;    //Key: recId, value: qty to be moved
    Map         wmsOrderTransMoveCWQtyMap;  //Key: recId, value: CW qty to be moved
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkSplitTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Splits the <c>WMSOrderTrans</c> record if only part of its quantity
    ///    (and its CW quantity when applicable) should be unloaded.
    /// </summary>
    /// <param name="_wmsOrderTrans">
    ///    The <c>WMSOrderTrans</c> that should be unloaded.
    /// </param>
    /// <remarks>
    ///    The warehouse management order transaction is split only if part of its quantity should be moved.
    /// </remarks>
    protected void checkSplitTrans(WMSOrderTrans _wmsOrderTrans)
    {
        // If this method is updated, the functionality in the method of the same name in
        // the other WMSShipmentLine* classes may also need to be updated.

        InventQty       qtyToMove;
        PdsCWInventQty  cwQtyToMove;

        if (wmsOrderTransMoveQtyMap && wmsOrderTransMoveQtyMap.exists(_wmsOrderTrans.RecId))
        {
            qtyToMove = wmsOrderTransMoveQtyMap.lookup(_wmsOrderTrans.RecId);
            if (qtyToMove && _wmsOrderTrans.Qty > qtyToMove)
            {
                if (wmsOrderTransMoveCWQtyMap && wmsOrderTransMoveCWQtyMap.exists(_wmsOrderTrans.RecId))
                {
                    cwQtyToMove = wmsOrderTransMoveCWQtyMap.lookup(_wmsOrderTrans.RecId);
                    if (cwQtyToMove && _wmsOrderTrans.PdsCWQty > cwQtyToMove)
                    {
                        _wmsOrderTrans.split(_wmsOrderTrans.Qty - qtyToMove,
                                             _wmsOrderTrans.PdsCWQty - cwQtyToMove);
                    }
                }
                else
                {
                    _wmsOrderTrans.split(_wmsOrderTrans.Qty - qtyToMove);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLinesToUpdate</Name>
				<Source><![CDATA[
        public Counter parmLinesToUpdate(Counter _linesToUpdate = linesToUpdate)
    {
        linesToUpdate = _linesToUpdate;
        return linesToUpdate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWmsOrderTransMoveCWQtyMap</Name>
				<Source><![CDATA[
        public Map parmWmsOrderTransMoveCWQtyMap(Map _wmsOrderTransMoveCWQtyMap = wmsOrderTransMoveCWQtyMap)
    {
        wmsOrderTransMoveCWQtyMap = _wmsOrderTransMoveCWQtyMap;
        return wmsOrderTransMoveCWQtyMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWmsOrderTransMoveQtyMap</Name>
				<Source><![CDATA[
        public Map parmWmsOrderTransMoveQtyMap(Map _wmsOrderTransMoveQtyMap = wmsOrderTransMoveQtyMap)
    {
        wmsOrderTransMoveQtyMap = _wmsOrderTransMoveQtyMap;
        return wmsOrderTransMoveQtyMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateWMSOrderTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Updates the <c>WMSOrderTrans</c> record based on the user selections.
    /// </summary>
    /// <param name="_wmsOrderTrans">
    ///    The <c>WMSOrderTrans</c> record that should be updated.
    /// </param>
    void updateWMSOrderTrans(WMSOrderTrans _wmsOrderTrans)
    {
        if (_wmsOrderTrans.canBeUnloaded() && wmsOrderTransMoveQtyMap.exists(_wmsOrderTrans.RecId))
        {
            this.checkSplitTrans(_wmsOrderTrans);//split trans if needed

            _wmsOrderTrans.wmsOrderTransType().unload();

            linesToUpdate--;

            if (!linesToUpdate)
            {
                _wmsOrderTrans.wmsShipment(true).setStatus(NoYes::No, NoYes::No);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        protected static WMSShipmentLineUnload construct()
    {
        return new WMSShipmentLineUnload();
    }

]]></Source>
			</Method>
			<Method>
				<Name>unloadSelectedLines</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Handles the unload operations for all selected <c>WMSOrderTrans</c> records.
    /// </summary>
    /// <param name="_wmsOrderTransMap">
    ///    A map that contains <c>RecIds</c> as a key and a container with an <c>OrderId</c> and
    ///    <c>RecVersion</c> as the value.
    /// </param>
    /// <param name="_wmsOrderTransMoveQtyMap">
    ///    A map that contains <c>RecIds</c> as a key and the quantity that should be moved as the value.
    /// </param>
    /// <param name="_wmsOrderTransMoveCWQtyMap">
    ///    A map that contains <c>RecIds</c> as a key and the catch weight quantity that should
    ///    be moved as value; optional.
    /// </param>
    public static void unloadSelectedLines(Map _wmsOrderTransMap,        //Key: recId, value: wmsOrderId : used to hit index on select
                                                  Map _wmsOrderTransMoveQtyMap, //Key: recId, value: qty to be unloaded
                                                  Map _wmsOrderTransMoveCWQtyMap = null)  //Key: recId, value: CW qty to be loaded
    {
        WMSShipmentLineUnload   wmsShipmentLineUnload = WMSShipmentLineUnload::construct();

        if (!_wmsOrderTransMap || !_wmsOrderTransMoveQtyMap)
        {
            return;
        }

        wmsShipmentLineUnload.parmLinesToUpdate(_wmsOrderTransMap.elements());
        wmsShipmentLineUnload.parmWmsOrderTransMoveQtyMap(_wmsOrderTransMoveQtyMap);
        wmsShipmentLineUnload.parmWmsOrderTransMoveCWQtyMap(_wmsOrderTransMoveCWQtyMap);

        WMSOrderTrans::loopWMSOrderTransMulti(_wmsOrderTransMap.pack(),wmsShipmentLineUnload);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>