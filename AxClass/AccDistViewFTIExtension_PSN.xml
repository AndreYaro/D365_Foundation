<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AccDistViewFTIExtension_PSN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AccDistViewFTIExtension_PSN</c> class handles the Accounting Distribution view events for Free text Invoice.
/// </summary>
[SourceDocumentEnumClassExtAttribute(enumStr(SourceDocument_CustomerInvoice), SourceDocument_CustomerInvoice::CustomerFreeTextInvoice, classStr(AccountingDistributionFormView)),
SourceDocumentEnumClassExtAttribute(enumStr(SourceDocumentLine_CustomerInvoice), SourceDocumentLine_CustomerInvoice::CustomerFreeTextInvoiceLine, classStr(AccountingDistributionFormView))]
public class AccDistViewFTIExtension_PSN extends AccDistViewExtension
{
    AccountingDistributionFormView  accountingDistributionFormView;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the extension of the accounting distribution view.
    /// </summary>
    public void initialize()
    {
        accountingDistributionFormView = accountingDistributionView;

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAccDistControlAccess</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the accessibility of accounting distribution view controls.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void setAccDistControlAccess()
    {
        if (!this.isAccountingDistributionEditable())
        {
            accountingDistributionFormView.parmLedgerDimensionControl().enabled(false);
            accountingDistributionFormView.parmDistributionPercentControl().allowEdit(false);
            accountingDistributionFormView.parmAllocationMethodControl().allowEdit(false);
            accountingDistributionFormView.parmSplitDistributionControl().enabled(false);
            accountingDistributionFormView.parmDeleteDistributionControl().enabled(false);
            accountingDistributionFormView.parmResetDistributionControl().enabled(false);
            accountingDistributionFormView.parmDistributeEquallyControl().enabled(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>subscribe</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the events of the accounting distribution view extension.
    /// </summary>
    protected void subscribe()
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(PublicSector)))
        {
            AccountingDistributionFormView.initializedViewForDistributionControls += eventhandler(this.setAccDistControlAccess);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAccountingDistributionEditable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the editing is allowed for accounting distributions based on the billing code version.
    /// </summary>
    /// <returns>
    /// true if the editing is allowed, otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected boolean isAccountingDistributionEditable()
    {
        CustBillingCodeVersion  custBillingCodeVersion;
        CustInvoiceLine         custInvoiceLine;
        InvoiceDate             invoiceDate = CustInvoiceTable::findSourceDocumentHeader(accountingDistributionFormView.parmCallerSourceDocumentHeader().RecId).InvoiceDate;
        boolean                 allowEdit = true;

        select ValidTimeState(invoiceDate) AllowChangeLedgerAccount from custBillingCodeVersion
             exists join custInvoiceLine
                where custInvoiceLine.SourceDocumentLine == accountingDistributionFormView.parmCallerSourceDocumentLine().RecId
                    && custBillingCodeVersion.CustBillingCode == custInvoiceLine.CustBillingCode;

        if (custBillingCodeVersion != null && custBillingCodeVersion.AllowChangeLedgerAccount == NoYes::No)
        {
            allowEdit = false;
        }

        return allowEdit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>