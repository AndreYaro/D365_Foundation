<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailCatalogCopySchedulerJob</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailCatalogCopySchedularJob</c> class is a dialog class
/// used to schedule retail catalog copy job.
/// </summary>
public class RetailCatalogCopySchedulerJob extends RunBaseBatch implements BatchRetryable
{
    int64       catalogId;
    Catalog     targetCatalog;
    FormRun     callerForm;
    container   packedRetailCatalogCopyFlags;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        catalogId,
        packedRetailCatalogCopyFlags
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getLast</Name>
				<Source><![CDATA[
    public void getLast()
    {
        // override so we preserve theinput parameters and not retrieve the last ones.
        // super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
	/// <summary>
    /// Serializes the current instance of the <c>RetailCatalogCopySchedulerJob</c> class.
    /// </summary>
    /// <returns>A container that contains the current instance of <c>RetailCatalogCopySchedulerJob</c>.</returns>
    public container pack()
    {
        container ret;

        ret = [#CurrentVersion,#CurrentList];

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCatalogId</Name>
				<Source><![CDATA[
    public CatalogRefRecId parmCatalogId(CatalogRefRecId _catalogId = catalogId)
    {
        catalogId = _catalogId;

        return catalogId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTargetCatalog</Name>
				<Source><![CDATA[
    public Catalog parmTargetCatalog(Catalog _targetCatalog = targetCatalog)
    {
        targetCatalog = _targetCatalog;

        return targetCatalog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRetailCatalogCopyFlag</Name>
				<Source><![CDATA[
    /// <summary>
    /// Save the packed object.
    /// </summary>
    /// <param name="_retailCatalogCopyFlags">
    /// Retail catalog copy flags from the dialog.
    /// </param>
    public void setRetailCatalogCopyFlag(RetailCatalogCopyFlags _retailCatalogCopyFlags)
    {
        packedRetailCatalogCopyFlags = _retailCatalogCopyFlags.pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
	/// <summary>
    /// Runs the implementation of the class.
    /// </summary>
    public void run()
    {
        CatalogManager catalogManager = CatalogManager::construct(catalogId);

        RetailCatalogCopyFlags  locCatalogCopyFlag = new RetailCatalogCopyFlags();

        locCatalogCopyFlag.unpack(packedRetailCatalogCopyFlags);

        targetCatalog = catalogManager.copyCatalog(locCatalogCopyFlag);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
	/// <summary>
    /// Deserializes the <paramref name="_packedClass"/> parameter to the current instance of <c>RetailCatalogCopySchedulerJob</c>.
    /// </summary>
    /// <param name = "packedClass">The container from which to deserialize the instance.</param>
    /// <returns>true if deserialization was successful; otherwise, false.</returns>
    public boolean unpack(container packedClass)
    {
        Version version = RunBase::getVersion(packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static RetailCatalogCopySchedulerJob construct()
    {
        return new RetailCatalogCopySchedulerJob();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
	/// <summary>
    /// Provides class description.
    /// </summary>
    /// <returns>The <c>ClassDescription</c>.</returns>
    public static ClassDescription description()
    {
        return "@SYS132119";
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the copy catalog form for the selection.
    /// </summary>
    /// <returns>The dialog box for the Copy catalog.</returns>
    public Object dialog()
    {
        DialogRunbase dialog = Dialog::newFormnameRunbase(formstr(RetailCopyCatalogDialog), this);

        dialog = this.dialogInternal(dialog);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// The main method which initializes the call for the other methods.
    /// </summary>
    /// <param name="_args">
    /// Retail catalog scheduler job record.
    /// </param>
    /// <param name="_supress_dialog">
    /// Boolean value, which shows suppress dialog state.
    /// </param>
    public static void main(Args _args, boolean _supress_dialog = false)
    {
        MenuFunction menuFunction;

        Args args = new Args();

        RetailCatalogCopySchedulerJob catalogCopy = RetailCatalogCopySchedulerJob::construct();

        Catalog inputCatalog = _args.record();

        catalogCopy.parmCatalogId(inputCatalog.RecId);

        if (!_supress_dialog)
        {
            if (catalogCopy.prompt())
            {
                catalogCopy.runOperation();

                if (!catalogCopy.isInBatch())
                {
                    // Open RetailCatalogDetails form for new catalog in Edit mode.
                    menuFunction = new MenuFunction(menuitemDisplayStr(RetailCatalogDetails), MenuItemType::Display);
                    args.record(args.lookupRecord(catalogCopy.parmTargetCatalog()));
                    menuFunction.openMode(OpenMode::Edit);
                    menuFunction.run(args);
                }
            }
        }
        else
        {
            catalogCopy.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns a value indicating if the <c>RetailCatalogCopySchedulerJob</c>
    /// class can be run in a new session.
    /// </summary>
    /// <returns>true if the the class <c>RetailCatalogCopySchedulerJob</c> can
    /// be run in a new session; otherwise, false.</returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>