<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxFiscalDocumentCancel_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is responsible for reversing a tax fiscal document, which is
/// the way we process the cancelling of a fiscal document.
/// </summary>
class TaxFiscalDocumentCancel_BR
{
    TaxFiscalDocument_BR        newTaxFiscalDocument, originalTaxFiscalDocument;
    boolean                     reverseSign;
    RefRecId                    reasonTableRef, originalTaxFiscalDocumentRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Performs the fiscal document order copy process
    /// </summary>
    /// <returns>
    /// RecID of the new <c>TaxFiscalDocumentTable_BR</c> record created.
    /// </returns>
    /// <remarks>
    ///     Performs the copy of the fiscal document. Reverses the sign of the amount.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public RecId copy()
    {
        TaxFiscalDocumentLine_BR        newtaxFiscalDocLine, oldtaxFiscalDocLine;

        ttsbegin;

        this.copyHeader();

        while select oldtaxFiscalDocLine
            where oldtaxFiscalDocLine.TaxFiscalDocument == originalTaxFiscalDocumentRecId
        {
            newtaxFiscalDocLine.clear();
            newtaxFiscalDocLine.TaxCode                   = oldtaxFiscalDocLine.TaxCode;
            newtaxFiscalDocLine.cfopTable_BR              = oldtaxFiscalDocLine.cfopTable_BR;
            newtaxFiscalDocLine.TaxFiscalDocument         = newTaxFiscalDocument.RecId;
            newtaxFiscalDocLine.Description               = oldtaxFiscalDocLine.Description;
            newtaxFiscalDocLine.Amount                    = -oldtaxFiscalDocLine.Amount;

            newtaxFiscalDocLine.insert();
        }

        ttscommit;

        return newTaxFiscalDocument.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyHeader</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Copies the header from the original posted fiscal document to the new fical document order being created to revert the original one
    /// </summary>
    /// <remarks>
    ///     Not all information necessary for the order are present in the posted fiscal document. So, in order to copy all the necessary data
    ///     to the reversal document, the original order is being retrieved and its data are being copied as well.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void copyHeader()
    {
        FiscalDocument_BR originalFiscalDocument;

        ttsbegin;

        select forupdate originalTaxFiscalDocument
            where originalTaxFiscalDocument.RecId == originalTaxFiscalDocumentRecId;

        newTaxFiscalDocument.initFromTaxFiscalDocument(originalTaxFiscalDocument);
        newTaxFiscalDocument.Status = TaxFiscalDocumentStatus_BR::Open;
        newTaxFiscalDocument.CancelReason = reasonTableRef;
        newTaxFiscalDocument.Reversal = true;
        newTaxFiscalDocument.TaxFiscalDocToCancelRecID = originalTaxFiscalDocument.RecId;
        newTaxFiscalDocument.insert();

        originalTaxFiscalDocument.Status = TaxFiscalDocumentStatus_BR::CancelInProgress;
        originalTaxFiscalDocument.CancelReason = reasonTableRef;
        originalTaxFiscalDocument.update();

        originalFiscalDocument = FiscalDocument_BR::findByRef(originalTaxFiscalDocument.TableId, originalTaxFiscalDocument.RecId);
        FiscalDocumentTextUpdate_BR::newFromRecord(newTaxFiscalDocument).copy(originalFiscalDocument);

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a new class for a tax fiscal document that will be canceled.
    /// </summary>
    /// <param name="_originalTaxFiscalDocument">
    /// Record ID of the tax fiscal document to be canceled.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void new(RefRecId _originalTaxFiscalDocument)
    {
        originalTaxFiscalDocumentRecId = _originalTaxFiscalDocument;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReasonCodeRef</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the reason code reference.
    /// </summary>
    /// <param name="_reasonCodeRef">
    /// The reason code to be set (optional).
    /// </param>
    /// <returns>
    /// The current reason code for this copy.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public RecId parmReasonCodeRef(RecId _reasonCodeRef = reasonTableRef)
    {
        reasonTableRef = _reasonCodeRef;
        return reasonTableRef;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>