<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSCreateSerialNumber</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSCreateSerialNumber</c> class is used to generate serial number for items.
/// </summary>
public class WHSCreateSerialNumber
{
    protected ItemId itemId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getInventSerialId</Name>
				<Source><![CDATA[
	/// <summary>
	/// Get a serial id from user.
	/// </summary>
	/// <returns>
	/// The serial id.
	/// </returns>
    protected InventSerialId getInventSerialId()
    {
        WHSSerialId     serialId;

        Dialog dialog = new Dialog();
        dialog.caption("@WAX2115");
        dialog.windowType(FormWindowType::PopUp);

        DialogField dfSerialId = dialog.addField(extendedTypeStr(WHSSerialId));

        if (dialog.run())
        {
            serialId = dfSerialId.value();
        }

        return serialId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmItemId</Name>
				<Source><![CDATA[
    public ItemId parmItemId(ItemId _itemId = itemId)
    {
        itemId = _itemId;

        return itemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
	/// Creates a new serial id from user input for an item.
	/// </summary>
	/// <returns>
	/// Serial id inputted by user.
	/// </returns>
    public WHSSerialId run()
    {
        WHSSerialId serialId = this.getInventSerialId();

        if (serialId)
        {
            WHSCreateSerialNumber::createNewSerialId(itemId, serialId);
        }

        return serialId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static WHSCreateSerialNumber construct()
    {
        return new WHSCreateSerialNumber();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createNewSerialId</Name>
				<Source><![CDATA[
    internal static void createNewSerialId(itemId _itemId, WHSSerialId _serialId)
    {
        InventSerial inventSerial;

        ttsbegin;

        if (!InventSerial::exist(_serialId, _itemId))
        {
            inventSerial.InventSerialId = _serialId;
            inventSerial.ItemId = _itemId;

            if (!inventSerial.validateWrite())
            {
                throw error(strFmt("@WAX:WHSCannotInsertRecordInTable", tablePName(InventSerial)));
            }
            else
            {
                inventSerial.insert();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
	/// Creates a new instance with an item id.
	/// </summary>
	/// <param name="_itemId">
	/// Item id to be stored in the instance.
	/// </param>
	/// <returns>
	/// The newly created instance.
	/// </returns>
    public static WHSCreateSerialNumber newStandard(ItemId _itemId)
    {
        WHSCreateSerialNumber whsCreateSerialNumber = WHSCreateSerialNumber::construct();
        whsCreateSerialNumber.parmItemId(_itemId);

        return whsCreateSerialNumber;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>