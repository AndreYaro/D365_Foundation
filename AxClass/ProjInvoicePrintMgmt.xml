<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjInvoicePrintMgmt</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class to handle report format
/// </summary>
public class ProjInvoicePrintMgmt
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createOrUpdateInvoicePrintSettings</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Create or update the print settings
    /// </summary>
    /// <param name="_document">
    ///  _document record passed
    /// </param>
    /// <param name="_nodeType">
    /// _nodeType type of document
    /// </param>
    /// <param name="_reportFormat">
    /// _reportFormat report format description
    /// </param>
    /// <exception cref="Exception::UpdateConflictNotRecovered">
    /// throws exception if update conflict is not recovered
    /// </exception>
    /// <exception cref="Exception::UpdateConflict">
    /// throws exception if update conflict occurs
    /// </exception>
    /// <exception cref="Exception::Error">
    /// throws exception in case of error
    /// </exception>
    public static void createOrUpdateInvoicePrintSettings(Common _document,PrintMgmtNodeType _nodeType,PrintMgmtReportFormatDescription _reportFormat)
    {
        #OCCRetryCount
        PrintMgmtDocInstance        printMgmtDocInstance;
        PrintMgmtSettings           printMgmtSettings;
        RecId                       reportFmtRecId;

        reportFmtRecId = PrintMgmtReportFormat::findByDescription(PrintMgmtDocumentType::SIProjInvoice,_reportFormat).RecId;
        printMgmtDocInstance =  PrintMgmtDocInstance::findByPrintType(_document.RecId,
                                                                        _document.TableId,
                                                                        _nodeType,
                                                                        PrintMgmtDocumentType::SIProjInvoice,
                                                                        PrintMgmtDocInstanceType::Original);
        if (printMgmtDocInstance)
        {
            try
            {
                ttsbegin;
                printMgmtSettings = PrintMgmtSettings::find(printMgmtDocInstance.RecId,printMgmtDocInstance.PriorityId,true);
                printMgmtSettings.ReportFormat = reportFmtRecId;
                printMgmtSettings.update();
                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                retry;
            }
            catch (Exception::UpdateConflict)
            {
                if (appl.ttsLevel() == 0)
                {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        retry;
                    }
                }
                else
                {
                    throw Exception::UpdateConflict;
                }
            }
            catch(Exception::Error)
            {
                throw Exception::Error;
            }
        }
        else
        {
            PrintMgmtDocInstance::createPrintMgmtDocSettings(_document.RecId,
                                                                _document.TableId,
                                                                _nodeType,
                                                                PrintMgmtDocumentType::SIProjInvoice,
                                                                PrintMgmtDocInstanceType::Original,
                                                                reportFmtRecId);
        }
    }
]]></Source>
			</Method>
			<Method>
				<Name>getReportFormat</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Gets the report format from print settings based on inheritance
    /// </summary>
    /// <param name="_document">
    ///  record buffer
    /// </param>
    /// <returns>
    /// returns report format description
    /// </returns>
    public static PrintMgmtReportFormatDescription getReportFormat(Common _document= null)
    {
        PrintMgmtReportFormat   printMgmtReportFormat;
        PrintMgmtSettings       printMgmtSettings;
        ProjProposalJour        projProposalJour;
        ProjInvoiceTable        projInvoiceTable;
        CustTable               custTable;
        ProjFundingSource       projFundingSource;
        RecId                   custTableRecId;
        RecId                   printMgmtDocInstanceRecId;
        PrintMgmtReportFormatDescription
                                reportFormat;

        Common document = _document;

        switch (document.TableId)
        {
            case tableNum(ProjProposalJour):
                projProposalJour = _document;
                if (projProposalJour.RecId)
                {
                    printMgmtDocInstanceRecId = PrintMgmtDocInstance::getPrintMgmtDocRecId(projProposalJour.RecId,
                                                                                                tableNum(ProjProposalJour),
                                                                                                PrintMgmtNodeType::ProjProposalJour,
                                                                                                PrintMgmtDocumentType::SIProjInvoice,
                                                                                                PrintMgmtDocInstanceType::Original);
                }
                if (printMgmtDocInstanceRecId)
                {
                    break;
                }
            case tableNum(ProjInvoiceTable):
                if (document.TableId == tableNum(ProjInvoiceTable))
                {
                    projInvoiceTable = document;
                }
                else
                {
                    select firstonly ProjInvoiceProjId from projInvoiceTable
                            where projInvoiceTable.ProjInvoiceProjId == projProposalJour.ProjInvoiceProjId;
                }
                if (projInvoiceTable.RecId)
                {
                    printMgmtDocInstanceRecId = PrintMgmtDocInstance::getPrintMgmtDocRecId(projInvoiceTable.RecId,
                                                                                                tableNum(ProjInvoiceTable),
                                                                                                PrintMgmtNodeType::ProjInvoiceTable,
                                                                                                PrintMgmtDocumentType::SIProjInvoice,
                                                                                                PrintMgmtDocInstanceType::Original);
                }
                if (printMgmtDocInstanceRecId)
                {
                    break;
                }
            case tableNum(CustTable):
                if (document.TableId == tableNum(CustTable))
                {
                    custTableRecId = document.RecId;
                }
                else if (document.TableId == tableNum(ProjProposalJour))
                {
                    select firstonly RecId from custTable
                        exists join projFundingSource
                            where  projFundingSource.CustAccount == custTable.AccountNum
                                && projFundingSource.RecId == projProposalJour.FundingSource;
                    custTableRecId = custTable.RecId;
                }
                else
                {
                    select firstonly RecId from custTable
                        exists join projFundingSource
                            where projFundingSource.CustAccount == custTable.AccountNum &&
                                projFundingSource.ContractId == projInvoiceTable.ProjInvoiceProjId &&
                                projFundingSource.FundingType == ProjFundingType::Customer;
                     custTableRecId = custTable.RecId;
                }
                if (custTableRecId)
                {
                    printMgmtDocInstanceRecId = PrintMgmtDocInstance::getPrintMgmtDocRecId(custTableRecId,
                                                                                                tableNum(CustTable),
                                                                                                PrintMgmtNodeType::CustTable,
                                                                                                PrintMgmtDocumentType::SIProjInvoice,
                                                                                                PrintMgmtDocInstanceType::Original);
                }
                if (printMgmtDocInstanceRecId)
                {
                    break;
                }
            default:
                printMgmtDocInstanceRecId = PrintMgmtDocInstance::getPrintMgmtDocRecId(0,0,
                                                                                        PrintMgmtNodeType::Project,
                                                                                        PrintMgmtDocumentType::SIProjInvoice,
                                                                                        PrintMgmtDocInstanceType::Original);
                break;
        }
        if (printMgmtDocInstanceRecId)
        {
            select firstonly Description from printMgmtReportFormat
                exists join printMgmtSettings
                    where printMgmtSettings.ReportFormat == printMgmtReportFormat.RecId
                    && printMgmtSettings.ParentId == printMgmtDocInstanceRecId;
            reportFormat = printMgmtReportFormat.Description;
        }
        return reportFormat;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>