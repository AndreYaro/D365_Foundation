<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustLedgerTransactionsContract_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data Contract class for <c>CustLedgerTransactions_CN</c> SSRS report.
/// </summary>
[
    DataContractAttribute,
    SysOperationContractProcessingAttribute(
        classstr(CustLedgerTransactionsUIBuilder_CN),
        SysOperationDataContractProcessingMode::CreateSeparateUIBuilderForEachContract)
]
public class CustLedgerTransactionsContract_CN implements SysOperationValidatable, SrsReportRowCountable_CN
{
    CustPostingProfile fromPostingProfile;
    CustPostingProfile toPostingProfile;
    LedgerFiscalPeriodNameContract_CN ledgerFiscalPeriodNameContract_CN;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getRowsCount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the rows count of the report primary query's result by data contract parameters.
    /// </summary>
    /// <param name="_query">
    /// Primary query
    /// </param>
    /// <returns>
    /// A <c>Container</c> with first element being rows count and second element being a message
    /// that shall be shown if validation fails.
    /// </returns>
    public Container getRowsCount(Query _query)
    {
        Query           query;
        int             rowsCount;
        SRSMaxRow_CN    maxRows = SystemParameters::find().SRSMaxRow_CN;

        if (_query && _query.name() == queryStr(CustLedgerTransactions_CN))
        {
            query = _query;
        }
        else
        {
            query = new Query(queryStr(CustLedgerTransactions_CN));
        }
        query = CustLedgerTransactionsDP_CN::initQueryByDataContract(query, this);
        rowsCount = QueryRun::getQueryRowCount(query, maxRows + 1);

        //The output rows is over the allowed maximum rows of SSRS reports, please increase the value in the System parameters and run the report again.
        return [rowsCount, "@GLS220075"];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFromPostingProfile</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the value of the datacontract parameter FromPostingProfile.
    /// </summary>
    /// <param name="_fromPostingProfile">
    /// The new value of the datacontract parameter FromPostingProfile; optional.
    /// </param>
    /// <returns>
    /// The current value of datacontract parameter FromPostingProfile.
    /// </returns>
    [
        DataMemberAttribute('FromProfile'),
        SysOperationLabelAttribute(literalstr("@SYS179495")),
        SysOperationHelpTextAttribute(literalstr("@SYS179495")),
        SysOperationDisplayOrderAttribute('2')
    ]
    public CustPostingProfile parmFromPostingProfile(CustPostingProfile _fromPostingProfile = fromPostingProfile)
    {
        fromPostingProfile = _fromPostingProfile;
        return fromPostingProfile;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLedgerFiscalPeriodNameContract_CN</Name>
				<Source><![CDATA[
        [
        DataMemberAttribute('LedgerFiscalPeriodNameContract'),
        SysOperationDisplayOrderAttribute('1')
    ]
    public LedgerFiscalPeriodNameContract_CN parmLedgerFiscalPeriodNameContract_CN(LedgerFiscalPeriodNameContract_CN _ledgerFiscalPeriodNameContract_CN = ledgerFiscalPeriodNameContract_CN)
    {
        ledgerFiscalPeriodNameContract_CN = _ledgerFiscalPeriodNameContract_CN;

        return ledgerFiscalPeriodNameContract_CN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmToPostingProfile</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the value of the datacontract parameter ToPostingProfile.
    /// </summary>
    /// <param name="_toPostingProfile">
    /// The new value of the datacontract parameter ToPostingProfile; optional.
    /// </param>
    /// <returns>
    /// The current value of datacontract parameter ToPostingProfile.
    /// </returns>
    [
        DataMemberAttribute('ToProfile'),
        SysOperationLabelAttribute(literalstr("@SYS179496")),
        SysOperationHelpTextAttribute(literalstr("@SYS179496")),
        SysOperationDisplayOrderAttribute('3')
    ]
    public CustPostingProfile parmToPostingProfile(CustPostingProfile _toPostingProfile = toPostingProfile)
    {
        toPostingProfile = _toPostingProfile;
        return toPostingProfile;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the user parameter values.
    /// </summary>
    /// <returns>
    /// <c>true</c> if valid; else <c>false</c>
    /// </returns>
    public boolean validate()
    {
        boolean ret = true;

        if (ledgerFiscalPeriodNameContract_CN.parmFromDate() || ledgerFiscalPeriodNameContract_CN.parmToDate())
        {
            ret = ledgerFiscalPeriodNameContract_CN.validate();
        }

        if (!fromPostingProfile)
        {
            //From posting profile:cannot be empty.
            ret = checkFailed(strFmt("@SYS136411", "@SYS179495"));
        }

        if (!toPostingProfile)
        {
            //To posting profile:cannot be empty.
            ret = checkFailed(strFmt("@SYS136411", "@SYS179496"));
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>