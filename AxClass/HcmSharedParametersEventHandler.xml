<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmSharedParametersEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
class HcmSharedParametersEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>PayrollPage_OnPageActivated</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize the tax version fields on activation of the payroll tab.
    /// </summary>
    /// <param name="_sender">The form control raising the event.</param>
    /// <param name="_e">Args for the event.</param>
    [FormControlEventHandler(formControlStr(HcmSharedParameters, PayrollPage), FormControlEventType::PageActivated)]
    public static void PayrollPage_OnPageActivated(FormControl _sender, FormControlEventArgs _e)
    {
        info("@PayrollProcessing:PayrollDeprecationWarningMessage");
        HcmSharedParametersEventHandler::updateTaxEngineVersionFields(_sender);
    }

]]></Source>
			</Method>
			<Method>
				<Name>MenuItemButton_OnClicked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Refresh the data after initiating the tax data update.
    /// </summary>
    /// <param name="_sender">The form control raising the event.</param>
    /// <param name="_e">Args for the event.</param>
    [FormControlEventHandler(formControlStr(HcmSharedParameters, MenuItemButton), FormControlEventType::Clicked)]
    public static void MenuItemButton_OnClicked(FormControl _sender, FormControlEventArgs _e)
    {
        FormRun hcmSharedParametersForm = _sender.formRun();
        FormDataSource hcmSharedParameters_ds = hcmSharedParametersForm.dataSource(formDataSourceStr(HcmSharedParameters, HcmSharedParameters)) as FormDataSource;

        HcmSharedParameters hcmSharedParametersRecord = hcmSharedParameters_ds.cursor() as HcmSharedParameters;

        //
        // Ensure that any enabling or disabling of railroad tax codes has been saved prior
        // to refreshing worker tax codes.
        //
        if (hcmSharedParametersRecord.EnableRailroadTaxes)
        {
            hcmSharedParameters_ds.write();
        }

        HcmSharedParameters_ds.executeQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTaxEngineVersionFields</Name>
				<Source><![CDATA[
    private static void updateTaxEngineVersionFields(FormControl _sender)
    {
        FormRun hcmSharedParametersForm = _sender.formRun();

        var taxEngineVersionFormControl = hcmSharedParametersForm.design().controlName(formControlStr(HcmSharedParameters, TaxEngineVersion)) as FormStringControl;
        var taxCodeVersionFormControl = hcmSharedParametersForm.design().controlName(formControlStr(HcmSharedParameters, TaxCodeVersion)) as FormStringControl;
        var taxDataVersionServerFormControl = hcmSharedParametersForm.design().controlName(formControlStr(HcmSharedParameters, TaxDataVersionServer)) as FormStringControl;

        str expectedTaxEngineBaseCodeVersion = PayrollTaxEngineUtil::getExpectedTaxEngineBaseCodeVersion();

        // We only want to retrieve these fields once, as they are static values.
        if (taxEngineVersionFormControl.text() == ''
            && isConfigurationkeyEnabled(configurationKeyNum(PayrollAdministration)))
        {
            // Currently-loaded tax logic version
            taxCodeVersionFormControl.text(expectedTaxEngineBaseCodeVersion);

            // Tax engine (server) fields
            taxEngineVersionFormControl.text(PayrollTaxEngineUtil::getTaxEngineCodeCompatibilityVersion());
            taxDataVersionServerFormControl.text(PayrollTaxEngineUtil::getTaxEngineVersion());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>