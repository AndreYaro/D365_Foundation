<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerEuroDenominationValidateDeleteConstraint</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Ledger euro denomination validation helper class.
/// </summary>
[ExportMetadataAttribute("EuroDenominationConstraint", "ValidateDeleteConstraint"),ExportAttribute("Dynamics.AX.Application.EuroDenominationIValidateDeleteConstraint")]
public class LedgerEuroDenominationValidateDeleteConstraint implements EuroDenominationIValidateDeleteConstraint
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>check</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check ledgers for currency code use.
    /// </summary>
    /// <param name = "ret">
    /// Boolean from previous checks.
    /// </param>
    /// <param name = "_currencyCode">
    /// Currency code to check.
    /// </param>
    /// <returns>
    /// Ture if the Currency is valid; Otherwise false.
    /// </returns>
    public boolean check(boolean ret, CurrencyCode _currencyCode)
    {
        GeneralJournalAccountEntry generalJournalAccountEntry;
        Ledger ledger;

        // Validate that the currency code is not the Accounting currency for any ledgers
        select firstonly Name from ledger
            where ledger.AccountingCurrency == _currencyCode;

        if (ledger)
        {
            ret = ret && checkFailed(strfmt("@SYS313811", _currencyCode, ledger.Name));
        }

        // Validate that the currency code is not the Reporting currency for any ledgers
        select firstonly Name from ledger
            where ledger.ReportingCurrency == _currencyCode;

        if (ledger)
        {
            ret = ret && checkFailed(strfmt("@SYS313812", _currencyCode, ledger.Name));
        }

        // Validate that no transactions exist for the euro denomination and warn the user
        select firstonly RecId from generalJournalAccountEntry
            where generalJournalAccountEntry.TransactionCurrencyCode == _currencyCode;

        if (generalJournalAccountEntry.RecId && !Box::yesNo("@SYS313788", 0))
        {
            ret = ret && false;
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>