<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PmfProdCoByStatus_CostEstimate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class in the <c>PmfProdCoByStatus</c> hierarchy for Co/By lines
/// for production orders in status ProdStatus::CostEstimated
/// </summary>
class PmfProdCoByStatus_CostEstimate extends PmfProdCoByStatus
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleting</Name>
				<Source><![CDATA[
    public void deleting()
    {
        throw error(strfmt("@SYS22042",prodCoBy.prodTable().ProdStatus));
    }

]]></Source>
			</Method>
			<Method>
				<Name>inserting</Name>
				<Source><![CDATA[
    public void inserting()
    {
        super();

        prodCoBy.type().setCoByEstimation();
        prodCoBy.type().setBackStatus();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runStatusDecrease</Name>
				<Source><![CDATA[
    /// <summary>
    /// Reverse the effects of the transition to a higher status
    /// </summary>
    /// <param name="_prodParmStatusDecrease">
    /// The parameters for the status decrease operation
    /// </param>
    /// <returns>
    /// true, if the decrease of the status was successful
    /// </returns>
    /// <remarks>
    /// Reverses the transition from status <c>ProdStatus::Created</c> to status
    /// <c>ProdStatus::CostEstimated</c>
    /// </remarks>
    public boolean runStatusDecrease(ProdParmStatusDecrease _prodParmStatusDecrease)
    {
        prodCoBy.RemainCoByPhysical = 0;
        prodCoBy.QtyCoByCalc        = 0;
        prodCoBy.EndConsump         = NoYes::No;
        prodCoBy.CalcDate           = dateNull();

        prodCoBy.PdsCWRemainPhysical = 0;
        prodCoBy.PdsCWInventCalc     = 0;

        prodCoBy.update();

        return this.reverseDerivedCoBy(_prodParmStatusDecrease);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updating</Name>
				<Source><![CDATA[
    public void updating()
    {
        super();

        if (prodCoBy.CoByQty != prodCoBy.orig().CoByQty)
        {
            prodCoBy.type().setCoByEstimation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiate a new PmfProdCoByStatus_CostEstimate object for the
    /// production co/by product line
    /// </summary>
    /// <param name="_pmfProdCoBy">
    /// The production order co/by product line
    /// </param>
    /// <returns>
    /// The new co/by status object
    /// </returns>
    public static PmfProdCoByStatus_CostEstimate construct(PmfProdCoBy _pmfProdCoBy)
    {
        return new PmfProdCoByStatus_CostEstimate(_pmfProdCoBy);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>