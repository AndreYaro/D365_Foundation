<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerTransListDateDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerTransListDateDP</c> class is the data provider for the <c>LedgerTransListDate</c> report.
/// </summary>
[SRSReportQuery(queryStr(LedgerTransListReport))]
[SRSReportParameterAttribute(classStr(LedgerTransListDateContract))]
public class LedgerTransListDateDP extends SrsReportDataProviderPreProcessTempDB
{
    LedgerTransactionListTmp ledgerTransListTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getLedgerTransListTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the temporary table.
    /// </summary>
    /// <returns>
    /// The <c>LedgerTransactionListTmp</c> temporary table.
    /// </returns>
    [SRSReportDataSet(tableStr(LedgerTransactionListTmp))]
    public LedgerTransactionListTmp getLedgerTransListTmp()
    {
        select ledgerTransListTmp;
        return ledgerTransListTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSkipReportTransaction</Name>
				<Source><![CDATA[
    public boolean parmSkipReportTransaction(boolean _skipReportTransaction = skipReportTransaction)
    {
        if (FeatureStateProvider::isFeatureEnabled(LedgerSSRSReportSkipReportTransactionParmFlight::instance()))
        {
            skipReportTransaction = true;
        }

        return skipReportTransaction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the report business logic.
    /// </summary>
    /// <remarks>
    /// This method is used to process the report business logic that is used by the
    /// <c>LedgerTransListDate</c> report.
    /// </remarks>
    public void processReport()
    {
        LedgerTransListReportHelper helper = new LedgerTransListReportHelper();

        // Set the user connection to use on tables.
        // This is needed because the createdTransactionId is different for the report than the default connection.
        ledgerTransListTmp.setConnection(this.parmUserConnection());
        helper.parmUserConnection(this.parmUserConnection());

        LedgerTransListDateContract contract = this.parmDataContract() as LedgerTransListDateContract;

        using (SysInstrumentationActivityContext activityContext = this.logProcessReport())
        {
            helper.createReportData(
                ledgerTransListTmp,
                this.parmQuery(),
                contract.parmFromDate(),
                contract.parmToDate(),
                contract.parmIncludeReversed(),
                contract.parmTaxSpecification(),
                false,
                contract.parmDisplayCompanyForLedgerAccount());

            // Log the customer activity properties after the report data has been generated
            // because the query is adjusted in the helper class
            this.addActivityContextForReport(
                activityContext,
                contract,
                this.parmQuery());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>logProcessReport</Name>
				<Source><![CDATA[
    private SysInstrumentationActivityContext logProcessReport()
    {
        const str InstrumentationNamespace = 'LedgerTransListDate';

        SysInstrumentationLogger instrumentationLogger = SysInstrumentationLoggerFactory::CreateLogger(InstrumentationNamespace);

        const str MetricName = methodStr(LedgerTransListDateDP, processReport);

        SysInstrumentationActivity activityContext = SysInstrumentationActivity::construct(
            MetricName,
            InstrumentationNamespace);

        return instrumentationLogger.activityContextWithCustomProperties(activityContext);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addActivityContextForReport</Name>
				<Source><![CDATA[
    private void addActivityContextForReport(
        SysInstrumentationActivityContext _activityContext,
        LedgerTransListDateContract _reportContract,
        Query _query)
    {
        int rowCount = QueryRun::getQueryRowCount(_query, maxInt());
        _activityContext.addCustomProperty('RowCountForReport', int2Str(rowCount));

        _activityContext.addCustomProperty('IncludeReversed', _reportContract.parmIncludeReversed() ? 'Yes' : 'No');
        _activityContext.addCustomProperty('TaxSpecification', _reportContract.parmTaxSpecification() ? 'Yes' : 'No');
        _activityContext.addCustomProperty('FromDate', date2StrXpp(_reportContract.parmFromDate()));
        _activityContext.addCustomProperty('ToDate', date2StrXpp(_reportContract.parmToDate()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>