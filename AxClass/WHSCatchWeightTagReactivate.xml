<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSCatchWeightTagReactivate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSCatchWeightTagReactivate</c> class handles reactivating a catch weight tag that was previously deleted.
/// </summary>
internal class WHSCatchWeightTagReactivate
{
    private WHSCatchWeightTag catchWeightTag;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal void new()
    {
        // empty
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCatchWeightTag</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal WHSCatchWeightTag parmCatchWeightTag(WHSCatchWeightTag _catchWeightTag = catchWeightTag)
    {
        catchWeightTag = _catchWeightTag;

        return catchWeightTag;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the reactivation for the catch weight tag assigned as a parameter.
    /// </summary>
    [Hookable(false)]
    internal void run()
    {
        if (!catchWeightTag.RecId)
        {
            throw error(error::wrongUseOfFunction(funcName()));
        }

        if (!catchWeightTag.IsDeleted)
        {
            throw error(strFmt("@WAX:CatchWeightTagReactivateActiveError", catchWeightTag.TagNumber));
        }

        ttsbegin;

        catchWeightTag.selectForUpdate(true);

        catchWeightTag.IsDeleted = false;
        catchWeightTag.doUpdate();

        WHSCatchWeightTagEventRegistration tagRegistration = WHSCatchWeightTagEventRegistration::instantiateFromTagRegistration(WHSCatchWeightTagRegistrationEvent::TagUpdated);
        tagRegistration.registerEvent(catchWeightTag);

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static void main(Args _args)
    {
        if (!_args.dataset() == tableNum(WHSCatchWeightTag))
        {
            throw error(error::missingRecord(classStr(WHSCatchWeightTagReactivate)));
        }

        WHSCatchWeightTag tag = _args.record() as WHSCatchWeightTag;

        WHSCatchWeightTagReactivate reactivate = new WHSCatchWeightTagReactivate();

        reactivate.parmCatchWeightTag(tag);

        if (tag.IsDeleted
            && Box::yesNo(strFmt("@WAX:ReactivateCatchWeightTagConfirm", tag.TagNumber), DialogButton::No) == DialogButton::Yes)
        {
            reactivate.run();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>