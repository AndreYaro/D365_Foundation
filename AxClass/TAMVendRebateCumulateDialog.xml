<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMVendRebateCumulateDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMVendRebateCumulateDialog</c> class serves to cumulate selected rebates.
/// </summary>
class TAMVendRebateCumulateDialog extends RunBaseBatch implements BatchRetryable
{
    TAMVendRebateCumulateDialog     vendRebateCumulateDialog;
    VendAccount                     vendAccount;
    StartDate                       startDate;
    EndDate                         endDate;
    DialogField                     diagVendAccount;
    DialogField                     diagStartDate;
    DialogField                     diagEndDate;
    DialogRunbase                   dialogRunbase;

    #DEFINE.CurrentVersion(3)
    #LOCALMACRO.CurrentList
    vendAccount,
    startDate,
    endDate
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the vendor and date range fields to the dialog box.
    /// </summary>
    /// <returns>
    /// The dialog box object.
    /// </returns>
    public Object dialog()
    {
        dialogRunbase = super();

        diagVendAccount = dialogRunbase.addFieldValue(
                                extendedtypestr(VendAccount),
                                vendAccount,
                                "@SYS80105",
                                "@SYS14048");
        diagStartDate = dialogRunbase.addFieldValue(
                                extendedtypestr(StartDate),
                                startDate,
                                "@SYS6255",
                                "@SYS6255");
        diagEndDate = dialogRunbase.addFieldValue(
                                extendedtypestr(EndDate),
                                endDate,
                                "@SYS22882",
                                "@SYS22882");

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the dialog vendor and date range values.
    /// </summary>
    /// <returns>
    /// true if the values were retrieved successfully; otherwise, false.
    /// </returns>
    public boolean getFromDialog()
    {
        vendAccount = diagVendAccount.value();
        startDate = diagStartDate.value();
        endDate = diagEndDate.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the application module value.
    /// </summary>
    /// <returns>
    /// The vendor application module value.
    /// </returns>
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Substantiates the <c>TAMVendRebateCumulateDialog</c> object.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Stores the dialog object parameters.
    /// </summary>
    /// <returns>
    /// The list of stored parameters.
    /// </returns>
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches the cumulate rebate functionality.
    /// </summary>
    public void run()
    {
        TAMVendRebateTable::cumulateRebates(vendAccount, startDate, endDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Restores the last parameters saved.
    /// </summary>
    /// <param name="_packedClass">
    /// The list of stored parameters.
    /// </param>
    /// <returns>
    /// true if the restore succeded; otherwise, false.
    /// </returns>
    public boolean unpack(container _packedClass)
    {
        Integer     version     = conpeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case #CurrentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            default :
                ret = false;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates dialog box values.
    /// </summary>
    /// <param name="_calledFrom">
    /// The object that called the cumulate.
    /// </param>
    /// <returns>
    /// true if the vendor exists; otherwise, false.
    /// </returns>
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret;

        if (vendAccount)
        {
            ret = VendTable::checkExist(vendAccount);
        }
        else
        {
            ret = checkFailed("@SYS23532");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns an instance of the <c>TAMVendRebateCumulateDialog</c> class.
    /// </summary>
    /// <returns>
    /// An instance of the <c>TAMVendRebateCumulateDialog</c> class.
    /// </returns>
    public static TAMVendRebateCumulateDialog construct()
    {
        return new TAMVendRebateCumulateDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the class description.
    /// </summary>
    /// <returns>
    /// The class description.
    /// </returns>
    public static ClassDescription description()
    {
        return "@PRO1123";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs an instance and runs the <c>TAMVendRebateCumulateDialog</c> class.
    /// </summary>
    /// <param name="_args">
    /// Any arguments passed to the class.
    /// </param>
    public static void main(Args _args)
    {
        Common                  record;
        FormDataSource          tamVendRebateTable_ds;

        TAMVendRebateCumulateDialog tamVendRebateCumulateDialog =
            TAMVendRebateCumulateDialog::construct();

        try
        {
            if (tamVendRebateCumulateDialog.prompt())
            {
                tamVendRebateCumulateDialog.runOperation();

                record = _args.record();
                if (record && FormDataUtil::getFormDataSource(record))
                {
                    tamVendRebateTable_ds = FormDataUtil::getFormDataSource(record);
                    tamVendRebateTable_ds.research();
                }
            }
        }
        catch (Exception::Error)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>