<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetEntityHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class contain helper methods for Fixed Asset entities.
/// </summary>
public static class AssetEntityHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applyEntityDataToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies updated entity fields to the record.
    /// </summary>
    /// <param name = "_entity">Data entity with specified values.</param>
    /// <param name = "_dataSourceName">Name of data entity datasource.</param>
    /// <param name = "_operation">Current operation.</param>
    /// <param name = "_record">Record to update.</param>
    public static void applyEntityDataToDataSource(Common _entity, Name _dataSourceName, DataEntityDatabaseOperation _operation, Common _record)
    {
        DictTable dictTable = new DictTable(_record.TableId);
        DictDataEntity dictEntity = new DictDataEntity(_entity.TableId);

        if (!dictTable || !dictEntity)
        {
            return;
        }

        for (FieldId entityFieldId = dictEntity.fieldNext(0); entityFieldId; entityFieldId = dictEntity.fieldNext(entityFieldId))
        {
            boolean processField;
            if (_operation == DataEntityDatabaseOperation::Insert)
            {
                processField = _entity.isFieldSet(entityFieldId);
            }
            else
            {
                processField = _entity.isFieldSet(entityFieldId) && (_entity.(entityFieldId) != _entity.orig().(entityFieldId));
            }

            if (processField &&
                !isSysId(entityFieldId))
            {
                DictDataEntityField dictEntityField = new DictDataEntityField(_entity.TableId, entityFieldId);
                if (dictEntityField.dataSource() != _dataSourceName ||
                    !dictEntityField.dataField())
                {
                    continue;
                }

                FieldId tableFieldId = fieldName2Id(_record.TableId, dictEntityField.dataField());
                if (tableFieldId)
                {
                    _record.(tableFieldId) = _entity.(entityFieldId);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>