<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSAFTCashRegisterService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailSAFTCashRegisterService</c> class is the service class for SAF-T Cash register report.
/// </summary>
class RetailSAFTCashRegisterService extends SysOperationServiceBase
{
    private RetailSAFTCashRegisterContract dataContract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>start</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs SAF-T Cash register report.
    /// </summary>
    /// <param name = "_dataContract">SAF-T Cash register data contract.</param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void start(RetailSAFTCashRegisterContract _dataContract)
    {
        dataContract = _dataContract;

        try
        {
            ERFormatMappingId formatMappingId = this.getERFormatMappingRecId();
            ERIFormatMappingRun erRun = ERObjectsFactory::createFormatMappingRunByFormatMappingId(formatMappingId);
            ERModelDefinitionInputParametersAction parameters = this.getERParameters();

            erRun.withFileDestination(_dataContract.getFileDestination())
                .withParameter(parameters)
                .run();
        }
        catch
        {
            error("@SYS88909");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets parameters for current report.
    /// </summary>
    /// <returns>Parameters for current report.</returns>
    private ERModelDefinitionInputParametersAction getERParameters()
    {
        const str erParmCompany = 'model/Company';
        const str erParmCashRegister = 'model/CashRegister';
        const str erParmStore = 'model/Store';
        const str erParmEndDate = 'model/EndDate';
        const str erParmStartDate = 'model/StartDate';

        ERModelDefinitionInputParametersAction modelDefinitionInputParametersAction = new ERModelDefinitionInputParametersAction();

        modelDefinitionInputParametersAction
                .addParameter(erParmCompany, dataContract.parmCompany())
                .addParameter(erParmCashRegister, dataContract.parmRetailTerminal())
                .addParameter(erParmStore, dataContract.parmRetailStore())
                .addParameter(erParmEndDate, dataContract.parmDateTo())
                .addParameter(erParmStartDate, dataContract.parmDateFrom());

        return modelDefinitionInputParametersAction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERFormatMappingRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets current SAF-T Cash register export format.
    /// </summary>
    /// <returns>SAF-T Cash register export format.</returns>
    private ERFormatMappingId getERFormatMappingRecId()
    {
        return RetailParameters::find().SAFTERFileFormatMappingId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>