<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRCouponImport_Item</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>MCRCouponImport_Item</c> class handles the import process of items for coupons.
/// </summary>
class MCRCouponImport_Item extends MCRCouponImport
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preImport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares for import by deleting the items associated with a coupon if append is unchecked.
    /// </summary>
    public void preImport()
    {
        MCRCouponItem  mcrCouponItem;
        if (!this.parmAppend() && this.parmMCRCouponId() != "")
        {
            delete_from mcrCouponItem
                where mcrCouponItem.MCRCouponId == this.parmMCRCouponId();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>importRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Imports item record for a given coupon.
    /// </summary>
    /// <param name = "_itemId">The itemid for the record to import.</param>
    /// <param name = "_insertList">The record insert list instance to add the record to.</param>
    public void importRecord(ItemId _itemId, RecordInsertList _insertList)
    {
        MCRCouponItem       mcrCouponItem;

        if (this.validateItemId(_itemId))
        {
            mcrCouponItem.MCRCouponId = this.parmMCRCouponId();
            mcrCouponItem.ItemRelation = _itemId;
            mcrCouponItem.ItemCode = TableGroupAll::Table;
            _insertList.add(mcrCouponItem);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateItemId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that an item is valid and can be imported and associated with a coupon.
    /// </summary>
    /// <param name="_itemId">
    /// The reference to the item to validate.
    /// </param>
    /// <returns>
    /// true if the item is valid to be linked to a coupon; otherwise, false.
    /// </returns>
    boolean validateItemId(ItemId _itemId)
    {
        boolean ok = true;
        if (!InventTable::find(_itemId))
        {
            ok = checkFailed(strFmt("@SYS10752", _itemId));
        }
        return ok;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>