<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EFDocRecXmlUpdICMSST_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class <c>EFDocRecXmlUpdICMSST_BR</c> updates ICMS and ICMS-ST taxes from xml file.
/// </summary>
internal class EFDocRecXmlUpdICMSST_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createMappingFromFiscalDocument</Name>
				<Source><![CDATA[
    private EFDocRecXmlMapToViLineTmp_BR createMappingFromFiscalDocument(FiscalDocument_BR _fiscalDocument)
    {
        EFDocumentReceivedXML_BR            receivedXml;
        EFDocumentReceivedXmlView_BR        receivedXmlView;
        
        select firstOnly RecId from receivedXml
            where receivedXml.Accesskey == _fiscalDocument.AccessKey;
        
        if (receivedXml.RecId)
        {
            receivedXmlView = EFDocumentReceivedXmlView_BR::findOrCreate(receivedXml.RecId);
        }
        
        return EFDocRecXmlMapToViLineTmp_BR::createFromFiscalDocument(receivedXmlView.RecId, _fiscalDocument);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createMappingFromVendInvoice</Name>
				<Source><![CDATA[
    private EFDocRecXmlMapToViLineTmp_BR createMappingFromVendInvoice(VendInvoiceInfoTable _vendorInvoice)
    {
        VendInvoiceInfoTable_W              vendInvoiceInfoTableW;
        EFDocumentReceivedXML_BR            receivedXml;
        EFDocumentReceivedXmlView_BR        receivedXmlView;
        
        select firstonly RecId from receivedXml
            exists join vendInvoiceInfoTableW
                where vendInvoiceInfoTableW.AccessKey_BR == receivedXml.Accesskey
                    && vendInvoiceInfoTableW.vendInvoiceInfoTable == _vendorInvoice.RecId;
        
        if (receivedXml.RecId)
        {
            receivedXmlView = EFDocumentReceivedXmlView_BR::findOrCreate(receivedXml.RecId);
        }
        
        return EFDocRecXmlMapToViLineTmp_BR::create(receivedXmlView.RecId, _vendorInvoice);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getICMSSTTaxationCode</Name>
				<Source><![CDATA[
    internal TaxationCode_BR getICMSSTTaxationCode(VendInvoiceInfoTable _vendorInvoice, RefRecId _lineRecId)
    {
        EFDocRecXmlMapToViLineTmp_BR _mappedValues = this.createMappingFromVendInvoice(_vendorInvoice);
        EFDocRecXmlMapViLineUpdater_BR updater = EFDocRecXmlMapViLineUpdater_BR::newFromMapping(_mappedValues);
        
        return updater.getICMSSTTaxationCode(_lineRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateFiscalDocumentICMSSTFromMapping</Name>
				<Source><![CDATA[
    private boolean updateFiscalDocumentICMSSTFromMapping(EFDocRecXmlMapToViLineTmp_BR _mappedValues)
    {
        EFDocRecXmlMapViLineUpdater_BR updater = EFDocRecXmlMapViLineUpdater_BR::newFromMapping(_mappedValues);
        
        return updater.updateICMSSTFiscalDoc();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateVendorInvoiceICMSSTFromMapping</Name>
				<Source><![CDATA[
    private boolean updateVendorInvoiceICMSSTFromMapping(EFDocRecXmlMapToViLineTmp_BR _mappedValues)
    {
        EFDocRecXmlMapViLineUpdater_BR updater = EFDocRecXmlMapViLineUpdater_BR::newFromMapping(_mappedValues);
        
        return updater.updateICMSST();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTaxesOfFiscalDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates fiscal document presumed taxes from xml file by fiscal document.
    /// </summary>
    /// <param name="_fiscalDocument">
    /// Fiscal document.
    /// </param>
    /// <returns>
    /// True, if presumed taxes were updated; otherwise false.
    /// </returns>
    internal static boolean updateTaxesOfFiscalDocument(FiscalDocument_BR _fiscalDocument)
    {
        EFDocRecXmlMapToViLineTmp_BR mappedValues;
        EFDocRecXmlUpdICMSST_BR efDocRecXmlUpdICMSST;
        
        if (_fiscalDocument == null)
        {
            throw error(error::wrongUseOfFunction(funcName()));
        }
        
        efDocRecXmlUpdICMSST = new EFDocRecXmlUpdICMSST_BR();
        mappedValues = efDocRecXmlUpdICMSST.createMappingFromFiscalDocument(_fiscalDocument);
        return efDocRecXmlUpdICMSST.updateFiscalDocumentICMSSTFromMapping(mappedValues);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTaxesOfVendorInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates fiscal document taxes from xml file by vendor invoice.
    /// </summary>
    /// <param name="_vendorInvoice">
    /// Vendor invoice.
    /// </param>
    /// <returns>
    /// True, if taxes were updated; otherwise false.
    /// </returns>
    internal static boolean updateTaxesOfVendorInvoice(VendInvoiceInfoTable _vendorInvoice)
    {
        EFDocRecXmlMapToViLineTmp_BR mappedValues;
        EFDocRecXmlUpdICMSST_BR efDocRecXmlUpdICMSST;
        
        if (_vendorInvoice == null)
        {
            throw error(error::wrongUseOfFunction(funcName()));
        }
        
        efDocRecXmlUpdICMSST = new EFDocRecXmlUpdICMSST_BR();
        mappedValues = efDocRecXmlUpdICMSST.createMappingFromVendInvoice(_vendorInvoice);
        return efDocRecXmlUpdICMSST.updateVendorInvoiceICMSSTFromMapping(mappedValues);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>