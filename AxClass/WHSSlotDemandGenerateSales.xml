<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSSlotDemandGenerateSales</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Slot demand for sales.
/// </summary>
[WHSSlotDemandGenerateAttribute(WHSSlotDemandType::Sales)]
final internal class WHSSlotDemandGenerateSales extends WHSSlotDemandGenerate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    final protected void build()
    {
        using (var activityContext = this.instrumentationLogger().demandSlottingActivities().buildDemand(this.parmSlotTemplate().SlotDemandStrategy, this.parmSlotTemplate().UseWorkForWaveReplen))
        {
            this.instrumentationLogger().logRecordInformation(this.parmSlotTemplate());

            if (this.parmSlotTemplate().PackedQueryRun == conNull())
            {
                throw error(Error::wrongUseOfFunction(funcName()));
            }

            QueryRun qr = new QueryRun(this.parmSlotTemplate().PackedQueryRun);
            Query query = qr.query();

            // Only build demand for warehouse enabled non-service items.
            QueryBuildDataSource    qbdsWHSInvent = query.dataSourceTable(tableNum(SalesLine)).addDataSource(tableNum(WHSInventTable));
            qbdsWHSInvent.joinMode(JoinMode::ExistsJoin);
            qbdsWHSInvent.addLink(fieldNum(SalesLine, ItemId), fieldNum(WHSInventTable, ItemId));

            QueryBuildDataSource qbdsInvent = qbdsWHSInvent.addDataSource(tableNum(InventTable));
            qbdsInvent.relations(true);
            qbdsInvent.joinMode(JoinMode::ExistsJoin);
            qbdsInvent.addRange(fieldNum(InventTable, ItemType)).value(SysQuery::valueNot(ItemType::Service));

            QueryBuildDataSource qbdsSalesTable = query.dataSourceTable(tableNum(SalesTable));
            SysQuery::findOrCreateRange(qbdsSalesTable, fieldNum(SalesTable, SalesStatus)).value(SysQuery::value(SalesStatus::Backorder));

            ttsbegin;

            this.deleteDemand();

            while (qr.next())
            {
                SalesLine salesLine = qr.get(tableNum(SalesLine)) as SalesLine;

                this.createSalesLineDemand(salesLine);
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createSalesLineDemand</Name>
				<Source><![CDATA[
    private void createSalesLineDemand(SalesLine _salesLine)
    {       
        InventQty runningQty = this.demandQuantity(this.parmSlotTemplate().SlotDemandStrategy, _salesLine);

        if (!this.createLineDemand(_salesLine.ItemId, runningQty, _salesLine.InventDimId))
        {
            // If we've exhausted the unit of measure breakdown and there's still running quantity, the method was called with incorrect units.
            this.instrumentationLogger().logRecordInformation(_salesLine);

            throw error(strFmt("@WAX:UnableToCreateSlottingDemandError", _salesLine.ItemId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>demandQuantity</Name>
				<Source><![CDATA[
    private InventHandlingQty demandQuantity(WHSSlotDemandStrategy _demandStrategy, SalesLine _salesLine)
    {
        InventHandlingQty demandQty;

        // Initialize running quantity based on slot demand strategy.
        switch (_demandStrategy)
        {
            case WHSSlotDemandStrategy::Ordered:
                demandQty = WHSCatchWeightHelper::convertInventQuantity(_salesLine.ItemId, _salesLine.inventTable().inventUnitId(), _salesLine.QtyOrdered, _salesLine.InventDimId);
                break;

            case WHSSlotDemandStrategy::Reserved:
                // Since CW items are reserved at minimum quantity we can not only look at reserved physical then convert.
                if (PdsGlobal::pdsIsCWItem(_salesLine.ItemId))
                {
                    demandQty = _salesLine.pdsCWReservedPhysical();
                }
                else
                {
                    demandQty = _salesLine.reservedPhysical();
                }
                break;

            case WHSSlotDemandStrategy::Released:
                demandQty = WHSSalesLine::find(_salesLine.InventTransId).ReleasedQty - WHSLoadLine::getTotalWorkCreatedQty(_salesLine.InventTransId);
                break;
        }

        return demandQty;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>