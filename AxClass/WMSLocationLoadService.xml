<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsLocationLoadService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WMSLocationLoadService</c> class provides the stateless service operations for the
/// <c>WMSLocationLoad</c> table.
/// </summary>
class WmsLocationLoadService
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>sumUpLoad</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sums up the load adjustments for the <c>WMSLocationLoad</c> table per the specified locations.
    /// </summary>
    /// <param name="_dataContract">
    /// An instance of <c>WMSLocationLoadDataContract</c> class.
    /// </param>
    public void sumUpLoad(WMSLocationLoadDataContract _dataContract)
    {
        // Decode the container from the string until query is supported as a data contract
        container           queryContainer = SysOperationHelper::base64Decode(_dataContract.wmsLocationQuery());
        QueryRun            queryRun;
        WMSLocation         wmsLocation;
        WMSLocationLoad     wmsLocationLoad;
        Counter             inserterRecordCounter;
        SysDictTable        wmsLocationLoadTable;
        utcdatetime         utcDateTimeNow = DateTimeUtil::utcNow();

        if (!queryContainer)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        queryRun = new QueryRun(new Query(queryContainer));

        while (queryRun.next())
        {
            wmsLocation = queryRun.get(tableNum(WMSLocation));

            ttsbegin;

            wmsLocationLoad = WMSLocationLoad::selectSummedLoadCreatedBeforeDate(wmsLocation.wmsLocationId, wmsLocation.InventLocationId, wmsLocation.DataAreaId, utcDateTimeNow);
            wmsLocationLoad.initFromWMSLocation(wmsLocation);

            WMSLocationLoad::deleteLoadCreatedBeforeDate(wmsLocation.wmsLocationId, wmsLocation.InventLocationId, wmsLocation.DataAreaId, utcDateTimeNow);

            if (!wmsLocationLoad.isAllLoadFieldsEmpty())
            {
                wmsLocationLoad.insert();
                inserterRecordCounter++;
            }

            ttscommit;
        }

        wmsLocationLoadTable = new SysDictTable(tableNum(WMSLocationLoad));

        info(strFmt("@SYS74545", inserterRecordCounter, wmsLocationLoadTable.label()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
