<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JournalErrorTransList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JournalErrorTransList</c> class is a part of the error handling capability in the journal
///    framework.
/// </summary>
// This class contains a list of journalError buffers that contains the infolog per journalTrans
// It should normally not be extended.
class JournalErrorTransList extends JournalErrorList
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addInfolog</Name>
				<Source><![CDATA[
    public boolean addInfolog(
        JournalCheckPost _journalCheckPost,
        JournalTransData _journalTransData)
    {
        container           infologOrig;
        Integer             logPoint;

        if (this.find(_journalTransData.journalTrans().LineNum))
        {
            infologOrig = journalError.Infolog;
            this.remove();
        }

        journalError.RefTableId         =  journalTableData.journalTable().TableId;
        journalError.JournalId          = _journalTransData.journalTrans().JournalId;
        if (_journalTransData.journalTableData().journalVoucherNum())
        {
            journalError.Voucher        = _journalTransData.journalTrans().Voucher;
        }
        else
        {
            journalError.Voucher        = '';
        }

        journalError.LineNum            = _journalTransData.journalTrans().LineNum;
        [journalError.Exception,
         journalError.Infolog,
         journalError.InfologAction,
         logPoint]                      = JournalCheckPost::infologPack(_journalTransData.journalTrans().data(),_journalCheckPost.logPoint());

        _journalCheckPost.logPoint(logPoint);

        if (infologOrig)
        {
            journalError.Infolog        = infologOrig + journalError.Infolog;
        }

        this.add(journalError);

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    public boolean find(LineNum _lineNum)
    {
        if (! this.selectDbCalled())
        {
            this.initDb();
        }

        journalError.LineNum = _lineNum;

        return cacheJournalError.find(journalError);
    }

]]></Source>
			</Method>
			<Method>
				<Name>infolog</Name>
				<Source><![CDATA[
    public void infolog(JournalTransMap _journalTrans)
    {
        if (this.find(_journalTrans.LineNum))
        {
            JournalError::infolog(this.item());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initDb</Name>
				<Source><![CDATA[
    public void initDb()
    {
        this.selectDb(journalTableData.journalTable(),false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(JournalTableData _journalTableData)
    {
        super(_journalTableData);

        cacheJournalError.sortOrder(fieldNum(JournalError,LineNum));
    }

]]></Source>
			</Method>
			<Method>
				<Name>packInitDb</Name>
				<Source><![CDATA[
    public static container packInitDb(JournalTableMap _journalTable)
    {
        JournalTableData        journalTableData        = JournalTableData::newTable(_journalTable);
        JournalErrorTransList   journalErrorTransList   = journalTableData.journalStatic().newJournalErrorTransList(journalTableData);

        journalErrorTransList.selectDb(_journalTable,false);

        return journalErrorTransList.pack();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>