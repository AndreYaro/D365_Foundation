<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ConsignmentReplenishmentOrderFormLetterJournalCreate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ConsignmentReplenishmentOrderFormLetterJournalCreate</c> class is the class used to create
///    journals when updating consignment replenishment orders.
/// </summary>
public class ConsignmentReplenishmentOrderFormLetterJournalCreate extends FormletterJournalCreate
{
    protected ConsignmentProductReceiptJournalHeader          consignmentProductReceiptJournalHeader;
    protected ConsignmentReplenishmentOrderHeader             consignmentReplenishmentOrderHeader;
    protected ConsignmentDraftReplenishmentOrderJournalHeader consignmentDraftReplenishmentOrderJournalHeader;
    protected RecordSortedList                                recordSortedListLine;
    protected Num                                             internalNumber;
    protected ConsignmentDraftReplenishmentOrderJournalLine   replenishmentOrderLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Constructs a new instance of the <c>ConsignmentReplenishmentOrderFormLetterJournalCreate</c> class.
    /// </summary>
    /// <returns>
    ///  A <c>ConsignmentReplenishmentOrderFormLetterJournalCreate</c> class instance.
    /// </returns>
    public static ConsignmentReplenishmentOrderFormLetterJournalCreate construct()
    {
        return new ConsignmentReplenishmentOrderFormLetterJournalCreate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldIdParmLine_OrigOrderId</Name>
				<Source><![CDATA[
    protected FieldId fieldIdParmLine_OrigOrderId()
    {
        return fieldNum(ConsignmentDraftReplenishmentOrderJournalLine, ReplenishmentOrderNumber);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initJournalHeader</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Initializes the journal header record.
    /// </summary>
    public void initJournalHeader()
    {
        consignmentProductReceiptJournalHeader.clear();
        consignmentProductReceiptJournalHeader.initValue();

        consignmentProductReceiptJournalHeader.InternalProductReceiptNumber = NumberSeq::newGetNum(this.numberSequenceReferenceNum()).num();
        consignmentProductReceiptJournalHeader.ExternalProductReceiptNumber = consignmentDraftReplenishmentOrderJournalHeader.ExternalDocumentNumber;
        consignmentProductReceiptJournalHeader.DeliveryDate = consignmentDraftReplenishmentOrderJournalHeader.DeliveryDate;
        consignmentProductReceiptJournalHeader.ExternalDocumentDate = consignmentDraftReplenishmentOrderJournalHeader.ExternalDocumentDate;

        consignmentProductReceiptJournalHeader.LedgerVoucher = NumberSeq::newGetVoucher(PurchParameters::numRefConsignmentProductReceiptVoucher()).voucher();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createJournalHeader</Name>
				<Source><![CDATA[
    protected void createJournalHeader()
    {
        consignmentProductReceiptJournalHeader.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initJournalLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes a <c>ConsignmentProductReceiptJournalLine</c> from a <c>ConsignmentDraftReplenishmentOrderJournalLine</c>
    /// </summary>
    /// <param name="_consignmentDraftReplenishmentOrderJournalLine">
    ///     The record to use for initializing the new record.
    /// </param>
    /// <returns>
    ///     A <c>ConsignmentProductReceiptJournalLine</c> table instance.
    /// </returns>
    protected ConsignmentProductReceiptJournalLine initJournalLine(ConsignmentDraftReplenishmentOrderJournalLine _consignmentDraftReplenishmentOrderJournalLine)
    {
        ConsignmentProductReceiptJournalLine consignmentProductReceiptJournalLine;

        consignmentProductReceiptJournalLine.initFromReplenishmentOrderLine(_consignmentDraftReplenishmentOrderJournalLine.replenishmentOrderLine());
        consignmentProductReceiptJournalLine.initFromConsignmentProductReceiptJournalHeader(consignmentProductReceiptJournalHeader);

        consignmentProductReceiptJournalLine.InventDimId = _consignmentDraftReplenishmentOrderJournalLine.InventDimId;

        return consignmentProductReceiptJournalLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createJournalLine</Name>
				<Source><![CDATA[
    protected void  createJournalLine(Common _consignmentDraftReplenishmentOrderJournalLine)
    {
        ConsignmentDraftReplenishmentOrderJournalLine consignmentDraftReplenishmentOrderJournalLine = _consignmentDraftReplenishmentOrderJournalLine as ConsignmentDraftReplenishmentOrderJournalLine;
        ConsignmentProductReceiptJournalLine consignmentProductReceiptJournalLine =
            this.initJournalLine(consignmentDraftReplenishmentOrderJournalLine);

        consignmentProductReceiptJournalLine.ReplenishmentQuantity          = consignmentDraftReplenishmentOrderJournalLine.ReceiveReplenishmentQuantityNow;
        consignmentProductReceiptJournalLine.RemainingReplenishmentQuantity = consignmentDraftReplenishmentOrderJournalLine.RemainingReplenishmentQuantityAfter;

        this.insertJournalLine(consignmentProductReceiptJournalLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertJournalLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts a record in the journal line table.
    /// </summary>
    /// <param name="_journalLine">
    /// The record to insert.
    /// </param>
    public void insertJournalLine(Common _journalLine)
    {
        super(_journalLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createJournalLink</Name>
				<Source><![CDATA[
    protected void createJournalLink(Common _common)
    {
        // no implementation required
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJournal</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the current journal header.
    /// </summary>
    /// <returns>
    ///   A journal header record.
    /// </returns>
    public Common getJournal()
    {
        return consignmentProductReceiptJournalHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initTotals</Name>
				<Source><![CDATA[
    protected void initTotals()
    {
        recordSortedListLine = new RecordSortedList(tablenum(ConsignmentDraftReplenishmentOrderJournalLine));
        recordSortedListLine.sortOrder(fieldnum(ConsignmentDraftReplenishmentOrderJournalLine, RecId));

        QueryRun queryTransact = this.queryBuild();
        while (queryTransact.next())
        {
            replenishmentOrderLine = this.queryGet(queryTransact);
            if (!replenishmentOrderLine)
            {
                continue;
            }

            recordSortedListLine.ins(replenishmentOrderLine);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
    private Object queryBuild()
    {
        QueryRun  query;

        query = new QueryRun(queryStr(ConsignmentDraftReplenishmentOrderJournalLine));
        query.query().dataSourceTable(tableNum(ConsignmentDraftReplenishmentOrderJournalLine)).rangeField(fieldNum(ConsignmentDraftReplenishmentOrderJournalLine, ParmId)).value(queryValue(consignmentDraftReplenishmentOrderJournalHeader.ParmId));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryGet</Name>
				<Source><![CDATA[
    private Common queryGet(QueryRun _queryRun)
    {
        if (_queryRun.changed(tableNum(ConsignmentDraftReplenishmentOrderJournalLine)))
        {
            replenishmentOrderLine = _queryRun.get(tableNum(ConsignmentDraftReplenishmentOrderJournalLine));
        }

        return replenishmentOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>journalLineTableId</Name>
				<Source><![CDATA[
    protected TableId journalLineTableId()
    {
        return tableNum(ConsignmentProductReceiptJournalLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>linksTableId</Name>
				<Source><![CDATA[
    protected TableId linksTableId()
    {
        return tableNum(ConsignmentProductReceiptJournalHeader);
    }

]]></Source>
			</Method>
			<Method>
				<Name>numberSequenceReferenceNum</Name>
				<Source><![CDATA[
    protected NumberSequenceReference numberSequenceReferenceNum()
    {
        return PurchParameters::numRefConsignmentInternalProductReceiptNumber();
    }

]]></Source>
			</Method>
			<Method>
				<Name>recordListParmLine</Name>
				<Source><![CDATA[
    protected RecordSortedList recordListParmLine()
    {
        return recordSortedListLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceTable</Name>
				<Source><![CDATA[
    protected Common sourceTable()
    {
        return consignmentReplenishmentOrderHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmParmTable</Name>
				<Source><![CDATA[
    public Common parmParmTable(Common _parmTable = consignmentDraftReplenishmentOrderJournalHeader)
    {
        consignmentDraftReplenishmentOrderJournalHeader = _parmTable;

        return consignmentDraftReplenishmentOrderJournalHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>totals</Name>
				<Source><![CDATA[
    protected Object totals()
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>typeParmLine</Name>
				<Source><![CDATA[
    protected Common typeParmLine()
    {
        ConsignmentDraftReplenishmentOrderJournalLine consignmentDraftReplenishmentOrderJournalLine;
        return consignmentDraftReplenishmentOrderJournalLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSourceTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the source table record.
    /// </summary>
    protected void initSourceTable()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>