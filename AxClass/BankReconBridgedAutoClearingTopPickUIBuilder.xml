<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankReconBridgedAutoClearingTopPickUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The UI builder class for the automatic clearing of bridged transactions batch job during bank reconciliation.
/// </summary>
internal final class BankReconBridgedAutoClearingTopPickUIBuilder extends SysOperationAutomaticUIBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void postBuild()
    {
        DialogField reconciliationIdDialogField = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(BankReconBridgedAutoClearingTopPickContract, parmBankReconciliationId));

        if (reconciliationIdDialogField)
        {
            reconciliationIdDialogField.fieldControl().mandatory(true);
            reconciliationIdDialogField.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(BankReconBridgedAutoClearingTopPickUIBuilder, reconciliationIdLookup), this);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>reconciliationIdLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is the custom lookup code for the bank reconciliation id field on the user prompt dialog.
    /// </summary>
    /// <param name = "_control">Bank reconciliation control.</param>
    private void reconciliationIdLookup(FormStringControl _control)
    {
        BankReconBridgedExistReconcileIdTmp tmp = BankReconBridgedExistReconcileIdTmp::insertTmpData();

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(BankReconBridgedExistReconcileIdTmp));
        qbds.addSortField(fieldNum(BankReconBridgedExistReconcileIdTmp, ReconciledDate), SortOrder::Descending);

        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(BankReconBridgedExistReconcileIdTmp), _control, true);
        sysTableLookup.addLookupfield(fieldNum(BankReconBridgedExistReconcileIdTmp, ReconciliationId), true);
        sysTableLookup.addLookupfield(fieldNum(BankReconBridgedExistReconcileIdTmp, AccountId));
        sysTableLookup.addLookupfield(fieldNum(BankReconBridgedExistReconcileIdTmp, ReconciledDate));

        sysTableLookup.parmQuery(query);
        sysTableLookup.parmTmpBuffer(tmp);

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>