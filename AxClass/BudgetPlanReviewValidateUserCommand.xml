<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanReviewValidateUserCommand</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetPlanReviewValidateUserCommand</c> class manages the user validation for request change and
/// delegate workflow actions.
/// </summary>
public class BudgetPlanReviewValidateUserCommand extends WorkflowWorkItemActionManager
{
    BudgetPlanHeader budgetPlanHeader;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
        public Args parmArgs(Args _args = args)
    {
        Args returnedArgs;

        returnedArgs = super(_args);

        if (_args)
        {
            this.parmCaller(_args.caller());

            if (_args.record() &&
                _args.record().TableId == tablenum(BudgetPlanHeader))
            {
                budgetPlanHeader = _args.record() as BudgetPlanHeader;
            }
        }

        return returnedArgs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptWorkflowWorkItemAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the request change or delegate prompt dialog for workflow and then validates the user.
    /// </summary>
    protected void promptWorkflowWorkItemAction()
    {
        HcmWorkerRecId hcmWorkerRecId;

        super();

        if (this.parmIsActionDialogClosedOK() && this.parmTargetUser())
        {
            hcmWorkerRecId = HcmWorker::userId2Worker(this.parmTargetUser());

            if (!BudgetPlan::isOrganizationVisibleToWorker(
                budgetPlanHeader.BudgetPlanningProcess,
                budgetPlanHeader.BudgetingOrganization,
                hcmWorkerRecId))
            {
                throw error(strFmt("@GLS150023", this.parmTargetUser(), budgetPlanHeader.DocumentNumber));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the workflow action.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    /// The <c>Args</c> parameter is null.
    /// </exception>
    public void run()
    {
        if (!this.parmArgs())
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is main method to be invoked via the selection of a menu item.
    /// </summary>
    /// <param name="_args">
    /// Menu item arguments.
    /// </param>
    public static void main(Args _args)
    {
        BudgetPlanReviewValidateUserCommand budgetPlanReviewValidateUserCommand = new BudgetPlanReviewValidateUserCommand();

        budgetPlanReviewValidateUserCommand.parmArgs(_args);

        budgetPlanReviewValidateUserCommand.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>