<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDimFormBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventDimFormBuilder</c> class enables adding dimension controls to a design time form.
/// </summary>
abstract class InventDimFormBuilder
{
    Map controlIds = new Map(Types::Container, Types::Integer);

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>controlKey</Name>
				<Source><![CDATA[
    private container controlKey(FormBuildDatasource _dataSource, FieldId _fieldId)
    {
        return [_fieldId, _dataSource.id()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>controlId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the Id of an added control.
    /// </summary>
    /// <param name = "_dataSource">The data source for the control.</param>
    /// <param name = "_fieldId">The field Id for the control.</param>
    /// <returns>The Id of the added control.</returns>
    public int controlId(FormBuildDataSource _dataSource, FieldId _fieldId)
    {
        return controlIds.lookup(this.controlKey(_dataSource, _fieldId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>setControlLabel</Name>
				<Source><![CDATA[
    private final void setControlLabel(FormBuildControl _control, LabelString _label)
    {
        // These are the 3 types of dimensions supported

        FormBuildStringControl stringControl = _control as FormBuildStringControl;
        if (stringControl)
        {
            stringControl.label(_label);
        }
        else
        {
            FormBuildRealControl realControl = _control as FormBuildRealControl;
            if (realControl)
            {
                realControl.label(_label);
            }
            else
            {
                FormBuildDateTimeControl dateTimeControl = _control as FormBuildDateTimeControl;
                if (dateTimeControl)
                {
                    dateTimeControl.label(_label);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addDimensionControl</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a dimension control to the form.
    /// </summary>
    /// <param name = "_parentControl">The parent control for the new dimension control.</param>
    /// <param name = "_fieldId">The field Id for the dimension.</param>
    /// <param name = "_dataSource">The data source for the <c>InventDim</c> table that will bind the control.</param>
    /// <param name = "_label">The label for the control.</param>
    /// <param name = "_name">The name for the control.</param>
    protected final void addDimensionControl(FormBuildGroupControl _parentControl, FieldId _fieldId, FormBuildDataSource _dataSource, LabelString _label = '', FormControlName _name = '')
    {
        FormBuildControl control = _parentControl.addDataField(_dataSource.id(), _fieldId);
        
        controlIds.insert(this.controlKey(_dataSource, _fieldId), control.id());

        if (_label)
        {
            this.setControlLabel(control, _label);
        }

        if (_name)
        {
            control.name(_name);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the form by adding the dimension controls.
    /// </summary>
    public abstract void build()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>