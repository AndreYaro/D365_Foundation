<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjJourTransMapForm_ProdJourRoute</Name>
	<SourceCode>
		<Declaration><![CDATA[
class ProjJourTransMapForm_ProdJourRoute extends ProjJournalTransMapForm
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>activityNumberFieldId</Name>
				<Source><![CDATA[
    protected FieldId activityNumberFieldId()
    {
        return fieldNum(ProdJournalRouteProj, ActivityNumber);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canValidate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates if the current record should be checked for Category Ids
    /// </summary>
    /// <returns>
    /// true if record should be checked; false otherwise
    /// </returns>
    protected boolean canValidate()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        return prodJournalRoute.willCostBePosted(this.prodRouteTransType());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDimensionField</Name>
				<Source><![CDATA[
    protected DimensionDefault getDimensionField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;
        return prodJournalRoute.DefaultDimension;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjCategoryIdField</Name>
				<Source><![CDATA[
    protected ProjCategoryId getProjCategoryIdField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        return (this.prodRouteTransType() == ProdRouteTransType::Time ? prodJournalRoute.CategoryHoursId : prodJournalRoute.CategoryQtyId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjCostPriceField</Name>
				<Source><![CDATA[
    protected ProjCostPriceCost getProjCostPriceField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        return (this.prodRouteTransType() == ProdRouteTransType::Time ? prodJournalRoute.HourPrice : prodJournalRoute.QtyPrice);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQtyField</Name>
				<Source><![CDATA[
    protected ProjQtyEmpl getQtyField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        return (this.prodRouteTransType() == ProdRouteTransType::Time ? prodJournalRoute.Hours : prodJournalRoute.QtyGood + prodJournalRoute.QtyError);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getResourceField</Name>
				<Source><![CDATA[
    protected ResourceRecId getResourceField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer as ProdJournalRoute;

        return ResourceFacade::findByResourceBackingEntity(ProdJournalRoute.wrkCtrTable());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransDateField</Name>
				<Source><![CDATA[
    protected TransDate getTransDateField()
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        return prodJournalRoute.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProdJournalRouteProj</Name>
				<Source><![CDATA[
    void initProdJournalRouteProj(ProdRouteTransType   type)
    {
        ProdTable               prodTable;
        ProdRoute               prodRoute;
        ProdRouteProj   prodRouteProj;
        ProjTable               projTable;
        ProdJournalRoute        prodJournalRoute = childBuffer;

        prodTable = prodJournalRoute.prodTable();
        prodRoute = prodJournalRoute.prodRoute();

        if (prodRoute)
        {
            if (type == ProdRouteTransType::Time)
            {
                if (prodJournalRoute.JobType == RouteJobType::Setup)
                {
                    prodRouteProj = prodRoute.prodRouteProj(ItemCalcType::Setup);
                }
                else if (prodJournalRoute.JobType == RouteJobType::Process)
                {
                    prodRouteProj = prodRoute.prodRouteProj(ItemCalcType::Process);
                }
            }
            else
            {
                prodRouteProj = prodRoute.prodRouteProj(ItemCalcType::Qty);
            }
        }

        if (prodRoute && prodRouteProj)
        {
            projJournalTransMap.ProjTransDate = prodJournalRoute.TransDate;

            projJournalTransMap.ProjId = prodTable.ProjId;
            projTable = prodTable.projTable();

            projJournalTransMap.ActivityNumber = prodRouteProj.ActivityNumber;
            this.setProjCategoryIdField(type == ProdRouteTransType::Time ? prodJournalRoute.CategoryHoursId : prodJournalRoute.CategoryQtyId);
            projJournalTransMap.ProjLinePropertyId = prodRouteProj.ProjLinePropertyId;
            projJournalTransMap.ProjSalesCurrencyId = prodRouteProj.ProjSalesCurrencyId;
            this.setHourSalesPrice();
            if (this.resource())
            {
                this.setPeriodDate();
            }
            projJournalTransMap.ProjTaxGroupId = prodRouteProj.ProjTaxGroupId;
            projJournalTransMap.ProjTaxItemGroupId = prodRouteProj.ProjTaxItemGroupId;
            this.setTransDate();
            projJournalTransMap.Txt = prodRouteProj.Txt;
        }
        else
        {
            this.setTransDateField(prodJournalRoute.TransDate);

            projJournalTransMap.ProjId = prodTable.ProjId;
            this.setProjCategoryIdField(type == ProdRouteTransType::Time ? prodJournalRoute.CategoryHoursId : prodJournalRoute.CategoryQtyId);
            projJournalTransMap.Txt = '';
            projJournalTransMap.ProjTransDate = prodJournalRoute.TransDate;

            if (projJournalTransMap.ProjId)
            {
                this.initFromProjTable(ProjTable::find(prodTable.ProjId));
            }

            if (this.getProjCategoryIdField())
            {
                projJournalTransMap.ProjTaxItemGroupId = ProjCategory::find(this.getProjCategoryIdField()).TaxItemGroupId;
            }

            if (this.resource())
            {
                this.setTransDate();
                if (!projJournalTransMap.ProjId)
                {
                    this.setHourSalesPrice();
                    this.setHourCostPrice();
                }
            }

            projJournalTransMap.ActivityNumber = prodTable.ActivityNumber;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertPost</Name>
				<Source><![CDATA[
    void insertPost()
    {
        ProdJournalRouteProj    prodJournalRouteProj = projJournalTransMap;
        ProdJournalRoute        prodJournalRoute = childBuffer;
        ProdRouteProj           prodRouteProj;
        ;

        prodRouteProj = ProdRouteProj::find(prodJournalRoute.ProdId, prodJournalRoute.OprNum, prodJournalRoute.OprPriority, prodJournalRouteProj.itemCalcType(prodJournalRoute));

        if (projJournalTransMap.ProjId)
        {
            if (prodRouteProj)
            {
                if (((select projEmplTrans where projEmplTrans.TransId == prodRouteProj.ProjTransId).RecId != 0) ||
                ((select prodJournalRouteProj where prodJournalRouteProj.ProjTransId == prodRouteProj.ProjTransId).RecId != 0))
                {
                    projJournalTransMap.ProjTransId = ProjParameters::newTransId();
                }
                else
                {
                    projJournalTransMap.ProjTransId = prodRouteProj.ProjTransId;
                }
            }
            else
            {
                projJournalTransMap.ProjTransId = ProjParameters::newTransId();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustSetCostPrice</Name>
				<Source><![CDATA[
    boolean mustSetCostPrice()
    {
        //Use production cost price
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prodRouteTransType</Name>
				<Source><![CDATA[
    private ProdRouteTransType prodRouteTransType()
    {
        ProdJournalRouteProj prodJournalRouteProj;

        prodJournalRouteProj = projJournalTransMap;

        return prodJournalRouteProj.TransType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDimensionField</Name>
				<Source><![CDATA[
    protected void setDimensionField(DimensionDefault _defaultdimension)
    {
        //No need to set Dimension
    }

]]></Source>
			</Method>
			<Method>
				<Name>setProjCategoryIdField</Name>
				<Source><![CDATA[
    protected void setProjCategoryIdField(ProjCategoryId _projCategoryId)
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        if (this.prodRouteTransType() == ProdRouteTransType::Time)
        {
            prodJournalRoute.CategoryHoursId = _projCategoryId;
        }
        else
        {
            prodJournalRoute.CategoryQtyId = _projCategoryId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setProjCostPriceField</Name>
				<Source><![CDATA[
    protected void setProjCostPriceField(ProjCostPriceCost _projCostPrice)
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        if (this.prodRouteTransType() == ProdRouteTransType::Time)
        {
            prodJournalRoute.HourPrice = _projCostPrice;
        }
        else
        {
            prodJournalRoute.QtyPrice = _projCostPrice;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQtyField</Name>
				<Source><![CDATA[
    protected void setQtyField(ProjQtyEmpl _qty)
    {
        ProdJournalRoute    prodJournalRoute = childBuffer;

        if (this.prodRouteTransType() == ProdRouteTransType::Time)
        {
            prodJournalRoute.Hours = _qty;
        }

        //For trans type ProdRouteTransType::Qty, no need to set qty since it is a computed value
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTransDateField</Name>
				<Source><![CDATA[
    protected void setTransDateField(TransDate _transDate)
    {
        //No need to set TransDate
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>