<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>rDeferralsCopy</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class rDeferralsCopy extends RunBase
{
    RDeferralsId         deferralsId;
    DialogField          dialogDeferralsId;
    RDeferralsTable      deferralsTable;
    RDeferralsTable      deferralsTableNew;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deferralsTableNew</Name>
				<Source><![CDATA[
    public RDeferralsTable deferralsTableNew()
    {
        return deferralsTableNew;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    /// interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    public Object dialog()
    {
        DialogRunbase dialog = super();
        NumberSeq     num;

        dialog.caption("@GLS103816");
        dialogDeferralsId = dialog.addField(extendedtypestr(RDeferralsId), "@GLS103668");
        dialogDeferralsId.lookupButton(1);

        num = RDeferralsTable::initDeferralsNumberSeq();

        if (! num)
            throw error("@SYS27388");

        dialogDeferralsId.value(num.num());

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        deferralsId = dialogDeferralsId.value();
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(RDeferralsTable _deferralsTable)
    {
        super();

        deferralsTable = _deferralsTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return connull();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRDeferralsId</Name>
				<Source><![CDATA[
    public RDeferralsId parmRDeferralsId(RDeferralsId _deferralsId = deferralsId)
    {
        deferralsId = _deferralsId;
        return deferralsId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        RDeferralsBook deferralsBook;
        RDeferralsBook deferralsBookNew;

        ttsbegin;

        deferralsTableNew.data(deferralsTable.data());

        deferralsTableNew.DeferralsId       = deferralsId;
        deferralsTableNew.RefTableId        = 0;
        deferralsTableNew.RefRecId          = 0;
        deferralsTableNew.rTax25ProfitTable = 0;

        while select deferralsBook
               where deferralsBook.DeferralsId == deferralsTable.DeferralsId
        {
            deferralsBookNew.data(deferralsBook);
            deferralsBookNew.DeferralsId           = deferralsTableNew.DeferralsId;
            deferralsBookNew.Status                = RDeferralsStatus::Scheduled;
            deferralsBookNew.LastDeferralsDate     = dateNull();
            deferralsBookNew.insert();
        }

        deferralsTableNew.insert();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean     ret = true;

        if (! deferralsId)
        {
            ret = checkFailed("@GLS103819");
        }

        if (RDeferralsTable::exist(deferralsId))
        {
            ret = checkFailed(strfmt("@GLS103820", deferralsId));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static public ClassDescription description()
    {
        return "@SYS67426";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        RDeferralsCopy  rDeferralsCopy;
        FormDataSource  formDataSource;

        if (! _args || _args.dataset() != tablenum(RDeferralsTable))
            throw error(strfmt("@SYS25516", RDeferralsCopy::description()));

        rDeferralsCopy = new RDeferralsCopy(_args.record());

        if (rDeferralsCopy.prompt())
        {
            rDeferralsCopy.runOperation();

            formDataSource = FormDataUtil::getFormDataSource(_args.record());
            formDataSource.executeQuery();
            formDataSource.positionToRecord(rDeferralsCopy.deferralsTableNew());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>