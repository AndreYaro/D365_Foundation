<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsControlPONum</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WhsControlPONum</c> class is responsible for handling the purchase order number controls on the mobile device.
/// </summary>
#WHSRF
[WhsControlFactory(#PONum)]
internal class WhsControlPONum extends WhsControl
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>process</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final boolean process()
    {
        if (!this.validate())
        {
            return false;
        }

        controlData.parmPurchId(this.parmData());
        fieldValues.insert(#PONum, this.parmData());

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validate the purchase order number control value.
    /// </summary>
    /// <returns>true if the control value is valid; otherwise; false.</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute, Wrappable(true)]
    internal boolean validate()
    {
        PurchTable purchTable = PurchTable::find(this.parmData());

        if (!purchTable)
        {
            return this.fail("@WAX1242");
        }

        if (!purchTable.isOpenOrder())
        {
            return this.fail("@WAX4809");
        }

        if (mode == WHSWorkExecuteMode::LoadItemReceiving || mode == WHSWorkExecuteMode::LoadItemReceivingAndLocate)
        {
            if (this.parmEnabled() && !controlData.parmWorkLine()) // !workLine means that we are not in the put away step
            {
                InventDim itemInventDim;
                WHSLoadItemReceivingPurchLineProvider purchLineProvider = controlData.loadItemReceivingPurchLineProvider(pass.lookup(#LoadId), fieldValues.parmItemId(), itemInventDim, this.parmData());
                
                if (!purchLineProvider.isReceiptPossible())
                {
                    return this.fail("@WAX1242");
                }
            }
        }

        WHSWorkExecutionMode workExecutionMode = WHSWorkExecutionMode::newFromMode(mode);

        if (workExecutionMode.isPurchaseReceiving())
        {
            WHSPurchaseOrderReceivingValidator purchaseOrderReceivingValidator = WHSPurchaseOrderReceivingValidator::newFromPurchTable(purchTable);
            if (!purchaseOrderReceivingValidator.canReceivePurchaseOrder())
            {
                return this.fail(strFmt("%1\n%2", "@SYP4980046", "@SYP4980047"));
            }
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>