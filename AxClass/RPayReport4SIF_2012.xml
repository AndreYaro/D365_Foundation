<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RPayReport4SIF_2012</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class RPayReport4SIF_2012 extends RunBase
{
    ComExcelDocument_RU             xls;
    RPayReportSocialTaxPeriod       reportPeriod;
    RPayYearPeriod                  payYear;
    TransDate                       reportDate;
    RPay4FSSOfficialsType           officialsType;
    RHRMEmplId                      responsibleId;
    DialogField                     dfReportPeriod;
    DialogField                     dfPayYear;
    DialogField                     dfResponsibleId;
    DialogField                     dfCorrecting;
    DialogField                     dfReportDate;
    DialogField                     dfOfficialsType;
    NoYes                           correcting;
    RPayCounterUnit                 fundSocialInsurance;
    RPayCounterUnit                 fundSocialPayments;
    RPayCounterUnit                 fundSick;
    RPayCounterUnit                 fundTraumaInsurance;
    RPayCounterUnit                 fundPayments;
    str                             pctPregnant;
    str                             pctSickChild1, pctSickChild2;
    str                             pctChildBorn;
    str                             pctFuneral;
    str                             pctInvalidChild;
    str                             pctFutureParent;
    str                             rateInsuranceCut;
    str                             rateInsuranceExtra;
    str                             pctIncident;
    str                             pctProfRisk;
    str                             pctHealing;
    RPayGroup                       groupWorkers;
    RPayGroup                       groupWomen;
    RPayGroup                       groupHarmful;
    str                             pctOverSick;
    str                             pctOverPregnant;
    str                             pctOverChild1, pctOverChild2;
    str                             pctOverFuneral;

    date                            periodEnd;

    str                             fromYearStart;
    str                             beforeQuarter;
    str                             thisQuarter;
    str                             month1;
    str                             month2;
    str                             month3;
    str                             mthnames[];
    RpayTaxParameters               rpayTaxParameters;
    CompanyInfo                     companyInfo;
    RPayRateCode                    rounding,
                                    invalidRate;
    RpayFundDeductionCode           deductionCodeInvalids,
                                    deductionCodeExceedMax,
                                    Article58_33Deduction,
                                    Article58_34Deduction;

    int                             currentSheet;
    Range                           sortOfWorkMain,
                                    sortOfWorkPluralistically;
    RoundOff                        roundValue;

    RPayRateTable                   rateTable;

    RPayAmount                      invalidFundTotal, invalidFund1Mth, invalidFund2Mth, invalidFund3Mth;
    RPayAmount                      invalidDedTotal,  invalidDed1Mth, invalidDed2Mth, invalidDed3Mth;

    Map             invalidAmountsMap;
    Counter         invalidRateQty, womenQty, invalidWithAmountsQty;

    #define.cellSpace(3)

    #define.CurrentVersion(2)
    #localmacro.CurrentList
        reportPeriod,
        payYear,
        officialsType,
        responsibleId
    #endmacro

    #localmacro.InvalidAmounts
        invalidDedTotal,  invalidDed1Mth, invalidDed2Mth, invalidDed3Mth
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcCostType</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected container calcCostType(str             _period,
                                     RPayCounterUnit _fund,
                                     str             _payCType,
                                     str             _payGroup = '',
                                     Range           _sortOfWork = '',
                                     boolean         _rPayEmplPeriodUniqueCount = false)

    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds  = query.addDataSource(tablenum(RPayTrans));
        Query                   query1;
        QueryBuildDataSource    qbds2, qbdsPerson, qbdsEmpl;
        RPayTrans               rPayTrans;
        RPayTrans               rPayTrans1;
        QueryRun                qr;
        int                     rPayEmplPeriodUniqueCount;
        container               ret;

        ;

        if (! _payCType)
        {
            return [0, 0, 0];
        }

        qbds.addSelectionField(fieldnum(RPayTrans, Amount), SelectionField::Sum);
        qbds.addSelectionField(fieldnum(RPayTrans, QtyDay), SelectionField::Sum);
        qbds.addSelectionField(fieldnum(RPayTrans, RecId), SelectionField::Count);

        if (correcting)
        {
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(_period);
        }
        else
        {
            qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(_period);
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
        }

        qbds.addRange(fieldnum(RPayTrans, PayCType)).value(_payCType);

        if (_payGroup)
        {
            if (RPayGroupTable::find(_payGroup).Type == RPayGroupType::Employee)
            {
                qbds2 = qbds.addDataSource(tablenum(RPayGroupMember));
                qbds2.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RPayGroupMember, PersonEmplId));
            }
            else
            {
                qbdsEmpl = qbds.addDataSource(tablenum(RHRMEmplTable));
                qbdsEmpl.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RHRMEmplTable, EmployeeId));

                qbdsPerson = qbdsEmpl.addDataSource(tablenum(RHRMPersonTable));
                qbdsPerson.addLink(fieldnum(RHRMPersonTable, RecId), fieldnum(RHRMEmplTable, RHRMPersonTable));

                qbds2 = qbdsPerson.addDataSource(tablenum(RPayGroupMember));
                qbds2.addLink(fieldnum(RHRMPersonTable, PersonnelNumber), fieldnum(RPayGroupMember, PersonEmplId));
            }

            qbds2.addRange(fieldnum(RPayGroupMember, PayGroup)).value(_payGroup);
            qbds2.fetchMode(QueryFetchMode::One2One);
            qbds2.joinMode(JoinMode::ExistsJoin);
        }

        if (_sortOfWork)
        {
            qbds2 = qbds.addDataSource(tablenum(RHRMEmplTable));
            qbds2.joinMode(JoinMode::ExistsJoin);
            qbds2.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RHRMEmplTable, EmployeeId));
            qbds2.fetchMode(QueryFetchMode::One2One);
            qbds2.addRange(fieldnum(RHRMEmplTable, SortOfWork)).value(_sortOfWork);
        }

        query1 = new Query(query.pack());

        qbds = query.dataSourceTable(tablenum(RPayTrans));
        qbds = qbds.addDataSource(tablenum(RpayFundEmplSum));
        qbds.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RpayFundEmplSum, EmplId));
        qbds.joinMode(JoinMode::ExistsJoin);
        qbds.fetchMode(QueryFetchMode::One2One);

        qbds.addRange(fieldnum(RpayFundEmplSum, RpayCounterUnit)).value(_fund);

        if (correcting)
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(_period);
        }
        else
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, PayPeriod)).value(_period);
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        qbds = query.dataSourceTable(tablenum(RPayTrans));
        qbds = qbds.addDataSource(tablenum(RPayFundMember));
        qbds.addLink(fieldnum(RPayTrans, PayCType), fieldnum(RPayFundMember, PayCostType));
        qbds.addRange(fieldnum(RPayFundMember, CounterUnit)).value(con2str([fundSocialPayments, fundSick]));
        qbds.joinMode(JoinMode::NoExistsJoin);
        qbds.fetchMode(QueryFetchMode::One2One);

        rPayTrans = RPayReport4SIF_2012::executeQuery(query, tablenum(RPayTrans));

        qbds = query1.dataSourceTable(tablenum(RPayTrans));
        qbds = qbds.addDataSource(tablenum(RPayFundMember));
        qbds.addLink(fieldnum(RPayTrans, PayCType), fieldnum(RPayFundMember, PayCostType));
        qbds.addRange(fieldnum(RPayFundMember, CounterUnit)).value(fundSocialPayments);
        qbds.addRange(fieldnum(RPayFundMember, CounterUnit)).value(fundSick);
        qbds.joinMode(JoinMode::ExistsJoin);

        rPayTrans1 = RPayReport4SIF_2012::executeQuery(query1, tablenum(RPayTrans));

        ret = [rPayTrans.Amount + rPayTrans1.Amount,
        rPayTrans.QtyDay + rPayTrans1.QtyDay,
        rPayTrans.RecId  + rPayTrans1.RecId];

        if (_rPayEmplPeriodUniqueCount)
        {
            query1.dataSourceTable(tableNum(RPayTrans)).addSelectionField(fieldnum(RPayTrans, SourceRecId), SelectionField::Count);
            query1.dataSourceTable(tableNum(RPayTrans)).addRange(fieldNum(RPayTrans, SourceTableId)).value(queryValue(tableNum(RPayEmplPeriodTrans)));
            query1.dataSourceTable(tableNum(RPayTrans)).addGroupByField(fieldNum(RPayTrans, SourceRecId));

            qr = new QueryRun(query1);

            while (qr.next())
            {
                rPayEmplPeriodUniqueCount++;
            }

            ret += rPayEmplPeriodUniqueCount;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcCostTypeFromFund</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected container calcCostTypeFromFund(str             _period,
                                             RPayCounterUnit _fund,
                                             Range           _sortOfWork = '')
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RPayTrans));
        QueryBuildDataSource    qbds2;
        RPayTrans               rPayTrans;

        ;

        if (! _fund)
        {
            return [0, 0, 0];
        }

        qbds.addSelectionField(fieldnum(RPayTrans, Amount), SelectionField::Sum);
        qbds.addSelectionField(fieldnum(RPayTrans, QtyDay), SelectionField::Sum);
        qbds.addSelectionField(fieldnum(RPayTrans, RecId), SelectionField::Count);

        if (correcting)
        {
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(_period);
        }
        else
        {
        qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(_period);
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
        }

        if (_sortOfWork)
        {
            qbds2 = qbds.addDataSource(tablenum(RHRMEmplTable));
            qbds2.joinMode(JoinMode::ExistsJoin);
            qbds2.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RHRMEmplTable, EmployeeId));
            qbds2.fetchMode(QueryFetchMode::One2One);
            qbds2.addRange(fieldnum(RHRMEmplTable, SortOfWork)).value(_sortOfWork);
        }

        qbds = qbds.addDataSource(tablenum(RPayFundMember));
        qbds.addLink(fieldnum(RPayTrans, PayCType), fieldnum(RPayFundMember, PayCostType));
        qbds.joinMode(JoinMode::ExistsJoin);
        qbds.fetchMode(QueryFetchMode::One2One);
        qbds.addRange(fieldnum(RPayFundMember, CounterUnit)).value(_fund);

        rPayTrans = RPayReport4SIF_2012::executeQuery(query, tablenum(RPayTrans));

        return [rPayTrans.Amount,
                rPayTrans.QtyDay,
                rPayTrans.RecId];
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcFundBase</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected RPayAmount calcFundBase(str _period,
                                      str _fundToInclude,
                                      str _fundToExclude = "")
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RPayTrans));
        QueryBuildDataSource    qbds2;
        RPayTrans               rPayTrans;

        ;

        if (! _fundToInclude)
        {
            return 0;
        }
        qbds.addSelectionField(fieldnum(RPayTrans, Amount), SelectionField::Sum);

        if (correcting)
        {
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(_period);
        }
        else
        {
            qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(_period);
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
        }

        qbds2 = qbds.addDataSource(tablenum(RPayFundMember));
        qbds2.addLink(fieldnum(RPayTrans, PayCType), fieldnum(RPayFundMember, PayCostType));
        qbds2.addRange(fieldnum(RPayFundMember, CounterUnit)).value(_fundToInclude + (_fundToExclude ? ',!' + _fundToExclude : ''));
        qbds2.joinMode(JoinMode::ExistsJoin);

        rPayTrans = RPayReport4SIF_2012::executeQuery(query, tablenum(RPayTrans));

        return rPayTrans.Amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcInvalids</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void calcInvalids()
    {
        Query                   query;
        QueryBuildDataSource    qbds, qbdsDeduction;
        QueryRun                queryRun;
        RHRMEmplTable           emplTable;
        RPayHistory             payHistory;
        RPayFundEmplSum         rPayFundEmplSumLocal;

        query = new Query();
        qbds = query.addDataSource(tablenum(RPayFundEmplSum));

        qbds.addRange(fieldnum(RPayFundEmplSum, RpayCounterUnit)).value(fundSocialInsurance);
        qbds.orderMode(OrderMode::GroupBy);
        qbds.addSortField(fieldnum(RPayFundEmplDeduction, EmplId));
        qbds.addSelectionField(fieldnum(RPayFundEmplSum, RpayFundTotalAmount), SelectionField::Sum);

        qbdsDeduction = qbds.addDataSource(tablenum(RPayFundEmplDeduction));
        qbdsDeduction.relations(true);
        qbdsDeduction.joinMode(JoinMode::ExistsJoin);
        qbdsDeduction.addRange(fieldnum(RPayFundEmplDeduction, RpayFundDeductionCode)).value(deductionCodeInvalids);

        if (correcting)
        {
            qbds.addRange(fieldnum(RPayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }
        else
        {
            qbds.addRange(fieldnum(RPayFundEmplSum, PayPeriod)).value(fromYearStart);
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            rPayFundEmplSumLocal = queryRun.get(tablenum(RPayFundEmplSum));

            if (! rPayFundEmplSumLocal.RpayFundTotalAmount)
            {
                continue;
            }

            invalidDedTotal = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, deductionCodeInvalids, '', rPayFundEmplSumLocal.EmplId);

            if (invalidDedTotal != 0)
            {
                invalidFundTotal += this.getFundTotalAmountSum(fromYearStart, fundTraumaInsurance, false, '', rPayFundEmplSumLocal.EmplId) -
                                    this.getFundTotalDeductionAmount(fromYearStart,  fundTraumaInsurance, '', '', rPayFundEmplSumLocal.EmplId, true);

                invalidFund1Mth  += this.getFundTotalAmountSum(month1, fundTraumaInsurance, false, '', rPayFundEmplSumLocal.EmplId)-
                                    this.getFundTotalDeductionAmount(month1,  fundTraumaInsurance, '', '', rPayFundEmplSumLocal.EmplId, true);

                invalidFund2Mth  += this.getFundTotalAmountSum(month2, fundTraumaInsurance, false, '', rPayFundEmplSumLocal.EmplId) -
                                    this.getFundTotalDeductionAmount(month2,  fundTraumaInsurance, '', '', rPayFundEmplSumLocal.EmplId, true);

                invalidFund3Mth  += this.getFundTotalAmountSum(month3, fundTraumaInsurance, false, '', rPayFundEmplSumLocal.EmplId) -
                                    this.getFundTotalDeductionAmount(month3,  fundTraumaInsurance, '', '', rPayFundEmplSumLocal.EmplId, true);

                invalidDed1Mth   =  this.getFundTotalDeductionAmount(month1,  fundSocialInsurance, deductionCodeInvalids, '', rPayFundEmplSumLocal.EmplId);
                invalidDed2Mth   =  this.getFundTotalDeductionAmount(month2,  fundSocialInsurance, deductionCodeInvalids, '', rPayFundEmplSumLocal.EmplId);
                invalidDed3Mth   =  this.getFundTotalDeductionAmount(month3,  fundSocialInsurance, deductionCodeInvalids, '', rPayFundEmplSumLocal.EmplId);

                invalidAmountsMap.insert(rPayFundEmplSumLocal.EmplId, [#InvalidAmounts]);
            }
        }

        invalidWithAmountsQty = invalidAmountsMap.elements();

        if (invalidAmountsMap.elements())
        {
            invalidFundTotal = this.round(invalidFundTotal, roundValue);
            invalidFund1Mth  = this.round(invalidFund1Mth, roundValue);
            invalidFund2Mth  = this.round(invalidFund2Mth, roundValue);
            invalidFund3Mth  = this.round(invalidFund3Mth, roundValue);
        }

        while select EmployeeId from emplTable
            where emplTable.EmployeeId == emplTable.PayMainEmplId
        exists join payHistory
            where payHistory.Number         == emplTable.EmployeeId &&
                    payHistory.StartDate      <= periodEnd        &&
                    payHistory.operationType  == RHRMJournalType::Receive
        {
            if (this.isEmployed(emplTable.EmployeeId) && this.isInvalid(emplTable.EmployeeId))
            {
                ++invalidRateQty;
            }
        }

        if (invalidRateQty < invalidAmountsMap.elements())
        {
            warning(strfmt("@RUP3416", invalidRateQty, invalidAmountsMap.elements()));
            invalidRateQty = invalidAmountsMap.elements();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcPayment</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected int calcPayment(str             _dates,
                              RPayCounterUnit _fund,
                              str             _payCType)
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RHRMEmplTable));

        if (! _payCType)
        {
            return 0;
        }
        qbds.addSelectionField(fieldnum(RHRMEmplTable, RecId), SelectionField::Count);
        qbds = qbds.addDataSource(tablenum(RPayTrans));
        qbds.addLink(fieldnum(RHRMEmplTable, EmployeeId), fieldnum(RPayTrans, EmplId));
        if (correcting)
        {
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(_dates);
        }
        else
        {
            qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(_dates);
            qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
        }
        qbds.addRange(fieldnum(RPayTrans, PayCType)).value(_payCType);
        qbds.joinMode(JoinMode::ExistsJoin);
        qbds.fetchMode(QueryFetchMode::One2One);

        qbds = query.dataSourceTable(tablenum(RHRMEmplTable));
        qbds = qbds.addDataSource(tablenum(RpayFundEmplSum));
        qbds.addLink(fieldnum(RHRMEmplTable, EmployeeId), fieldnum(RpayFundEmplSum, EmplId));
        qbds.joinMode(JoinMode::ExistsJoin);
        qbds.fetchMode(QueryFetchMode::One2One);

        qbds.addRange(fieldnum(RpayFundEmplSum, RpayCounterUnit)).value(_fund);

        if (correcting)
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(_dates);
        }
        else
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, PayPeriod)).value(_dates);
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        return any2Int(RPayReport4SIF_2012::executeQuery(query, tablenum(RHRMEmplTable)).RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkAndPrepareParameters</Name>
				<Source><![CDATA[
    private void checkAndPrepareParameters()
    {
        RPayReportParameter get(int _pos, str _msg1, str _msg2, boolean _check = true)
        {
            RPayReportParameter parameter;

            parameter = RPayReportTuningTable::find(RPayReportName::RPayReport4SIF_2012, _pos).parameter;

            if (! parameter && _check)
            {
                checkFailed(strFmt("@SYS76498", _msg1, _msg2));
            }

            return parameter;
        }
        ;

        setprefix("@RUP2178");

        rpayTaxParameters       = RpayTaxParameters::find();
        fundSocialInsurance     = rpayTaxParameters.CounterUnit2011;
        deductionCodeInvalids   = rpayTaxParameters.FssInvalidDeduction;
        deductionCodeExceedMax  = rpayTaxParameters.FssBaseLimitDeduction;
        roundValue              = rpayTaxParameters.TaxSocialRound;

        fundSocialPayments      = get(1, "@RUP906",   "@RUP3420");
        fundSick                = get(2, "@RUP906",   "@RUP3421");
        pctPregnant             = get(3, "@RUP2170",  "@RUP2146");
        pctSickChild1           = get(4, "@RUP2170",  "@RUP2357");
        pctChildBorn            = get(5, "@RUP2170",  "@RUP2148");
        pctFuneral              = get(6, "@RUP2170",  "@RUP2149");
        pctInvalidChild         = get(7, "@RUP2170",  "@RUP2150");
        pctFutureParent         = get(8, "@RUP2170",  "@RUP2151");
        fundTraumaInsurance     = get(9, "@RUP906",   "@RUP3422");
        fundPayments            = get(10, "@RUP906",  "@RUP3423");
        rateInsuranceCut        = get(11, "@RUP2170", "@RUP2155");
        rateInsuranceExtra      = get(12, "@RUP2170", "@RUP2156");
        pctIncident             = get(13, "@RUP2170", "@RUP2157");
        pctProfRisk             = get(14, "@RUP2170", "@RUP2158");
        pctHealing              = get(15, "@RUP2170", "@RUP2159");
        groupWorkers            = get(16, "@RUP1230", "@RUP2160");
        groupWomen              = get(17, "@RUP1230", "@RUP2162");
        pctOverSick             = get(18, "@RUP2170", "@RUP2166");
        pctOverPregnant         = get(19, "@RUP2170", "@RUP2167");
        pctOverChild1           = get(20, "@RUP2170", "@RUP2358");
        pctOverFuneral          = get(21, "@RUP2170", "@RUP2169");
        pctSickChild2           = get(22, "@RUP2170", "@RUP2359");
        pctOverChild2           = get(23, "@RUP2170", "@RUP2360");
        groupHarmful            = get(24, "@RUP1230", "@RUP2361");
        rounding                = get(25, "@RUP2170", "@RUP108");
        Article58_34Deduction   = get(26, "@RUP2170", "@RUP2985", false);
        Article58_33Deduction   = get(27, "@RUP2170", "@RUP2986", false);

        companyInfo             = CompanyInfo::find();

        sortOfWorkMain          = strfmt('%1,%2', RHRMSortOfWork::Direct, RHRMSortOfWork::CivilContract);
        sortOfWorkPluralistically = strfmt('%1', RHRMSortOfWork::Pluralistically);

        invalidRate = RpayFundDeductionTable::find(RpayTaxParameters::find().FssInvalidDeduction).RpayRateCodeEmpl;
        rateTable   = RPayRateTable::find(invalidRate);

        if (groupWorkers)
        {
            this.checkCalculatedAvgQty();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkCalculatedAvgQty</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected boolean checkCalculatedAvgQty()
    {
        RPayAverageQuantity  averageQuantity;
        ReportPeriod_RU      dtIterator = endmth(dateStartYr(periodEnd));
        boolean ret = true;
        ;

        for (dtIterator = endmth(dateStartYr(periodEnd)); dtIterator <= periodEnd; dtIterator = endmth(nextmth(dtIterator)))
        {
            averageQuantity = RPayAverageQuantity::Find(groupWorkers, dtIterator);
            if (averageQuantity.RecId == 0)
            {
                ret = checkFailed(strfmt("@RUP3417", dtIterator, groupWorkers)) && ret;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        Dialog dialog = super();

        dialog.addGroup("@SYS28007");
        dfReportDate    = dialog.addFieldValue(extendedTypeStr(TransDate), DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), "@SYS117650", "@SYS74065");
        dfPayYear       = dialog.addFieldValue(extendedTypeStr(RPayYearPeriod), payYear, "@SYS5563", "@SYS5563");
        dfReportPeriod  = dialog.addFieldValue(enumStr(RPayReportSocialTaxPeriod), reportPeriod);
        dfCorrecting    = dialog.addFieldValue(enumStr(NoYes), correcting, "@GLS111911");
        dialog.addGroup("@GLS102112");
        dfOfficialsType = dialog.addFieldValue(enumStr(RPay4FSSOfficialsType), officialsType);
        dfResponsibleId = dialog.addFieldValue(extendedTypeStr(RHRMEmplId), responsibleId, "@SYS138179", "@RUP2143");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>emplAvgQtyByGroup</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected RPayAverageQty emplAvgQtyByGroup(RPayGroup _payGroup)
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RPayAverageQuantity));
        QueryBuildDatasource    qbdsFund, qbdsMember, qbdsPerson, qbdsEmpl;
        RPayAverageQuantity     rPayAverageQuantity;

        qbds.addSelectionField(fieldnum(RPayAverageQuantity, AverageQty), SelectionField::Avg);
        qbds.addRange(fieldnum(RPayAverageQuantity, PayPeriod)).value(fromYearStart);
        qbds.addRange(fieldnum(RPayAverageQuantity, PayGroup)).value(_payGroup);

        qbdsMember = qbds.addDataSource(tablenum(RPayGroupMember));
        qbdsMember.addLink(fieldnum(RPayAverageQuantity, PayGroup), fieldnum(RPayGroupMember, PayGroup));

        if (RPayGroupTable::find(_payGroup).Type == RPayGroupType::Employee)
        {
            qbdsFund = qbdsMember.addDataSource(tablenum(RPayFundEmplSum));
            qbdsFund.addLink(fieldnum(RPayGroupMember, PersonEmplId), fieldnum(RPayFundEmplSum, EmplId));
        }
        else
        {
            qbdsPerson = qbdsMember.addDataSource(tableNum(RHRMPersonTable));
            qbdsPerson.addLink(fieldnum(RPayGroupMember, PersonEmplId), fieldnum(RHRMPersonTable, PersonnelNumber));
            qbdsPerson.addSelectionField(fieldNum(RHRMPersonTable, RecId));

            qbdsEmpl = qbdsPerson.addDataSource(tableNum(RHRMEmplTable));
            qbdsEmpl.addLink(fieldnum(RHRMPersonTable, RecId), fieldnum(RHRMEmplTable, RHRMPersonTable));
            qbdsEmpl.addRange(fieldNum(RHRMEmplTable, EmployeeId)).value(strFmt('(%1 == %2)',
                fieldstr(RHRMEmplTable, PayMainEmplId),
                fieldstr(RHRMEmplTable, EmployeeId)));

            qbdsFund = qbdsEmpl.addDataSource(tablenum(RPayFundEmplSum));
            qbdsFund.addLink(fieldnum(RHRMEmplTable, PayMainEmplId), fieldnum(RPayFundEmplSum, EmplId));
        }

        if (reportPeriod == RPayReportSocialTaxPeriod::Year || !correcting)
        {
            qbdsFund.addRange(fieldnum(RPayFundEmplSum, PayPeriod)).value(fromYearStart);
        }
        else
        {
            qbdsFund.addRange(fieldnum(RPayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        rPayAverageQuantity = RPayReport4SIF_2012::executeQuery(query, tablenum(RPayAverageQuantity));

        return this.round(rPayAverageQuantity.AverageQty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillAddress</Name>
				<Source><![CDATA[
    private void fillAddress()
    {
        LogisticsPostalAddress logisticsPostalAddress = companyInfo.postalAddress();

        xls.insertValue('AddressZipCode', logisticsPostalAddress.ZipCode, currentSheet);
        xls.insertValue('Address_Line1', logisticsPostalAddress.countyFullName_RU(), currentSheet);
        xls.insertValue('Address_Line2', logisticsPostalAddress.City ? logisticsPostalAddress.City : logisticsPostalAddress.stateFullName_RU(), currentSheet);
        xls.insertValue('Address_Line3', LogisticsAddressDistrict::find_RU(logisticsPostalAddress.District).fullName_RU(), currentSheet);
        xls.insertValue('Address_Line4', LogisticsAddressStreet_RU::findRecID(logisticsPostalAddress.StreetId_RU).fullName(), currentSheet);

        xls.insertValue('Estate', logisticsPostalAddress.BuildingCompliment, currentSheet);
        xls.insertValue('Building', logisticsPostalAddress.Building_RU, currentSheet);
        xls.insertValue('Flat', logisticsPostalAddress.Apartment_RU, currentSheet);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillDocument</Name>
				<Source><![CDATA[
    #AviFiles
    private void fillDocument()
    {
        void advanceProgress(int _tableNumber)
        {
            progress.setText(strfmt("@SYS55826", _tableNumber));
            progress.incCount();
        }
        ;

        this.progressInit("@RUP2178", 12, #AviFormLetter);

        progress.setText("@RUP2171");
        progress.incCount();

        currentSheet = 1;
        progress.setText("@SYS16146");
        progress.incCount();
        this.fillTitle();

        currentSheet++;
        advanceProgress(1);
        this.fillTable1();

        currentSheet++;
        advanceProgress(2);
        if (this.fillTable2())
        {
            xls.deleteWorkSheet(currentSheet);
            --currentSheet;
        }

        currentSheet++;
        advanceProgress(3);
        this.fillTable3();

        currentSheet++;
        advanceProgress(3);
        if (this.fillTable31())
        {
            xls.deleteWorkSheet(currentSheet);
            --currentSheet;
        }

        currentSheet ++;
        advanceProgress(5);
        if (this.fillTable5())
        {
            xls.deleteWorkSheet(currentSheet);
            --currentSheet;
        }

        currentSheet++;

        advanceProgress(6);
        this.fillTable6();

        advanceProgress(7);
        this.fillTable7();

        currentSheet++;
        advanceProgress(8);

        if (this.fillTable8())
        {
            xls.deleteWorkSheet(currentSheet);
            --currentSheet;
        }
        else
        {
            advanceProgress(9);
            this.fillTable9();
        }

        this.fillRegularCells('TotalPages', this.pageNum2str(), 3, #cellSpace, 1, true, true);

        xls.visible(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillEmplQty</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void fillEmplQty()
    {
        Counter  emplAvgQty, tmpQty, harmQty;
        #define.EmplQtyLen(6)

        womenQty = this.getEmplQty(groupWomen);
        this.fillWomen();
        harmQty = this.getEmplQty(groupHarmful);
        this.fillRegularCells('HarmQty', strFmt('%1', harmQty), #EmplQtyLen, #cellSpace, currentSheet, true, true);
        this.fillRegularCells('InvalidQty', strFmt('%1', invalidWithAmountsQty), #EmplQtyLen, #cellSpace, currentSheet, true, true);

        emplAvgQty = real2int(decRound(this.emplAvgQtyByGroup(groupWorkers), 0));
        tmpQty     = real2int(max(womenQty, max(invalidWithAmountsQty, harmQty)));

        if (emplAvgQty < tmpQty)
        {
            warning(strfmt("@RUP3418", emplAvgQty, tmpQty));
            emplAvgQty = tmpQty;
        }

        this.fillRegularCells('EmplQty', strFmt('%1', emplAvgQty), #EmplQtyLen, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillOfficials</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void fillOfficials()
    {
        RHRMEmplTable   emplTable;

        switch (officialsType)
        {
            case RPay4FSSOfficialsType::Payer:
                xls.insertValue('Responsible', '1', currentSheet);
                break;

            case RPay4FSSOfficialsType::Representative:
                xls.insertValue('Responsible', '2', currentSheet);
                break;

            case RPay4FSSOfficialsType::Successor:
                xls.insertValue('Responsible', '3', currentSheet);
                break;
        }

        if (responsibleId)
        {
            emplTable = RHRMEmplTable::find(responsibleId);
            xls.insertValue('ResponsibleName', RHRMEmplTable::find(emplTable.EmployeeId).personTable().fio(false), currentSheet);
        }
        else
        {
            xls.insertValue('ResponsibleName' , OfficialsTable_RU::findDirector().Name, currentSheet);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillRegularCells</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void fillRegularCells(Bookmark _bookmark,
                                    str      _text,
                                    int      _maxLen,
                                    int      _cellSpace = #cellSpace,
                                    int      _workSheet = 0,
                                    boolean  _horizontal = true,
                                    boolean  _alignRight = false)
    {
        int                             i, col, row;
        COM                             cell;
        COM                             comApplication, comDocument;
        COM                             comWorksheets, comWorksheet;
        Bookmark                        bookmark;
        ;

        if (!_bookmark || !_text || !_maxLen || !_cellSpace)
        {
            return;
        }

        comDocument = xls.getComDocument();
        comApplication = comDocument.Application();
        bookmark = _bookmark;

        if (_workSheet)
        {
            comWorksheets = comDocument.Worksheets();
            comWorksheet  = comWorksheets.item(_workSheet);
            cell = comWorksheet.Range(bookmark);
        }
        else
        {
            cell = comApplication.Range(bookmark);
        }

        col = cell.Column();
        row = cell.Row();

        if (_alignRight)
        {
            if (_horizontal)
            {
                col += _cellSpace * (_maxLen - strlen(_text));
            }
            else
            {
                row += _cellSpace * (_maxLen - strlen(_text));
            }
        }

        while (i < strlen(_text) && i < _maxLen)
        {
            i++;
            bookmark = COMExcelDocument_RU::numToNameCell(col, row);
            cell = _workSheet ? comWorksheet.Range(bookmark) : comApplication.Range(bookmark);
            cell.value2(substr(_text, i, 1));
            if (_horizontal)
            {
                col += _cellSpace;
            }
            else
            {
                row += _cellSpace;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillSheetCommon</Name>
				<Source><![CDATA[
    private void fillSheetCommon()
    {
        #define.FSSLength(10)
        #define.SubCodeLen(5)

        this.fillRegularCells('RegNumFSS', companyInfo.FSS_RU, #FSSLength, #cellSpace, currentSheet);
        this.fillRegularCells('SubCode' + int2str(currentSheet), companyInfo.SubordinateCode, #SubCodeLen,  #cellSpace, currentSheet);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable1</Name>
				<Source><![CDATA[
    private void fillTable1()
    {
        //NoYes tmpCorrecting;
        Range prevPeriods = SysQuery::range(datenull(), dateStartYr(payYear) - 1);
        ;

        xls.insertValue('T1R2PAST',     this.getFundAmountSum(fundSocialInsurance, beforeQuarter), currentSheet);
        xls.insertValue('T1R2M1',       this.getFundAmountSum(fundSocialInsurance, month1), currentSheet);
        xls.insertValue('T1R2M2',       this.getFundAmountSum(fundSocialInsurance, month2), currentSheet);
        xls.insertValue('T1R2M3',       this.getFundAmountSum(fundSocialInsurance, month3), currentSheet);

        xls.insertValue('T1R15PAST',    this.round(this.calcFundBase(beforeQuarter, fundSocialPayments),
                                                   roundValue));
        xls.insertValue('T1R15M1',      this.round(this.calcFundBase(month1, fundSocialPayments),
                                                   roundValue));
        xls.insertValue('T1R15M2',      this.round(this.calcFundBase(month2, fundSocialPayments),
                                                   roundValue));
        xls.insertValue('T1R15M3',      this.round(this.calcFundBase(month3, fundSocialPayments),
                                                   roundValue));

        //tmpCorrecting = correcting;
        //correcting = true;
        //xls.insertValue('T1R4',         this.round(this.getFundAmountSum(fundSocialInsurance, prevPeriods),roundValue));
        //correcting = tmpCorrecting;

        this.fillRegularCells('CZ2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable2</Name>
				<Source><![CDATA[
    private boolean fillTable2()
    {
        RPayAmount              amount, amountC5, amount2Check;
        RPayDayHour             qty;
        int                     cnt;
        int                     rPayEmplPeriodTransUniqueCount;
        Counter                 wQty, tmpWqty;

        int countSick(Range _sortOfWork = '', RPayIllType _illType = 0, RPayCostType _costType = '')
        {
            RPayAverageEarnEmplPeriodTable  averageEarnEmplPeriodTable;
            TransLink_RU                    transLink;
            Query                           query = new Query();
            QueryBuildDataSource            qbds = query.addDataSource(tablenum(RPayAverageEarnEmplPeriodTable));
            QueryBuildDataSource            qbds2;
            QueryRun                        queryRun;
            int                             i;

            qbds.addRange(fieldnum(RPayAverageEarnEmplPeriodTable, AverageEarnCodeType)).value(queryValue(RPayAverageEarnCodeType::SickList));
            qbds.addRange(fieldnum(RPayAverageEarnEmplPeriodTable, PrevPaySickListId)).value(SysQuery::valueEmptyString());

            if (_sortOfWork || !prmisdefault(_illType))
            {
                qbds2 = qbds.addDataSource(tablenum(RHRMEmplTable));
                qbds2.joinMode(JoinMode::ExistsJoin);
                qbds2.addLink(fieldnum(RPayAverageEarnEmplPeriodTable, EmplId), fieldnum(RHRMEmplTable, EmployeeId));
                qbds2.fetchMode(QueryFetchMode::One2One);
                if (_sortOfWork)
                {
                    qbds2.addRange(fieldnum(RHRMEmplTable, SortOfWork)).value(_sortOfWork);
                }

                if (!prmisdefault(_illType))
                {
                    qbds.addRange(fieldnum(RPayAverageEarnEmplPeriodTable, RpayIllType)).value(queryValue(_illType));
                }
            }

            qbds = qbds.addDataSource(tablenum(RPayAverageEarnValue));
            qbds.joinMode(JoinMode::ExistsJoin);
            qbds.addLink(fieldnum(RPayAverageEarnEmplPeriodTable, RecId), fieldnum(RPayAverageEarnValue, AverageEarnEmplPeriodRecid));
            qbds.fetchMode(QueryFetchMode::One2One);

            qbds = qbds.addDataSource(tablenum(RPayTrans));
            qbds.joinMode(JoinMode::ExistsJoin);
            qbds.addLink(fieldnum(RPayAverageEarnValue, RecId), fieldnum(RPayTrans, SourceRecId));

            if (!prmisdefault(_costType))
            {
                qbds.addRange(fieldnum(RPayTrans, PayCType)).value(_costType);
            }

            if (correcting)
            {
                qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
            }
            else
            {
                qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(fromYearStart);
                qbds.addRange(fieldnum(RPayTrans, CorrectedPeriod)).value(fromYearStart);
            }

            qbds = qbds.addDataSource(tablenum(RPayFundMember));
            qbds.joinMode(JoinMode::ExistsJoin);
            qbds.addLink(fieldnum(RPayTrans, PayCType), fieldnum(RPayFundMember, PayCostType));
            if (prmisdefault(_costType))
            {
                qbds.addRange(fieldnum(RPayFundMember, CounterUnit)).value(fundSick);
            }

            queryRun = new QueryRun(query);
            while (queryRun.next())
            {
                averageEarnEmplPeriodTable = queryRun.get(tablenum(RPayAverageEarnEmplPeriodTable));
                transLink = TransLink_RU::findSec(averageEarnEmplPeriodTable.TableId, averageEarnEmplPeriodTable.RecId);
                if (transLink.Reversed && transLink.TransLinkType == TransLinkType_RU::Storno)
                {
                    i--;
                }
                else
                {
                    i++;
                }
            }

            return i;
        }

        int countRecipients(str _payCType = '', Range _sortOfWork = '')
        {
            Query                   query = new Query();
            QueryBuildDataSource    qbds, qbds2;
            QueryRun                queryRun;
            int                     j;

            qbds = query.addDataSource(tablenum(RPayTrans));
            qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(fromYearStart);
            qbds.addRange(fieldnum(RPayTrans, PayCType)).value(_payCType);
            qbds.orderMode(OrderMode::GroupBy);
            qbds.addSortField(fieldnum(RPayTrans, EmplId));

            if (_sortOfWork)
            {
                qbds2 = qbds.addDataSource(tablenum(RHRMEmplTable));
                qbds2.joinMode(JoinMode::ExistsJoin);
                qbds2.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RHRMEmplTable, EmployeeId));
                qbds2.fetchMode(QueryFetchMode::One2One);
                qbds2.addRange(fieldnum(RHRMEmplTable, SortOfWork)).value(_sortOfWork);
            }

            queryRun = new QueryRun(query);

            while (queryRun.next())
            {
                j ++;
            }

            return j;
        }
        ;

        xls.insertValue('T2SickOccurencies', countSick());

        [amount, qty, cnt] = this.calcCostTypeFromFund(fromYearStart, fundSick);
        xls.insertValue('T2R1Qty', qty);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverSick);
        amount += amountC5;
        xls.insertValue('T2R1Pay', this.round(amount, roundValue));
        xls.insertValue('T2R1C5', this.round(amountC5, roundValue));
        amount2Check = this.round(amount, roundValue);

        xls.insertValue('T2SickOccurenciesPluralistically', countSick(sortOfWorkPluralistically));

        [amount, qty, cnt] = this.calcCostTypeFromFund(fromYearStart, fundSick, sortOfWorkPluralistically);
        xls.insertValue('T2R2Qty', qty);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverSick, '', sortOfWorkPluralistically);
        amount += amountC5;
        xls.insertValue('T2R2Pay', this.round(amount, roundValue));
        xls.insertValue('T2R2C5', this.round(amountC5, roundValue));

        wQty = countSick('', RPayIllType::maternity_leave, pctPregnant);
        xls.insertValue('T2PregnancyOccurenciesAll', wQty);
        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctPregnant);
        xls.insertValue('T2R3Qty', qty);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverPregnant);
        amount += amountC5;
        xls.insertValue('T2R3Pay', this.round(amount, roundValue));
        xls.insertValue('T2R3C5', this.round(amountC5, roundValue));
        amount2Check += this.round(amount, roundValue);

        tmpWqty = countSick(sortOfWorkPluralistically, RPayIllType::maternity_leave, pctPregnant);
        xls.insertValue('T2PregnancyOccurenciesPluralistically', tmpWqty);
        wQty = max(wQty, tmpWqty);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctPregnant, '', sortOfWorkPluralistically);
        xls.insertValue('T2R4Qty', qty);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverPregnant, '', sortOfWorkPluralistically);
        amount += amountC5;
        xls.insertValue('T2R4Pay', this.round(amount, roundValue));
        xls.insertValue('T2R4C5',  this.round(amountC5, roundValue));

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctFutureParent);
        xls.insertValue('T2R5Qty', qty);
        xls.insertValue('T2R5Pay', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctChildBorn);
        xls.insertValue('T2R6Qty', qty);
        xls.insertValue('T2R6Pay', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        xls.insertValue('T2R7C1Qty', countRecipients(con2str([pctSickChild1, pctSickChild2, pctOverChild1, pctOverChild2]), ''));

        [amount, qty, cnt, rPayEmplPeriodTransUniqueCount] = this.calcCostType(fromYearStart, fundSocialInsurance, con2Str([pctSickChild1, pctOverChild1]), '', '', true);
        xls.insertValue('T2R9Qty', rPayEmplPeriodTransUniqueCount);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverChild1, '', '');

        xls.insertValue('T2R9Pay', this.round(amount, roundValue));
        xls.insertValue('T2R8C5', this.round(amountC5, roundValue));
        xls.insertValue('T2R9C1Qty', countRecipients(con2Str([pctSickChild1, pctOverChild1]), ''));
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt, rPayEmplPeriodTransUniqueCount] = this.calcCostType(fromYearStart, fundSocialInsurance, con2Str([pctSickChild2, pctOverChild2]), '', '', true);
        xls.insertValue('T2R10Qty', rPayEmplPeriodTransUniqueCount);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverChild2, '', '');

        xls.insertValue('T2R10Pay', this.round(amount, roundValue));
        xls.insertValue('T2R9C5', this.round(amountC5, roundValue));
        xls.insertValue('T2R10C1Qty', countRecipients(con2Str([pctSickChild2, pctOverChild2]), ''));
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctInvalidChild);
        xls.insertValue('T2R14Qty', qty);
        xls.insertValue('T2R14Pay', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctFuneral);
        xls.insertValue('T2R15Qty', qty);
        [amountC5, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverFuneral);
        amount += amountC5;
        xls.insertValue('T2R15Pay', this.round(amount, roundValue));
        xls.insertValue('T2R11C5', this.round(amountC5, roundValue));
        amount2Check += this.round(amount, roundValue);

        //adjust qty of women
        if (womenQty < wQty)
        {
            warning (strfmt("@RUP3419", womenQty, wQty));
            womenQty = wQty;
            this.fillWomen();
        }

        this.fillRegularCells('DC2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable3</Name>
				<Source><![CDATA[
    private void fillTable3()
    {
        RPayAmount              amount;
        ;

        amount = this.getFundTotalAmountSum(fromYearStart,  fundSocialInsurance);
        xls.insertValue('T3C3Total', this.round(amount, roundValue));
        amount = this.getFundTotalAmountSum(month1, fundSocialInsurance);
        xls.insertValue('T3C3M1',    this.round(amount, roundValue));
        amount = this.getFundTotalAmountSum(month2,         fundSocialInsurance);
        xls.insertValue('T3C3M2',    this.round(amount, roundValue));
        amount = this.getFundTotalAmountSum(month3,         fundSocialInsurance);
        xls.insertValue('T3C3M3',    this.round(amount, roundValue));

        amount = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, '', strfmt('%1,%2', deductionCodeInvalids, deductionCodeExceedMax));
        xls.insertValue('T3C4Total', this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month1,         fundSocialInsurance, '', strfmt('%1,%2', deductionCodeInvalids, deductionCodeExceedMax));
        xls.insertValue('T3C4M1',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month2,         fundSocialInsurance, '', strfmt('%1,%2', deductionCodeInvalids, deductionCodeExceedMax));
        xls.insertValue('T3C4M2',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month3,         fundSocialInsurance, '', strfmt('%1,%2', deductionCodeInvalids, deductionCodeExceedMax));
        xls.insertValue('T3C4M3',    this.round(amount, roundValue));

        amount = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, deductionCodeInvalids);
        xls.insertValue('T3C5Total', this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month1,         fundSocialInsurance, deductionCodeInvalids);
        xls.insertValue('T3C5M1',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month2,         fundSocialInsurance, deductionCodeInvalids);
        xls.insertValue('T3C5M2',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month3,         fundSocialInsurance, deductionCodeInvalids);
        xls.insertValue('T3C5M3',    this.round(amount, roundValue));

        amount = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, deductionCodeExceedMax);
        xls.insertValue('T3C6Total', this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month1,         fundSocialInsurance, deductionCodeExceedMax);
        xls.insertValue('T3C6M1',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month2,         fundSocialInsurance, deductionCodeExceedMax);
        xls.insertValue('T3C6M2',    this.round(amount, roundValue));
        amount = this.getFundTotalDeductionAmount(month3,         fundSocialInsurance, deductionCodeExceedMax);
        xls.insertValue('T3C6M3',    this.round(amount, roundValue));

        if (Article58_34Deduction)
        {
            amount = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, Article58_34Deduction);
            xls.insertValue('T3R6Total', this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month1,         fundSocialInsurance, Article58_34Deduction);
            xls.insertValue('T3R6M1',    this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month2,         fundSocialInsurance, Article58_34Deduction);
            xls.insertValue('T3R6M2',    this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month3,         fundSocialInsurance, Article58_34Deduction);
            xls.insertValue('T3R6M3',    this.round(amount, roundValue));
        }

        if (Article58_33Deduction)
        {
            amount = this.getFundTotalDeductionAmount(fromYearStart,  fundSocialInsurance, Article58_33Deduction);
            xls.insertValue('T3R7Total', this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month1,         fundSocialInsurance, Article58_33Deduction);
            xls.insertValue('T3R7M1',    this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month2,         fundSocialInsurance, Article58_33Deduction);
            xls.insertValue('T3R7M2',    this.round(amount, roundValue));
            amount = this.getFundTotalDeductionAmount(month3,         fundSocialInsurance, Article58_33Deduction);
            xls.insertValue('T3R7M3',    this.round(amount, roundValue));
        }

        this.fillRegularCells('CW2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable31</Name>
				<Source><![CDATA[
    #define.InvPerPage(40)
    #define.InvFirstRow(14)
    #define.TableExtTitle("Продолжение Таблицы 3.1")
    #define.InvalidsFirstPage(5)

    private boolean fillTable31()
    {
        int                     invalidPages;
        int                     invalidCount;

        TransDate               disAssignDate, disCancelDate;
        RHRMEmplId              emplId;
        MapEnumerator           mapEnum;

        RPayAmount      amountTotal, amountFirst,
                        amountSecond, amountThird;

        int             currentRow;
        int             invalidsOnPageCount;
        int             invalidsTotal;

        BookMark makeBookMark(int _sheet, str _colName, int _row)
        {
            return xls.getWorkSheetName(_sheet) + '!' + _colName + int2str(_row);
        }

        void insertInvalid(RHRMEmplId _emplId, int _row)
        {
            xls.insertValue(makeBookMark(currentSheet, 'A', _row), invalidCount);
            xls.insertValue(makeBookMark(currentSheet, 'F', _row), RHRMEmplTable::find(_emplId).personTable().fio(false));
            xls.insertValue(makeBookMark(currentSheet, 'AV', _row), date2str(disAssignDate, 123, 2, 2, 2, 2, 4, DateFlags::None));

            if (disAssignDate)
            {
                disCancelDate = this.findUnDisRate(invalidRate, _emplId, disAssignDate);
                if (disCancelDate)
                {
                    xls.insertValue(makeBookMark(currentSheet, 'BG', _row), date2str(disCancelDate - 1, 123, 2, 2, 2, 2, 4, DateFlags::None));
                }
            }

            amountTotal += invalidDedTotal;
            xls.insertValue(makeBookMark(currentSheet, 'BR', _row), invalidDedTotal);

            amountFirst += invalidDed1Mth;
            xls.insertValue(makeBookMark(currentSheet, 'CD', _row), invalidDed1Mth);

            amountSecond += invalidDed2Mth;
            xls.insertValue(makeBookMark(currentSheet, 'CP', _row), invalidDed2Mth);

            amountThird += invalidDed3Mth;
            xls.insertValue(makeBookMark(currentSheet, 'DB', _row), invalidDed3Mth);

            return;
        }

        void insertNewPage()
        {
            xls.insertSheet(currentSheet, currentSheet-1);
            this.fillRegularCells('DC2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);

            if (invalidPages > 1)
            {
                xls.insertValue('CJ9', #TableExtTitle, currentSheet);

                xls.deleteRow(54,   currentSheet-1);
                xls.insertRow(54,   currentSheet-1);
            }

            invalidsOnPageCount = 0;
        }

        currentSheet = #InvalidsFirstPage;
        currentRow = #InvFirstRow;

        invalidsTotal = invalidAmountsMap.elements();
        mapEnum = invalidAmountsMap.getEnumerator();

        if (invalidsTotal == 0)
        {
            return true;
        }

        invalidPages = 1;
        insertNewPage();

        while (mapEnum.moveNext())
        {
            if (invalidsOnPageCount >= #InvPerPage)
            {
                currentSheet++;

                if (invalidsTotal - invalidCount > 0)
                {
                    invalidPages++;
                    insertNewPage();
                }
            }

            emplId            = mapEnum.currentKey();
            [#InvalidAmounts] = mapEnum.currentValue();
            disAssignDate     = this.isInvalid(emplId);

            invalidsOnPageCount++;
            invalidCount++;

            insertInvalid(emplId, #invFirstRow + invalidsOnPageCount - 1);
        }

        if (invalidPages > 0)
        {
            xls.insertValue(makeBookMark(currentSheet, 'BR', 54),   amountTotal);
            xls.insertValue(makeBookMark(currentSheet, 'CD', 54),   amountFirst);
            xls.insertValue(makeBookMark(currentSheet, 'CP', 54),   amountSecond);
            xls.insertValue(makeBookMark(currentSheet, 'DB', 54),   amountThird);
        }

        currentSheet++;

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable5</Name>
				<Source><![CDATA[
    private boolean fillTable5()
    {
        RPayAmount      amount, amount2Check;
        RPayDayHour     qty;
        int             cnt;

        int countRecipients(str _payCType = '', Range _sortOfWork = '')
        {
            Query                   query = new Query();
            QueryBuildDataSource    qbds, qbds2;
            QueryRun                queryRun;
            int                     j;

            qbds = query.addDataSource(tablenum(RPayTrans));
            qbds.addRange(fieldnum(RPayTrans, PayPeriod)).value(fromYearStart);
            qbds.addRange(fieldnum(RPayTrans, PayCType)).value(_payCType);
            qbds.orderMode(OrderMode::GroupBy);
            qbds.addSortField(fieldnum(RPayTrans, EmplId));

            if (_sortOfWork)
            {
                qbds2 = qbds.addDataSource(tablenum(RHRMEmplTable));
                qbds2.joinMode(JoinMode::ExistsJoin);
                qbds2.addLink(fieldnum(RPayTrans, EmplId), fieldnum(RHRMEmplTable, EmployeeId));
                qbds2.fetchMode(QueryFetchMode::One2One);
                qbds2.addRange(fieldnum(RHRMEmplTable, SortOfWork)).value(_sortOfWork);
            }

            queryRun = new QueryRun(query);

            while (queryRun.next())
            {
                j ++;
            }

            return j;
        }
        ;

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverSick);
        xls.insertValue('T8R1C3', this.calcPayment(fromYearStart, fundSocialInsurance, pctOverSick));
        xls.insertValue('T8R1C4', qty);
        amount2Check = this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverPregnant);
        xls.insertValue('T8R2C3', this.calcPayment(fromYearStart, fundSocialInsurance, pctOverPregnant));
        xls.insertValue('T8R2C4', qty);
        amount2Check += this.round(amount, roundValue);

        xls.insertValue('T5R3C3', countRecipients(con2str([pctOverChild1, pctOverChild2]), ''));

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverChild1);
        xls.insertValue('T8R4C3', this.calcPayment(fromYearStart, fundSocialInsurance, pctOverChild1));
        xls.insertValue('T8R4C4', cnt);
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverChild2);
        xls.insertValue('T8R5C3', this.calcPayment(fromYearStart, fundSocialInsurance, pctOverChild2));
        xls.insertValue('T8R5C4', cnt);
        amount2Check += this.round(amount, roundValue);

        [amount, qty, cnt] = this.calcCostType(fromYearStart, fundSocialInsurance, pctOverFuneral);
        xls.insertValue('T8R6C3', this.calcPayment(fromYearStart, fundSocialInsurance, pctOverFuneral));
        xls.insertValue('T8R6C4', cnt);
        amount2Check += this.round(amount, roundValue);

        this.fillRegularCells('EY2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable6</Name>
				<Source><![CDATA[
    private void fillTable6()
    {
        RPayAmount roundFundTotalAmountSum(str             _dates,
                                           RPayCounterUnit _fund,
                                           boolean         _useGroup = false,
                                           RPayGroup       _group = "")
        {
            return this.round(this.getFundTotalAmountSum(_dates, _fund, _useGroup, _group), roundValue);
        }

        xls.insertValue('T9C4Total',roundFundTotalAmountSum(fromYearStart, fundTraumaInsurance) -
                                    this.round(this.getFundTotalDeductionAmount(fromYearStart, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C4M1',   roundFundTotalAmountSum(month1, fundTraumaInsurance) -
                                    this.round(this.getFundTotalDeductionAmount(month1, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C4M2',   roundFundTotalAmountSum(month2, fundTraumaInsurance) -
                                    this.round(this.getFundTotalDeductionAmount(month2, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C4M3',   roundFundTotalAmountSum(month3, fundTraumaInsurance) -
                                    this.round(this.getFundTotalDeductionAmount(month3, fundTraumaInsurance, '*', '', '', true)));

        xls.insertValue('T9C5Total', invalidFundTotal);
        xls.insertValue('T9C5M1',   invalidFund1Mth);
        xls.insertValue('T9C5M2',   invalidFund2Mth);
        xls.insertValue('T9C5M3',   invalidFund3Mth);

        xls.insertValue('T9C6',     this.round(this.calcFundBase(fromYearStart, fundPayments, fundTraumaInsurance),
                                               roundValue) +
                                    this.round(this.getFundTotalDeductionAmount(fromYearStart, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C6M1',   this.round(this.calcFundBase(month1, fundPayments, fundTraumaInsurance),
                                               roundValue) +
                                    this.round(this.getFundTotalDeductionAmount(month1, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C6M2',   this.round(this.calcFundBase(month2, fundPayments, fundTraumaInsurance),
                                               roundValue) +
                                    this.round(this.getFundTotalDeductionAmount(month2, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C6M3',   this.round(this.calcFundBase(month3, fundPayments, fundTraumaInsurance),
                                               roundValue) +
                                    this.round(this.getFundTotalDeductionAmount(month3, fundTraumaInsurance, '*', '', '', true)));
        xls.insertValue('T9C7',     RPayRateTable::find(RPayCounterTable::find(fundTraumaInsurance).PayRate).GetValue(RPayRateCallContext::construct(periodEnd)));
        xls.insertValue('T9C8',     RPayRateTable::find(rateInsuranceCut).GetValue(RPayRateCallContext::construct(periodEnd)));
        xls.insertValue('T9C9',     date2strusr(RPayRateTrans::find(rateInsuranceExtra, periodEnd).RateDate, DateFlags::None));
        xls.insertValue('T9C10',    RPayRateTable::find(rateInsuranceExtra).GetValue(RPayRateCallContext::construct(periodEnd)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable7</Name>
				<Source><![CDATA[
    private void fillTable7()
    {
        RPayAmount      amount;
        str             payCostTypes;

        str merge(str a, str b)
        {
            return a + (a ? "@SYS35667" : '') + b;
        }

        ;

        payCostTypes = merge(merge(pctIncident, pctProfRisk), pctHealing);

        xls.insertValue('T10R2PAST', this.getFundAmountSum(fundTraumaInsurance, beforeQuarter));
        xls.insertValue('T10R2M1',   this.getFundAmountSum(fundTraumaInsurance, month1));
        xls.insertValue('T10R2M2',   this.getFundAmountSum(fundTraumaInsurance, month2));
        xls.insertValue('T10R2M3',   this.getFundAmountSum(fundTraumaInsurance, month3));

        [amount] = this.calcCostType(beforeQuarter, fundSocialInsurance, payCostTypes);
        xls.insertValue('T10R10PAST',this.round(amount, roundValue));

        [amount] = this.calcCostType(month1, fundSocialInsurance, payCostTypes);
        xls.insertValue('T10R10M1',  this.round(amount, roundValue));

        [amount] = this.calcCostType(month2, fundSocialInsurance, payCostTypes);
        xls.insertValue('T10R10M2',  this.round(amount, roundValue));

        [amount] = this.calcCostType(month3, fundSocialInsurance, payCostTypes);
        xls.insertValue('T10R10M3',  this.round(amount, roundValue));

        this.fillRegularCells('DA2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable8</Name>
				<Source><![CDATA[
    private boolean fillTable8()
    {
        RPayAmount      amount, amount2Check;
        RPayDayHour     days;
        ;

        [amount, days] = this.calcCostType(fromYearStart, fundSocialInsurance, pctIncident);
        xls.insertValue('T11R1C3', days);
        xls.insertValue('T11R1C4', this.round(amount, roundValue));
        amount2Check = this.round(amount, roundValue);

        [amount, days] = this.calcCostType(fromYearStart, fundSocialInsurance, pctIncident, '', sortOfWorkPluralistically);
        xls.insertValue('T11R2C3', days);
        xls.insertValue('T11R2C4', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        [amount, days] = this.calcCostType(fromYearStart, fundSocialInsurance, pctProfRisk);
        xls.insertValue('T11R4C3', days);
        xls.insertValue('T11R4C4', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        [amount, days] = this.calcCostType(fromYearStart, fundSocialInsurance, pctProfRisk, '', sortOfWorkPluralistically);
        xls.insertValue('T11R5C3', days);
        xls.insertValue('T11R5C4', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        [amount, days] = this.calcCostType(fromYearStart, fundSocialInsurance, pctHealing);
        xls.insertValue('T11R7C3', days);
        xls.insertValue('T11R7C4', this.round(amount, roundValue));
        amount2Check += this.round(amount, roundValue);

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTable9</Name>
				<Source><![CDATA[
    private void fillTable9()
    {
        xls.insertValue('T13R1', this.calcPayment(fromYearStart, fundSocialInsurance, pctIncident));
        xls.insertValue('T13R3', this.calcPayment(fromYearStart, fundSocialInsurance, pctProfRisk));

        this.fillRegularCells('DC2', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillTitle</Name>
				<Source><![CDATA[
    private void fillTitle()
    {
        int i;
        OKDP_RU okdp;

        #define.INNLength(12)
        #define.KPPLength(9)
        #define.OGRNLength(15)
        #define.CorrectSrc('000')
        #define.Correct('XXX')
        #define.PeriodNumLen(2)
        #define.OKATOLen(11)
        #define.PhoneLength(15)

        this.fillSheetCommon();
        xls.insertValue('FullName',             companyInfo.RFullName, currentSheet);
        this.fillRegularCells('CorrectNum',     correcting ? #Correct : #CorrectSrc, strlen(#Correct), #cellSpace, currentSheet);
        this.fillRegularCells('ReportPeriod',   this.reportPeriod2Str(), #PeriodNumLen, #cellSpace, currentSheet);
        this.fillRegularCells('PayYr',          strFmt('%1', year(payYear)), 4, #cellSpace, currentSheet);
        this.fillRegularCells('OKATO',          CompanyInfoHelper::partyOKATOasOfDate_RU(companyInfo, reportDate), #OKATOLen, #cellSpace, currentSheet, true, true);
        this.fillRegularCells('INN',            companyInfo.partyINNasOfDate_RU(reportDate), #INNLength, #cellSpace, currentSheet, true, true);
        this.fillRegularCells('KPP',            companyInfo.partyKPPasOfDate_RU(reportDate), #KPPLength, #cellSpace, currentSheet, true, true);
        this.fillRegularCells('OGRN',           CompanyInfoHelper::partyOGRNasOfDate_RU(companyInfo, reportDate), #OGRNLength, #cellSpace, currentSheet, true, true);
        this.fillRegularCells('Phone',          companyInfo.phone(), #PhoneLength, #cellSpace, currentSheet);

        okdp = strrem(CompanyInfoHelper::partyOKDPasOfDate_RU(companyInfo, reportDate), '.- ');

        for (i = 1; i <= strlen(okdp); i += 2)
        {
            this.fillRegularCells('OKDP' + int2str((i div 2) + 1), substr(okdp, i, 2), 2, #cellSpace, currentSheet);
        }

        this.fillAddress();
        this.fillEmplQty();
        this.fillOfficials();

        if (reportDate)
        {
            this.fillRegularCells('ReportDay' , (dayofmth(reportDate) < 10) ? '0' + int2str(dayofmth(reportDate)) : int2str(dayofmth(reportDate)), 2, #cellSpace, currentSheet);
            this.fillRegularCells('ReportMth' , (mthofyr(reportDate) < 10) ? '0' + int2str(mthofyr(reportDate)) : int2str(mthofyr(reportDate)), 2, #cellSpace, currentSheet);
            this.fillRegularCells('ReportYr', strFmt('%1', year(payYear)), 4, #cellSpace, currentSheet);
        }

        this.fillRegularCells('DD10', this.pageNum2str(), 3, #cellSpace, currentSheet, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillWomen</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void fillWomen()
    {
        #define.EmplQtyLen(6)
        this.fillRegularCells('WomenQty', strFmt('%1', womenQty), #EmplQtyLen, #cellSpace, 1, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>findDisRate</Name>
				<Source><![CDATA[
    private RPayRateTrans findDisRate(RPayRateCode _payRateCode, RHRMEmplId _emplId)
    {
        TransDate               transDate = periodEnd;
        RPayRateTrans           rPayRateTrans;

        do
        {
            rPayRateTrans = RPayRateTrans::find(_payRateCode, transDate, _emplId);

            if (! rPayRateTrans.RecId)
                break;

            transDate = rPayRateTrans.RateDate - 1;
        }
        while (! any2int(rPayRateTrans.RateValue));

        return rPayRateTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findUnDisRate</Name>
				<Source><![CDATA[
    private  TransDate findUnDisRate(RPayRateCode _payRateCode,
                                     RHRMEmplId   _emplId,
                                     TransDate    _startDate)
    {
        TransDate               transDate = _startDate;
        RPayRateTrans           rPayRateTrans;
        TransDate               currentPeriod, ret;
        boolean                 rateValue;

        if (rateTable.RateValueType == RPayRateValueType::CounterValue)
        {
            for (currentPeriod = dateEndMth(_startDate) + 1; currentPeriod <= periodEnd; currentPeriod = endmth(nextMth(currentPeriod)))
            {
                rateValue = rateTable.getValue(RPayRateCallContext::construct(currentPeriod, _emplId)) == 0;
                if (rateValue)
                {
                    ret = dateEndMth(currentPeriod);
                    break;
                }
            }
        }
        else
        {
            do
            {
                select firstonly RecId, RateDate, RateValue from rPayRateTrans
                     where rPayRateTrans.PayRate    == _payRateCode    &&
                           rPayRateTrans.RateDate   >  transDate       &&
                           rPayRateTrans.RateTypeAC == _emplId;

                if (! rPayRateTrans.RecId)
                {
                    break;
                }

                transDate = rPayRateTrans.RateDate;
            }
            while (any2int(rPayRateTrans.RateValue));

            ret = rPayRateTrans.RateDate;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEmplAvgQty</Name>
				<Source><![CDATA[
    private RPayAverageQty getEmplAvgQty(RPayCounterUnit _fund, RPayCounterUnit _payGroup)
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RPayAverageQuantity));
        QueryBuildDatasource    qbdsFund, qbdsMember, qbdsPerson, qbdsEmpl;
        RPayAverageQuantity     rPayAverageQuantity;

        qbds.addSelectionField(fieldnum(RPayAverageQuantity, AverageQty), SelectionField::Avg);
        qbds.addRange(fieldnum(RPayAverageQuantity, PayPeriod)).value(fromYearStart);
        qbds.addRange(fieldnum(RPayAverageQuantity, PayGroup)).value(_payGroup);

        qbdsMember = qbds.addDataSource(tablenum(RPayGroupMember));
        qbdsMember.addLink(fieldnum(RPayAverageQuantity, PayGroup), fieldnum(RPayGroupMember, PayGroup));

        if (RPayGroupTable::find(_payGroup).Type == RPayGroupType::Employee)
        {
            qbdsFund = qbdsMember.addDataSource(tablenum(RPayFundEmplSum));
            qbdsFund.addLink(fieldnum(RPayGroupMember, PersonEmplId), fieldnum(RPayFundEmplSum, EmplId));
        }
        else
        {
            qbdsPerson = qbdsMember.addDataSource(tableNum(RHRMPersonTable));
            qbdsPerson.addLink(fieldnum(RPayGroupMember, PersonEmplId), fieldnum(RHRMPersonTable, PersonnelNumber));
            qbdsPerson.addSelectionField(fieldNum(RHRMPersonTable, RecId));

            qbdsEmpl = qbdsPerson.addDataSource(tableNum(RHRMEmplTable));
            qbdsEmpl.addLink(fieldnum(RHRMPersonTable, RecId), fieldnum(RHRMEmplTable, RHRMPersonTable));
            qbdsEmpl.addRange(fieldNum(RHRMEmplTable, EmployeeId)).value(strFmt('(%1 == %2)',
                fieldstr(RHRMEmplTable, PayMainEmplId),
                fieldstr(RHRMEmplTable, EmployeeId)));

            qbdsFund = qbdsEmpl.addDataSource(tablenum(RPayFundEmplSum));
            qbdsFund.addLink(fieldnum(RHRMEmplTable, PayMainEmplId), fieldnum(RPayFundEmplSum, EmplId));
        }

        qbdsFund.addRange(fieldnum(RPayFundEmplSum, RpayCounterUnit)).value(_fund);

        if (reportPeriod == RPayReportSocialTaxPeriod::Year || !correcting)
        {
            qbdsFund.addRange(fieldnum(RPayFundEmplSum, PayPeriod)).value(fromYearStart);
        }
        else
        {
            qbdsFund.addRange(fieldnum(RPayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        rPayAverageQuantity = RPayReport4SIF_2012::executeQuery(query, tablenum(RPayAverageQuantity));

        return this.round(rPayAverageQuantity.AverageQty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEmplQty</Name>
				<Source><![CDATA[
    private Counter getEmplQty(RPayCounterUnit _payGroup)
    {
        RPayGroupMember groupMember;
        RHRMEmplTable   emplTable;
        RHRMPersonTable personTable;

        Set st = new Set(Types::String);

        boolean findHistory(RHRMEmplId _emplId)
        {
            RPayHistory payHistory, payHistoryDismiss;
            select firstonly recId from payHistory
                order by startDate desc, transTime desc
                where payHistory.Number        == _emplId &&
                     (payHistory.operationType == RHRMJournalType::Receive   ||
                      payHistory.operationType == RHRMJournalType::Transfer) &&
                      payHistory.StartDate     <= periodEnd
                notexists join payHistoryDismiss
                    where payHistoryDismiss.Number          == payHistory.Number        &&
                          payHistoryDismiss.operationType   == RHRMJournalType::Dismiss &&
                          payHistoryDismiss.StartDate       >= payHistory.StartDate     &&
                          payHistoryDismiss.StartDate       <  dateStartYr(payYear);

            return payHistory.RecId != 0;
        }

        if (RPayGroupTable::find(_payGroup).Type == RPayGroupType::Employee)
        {
            while select PersonEmplId from groupMember
                where groupMember.PayGroup == _payGroup
            join payMainEmplId from emplTable
                where emplTable.EmployeeId == groupMember.PersonEmplId
            {
                if (findHistory(groupMember.PersonEmplId))
                {
                    st.add(emplTable.PayMainEmplId);
                }
            }
        }
        else
        {
            while select PersonEmplId from groupMember
                where groupMember.PayGroup == _payGroup
            join RecId from personTable
                where personTable.PersonnelNumber == groupMember.PersonEmplId
            join payMainEmplId, EmployeeId from emplTable
                where emplTable.RHRMPersonTable == personTable.RecId
            {
                if (findHistory(emplTable.EmployeeId))
                {
                    st.add(emplTable.PayMainEmplId);
                }
            }
        }

        return st.elements();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean     ret = super();
        date        d;

        if (ret)
        {
            payYear         = dfPayYear.value();
            reportPeriod    = dfReportPeriod.value();
            officialsType   = dfOfficialsType.value();
            responsibleId   = dfResponsibleId.value();
            correcting      = dfCorrecting.value();
            reportDate      = dfReportDate.value();

            if (payYear == dateNull())
            {
                ret = checkFailed("@RUP2174");
            }

            d               = mkdate(1, reportPeriod * 3 + 1, year(payYear));
            beforeQuarter   = SysQuery::range(dateStartYr(d), d - 1);
            thisQuarter     = SysQuery::range(d, dateEndQtr(d));
            fromYearStart   = SysQuery::range(dateStartYr(d), dateEndQtr(d));
            month1          = SysQuery::range(d, endmth(d));
            mthnames[1]     = strlwr(mthname(mthofyr(d)));
            d               = nextmth(d);
            month2          = SysQuery::range(d, endmth(d));
            mthnames[2]     = strlwr(mthname(mthofyr(d)));
            d               = nextmth(d);
            month3          = SysQuery::range(d, endmth(d));
            mthnames[3]     = strlwr(mthname(mthofyr(d)));
            periodEnd       = endmth(mkdate(1, (reportPeriod + 1) * 3, year(payYear)));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFundAmountSum</Name>
				<Source><![CDATA[
    private RPayAmount getFundAmountSum(RPayCounterUnit _fund, str _dates)
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RpayFundEmplSum));
        RpayFundEmplSum         rpayFundEmplSum;

        if (! _fund)
        {
            return 0;
        }

        qbds.addSelectionField(fieldnum(RpayFundEmplSum, FundAmount), SelectionField::Sum);
        qbds.addRange(fieldnum(RpayFundEmplSum, RpayCounterUnit)).value(_fund);

        if (correcting)
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(_dates);
        }
        else
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, PayPeriod)).value(_dates);
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        RpayFundEmplSum = RPayReport4SIF_2012::executeQuery(query, tablenum(RpayFundEmplSum));

        return this.round(rpayFundEmplSum.FundAmount, roundValue);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFundTotalAmountSum</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected RPayAmount getFundTotalAmountSum(str             _dates,
                                               RPayCounterUnit _fund,
                                               boolean         _useGroup     = false,
                                               RPayGroup       _group        = '',
                                               RHRMEmplId      _emplId       = '')
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbdsPerson, qbdsEmpl, qbds = query.addDataSource(tablenum(RpayFundEmplSum));
        RpayFundEmplSum         rpayFundEmplSum;
        Amount                  amount;

        if (! _fund || (_useGroup && ! _group))
        {
            return 0;
        }
        qbds.addSelectionField(fieldnum(RpayFundEmplSum, RpayFundTotalAmount), SelectionField::Sum);
        qbds.addRange(fieldnum(RpayFundEmplSum, RpayCounterUnit)).value(_fund);

        if (_emplId)
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, EmplID)).value(_emplId);
        }

        if (correcting)
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(_dates);
        }
        else
        {
            qbds.addRange(fieldnum(RpayFundEmplSum, PayPeriod)).value(_dates);
            qbds.addRange(fieldnum(RpayFundEmplSum, CorrectedPeriod)).value(fromYearStart);
        }

        if (_useGroup)
        {
            if (RPayGroupTable::find(_group).Type == RPayGroupType::Employee)
            {
                qbds = qbds.addDataSource(tablenum(RPayGroupMember));
                qbds.joinMode(JoinMode::ExistsJoin);

                qbds.addLink(fieldnum(RpayFundEmplSum, EmplId), fieldnum(RPayGroupMember, PersonEmplId));
            }
            else
            {
                qbdsEmpl = qbds.addDataSource(tablenum(RHRMEmplTable));
                qbdsEmpl.addLink(fieldnum(RpayFundEmplSum, EmplId), fieldnum(RHRMEmplTable, PayMainEmplId));

                qbdsPerson = qbds.addDataSource(tablenum(RHRMPersonTable));
                qbdsPerson.addLink(fieldnum(RHRMEmplTable, RHRMPersonTable), fieldnum(RHRMPersonTable, RecId));

                qbds = qbdsPerson.addDataSource(tablenum(RPayGroupMember));
                qbds.joinMode(JoinMode::ExistsJoin);

                qbds.addLink(fieldnum(RHRMPersonTable, PersonnelNumber), fieldnum(RPayGroupMember, PersonEmplId));
            }

            qbds.addRange(fieldnum(RPayGroupMember, PayGroup)).value(_group);
        }

        rpayFundEmplSum = RPayReport4SIF_2012::executeQuery(query, tablenum(RpayFundEmplSum));
        amount = rpayFundEmplSum.RpayFundTotalAmount;

        return amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFundTotalDeductionAmount</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected RPayAmount getFundTotalDeductionAmount(str             _period,
                                                     RPayCounterUnit _fund,
                                                     str             _deductionCodesToInclude = '',
                                                     str             _deductionCodesToExclude = '',
                                                     RHRMEmplId      _emplId                  = '',
                                                     boolean         _excludeDeductionPercent = false)
    {
        Query                   query = new Query();
        QueryBuildDataSource    qbds = query.addDataSource(tablenum(RpayFundEmplDeduction)), qbdsDeductionCodePct;
        RpayFundEmplDeduction   rpayFundEmplDeduction;
        ;

        if ( ! (_fund && (_deductionCodesToInclude || _deductionCodesToExclude)))
        {
            return 0;
        }

        qbds.addSelectionField(fieldnum(RpayFundEmplDeduction, DeductionAmount), SelectionField::Sum);
        qbds.addRange(fieldnum(RpayFundEmplDeduction, RpayCounterUnit)).value(_fund);

        if (correcting)
        {
            qbds.addRange(fieldnum(RpayFundEmplDeduction, CorrectedPeriod)).value(_period);
        }
        else
        {
            qbds.addRange(fieldnum(RpayFundEmplDeduction, PayPeriod)).value(_period);
            qbds.addRange(fieldnum(RpayFundEmplDeduction, CorrectedPeriod)).value(fromYearStart);
        }

        if (_deductionCodesToInclude)
        {
            qbds.addRange(fieldnum(RpayFundEmplDeduction, RpayFundDeductionCode)).value(_deductionCodesToInclude);
        }

        if (_deductionCodesToExclude)
        {
            _deductionCodesToExclude = '!' + strreplace(_deductionCodesToExclude, ',', ',!');
            qbds.addRange(fieldnum(RpayFundEmplDeduction, RpayFundDeductionCode)).value(_deductionCodesToExclude);
        }

        if (_emplId)
        {
            qbds.addRange(fieldnum(RpayFundEmplDeduction, EmplId)).value(_emplId);
        }

        if (_excludeDeductionPercent)
        {
            qbdsDeductionCodePct = qbds.addDataSource(tablenum(RPayFundDeductionTable));
            qbdsDeductionCodePct.addLink(fieldnum(RpayFundEmplDeduction, RpayFundDeductionCode), fieldnum(RPayFundDeductionTable, RpayFundDeductionCode));
            qbdsDeductionCodePct.joinMode(JoinMode::ExistsJoin);
            qbdsDeductionCodePct.addRange(fieldnum(RPayFundDeductionTable, RpayRateTaxValue)).value(Sysquery::valueEmptyString());
        }

        rpayFundEmplDeduction = RPayReport4SIF_2012::executeQuery(query, tablenum(rpayFundEmplDeduction));

        return rpayFundEmplDeduction.DeductionAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isEmployed</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected boolean isEmployed(RHRMEmplId _mainEmplId)
    {
        RPayHistory     payHistory, payHistoryDismiss;
        RHRMEmplTable   emplTableEmployee;

        void findHistory(RHRMEmplId _emplId)
        {
            select firstonly recId from payHistory
                order by startDate desc, transTime desc
                where payHistory.Number        == _emplId &&
                     (payHistory.operationType == RHRMJournalType::Receive   ||
                      payHistory.operationType == RHRMJournalType::Transfer) &&
                      payHistory.StartDate     <= periodEnd
                notexists join payHistoryDismiss
                    where payHistoryDismiss.Number          == payHistory.Number        &&
                          payHistoryDismiss.operationType   == RHRMJournalType::Dismiss &&
                          payHistoryDismiss.StartDate       >  payHistory.StartDate     &&
                          payHistoryDismiss.StartDate       <= periodEnd;
        }

        findHistory(_mainEmplId);

        if (payHistory.RecId != 0)
        {
            return true;
        }

        while select EmployeeId from emplTableEmployee
            order by EmployeeId
            where emplTableEmployee.PayMainEmplId == _mainEmplId &&
                  emplTableEmployee.employeeId           != _mainEmplId
        {
            findHistory(emplTableEmployee.EmployeeId);
            if (payHistory.RecId != 0)
            {
                return true;
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInvalid</Name>
				<Source><![CDATA[
    private TransDate isInvalid(RHRMEmplId _emplId)
    {
        RPayRateCode            payRateCode;
        TransDate               currentPeriod;
        boolean                 rateValue;
        TransDate               periodStart;

        TransDate               disDate, unDisDate;

        payRateCode  = invalidRate;

        if (rateTable.RateValueType == RPayRateValueType::CounterValue)
        {
            for (currentPeriod = dateEndMth(dateStartYr(periodEnd)); currentPeriod <= periodEnd; currentPeriod = endmth(nextMth(currentPeriod)))
            {
                rateValue = rateTable.getValue(RPayRateCallContext::construct(currentPeriod, _emplId)) > 0;
                if (rateValue)
                {
                    return currentPeriod;
                }
            }
        }
        else
        {
            periodStart = dateStartYr(periodEnd);

            disDate =   this.findDisRate(payRateCode, _emplId).RateDate;

            if (! disDate)
            {
                return dateNull();
            }

            if (periodStart < disDate)
            {
                return disDate;
            }

            unDisDate = this.findUnDisRate(payRateCode, _emplId, disDate);

            if (! unDisDate)
            {
                return disDate;
            }

            if (unDisDate && periodStart < unDisDate)
            {
                return disDate;
            }
        }

        return dateNull();
    }

]]></Source>
			</Method>
			<Method>
				<Name>makeDocument</Name>
				<Source><![CDATA[
    private boolean makeDocument()
    {
        xls = new ComExcelDocument_RU();

        if (! xls.newFile(RPayReport4SIF_2012::fullTemplateName(), false))
        {
            return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,#CurrentList,''];
    }

]]></Source>
			</Method>
			<Method>
				<Name>pageNum2str</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected str pageNum2str()
    {
        int len;
        str tmpStr;
        #define.Mask('000')

        tmpStr = int2str(currentSheet);
        len    = strlen(tmpStr);
        tmpStr = #Mask + tmpStr;

        tmpStr = strdel(tmpStr, 1, len);

        return tmpStr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reportPeriod2Str</Name>
				<Source><![CDATA[
    private str reportPeriod2Str()
    {
        str period;
        ;

        switch (reportPeriod)
        {
            case RPayReportSocialTaxPeriod::Quarter:
                period = '03';
                break;
            case RPayReportSocialTaxPeriod::HalfYear:
                period = '06';
                break;
            case RPayReportSocialTaxPeriod::NineMonth:
                period = '09';
                break;
            case RPayReportSocialTaxPeriod::Year:
                period = '12';
                break;
            default:
                period = '00';
        }

        return period;
    }

]]></Source>
			</Method>
			<Method>
				<Name>round</Name>
				<Source><![CDATA[
    private RPayAmount round(RPayAmount _amount, RoundOff _round = 0)
    {
        if (rounding)
            return round(_amount, RPayRateTable::find(rounding).getValue(RPayRateCallContext::construct(periodEnd)));
        if (_round)
            return round(_amount, _round);
        return _amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        if (this.makeDocument())
        {
            invalidAmountsMap = new Map(Types::String, Types::Container);
            this.checkAndPrepareParameters();
            this.calcInvalids();
            this.fillDocument();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer version = conpeek(packedClass, 1);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = packedClass;
                break;

            default:
                return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ClassDescription description()
    {
        return "@RUP2983";
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeQuery</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static Common executeQuery(Query _query, tableId _tableId)
    {
        QueryRun qr = new QueryRun(_query);
        ;

        qr.next();

        return qr.get(_tableId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fullTemplateName</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static str fullTemplateName()
    {
        resourceNode rn = SysResource::getResourceNode(resourceStr(RPayReport4SIF_2012));
        rn.AOTload();
        return SysResource::saveToTempFile(rn, false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        RPayReport4SIF_2012 report = new RPayReport4SIF_2012();

        if (report.prompt())
        {
            report.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>