<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventItemGroupItem_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>InventItemGroupItem_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class InventItemGroupItem_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static InventItemGroupItem_DataSharingTableEvents construct()
    {
        return new InventItemGroupItem_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = InventItemGroupItem_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Inserted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventItemGroupItem origInventItemGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;
                InventItemGroupItem inventItemGroupItem;
                buf2Buf(origInventItemGroupItem, inventItemGroupItem);
                inventItemGroupItem.ItemDataAreaId = _dataSharingCompany;
                inventItemGroupItem.ItemGroupDataAreaId = _dataSharingCompany;
                if (!inventItemGroupItem.validateWrite())
                {
                    throw error(strFmt("@SCM:SysDataSharingCannotInsertRecordInTable", tablePName(InventItemGroupItem), _dataSharingCompany));
                }
                inventItemGroupItem.insert();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Deleted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventItemGroupItem origInventItemGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                InventItemGroupItem inventItemGroupItem = InventItemGroupItem::findByItemIdLegalEntity(origInventItemGroupItem.ItemId, _dataSharingCompany, true);
                if (inventItemGroupItem.RecId)
                {
                    if (!inventItemGroupItem.validateDelete())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotDeleteRecordInTable", tablePName(InventItemGroupItem), _dataSharingCompany));
                    }
                    inventItemGroupItem.delete();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventItemGroupItem origInventItemGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                InventItemGroupItem inventItemGroupItem = InventItemGroupItem::findByItemIdLegalEntity(origInventItemGroupItem.ItemId, _dataSharingCompany, true);
                if (inventItemGroupItem.RecId)
                {
                    buf2Buf(origInventItemGroupItem, inventItemGroupItem);
                    inventItemGroupItem.ItemDataAreaId = _dataSharingCompany;
                    inventItemGroupItem.ItemGroupDataAreaId = _dataSharingCompany;
                    if (!inventItemGroupItem.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotUpdateRecordInTable", tablePName(InventItemGroupItem), _dataSharingCompany));
                    }
                    inventItemGroupItem.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventItemGroup), tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemGroupItem_onInserted</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of InventItemGroupItem table
    [DataEventHandler(tableStr(InventItemGroupItem), DataEventType::Inserted)]
    public static void InventItemGroupItem_onInserted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Inserted);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventItemGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemGroupItem = InventItemGroupItem_DataSharingTableEvents::construct();
                tableEventsProcessorInventItemGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemGroupItem);
                using (var contextInsertCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemGroupItem), DataEventType::Inserted))
                {
                    processor.insertCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemGroupItem_onDeleted</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(InventItemGroupItem), DataEventType::Deleted)]
    public static void InventItemGroupItem_onDeleted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Deleted);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventItemGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemGroupItem = new InventItemGroupItem_DataSharingTableEvents();
                tableEventsProcessorInventItemGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemGroupItem);
                using (var contextDeleteCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemGroupItem), DataEventType::Deleted))
                {
                    processor.deleteCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemGroupItem_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(InventItemGroupItem), DataEventType::Updated)]
    public static void InventItemGroupItem_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemGroupItem), DataEventType::Updated);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventItemGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemGroupItem = new InventItemGroupItem_DataSharingTableEvents();
                tableEventsProcessorInventItemGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemGroupItem);
                using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemGroupItem), DataEventType::Updated))
                {
                    processor.updateCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>