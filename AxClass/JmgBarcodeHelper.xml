<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgBarcodeHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgBarcodeHelper</c> class is the helper class that is used for the shop floor control
///    reports.
/// </summary>
class JmgBarcodeHelper
{
    BarcodeSetupId  barcodeSetupId;
    BarcodeSetup    barcodeSetup;
    Barcode         barcode;
    FontName        barcodeFontName;
    FontSize        barcodeFontSize;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>barcodeValidation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs validation that the bar-code string fulfills the rules setup for the bar-code if one is
    /// used.
    /// </summary>
    /// <param name="_barCodeString">
    /// A bar-code string.
    /// </param>
    public void barcodeValidation(BarCodeString _barCodeString)
    {
        BarCodeString barcodeStringUpperCase = strupr(_barCodeString);

        // the job id is empty or if no barcode is used, then skip barcode validation
        if (_barCodeString == '' || barcodeSetup.BarcodeType == BarcodeType::NoBarcode)
        {
            return;
        }

        if (!barcodeSetup.validateBarcode(barcodeStringUpperCase))
        {
            throw(error(strfmt("@SYS41409", barcode.barcodeType(), _barCodeString)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>encodeBarcode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates and formats a bar code value before it can be displayed.
    /// </summary>
    /// <param name="_barcodeValue">
    /// A string to be encoded to a bar code format.
    /// </param>
    /// <returns>
    /// The encoded bar code string
    /// </returns>
    public BarCodeString encodeBarcode(Str _barcodeValue)
    {
        if (barcodeSetup.validateBarcode(_barcodeValue))
        {
            barcode.string(true, _barcodeValue);
            barcode.encode();
        }
        else
        {
            throw(error(strfmt("@SYS41409", barcode.barcodeType(), _barcodeValue)));
        }

        return barcode.barcodeStr();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBarcodeFontName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the font name that is related to a bar-code.
    /// </summary>
    /// <returns>
    /// A font name.
    /// </returns>
    public FontName getBarcodeFontName()
    {
        return barcodeFontName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBarcodeFontSize</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the font size that is related to a barcode.
    /// </summary>
    /// <returns>
    ///    The font size that is related to a barcode.
    /// </returns>
    public FontSize getBarcodeFontSize()
    {
        return barcodeFontSize;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeBarcode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes bar-code elements.
    /// </summary>
    protected void initializeBarcode()
    {
        barcodeSetupId  = JmgParameters::find().getBarcodeSetupId();
        barcodeSetup    = BarcodeSetup::find(barcodeSetupId);
        barcode         = barcodeSetup.barcode();

        if (barcodeSetup.BarcodeType != BarcodeType::NoBarcode)
        {
            barcodeFontName = barcodeSetup.FontName;
            barcodeFontSize = barcodeSetup.FontSize;
        }
        else
        {
            barcodeFontName = '';
            barcodeFontSize = 0;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of the <c>JmgBarcodeHelper</c> class.
    /// </summary>
    /// <returns>
    /// A new instance of the <c>JmgBarcodeHelper</c> class.
    /// </returns>
    public static JmgBarcodeHelper construct()
    {
        JmgBarcodeHelper jmgBarcodeHelper;
        jmgBarcodeHelper = new JmgBarcodeHelper();
        jmgBarcodeHelper.initializeBarcode();

        return jmgBarcodeHelper;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>