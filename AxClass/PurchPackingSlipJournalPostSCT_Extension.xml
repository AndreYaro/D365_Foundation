<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchPackingSlipJournalPostSCT_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(PurchPackingSlipJournalPost))]
internal final class PurchPackingSlipJournalPostSCT_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>endPost</Name>
				<Source><![CDATA[
    protected void endPost()
    {
        next endPost();

        if (FeatureStateProvider::isFeatureEnabled(SCTTracingActivityFeature::instance()))
        {
            this.createTracingActivity();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createTracingActivity</Name>
				<Source><![CDATA[
    private void createTracingActivity()
    {
        PurchTable purchTableCopy = this.sourceTable() as PurchTable;
        VendPackingSlipTrans vendPackingSlipTransLocal;
        InventTransOrigin inventTransOrigin;
        InventTrans inventTrans;
        Set transactionSourceSet = new Set(Types::Record);

        while select ItemId, Qty, InventDimId, PackingSlipId from inventTrans
            exists join InventTransId from inventTransOrigin
                where inventTrans.InventTransOrigin == inventTransOrigin.RecId
            exists join InventTransId from vendPackingSlipTransLocal
                where inventTransOrigin.InventTransId == vendPackingSlipTransLocal.InventTransId
                    && vendPackingSlipTransLocal.VendPackingSlipJour == vendPackingSlipJour.RecId
                    && inventTrans.PackingSlipId == vendPackingSlipJour.PackingSlipId
        {
            transactionSourceSet.add(inventTrans);
        }

        SCTTracingActivityHelper::createAndInsertTracingActivity(
            SCTTracingActivityType::Purchase,
            SCTTracingActivityCode::PurchaseGoodsReceipt,
            purchTableCopy,
            vendPackingSlipJour,
            transactionSourceSet,
            null, 
            null);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>