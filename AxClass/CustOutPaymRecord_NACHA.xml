<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOutPaymRecord_NACHA</Name>
	<SourceCode>
		<Declaration><![CDATA[
class CustOutPaymRecord_NACHA extends CustOutPaymRecord
{
    container record;
    boolean endOfText;

    #define.transactionCodeOffset(2)

    #define.recordTypeCodeOffset(1)

    #define.NACHARecLen(94)
    #define.RoutingNumLength(8)
    #define.receivingDFI_IdentificationLength(8)
    #define.receivingDFI_IdentificationOffset(4)
    #define.checkDigitOffset(12)
    #define.DFI_AccountNumberOffset(13)
    #define.amountOffset(30)
    #define.identificationNumberLength(15)
    #define.identificationNumberOffset(40)
    #define.discretionaryDataLength(2)
    #define.discretionaryDataOffset(77)
    #define.addendaRecordIndicatorOffset(79)
    #define.traceNumberOffset(80)

    str #RoutingNumLength bankRoutingNumber;

    str #NACHARecLen outputRecord;
    CustBankAccount custBankAccount;
    AmountCur transactionAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkValues</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks that the data that is needed is valid. Validates that the payment specification has been
    ///    assigned to the payment mode.
    /// </summary>
    /// <param name="_ledgerJournalTrans">
    ///    The ledger journal transaction.
    /// </param>
    /// <param name="_runbaseProgress">
    ///    The run base progress.
    /// </param>
    /// <returns>
    ///    true if the data is valid; otherwise, false.
    /// </returns>
    public boolean checkValues(LedgerJournalTrans _ledgerJournalTrans = null, RunbaseProgress _runbaseProgress = null)
    {
        CustPaymModeSpec custPaymModeSpec;
        boolean ret = super(_ledgerJournalTrans, _runbaseProgress);

        if (!_ledgerJournalTrans.PaymSpec)
        {
            ret = checkFailed(strfmt("@SYS73168", custVendOutPaym.interfaceName(), fieldpname(LedgerJournalTrans, PaymSpec)));
        }

        if (ret)
        {
            // The Payment Mode (Method of Payment) can have only one payment specification for NACHA...
            select count(RecId) from custPaymModeSpec
                   where custPaymModeSpec.PaymMode == _ledgerJournalTrans.PaymMode;

            if (custPaymModeSpec.RecId > 1)
            {
                // Payments made via the '%1' method of payment can only have one payment specification.
                ret = checkFailed(strfmt("@SYS98110", _ledgerJournalTrans.PaymMode));
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField01</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the Record Type Code in the NACHA CCD, CTX, IAT or PPD detail record.
    /// </summary>
    /// <remarks>
    /// The filled value will always be 6.
    /// </remarks>
    // Record Type Code -- Always '6' for a CCD, CTX, IAT, PPD detail record
    //
    protected void fillField01()
    {
        #define.recordTypeCode('6')

        this.strInsert(#recordTypeCode, #recordTypeCodeOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField02</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the 'Transaction Code' in the NACHA CCD, CTX, IAT or PPD detail record.
    /// </summary>
    /// <remarks>
    ///    The transaction code will be '27' if the bank account type is 'Checking', or '37' if the bank
    ///    account type is 'Savings'.
    /// </remarks>
    // Transaction Code:
    //
    // Module               Bank Account Type   Transaction Code Value
    // -------------------- ------------------- ----------------------
    // Accounts Payable     Checking            22
    // Accounts Payable     Savings             32
    // Accounts Receivable  Checking            27
    // Accounts Receivable  Savings             37
    //
    protected void fillField02()
    {
        this.strInsert(custVendOutPaym.getTransactionCode(custBankAccount), #transactionCodeOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField03</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the Receiving DFI Identification in the NACHA CCD, CTX, IAT or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This is the eight digit bank routing number excluding the check digit.
    /// </remarks>
    // Receiving DFI Identification (8 Digits Only - Exclude the Check Digit)
    protected void fillField03()
    {
        bankRoutingNumber = substr(custVendPaym.recieversBankRegistrationNumber(), 1, #receivingDFI_IdentificationLength);
        this.strInsert(bankRoutingNumber, #receivingDFI_IdentificationOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField04</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the 'Check Digit' in the NACHA CCD, CTX, IAT or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This will be the ninth digit of the bank routing number.
    /// </remarks>
    // Check Digit -- 9th digit of the Bank Routing Number (Registration Number)
    protected void fillField04()
    {
        #define.CheckDigitInRoutingNumberOffset(9)

        this.strInsert(substr(custVendPaym.recieversBankRegistrationNumber(), #CheckDigitInRoutingNumberOffset, 1), #checkDigitOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField05</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the DFI Account Number in the NACHA CCD, CTX or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This will be the receivers' bank account number.
    /// </remarks>
    // DFI Account Number
    protected void fillField05()
    {
        this.strInsert(custVendPaym.recieversBankAccount(), #DFI_AccountNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField06</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the Amount in the NACHA CCD, CTX or PPD detail record.
    /// </summary>
    /// <remarks>
    /// The payment amount will be formatted as $$$$$$$$cc.
    /// </remarks>
    // Amount in format $$$$$$$$cc
    protected void fillField06()
    {
        transactionAmount = custVendPaym.paymAmountInSendersCurrency();
        this.strInsert(custVendOutPaym.num2StrNACHA(transactionAmount, 10), #amountOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField07</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the Identification Number in the NACHA CCD, CTX or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This is the sender's Identification of Receiver, or the Customer ID.
    /// </remarks>
    // Identification Number
    protected void fillField07()
    {
        this.strInsert(substr(custVendPaym.sendersIdentificationOfReciever(), 1, #identificationNumberLength), #identificationNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField09</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the 'Discretionary Data' in the NACHA CCD or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This will always be blank.
    /// </remarks>
    // Discretionary Data <BLANK>
    protected void fillField09()
    {
        #define.blank(' ')

        this.strInsert(strrep(#blank, #discretionaryDataLength), #discretionaryDataOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField10</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills the Addenda Record Indicator in the NACHA CCD or PPD detail record.
    /// </summary>
    /// <remarks>
    /// This will always be 0 (zero).
    /// </remarks>
    // Addenda Record Indicator -- Always '0' <ZERO>
    //
    // A value of '1' would indicate that one or more addenda records follow, '0' indicates no such record(s) is/are present.
    protected void fillField10()
    {
        #define.addendaIndicator('0')

        this.strInsert(#addendaIndicator, #addendaRecordIndicatorOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField11</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the 'Trace Number' in the NACHA CCD or PPD detail record.
    /// </summary>
    // Trace Number
    //
    // This is a number that we can use to attempt to uniquely identify the transaction.
    // Ideally, this would consist of an EFT Number, which would function like a Check Number.
    //
    // But because of time constraints, the Trace Number will be generated by concatinating
    // the Journal Numer & Line Number with the following limitations to create a number that
    // will be unique for most users:
    //
    //  The Line Number portion of the Trace Number will be at least four characters,
    //  allowing for a minimum of 10000 unique lines per journal. If the Journal Number
    //  is shorter than 11 characters, the number of characters the line number uses will
    //  grow to fill the 15 characters allowed for the Trace Number.
    //  If the Journal Number is longer than 11 characters, this method will use the
    //  right-most 11 characters from the Journal Number when generating the Trace Number.
    //  If the Line Number is greater than 9999, the right-most three characters of the
    //  Line Number will be used.
    //  So in the situation where there are only three characters for the line number, the
    //  numbers will wrap-around to 0000 each 10000 lines and icrement from there, which will
    //  make this trace number non-unique and not suitable for electronic reconcile, but it
    //  should be unique enough for most manual needs.
    //
    //  EXAMPLES:
    //      Journal Number: 000097_003
    //      Line Number: 1
    //      Trace Number: "000097_00300001"
    //
    //      Journal Number: 1234567890_003
    //      Line Number: 11036
    //      Trace Number: "4567890_0031036"
    //
    protected void fillField11()
    {
        this.strInsert(this.generateTraceNumber(), #traceNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField13</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the 'Trace Number' in the NACHA CTX or IAT detail record.
    /// </summary>
    // Trace Number
    //
    // This is a number that we can use to attempt to uniquely identify the transaction.
    // Ideally, this would consist of an EFT Number, which would function like a Check Number.
    //
    // But because of time constraints, the Trace Number will be generated by concatinating
    // the Journal Numer & Line Number with the following limitations to create a number that
    // will be unique for most users:
    //
    //  The Line Number portion of the Trace Number will be at least four characters,
    //  allowing for a minimum of 10000 unique lines per journal. If the Journal Number
    //  is shorter than 11 characters, the number of characters the line number uses will
    //  grow to fill the 15 characters allowed for the Trace Number.
    //  If the Journal Number is longer than 11 characters, this method will use the
    //  right-most 11 characters from the Journal Number when generating the Trace Number.
    //  If the Line Number is greater than 9999, the right-most three characters of the
    //  Line Number will be used.
    //  So in the situation where there are only three characters for the line number, the
    //  numbers will wrap-around to 0000 each 10000 lines and icrement from there, which will
    //  make this trace number non-unique and not suitable for electronic reconcile, but it
    //  should be unique enough for most manual needs.
    //
    //  EXAMPLES:
    //      Journal Number: 000097_003
    //      Line Number: 1
    //      Trace Number: "000097_00300001"
    //
    //      Journal Number: 1234567890_003
    //      Line Number: 11036
    //      Trace Number: "4567890_0031036"
    //
    protected void fillField13()
    {
        this.strInsert(this.generateTraceNumber(), #traceNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the output record.
    /// </summary>
    protected void fillRecord()
    {
        throw error(Error::missingOverload(funcname()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateTraceNumber</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Generates the trace number.
    /// </summary>
    /// <returns>
    ///    The trace number.
    /// </returns>
    /// <remarks>
    ///    The trace number will be generated by concatenating the journal number and line number with the
    ///    following limitations, to create a number that will be unique for most users:
    ///    <list type="bullet">
    ///       <item>
    ///          <description>The line number part of the trace number will be at least 4 characters. This allows
    ///          for a minimum of 10000 unique lines per journal. If the journal number is shorter than 11
    ///          characters, the number of characters will grow to fill the 15 characters that are allowed for the
    ///          trace number. </description>
    ///       </item>
    ///       <item>
    ///          <description>If the journal number is longer than 11 characters, this method will use the
    ///          right-most 11 characters from the journal number when you generate the trace number.</description>
    ///       </item>
    ///       <item>
    ///          <description>If the line number is more than 9999, the right-most 4 characters of the line number
    ///          will be used.</description>
    ///       </item>
    ///    </list>
    ///     In the situation where there are only 4 characters for the line number, the numbers will
    ///    wrap-around to 0000 at every 10000 lines and will increment from there. This will make this trace
    ///    number non-unique and not suited for electronic reconciling, but it should be unique enough for
    ///    most manual needs.
    /// </remarks>
    // Trace Number
    //
    // This is a number that we can use to attempt to uniquely identify the transaction.
    // Ideally, this would consist of an EFT Number, which would function like a Check Number.
    //
    // But because of time constraints, the Trace Number will be generated by concatinating
    // the Journal Numer & Sequence Number with the following limitations to create a number that
    // will be unique for most users:
    //
    //  The Seq Number portion of the Trace Number will be at least four characters,
    //  allowing for a minimum of 10000 unique lines per journal. If the Journal Number
    //  is shorter than 11 characters, the number of characters the seq number uses will
    //  grow to fill the 15 characters allowed for the Trace Number.
    //  If the Journal Number is longer than 11 characters, this method will use the
    //  right-most 11 characters from the Journal Number when generating the Trace Number.
    //  If the Seq Number is greater than 9999, the right-most three characters of the
    //  Line Number will be used.
    //  So in the situation where there are only four characters for the seq number, the
    //  numbers will wrap-around to 0000 each 10000 lines and increment from there, which will
    //  make this trace number non-unique and not suitable for electronic reconcile, but it
    //  should be unique enough for most manual needs.
    //
    //  EXAMPLES:
    //      Journal Number: 000097_003
    //      Seq Number: 1
    //      Trace Number: "000097_00300001"
    //
    //      Journal Number: 1234567890_003
    //      Seq Number: 11036
    //      Trace Number: "4567890_0031036"
    //
    public str generateTraceNumber()
    {
        #define.MaxJournalNumLength(11)
        #define.MaxLen(15)
        #define.MinSeqLen(4)

        str journalNumber;
        str lineNumber;
        int strLength;

        journalNumber = custVendPaym.ledgerJournalTrans().JournalNum;

        strLength = strlen(journalNumber);

        if (strLength <= #MaxJournalNumLength)
        {
            // If the length of the Journal Number is less than 12 characters,
            // then we can use four or more characters from the line number:
            lineNumber = num2Str0(custVendOutPaym.getNextSequenceNumber(), #MaxLen);
            lineNumber = substr(lineNumber, strLength + 1, #MaxLen - strLength);
        }
        else
        {
            // If the length of the Journal Number is greater than 11 characters,
            // we need to shorten the Journal Number (use right-most characters)
            // to allow for four characters from the line number:
            journalNumber = substr(journalNumber, strLength - 10, #MaxJournalNumLength);
            lineNumber = num2Str0(custVendOutPaym.getNextSequenceNumber(), #MaxLen);
            lineNumber = substr(lineNumber, #MaxJournalNumLength + 1, #MinSeqLen);
        }

        return journalNumber + lineNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the output record.
    /// </summary>
    protected void initRecord()
    {
        int i;

        #define.nullStr('')

        record = connull();

        for (i = 1; i <= #NACHARecLen; i++)
        {
            record += #nullStr;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>outPut</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Writes the payment detail to the Electronic Funds Transfer (EFT) output file and increments the totals for the transaction.
    /// </summary>
    public void outPut()
    {
        custVendOutPaym.total(custVendPaym.paymCurrencyCode(), custVendPaym.paymAmountInSendersCurrency());

        this.initRecord();
        this.fillRecord();
        file.writeExp(record);
    }

]]></Source>
			</Method>
			<Method>
				<Name>strInsert</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Writes the supplied data at the specified position in the NACHA CCD, CTX, IAT or PPD payment record.
    /// </summary>
    /// <param name="_fieldValue">
    ///    The data to write.
    /// </param>
    /// <param name="_fieldPosition">
    ///    The position at which to write the data.
    /// </param>
    protected void strInsert(str _fieldValue, int _fieldPosition)
    {
        //  Poke a supplied string into the "outputRecord" at a given position
        outputRecord = strpoke(outputRecord, _fieldValue, _fieldPosition);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>