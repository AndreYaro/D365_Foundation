<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmPositionCopyBudgetEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
class HcmPositionCopyBudgetEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmPositionCopy_onCopyingFinancialDimensions</Name>
				<Source><![CDATA[
    /// <summary>
    /// The copyingFinancialDimensions event handler for the HcmPositionCopy class.
    /// </summary>
    /// <param name = "_positionRecId">The RecId of the position being copied.</param>
    /// <param name = "_destinationPositionRecId">The RecId of the new position being created.</param>
    /// <param name = "_asOfDateTime">The point in time to be used when reading the date effective information from the source table.</param>
    /// <param name = "_validFrom">The beginning utcDateTime value for the time state.</param>
    /// <param name = "_validTo">The ending utcDateTime value for the time state.</param>
    [SubscribesTo(classStr(HcmPositionCopy), delegateStr(HcmPositionCopy, copyingFinancialDimensions))]
    public static void HcmPositionCopy_onCopyingFinancialDimensions(HcmPositionRecId _positionRecId, HcmPositionRecId _destinationPositionRecId, utcdatetime _asOfDateTime, utcDateTime _validFrom, utcDateTime _validTo)
    {
        HcmPositionCopyBudgetEventHandler::copyBudget(_positionRecId, _destinationPositionRecId, _asOfDateTime, _validFrom, _validTo);
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyBudget</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the budget related data from a source position to a destination position record.
    /// </summary>
    /// <param name = "_positionRecId">The RecId of the position being copied.</param>
    /// <param name = "_destinationPositionRecId">The RecId of the new position being created.</param>
    /// <param name = "_asOfDateTime">The point in time to be used when reading the date effective information from the source table.</param>
    /// <param name = "_validFrom">The beginning utcDateTime value for the time state.</param>
    /// <param name = "_validTo">The ending utcDateTime value for the time state.</param>
    private static void copyBudget(HcmPositionRecId _positionRecId, HcmPositionRecId _destinationPositionRecId, utcdatetime _asOfDateTime, utcDateTime _validFrom, utcDateTime _validTo)
    {
        HcmPositionBudgetDefaultValue   hcmPositionBudgetDefaultValue;
        HcmPositionBudgetDefaultValue   hcmPositionBudgetDefaultValueCopy;
        HcmPositionBudgetAccountLine    hcmPositionBudgetAccountLine;
        HcmPositionBudgetAccountLine    hcmPositionBudgetAccountLineCopy;

        while select validTimeState(_asOfDateTime) hcmPositionBudgetDefaultValue
            where hcmPositionBudgetDefaultValue.Position == _positionRecId
        {
            buf2Buf(hcmPositionBudgetDefaultValue, hcmPositionBudgetDefaultValueCopy);
            hcmPositionBudgetDefaultValueCopy.Position = _destinationPositionRecId;
            hcmPositionBudgetDefaultValueCopy.ValidFrom = _validFrom;
            hcmPositionBudgetDefaultValueCopy.ValidTo = _validTo;
            hcmPositionBudgetDefaultValueCopy.insert();
        }

        while select validTimeState(_asOfDateTime) hcmPositionBudgetAccountLine
            where hcmPositionBudgetAccountLine.Position == _positionRecId
        {
            buf2Buf(hcmPositionBudgetAccountLine, hcmPositionBudgetAccountLineCopy);
            hcmPositionBudgetAccountLineCopy.Position = _destinationPositionRecId;
            hcmPositionBudgetAccountLineCopy.ValidFrom = _validFrom;
            hcmPositionBudgetAccountLineCopy.ValidTo = _validTo;
            hcmPositionBudgetAccountLineCopy.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>