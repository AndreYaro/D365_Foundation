<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymAdvicePrintMgmtReportFormatSetup</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class BankPaymAdvicePrintMgmtReportFormatSetup
{
    private const PrintMgmtReportFormatName BankPaymAdviceCust = ssrsReportStr(BankPaymAdviceCust, Report);
    private const PrintMgmtReportFormatName BankPaymAdviceCustV2 = ssrsReportStr(BankPaymAdviceCustV2, Report);
    private const PrintMgmtReportFormatName BankPaymAdviceVend = ssrsReportStr(BankPaymAdviceVend, Report);
    private const PrintMgmtReportFormatName BankPaymAdviceVendV2 = ssrsReportStr(BankPaymAdviceVendV2, Report);

    private NoYes isReportV2Enabled = BankPaymAdviceReportV2FeatureExposure::isEnabled();

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setDefaultFormat</Name>
				<Source><![CDATA[
    internal static void setDefaultFormat(PrintMgmtDocumentType _documentType)
    {
        BankPaymAdvicePrintMgmtReportFormatSetup setup = new BankPaymAdvicePrintMgmtReportFormatSetup();

        setup.setDefaultFormatInternal(_documentType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultFormatInternal</Name>
				<Source><![CDATA[
    private void setDefaultFormatInternal(PrintMgmtDocumentType _documentType)
    {
        PrintMgmtReportFormat printMgmtReportFormat = PrintMgmtReportFormat::findSystem(_documentType);

        if (printMgmtReportFormat.Name != this.getCurrentReportFormat(_documentType))
        {
            this.defaultPrintMgmtReportFormat();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentReportFormat</Name>
				<Source><![CDATA[
    private PrintMgmtReportFormatName getCurrentReportFormat(PrintMgmtDocumentType _documentType)
    {
        PrintMgmtReportFormatName formatName;

        if (_documentType == PrintMgmtDocumentType::CustPaymAdvice)
        {
            formatName = this.getCurrentCustReportFormat();
        }
        else if (_documentType == PrintMgmtDocumentType::VendPaymAdvice)
        {
            formatName = this.getCurrentVendReportFormat();
        }

        return formatName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentCustReportFormat</Name>
				<Source><![CDATA[
    private PrintMgmtReportFormatName getCurrentCustReportFormat()
    {
        return isReportV2Enabled ? BankPaymAdviceCustV2 : BankPaymAdviceCust;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentVendReportFormat</Name>
				<Source><![CDATA[
    private PrintMgmtReportFormatName getCurrentVendReportFormat()
    {
        return isReportV2Enabled ? BankPaymAdviceVendV2 : BankPaymAdviceVend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultPrintMgmtReportFormat</Name>
				<Source><![CDATA[
    /// <summary>
    /// Regenerates the print management system defaults for the given document type.
    /// </summary>
    /// <param name = "_documentType">The document type to force recreate.</param>
    /// <remarks>
    /// The delete action here will redirect any print management settings which leverage the system default.
    /// Existing links to non-default print management documents will be preserved.
    /// </remarks>
    private void defaultPrintMgmtReportFormat()
    {
        ttsbegin;

        this.deleteReportFormat(PrintMgmtDocumentType::CustPaymAdvice, BankPaymAdviceCust);
        this.deleteReportFormat(PrintMgmtDocumentType::CustPaymAdvice, BankPaymAdviceCustV2);

        this.deleteReportFormat(PrintMgmtDocumentType::VendPaymAdvice, BankPaymAdviceVend);
        this.deleteReportFormat(PrintMgmtDocumentType::VendPaymAdvice, BankPaymAdviceVendV2);

        PrintMgmtReportFormat::populate();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteReportFormat</Name>
				<Source><![CDATA[
    private void deleteReportFormat(PrintMgmtDocumentType _documentType, PrintMgmtReportFormatName _name)
    {
        PrintMgmtReportFormat printMgmtReportFormat;

        delete_from printMgmtReportFormat
            where printMgmtReportFormat.DocumentType == _documentType
                && printMgmtReportFormat.Description == _name;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>