<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventABC_ContributionMargin</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventABC_ContributionMargin extends InventABC
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getCategory</Name>
				<Source><![CDATA[
        protected ABC getCategory(InventTable _inventTable)
    {
        return _inventTable.abcContributionMargin;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setCategory</Name>
				<Source><![CDATA[
        protected void setCategory(
        InventTable _inventTable,
        ABC         _category)
    {
        _inventTable.abcContributionMargin = _category;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sumUpValue</Name>
				<Source><![CDATA[
        protected void sumUpValue()
    {
        InventTable             previousInventTable;

        Query                   query = new Query(itemQueryRun.query().pack());
        QueryBuildDataSource    qbdsItem = query.dataSourceTable(tableNum(InventTable));
        QueryBuildDataSource    qbdsTrans = qbdsItem.addDataSource(tableNum(CustInvoiceTrans));
        QueryBuildRange         itemTypeRange = SysQuery::findOrCreateRange(qbdsItem, fieldNum(InventTable, ItemType));

        AmountMST               contributionMarginMST;

        // ensure that results are sorted by item
        qbdsItem.addOrderByField(fieldNum(InventTable, ItemId));

        itemTypeRange.value(SysQuery::valueNot(queryValue(ItemType::Service)));

        qbdsTrans.relations(false);
        qbdsTrans.addLink(fieldNum(InventTable, ItemId), fieldNum(CustInvoiceTrans, ItemId));
        qbdsTrans.joinMode(JoinMode::OuterJoin);
        qbdsTrans.fetchMode(QueryFetchMode::One2One);

        // filter on dates
        qbdsTrans.addRange(fieldNum(CustInvoiceTrans, InvoiceDate)).value(SysQuery::range(fromDate, toDate));

        QueryRun marginQueryRun = new QueryRun(query);

        while (marginQueryRun.next())
        {
            if (marginQueryRun.changed(tableNum(InventTable)))
            {
                InventTable inventTable = marginQueryRun.get(tableNum(InventTable)) as InventTable;
                if (previousInventTable.ItemId)
                {
                    this.saveAmount(previousInventTable, contributionMarginMST);
                    contributionMarginMST = 0;
                }
                previousInventTable.data(inventTable);
            }

            if (marginQueryRun.changed(tableNum(CustInvoiceTrans)))
            {
                CustInvoiceTrans custInvoiceTrans = marginQueryRun.get(tableNum(CustInvoiceTrans)) as CustInvoiceTrans;
                contributionMarginMST += custInvoiceTrans.contributionMarginMST();
            }
        }
        if (previousInventTable.ItemId)
        {
            this.saveAmount(previousInventTable, contributionMarginMST);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static InventABC_ContributionMargin construct()
    {
        return new InventABC_ContributionMargin();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>