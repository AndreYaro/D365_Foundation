<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSShipmentFormPartForm</Name>
	<SourceCode>
		<Declaration><![CDATA[/// <summary>
/// The <c>WHSShipmentFormPartForm</c> class contains the business logic for <c>WHSShipmentFormPart</c> form.
/// </summary>
public class WHSShipmentFormPartForm
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildRange</Name>
				<Source><![CDATA[	/// <summary>
	/// Builds shipment range.
	/// </summary>
	/// <param name="_workArgs">
	/// Work record as argument.
	/// </param>
	/// <param name="_waveArgs">
	/// Wave record as argument.
	/// </param>
	/// <param name="_driverLogArgs">
	/// Driver log record as argument.
	/// </param>
	/// <returns>
	/// Range string containing shipment ids.
	/// </returns>
        public str buildRange(WHSWorkTable _workArgs,
                          WHSWaveTable _waveArgs,
                          TMSDriverLog _driverLogArgs)
    {
        WHSWorkLine         workLine;
        WHSLoadLine         loadLine;
        WHSWaveLine         waveLine;
        TMSAppointment      appointment;
        WHSLoadTable        loadTable;
        WHSShipmentTable    shipmentTable;

        str range = '';

        if (_workArgs.RecId)
        {
            while select workLine
                join loadLine
                group by loadLine.ShipmentId
                where loadLine.RecId        == workLine.LoadLineRefRecId &&
                      workLine.WorkId       == _workArgs.WorkId
            {
                if (range)
                {
                    range += ',' + loadLine.ShipmentId;
                }
                else
                {
                    range = loadLine.ShipmentId;
                }
            }
        }
        else if (_waveArgs.RecId)
        {
            while select waveLine
                group by waveLine.ShipmentId
                where waveLine.WaveId == _waveArgs.WaveId
            {
                if (range)
                {
                    range += ',' + waveLine.ShipmentId;
                }
                else
                {
                    range = waveLine.ShipmentId;
                }
            }
        }
        else if (_driverLogArgs.RecId)
        {
            while select ApptId, ApptRefType, ApptRefNum from appointment
                join LoadId from loadTable
                where appointment.ApptId        == _driverLogArgs.ApptId    &&
                      appointment.ApptRefType   == TMSApptRefType::Load     &&
                      appointment.ApptRefNum    == loadTable.LoadId
                    join ShipmentId, LoadId from shipmentTable
                    where shipmentTable.LoadId == loadTable.LoadId
            {
                if (range)
                {
                    range += ',' + shipmentTable.ShipmentId;
                }
                else
                {
                    range = shipmentTable.ShipmentId;
                }
            }
        }

        return range;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
