<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EFiscalDocumentReceivedXmlValidation_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains validations specific to received electronic fiscal documents that were issued by third parties
/// </summary>
class EFiscalDocumentReceivedXmlValidation_BR extends FiscalDocumentValidationBase_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkFiscalEstablishmentAgainstXml</Name>
				<Source><![CDATA[
        private boolean checkFiscalEstablishmentAgainstXml(FiscalEstablishment_BR _fiscalEstablishment, EFDocumentReceivedXml_BR _receivedXml)
    {
        if (_fiscalEstablishment.RecId != _receivedXml.FiscalEstablishment_BR)
        {
            logger.logError(strFmt("@SYS4003249", _fiscalEstablishment.FiscalEstablishmentId, _receivedXml.getFiscalEstablishmentId()));
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkXmlIsValidatedAtSefaz</Name>
				<Source><![CDATA[
        private boolean checkXmlIsValidatedAtSefaz(EFDocumentReceivedXML_BR _receivedXml)
    {
        if (!_receivedXml.RecId)
        {
            logger.logError("@SYS4003324");
            return false;
        }

        if (_receivedXml.InquiryStatus == FiscalDocumentStatus_BR::Created)
        {
            logger.logError(strFmt("@SYS4003098", _receivedXml.Accesskey));
            return false;
        }

        if (_receivedXml.InquiryStatus != FiscalDocumentStatus_BR::Approved && _receivedXml.InquiryStatus != FiscalDocumentStatus_BR::Created)
        {
            logger.logError(strFmt("@SYS4003209", this.parmFiscalDocumentParmData().parmFiscalDocumentAccessKey(), _receivedXml.InquiryStatus));
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isIssuedByThirdParty</Name>
				<Source><![CDATA[
        private boolean isIssuedByThirdParty()
    {
        return this.parmFiscalDocumentParmData().parmFiscalDocumentIssuer() == FiscalDocumentIssuer_BR::ThirdParty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldCheckFiscalValues</Name>
				<Source><![CDATA[
        private boolean shouldCheckFiscalValues(EFDocumentReceivedXml_BR _receivedXml)
    {
        return _receivedXml.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldCheckIfXmlIsApprovedFor</Name>
				<Source><![CDATA[
        private boolean shouldCheckIfXmlIsApprovedFor(FiscalEstablishment_BR _fiscalEstablishment)
    {
        return _fiscalEstablishment.EFDocBlockPostingNotValidatedXml == NoYes::Yes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateHeader</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the header of the NF-e, if necessary.
    /// </summary>
    /// <returns>
    /// True if validation is successful; otherwise, false.
    /// </returns>
    [
        FiscalDocumentValidationAttribute,
        FiscalDocumentValidationElementAttribute(FiscalDocumentElement_BR::Header)
    ]
    public boolean validateHeader()
    {
        if (this.isIssuedByThirdParty())
        {
            return this.validateThirdPartyNfeAgainstReceivedXml();
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateThirdPartyNfeAgainstReceivedXml</Name>
				<Source><![CDATA[
        private boolean validateThirdPartyNfeAgainstReceivedXml()
    {
        boolean                     ok = true;
        FiscalEstablishment_BR      fiscalEstablishment;
        EFDocumentReceivedXml_BR    receivedXml;

        select firstOnly EFDocBlockPostingNotValidatedXml
            from fiscalEstablishment
                where fiscalEstablishment.FiscalEstablishmentId == this.parmFiscalDocumentParmData().parmFiscalEstablishmentID();

        select firstOnly AccessKey, CNPJNum_BR, FiscalEstablishment_BR, InquiryStatus
            from receivedXml
                where receivedXml.Accesskey == this.parmFiscalDocumentParmData().parmFiscalDocumentAccessKey();

        if (this.shouldCheckIfXmlIsApprovedFor(fiscalEstablishment))
        {
            ok = this.checkXmlIsValidatedAtSefaz(receivedXml) && ok;
        }

        if (this.shouldCheckFiscalValues(receivedXml))
        {
            ok = this.checkFiscalEstablishmentAgainstXml(fiscalEstablishment, receivedXml) && ok;
        }

        return ok;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>