<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceJourUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustInvoiceJourUIBuilder</c> class builds the UI for the <c>CustInvoiceJour</c> SSRS report parameters.
/// </summary>
public class CustInvoiceJourUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField billingClassificationField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>billingClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a Billing Classification lookup.
    /// </summary>
    public void billingClassificationLookup()
    {
        Query       query               = new Query(queryStr(CustBillingClassificationLookup));
        TableId     multiSelectTableNum = tableNum(CustBillingClassification);
        container   selectedFields      = [multiSelectTableNum, fieldName2id(multiSelectTableNum, fieldStr(CustBillingClassification, BillingClassification))];

        SysLookupMultiSelectCtrl::constructWithQuery(this.dialog().dialogForm().formRun(), billingClassificationField.control(), query, false, selectedFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// The <c>CustInvoiceJourUIBuilder</c> class builds the UI for the <c>CustInvoiceJour</c> SSRS report parameters.
    /// </summary>
    public void build()
    {
        boolean                 isBillingClassEnabled;
        DialogField             inclTransWithNoBillingClassField;
        CustInvoiceJourContract custInvoiceJourContract;

        super();

        custInvoiceJourContract = this.dataContractObject() as CustInvoiceJourContract;

        billingClassificationField          = this.bindInfo().getDialogField(custInvoiceJourContract, methodStr(CustInvoiceJourContract, parmBillingClassification));
        inclTransWithNoBillingClassField    = this.bindInfo().getDialogField(custInvoiceJourContract, methodStr(CustInvoiceJourContract, parmInclTransWithNoBillingClass));

        // controlling visibility
        if (billingClassificationField && inclTransWithNoBillingClassField)
        {
            isBillingClassEnabled = CustParameters::isBillingClassificationEnabled();

            billingClassificationField.visible(isBillingClassEnabled);
            inclTransWithNoBillingClassField.visible(isBillingClassEnabled);
            billingClassificationField.lookupButton(FormLookupButton::Always);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implements the functionality that is supposed to execute after run.
    /// </summary>
    public void postRun()
    {
        if (CustParameters::isBillingClassificationEnabled())
        {
            this.billingClassificationLookup();
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>