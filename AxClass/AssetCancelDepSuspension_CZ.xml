<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetCancelDepSuspension_CZ</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The AssetCancelDepSuspension_CZ class deletes the suspension and restores
///     the last depreciation date and depreciation start date.
/// </summary>
class AssetCancelDepSuspension_CZ extends RunBase
{
    AssetDepSuspension_CZ assetDepSuspension_CZ;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialog;

        dialog = super();
        dialog.addText("@GLS112902");
        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    public void initParameters(Args _args)
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceCZ00005, funcName());

        assetDepSuspension_CZ = _args.record() as AssetDepSuspension_CZ;

        if (assetDepSuspension_CZ == null)
        {
            throw error(Error::missingRecord(tableStr(AssetDepSuspension_CZ)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        AssetBook assetBook;

        ttsBegin;

        super();

        assetBook = AssetBook::find(assetDepSuspension_CZ.AssetId, assetDepSuspension_CZ.BookId, true);
        if (assetBook != null)
        {
            assetBook.DepreciationStartDate = assetDepSuspension_CZ.DepreciationStartDate;
            assetBook.LastDepreciationDate = assetDepSuspension_CZ.LastDepreciationDate;
            assetBook.update();

            assetDepSuspension_CZ.selectForUpdate(true);
            assetDepSuspension_CZ.delete();
        }

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean isValid;

        isValid = super(_calledFrom);

        if (!assetDepSuspension_CZ)
        {
            return false;
        }

        if (AssetDepSuspension_CZ::findNextSuspensionDate(assetDepSuspension_CZ) != null)
        {
            isValid = checkFailed("@GLS112903");
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static Name description()
    {
        return "@GLS112896";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        AssetCancelDepSuspension_CZ cancelDepSuspension;

        cancelDepSuspension = new AssetCancelDepSuspension_CZ();
        cancelDepSuspension.initParameters(_args);
        if (cancelDepSuspension.prompt())
        {
            cancelDepSuspension.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>