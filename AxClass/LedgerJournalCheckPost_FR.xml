<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalCheckPost_FR</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LedgerJournalCheckPost_FR
{
    #ISOCountryRegionCodes

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isDerogatoryTaxJournal</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Verifies whether the value model specified on the transaction is a derogatory tax model.
    /// </summary>
    /// <param name="_ledgerJournalTrans">
    ///    The journal transaction object for which verification for derogatory tax model will be done.
    /// </param>
    /// <returns>
    ///    true if the transaction is derogatory tax model; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    This method is used for French derogatory depreciation processing.
    /// </remarks>
    public static boolean isDerogatoryTaxJournal(LedgerJournalTrans _ledgerJournalTrans)
    {
        boolean                     retValue = false;
        LedgerJournalTrans_Asset    ledgerJournalTrans_Asset;

        if (_ledgerJournalTrans.AccountType == LedgerJournalACType::FixedAssets
            && SysCountryRegionCode::isLegalEntityInCountryRegion([#isoFR]))
        {
            // This is a fixed assets transaction and the appropriate configuration key is enabled.

            ledgerJournalTrans_Asset = _ledgerJournalTrans.findLedgerJournalTransAssetSibling();

            if (!_ledgerJournalTrans.Company)
            {
                _ledgerJournalTrans.Company = curext();
            }
            else if (!xDataArea::exist(_ledgerJournalTrans.Company))
            {
                throw warning(strFmt("@SYS10666", _ledgerJournalTrans.Company));
            }

            changecompany (_ledgerJournalTrans.Company)
            {
                if (AssetBookTable::find(ledgerJournalTrans_Asset.BookId).IsDerogatoryBook_FR)
                {
                    retValue = true;
                }
            }
        }

        return retValue;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>