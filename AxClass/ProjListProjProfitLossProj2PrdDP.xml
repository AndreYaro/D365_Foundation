<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjProfitLossProj2PrdDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjProfitLossProj2PrdDP</c> class provides data for the
///    <c>ProjListProjProfitLossProj2ColumnPeriod</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjProfitLossProjContract))
]
class ProjListProjProfitLossProj2PrdDP extends SrsReportDataProviderPreProcessTempDB
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    ProjOptionRevenue       revenue;
    ProjOptionOnAcc         onAcc;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    TransDate               ledgerFromDate1;
    TransDate               ledgerToDate1;
    ProjListColumnData      actualVsBudget;
    ForecastModelId         forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;

    ProjListProjTrans_ProfitLoss    listProjTrans;
    projListProjTrans2Period        listProjTrans2Period;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjHelpClass           period1ProjHelpClass, period2ProjHelpClass;
    ProjListProjProfitLossTmp       projListProjProfitLossTmp;

    ProjTable               projTable;

    QueryRun                queryRun;

    ProjId                  projIdFound, projId;
    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    int                     showQtyHour, showTotalCost, showRevenue, showContributionMargin,
                            hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals;
    str                     totalCostLabel, period1Label, period2Label, headerLabel;
    AmountMST               period1TotalCost, period2TotalCost, period1Revenue, period2Revenue,
                            period1HourQty, period2HourQty, period1Margin, period2Margin;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculate the amounts that are needed for the report.
    /// </summary>
    void calculate()
    {
        period1ProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        period2ProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        period1ProjHelpClass.setParmField();
        period2ProjHelpClass.setParmField();

        if (amountQty == ProjAmountQty::Qty || amountQty ==ProjAmountQty::AmountQty)
        {
            period1HourQty     += period1ProjHelpClass.parmProjStatement().a310_Cost_Hour_Qty(false,ProjListStateType::ProfitLoss);
            period2HourQty     += period2ProjHelpClass.parmProjStatement().a310_Cost_Hour_Qty(false,ProjListStateType::ProfitLoss);
        }

        period1TotalCost   += period1ProjHelpClass.parmProjStatement().a300_Total_Cost(false,ProjListStateType::ProfitLoss);
        period1Revenue     += period1ProjHelpClass.parmProjStatement().a100_Total_Revenue();
        period1Margin      += period1ProjHelpClass.parmProjStatement().a500_Gross_Margin();

        period2ProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        period2ProjHelpClass.setParmField();

        period2TotalCost   += period2ProjHelpClass.parmProjStatement().a300_Total_Cost(false,ProjListStateType::ProfitLoss);
        period2Revenue     += period2ProjHelpClass.parmProjStatement().a100_Total_Revenue();
        period2Margin      += period2ProjHelpClass.parmProjStatement().a500_Gross_Margin();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks the parameters to determine if a line should be printed.
    /// </summary>
    /// <returns>
    ///    true if the line should be printed; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(period1ProjHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should be printed and inserts it into the temporary table.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record buffer that contains the information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (listProjTransLayout.parmStateProjType() == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= listProjTransLayout.parmLevel()) ||
                (projTableLocal.levelFormat(listProjTransLayout.parmLevel())   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    period1ProjHelpClass.setName();
                    this.insertProjListProjProfitLossTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            period1ProjHelpClass.setName();
            this.insertProjListProjProfitLossTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the data from the <c>projListProjProfitLossTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>projListProjProfitLossTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('projListProjProfitLossTmp')]
    public ProjListProjProfitLossTmp getProjListProjProfitLossTmp()
    {
        select * from projListProjProfitLossTmp;

        return projListProjProfitLossTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the <c>ProjListProjTrans</c> class by using the parameters.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans2Period = projListProjTrans2Period::construct(ProjListStateType::ProfitLoss, ProjListStateId::Project);
        listProjTrans = listProjTrans2Period.getProjListProjTrans1();
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans2Period.getProjListProjTrans1());

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);
        listProjTrans.parmTime(time);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);
        listProjTrans.parmRevenue(revenue);
        listProjTrans.parmOnAcc(onAcc);

        listProjTrans2Period.parmPeriod1FromDate(ledgerFromDate);
        listProjTrans2Period.parmPeriod1ToDate(ledgerToDate);
        listProjTrans2Period.parmPeriod2FromDate(ledgerFromDate1);
        listProjTrans2Period.parmPeriod2ToDate(ledgerToDate1);

        listProjTrans.parmColumnData(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjProfitLossTmp()
    {
        projListProjProfitLossTmp.clear();

        projListProjProfitLossTmp.Id            = period1ProjHelpClass.id();
        projListProjProfitLossTmp.IdLabel       = enum2str(stateProjType);
        projListProjProfitLossTmp.Name          = period1ProjHelpClass.name();

        projListProjProfitLossTmp.QtyHour       = period1HourQty;
        projListProjProfitLossTmp.QtyHour1      = period2HourQty;

        projListProjProfitLossTmp.TotalCost     = listProjTransLayout.showAmountReport(period1TotalCost, showAmount);
        projListProjProfitLossTmp.TotalCost1    = listProjTransLayout.showAmountReport(period2TotalCost, showAmount);

        projListProjProfitLossTmp.Revenue       = listProjTransLayout.showAmountReport(period1Revenue, showAmount);
        projListProjProfitLossTmp.Revenue1      = listProjTransLayout.showAmountReport(period2Revenue, showAmount);

        projListProjProfitLossTmp.ContributionMargin = listProjTransLayout.showAmountReport(period1Margin, showAmount);
        projListProjProfitLossTmp.ContributionMargin1= listProjTransLayout.showAmountReport(period2Margin, showAmount);

        // set labels
        projListProjProfitLossTmp.HeaderLabel     = headerLabel;
        projListProjProfitLossTmp.PeriodLabel     = period1Label;
        projListProjProfitLossTmp.PeriodLabel1    = period2Label;
        projListProjProfitLossTmp.TotalCostLabel  = totalCostLabel;

        // set decimal values
        projListProjProfitLossTmp.DecimalHourQty     = hourQtyDecimals;
        projListProjProfitLossTmp.DecimalValueAdded  = valueAddedDecimals;
        projListProjProfitLossTmp.DecimalGrossMargin = grossMarginDecimals;
        projListProjProfitLossTmp.DecimalAmount      = amountDecimals;

        // set field visibility
        projListProjProfitLossTmp.ShowContributionMargin = showContributionMargin;
        projListProjProfitLossTmp.ShowQtyHour            = showQtyHour;
        projListProjProfitLossTmp.ShowRevenue            = showRevenue;
        projListProjProfitLossTmp.ShowTotalCost          = showTotalCost;

        projListProjProfitLossTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks whether the amounts are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise, false.
    /// </returns>
    boolean lineValue()
    {
        return (period1TotalCost != 0) ||
               (period1Revenue   != 0) ||
               (period2TotalCost != 0) ||
               (period2Revenue   != 0) ||
               (period1HourQty   != 0) ||
               (period2HourQty   != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = queryRun.query().dataSourceTable(tableNum(ProjTable));
        qbdsProjTable.addSortField(fieldNum(ProjTable, ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldNum(ProjTable, Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = queryRun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
            PSAContractLineItemRangesHelper::addTransactionTypeRangesToDS(
                qbdsPSAContractLineItems,
                hour,
                expenses,
                revenue,
                item,
                false);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Internal));
            }

            if (time)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable, Type)).value(queryValue(ProjType::Time));
            }

            if (!(timeMaterial || fixedPrice || investment || internalCost || time))
            {
                QueryBuildRange rangeDummy = qbdsProjTable.addRange(fieldNum(ProjTable, Type));
                rangeDummy.value(queryValue(SysQuery::valueEmptyString()));
            }
        }

        if (skipZero)
        {
            QueryBuildDataSource qbdsProjectTrans;

            if (actualVsBudget == ProjListColumnData::Actual)
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransPosting)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransPosting, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod1FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod1ToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod2FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod2ToDate()));
            }
            else
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransBudget)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransBudget, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod1FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod1ToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod2FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod2ToDate()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = queryRun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = queryRun.get(tablenum(ProjFundingSource));
        }
        period1ProjHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);

        if (! listProjTransLayout.canPrintId(period1ProjHelpClass.idNew()))
        {
            period1ProjHelpClass.setOld();
            return;
        }

        if (! firstRun && period1ProjHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        period1ProjHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(listProjTransLayout.parmLevel());
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SQL
    ///    Server Reporting Services (SSRS) at run time. This method should compute the data and populate the
    ///    data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjProfitLossProjContract contract = this.parmDataContract() as ProjListProjProfitLossProjContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();
        time = contract.parmTime();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();
        revenue = contract.parmRevenue();
        onAcc = contract.parmOnAcc();

        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();
        ledgerFromDate1 = contract.parmLedgerFromDate1();
        ledgerToDate1 = contract.parmLedgerToDate1();

        actualVsBudget = contract.parmActualVsBudget();
        forecastModelId = contract.parmForecastModelIdV2();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();

        firstRun = true;

        this.initProjListTrans();

        totalCostLabel = listProjTrans.setDisplayLabel2PeriodColumn(hour, expenses, item);
        [showQtyHour, showTotalCost, showRevenue, showContributionMargin] = listProjTrans.setDisplayFlags2PeriodColumn(hour, expenses, item, revenue, onAcc, amountQty);

        [hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        period1Label = listProjTrans2Period.formatPeriodLabel("@SYS9308",1, DateFlags::None);
        period2Label = listProjTrans2Period.formatPeriodLabel("@SYS13183",2, DateFlags::None);
        headerLabel = listProjTrans2Period.formatSelection(showAmount);

        period1ProjHelpClass       = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newProjListProfitLoss(
                                        listProjTrans2Period.getProjListProjTrans1(),
                                        stateProjType,
                                        projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                                        amountQty,
                                        forecastModelId),
                                    stateProjType);

        period2ProjHelpClass       = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newProjListProfitLoss(
                                        listProjTrans2Period.getProjListProjTrans2(),
                                        stateProjType,
                                        projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                                        amountQty,
                                        forecastModelId),
                                    stateProjType);

        queryRun = new SysQueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (queryRun.next())
        {
            this.printProject();
            firstRun    = false;
        }

        if (this.checkPrintLine())
        {
            period1ProjHelpClass.setName();
            this.insertProjListProjProfitLossTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the variables to zero.
    /// </summary>
    void setFieldZero()
    {
        period1TotalCost   = 0;
        period1Revenue     = 0;
        period1Margin      = 0;
        period1HourQty     = 0;

        period2TotalCost   = 0;
        period2Revenue     = 0;
        period2Margin      = 0;
        period2HourQty     = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        queryRun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>