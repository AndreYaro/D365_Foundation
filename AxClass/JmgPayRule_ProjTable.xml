<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayRule_ProjTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgPayRule_ProjTable</c> class represents the pay rule of the project.
/// </summary>
/// <remarks>
///    This class lets users delimit the pay agreement lines by projects.
/// </remarks>
class JmgPayRule_ProjTable extends JmgPayRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the description of the pay rule of the project.
    /// </summary>
    /// <returns>
    ///    The description of the pay rule of the project.
    /// </returns>
    public JmgDescription description()
    {
        return "@SYS36368";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDesignTimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the design time data source for the pay rule of the project.
    /// </summary>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the design time data source.
    /// </returns>
    /// <remarks>
    ///    The pay rule of the project adds a <c>ProjId</c> field to the custom design data source.
    /// </remarks>
    protected ExpressionDataSource getDesignTimeCustomDataSource()
    {
        ExpressionDataSource ret;

        ret = super();
        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(ProjId));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJoinConditions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves an expression with conditions for the Projects pay rule
    /// </summary>
    /// <returns>
    ///    An expression with conditions for the Projects pay rule.
    /// </returns>
    /// <remarks>
    ///    The Projects pay rule adds one condition: the <c>ProjId</c> field equals the <c>Query.ProjId</c>
    ///    value.
    /// </remarks>
    public Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement getJoinConditions()
    {
        Query q = this.getQuery();

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionProjection designTimeProjection =
            SysExpression::getDesignTimeProjectionModel(classstr(JmgPayRule_ProjTable), this.getDesignTimeCustomDataSources());

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement expressionDefinition =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement::Create();

        Microsoft.Dynamics.AX.Framework.Expression.Model.Field projTableField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionProjTableField;

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement andElement =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement::Create();

        projTableField = designTimeProjection.GetField(q.dataSourceTable(tablenum(ProjTable)).name(), fieldstr(ProjTable, ProjId));
        transactionProjTableField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(ProjId));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldStringElement::Create(
                projTableField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.StringOperator::Equals,
                transactionProjTableField));

        expressionDefinition.AddAndElement(andElement);

        return expressionDefinition;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the name of the <c>JmgPayRuleProjTable</c> query.
    /// </summary>
    /// <returns>
    ///    The name of the <c>JmgPayRuleProjTable</c> query.
    /// </returns>
    queryName getQueryName()
    {
        return querystr(JmgPayRuleProjTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRuntimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the runtime custom time data source for the pay rule of the project.
    /// </summary>
    /// <param name="_jmgStampJournalTrans">
    ///    The transaction from where the values of the runtime fields are initialized.
    /// </param>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the run time data source for the pay rule of
    ///    the project.
    /// </returns>
    /// <remarks>
    ///    Adds one run time field: the <c>ProjId</c> field that is initialized from the <c>JobRef</c> field
    ///    of the transaction.
    /// </remarks>
    protected ExpressionDataSource getRuntimeCustomDataSource(JmgStampJournalTrans _jmgStampJournalTrans)
    {
        ExpressionDataSource expressionDataSource;

        expressionDataSource = super(_jmgStampJournalTrans);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(ProjId), _jmgStampJournalTrans.JobRef);
        return expressionDataSource;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>