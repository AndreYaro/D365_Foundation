<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailCouponCopy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>RetailCouponCopy</c> copies coupons.
/// </summary>
class RetailCouponCopy
{
    RetailOfferIdEx3                retailPeriodicDiscountOfferId;
    RetailCouponNumber              origRetailCouponNumber;
    RetailCouponNumber              newRetailCouponNumber;
    RetailDescriptionEx2            couponDesc;
    boolean                         copyAttributes;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyCoupon</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new <c>RetailCoupon</c> record that is a
    /// copy of the record used to instantiate this class.
    /// </summary>
    /// <exception cref="Exception::Error">
    /// Throw error when validateWrite fails.
    /// </exception>
    public void copyCoupon()
    {
        RetailCoupon       newCoupons;

        // Create the new coupon
        ttsBegin;

        newCoupons.CouponNumber = newRetailCouponNumber;
        newCoupons.Description = couponDesc;
        newCoupons.DiscountOfferId = retailPeriodicDiscountOfferId;
        
        if (newCoupons.validateWrite())
        {
            newCoupons.insert();
        }
        else
        {
            throw error("@SYS104318");
        }

        ttsCommit;

        // Copy the coupon attributes
        if (this.parmCopyAttributes())
        {
            this.copyCouponValueAttr();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyCouponValueAttr</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new <c>RetailCoupon</c> record that is a copy
    /// of the record that has the value in the origCouponId calss
    /// variable.
    /// </summary>
    /// <exception cref="Exception::Error">
    /// Throw error when validateWrite fails.
    /// </exception>
    public void copyCouponValueAttr()
    {
        int  counter = 0;
        RetailCoupon          newCoupon, originalCoupon;
        RetailCouponCodeTable origRetailCouponCodeTable, newRetailCouponCodeTable;
        RecordInsertList      retailCouponCodeTableList;
        RetailCouponCodeId    newCouponCodeId = '';

        ttsBegin;

        originalCoupon = RetailCoupon::find(origRetailCouponNumber);

        newCoupon = RetailCoupon::find(newRetailCouponNumber, true);
                             
        newCoupon.Status                 =    originalCoupon.Status;
        newCoupon.ValidFrom              =    originalCoupon.ValidFrom;
        newCoupon.ValidTo                =    originalCoupon.ValidTo;
        newCoupon.UsageLimit             =    originalCoupon.UsageLimit;
        newCoupon.UsageLimitType         =    originalCoupon.UsageLimitType;
        newCoupon.IsCustomerRequired     =    originalCoupon.IsCustomerRequired;
        newCoupon.IsExclusive            =    originalCoupon.IsExclusive;
        newCoupon.ApplyWithoutCouponCode =    originalCoupon.ApplyWithoutCouponCode;

        if (newCoupon.validateWrite())
        {
            newCoupon.update();
        }
        else
        {
            throw error("@SYS104318");
        }
          
        retailCouponCodeTableList = new RecordInsertList(tableNum(RetailCouponCodeTable));

        // Copy data for RetailCouponCodeTable as per current RetailCoupon data.
        while select origRetailCouponCodeTable
                    where origRetailCouponCodeTable.CouponNumber == origRetailCouponNumber
        {            
            newRetailCouponCodeTable.data(origRetailCouponCodeTable);

            newCouponCodeId = RetailCouponCodeTable::GetNextCouponCodeId();

            newRetailCouponCodeTable.CouponCodeId = newCouponCodeId;

            newRetailCouponCodeTable.CouponNumber = newRetailCouponNumber;

            if (counter == 0)
            {
                newRetailCouponCodeTable.CouponCode = newRetailCouponNumber;
            }
            else
            {
                newRetailCouponCodeTable.CouponCode = newRetailCouponNumber + '_' + int2Str(counter);
            }
            
            newRetailCouponCodeTable.BarCode = RetailCouponHelper::createBarCode(newRetailCouponCodeTable.CouponCode);

            counter++;

            retailCouponCodeTableList.add(newRetailCouponCodeTable);
        }

        retailCouponCodeTableList.insertDatabase();
       
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new object of <c>RetailCouponCopy</c> class, setting
    /// class variables to the passed values.
    /// </summary>
    /// <param name="_origRetailCouponNumber">
    /// Coupon ID of the current <c>RetailCoupon</c> record.
    /// </param>
    /// <param name="_mcrCouponNumber">
    /// Coupon number of the current <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_couponDesc">
    /// Coupon description of the current <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_retailPeriodicDiscountOfferId">
    /// Coupon associated discountId of the current <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_copyAttributes">
    /// A <c>boolean</c> value used to determine whether or not to copy the attributes
    /// for the current coupon to the new one.
    /// </param>
    public void new(RetailCouponNumber                  _origRetailCouponNumber,
                    RetailDescriptionEx2                _couponDesc,
                    RetailOfferIdEx3                    _retailPeriodicDiscountOfferId,
                    boolean                             _copyAttributes)
    {
        this.parmCopyAttributes(_copyAttributes);
        this.parmCouponNumber(_origRetailCouponNumber);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyAttributes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set value for parmCopyAttributes.
    /// </summary>
    /// <param name="_copyAttributes">
    /// Coupon attributes value.
    /// </param>
    public boolean parmCopyAttributes (boolean _copyAttributes = copyAttributes)
    {
        copyAttributes = _copyAttributes;
        return  copyAttributes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCouponDesc</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set value for parmCouponDesc.
    /// </summary>
    /// <param name="_couponDesc">
    /// Coupon Description value.
    /// </param>
    public RetailDescriptionEx2 parmCouponDesc (RetailDescriptionEx2 _couponDesc = couponDesc)
    {
        couponDesc = _couponDesc;
        return couponDesc;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCouponNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set value for parmCouponNumber.
    /// </summary>
    /// <param name="_origRetailCouponNumber">
    /// Existing Coupon number value.
    /// </param>
    public RetailCouponNumber parmCouponNumber (RetailCouponNumber _origRetailCouponNumber = origRetailCouponNumber)
    {
        origRetailCouponNumber = _origRetailCouponNumber;
        return origRetailCouponNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNewCouponNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set value for parmNewCouponNumber.
    /// </summary>
    /// <param name="_newRetailCouponNumber">
    /// New Coupon number value.
    /// </param>
    public RetailCouponNumber parmNewCouponNumber(RetailCouponNumber _newRetailCouponNumber = newRetailCouponNumber)
    {
        newRetailCouponNumber = _newRetailCouponNumber;
        return _newRetailCouponNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNewRetailPeriodicDiscountOfferId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set value for parmNewRetailOfferIdEx3.
    /// </summary>
    /// <param name="_copyAttributes">
    /// Coupon discount value.
    /// </param>
    public RetailOfferIdEx3 parmNewRetailPeriodicDiscountOfferId (RetailOfferIdEx3 _retailPeriodicDiscountOfferId = retailPeriodicDiscountOfferId)
    {
        retailPeriodicDiscountOfferId = _retailPeriodicDiscountOfferId;
        return retailPeriodicDiscountOfferId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>