<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSupplyRiskSelectPeriodModel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendSupplyRiskSelectPeriodModel</c> class models the input of a period using ledger period codes.
/// </summary>
internal final class VendSupplyRiskSelectPeriodModel
{
    private VendSupplyRiskSelectPeriodState periodState;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setState</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the state of the model.
    /// </summary>
    /// <param name = "_vendSupplyRiskSelectPeriodState">The <c>VendSupplyRiskSelectPeriodState</c> class instance.</param>
    internal void setState(VendSupplyRiskSelectPeriodState _vendSupplyRiskSelectPeriodState)
    {
        periodState = _vendSupplyRiskSelectPeriodState;
        this.notifyDateCodeChange(_vendSupplyRiskSelectPeriodState.parmDateCode());
        this.notifyFromDateChange(_vendSupplyRiskSelectPeriodState.parmFromDate());
        this.notifyToDateChange(_vendSupplyRiskSelectPeriodState.parmToDate());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getState</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the state of the model.
    /// </summary>
    /// <returns>The <c>VendSupplyRiskSelectPeriodState</c> class instance.</returns>
    internal VendSupplyRiskSelectPeriodState getState()
    {
        return periodState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDateCode</Name>
				<Source><![CDATA[
    internal DateCode parmDateCode(DateCode _dateCode = periodState.parmDateCode())
    {
        if (!prmisDefault(_dateCode))
        {
            periodState.parmDateCode(_dateCode);

            this.notifyDateCodeChange(this.parmDateCode());
            this.notifyFromDateChange(this.parmFromDate());
            this.notifyToDateChange(this.parmToDate());
        }

        return periodState.parmDateCode();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFromDate</Name>
				<Source><![CDATA[
    internal FromDate parmFromDate(FromDate _fromDate = periodState.parmFromDate())
    {
        if (!prmisDefault(_fromDate))
        {
            periodState.parmFromDate(_fromDate);

            this.notifyDateCodeChange(this.parmDateCode());
            this.notifyFromDateChange(this.parmFromDate());
        }

        return periodState.parmFromDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmToDate</Name>
				<Source><![CDATA[
    internal ToDate parmToDate(ToDate _toDate = periodState.parmToDate())
    {
        if (!prmisDefault(_toDate))
        {
            periodState.parmToDate(_toDate);

            this.notifyDateCodeChange(this.parmDateCode());
            this.notifyToDateChange(this.parmToDate());
        }

        return periodState.parmToDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the period is valid.
    /// </summary>
    internal void validate()
    {
        if (!this.parmToDate())
        {
            this.parmToDate(dateMax());
        }

        if (this.parmFromDate() > this.parmToDate())
        {
            throw error("@SYS91020");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyDateCodeChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Notifies that the <c>DateCode</c> in the model is changed.
    /// </summary>
    /// <param name = "_dateCode">The new value of the <c>DateCode</c>.</param>
    delegate void notifyDateCodeChange(DateCode _dateCode)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyFromDateChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Notifies that the <c>FromDate</c> in the model is changed.
    /// </summary>
    /// <param name = "_fromDate">
    /// The new value of the <c>FromDate</c>.
    /// </param>
    delegate void notifyFromDateChange(FromDate _fromDate)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyToDateChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Notifies that the <c>ToDate</c> in the model is changed.
    /// </summary>
    /// <param name = "_toDate">The new value of the <c>ToDate</c>.</param>
    delegate void notifyToDateChange(ToDate _toDate)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static VendSupplyRiskSelectPeriodModel construct()
    {
        return new VendSupplyRiskSelectPeriodModel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        periodState = VendSupplyRiskSelectPeriodState::construct();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>