<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMixedLicensePlateInfoPurchaseOrderCalculator</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSMixedLicensePlateInfoPurchaseOrderCalculator extends WHSMixedLicensePlateInfoCalculator
{
    private WHSLicensePlateId mixedLicensePlateId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromMixedLicensePlateId</Name>
				<Source><![CDATA[
    internal static WHSMixedLicensePlateInfoPurchaseOrderCalculator newFromMixedLicensePlateId(WHSLicensePlateId _mixedLicensePlateId)
    {
        WHSMixedLicensePlateInfoPurchaseOrderCalculator calculator = new WHSMixedLicensePlateInfoPurchaseOrderCalculator();
        calculator.mixedLicensePlateId = _mixedLicensePlateId;

        return calculator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateLicensePlateInfo</Name>
				<Source><![CDATA[
    internal List calculateLicensePlateInfo()
    {
        List licensePlateInfo = new List(Types::String);

        WHSMixedLicensePlateReceiptLine mixedLPLines;

        while select InventTransId, InventTransType, LineNum, InventQty, CapturedWeight, InventDimId from mixedLPLines
            order by LineNum
            where mixedLPLines.MixedLPReceivingTable == WHSMixedLicensePlateReceipt::findOpenByLicensePlateId(mixedLicensePlateId).RecId
        {
            var orderProvider = this.getOrderProviderFromMixedLPLine(mixedLPLines);
            WHSReceiptOrderLine orderLine = orderProvider.findInventTransId(mixedLPLines.InventTransId);

            boolean isCWItem = PdsGlobal::pdsIsCWItem(orderLine.ItemId);
            
            str lpInfo = strFmt('%1\n%2\n%3\n%4\n%5%6\n%7',
                                strFmt("@WAX3192", "@SYS70473", mixedLPLines.LineNum),
                                strFmt("@WAX3192", orderProvider.orderFieldLabel(), orderLine.OrderNumber),
                                strFmt("@WAX3192", "@WAX1240", orderLine.ItemId),
                                strFmt("@WAX3192", orderProvider.lineQtyFieldLabel(), isCWItem ? orderLine.PdsCWQty : orderLine.InventQty),
                                strFmt("@WAX3192", "@WAX:LPQty", mixedLPLines.InventQty),
                                isCWItem ? strFmt('\n%1\n%2',
                                                    strFmt("@WAX3192", "@SYS153282", WHSCatchWeightHelper::inventHandlingUnitId(orderLine.ItemId)),
                                                    strFmt("@WAX3192", "@WAX:WeightCaptured", mixedLPLines.CapturedWeight)) : '',
                                strFmt("@WAX3192", "@SYS153282", InventTableModule::find(orderLine.ItemId, ModuleInventPurchSales::Invent).UnitId));

            lpInfo = lpInfo + this.calculateProductAndTrackingDimensionsInfo(mixedLPLines.InventDimId);

            licensePlateInfo.addEnd(lpInfo);
        }

        return licensePlateInfo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOrderProviderFromMixedLPLine</Name>
				<Source><![CDATA[
    private WHSWorkExecuteDisplayReceivingOrderProvider getOrderProviderFromMixedLPLine(WHSMixedLicensePlateReceiptLine _mixedLPLine)
    {
        var orderProvider = WHSWorkExecuteDisplayReceivingOrderProvider::newFromInventTransType(_mixedLPLine.InventTransType);
        return orderProvider ? orderProvider : new WHSWorkExecuteDisplayReceivingPurchOrderProvider();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>