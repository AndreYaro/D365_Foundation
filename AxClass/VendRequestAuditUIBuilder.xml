<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendRequestAuditUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendRequestAuditUIBuilder</c> class is the UI builder class of the <c>VendRequestAudit</c>
/// report.
/// </summary>
class VendRequestAuditUIBuilder extends SrsReportDataContractUIBuilder
{
    VendRequestAuditContract        rdpContract;
    VendTmpCompanyInfo              vendTmpCompanyInfo;

    DialogField                     companyField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Override the build method and specifies that parameters are shown in three columns.
    /// </summary>
    public void build()
    {
        rdpContract = this.dataContractObject() as VendRequestAuditContract;

        vendTmpCompanyInfo.initTable();

        super();

        companyField = this.bindInfo().getDialogField(rdpContract,methodStr(VendRequestAuditContract,parmCompany));
        companyField.lookupButton(2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>companyLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the UI modified event for the detail dialog field.
    /// </summary>
    /// <param name="_control">
    /// The control behind the changed dialog field.
    /// </param>
    /// <returns>
    /// true, if the control value has been modified; otherwise false.
    /// </returns>
    /// <remarks>
    /// The level must enabled and disabled according to value in detail field.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean companyLookup(FormStringControl _control)
    {
        boolean ret = true;

        SysTableLookup          sysTableLookup = SysTableLookup::newParameters(tablenum(VendTmpCompanyInfo),this.dialog().formRun().controlCallingMethod());

        sysTableLookup.addLookupfield(fieldnum(VendTmpCompanyInfo, DataArea));

        sysTableLookup.parmTmpBuffer(VendTmpCompanyInfo);
        sysTableLookup.performFormLookup();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfers data from the dialog box into the data contract object.
    /// </summary>
    public void getFromDialog()
    {
        super();

        select firstOnly DataAreaRecId from vendTmpCompanyInfo
            where VendTmpCompanyInfo.DataArea == companyField.value();

        rdpContract.parmCompanyAccount(vendTmpCompanyInfo.DataAreaRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers the method override in order to capture dialog box events.
    /// </summary>
    public void postRun()
    {
        super();

        dialog.dialogForm().formRun().controlMethodOverload(false);
        companyField.registerOverrideMethod(methodstr(FormStringControl, lookup), methodstr(VendRequestAuditUIBuilder, companyLookup), this);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>