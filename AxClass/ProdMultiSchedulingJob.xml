<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdMultiSchedulingJob</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProdMultiSchedulingJob</c> class shows the user dialog for job scheduling of production
///    order.
/// </summary>
/// <remarks>
///    Several production orders can be selected in the user dialog. Each production order will be
///    scheduled by using the <see cref="T:ProdUpdScheduling_Job" />.
/// </remarks>
class ProdMultiSchedulingJob extends ProdMultiScheduling implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBuffer</Name>
				<Source><![CDATA[
    public boolean checkBuffer(Common _prodTable)
    {
        ProdTable prodTable = _prodTable as ProdTable;
        Common anyBuffer;

        return  !ProdParmScheduling::exist(parmId, prodTable.ProdId) && prodTable.status().runJobScheduling(anyBuffer,true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>formName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the name of the form that is provided with a user interface for specifying parameters for
    ///    production updates.
    /// </summary>
    /// <returns>
    ///    The name of the form.
    /// </returns>
    formName formName()
    {
        return formstr(ProdSchedule);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    void insert(
        Common          _common,
        ParmBuffer      _prodParmScheduling
        )
    {
        ProdParmScheduling prodParmScheduling = _prodParmScheduling;
        super(_common,prodParmScheduling);

        prodParmScheduling.SchedMethod  = ProdSchedMethod::JobScheduling;
        prodParmScheduling.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>query</Name>
				<Source><![CDATA[
    Query query()
    {
        if (toggleIsEnabledForEnsuringParamsAreSet)
        {
            Query query = super();

            return (query ? query : new QueryRun(queryStr(ProdMultiSelectSchedulingJob)).query());
        }
        else
        {
            return new QueryRun(querystr(ProdMultiSelectSchedulingJob)).query();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createProductionOrderScheduler</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a work scheduler for production orders.
    /// </summary>
    /// <returns>
    /// A <c>WrkCtrScheduler_Prod</c> work scheduler for production orders.
    /// </returns>
    protected WrkCtrScheduler_Prod createProductionOrderScheduler()
    {
        return WrkCtrScheduler_Prod::newCheckLockingTTSLevel(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        var instrumentationAdditionalParameters = SysInstrumentationAdditionalParameters::construct().addIsBatchParameter(this.inBatch);

        using (var instrumentation =  ProdInstrumentationSegmentFactory::create(ProdInstrumentationActivities::MultiSchedulingJob, ProdInstrumentationActivities::DefaultActivityId, instrumentationAdditionalParameters))
        {
            #OCCRetryCount
            super();

            WrkCtrScheduler wrkCtrScheduler = this.createProductionOrderScheduler();

            ProdMultiPurchLateSubmissionToWorkflow subcontractingHelper = ProdMultiPurchLateSubmissionToWorkflow::construct();
            subcontractingHelper.setLateSubmissionOfPurchaseOrdersToWorkflow(this.setProduction());

            ProdPurch prodPurch = ProdPurch::newProd();

            this.updateProdDeleted();
        
            ProdParmScheduling prodParmScheduling = this.setProduction();

            int parametersCount = 0;
            
            setPrefix(ProdMultiSchedulingJob::description());
            while (prodParmScheduling)
            {
                parametersCount++;

                setPrefix(#PreFixField(ProdParmScheduling,ProdId));
                try
                {
                    this.jobScheduleProductionOrder(prodParmScheduling, prodPurch, wrkCtrScheduler);
                }
                catch (Exception::Deadlock)
                {
                    retry;
                }
                catch (Exception::TransientSqlConnectionError)
                {
                    if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                    {
                        retry;
                    }
                    else
                    {
                        throw;
                    }
                }
                catch (Exception::UpdateConflict)
                {
                    if (appl.ttsLevel() == 0)
                    {
                        if (xSession::currentRetryCount() >= #RetryNum)
                        {
                            throw Exception::UpdateConflictNotRecovered;
                        }
                        else
                        {
                            retry;
                        }
                    }
                    else
                    {
                        throw Exception::UpdateConflict;
                    }
                }
                catch (Exception::Error)
                {
                    exceptionTextFallThrough();
                }
                next prodParmScheduling;
            }

            subcontractingHelper.performLateSubmissionOfPurchaseOrdersToWorkflow();

            wrkCtrScheduler.dispose();

            instrumentation.addAdditionalParameterToStopEvent(ReqInstrumentationActivities::ParmIsMoreThanOne, int2Str(parametersCount > 1));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>jobScheduleProductionOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs a job scheduling on a single production order.
    /// </summary>
    /// <param name = "_prodParmScheduling">Production order scheduling parameters.</param>
    /// <param name = "_prodPurch">A linked purchase order creation helper.</param>
    /// <param name = "_wrkCtrScheduler">A resource scheduling processor.</param>
    [Replaceable]
    protected void jobScheduleProductionOrder(ProdParmScheduling _prodParmScheduling, ProdPurch _prodPurch, WrkCtrScheduler _wrkCtrScheduler)
    {
        // Only run when the Prod Order is not Stopped.
        if (WHSProdTable::find(_prodParmScheduling.ProdId).Stopped)
        {
            error(strFmt("@WAX1701", _prodParmScheduling.ProdId));
        }
        else
        {
            // Only run when the Prod Order has no active work.
            if (WHSProdTable::isWorkActiveForProdOrder(_prodParmScheduling.ProdId))
            {
                warning("@SYS4010250");
            }
            else
            {
                this.initProdTable(_prodParmScheduling.ProdId).status().runJobScheduling(_prodParmScheduling, false, _prodPurch, this, _wrkCtrScheduler);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static ClassDescription description()
    {
        return "@SYS4398" + ' - ' + "@SYS16375";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (_args.parmEnum() == RunChoose::Run)
        {
            ProdMultiSchedulingJob prodMultiScheduling = ProdMultiScheduling::construct(ProdSchedMethod::JobScheduling);
            RunBaseMultiParm::initFromForm(prodMultiScheduling,_args);

            if (! prodMultiScheduling.prompt())
            {
                return;
            }

            using (SysErrorMessageHelpScope scope = SysErrorMessageHelpScope::newErrorContextByFormAndAction(prodMultiScheduling.formName(), _args.menuItemName()))
            {
                prodMultiScheduling.runOnServer();
            }
            prodMultiScheduling.refreshCaller(_args);
        }
        else
        {
            RunBaseMultiParm::runSpecial(_args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object calledFrom = null)
    {
        boolean errorEncountered;
        ProdParmScheduling prodParmScheduling = this.setProduction();

        while (prodParmScheduling)
        {
            if (prodParmScheduling.JobId && !prodParmScheduling.SyncRef && prodParmScheduling.SchedRef)
            {
                error(strFmt("@SCMPlanning:ProdSchedJobIdAndSchedRefWithoutSyncRefError", prodParmScheduling.ProdId));
                errorEncountered = true;
            }

            next prodParmScheduling;
        }

        if (errorEncountered)
        {
            return false;
        }

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>