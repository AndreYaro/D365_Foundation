<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLineVendInvoiceImpl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for <c>VendInvoiceInfoLine</c>.
/// </summary>
[TaxModelDocumentLineAttribute(tableStr(VendInvoiceInfoLine))]
public class TaxModelDocLineVendInvoiceImpl_IN extends TaxModelDocLineWithholdImpl_IN
{
    VendInvoiceInfoTable    vendInvoiceInfoTable;
    VendInvoiceInfoLine     vendInvoiceInfoLine;
    VendInvoiceInfoLine_W   vendInvoiceInfoLine_W;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssessableValueTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the assessable value in transaction currency for the document line.
    /// </summary>
    /// <returns>
    /// The assessable value in transaction currency.
    /// </returns>
    public AmountCur getAssessableValueTransactionCurrency()
    {
        return vendInvoiceInfoLine_W.AssessableValue_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssessableValueAccountingCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the assessable value in accounting currency for the document line.
    /// </summary>
    /// <returns>
    /// The assessable value in accounting currency.
    /// </returns>
    public AmountCur getAssessableValueAccountingCurrency()
    {
        return vendInvoiceInfoLine_W.AssessableValueAccountingCurrency_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustomsDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Customs Direction
    /// </summary>
    /// <returns>
    /// The <c>Direction_IN</c>
    /// </returns>
    public Direction_IN getCustomsDirection()
    {
        return Direction_IN::Import;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDeliveryDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction delivery date
    /// </summary>
    /// <returns>
    /// The transaction delivery date
    /// </returns>
    public Date getDeliveryDate()
    {
        return VendInvoiceInfoTable.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDiscAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the total discount amount.
    /// </summary>
    /// <returns>The total discount amount</returns>
    public Amount getDiscAmount()
    {
        Amount totalDisc;

        PriceUnit priceUnit = vendInvoiceInfoLine.PriceUnit == 0 ? 1 : vendInvoiceInfoLine.PriceUnit;

        Amount netAmount = (vendInvoiceInfoLine.ReceiveNow * ((vendInvoiceInfoLine.PurchPrice - vendInvoiceInfoLine.discAmount()) / priceUnit)) * (100 - vendInvoiceInfoLine.discPercent()) / 100;

        totalDisc = (vendInvoiceInfoLine.ReceiveNow * vendInvoiceInfoLine.PurchPrice / priceUnit) - netAmount;

        if (!vendInvoiceInfoLine.isNonPO())
        {
            totalDisc = totalDisc + netAmount * vendInvoiceInfoLine.purchTable().DiscPercent / 100;
        }

        return totalDisc;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentLineOriginalTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the document line original table.
    /// </summary>
    /// <returns>
    /// The document line original table.
    /// </returns>
    public Common getDocumentLineOriginalTable()
    {
        Common ret;

        if (vendInvoiceInfoLine.isNonPO())
        {
            ret = vendInvoiceInfoLine;
        }
        else
        {
            ret = vendInvoiceInfoLine.purchLine();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventDimId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>InventDimId</c> of the document line.
    /// </summary>
    /// <returns>
    /// The <c>InventDimId</c>
    /// </returns>
    public InventDimId getInventDimId()
    {
        return vendInvoiceInfoLine.InventDimId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the line amount of the document line.
    /// </summary>
    /// <returns>
    /// The line amount.
    /// </returns>
    public LineAmount getLineAmount()
    {
        return vendInvoiceInfoLine.LineAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMaximumRetailPriceTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the maximum retail price of the document line.
    /// </summary>
    /// <returns>
    /// The maximum retail price.
    /// </returns>
    public MaximumRetailPrice_IN getMaximumRetailPriceTransactionCurrency()
    {
        return vendInvoiceInfoLine_W.MaximumRetailPrice_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the quantity from the transaction line.
    /// </summary>
    /// <returns>
    ///    The quantity.
    /// </returns>
    /// <remarks>
    ///    If the transaction does not support quantities, 0 may be passed as the quantity. This will be used
    ///    to calculate taxes by unit.
    /// </remarks>
    public Qty getQuantity()
    {
        return vendInvoiceInfoLine.ReceiveNow;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRelatedPackingSlip</Name>
				<Source><![CDATA[
    private VendPackingSlipTrans getRelatedPackingSlip()
    {
        VendInvoiceInfoSubLine  vendInvoiceInfoSubLine;
        VendPackingSlipTrans    vendPackingSlipTrans;

        select firstOnly RecId, deliveryDate from vendPackingSlipTrans
            join RecId from vendInvoiceInfoSubLine
                where vendPackingSlipTrans.RecId == vendInvoiceInfoSubLine.JournalRefRecId
                    && vendInvoiceInfoSubLine.JournalRefTableId == tableNum(VendPackingSlipTrans)
                    && vendInvoiceInfoSubLine.LineRefRecId == vendInvoiceInfoLine.RecId;

        return vendPackingSlipTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax direction.
    /// </summary>
    /// <returns>
    /// The tax direction
    /// </returns>
    public TaxDirection getTaxDirection()
    {
        return TaxDirection::IncomingTax;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxRateBasingDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax rate basing date for the given tax type.
    /// </summary>
    /// <param name="_taxType">
    /// The given tax type.
    /// </param>
    /// <returns>
    /// The tax rate date.
    /// </returns>
    public TransDate getTaxRateBasingDate(TaxType_IN _taxType)
    {
        TransDate               taxCalculationDate;
        TaxParameters           taxParameters;
        VendPackingSlipTrans    vendPackingSlipTrans;

        if (_taxType == TaxType_IN::Excise)
        {
            taxParameters = TaxParameters::find();
            switch (taxParameters.VendorCalculationDateType_IN)
            {
                case VendorCalculationDateType_IN::DeliveryDate :
                    vendPackingSlipTrans = this.getRelatedPackingSlip();
                    if (vendPackingSlipTrans.RecId != 0)
                    {
                        taxCalculationDate = vendPackingSlipTrans.DeliveryDate;
                    }
                    else
                    {
                        taxCalculationDate = vendInvoiceInfoTable.Transdate;
                    }
                    break;
                case VendorCalculationDateType_IN::DocumentDate :
                    if (vendInvoiceInfoTable.DocumentDate)
                    {
                        taxCalculationDate = vendInvoiceInfoTable.DocumentDate;
                    }
                    else
                    {
                        taxCalculationDate = vendInvoiceInfoTable.Transdate;
                    }
                    break;
                case VendorCalculationDateType_IN::InvoiceDate  :
                    taxCalculationDate = vendInvoiceInfoTable.Transdate;
                    break;
            }
        }
        else
        {
            if (vendInvoiceInfoTable.useDocumentDate())
            {
                taxCalculationDate = vendInvoiceInfoTable.DocumentDate;
            }
            else
            {
                taxCalculationDate = vendInvoiceInfoTable.TransDate;
            }
        }

        return taxCalculationDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the document line transaction currency.
    /// </summary>
    /// <returns>The document line transaction currency.</returns>
    public CurrencyCode getTransactionCurrency()
    {
        return vendInvoiceInfoLine.CurrencyCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction posting date
    /// </summary>
    /// <returns>
    /// The transaction posting date
    /// </returns>
    public TransDate getTransDate()
    {
        return vendInvoiceInfoTable.UpdateDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVendTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>VendTable</c> based on the vendor account on the transaction line.
    /// </summary>
    /// <returns>
    /// The <c>VendTable</c>
    /// </returns>
    public VendTable getVendTable()
    {
        return vendInvoiceInfoTable.vendTable_InvoiceAccount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        vendInvoiceInfoLine = _documentLineMap;
        vendInvoiceInfoLine_W = vendInvoiceInfoLine.vendInvoiceInfoLine_W();
        vendInvoiceInfoTable = vendInvoiceInfoLine.vendInvoiceInfoTable();

        super(vendInvoiceInfoLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isForeignTransaction</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if the transaction is a foreign transaction or not.
    /// </summary>
    /// <returns>
    /// True if the document line is a foregin foreign transaction; otherwise, false.
    /// </returns>
    public NoYes isForeignTransaction()
    {
        return vendInvoiceInfoLine.purchLine().purchTable().purchTable_W().CustomsImportOrder_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldPostQuantityRegister</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if excise quantity register should be posted or not.
    /// </summary>
    /// <returns>
    /// True if the document line should post excise quantity register; otherwise, false.
    /// </returns>
    public NoYes shouldPostQuantityRegister()
    {
        TaxParameters   taxParameters;
        TaxOnItem       taxOnItem;
        boolean ret;

        if (this.getExciseTaxAccountType(taxOnItem) != TaxAccountType_IN::ExciseExpenseAccount)
        {
            taxParameters = TaxParameters::find();

            switch (taxParameters.VendorCalculationDateType_IN)
            {
                case VendorCalculationDateType_IN::DeliveryDate :
                case VendorCalculationDateType_IN::DocumentDate :
                    ret = false;
                    break;
                case VendorCalculationDateType_IN::InvoiceDate :
                    ret = true;
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canModifyTaxInformation</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if the transaction tax information can be modified or not.
    /// </summary>
    /// <returns>
    /// True if the tax information of document line can be modified; otherwise, false.
    /// </returns>
    public NoYes canModifyTaxInformation()
    {
        return vendInvoiceInfoLine.isNonPO() || vendInvoiceInfoTable.InvoiceType == PurchInvoiceType::VendorAdvance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxModuleType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax module type.
    /// </summary>
    /// <returns>
    /// The transaction tax module type with purch invoice.
    /// </returns>
    public TaxModuleType getTaxModuleType()
    {
        return TaxModuleType::PurchInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxLogisticsPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax <c>LogisticsPostalAddress</c> of this transaction document line.
    /// </summary>
    /// <returns>
    /// The tax <c>LogisticsPostalAddress</c>
    /// </returns>
    /// This method is used to init TransTaxInformation.CompanyLocation, which should be defaulted from transaction line delivery address
    public LogisticsPostalAddress getTaxLogisticsPostalAddress(TransTaxInformationType_IN _type = TransTaxInformationType_IN::Normal)
    {
        CompanyInfo         companyInfoLoc;
        DirPartyLocation    dirPartyLocation;

        if (this.isBasedOnReferenceTaxModelDocLine())
        {
            return this.getReferenceTaxModelDocLine().getTaxLogisticsPostalAddress(TransTaxInformationType_IN::Normal);
        }
        else
        {
            if (documentLineMap.DeliveryLocation)
            {
                return LogisticsPostalAddress::findRecId(documentLineMap.DeliveryLocation);
            }
            else
            {
                companyInfoLoc = CompanyInfo::findByCompany_IN(curExt());

                dirPartyLocation = DirPartyLocation::findPrimaryPartyLocation(companyInfoLoc.RecId);

                return LogisticsPostalAddress::findByLocation(dirPartyLocation.Location);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the unit of the item in this transaction document line.
    /// </summary>
    /// <returns>The unit of the item in this transaction line.</returns>
    public UnitOfMeasureSymbol getUnit()
    {
        if (vendInvoiceInfoLine.PurchUnit)
        {
            return vendInvoiceInfoLine.PurchUnit;
        }
        else
        {
            return vendInvoiceInfoLine.purchLine().PurchUnit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentInvoiceDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction invoice date
    /// </summary>
    /// <returns>
    /// The transaction invoice date
    /// </returns>
    public InvoiceDate getDocumentInvoiceDate()
    {
        return vendInvoiceInfoTable.DocumentDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction document date
    /// </summary>
    /// <returns>
    /// The transaction document date
    /// </returns>
    public DocumentDate getDocumentDate()
    {
        return vendInvoiceInfoTable.DocumentDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isSupportCalcTaxInAccountingCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Whether support calculate tax in accounting currency for this transaction line.
    /// </summary>
    /// <returns>
    /// True if support, otherwise false.
    /// </returns>
    [Hookable(false)]
    internal boolean isSupportCalcTaxInAccountingCurrency()
    {
        return this.isForeignTransaction();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAssessableValueAccountingCurrencyAvailable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Whether user can set assessable value on UI for this transaction line.
    /// </summary>
    /// <returns>
    /// True if can, otherwise false;
    /// </returns>
    [Hookable(false)]
    internal boolean isAssessableValueAccountingCurrencyAvailable()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getIsSEZParty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the SEZ property of party.
    /// </summary>
    /// <returns>
    /// true if the party is SEZ; otherwise, false.
    /// </returns>
    internal NoYes getIsSEZParty()
    {
        return LogisticsLocationExt::find(this.getPartyLogisticsPostalAddress().Location).TaxGSTEPZCode_IN == TaxGSTEPZCode_IN::SEZ;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getIsDeemedExport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Deemed export property of party.
    /// </summary>
    /// <returns>
    /// yes if it is Deemed export; otherwise, no.
    /// </returns>
    public NoYes getIsDeemedExport()
    {
        return LogisticsLocationExt::find(this.getPartyLogisticsPostalAddress().Location).isDeemedExportType();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>