<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventSumFinancialItem</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventSumFinancialItem extends InventSumFinancial
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setValueQty</Name>
				<Source><![CDATA[
        protected  void setValueQty()
    {
        InventTrans           inventTrans;
        InventTransPosting    inventTransPosting;
        InventSettlement      inventSettlement;
        InventSum             inventSum;

        select sum(PostedQty),sum(PostedValue) from inventSum
            where inventSum.ItemId      == itemId
              &&  inventSum.Closed      == NoYes::No;

        qty    = inventSum.PostedQty;
        value  = inventSum.PostedValue;

        select forceplaceholders TableId from inventTransPosting
            where inventTransPosting.ItemId                 == itemId
              &&  inventTransPosting.InventTransPostingType == InventTransPostingType::Financial
              &&  inventTransPosting.TransDate              >  perDate
        join sum(CostAmountPosted),sum(Qty) from inventTrans
            where inventTrans.Voucher                       == inventTransPosting.Voucher
              &&  inventTrans.DateFinancial                 == inventTransPosting.TransDate
              &&  inventTrans.InventTransOrigin             == inventTransPosting.InventTransOrigin
              &&  inventTrans.PackingSlipReturned           == NoYes::No;

        qty   -= inventTrans.Qty;
        value -= inventTrans.CostAmountPosted;

        select sum(CostAmountAdjustment) from inventSettlement
            where inventSettlement.ItemId       == itemId
              &&  inventSettlement.Cancelled    == NoYes::No
              &&  inventSettlement.SettleModel  != InventSettleModel::PhysicalValue
              // <GEERU>
              &&  inventSettlement.InventTransCurrency_RU == InventTransCurrency_RU::PrimaryCur
              // </GEERU>
              &&  inventSettlement.TransDate    >  perDate;

        value -= inventSettlement.CostAmountAdjustment;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>