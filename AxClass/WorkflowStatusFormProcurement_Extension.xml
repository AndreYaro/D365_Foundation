<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WorkflowStatusFormProcurement_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(WorkflowStatus))]
internal final class WorkflowStatusFormProcurement_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    internal void init()
    {
        next init();
        this.enableResetControl(false); 
    }

]]></Source>
			</Method>
			<Method>
				<Name>WorkflowTrackingStatusTable_OnSelectionChanged</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(WorkflowStatus, WorkflowTrackingStatusTable), FormDataSourceEventType::SelectionChanged)]
    public void WorkflowTrackingStatusTable_OnSelectionChanged(FormDataSource sender, FormDataSourceEventArgs e)
    {
        WorkflowTrackingStatusTable record = WorkflowTrackingStatusTable_ds.getFirst(true);

        if (!record || record.TableId != tableNum(WorkflowTrackingStatusTable))
        {
            this.enableResetControl(false);
            return;
        }

        if (!this.isValidContextTableId(WorkflowTrackingStatusTable::findRecId(record.RecId)))
        {
            this.enableResetControl(false);
            return;
        }

        if (record.TrackingStatus != WorkflowTrackingStatus::Unrecoverable)
        {
            this.enableResetControl(false);
            return;
        }
        this.enableResetControl(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidContextTableId</Name>
				<Source><![CDATA[
    private boolean isValidContextTableId(WorkflowTrackingStatusTable _workflowTrackingStatusTable)
    {
        WorkflowContextTableId tableId = _workflowTrackingStatusTable.ContextTableId;

        if (tableId == tableName2id('PurchReqTable') ||
            tableId == tableName2id('PurchTable') ||
            tableId == tableName2id('VendTable'))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableResetControl</Name>
				<Source><![CDATA[
    private void enableResetControl(boolean _isEnabled = false)
    {
        ResetActionPaneTab.visible(_isEnabled);
        ResetProcurementWorkflow.visible(_isEnabled);
        ResetProcurementWorkflow.enabled(_isEnabled);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>