<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjPeriodPostingSelectSales_Trans</Name>
	<SourceCode>
		<Declaration><![CDATA[
class   ProjPeriodPostingSelectSales_Trans extends ProjPeriodPostingSelectSales
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    /// interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    /// A dialog box can be either built by using the <c>Dialog</c> class or by using a class that is
    /// created in the AOT.
    /// </remarks>
    public Object dialog()
    {
        #define.two(2)

        DialogRunbase  dialogRunbase = super();

        dialogRunbase.addGroup("@SYS90565");
        if (reverseTurnover)
        {
            dialogReverseTurnover   = dialogRunbase.addFieldValue(enumStr(ProjRevenueDirection), ProjRevenueDirection::ReverseRevenue);
        }
        else
        {
            dialogReverseTurnover   = dialogRunbase.addFieldValue(enumStr(ProjRevenueDirection), ProjRevenueDirection::AccrueRevenue);
        }
        dialogRunbase.addGroup("@SYS78373",dialogGroupCostSale);

        dialogRunbase.addGroup("@SYS23410");
        dialogTransDate = dialogRunbase.addFieldValue(extendedTypeStr(TransDate), transActionDate, "@SYS14475", "@SYS90567");

        dialogGroupCostSale = dialogRunbase.addGroup('');
        dialogGroupCostSale.frameType(1);
        dialogGroupCostSale.columns(#two);

        this.initQuery();
        dialogRunbase.useConfigKey(true);

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
        public boolean getFromDialog()
    {
        boolean ret;
        ProjRevenueDirection projRevenueDirection;

        ret = super();

        projRevenueDirection = dialogReverseTurnover.value();
        if (projRevenueDirection == ProjRevenueDirection::ReverseRevenue)
        {
            this.parmReverseTurnover(true);
        }
        else
        {
            this.parmReverseTurnover(false);
        }

        this.parmTransActionDate(dialogTransDate.value());

        this.getFromDialogMapTransId();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialogMapTransId</Name>
				<Source><![CDATA[
        void getFromDialogMapTransId()
    {
        List            list;
        ListIterator    li;

        Common      commonTrans;

        if (this.parmTransIdList())
        {
            list = List::create(this.parmTransIdList());
        }

        li = new ListIterator(list);

        li.begin();
        if (li.more())
        {
            commonTrans     = li.value();
        }

        queryEmpl           = false;
        queryCost           = false;
        queryItem           = false;
        queryRevenue        = false;

        switch (commonTrans.TableId)
        {
            case tableNum(ProjEmplTrans):
                queryEmpl   = true;
                break;

            case tableNum(ProjCostTrans):
                queryCost   = true;
                break;

            case tableNum(ProjItemTrans):
                queryItem   = true;
                break;

            case tableNum(ProjRevenueTrans):
                queryRevenue = true;
                break;
        }

        fromDate           = dateNull();
        toDate             = dateMax();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
        void initQuery()
    {
        emplDataSource          = queryRun.query().dataSourceTable(tableNum(ProjEmplTrans));
        emplDataSourceSale      = queryRun.query().dataSourceTable(tableNum(ProjEmplTransSale));
        costDataSource          = queryRun.query().dataSourceTable(tableNum(ProjCostTrans));
        costDataSourceSale      = queryRun.query().dataSourceTable(tableNum(ProjCostTransSale));
        itemDataSource          = queryRun.query().dataSourceTable(tableNum(ProjItemTrans));
        itemDataSourceSale      = queryRun.query().dataSourceTable(tableNum(ProjItemTransSale));
        revenueDataSource       = queryRun.query().dataSourceTable(tableNum(ProjRevenueTrans));
        revenueDataSourceSale   = queryRun.query().dataSourceTable(tableNum(ProjRevenueTransSale));

        this.initQueryEmpl();
        this.initQueryCost();
        this.initQueryItem();
        this.initQueryRevenue();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
        public container pack()
    {
        return [#CurrentVersion,#CurrentList,queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
        public QueryRun queryBuild()
    {
        Query                   query = new Query();
        QueryBuildDataSource    dsProjEmplTrans, dsProjCostTrans, dsProjItemTrans, dsProjRevenueTrans;
        QueryBuildDataSource    dsProjEmplTransSale, dsProjCostTransSale, dsProjItemTransSale, dsProjRevenueTransSale;

        dsProjEmplTrans     = query.addDataSource(tableNum(ProjEmplTrans));

        dsProjEmplTransSale = dsProjEmplTrans.addDataSource(tableNum(ProjEmplTransSale));
        dsProjEmplTransSale.relations(true);
        dsProjEmplTransSale.joinMode(JoinMode::ExistsJoin);

        dsProjCostTrans     = query.addDataSource(tableNum(ProjCostTrans));

        dsProjCostTransSale = dsProjCostTrans.addDataSource(tableNum(ProjCostTransSale));
        dsProjCostTransSale.relations(true);
        dsProjCostTransSale.joinMode(JoinMode::ExistsJoin);

        dsProjItemTrans     = query.addDataSource(tableNum(ProjItemTrans));

        dsProjItemTransSale = dsProjItemTrans.addDataSource(tableNum(ProjItemTransSale));
        dsProjItemTransSale.relations(true);
        dsProjItemTransSale.joinMode(JoinMode::ExistsJoin);

        dsProjRevenueTrans  = query.addDataSource(tableNum(ProjRevenueTrans));

        dsProjRevenueTransSale = dsProjRevenueTrans.addDataSource(tableNum(ProjRevenueTransSale));
        dsProjRevenueTransSale.relations(true);
        dsProjRevenueTransSale.joinMode(JoinMode::ExistsJoin);

        queryRun = new SysQueryRun(query);

        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDialogRanges</Name>
				<Source><![CDATA[
        void setDialogRanges()
    {
        QueryBuildDataSource    dsProjEmplTrans, dsProjCostTrans, dsProjItemTrans, dsProjRevenueTrans;
        QueryBuildRange         queryRangeTransIdEmpl,queryRangeTransIdCost, queryRangeTransIdItem, queryRangeTransIdRevenue;

        super();

        dsProjEmplTrans     = queryRun.query().dataSourceTable(tableNum(ProjEmplTrans));
        dsProjCostTrans     = queryRun.query().dataSourceTable(tableNum(ProjCostTrans));
        dsProjItemTrans     = queryRun.query().dataSourceTable(tableNum(ProjItemTrans));
        dsProjRevenueTrans  = queryRun.query().dataSourceTable(tableNum(ProjRevenueTrans));

        if (queryCost)
        {
            queryRangeTransIdCost    = dsProjCostTrans.findRange(fieldNum(ProjCostTrans,ProjId));

            while (queryRangeTransIdCost)
            {
                dsProjCostTrans.clearRange(fieldNum(ProjCostTrans,ProjId));
                queryRangeTransIdCost    = dsProjCostTrans.findRange(fieldNum(ProjCostTrans,ProjId));
            }

            this.rangeTransId(dsProjCostTrans);
        }

        if (queryEmpl)
        {
            queryRangeTransIdEmpl    = dsProjEmplTrans.findRange(fieldNum(ProjEmplTrans,ProjId));

            while (queryRangeTransIdEmpl)
            {
                dsProjEmplTrans.clearRange(fieldNum(ProjEmplTrans,ProjId));
                queryRangeTransIdEmpl    = dsProjEmplTrans.findRange(fieldNum(ProjEmplTrans,ProjId));
            }

            this.rangeTransId(dsProjEmplTrans);
        }

        if (queryItem)
        {
            queryRangeTransIdItem    = dsProjItemTrans.findRange(fieldNum(ProjItemTrans,ProjId));

            while (queryRangeTransIdItem)
            {
                dsProjItemTrans.clearRange(fieldNum(ProjItemTrans,ProjId));
                queryRangeTransIdItem    = dsProjItemTrans.findRange(fieldNum(ProjItemTrans,ProjId));
            }

            this.rangeTransId(dsProjItemTrans);
        }

        if (queryRevenue)
        {
            queryRangeTransIdRevenue    = dsProjRevenueTrans.findRange(fieldNum(ProjRevenueTrans,ProjId));

            while (queryRangeTransIdRevenue)
            {
                dsProjRevenueTrans.clearRange(fieldNum(ProjRevenueTrans,ProjId));
                queryRangeTransIdRevenue    = dsProjRevenueTrans.findRange(fieldNum(ProjRevenueTrans,ProjId));
            }

            this.rangeTransId(dsProjRevenueTrans);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether to add a select button to the dialog box.
    /// </summary>
    /// <returns>
    /// Always returns false.
    /// </returns>
    /// <remarks>
    /// If you click this button, it will show the query form. Therefore, the <c>queryRun</c> method has to
    /// return a valid instance of the <c>QueryRun</c> class. If you change the return value to true, the
    /// button will be added.
    /// </remarks>
    public boolean showQueryValues()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
        boolean unpack(container packedClass)
    {
        Integer         version         = conPeek(packedClass,1);
        container       packedQuery;

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList,packedQuery] = packedClass;
                if (SysQuery::isPackedOk(packedQuery))
                {
                    queryRun = new QueryRun(packedQuery);
                }
                else
                {
                    queryRun = this.queryBuild();
                }
                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
        static public ClassDescription description()
    {
        return "@SYS87008";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>