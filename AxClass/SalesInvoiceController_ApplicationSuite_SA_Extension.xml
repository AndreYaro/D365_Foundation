<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesInvoiceController_ApplicationSuite_SA_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class complements the logic of <c>SalesInvoiceController</c> in order to support multilingual invoice printout.
/// </summary>
[ExtensionOf(classStr(SalesInvoiceController))]
public final class SalesInvoiceController_ApplicationSuite_SA_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getCustLanguageId</Name>
				<Source><![CDATA[
    protected static CustLanguageId getCustLanguageId(CustInvoiceJour _custInvoiceJour)
    {
        CustLanguageId custLanguageId = next getCustLanguageId(_custInvoiceJour);
        SalesInvoiceLanguageContext_SA salesInvoiceLanguageContext_SA = SalesInvoiceLanguageContext_SA::current();

        if (salesInvoiceLanguageContext_SA)
        {
            custLanguageId = salesInvoiceLanguageContext_SA.parmCurrentLanguageId();
        }

        return custLanguageId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>documentTitle</Name>
				<Source><![CDATA[
    protected str documentTitle()
    {
        #isoCountryRegionCodes
        str ret = next documentTitle();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoSA]))
        {
            boolean isCreditNote = custInvoiceJour.InvoiceAmount < 0
                    || (custInvoiceJour.InvoiceAmount == 0 && custInvoiceJour.Qty < 0);
            boolean isDebitNote;

            if (!isCreditNote)
            {
                isDebitNote = CustVendCreditInvoicingJour::find(custInvoiceJour.TableId, custInvoiceJour.Recid).CustVendCorrectedInvoiceId != '';
            }

            ret = CustInvoiceJour::getDocumentTitle_SA(
                isCreditNote,
                isDebitNote,
                salesInvoiceContract.parmPrintCopyOriginal(),
                custInvoiceJour.isProforma(),
                FeatureStateProvider::isFeatureEnabled(SimplifiedInvoiceSAFeature::instance())
                    && custInvoiceJour.InvoiceType_W == InvoiceType_W::SimplifiedInvoice,
                SalesInvoiceController::getCustLanguageId(custInvoiceJour));
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>