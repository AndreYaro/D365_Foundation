<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceJournal_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendInvoiceJournal_ApplicationSuite_Extension</c> class is an extension class which extends the <c>VendInvoiceJournal</c> form.
/// </summary>
[ExtensionOf(formStr(vendInvoiceJournal))]
public final class VendInvoiceJournal_ApplicationSuite_Extension
{
    #ISOCountryRegionCodes

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoEG, #isoPL]))
        {
            FormButtonControl invocieUUIDButton = this.design().controlName(formControlStr(VendInvoiceJournal, EnterInvoiceUUIDButton)) as FormButtonControl;
            invocieUUIDButton.registerOverrideMethod(methodStr(FormButtonControl, clicked), methodStr(VendInvoiceJournal_ApplicationSuite_Extension, EnterInvoiceUUIDButton_OnClicked), this);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>EnterInvoiceUUIDButton_OnClicked</Name>
				<Source><![CDATA[
    private void EnterInvoiceUUIDButton_OnClicked(FormButtonControl sender)
    {
        Dialog dialog = new Dialog("@BusinessDocumentSubmissionService:EnterSubmissionUUIDDialogCaption");
        DialogField field = dialog.addField(extendedTypeStr(UUID), "@BusinessDocumentSubmissionService:SubmissionUUID");
        field.value(vendInvoiceJour_W.SubmissionUUID_EG);
        dialog.run();
        if (dialog.closedOk())
        {
            if (!vendInvoiceJour_W.vendInvoiceJour)
            {
                vendInvoiceJour_W.vendInvoiceJour = vendInvoiceJour.RecId;
            }
            vendInvoiceJour_W.SubmissionUUID_EG = field.value();
            VendInvoiceJour_W_ds.write();
            VendInvoiceJour_W_ds.refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateSubmissionUUID</Name>
				<Source><![CDATA[
    private void updateSubmissionUUID(VendInvoiceJour_W _vendInvoiceJourW, UUID _value)
    {
        ttsbegin;
        _vendInvoiceJourW.SubmissionUUID_EG = _value;
        _vendInvoiceJourW.update();
        ttscommit;
        vendInvoiceJour_w_ds.reread();
        vendInvoiceJour_W_ds.refresh();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSubmissionUUID</Name>
				<Source><![CDATA[
    private void setSubmissionUUID(VendInvoiceJour _vendInvoiceJour, UUID _value)
    {
        VendInvoiceJour_W vendInvoiceJourW;
        ttsbegin;
        vendInvoiceJourW.vendInvoiceJour = _vendInvoiceJour.RecId;
        vendInvoiceJourW.SubmissionUUID_EG = _value;
        vendInvoiceJourW.insert();
        ttscommit;
        vendInvoiceJour_ds.research(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>