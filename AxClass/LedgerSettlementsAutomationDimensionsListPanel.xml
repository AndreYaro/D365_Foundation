<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerSettlementsAutomationDimensionsListPanel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerSettlementsAutomationDimensionsListPanel</c> class manages a list panel control to use to select the
///    Ledger settlement automation dimension selections.
/// </summary>
public class LedgerSettlementsAutomationDimensionsListPanel extends SysListPanel
{
    LedgerSettlementsAutomationCriteria ledgerSettlementsAutomationCriteria;
    private boolean listPanelInitialized;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>selectionCount</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the number of items populated in the selected list panel.
    /// </summary>
    /// <returns>
    ///    An integer with the number of items populated in the selected list panel.
    /// </returns>
    internal int selectionCount()
    {
        return viewSelected.getCount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeAllSelected</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Removes all items that were populated in the selected list panel, returning them to the available list panel.
    /// </summary>
    internal void removeAllSelected()
    {
        viewSelected.deleteAll();
    }

]]></Source>
			</Method>
			<Method>
				<Name>commitSelectionToDatabase</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Save the selected Financial dimension entries to the LedgerSettlementsAutomationMatchSelection table
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The parent record from the LedgerSettlementsAutomationCriteria table that the selected dimensions are bound to.
    /// </param>
    [Hookable(false)]
    public void commitSelectionToDatabase(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        DimensionAttributeRecId dimAttributeId;
        LedgerSettlementsAutomationMatchSelection ledgerSettlementsAutomationMatchSelection;

        // Need to remove all previous selections that were chosen.
        this.deletePreviousSelection(_ledgerSettlementsAutomationCriteriaRecId);

        FormListItem formListItem;
        container data;
        int idx = viewSelected.getCount() - 1;
        int i = 1;

        while(idx != -1)
        {
            formListItem = viewSelected.getItem(idx);
            data += formListItem.data();

            dimAttributeId = conpeek(data, i);

            ledgerSettlementsAutomationMatchSelection.LedgerSettlementsAutomationCriteria = _ledgerSettlementsAutomationCriteriaRecId;
            ledgerSettlementsAutomationMatchSelection.SelectionRecId = dimAttributeId;
            ledgerSettlementsAutomationMatchSelection.SelectionCriteria = LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions;
            ledgerSettlementsAutomationMatchSelection.insert();

            idx --;
            i ++;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deletePreviousSelection</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Remove all saved Financial dimensions selected that are bound to the LedgerSettlementsAutomationCriteria record.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The parent record from the LedgerSettlementsAutomationCriteria table that the selected dimensions are bound to.
    /// </param>
    [Hookable(false)]
    public void deletePreviousSelection(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        LedgerSettlementsAutomationMatchSelection ledgerSettlementsAutomationMatchSelection;

        delete_from ledgerSettlementsAutomationMatchSelection
            where ledgerSettlementsAutomationMatchSelection.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
                && ledgerSettlementsAutomationMatchSelection.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Stub class to satisfy the implemention requirement of this abstract method from class SysListPanel
    /// </summary>
    /// <param name="_data">
    ///    The item that was added to the list of selected financial dimensions.
    /// </param>
    internal void addData(container _data)
    {
        //super(); No need to call super() as we aren't immediately adding data, but stil need the method to satisfy the abstract method's implementation.
    }

]]></Source>
			</Method>
			<Method>
				<Name>getData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items for the left and right lists of the list panel.
    /// </summary>
    /// <returns>
    ///    A container with the items to add to both the selected and available lists of the list panel.
    /// </returns>
    internal container getData()
    {
        container ledgerDimensionsSelected;
        container ledgerDimensionsAvailable;

        RefRecId recId = ledgerSettlementsAutomationCriteria.RecId;

        if (!listPanelInitialized)
        {
            ledgerDimensionsSelected = LedgerSettlementsAutomationDimensionsListPanel::getLedgerDimensionsSelected(recId);

            // Get the ledger dimensions available to select.
            ledgerDimensionsAvailable = LedgerSettlementsAutomationDimensionsListPanel::getLedgerDimensionsAvailable(recId);

            listPanelInitialized = true;
        }

        return [ledgerDimensionsAvailable, ledgerDimensionsSelected];
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Typically, this method would remove a Financial dimension entry from the "Selected financial dimensions" list,
    ///    but seeing as we are not actually storing the interim selections in a table, this method is not used, but since
    ///    we inherited it, we still need to have it's stub in this class.
    /// </summary>
    /// <param name="_data">
    ///    The item that was selected from the list of selected financial dimensions.
    /// </param>
    internal void removeData(container _data)
    {
        //super(); No need to call super() as we aren't immediately updating data, but stil need the method to satisfy the abstract method's implementation.
    }

]]></Source>
			</Method>
			<Method>
				<Name>setLedgerSettlementsAutomationCriteria</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the LedgerSettlementsAutomationCriteria reference for the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>LedgerSettlementsAutomationCriteria</c> record.
    /// </param>
    internal void setLedgerSettlementsAutomationCriteria(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        ledgerSettlementsAutomationCriteria = LedgerSettlementsAutomationCriteria::find(_ledgerSettlementsAutomationCriteriaRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>sortMethod</Name>
				<Source><![CDATA[
    protected FormListSort sortMethod()
    {
        // Do not sort the list as the data is returned in the correct order.
        return FormListSort::NoSort;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the selected item can be added or removed from the list.
    /// </summary>
    /// <param name="_data">
    ///    The selected item in the panel list.
    /// </param>
    /// <param name="_addRemove">
    ///    Specifies whether the selected item is being added or removed.
    /// </param>
    /// <returns>
    ///    true if the selected item can be added or removed; otherwise, false.
    /// </returns>
    internal boolean validate(anytype _data, AddRemove _addRemove)
    {
        // Active configurations or configurations locked for editing by another user are not allowed to be updated.
        return super(_data, _addRemove);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Constructs an instance of the <c>LedgerSettlementsAutomationDimensionsListPanel</c> class.
    /// </summary>
    /// <returns>
    ///    An instance of the <c>LedgerSettlementsAutomationDimensionsListPanel</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    internal static LedgerSettlementsAutomationDimensionsListPanel construct()
    {
        return new LedgerSettlementsAutomationDimensionsListPanel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLedgerDimensionsAvailable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items for the left list of the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>ledgerSettlementsAutomationCriteria</c> record.
    /// </param>
    /// <returns>
    ///    A container with the items to add to left list of the list panel.
    /// </returns>
    private static container getLedgerDimensionsAvailable(
        RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        container ledgerDimensionsAvailable;

        DimensionAttribute dimAttribute;
        DimensionAttributeSetItem dimAttributeSetItem;
        DimensionHierarchyLevel dimHierarchyLevel;
        DimensionHierarchy dimHierarchy;
        LedgerSettlementsAutomationMatchSelection alreadySelectedDimensions;

        // Populate the list of dimensions that are available to be selected for financial dimensions.
        if (FeatureStateProvider::isFeatureEnabled(LedgerSettlementAvailableDimensionsFlight::instance()))
        {
            DimensionEnumeration allDimAttributesForLedger = DimensionCache::getDimensionAttributeSetForLedger();

            while select RecId, Name from dimAttribute
                order by Name
                where dimAttribute.Type != DimensionAttributeType::MainAccount
                join dimAttributeSetItem
                    where dimAttributeSetItem.DimensionAttribute == dimAttribute.RecId
                        && dimAttributeSetItem.DimensionAttributeSet == allDimAttributesForLedger
                notexists join alreadySelectedDimensions
                    where dimAttribute.RecId == alreadySelectedDimensions.SelectionRecId
                        && alreadySelectedDimensions.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions
                        && alreadySelectedDimensions.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
 
            {
                ledgerDimensionsAvailable += [[dimAttribute.RecId, dimAttribute.localizedName()]];
            }
        }
        else
        {
            while select RecId, Name from dimAttribute
                order by Name
                exists join dimHierarchyLevel
                    where dimHierarchyLevel.DimensionAttribute == dimAttribute.RecId
                exists join dimHierarchy
                    where dimHierarchy.RecId == dimHierarchyLevel.DimensionHierarchy
                        && dimHierarchy.FocusState == DimensionFocusBalanceInitializationState::Initialized
                        && dimAttribute.Type != DimensionAttributeType::MainAccount
                notexists join alreadySelectedDimensions
                    where dimAttribute.RecId == alreadySelectedDimensions.SelectionRecId
                        && alreadySelectedDimensions.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions
                        && alreadySelectedDimensions.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
 
            {
                ledgerDimensionsAvailable += [[dimAttribute.RecId, dimAttribute.localizedName()]];
            }
        }

        return ledgerDimensionsAvailable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLedgerDimensionsSelected</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items of the right list of the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>LedgerSettlementsAutomationCriteria</c> record.
    /// </param>
    /// <returns>
    ///    A container with the items to add to the right list of the list panel.
    /// </returns>
    private static container getLedgerDimensionsSelected(
        RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        container ledgerDimensions;

        DimensionAttribute dimAttribute;
        LedgerSettlementsAutomationMatchSelection alreadySelectedDimensions;

        // Populate the list of dimensions that are currently selected.
        while select RecId, Name from dimAttribute
            order by Name
            join alreadySelectedDimensions
                where alreadySelectedDimensions.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions
                && alreadySelectedDimensions.SelectionRecId == dimAttribute.RecId
                && alreadySelectedDimensions.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
        {
            ledgerDimensions += [[dimAttribute.RecId, dimAttribute.localizedName()]];
        }

        return ledgerDimensions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes a new instance of the <c>LedgerSettlementsAutomationDimensionsListPanel</c> class.
    /// </summary>
    /// <param name="_formRun">
    ///    The form that will contain the list panel controls.
    /// </param>
    /// <param name="_parentId">
    ///    The form control that will contain the list panel controls.
    /// </param>
    /// <returns>
    ///    An instance of the <c>LedgerSettlementsAutomationDimensionsListPanel</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    internal static LedgerSettlementsAutomationDimensionsListPanel newForm(FormRun _formRun, int _parentId)
    {
        LedgerSettlementsAutomationDimensionsListPanel listPanel = LedgerSettlementsAutomationDimensionsListPanel::construct();

        listPanel.parmFormRun(_formRun);
        listPanel.parmParentId(_parentId);
        listPanel.parmCaptionSelected("@SYS315922");
        listPanel.parmCaptionAvailable("@SYS315923");

        listPanel.parmHasUpDownButton(false);
        listPanel.parmHasAddAllButton(false);
        listPanel.parmHasRemoveAllButton(false);

        listPanel.build();

        return listPanel;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>