<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetComponentPostReversal_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Post component transaction reversal.
/// </summary>
class AssetComponentPostReversal_JP extends AssetComponentPost_JP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isReversal</Name>
				<Source><![CDATA[
    public boolean isReversal()
    {
        return true;
    }
]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(
        LedgerJournalTrans_Asset    _ledgerJournalTrans_Asset,
        LedgerJournalTrans          _ledgerJournalTrans,
        AssetTrans                  _assetTrans,
        LedgerVoucher               _ledgerVoucher)
    {
        super(_ledgerJournalTrans_Asset,
            _ledgerJournalTrans,
            _assetTrans,
            _ledgerVoucher);
    }
]]></Source>
			</Method>
			<Method>
				<Name>postMovement</Name>
				<Source><![CDATA[
    protected void postMovement(AssetComponent_JP _assetComponent)
    {
        InventMov_Component_JP  movement;
        InventUpd_Financial     updFinancial;
        movement  = InventMovement::construct(_assetComponent);

        movement.cacheAccountOperations(ledgerJournalTrans.parmOffsetLedgerDimension());
        updFinancial = InventUpd_Financial::newPostAssetComponent_JP(movement, ledgerVoucher, -_assetComponent.CostAmount);
        updFinancial.updateNow();
    }
]]></Source>
			</Method>
			<Method>
				<Name>updateAssetComponentAfterPosting</Name>
				<Source><![CDATA[
    protected void updateAssetComponentAfterPosting(AssetComponent_JP _assetComponent)
    {
        AssetComponent_JP   assetComponentOrigin;
        InventTrans         inventTrans;
        RefRecId            inventTransOriginRecId;
        RefRecId            inventTransReverseOriginRecId;

        assetComponentOrigin = AssetComponent_JP::find(_assetComponent.StornoRecId, true);
        assetComponentOrigin.DisassembledQty = assetComponentOrigin.Qty;
        assetComponentOrigin.update(true);

        _assetComponent.selectForUpdate(true);
        _assetComponent.DisassembledQty = _assetComponent.Qty;
        _assetComponent.update(true);

        inventTrans = InventTrans::findTransId(assetComponentOrigin.InventTransId);
        if (!inventTrans.QtySettled)
        {
            inventTransOriginRecId = InventTransOriginAssetComponent_JP::findInventTransOriginId(assetComponentOrigin.RecId);
            inventTransReverseOriginRecId = InventTransOriginAssetComponent_JP::findInventTransOriginId(_assetComponent.RecId);
            //marking
            InventTransOrigin::updateMarking(inventTransOriginRecId,
                                                 inventTransReverseOriginRecId,
                                                 inventTrans.Qty,
                                                 inventTrans.Voucher);
            InventTransOrigin::updateMarking(inventTransReverseOriginRecId,
                                                 inventTransOriginRecId,
                                                 -inventTrans.Qty,
                                                 inventTrans.Voucher);
        }
    }
]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates new instance of <c>AssetComponentPost_JP</c>.
    /// </summary>
    /// <param name="_ledgerJournalTrans_Asset">
    /// The buffer of <c>LedgerJournalTrans_Asset</c>.
    /// </param>
    /// <param name="_ledgerJournalTrans">
    /// The buffer of <c>LedgerJournalTrans</c>.
    /// </param>
    /// <param name="_assetTrans">
    /// The buffer of <c>AssetTrans</c>.
    /// </param>
    /// <param name="_ledgerVoucher">
    /// The instance of <c>LedgerVoucher</c>.
    /// </param>
    /// <returns>
    /// An instance of <c>AssetComponentPost_JP</c>.
    /// </returns>
    public static AssetComponentPost_JP construct(
        LedgerJournalTrans_Asset    _ledgerJournalTrans_Asset,
        LedgerJournalTrans          _ledgerJournalTrans,
        AssetTrans                  _assetTrans,
        LedgerVoucher               _ledgerVoucher)
    {
        AssetComponentPost_JP assetComponentPost;

        assetComponentPost = new AssetComponentPostReversal_JP(
            _ledgerJournalTrans_Asset,
            _ledgerJournalTrans,
            _assetTrans,
            _ledgerVoucher);

        return assetComponentPost;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>