<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_VendInvoiceInfoLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
class LogisticsLocationSelectForm_VendInvoiceInfoLine extends LogisticsLocationSelectForm
{
    VendInvoiceInfoLine vendInvoiceInfoLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates the entities from which addresses can be selected.
    /// </summary>
    /// <returns>
    ///    A set of <c>LogisticsLocationSelectSourceTypes</c> enumeration types from which addresses can be
    ///    selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret = super();

        ret.add(LogisticsLocationSelectSourceType::PurchaseOrder);
        ret.add(LogisticsLocationSelectSourceType::PurchaseOrderLine);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuote);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuoteLine);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling vendor invoice line.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        vendInvoiceInfoLine.DeliveryPostalAddress = _addressRecId;
        vendInvoiceInfoLine.AddressRefRecId = _transferFromTable.RecId;
        vendInvoiceInfoLine.AddressRefTableId = _transferFromTable.TableId;
        vendInvoiceInfoLine.setDeliveryName();

        this.setTaxGroup(_addressRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType sourceType = super();
        Common addressOwner;

        if (vendInvoiceInfoLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(vendInvoiceInfoLine.AddressRefTableId, vendInvoiceInfoLine.AddressRefRecId);

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(vendInvoiceInfoLine.AddressRefTableId, sourceType);
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> record that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of the <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public RecId getLogisticsPostalAddressRecId()
    {
        return vendInvoiceInfoLine.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of the target form should have select upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (vendInvoiceInfoLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(vendInvoiceInfoLine.AddressRefTableId, vendInvoiceInfoLine.AddressRefRecId);
        }

        if (!addressOwner)
        {
            addressOwner = CompanyInfo::find();
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new(VendInvoiceInfoLine _vendInvoiceInfoLine)
    {
        vendInvoiceInfoLine = _vendInvoiceInfoLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTaxGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Copies the tax group from the address to the vendor invoice line.
    /// </summary>
    /// <param name="_addressRecId">
    ///    The record ID of an address.
    /// </param>
    private void setTaxGroup(LogisticsPostalAddressRecId _addressRecId)
    {
        TaxGroup taxGroup = LogisticsLocationExt::find(LogisticsPostalAddress::findRecId(_addressRecId).Location).TaxGroup;

        if (taxGroup)
        {
            vendInvoiceInfoLine.TaxGroup = taxGroup;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_VendInvoiceInfoLine</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A <c>LogisticsLocationSelectForm_VendInvoiceInfoLine</c> record to use to initialize the object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_VendInvoiceInfoLine</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_VendInvoiceInfoLine construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_VendInvoiceInfoLine(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>