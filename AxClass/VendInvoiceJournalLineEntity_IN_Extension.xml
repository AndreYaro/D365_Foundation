<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceJournalLineEntity_IN_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendInvoiceJournalLineEntity_IN_Extension</c> class extends the <c>VendorInvoiceLineEntity</c> data entity
/// </summary>
[ExtensionOf(tableStr(VendInvoiceJournalLineEntity))]
final class VendInvoiceJournalLineEntity_IN_Extension
{
    #ISOCountryRegionCodes
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertEntityDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts data entity for data source.
    /// </summary>
    /// <param name = "_entityCtx">The data entity runtime context.</param>
    /// <param name = "_dataSourceCtx">The data entity data source runtime context.</param>
    public boolean insertEntityDataSource(DataEntityRuntimeContext _entityCtx, DataEntityDataSourceRuntimeContext _dataSourceCtx)
    {
        boolean                             ret;
        LedgerJournalTrans                  ledgerJournalTrans;
        LedgerJournalTransTaxExtensionIN    ledgerJournalTransTaxExtensionIN;

        ret = next insertEntityDataSource(_entityCtx, _dataSourceCtx);

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN])
            && _dataSourceCtx.name() == dataEntityDataSourceStr(VendInvoiceJournalLineEntity, LedgerJournalTrans))
        {
            this.updateAssessableValueTransactionCurrency(_dataSourceCtx);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateEntityDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates data entity for data source.
    /// </summary>
    /// <param name = "_entityCtx">The data entity runtime context.</param>
    /// <param name = "_dataSourceCtx">The data entity data source runtime context.</param>
    public boolean updateEntityDataSource(DataEntityRuntimeContext _entityCtx, DataEntityDataSourceRuntimeContext _dataSourceCtx)
    {
        boolean                             ret;
        LedgerJournalTrans                  ledgerJournalTrans;
        LedgerJournalTransTaxExtensionIN    ledgerJournalTransTaxExtensionIN;

        ret = next updateEntityDataSource(_entityCtx, _dataSourceCtx);

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN])
            && _dataSourceCtx.name() == dataEntityDataSourceStr(VendInvoiceJournalLineEntity, LedgerJournalTrans))
        {
            this.updateAssessableValueTransactionCurrency(_dataSourceCtx);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateAssessableValueTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// update assessable value for ledgerJournalTransTaxExtensionIN.
    /// </summary>
    /// <param name = "_dataSourceCtx">The data entity data source runtime context.</param>
    private void updateAssessableValueTransactionCurrency(DataEntityDataSourceRuntimeContext _dataSourceCtx)
    {
        LedgerJournalTrans                  ledgerJournalTrans;
        LedgerJournalTransTaxExtensionIN    ledgerJournalTransTaxExtensionIN;

        ttsbegin;
        ledgerJournalTrans = _dataSourceCtx.getBuffer();
        ledgerJournalTransTaxExtensionIN = LedgerJournalTransTaxExtensionIN::findByLedgerJournalTrans(ledgerJournalTrans.RecId, true);

        if (ledgerJournalTrans && ledgerJournalTransTaxExtensionIN)
        {
            ledgerJournalTransTaxExtensionIN.LedgerJournalTrans = ledgerJournalTrans.RecId;
            ledgerJournalTransTaxExtensionIN.AssessableValueTransactionCurrency = ledgerJournalTrans.AmountCurDebit ? ledgerJournalTrans.AmountCurDebit : ledgerJournalTrans.AmountCurCredit;
            ledgerJournalTransTaxExtensionIN.Update();
        }
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>