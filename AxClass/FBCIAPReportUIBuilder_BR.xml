<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBCIAPReportUIBuilder_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// UI builder class for the CIAP report.
/// </summary>
class FBCIAPReportUIBuilder_BR extends SrsReportDataContractUIBuilder
{
    DialogField                         fiscalEstablishmentIdField;
    DialogField                         fiscalEstablishmentDataAreaField;
    DialogField                         fromDateField;
    DialogField                         toDateField;
    FormRun                             formRun;

    FBCIAPReportDataContract_BR          contract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the dialog box for the report.
    /// </summary>
    public void build()
    {
        contract = this.dataContractObject() as FBCIAPReportDataContract_BR;

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalEstablishmentLookup</Name>
				<Source><![CDATA[
    private void fiscalEstablishmentLookup(FormControl _formControl)
    {
        Args                args            = new Args(formStr(FBTaxAssessmentLookup_BR));
        FormStringControl   stringControl   = _formControl;

        args.caller(this);
        args.parmEnumType(enumNum(TaxType_BR));
        args.parmEnum(TaxType_BR::ICMS);
        formRun = classFactory::formRunClassOnClient(args);
        formRun.init();

        stringControl.performFormLookup(formRun);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalEstablishmentModified</Name>
				<Source><![CDATA[
    private boolean fiscalEstablishmentModified(FormControl _formControl = null)
    {
        Common              common;
        FBTaxAssessment_BR  taxAssessment;
        FBBookingPeriod_BR  bookingPeriod;

        if (formRun)
        {
            common = formRun.selectRecordModeSelectedRecord();

            if (common.TableId == tableNum(FBTaxAssessment_BR))
            {
                taxAssessment = common;
                bookingPeriod = taxAssessment.bookingPeriod();
                fiscalEstablishmentIdField.value        (bookingPeriod.FiscalEstablishmentId);
                fiscalEstablishmentDataAreaField.value  (bookingPeriod.FiscalEstablishmentDataArea);
                fromDateField.value                     (taxAssessment.FromDate);
                toDateField.value                       (taxAssessment.ToDate);
                contract.parmFBTaxAssessmentRecId       (taxAssessment.RecId);

                return true;
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the input dialog caption and changes the dimension focus dialog field to appear as lookup.
    /// </summary>
    public void postBuild()
    {
        fiscalEstablishmentIdField = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(FBCIAPReportDataContract_BR, parmFiscalEstablishmentId));

        fiscalEstablishmentIdField.lookupButton(2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides this method in order to register the dialog field methods to capture events.
    /// </summary>
    public void postRun()
    {
        Dialog dialogLocal = this.dialog();

        super();

        // This method should be called in order to handle events on dialogs.
        dialogLocal.dialogForm().formRun().controlMethodOverload(false);

        fiscalEstablishmentIdField       = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBCIAPReportDataContract_BR, parmFiscalEstablishmentId));
        fiscalEstablishmentDataAreaField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBCIAPReportDataContract_BR, parmFiscalEstablishmentDataArea));
        fromDateField                    = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBCIAPReportDataContract_BR, parmFromDate));
        toDateField                      = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBCIAPReportDataContract_BR, parmToDate));

        fiscalEstablishmentIdField.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(FBCIAPReportUIBuilder_BR, fiscalEstablishmentLookup), this);
        fiscalEstablishmentIdField.registerOverrideMethod(methodStr(FormStringControl, modified), methodStr(FBCIAPReportUIBuilder_BR, fiscalEstablishmentModified), this);
        FiscalEstablishmentIdField.label("@GLS220103");

        fiscalEstablishmentDataAreaField.allowEdit(false);
        fromDateField                   .allowEdit(false);
        toDateField                     .allowEdit(false);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>