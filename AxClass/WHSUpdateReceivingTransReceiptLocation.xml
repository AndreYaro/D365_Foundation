<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSUpdateReceivingTransReceiptLocation</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSUpdateReceivingTransReceiptLocation
{
    private InventTransferLine transferLine;
    private WMSLocationId startLocationId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateReceivingTransReceiptLocation</Name>
				<Source><![CDATA[
    internal void updateReceivingTransReceiptLocation()
    {
        InventDim           inventDim;
        InventTrans         inventTrans;
        InventTransOrigin   inventTransOrigin;

        ttsbegin;

        while select forupdate inventTrans
            where  inventTrans.StatusReceipt == StatusReceipt::Ordered
                && inventTrans.StatusIssue == StatusIssue::None
        join inventDim
            where  inventDim.InventDimId == inventTrans.InventDimId
                && inventDim.wMSLocationId != startLocationId
                && inventDim.wMSLocationId != ''
        exists join inventTransOrigin
            where  inventTransOrigin.RecId == inventTrans.InventTransOrigin
                && inventTransOrigin.InventTransId == transferLine.InventTransIdReceive
        {
            InventDim newTransInventDim;

            newTransInventDim.initFromInventDim(inventDim);
            newTransInventDim.wMSLocationId = startLocationId;
            newTransInventDim.clearLicensePlateIfInvalid();

            inventTrans.InventDimId = InventDim::findOrCreate(newTransInventDim).InventDimId;
            inventTrans.update();
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromTransferLineAndStartLocationId</Name>
				<Source><![CDATA[
    internal static WHSUpdateReceivingTransReceiptLocation newFromTransferLineAndStartLocationId(InventTransferLine _transferLine, WMSLocationId _startLocationId)
    {
        WHSUpdateReceivingTransReceiptLocation updateReceivingTransReceiptLocation = new WHSUpdateReceivingTransReceiptLocation();
        updateReceivingTransReceiptLocation.transferLine = _transferLine;
        updateReceivingTransReceiptLocation.startLocationId = _startLocationId;

        return updateReceivingTransReceiptLocation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>