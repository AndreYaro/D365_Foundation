<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchCORDeliveryDueWorkflow</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PurchCORDeliveryDueWorkflow</c> class is used to run a Delivery due date notification workflow.
/// </summary>
class PurchCORDeliveryDueWorkflow extends PurchCORWorkflow
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canSubmit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the <c>PurchLine</c> record can be submitted to the workflow.
    /// </summary>
    /// <returns>
    /// true if the <c>PurchLine</c> record can be submitted to the workflow; otherwise, false.
    /// </returns>
    /// <remarks>
    /// A COR policy must exist for the purchase line, the <c>wfDeliveryDueState</c> workflow state field
    /// must have the not submitted value, and an active workflow configuration must be found.
    /// </remarks>
    protected boolean canSubmit()
    {
        if (!this.checkPolicy())
        {
            return false;
        }

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>completeAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the name of a menu item that is used when taking the complete action.
    /// </summary>
    /// <returns>
    /// A menu item name.
    /// </returns>
    protected menuItemNameAction completeAction()
    {
        return menuitemActionStr(PurchCORDeliveryDueTaskComplete);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPurchLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the document record as a <c>PurchLine</c> record.
    /// </summary>
    /// <returns>
    ///    A <c>PurchLine</c> record.
    /// </returns>
    protected PurchLine getPurchLine()
    {
        return this.parmDocumentRecord() as PurchLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getState</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the <c>PurchLine</c> state.
    /// </summary>
    /// <returns>
    ///    The <c>PurchLine</c> state value.
    /// </returns>
    protected TradeWorkflowState getState()
    {
        return this.getPurchLine().WfDeliveryDueState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkflowType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the name of a workflow type that is used when starting the workflow.
    /// </summary>
    /// <returns>
    /// A workflow type name.
    /// </returns>
    protected workflowTypeName getWorkflowType()
    {
        return PurchCORDeliveryDueWorkflow::templateName();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setState</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the workflow state
    /// </summary>
    protected void setState()
    {
        PurchLine::setDeliveryDueWorkflowState(this.getPurchLine().RecId, TradeWorkflowState::Submitted);
    }

]]></Source>
			</Method>
			<Method>
				<Name>cancelDeliveryDateWorkflow</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Cancels the <c>DeliveryDate</c> workflows on the <c>PurchLine</c> record.
    /// </summary>
    /// <param name="_purchLine">
    ///    The workflow document <c>PurchLine</c> record.
    /// </param>
    static void cancelDeliveryDateWorkflow(PurchLine _purchLine)
    {
        PurchCORDeliveryDueWorkflow::newPurchLine(_purchLine).cancelWorkflow();
    }

]]></Source>
			</Method>
			<Method>
				<Name>activateCompleteTask</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Activates the <c>Complete</c> task action on the workflow.
    /// </summary>
    /// <param name="_purchLine">
    ///    The workflow document <c>PurchLine</c> record.
    /// </param>
    static void activateCompleteTask(PurchLine _purchLine)
    {
        PurchCORDeliveryDueWorkflow::newPurchLine(_purchLine).completeTask();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates an instance of the <c>PurchCORDeliveryDueWorkflow</c> class.
    /// </summary>
    /// <returns>
    ///     An instance of the <c>PurchCORDeliveryDueWorkflow</c> class.
    /// </returns>
    public static PurchCORDeliveryDueWorkflow construct()
    {
        return new PurchCORDeliveryDueWorkflow();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Submits a purchase line document and activates the corresponding workflow.
    /// </summary>
    /// <param name="_args">
    ///    The arguments of the <c>PurchCORDeliveryDueWorkflow</c> class.
    /// </param>
    public static void main(Args _args)
    {
        PurchCORDeliveryDueWorkflow purchCORDeliveryDueWorkflow;

        if (_args.dataset() != tableNum(PurchLine))
        {
            throw error(strFmt("@SYS19306",funcName()));
        }

        purchCORDeliveryDueWorkflow = PurchCORDeliveryDueWorkflow::newPurchLine(_args.record());

        if (purchCORDeliveryDueWorkflow.canSubmit())
        {
            purchCORDeliveryDueWorkflow.activateWorkflow(_args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newPurchLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates an instance of the <c>PurchCORDeliveryDueWorkflow</c> class.
    /// </summary>
    /// <param name="_purchLine">
    ///    The <c>PurchLine</c> record on which to submit the workflow.
    /// </param>
    /// <returns>
    ///     An instance of the <c>PurchCORDeliveryDueWorkflow</c> class.
    /// </returns>
    public static PurchCORDeliveryDueWorkflow newPurchLine(PurchLine _purchLine)
    {
        PurchCORDeliveryDueWorkflow purchCORDeliveryDueWorkflow = PurchCORDeliveryDueWorkflow::construct();

        purchCORDeliveryDueWorkflow.parmDocumentRecord(_purchLine);

        return purchCORDeliveryDueWorkflow;
    }

]]></Source>
			</Method>
			<Method>
				<Name>activatePurchaseWorkflow</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Activates the purchase requisition workflow.
    /// </summary>
    /// <param name="_purchLine">
    ///    The <c>PurchLine</c> record on which to submit the workflow.
    /// </param>
    /// <param name="_workflowComment">
    ///    The workflow comment to submit.
    /// </param>
    /// <remarks>
    ///    Activates the workflow configuration by calling the <c>ActivateFromWorkflowtemplate</c> method on
    ///    the <c>Workflow</c> class that has the information about the purchase line record.
    /// </remarks>
    public static void activatePurchaseWorkflow(PurchLine _purchLine, WorkflowComment _workflowComment = "@SYS303447")
    {
        PurchCORDeliveryDueWorkflow::newPurchLine(_purchLine).submit(_workflowComment);
    }

]]></Source>
			</Method>
			<Method>
				<Name>templateName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the AOT element name of this workflow.
    /// </summary>
    /// <returns>
    ///    The <c>WorkflowTemplateName</c> value of this workflows template element.
    /// </returns>
    public static workflowTypeName templateName()
    {
        return workFlowTypeStr(PurchCORNotifyDeliveryDue);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>