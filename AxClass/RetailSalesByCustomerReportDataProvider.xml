<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSalesByCustomerReportDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using ACM = Microsoft.Dynamics.AX.Analytics.CalculationModel;

/// <summary>
/// The <c>RetailSalesByCustomerReportDataProvider</c> class is the
/// data provider for retail sales by customers report.
/// </summary>
[SRSReportParameterAttribute(classstr(RetailReportParametersDataContract))]
class RetailSalesByCustomerReportDataProvider extends RetailSrsReportDataProviderBase
{
    RetailSalesByCustomerReportTable t;
    RetailReportParametersDataContract dc;

    ACM.DynamicMeasureDefinition amount;
    ACM.DynamicMeasureDefinition cogs;
    ACM.DynamicMeasureDefinition quantity;
    ACM.DynamicMeasureDefinition grossProfitMargin;
    ACM.NumericExpression grossProfitMarginPercentage;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is called by the SSRS framework to bring back data. It gets called after processReport.
    /// </summary>
    /// <returns>table buffer</returns>
    [SRSReportDataSetAttribute(tableStr(RetailSalesByCustomerReportTable))]
    public RetailSalesByCustomerReportTable getTable()
    {
        select t;
        return t;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new ()
    {
        super();
        amount = new ACM.DynamicMeasureDefinition(
            tableStr(RetailSalesByCustomerView),
            fieldStr(RetailSalesByCustomerView, LineAmountMST),
            ACM.Aggregation::Sum
        );
        cogs = new ACM.DynamicMeasureDefinition(
            tableStr(RetailSalesByCustomerView),
            fieldStr(RetailSalesByCustomerView, COGS),
            ACM.Aggregation::Sum
        );
        quantity = new ACM.DynamicMeasureDefinition(
            tableStr(RetailSalesByCustomerView),
            fieldStr(RetailSalesByCustomerView, Qty),
            ACM.Aggregation::Sum
        );
        grossProfitMargin = new ACM.DynamicMeasureDefinition(
            tableStr(RetailSalesByCustomerView),
            fieldStr(RetailSalesByCustomerView, GrossProfitMargin),
            ACM.Aggregation::Sum
        );
        ACM.LogicalExpression isTaxAmountZero = amount.CompareEquals(0);
        grossProfitMarginPercentage = new ACM.If(
            isTaxAmountZero,
            ACM.Null::ConstValue,
            grossProfitMargin.Divide(amount)
        );
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is called by the SSRS report framework to execute the report queries.
    /// </summary>
    public void processReport()
    {
        try
        {
            SysQueryRun::ExecuteInManagedStack(true);

            dc = this.parmDataContract();
            this.InsertChannelsToTmpTable(dc.parmHierarchyRelationId());

            Query q = new Query();
            QueryBuildDataSource qbds = q.addDataSource(tableNum(RetailSalesByCustomerView));

            qbds.addSelectionFieldWithAlias('AccountNum', fieldNum(RetailSalesByCustomerView, AccountNum));
            qbds.addSelectionFieldWithAlias('Name', fieldNum(RetailSalesByCustomerView, Name));
            qbds.addCalculationField(amount, 'LineAmountMST');
            qbds.addCalculationField(quantity, 'Qty');
            qbds.addCalculationField(grossProfitMargin, 'GrossProfitMargin');
            qbds.addCalculationField(grossProfitMarginPercentage, 'GrossProfitMarginPercentage');

            qbds.addGroupByField(fieldNum(RetailSalesByCustomerView, AccountNum));
            qbds.addGroupByField(fieldNum(RetailSalesByCustomerView, Name));

            qbds.addRange(fieldNum(RetailSalesByCustomerView, InvoiceDate)).value(this.buildDateRangeForSpecifiedField(dc.parmFromDate(), dc.parmToDate(), fieldStr(RetailSalesByCustomerView, InvoiceDate)));
            
            //Add join with RetailReportChannelTable.
            this.JoinChannelFilter(qbds, fieldNum(RetailSalesByCustomerView, RetailChannel));

            this.addSortOrder(qbds);
            q.topRows(dc.parmTopCount());

            QueryRun::runAndPopulate(q, t, this.buildMapping());

            this.ClearChannelFilterTable();
        }
        finally
        {
            SysQueryRun::ExecuteInManagedStack(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addSortOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Apply order by clause to datasource based on paramters.
    /// </summary>
    /// <param name = "ds">target datasource</param>
    private void addSortOrder(QueryBuildDataSource ds)
    {
        switch (dc.parmOrderBy())
        {
            case RetailReportOrderBy::Amount:
                ds.addOrderByCalculationField(amount, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Quantity:
                ds.addOrderByCalculationField(quantity, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Profit:
                ds.addOrderByCalculationField(grossProfitMargin, SortOrder::Descending);
                break;
            case RetailReportOrderBy::ProfitPercentage:
                ds.addOrderByCalculationField(grossProfitMarginPercentage, SortOrder::Descending);
                break;
            default:
                throw Global::error("@Retail:RetailReportOrderByNotSupported");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildMapping</Name>
				<Source><![CDATA[
    /// <summary>
    /// Build mapping between query result and temp table buffer.
    /// </summary>
    /// <returns>Mapping</returns>
    private Map buildMapping()
    {
        Map m = new Map(Types::String, Types::String);
        m.insert('AccountNum', fieldStr(RetailSalesByCustomerReportTable, CustAccount));
        m.insert('Name', fieldStr(RetailSalesByCustomerReportTable, CustomerName));
        m.insert('LineAmountMST', fieldStr(RetailSalesByCustomerReportTable, Amount));
        m.insert('Qty', fieldStr(RetailSalesByCustomerReportTable, Quantity));
        m.insert('GrossProfitMargin', fieldStr(RetailSalesByCustomerReportTable, Margin));
        m.insert('GrossProfitMarginPercentage', fieldStr(RetailSalesByCustomerReportTable, Percentage));
        return m;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>