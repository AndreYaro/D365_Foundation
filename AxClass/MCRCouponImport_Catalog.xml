<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRCouponImport_Catalog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>MCRCouponImport_Catalog</c> class handles the import process of catalogs.
/// </summary>
class MCRCouponImport_Catalog extends MCRCouponImport
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preImport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares for import by deleting the catalogs associated with a coupon if append is unchecked.
    /// </summary>
    void preImport()
    {
        MCRCouponCatalog mcrCouponCatalog;

        if (!this.parmAppend()
            && this.parmMCRCouponId() != "")
        {
            delete_from mcrCouponCatalog
                where mcrCouponCatalog.CouponId == this.parmMCRCouponId();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>importRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Imports catalog record for a given coupon.
    /// </summary>
    /// <param name = "_friendlyName">The friendly name for the catalog to import.</param>
    /// <param name = "_insertList">The record insert list instance to add the record to.</param>
    public void importRecord(Name _friendlyName, RecordInsertList _insertList)
    {
        CatalogTranslation  catalogTranslation;
        MCRCouponCatalog    mcrCouponCatalog;

        select firstonly Catalog, RecId from catalogTranslation
                    where catalogTranslation.FriendlyName == _friendlyName;

        if (catalogTranslation && this.validateCatalog(catalogTranslation.Catalog))
        {
            mcrCouponCatalog.CouponId = this.parmMCRCouponId();
            mcrCouponCatalog.Catalog = catalogTranslation.Catalog;
            mcrCouponCatalog.CatalogCode = MCRTableAll::Table;
            _insertList.add(mcrCouponCatalog);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCatalog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that a catalog is valid and can be imported and associated with a coupon.
    /// </summary>
    /// <param name="_catalog">
    /// The reference to the catalog to validate.
    /// </param>
    /// <returns>
    /// true if the catalog is valid to be linked to a coupon; Otherwise, false.
    /// </returns>
    boolean validateCatalog(CatalogRefRecId     _catalog)
    {
        boolean ok = true;
        Catalog catalog = Catalog::find(_catalog);

        if (!catalog)
        {
            ok = checkFailed(strFmt("@MCR40353", _catalog));
        }
        if (catalog.ValidTo && catalog.ValidTo < DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()))
        {
            warning(strFmt("@MCR40318", catalog.name()));
        }
        return ok;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>