<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetBalancesPeriodGERDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AssetBalancePeriodGERDP</c> class declares the global variables and tables for the
/// AssetBalancesPeriodGER (Fixed asset movements) report.
/// </summary>
internal final class AssetBalancesPeriodGERDP
{
    private AssetSumCalc assetSumCalc;
    private FromDate fromDate;
    private ToDate toDate;
    private boolean incomeTaxActDepEnabled;
    private AssetSumCalc_Trans_IN assetSumCalc_Trans_IN;

    // French derogatory information.
    private PrintFASetupInfo_FR printFAInfo_FR;
    private AssetBookTable assetBookTable;
    private AssetBook assetBook;
    private AssetTable assetTable;
    private AssetBalancesPeriodTmp assetBalancesPeriodTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static AssetBalancesPeriodGERDP construct()
    {
        AssetBalancesPeriodGERDP dataProvider;
        dataProvider = new AssetBalancesPeriodGERDP();
        return dataProvider;
    }

]]></Source>
			</Method>
			<Method>
				<Name>assetSetupValue</Name>
				<Source><![CDATA[
    private AssetDerogatorySetupInfo_FR assetSetupValue()
    {
        str displayAssetSetupFormat;

        AssetDepreciationProfile depreciationProfile;
        AssetDepreciationMethod profileMethod;

        // Print FA information if the user chooses to do so.
        if (printFAInfo_FR)
        {
            // The setup infor will have the following values:
            // Value model code;Depreciation method;Start Date;Service Life;RBSL %.
            depreciationProfile = assetBook.depreciationProfile();

            if (depreciationProfile)
            {
                profileMethod = depreciationProfile.Method;
            }

            displayAssetSetupFormat = '%1;%2;%3;%4;%5';
            displayAssetSetupFormat =  strfmt(displayAssetSetupFormat, assetBook.BookId, profileMethod,
                assetBook.DepreciationStartDate, assetBook.LifeTime, assetBook.rbslFactor);
        }

        return displayAssetSetupFormat;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssetBalancesPeriodTmp</Name>
				<Source><![CDATA[
    internal AssetBalancesPeriodTmp getAssetBalancesPeriodTmp(AssetBalancesPeriodGERContract _contract, Query _query)
    {
        #ISOCountryRegionCodes
        boolean isLatvia;

        AssetBalancesPeriodGERContract contract = _contract;
        Query query = _query;
        QueryRun queryRun = new QueryRun(query);

        fromDate = contract.parmFromDate();
        toDate = contract.parmToDate();
        LedgerCurrencySelection currencySelected = contract.parmCurrencySelection();

        if (contract.parmPrint() == true)
        {
            printFAInfo_FR = NoYes::Yes;
        }
        else
        {
            printFAInfo_FR = NoYes::No;
        }

        isLatvia = SysCountryRegionCode::isLegalEntityInCountryRegion([#isoLV]);

        incomeTaxActDepEnabled = AssetParameters::isDepreciationIncomeTaxActEnable_IN();

        while (queryRun.next())
        {
            assetBookTable = queryRun.get(tableNum(AssetBookTable)) as AssetBookTable;
            assetBook = queryRun.get(tablenum(AssetBook)) as AssetBook;
            assetTable = queryRun.get(tablenum(AssetTable)) as AssetTable;

            if (currencySelected == LedgerCurrencySelection::Accounting)
            {
                assetSumCalc = AssetSumCalc_Trans::newAssetPeriod(assetBook.AssetId, assetBook.BookId, fromDate, toDate);
            }
            else
            {
                assetSumCalc = AssetSumCalc_Trans::newAssetPeriodByCurrency(assetBook.AssetId, assetBook.BookId, fromDate, toDate, AssetSumCalcType::Reporting);
            }

            if (incomeTaxActDepEnabled)
            {
                // Used to get group net book value.
                assetSumCalc_Trans_IN = AssetSumCalc_Trans_IN::newAssetPeriod(
                    assetBook.AssetGroup,
                    assetBook.bookId,
                    fromDate,
                    toDate);
            }

            this.insertAssetBalancesPeriodTmp(isLatvia);
        }

        select assetBalancesPeriodTmp;
        return assetBalancesPeriodTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssetBalancesPeriodTmp</Name>
				<Source><![CDATA[
    private void insertAssetBalancesPeriodTmp(boolean isLatvia = false)
    {
        assetBalancesPeriodTmp.AssetSetup = this.assetSetupValue();
        assetBalancesPeriodTmp.FromDate = fromDate;
        assetBalancesPeriodTmp.ToDate = toDate;
        assetBalancesPeriodTmp.AssetName = assetTable.Name;

        if (isLatvia)
        {
            assetBalancesPeriodTmp.AcquisitionValue = assetSumCalc.acquisitionValue();
            assetBalancesPeriodTmp.WriteUpAdjValue = assetSumCalc.writeUpAdjValue() +
                assetSumCalc.writeDownAdjValue() +
                assetSumCalc.acquisitionAdjValue() +
                assetSumCalc.capitalReserveTransferValue();
        }
        else
        {
            assetBalancesPeriodTmp.AcquisitionValue = assetSumCalc.acquisitionValueAll();
            assetBalancesPeriodTmp.WriteUpAdjValue = assetSumCalc.writeUpDownAdjValue();
        }

        assetBalancesPeriodTmp.Bonus = assetSumCalc.bonusDepreciationValue();
        assetBalancesPeriodTmp.DepreciationValue = assetSumCalc.depreciationValueAll() + assetSumCalc.extraDepreciationValue();
        assetBalancesPeriodTmp.DerogatoryValue = assetSumCalc.derogatoryValue();
        assetBalancesPeriodTmp.DisposalSaleValue = assetSumCalc.disposalValue();
        assetBalancesPeriodTmp.RevaluationValue = assetSumCalc.revaluationValue();
        assetBalancesPeriodTmp.ProfitLossValue = assetSumCalc.profitLossValue() + assetSumCalc.capitalReserveValue();
        assetBalancesPeriodTmp.NetBookValue = assetSumCalc.netBookValue();
        assetBalancesPeriodTmp.AssetGroup = assetBook.AssetGroup;
        assetBalancesPeriodTmp.AssetId = assetBook.AssetId;
        assetBalancesPeriodTmp.Status = assetBook.Status;
        assetBalancesPeriodTmp.CurrentOperationsTax = assetBookTable.CurrentOperationsTax;
        assetBalancesPeriodTmp.BookId = assetBook.BookId;
        if (incomeTaxActDepEnabled)
        {
            assetSumCalc_Trans_IN.parmGroup_IN(NoYes::Yes);
            assetBalancesPeriodTmp.ShowGroupNetBookValue_IN     = NoYes::Yes;
            // Calculates the profit loss value first, because it affects the amount of group net book value.
            assetSumCalc_Trans_IN.profitLossValue();
            // Returns the group net book value to tmp table.
            assetBalancesPeriodTmp.AssetGroupNetBookValue_IN    = assetSumCalc_Trans_IN.netBookValue();
        }

        assetBalancesPeriodTmp.insert();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>