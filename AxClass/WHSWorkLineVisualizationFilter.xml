<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSWorkLineVisualizationFilter</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>WHSWorkTableVisualizationFilter</c> is specialized to handle the <c>WHSWorkLineDetails</c> list page.
/// </summary>
[WHSOutboundWorkLoadVisualizationFactoryAttribute(formStr(WHSWorkLineDetails))]
internal final class WHSWorkLineVisualizationFilter extends WHSWorkLoadVisualizationFilter
{
    private QueryBuildDataSource qbdsWorkHeader;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>workHeaderDataSource</Name>
				<Source><![CDATA[
    private QueryBuildDataSource workHeaderDataSource()
    {
        if (!qbdsWorkHeader)
        {
            qbdsWorkHeader = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine)).addDataSource(tableNum(WHSWorkTable));
            qbdsWorkHeader.relations(true);
            qbdsWorkHeader.joinMode(JoinMode::ExistsJoin);
        }

        return qbdsWorkHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyGroupTableFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyGroupTableFilter()
    {
        if  (filter.XAxisGroupTableId == tableNum(WHSWorkLine))
        {
            QueryBuildDataSource qbds = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine));
            this.setRangeValue(qbds, filter.XAxisGroupTableId, filter.XAxisGroupFieldId, parameters.parameter2());
        }
        else if (filter.XAxisGroupTableId == tableNum(WHSWorkTable))
        {
            this.setRangeValue(this.workHeaderDataSource(), filter.XAxisGroupTableId, filter.XAxisGroupFieldId, parameters.parameter2());            
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyValueTableFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyValueTableFilter()
    {
        if (filter.XAxisValueTableId == tableNum(WHSWorkLine))
        {
            QueryBuildDataSource qbds = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine));
            this.setRangeValue(qbds, filter.XAxisValueTableId, filter.XAxisValueFieldId, parameters.parameter1());            
        }
        else if (filter.XAxisValueTableId == tableNum(WHSWorkTable))
        {
            this.setRangeValue(this.workHeaderDataSource(), filter.XAxisValueTableId, filter.XAxisValueFieldId, parameters.parameter1());            
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>applySiteFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applySiteFilter()
    {
        if (filter.InventSiteId)
        {
            this.workHeaderDataSource().addRange(fieldNum(WHSWorkTable, InventSiteId))
                .value(SysQuery::value(filter.InventSiteId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyLocationFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyLocationFilter()
    {
        if (filter.InventLocationId)
        {
            this.workHeaderDataSource().addRange(fieldNum(WHSWorkTable, InventLocationId))
                .value(SysQuery::value(filter.InventLocationId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyCreatedDateTimeFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyCreatedDateTimeFilter()
    {
        int daysToInclude = (filter.DaysToInclude > 0) ? (filter.DaysToInclude - 1) : 0;

        this.workHeaderDataSource().addRange(fieldNum(WHSWorkTable, CreatedDateTime))
            .value(strFmt('> %1', SysQueryRangeUtil::value(DateTimeUtil::addDays(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone()), -daysToInclude))));
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyPickLineFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyPickLineFilter()
    {
        QueryBuildDataSource qbds = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine));

        qbds.addRange(fieldNum(WHSWorkLine, WorkType))
            .value(queryValue(WHSWorkType::Pick));
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyChartPickingTypeFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void applyChartPickingTypeFilter()
    {
        // In case of initial or staged picks add a join to the FirstPutWorkLine view
        if (this.filter.PickingType == WHSChartPickingType::InitialPicks)
        {
            this.populatePickingFilter('(%1.%2 > %3.%4)');
        }
        else if (this.filter.PickingType == WHSChartPickingType::StagedPicks)
        {
            this.populatePickingFilter('(%1.%2 < %3.%4)');
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>populatePickingFilter</Name>
				<Source><![CDATA[
    private void populatePickingFilter(str _condition)
    {
        QueryBuildDataSource putWorkLineView = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine)).addDataSource(tablenum(WHSFirstPutWorkLineView));
            
        putWorkLineView.relations(false);
        putWorkLineView.clearLinks();
        putWorkLineView.joinMode(JoinMode::InnerJoin);

        putWorkLineView.addLink(fieldNum(WHSWorkLine, WorkId), fieldNum(WHSFirstPutWorkLineView, WorkId));
            
        QueryBuildDataSource workLine = formDataSource.query().dataSourceTable(tableNum(WHSWorkLine));

        putWorkLineView.addRange(fieldNum(WHSFirstPutWorkLineView, LineNum))
                .value(strFmt(_condition,
                        putWorkLineView.name(),
                        fieldStr(WHSFirstPutWorkLineView, LineNum),
                        workLine.name(),
                        fieldStr(WHSWorkLine, LineNum)));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>