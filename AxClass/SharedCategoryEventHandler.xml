<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SharedCategoryEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
static class SharedCategoryEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkIfExpenseAndProduction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prevents a category from being used for both expense and production roles at the same time.
    /// </summary>
    /// <param name = "_sharedCategory">The shared category.</param>
    /// <param name = "_categoryRoleType">The role type being set.</param>
    /// <param name = "_canBeUsed">If the role can be used or not.</param>
    /// <param name = "_result">The event handler results.</param>
    [SubscribesTo(tableStr(SharedCategory), staticDelegateStr(SharedCategory, checkIfValidCategoryType))]
    public static void checkIfExpenseAndProduction(SharedCategory _sharedCategory, CategoryRoleType _categoryRoleType, NoYes _canBeUsed, EventHandlerResult _result)
    {
        boolean checkPassed = true;

        if (_canBeUsed)
        {
            if ( _categoryRoleType == CategoryRoleType::Expense)
            {
                CategoryTable       categoryTable;
                ProjCategory        projCategory;

                //check company-specific category reference with non expense transactions
                select firstonly crossCompany RecId from categoryTable
                    where categoryTable.SharedCategory == _sharedCategory.RecId
                        && categoryTable.UseInProject == NoYes::Yes
                    join recId from projCategory
                        where projCategory.CategoryId == categoryTable.CategoryId
                        && projCategory.CategoryType != ProjCategoryType::Cost
                        && categoryTable.dataAreaId == projCategory.dataAreaId;

                if (categoryTable.RecId)
                {
                    checkPassed = checkPassed && checkFailed("@SYS341258");
                }
            }

            if ((_sharedCategory.canBeUsedInExpense(false, NoYes::No) && _categoryRoleType == CategoryRoleType::Production)
            || (_sharedCategory.canBeUsedInProduction(false, NoYes::No) && _categoryRoleType == CategoryRoleType::Expense))
            {
                checkPassed = checkPassed && checkFailed("@SYS105286");
            }

            _result.booleanResult(checkPassed);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>