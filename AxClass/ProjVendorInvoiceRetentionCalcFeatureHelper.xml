<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjVendorInvoiceRetentionCalcFeatureHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class ProjVendorInvoiceRetentionCalcFeatureHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isFeatureEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether vendor retain amount should consider for cost amount calculation for vendor invoice.
    /// This feature is always considered enabled if customer is using Purchae Orders with Project Operations integration.
    /// </summary>
    /// <returns>
    /// true if the feature in Feature Management is enabled or customer is using purchase orders with proj ops integration; otherwise, false.
    /// </returns>
    public static boolean isFeatureEnabled()
    {
        return FeatureStateProvider::isFeatureEnabled(ProjVendorInvoiceRetentionCalcFeature::instance()) 
            || ProjProjectOperationsIntegrationFeatureHelper::isPurchaseOrderEnabledWithProjOps();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isTransitionFlightEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether <c>ProjVendorInvoiceRetentionCalcFeature</c> feature transition flight 
    /// <c>ProjVendorInvoiceRetentionCalcTransitionFlight</c> is enabled or not.
    /// This flight allows smooth project vendor retention partially posted purchase order transition when user enable the feature.
    /// </summary>
    /// <returns>
    /// true if the project vendor invoice retention calculation feature transition flight is enabled; otherwise, false.
    /// </returns>
    public static boolean isTransitionFlightEnabled()
    {
        boolean isTransitionFlightEnabled = FeatureStateProvider::isFeatureEnabled(ProjVendorInvoiceRetentionCalcTransitionFlight::instance());
        return isTransitionFlightEnabled;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldProcessWithFeatureEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if it should be processed as feature is enabled.
    /// </summary>
    /// <param name = "_isFeatureEnabled">Whether if feature is enabled. If feature is not enabled, then it will return not enabled.</param>
    /// <param name = "_inventTransId">The transaction <c>InventTransId</c>. This value exists in tables <c>PurchLine</c>, <c>VendInvoiceTrans</c>. For non-stocked item, it will be the same as <c>ProjTransPosting</c> InventTransId.</param>
    /// <returns>true if it should be processed as feature is enabled; otherwise, false.</returns>
    /// <remarks>
    /// Outcome is determined by <c>_isFeatureEnabled</c>. If this is false, then it will return false. Else it will look up by <c>InventTransId</c> if it should processed as feature enabled.
    /// </remarks>
    public static boolean shouldProcessWithFeatureEnabled(boolean _isFeatureEnabled, InventTransId _inventTransId)
    {
        boolean shouldProcessWithFeatureEnabled;

        if (_isFeatureEnabled
            && _inventTransId 
            && ProjVendorInvoiceRetentionCalcFeatureHelper::isTransitionFlightEnabled())
        {
            // Each PO line has a unique InventTransId. This is being used to lookup if the PO line has posted
            // any vendor invoice before the feature is enabled. If so, then it should be treated as 
            // the feature is not enabled from any further vendor invoice posting from this PO line.
            ProjNotQualifyVendorRetentionFeatureByInventTransIdView notQualifyForVendorRetentionFeature;

            select firstonly InventTransIdPurchLine from notQualifyForVendorRetentionFeature
                where notQualifyForVendorRetentionFeature.InventTransIdPurchLine == _inventTransId;

            if (!notQualifyForVendorRetentionFeature.InventTransIdPurchLine)
            {
                shouldProcessWithFeatureEnabled = true;
            }
        }
        
        return shouldProcessWithFeatureEnabled;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldProcessWithFeatureEnabledBySouceDocumentLineItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if it should be processed as feature is enabled.
    /// </summary>
    /// <param name = "_isFeatureEnabled">Whether if feature is enabled. If feature is not enabled, then it will return not enabled.</param>
    /// <param name = "_sourceDocumentLineItem">Source document line type object.</param>
    /// <returns>true if it should be processed as feature is enabled; otherwise, false.</returns>
    /// <remarks>
    /// Outcome is determined by if <c>_sourceDocumentLineItem</c> is type vendor invoice.
    /// If it is not vendor invoice, then it will return true as feature is enabled.
    /// Else if feature is not enabled, then it will determine as feature is not enabled
    /// If this is both vendor invoice line item and feature is enable, then it will be determined
    /// by ProjVendorInvoiceRetentionCalcFeatureHelper::shouldProcessWithFeatureEnabled().
    /// </remarks>
    public static boolean shouldProcessWithFeatureEnabledBySouceDocumentLineItem(boolean _isFeatureEnabled, SourceDocumentLineItem _sourceDocumentLineItem)
    {
        boolean processAsFeatureIsEnabled;

        if (ProjVendorInvoiceRetentionCalcFeatureHelper::isTransitionFlightEnabled())
        {
            boolean isVendorInvoiceLine = _sourceDocumentLineItem is VendorInvoiceLineSourceDocLineItem ? true : false;

            // If this is not a vendor invoice, then proceed as ProjVendorInvoiceRetentionCalcFeature is eanbeld.
            processAsFeatureIsEnabled = isVendorInvoiceLine ? _isFeatureEnabled : true;

            if (isVendorInvoiceLine && processAsFeatureIsEnabled)
            {
                // It must be a vendor invoice line item at this point
                VendorInvoiceLineSourceDocLineItem vendorInvoiceLineSourceDocLineItem = _sourceDocumentLineItem as VendorInvoiceLineSourceDocLineItem;

                PurchLine purchLine = vendorInvoiceLineSourceDocLineItem.parmPurchLine();

                if (purchLine
                    && purchLine.PSARetainScheduleId != ''
                    && vendorInvoiceLineSourceDocLineItem.parmLotId()) // parmLotId() holds the value of InventTransId
                {
                    processAsFeatureIsEnabled = ProjVendorInvoiceRetentionCalcFeatureHelper::shouldProcessWithFeatureEnabled(
                        _isFeatureEnabled, vendorInvoiceLineSourceDocLineItem.parmLotId());
                }
            }
        }
        else
        {
            processAsFeatureIsEnabled = _isFeatureEnabled;
        }

        return processAsFeatureIsEnabled;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>