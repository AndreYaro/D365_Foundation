<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ConsignmentReplenishmentOrderLineType</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ConsignmentReplenishmentOrderLineType</c> class is the base class that controls the types of consignment replenishment order lines.
/// </summary>
public class ConsignmentReplenishmentOrderLineType extends InventType implements InventDistinctProductOrderDefaulting
{
    ConsignmentReplenishmentOrderLine replenishmentOrderLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(ConsignmentReplenishmentOrderLine _replenishmentOrderLine)
    {
        super(_replenishmentOrderLine);

        replenishmentOrderLine = _replenishmentOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canInventBeReserved</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether inventory reservation is allowed.
    /// </summary>
    /// <returns>Always returns false.</returns>
    public boolean canInventBeReserved()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInventTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the replenishment line with the given <c>InventTable</c> record.
    /// </summary>
    /// <param name="_inventTable">
    /// The <c>InventTable</c> record that is used to initialize the replenishment order line record.
    /// </param>
    public void initFromInventTable(InventTable _inventTable)
    {
        InventDistinctProductOrderDefaultingController::itemIdModified(this, InventDistinctProductDimensionSpecificDefaultingArgs::construct(_inventTable, true, InventItemOrderSetupType::Purch));
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromReplenishmentOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the consignment replenishment order line type class.
    /// </summary>
    /// <param name = "_replenishmentOrderLine">The consignment replenishment order line.</param>
    /// <returns>An instance of the <c>ConsignmentReplenishmentOrderLineType</c> class.</returns>
    public static ConsignmentReplenishmentOrderLineType newFromReplenishmentOrder(ConsignmentReplenishmentOrderLine _replenishmentOrderLine)
    {
        return new ConsignmentReplenishmentOrderLineType(_replenishmentOrderLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initStorageDimensionsFromHeader</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes storage dimensions from header.
    /// </summary>
    public void initStorageDimensionsFromHeader()
    {
        var inventDimDefaultedFromHeader = replenishmentOrderLine.getDefaultInventDimFromReplenishmentOrderHeader();

        this.setInventDim(inventDimDefaultedFromHeader);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventDim</c> record buffer.
    /// </summary>
    /// <param name = "_inventDim">An <c>InventDim</c> record buffer.</param>
    public void setInventDim(InventDim _inventDim)
    {
        replenishmentOrderLine.setInventDimId(_inventDim.inventDimId, _inventDim);
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>InventDim</c> record buffer.
    /// </summary>
    /// <returns>An <c>InventDim</c> record buffer.</returns>
    public InventDim inventDim()
    {
        return replenishmentOrderLine.inventDim();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initReleasedProductSpecificDefaulting</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes settings that are based on the released product.
    /// </summary>
    /// <param name = "_inventTable">An <c>InventTable</c> record buffer.</param>
    public void initReleasedProductSpecificDefaulting(InventTable _inventTable)
    {
        if (!_inventTable)
        {
            return;
        }

        InventDim inventDimDefaultProductDimension = _inventTable.getDefaultProductDimensions(this.inventDim());

        this.setInventDim(inventDimDefaultProductDimension);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initDimensionsSpecificDefaulting</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes product dimension specific order defaulting.
    /// </summary>
    /// <param name = "_inventDistinctProductDimensionSpecificDefaultingArgs">Product dimension specific order defaulting arguments.</param>
    public void initDimensionsSpecificDefaulting(InventDistinctProductDimensionSpecificDefaultingArgs _inventDistinctProductDimensionSpecificDefaultingArgs)
    {
        // Not applicable
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>