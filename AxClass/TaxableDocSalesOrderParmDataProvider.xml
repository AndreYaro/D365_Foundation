<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocSalesOrderParmDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider for sales order parm.
/// </summary>
public class TaxableDocSalesOrderParmDataProvider extends TaxableDocumentDataProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to load detail data for taxable document. And setup the row definitions.
    /// </summary>
    /// <returns>
    /// The query to load detail data for taxable document.
    /// </returns>
    /// <remarks>
    /// The row definitions must be setup here. It defines the mapping between row and data source,
    /// the child/parent relations between rows, and the row data provider class.
    /// </remarks>
    public Query buildQuery()
    {
        Query                   query;
        QueryBuildDataSource    salesParmTableDataSource;
        QueryBuildDataSource    salesParmLineDataSource;
        QueryBuildDataSource    salesLineDataSource;
        QueryBuildDataSource    markupTransLineDataSource;

        query = new Query();

        salesParmTableDataSource = query.addDataSource(tableNum(SalesParmTable));
        salesParmTableDataSource.addRange(fieldNum(SalesParmTable, RecId)).value(queryValue(rootTable.RecId));

        salesParmLineDataSource  = salesParmTableDataSource.addDataSource(tableNum(SalesParmLine));
        salesParmLineDataSource.joinMode(JoinMode::OuterJoin);
        salesParmLineDataSource.relations(true);

        salesLineDataSource = salesParmLineDataSource.addDataSource(tableNum(SalesLine));
        salesLineDataSource.joinMode(JoinMode::OuterJoin);
        salesLineDataSource.relations(true);

        markupTransLineDataSource = salesLineDataSource.addDataSource(tableNum(MarkupTrans));
        markupTransLineDataSource.joinMode(JoinMode::OuterJoin);
        markupTransLineDataSource.relations(true);

        rowDefinitions.empty();
        this.registerRow(salesParmTableDataSource.id(), 0, TaxEngineIntegrationConstants::Header, classStr(TaxableDocSalesParmTableRowDataProvider));
        this.registerRow(salesParmLineDataSource.id(), salesParmTableDataSource.id(), TaxEngineModelFieldsConstants::Lines, classStr(TaxableDocSalesParmLineRowDataProvider));
        this.registerRow(markupTransLineDataSource.id(), salesParmLineDataSource.id(), TaxEngineModelFieldsConstants::Sublines, classStr(TaxableDocMarkupTransRowDataProvider));

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>