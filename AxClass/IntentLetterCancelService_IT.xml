<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntentLetterCancelService_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>IntentLetterCancelService_IT</c> class gets actions for intent letter canceling.
/// </summary>
public class IntentLetterCancelService_IT extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runProcessing</Name>
				<Source><![CDATA[
    public void runProcessing(IntentLetterCancelDataContract_IT _dataContract)
    {
        List intentLetterList = _dataContract.parmIntentLetterList();
        ListEnumerator enumerator = intentLetterList.getEnumerator();
        IntentLetter_IT intentLetter;
        Notes notes = _dataContract.parmNotes();

        while (enumerator.moveNext())
        {
            intentLetter = enumerator.current();
            
            if (this.cancelIntentLetter(intentLetter, notes))
            {
                info(strFmt("@ExtendedItallianLocalization:LetterHasBeenCanceled", intentLetter.IntentLetterId));
            }
            else
            {
                warning(strFmt("@ExtendedItallianLocalization:LeterCannotBeCanceled", intentLetter.IntentLetterId));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>cancelIntentLetter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Cancels the intent letter.
    /// </summary>
    /// <param name = "_intentLetter">The <c>IntentLetter_IT</c> record.</param>
    /// <param name = "_notes">The intent letter note to update.</param>
    /// <returns>true if intent letter was cancelled, otherwise false.</returns>
    public boolean cancelIntentLetter(IntentLetter_IT _intentLetter, Notes _notes)
    {
        boolean ret;

        if (_intentLetter.Status == IntentLetterStatus_IT::Open && !_intentLetter.isUsedByInvoices())
        {
            ttsbegin;
            _intentLetter.selectForUpdate(true);
            _intentLetter.cancel(_notes);
            _intentLetter.update();
            ttscommit;
            ret = true;
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>