<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchCopilotFeedbackManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class PurchCopilotFeedbackManager
{
    private SysInstrumentationLogger logger;
    private FormButtonControl likeButton;
    private FormButtonControl dislikeButton;
    private str activityName;

    private const str activityNameBase = 'AIFeedback.';
    private const str metricNamespace = 'PurchCopilot';
    private const str liked = 'Liked';
    private const str disliked = 'Disliked';
    private const str cleared = 'Cleared';
    private const str feedbackPropertyName = 'Feedback';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(SysInstrumentationLogger _logger, FormButtonControl _likeButton, FormButtonControl _dislikeButton, str _name)
    {
        logger = _logger;
        likeButton = _likeButton;
        dislikeButton = _dislikeButton;
        activityName = activityNameBase + _name;

        likeButton.registerOverrideMethod(methodStr(FormButtonControl, clicked), methodStr(PurchCopilotFeedbackManager, buttonClicked), this);
        dislikeButton.registerOverrideMethod(methodStr(FormButtonControl, clicked), methodStr(PurchCopilotFeedbackManager, buttonClicked), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buttonClicked</Name>
				<Source><![CDATA[
    private void buttonClicked(FormButtonControl _control)
    {
        using (var context = logger.activityContext(activityName, metricNamespace))
        {
            str feedbackValue = _control == likeButton ? liked : disliked;
            if (_control.toggleValue() == 0)
            {
                feedbackValue += cleared;
            }

            context.addCustomProperty(feedbackPropertyName, feedbackValue);

            FormButtonControl otherControl = _control == likeButton ? dislikeButton : likeButton;
            otherControl.toggleValue(false);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>