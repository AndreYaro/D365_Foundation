<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankReconciliationHeaderController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankReconciliationHeaderReportController</c> class is the controller class for the <c>BankReconciliationHeader</c> SSRS report.
/// </summary>
class BankReconciliationHeaderController extends SrsReportRunController implements BatchRetryable
{
    BankReconciliationHeader   reconciliation;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean isRetryable()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReconciliation</Name>
				<Source><![CDATA[
        public BankReconciliationHeader parmReconciliation(BankReconciliationHeader _reconciliation = reconciliation)
    {
        reconciliation = _reconciliation;

        return reconciliation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Sets default values for the report parameters.
    /// </summary>
    public void prePromptModifyContract()
    {
        BankReconciliationHeaderContract    rdpContract;
        BankAccountTable                    bankAccountTable = reconciliation.getBankAccountTable();
        BankReconciliationHeader            prevReconciliation = BankReconciliationHeader::findPrevReconciliation(reconciliation);

        rdpContract = this.parmReportContract().parmRdpContract() as BankReconciliationHeaderContract;
        rdpContract.parmReconciliationId(reconciliation.ReconciliationId);
        rdpContract.parmAccountName(bankAccountTable.Name);
        rdpContract.parmAccountNum(bankAccountTable.AccountNum);
        rdpContract.parmCurrency(bankAccountTable.CurrencyCode);
        rdpContract.parmPeriod(strFmt("%1..%2", prevReconciliation.CutOffDate, reconciliation.CutOffDate));
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args _args)
    {
        BankReconciliationHeaderController  controller;
        BankReconciliationHeader            reconciliation;

        if (!_args || !_args.record())
        {
            throw(error(strfmt("@SYS29104", classstr(BankReconciliationHeaderController))));
        }

        reconciliation = _args.record();

        if (reconciliation)
        {
            controller = new BankReconciliationHeaderController();
            controller.parmArgs(_args);
            controller.parmReportName(ssrsReportStr(BankReconciliationHeader, Design));
            controller.parmReconciliation(reconciliation);
            controller.parmShowDialog(false);
            controller.startOperation();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>