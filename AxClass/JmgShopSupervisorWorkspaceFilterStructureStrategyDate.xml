<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgShopSupervisorWorkspaceFilterStructureStrategyDate</Name>
	<SourceCode>
		<Declaration><![CDATA[
[SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorMyProdJobListQuery)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgProdJobSummaryQuery)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorMyScheduledOrders)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorMyScheduledOrdersV2)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorNotOnOpenWave)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorNotOnOpenWaveV2)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorUnprocessedWaves)),
 SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Date, queryStr(JmgShopSupervisorUnprocessedWork))]
public class JmgShopSupervisorWorkspaceFilterStructureStrategyDate extends SysFilterStructureStrategyQuery implements SysIFilterStructureStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resolve</Name>
				<Source><![CDATA[
    protected boolean resolve(SysIFilterValueType _filterValueType)
    {
        if (_filterValueType.parmTypeIdentifier() == ExtendedTypenum(JmgFilterDate))
        {
            if (query.name() == queryStr(JmgShopSupervisorMyProdJobListQuery))
            {
                QueryBuildDataSource jobDS = query.dataSourceTable(tableNum(JmgProdJobListView));
                QueryBuildRange dateRange = SysQuery::findOrCreateRange(jobDS, fieldNum(JmgProdJobListView, FromDate));
                dateRange.status(RangeStatus::Hidden);
                dateRange.value(strFmt('(%1<=%2.%3)',
                    fieldStr(JmgProdJobListView, FromDate),
                    filterDatasource.name(),
                    fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId())));
            }
            else if (query.name() == queryStr(JmgProdJobSummaryQuery))
            {
                QueryBuildDataSource jobDS = query.dataSourceTable(tableNum(JmgProdJobListView));
                QueryBuildRange dateRange = SysQuery::findOrCreateRange(jobDS, fieldNum(JmgProdJobListView, FromDate));
                dateRange.status(RangeStatus::Hidden);
                str openJobsRange = strFmt('((%1.%2 != %3) && (%1.%4 <= %5.%6))',
                    jobDS.name(),
                    fieldStr(JmgProdJobListView, JobStatus),
                    int2Str(JmgJobStatus::Completed),
                    fieldStr(JmgProdJobListView, FromDate),
                    filterDatasource.name(),
                    fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId()));

                str oldRangeValue = dateRange.value();
                str newRangeValue = oldRangeValue == '' ? openJobsRange : strFmt('(%1 || %2)', oldRangeValue, openJobsRange);
                dateRange.value(newRangeValue);
            }
            else if (query.name() == queryStr(JmgShopSupervisorMyScheduledOrders) ||
                     query.name() == queryStr(JmgShopSupervisorMyScheduledOrdersV2))
            {
                QueryBuildDataSource orderDS = query.dataSourceTable(tableNum(ProdSupervisorScheduledOrders));
                QueryBuildRange scheduledDateRange = SysQuery::findOrCreateRange(orderDS, fieldNum(ProdSupervisorScheduledOrders, SchedStart));
                scheduledDateRange.status(RangeStatus::Hidden);
                scheduledDateRange.value(strFmt('(%1<=%2.%3)',
                    fieldStr(ProdSupervisorScheduledOrders, SchedStart),
                    filterDatasource.name(),
                    fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId())));
            }
            else if (query.name() == queryStr(JmgShopSupervisorUnprocessedWaves)
                 ||  query.name() == queryStr(JmgShopSupervisorNotOnOpenWave)
                 ||  query.name() == queryStr(JmgShopSupervisorNotOnOpenWaveV2)
                 ||  query.name() == queryStr(JmgShopSupervisorUnprocessedWork))
            {
                QueryBuildDataSource orderDS            = query.dataSourceTable(tableNum(ProdBOM));
                QueryBuildRange      scheduledDateRange = SysQuery::findOrCreateRange(orderDS, fieldNum(ProdBOM, RawMaterialDate));
                scheduledDateRange.status(RangeStatus::Hidden);
                scheduledDateRange.value(strFmt('(%1<=%2.%3)',
                    fieldStr(ProdBOM, RawMaterialDate),
                    filterDatasource.name(),
                    fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId())));
            }
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>