<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsSpaceUtilPeriodTemplateBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WMSSpaceUtilPeriodTemplateBuilder</c> class encapsulates logic for building
/// up the load utilization period templates.
/// </summary>
public class WmsSpaceUtilPeriodTemplateBuilder
{
    int         numberOfDays;
    RefRecId    reportSetup;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createPeriodTemplate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates default work period template for the space utilization report.
    /// </summary>
    public void createPeriodTemplate()
    {
        WorkPeriodTemplate      periodTemplate;
        WorkPeriodTemplateLine  periodTemplateLine;

        ttsbegin;

        this.deletePeriodTemplate();

        periodTemplate.Name = this.getPeriodTemplateName();
        periodTemplate.LegalEntity = CompanyInfo::current();
        periodTemplate.insert();

        periodTemplateLine.PeriodTemplate = periodTemplate.RecId;
        periodTemplateLine.ExplodePeriods = NoYes::Yes;
        periodTemplateLine.NumberOfPeriods = numberOfDays;
        periodTemplateLine.Period = DayWeekMonthQuarterYear::Day;
        periodTemplateLine.PeriodDescription = "%1";
        periodTemplateLine.insert();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deletePeriodTemplate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes default work period template for the space utilization report.
    /// </summary>
    public void deletePeriodTemplate()
    {
        WorkPeriodTemplate  periodTemplate;

        ttsbegin;

        delete_from periodTemplate
            where periodTemplate.Name == this.getPeriodTemplateName();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPeriodTemplateName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets default work period template name for the space utilization report.
    /// </summary>
    /// <returns>
    /// A <c>PeriodTemplateName</c> value.
    /// </returns>
    public PeriodTemplateName getPeriodTemplateName()
    {
        #define.periodTemplateName('spaceUtilTemp')
        return strFmt('%1#%2', #periodTemplateName, reportSetup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNumberOfDays</Name>
				<Source><![CDATA[
        public int parmNumberOfDays(int _numberOfDays = numberOfDays)
    {
        numberOfDays = _numberOfDays;

        return numberOfDays;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReportSetup</Name>
				<Source><![CDATA[
        public RefRecId parmReportSetup(RefRecId _reportSetup = reportSetup)
    {
        reportSetup = _reportSetup;

        return reportSetup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static WMSSpaceUtilPeriodTemplateBuilder construct()
    {
        return new WMSSpaceUtilPeriodTemplateBuilder();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>