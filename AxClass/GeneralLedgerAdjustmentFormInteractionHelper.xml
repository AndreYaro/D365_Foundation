<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GeneralLedgerAdjustmentFormInteractionHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Shared form interaction logic for adjustment form behavior.
/// </summary>
internal static class GeneralLedgerAdjustmentFormInteractionHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>openGeneralJournalInJournalListPage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens a given journal in the general journal list page.
    /// </summary>
    /// <param name = "_journal">The instance of <c>LedgerJournalTable</c> to be opened in the general journal list page.</param>
    internal static void openGeneralJournalInJournalListPage(LedgerJournalTable _journal)
    {
        // Formally change the company context before opening the general journal list page
        appl.setDefaultCompany(_journal.DataAreaId, false);

        Args generalJournalFormArguments = new Args();
        generalJournalFormArguments.record(_journal);

        MenuFunction generalJournalMenuFunction = new MenuFunction(menuItemDisplayStr(LedgerJournalTable3), MenuItemType::Display);

        // Open the general journal list page
        FormRun generalJournalFormRun = generalJournalMenuFunction.create(generalJournalFormArguments);
        generalJournalFormRun.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>openVoucherInquiryFormForRecommendedAdjustment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens the voucher inquiry window for the original voucher associated with a given proposed adjustment.
    /// </summary>
    /// <param name = "_recommendedAdjustment">The proposed adjustment.</param>
    internal static void openVoucherInquiryFormForRecommendedAdjustment(GeneralLedgerAdjustmentProposedAdjustment _recommendedAdjustment)
    {
        // Find the GeneralJournalEntry associated with the selected voucher
        GeneralJournalEntry generalJournalEntry;

        changecompany (_recommendedAdjustment.VoucherDataAreaId)
        {
            select firstonly generalJournalEntry
                where generalJournalEntry.AccountingDate == _recommendedAdjustment.AccountingDate
                    && generalJournalEntry.SubledgerVoucher == _recommendedAdjustment.Voucher
                    && generalJournalEntry.SubledgerVoucherDataAreaId == _recommendedAdjustment.VoucherDataAreaId;
        }

        GeneralLedgerAdjustmentFormInteractionHelper::openVoucherInquiryFormForGeneralJournalEntry(generalJournalEntry);
    }

]]></Source>
			</Method>
			<Method>
				<Name>openVoucherInquiryFormForGeneralJournalEntry</Name>
				<Source><![CDATA[
    private static void openVoucherInquiryFormForGeneralJournalEntry(GeneralJournalEntry _generalJournalEntry)
    {
        // Explicitly change to the general journal entry's company before opening the voucher inquiry window
        appl.setDefaultCompany(_generalJournalEntry.SubledgerVoucherDataAreaId, false);

        Args args = new Args();
        args.record(_generalJournalEntry);

        // Open the form in the new company, if different than the current company
        FormRun formRun = new MenuFunction(menuItemDisplayStr(LedgerTransVoucher1), MenuItemType::Display).create(args);
        formRun.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>