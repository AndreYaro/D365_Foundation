<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLCActionAdaptor</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLCActionAdaptor</c> class delegates the actions on the <c>BankLCImport</c> and
///    <c>BankLCExport</c> forms.
/// </summary>
class BankLCActionAdaptor
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkParameterFailed</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Causes an exception if the parameter validation failed.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    ///    The parameter validation failed.
    /// </exception>
    private static void checkParameterFailed()
    {
        // Letter of Credit actions must be called with right parameter.
        throw error("@SYS316270");
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        static void  main(Args  args)
    {
        Common record;
        int    paramEnumType;
        int    paramEnum;

        if (!args)
        {
            BankLCActionAdaptor::checkParameterFailed();
        }

        record = args.record();
        if (!record || record.RecId == 0)
        {
            BankLCActionAdaptor::checkParameterFailed();
        }

        paramEnumType   = args.parmEnumType();
        paramEnum       = args.parmEnum();

        if (paramEnumType != enumnum(BankLCActionType)
            || paramEnum  == 0)
        {
            BankLCActionAdaptor::checkParameterFailed();
        }

        switch (record.TableId)
        {
            case tablenum(BankLCImport) :
                BankLCActionAdaptor::performImportLCActions(record, paramEnum);
                break;
            case tablenum(BankLCExport) :
                BankLCActionAdaptor::performExportLCActions(record, paramEnum);
                break;
            default :
                BankLCActionAdaptor::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>performExportLCActions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Performs actions for an export letter of credit.
    /// </summary>
    /// <param name="_bankLCExport">
    ///    The letter of the credit record
    /// </param>
    /// <param name="_paramEnum">
    ///    The type of action to perform.
    /// </param>
    private static void performExportLCActions(
        BankLCExport _bankLCExport,
        int          _paramEnum)
    {
        BankLCExportType bankLCExportType;

        bankLCExportType = BankLCExportType::construct(_bankLCExport.RecId);

        switch (_paramEnum)
        {
            case BankLCActionType::Fetch :
                bankLCExportType.createShipmentLines();
                break;
            default :
                BankLCActionAdaptor::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>performImportLCActions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Performs actions for an import letter of credit.
    /// </summary>
    /// <param name="_bankLCImport">
    ///    The letter of the credit record
    /// </param>
    /// <param name="_paramEnum">
    ///    The type of action to perform.
    /// </param>
    private static void performImportLCActions(
        BankLCImport _bankLCImport,
        int          _paramEnum)
    {
        BankLCImportType bankLCImportType;

        bankLCImportType = BankLCImportType::construct(_bankLCImport.RecId);

        switch (_paramEnum)
        {
            case BankLCActionType::Confirm :
                bankLCImportType.confirm();
                break;
            case BankLCActionType::Amend :
                bankLCImportType.amend();
                break;
            case BankLCActionType::Close :
                bankLCImportType.close();
                break;
            case BankLCActionType::Fetch :
                bankLCImportType.createShipmentLines();
                break;
            default :
                BankLCActionAdaptor::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>