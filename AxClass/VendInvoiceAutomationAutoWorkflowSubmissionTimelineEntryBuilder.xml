<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceAutomationAutoWorkflowSubmissionTimelineEntryBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Timeline entry builder for the automatic workflow submission AP automation event.
/// </summary>
[Export(identifierStr(Dynamics.AX.Application.DocumentEventHistoryITimelineEntryBuilder))]
[ExportMetadata(classStr(DocumentEventHistoryITimelineEntryBuilder), enumStr(VendInvoiceAutomationSubmitToWorkflow))]
internal final class VendInvoiceAutomationAutoWorkflowSubmissionTimelineEntryBuilder extends VendInvoiceAutomationHistoryBaseTimelineEntryBuilder
                                                                                     implements ProcessExecutionIOpenExecutionMessageLogDialog
{
    private VendInvoiceInfoTable vendorInvoice;
    private ProcessExecutionId autoSubmissionExecutionId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeForDocumentEventHistoryEvent</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected void initializeForDocumentEventHistoryEvent(DocumentEventHistoryEvent _documentHistoryEvent)
    {
        vendorInvoice = this.getVendorInvoiceFromDocumentHistoryEvent(_documentHistoryEvent);
        autoSubmissionExecutionId = _documentHistoryEvent.getDocumentEventLog().ExecutionId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTimelineEntryHeader</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected TimelineEntryHeader getTimelineEntryHeader(DocumentEventHistoryEvent _historyEvent)
    {
        TimelineEntryHeader resultMessage = _historyEvent.getDocumentEventLog().EventEnumValue == VendInvoiceAutomationSubmitToWorkflow::Succeeded 
            ? "@AccountsPayable:VendInvoiceAutomationStepSuccessful" 
            : "@AccountsPayable:VendInvoiceAutomationStepFailed";

        return strFmt("@AccountsPayable:VendInvoiceAutomationAutoSubmitToWorkflowTimelineEntryHeader", resultMessage);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTimelineEntryButtons</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected List getTimelineEntryButtons(DocumentEventHistoryEvent _historyEvent)
    {
        List timelineEntryButtons = super(_historyEvent);

        if (vendorInvoice)
        {
            Args messageLogArgs = new Args();
            messageLogArgs.caller(this);

            // Add a link to the automatic workflow submission message log for this invoice to
            // the timeline entry buttons
            TimelineButton messageLogButton = TimelineButton::newForMenuItem(
            menuItemDisplayStr(ProcessExecutionMessageLogDialog),
            MenuItemType::Display,
            messageLogArgs);

            timelineEntryButtons.addEnd(messageLogButton);
        }

        return timelineEntryButtons;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getContractForMessageLog</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public ProcessExecutionMessageLogContract getContractForMessageLog()
    {
        return ProcessExecutionMessageLogContract::newForSourceRecord(
            VendInvoiceAutomationWorkflowSubmissionTaskConstants::TypeName,
            vendorInvoice.TableId,
            vendorInvoice.RecId,
            autoSubmissionExecutionId);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>