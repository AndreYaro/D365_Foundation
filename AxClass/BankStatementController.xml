<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankStatementController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankStatementController</c> class is the controller class for the <c>BankStatement</c>
///    report.
/// </summary>
class BankStatementController extends SrsReportRunController implements BatchRetryable
{
    #define.ReportName('BankStatement.Design')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean isRetryable()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    protected void prePromptModifyContract()
    {
        this.setRanges(this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Modifies the report query and parameters based on caller settings.
    /// </summary>
    /// <param name="_query">
    ///    The query object.
    /// </param>
    /// <remarks>
    ///    Assigns values to the <c>BankStatementController</c> range.
    /// </remarks>
    public void setRanges(Query _query)
    {
        if (this.parmArgs()
            && this.parmArgs().dataset() == tableNum(BankStmtISOAccountStatement))
        {
            this.parmShowDialog(false);
            BankStmtISOAccountStatement statement = this.parmArgs().record();

            QueryBuildRange rangeStatementId = SysQuery::findOrCreateRange(
                _query.dataSourceTable(tableNum(BankStmtISOAccountStatement)),
                    fieldNum(BankStmtISOAccountStatement, Identification));
            rangeStatementId.value(queryValue(statement.Identification));

            rangeStatementId = SysQuery::findOrCreateRange(
                _query.dataSourceTable(tableNum(BankStmtISOAccountStatement)),
                    fieldNum(BankStmtISOAccountStatement, BankAccountTable));
            rangeStatementId.value(queryValue(statement.BankAccountTable));

            rangeStatementId = SysQuery::findOrCreateRange(
                _query.dataSourceTable(tableNum(BankStmtISOAccountStatement)),
                    fieldNum(BankStmtISOAccountStatement, RecId));
            rangeStatementId.value(queryValue(statement.RecId));
        }
        else
        {
            this.parmShowDialog(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        BankStatementController controller = new BankStatementController();

        controller.parmReportName(#ReportName);
        controller.parmArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>