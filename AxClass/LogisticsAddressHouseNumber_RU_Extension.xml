<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsAddressHouseNumber_RU_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of table <c>LogisticsPostalAddress</c>
/// </summary>
[ExtensionOf(tableStr(LogisticsAddressHouseNumber_RU))]
final class LogisticsAddressHouseNumber_RU_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupHouseId_RU_Extension</Name>
				<Source><![CDATA[
    protected client static QueryBuildDataSource lookupHouseId_RU_Extension(QueryBuildDataSource _qbds, FieldId _recIdNum, FieldId _tableIdNum)
    {
        next lookupHouseId_RU_Extension(_qbds, _recIdNum, _tableIdNum);

        return LogisticsAddressNotFoundRecords_RU::filterQueryByLog(_qbds, _recIdNum, _tableIdNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fullName</Name>
				<Source><![CDATA[
    display public Name fullName()
    {
        return  strRTrim(strRTrim(this.addrTypeName() + ' ' + this.HouseDescription));
    }

]]></Source>
			</Method>
			<Method>
				<Name>propertiesOKTMO_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays or sets OKTMO for address element
    /// </summary>
    /// <param name="_set">
    /// true when editing; false when viewing.
    /// </param>
    /// <param name="_oktmo">
    /// New value
    /// </param>
    /// <returns>
    /// Current (edited if _set is true) value
    /// </returns>
    [SysClientCacheDataMethodAttribute(true)]
    edit OKTMO_RU propertiesOKTMO_RU(
        boolean _set,
        OKATO_RU _oktmo)
    {
        if (!this.RecId && _set)
        {
            this.insert();
        }
        return LogisticsAddressFiasGuidCodes_RU::parmOKTMOByAddressTableId(this, _oktmo, _set);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupHouseIdWithGARLevel_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens lookup form for House groups
    /// </summary>
    /// <param name = "_ctrlHouse">House group control</param>
    /// <param name = "_countryRegionId">Parent country/region</param>
    /// <param name = "_stateId">Parent state</param>
    /// <param name = "_countyId">Parent county</param>
    /// <param name = "_settlementId">Related Urban/Rural settlement ID</param>
    /// <param name = "_cityId">Related city ID</param>
    /// <param name = "_localityId">Related locality ID</param>
    /// <param name = "_districtId">Parent district</param>
    /// <param name = "_streetId">Parent street</param>
    /// <returns>House group record selected by user</returns>
    internal client static LogisticsAddressHouseNumber_RU lookupHouseIdWithGARLevel_RU(
        FormReferenceControl _ctrlHouse,
        LogisticsAddressCountryRegionId _countryRegionId,
        LogisticsAddressStateId _stateId,
        LogisticsAddressCountyId _countyId,
        LogisticsAddressCityRecId _settlementId,
        LogisticsAddressCityRecId _cityId,
        LogisticsAddressCityRecId _localityId,
        LogisticsAddressDistrictRecId _districtId,
        LogisticsAddressStreetID_RU _streetId)
    {
        LogisticsAddressCityRecId cityTableRecId = _settlementId;
        if (_cityId)
        {
            cityTableRecId = _cityId;
        }
        if (_localityId)
        {
            cityTableRecId = _localityId;
        }

        SysReferenceTableLookup lookup = SysReferenceTableLookup::newParameters(
            tableNum(LogisticsAddressHouseNumber_RU),
            _ctrlHouse,
            true);

        lookup.addLookupfield(fieldNum(LogisticsAddressHouseNumber_RU, Name));
        lookup.addLookupfield(fieldNum(LogisticsAddressHouseNumber_RU, HouseDescription));
        lookup.addLookupfield(fieldNum(LogisticsAddressHouseNumber_RU, BuildingDescription));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressHouseNumber_RU, propertiesZipCode));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressHouseNumber_RU, propertiesGniCode));

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(LogisticsAddressHouseNumber_RU));
        qbds = LogisticsAddressHouseNumber_RU::lookupHouseId_RU_Extension(qbds, fieldNum(LogisticsAddressHouseNumber_RU, RecId), fieldNum(LogisticsAddressHouseNumber_RU, TableId));

        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, CountryRegion)).value(queryValue(_countryRegionId));
        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, State)).value(queryValue(_stateId));
        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, County)).value(queryValue(_countyId));
        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, City)).value(queryValue(cityTableRecId));
        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, District)).value(queryValue(_districtId));
        qbds.addRange(fieldNum(LogisticsAddressHouseNumber_RU, Street)).value(queryValue(_streetId));

        lookup.parmQuery(query);
        LogisticsAddressHouseNumber_RU selectedRecord = lookup.performFormLookup();

        return selectedRecord;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>