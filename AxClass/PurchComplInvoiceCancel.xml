<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchComplInvoiceCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class creates a copy of a purch complementary invoice and its lines with the amounts reversed.
/// </summary>
class PurchComplInvoiceCancel
{
    PurchComplementaryInvoiceHeader originalInvoice;
    ReasonTableRefRecId             reasonTableRef;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the copy of the purch complementary invoice and its lines.
    /// </summary>
    /// <returns>
    /// The RecID of the new invoice.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public RecId copy()
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR00026, funcName());

        PurchComplementaryInvoiceHeader newInvoice;
        PurchComplementaryInvoiceLine   newInvoiceLine, oldInvoiceLine;
        FiscalDocument_BR               originalFiscalDocument;

        ttsBegin;

        originalInvoice.selectForUpdate(true);
        originalInvoice.Status = ComplementaryInvoiceStatus::CancelInProgress;
        originalInvoice.update();

        newInvoice.data(originalInvoice);
        newInvoice.Status = ComplementaryInvoiceStatus::Open;
        newInvoice.CanceledInvoice = originalInvoice.RecId;
        newInvoice.SourceDocumentHeader = 0;
        newInvoice.DocumentDate = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone());
        newInvoice.LedgerVoucher = '';
        newInvoice.Number = "@SYS11318";
        newInvoice.ReasonTableRef_BR = reasonTableRef;
        newInvoice.RecId = 0;
        newInvoice.insert();

        while select oldInvoiceLine
            where oldInvoiceLine.PurchComplementaryInvoiceHeader == originalInvoice.RecId
        {
            newInvoiceLine.data(oldInvoiceLine);
            newInvoiceLine.Amount = newInvoiceLine.Amount * -1;
            newInvoiceLine.CorrectedAmount += newInvoiceLine.Amount;
            newInvoiceLine.AdditionalICMSAmount = newInvoiceLine.AdditionalICMSAmount * -1;
            newInvoiceLine.AdditionalIPIAmount = newInvoiceLine.AdditionalIPIAmount * -1;
            newInvoiceLine.PurchComplementaryInvoiceHeader = newInvoice.RecId;
            newInvoiceLine.SourceDocumentLine = 0;
            newInvoiceLine.RecId = 0;
            newInvoiceLine.insert();
        }

        originalFiscalDocument = FiscalDocument_BR::findByRef(originalInvoice.TableId, originalInvoice.RecId);
        FiscalDocumentTextUpdate_BR::newFromRecord(newInvoice).copy(originalFiscalDocument);

        ttsCommit;

        return newInvoice.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>PurchComplInvoiceCancel</c>
    /// </summary>
    /// <param name="_originalInvoice">
    /// Sales complementary invoice to be copied.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void new(PurchComplementaryInvoiceHeader _originalInvoice)
    {
        Debug::assert(_originalInvoice.RecId);
        originalInvoice = _originalInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReasonTableRef</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public ReasonTableRefRecId parmReasonTableRef(ReasonTableRefRecId _reasonTableRef = reasonTableRef)
    {
        reasonTableRef = _reasonTableRef;
        return reasonTableRef;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        PurchComplementaryInvoiceHeader invoiceToBeCanceled;
        PurchComplInvoiceCancel invoiceCancel;

        invoiceToBeCanceled = _args.record();
        invoiceCancel = new PurchComplInvoiceCancel(invoiceToBeCanceled);
        invoiceCancel.copy();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>