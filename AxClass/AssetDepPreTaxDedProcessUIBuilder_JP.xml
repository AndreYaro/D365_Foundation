<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepPreTaxDedProcessUIBuilder_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The UI builder of asset depreciation pre-tax deduction process.
/// </summary>
class AssetDepPreTaxDedProcessUIBuilder_JP extends SysOperationAutomaticUIBuilder
{
    DialogField     periodField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Does some additional logic after the <C>build</C> is done.
    /// </summary>
    public void postBuild()
    {
        DialogField     toDateField;
        DialogField     journalNameField;
        DialogField     processRecIdField;

        super();

        periodField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetDepPreTaxDedProcessContract_JP, parmPeriod));
        periodField.allowEdit(false);

        toDateField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetDepPreTaxDedProcessContract_JP, parmToDate));
        toDateField.mandatory_RU(true);
        toDateField.registerOverrideMethod(
            methodStr(FormDateControl, modified),
            methodStr(AssetDepPreTaxDedProcessUIBuilder_JP, toDateModified),
            this);
        toDateField.registerOverrideMethod(
            methodStr(FormDateControl, validate),
            methodStr(AssetDepPreTaxDedProcessUIBuilder_JP, toDateValidate),
            this);

        journalNameField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetDepPreTaxDedProcessContract_JP, parmJournalName));
        journalNameField.mandatory_RU(true);

        processRecIdField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetDepPreTaxDedProcessContract_JP, parmProcessRecId));
        processRecIdField.visible(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>toDateModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if the toDate field modified.
    /// </summary>
    /// <param name="_formDateControl">
    /// The form date control
    /// </param>
    /// <returns>
    /// Returns true if modified, otherwise false.
    /// </returns>
    public boolean toDateModified(FormDateControl _formDateControl)
    {
        boolean                                 ret;
        AssetDepPreTaxDedFullSettlePeriod_JP    newPeriod;

        ret = _formDateControl.modified();

        if (ret)
        {
            if (_formDateControl.dateValue() != dateNull())
            {
                    newPeriod =strFmt(
                        "@SYP4881521",    // From %1 to %2
                        LedgerFiscalCalendar::findFirstDayOfYear(Ledger::fiscalCalendar(), _formDateControl.dateValue()),
                        _formDateControl.dateValue());
            }

            periodField.value(newPeriod);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>toDateValidate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if the toDate field valid.
    /// </summary>
    /// <param name="_formDateControl">
    /// The form date control
    /// </param>
    /// <returns>
    /// Returns true if valid, otherwise false.
    /// </returns>
    public boolean toDateValidate(FormDateControl _formDateControl)
    {
        boolean                                 ret;
        LedgerFiscalCalendarYear                ledgerFiscalCalendarYear;

        ret = _formDateControl.validate();

        if (ret && _formDateControl.dateValue() != dateNull())
        {
            ledgerFiscalCalendarYear = LedgerFiscalCalendar::findLedgerYearByCalendarLedgerDate(Ledger::fiscalCalendar(), Ledger::current(), _formDateControl.dateValue());
            if (ledgerFiscalCalendarYear.RecId == 0 || ledgerFiscalCalendarYear.Status != FiscalYearStatus::Open)
            {
                // Settlement process period is allowed for the open fiscal year only.
                ret = checkFailed("@SYP4881522");
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>