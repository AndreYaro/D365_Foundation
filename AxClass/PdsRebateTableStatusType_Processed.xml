<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsRebateTableStatusType_Processed</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class that holds the rebate processes specific to rebate with status 'Processed'.
/// </summary>
public class PdsRebateTableStatusType_Processed extends PdsRebateTableStatusType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pdsRebateStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the rebate status as 'Processed'.
    /// </summary>
    /// <returns>
    /// The rebate status.
    /// </returns>
    public PdsRebateStatus pdsRebateStatus()
    {
        return PdsRebateStatus::Processed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPurge</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the rebate purge process.
    /// </summary>
    /// <exception cref="Exception::UpdateConflictNotRecovered">
    /// Update conflict is not recovered.
    /// </exception>
    /// <exception cref="Exception::UpdateConflict">
    /// Update conflict is present.
    /// </exception>
    public void runPurge()
    {
        #OCCRetryCount

        try
        {
            ttsBegin;

            pdsRebateTable.doDelete();

            ttsCommit;
        }
        catch (Exception::Error)
        {
            exceptionTextFallThrough();
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateFieldValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates changes to a field in the <c>PdsRebateTable</c> table.
    /// </summary>
    /// <param name="_fieldName">
    /// The field name to Validate.
    /// </param>
    /// <param name="_arrayIndex">
    /// The array index of the field to validate; optional.
    /// </param>
    /// <returns>
    /// true if the value in the field change is valid; otherwise, false.
    /// </returns>
    public boolean validateFieldValue(fieldName _fieldName, int _arrayIndex = 1)
    {
        boolean ret = true;

        if (_fieldName == fieldStr(PdsRebateTable, PdsMarkForCredit))
        {
            ret = checkFailed(strfmt("@PRO1902", pdsRebateTable.PdsRebateId));
        }

        ret = ret && super(_fieldName, _arrayIndex);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the class.
    /// </summary>
    /// <returns>
    /// A new class instance.
    /// </returns>
    public static PdsRebateTableStatusType_Processed construct()
    {
        return new PdsRebateTableStatusType_Processed();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>