<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResActivityResourceRoleSearchCriteria</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Activity resource role search criteria.
/// </summary>
public class ResActivityResourceRoleSearchCriteria extends ResSearchCriteriaItem
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applySearch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies activity resource role search criteria in the specified query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>Updated query applied with activity resource role search criteria.</returns>
    public Query applySearch(Query _query)
    {
        if (_query)
        {
            if (!this.getSearchQuery(_query))
            {
                warning("@Res:SearchCriteria_ActivityResourceRoleNotApplicable");
            }
        }

        return _query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSearchQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get activity resource role search criteria query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>true if query is updated; otherwise false.</returns>
    private boolean getSearchQuery(Query _query)
    {
        boolean queryUpdated;

        if (_query)
        {
            QueryBuildDataSource mainDS = _query.dataSourceNo(1);
            QueryBuildDataSource dataSource;
            QueryBuildRange range;

            if (ResUtil::isPerformanceEnhancementFeatureEnabled(ResEnableByDefaultPerfEnhancementFeatureFlight::instance().isEnabled()))
            {
                switch (mainDS.table())
                {
                    case tableNum(ResActivityResourceDetailViewV2):
                        dataSource = mainDS.addDataSource(tableNum(ResActivityResourceRolesViewV2));

                        this.linkTo_AssignmentResourcesViewV2(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResActivityResourceRolesViewV2, ResourceCategory));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;
                }
            }
            else
            {
                switch (mainDS.table())
                {
                    case tableNum(ResAssignmentResourcesDistinctView):
                        dataSource = mainDS.addDataSource(tableNum(ResAssignmentResourceCategoriesView));

                        this.linkTo_AssignmentResourcesView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResAssignmentResourceCategoriesView, ResourceCategory));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;

                    case tableNum(ResActivityResourceDetailView):
                        dataSource = mainDS.addDataSource(tableNum(ResActivityResourceRolesView));

                        this.linkTo_ActivityResourceDetailView(dataSource);
                        dataSource.joinMode(JoinMode::ExistsJoin);

                        range = dataSource.addRange(fieldNum(ResActivityResourceRolesView, ResourceCategory));
                        range.value(strFmt("%1", this.getValues()));
                        queryUpdated = true;
                        break;
                }
            }
        }

        return queryUpdated;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addResourceRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a criteria value to the criteria item.
    /// </summary>
    /// <param name = "_value">Resource role value.</param>
    public void addResourceRole(ResourceCategoryRecId _value)
    {
        this.addValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeResourceRoleValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes the criteria value from the criteria item.
    /// </summary>
    /// <param name = "_value">Resource role value.</param>
    public void removeResourceRoleValue(ResourceCategoryRecId _value)
    {
        this.removeValue(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of activity resource role search criteria.
    /// </summary>
    protected void new ()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of Activity resource role search criteria.
    /// </summary>
    /// <returns>Instance of <c>ResActivityResourceRoleSearchCriteria</c>.</returns>
    public static ResActivityResourceRoleSearchCriteria construct()
    {
        ResActivityResourceRoleSearchCriteria searchCriteriaItem = new ResActivityResourceRoleSearchCriteria();
        return searchCriteriaItem;
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_AssignmentResourcesView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Assignment resources view.
    /// </summary>
    /// <param name = "_categoryDS">Assignment resource categories data source.</param>
    private void linkTo_AssignmentResourcesView(QueryBuildDataSource _categoryDS)
    {
        FieldId activityField = fieldNum(ResAssignmentResourcesDistinctView, Activity);
        _categoryDS.addLink(activityField,
            fieldNum(ResAssignmentResourceCategoriesView, Activity));

        FieldId resourceField = fieldNum(ResAssignmentResourcesDistinctView, Resource);
        _categoryDS.addLink(resourceField,
            fieldNum(ResAssignmentResourceCategoriesView, Resource));

        FieldId activityResourceField = fieldNum(ResAssignmentResourcesDistinctView, ActivityResource);
        _categoryDS.addLink(activityResourceField,
            fieldNum(ResAssignmentResourceCategoriesView, ActivityResource));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_AssignmentResourcesViewV2</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Assignment resources view V2.
    /// </summary>
    /// <param name = "_categoryDS">Assignment resource categories data source.</param>
    private void linkTo_AssignmentResourcesViewV2(QueryBuildDataSource _categoryDS)
    {
        FieldId activityResourceField = fieldNum(ResActivityResourceDetailViewV2, ActivityResource);
        _categoryDS.addLink(activityResourceField, fieldNum(ResActivityResourceRolesViewV2, RecId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkTo_ActivityResourceDetailView</Name>
				<Source><![CDATA[
    /// <summary>
    /// Links Activity resources detail view.
    /// </summary>
    /// <param name = "_categoryDS">Assignment resource categories data source.</param>
    private void linkTo_ActivityResourceDetailView(QueryBuildDataSource _categoryDS)
    {
        FieldId activityResourceField = fieldNum(ResActivityResourceDetailView, ActivityResource);
        _categoryDS.addLink(activityResourceField, fieldNum(ResActivityResourceRolesView, RecId));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>