<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetProposalInsightsStateMachineV2</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;
using Microsoft.Dynamics.ServiceFramework.Communication.Http;

[Export(identifierStr(Dynamics.AX.Application.PowerAIInsightsIStateMachine))]
[ExportMetadata(classStr(PowerAIInsightsIStateMachine), identifierStr(BudgetProposalInsightsV2))]
internal class BudgetProposalInsightsStateMachineV2 implements PowerAIInsightsIStateMachine
{
    private SysInstrumentationLogger logger;
    private FinanceInsightsInitiatorServiceClient serviceClient;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        logger = SysInstrumentationLoggerFactory::CreateLogger(this.GetType().Name);

        serviceClient = FinanceInsightsInitiatorServiceClient::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canProcessEvents</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean canProcessEvents()
    {
        return BudgetProposalActivation::isConfigurable();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isPollingExternalServiceForStatus</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean isPollingExternalServiceForStatus(PowerAIInsightsStateMachineState _state)
    {
        return _state == BudgetProposalInsightsStateMachineState::ExternalDataServicePolling;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetchDataSyncState</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public DataJobServiceState fetchDataSyncState()
    {
        DataJobServiceState jobState;

        guid executionId = BudgetTimeSeriesParameters::findOrCreate().FinanceInsightsExecutionId;

        if (executionId != emptyGuid())
        {
            jobState = serviceClient.getBudgetProposalTimeSeriesClient().fetchExecutionStatus(executionId);
        }

        return jobState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transition</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public PowerAIInsightsStateMachineState transition(PowerAIInsightsStateMachineState _beginningState, PowerAIInsightsEvent _event)
    {
        PowerAIInsightsStateMachineState newState;

        switch (_beginningState)
        {
            case BudgetProposalInsightsStateMachineState::Disabled:
                switch (_event.getEvent())
                {
                    case PowerAIInsightsEventConstants::AcceptedTermsClicked:
                        newState = this.onAcceptedTermsClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::Enabled:
                switch (_event.getEvent())
                {
                    case BudgetProposalInsightsEventConstants::CreateBudgetProposalConfigurationClicked:
                        newState = this.onCreateBudgetProposalConfigurationClicked(_event as BudgetProposalConfigurationStateMachineEvent);
                        break;
                    case PowerAIInsightsEventConstants::DisableTermsClicked:
                        newState = this.onDisableTermsClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::BatchJobQueued:
                switch (_event.getEvent())
                {
                    case PowerAIInsightsEventConstants::BatchJobStarted:
                        newState = this.onBatchJobStarted(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::BatchJobInProgress:
                switch (_event.getEvent())
                {
                    case PowerAIInsightsEventConstants::BatchJobSucceeded:
                        newState = this.onBatchJobSucceeded(_event.getEvent());
                        break;
                    case PowerAIInsightsEventConstants::BatchJobFailed:
                        newState = this.onBatchJobFailed(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::BatchJobSucceeded:
                switch (_event.getEvent())
                {
                    case PowerAIInsightsEventConstants::TriggerDataFlow:
                        newState = this.onTriggerDataFlow(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::BatchJobFailed:
                switch (_event.getEvent())
                {
                    case PowerAIInsightsEventConstants::DisableTermsClicked:
                        newState = this.onDisableTermsClicked(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::ExternalServiceCallFailed:
                switch (_event.getEvent())
                {
                    // If recreate is clicked and we are in ExternalServiceCallFailed, we can just trigger the external api since data is already aggregated.
                    case PowerAIInsightsEventConstants::RecreateModelClicked:
                        newState = this.onTriggerDataFlow(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                    case PowerAIInsightsEventConstants::DisableTermsClicked:
                        newState = this.onDisableTermsClicked(_event.getEvent());
                        break;
                }
                break;
            case BudgetProposalInsightsStateMachineState::ExternalDataServicePolling:
                switch (_event.getEvent())
                {
                    // Don't change to a new state when we encounter polling state changes.
                    case PowerAIInsightsEventConstants::ExternalServicePolledAndReturnedInitializeCompleted,
                         PowerAIInsightsEventConstants::ExternalServicePolledAndReturnedInitializeFailed,
                         PowerAIInsightsEventConstants::ExternalServicePolledAndReturnedInitializeNone,
                         PowerAIInsightsEventConstants::ExternalServicePolledAndReturnedInitializeInProgress,
                         PowerAIInsightsEventConstants::PowerAIPolledAndReturnedWarmupSuccess,
                         PowerAIInsightsEventConstants::PowerAIPolledAndReturnedWarmupFailure,
                         PowerAIInsightsEventConstants::PowerAIPolledAndReturnedWarmupInProgress,
                         PowerAIInsightsEventConstants::PowerAIPolledAndReturnedWarmupUnknown:
                        newState = this.onExternalServicePolled(_event.getEvent());
                        break;
                    // If recreate is clicked and we are in ExternalDataServicePolling, we need to retrigger (delete + create) b/c we have no idea what has executed already externally.
                    case PowerAIInsightsEventConstants::RecreateModelClicked:
                        newState = this.onRetriggerDataFlow(_event.getEvent());
                        break;
                    case PowerAIInsightsEventConstants::DisableTermsClicked:
                        newState = this.onDisableTermsClicked(_event.getEvent());
                        break;
                    case BudgetProposalInsightsEventConstants::CreateBudgetProposalConfigurationClicked:
                        newState = this.onCreateBudgetProposalConfigurationClicked(_event as BudgetProposalConfigurationStateMachineEvent);
                        break;
                    case BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked:
                        newState = this.onDeleteBudgetProposalConfigurationClicked(_event.getEvent());
                        break;
                }
                break;
        }

        // Initialize to beginning state in case no transition is found.
        if (newState == '')
        {
            newState = _beginningState;
        }

        return newState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>onAcceptedTermsClicked</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onAcceptedTermsClicked(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityAcceptingTerms, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            // Update the parameters table
            BudgetTimeSeriesParameters::acceptTermsAndConditions(NoYes::Yes);

            return BudgetProposalInsightsStateMachineState::Enabled;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onDisableTermsClicked</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onDisableTermsClicked(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityDisablingFeature, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            serviceClient.getBudgetProposalTimeSeriesClient().deleteAllExecutions();

            BudgetTimeSeriesParameters::clearFinanceInsightsExecutionId();

            // Update the parameters table
            BudgetTimeSeriesParameters::acceptTermsAndConditions(NoYes::No);

            return BudgetProposalInsightsStateMachineState::Disabled;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCreateBudgetProposalConfigurationClicked</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onCreateBudgetProposalConfigurationClicked(BudgetProposalConfigurationStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(BudgetProposalInsightsEventConstants::CreateBudgetProposalConfigurationClicked, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            if (_event == null)
            {
                throw error(Error::wrongUseOfFunction(funcName()));
            }

            BudgetProposalGenerateSourceController budgetProposalGenerateSourceController = BudgetProposalGenerateSourceController::construct();

            BudgetProposalGenerateSourceContract budgetProposalGenerateSourceContract = budgetProposalGenerateSourceController.getDataContractObject() as BudgetProposalGenerateSourceContract;
            BudgetProposalConfiguration budgetProposalConfiguration = _event.budgetProposalConfiguration;

            if (budgetProposalGenerateSourceContract && budgetProposalConfiguration)
            {
                budgetProposalGenerateSourceContract.parmBudgetProposalConfiguration(budgetProposalConfiguration);
                budgetProposalGenerateSourceController.startOperation();
            }
            else
            {
                throw error(error::wrongUseOfFunction(funcName()));
            }

            return BudgetProposalInsightsStateMachineState::BatchJobQueued;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onDeleteBudgetProposalConfigurationClicked</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onDeleteBudgetProposalConfigurationClicked(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(BudgetProposalInsightsEventConstants::DeleteBudgetProposalConfigurationClicked, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            serviceClient.getBudgetProposalTimeSeriesClient().deleteAllExecutions();

            BudgetTimeSeriesParameters::clearFinanceInsightsExecutionId();
            
            return BudgetProposalInsightsStateMachineState::Enabled;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onBatchJobStarted</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onBatchJobStarted(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityBatchJobStarted, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            return BudgetProposalInsightsStateMachineState::BatchJobInProgress;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onBatchJobSucceeded</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onBatchJobSucceeded(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityBatchJobSucceeded, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            return BudgetProposalInsightsStateMachineState::BatchJobSucceeded;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onBatchJobFailed</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onBatchJobFailed(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityBatchJobFailed, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            return BudgetProposalInsightsStateMachineState::BatchJobFailed;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onTriggerDataFlow</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onTriggerDataFlow(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityTriggeringDataFlow, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            return this.tryInitializeBudgetProposalTimeSeries(context);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onRetriggerDataFlow</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onRetriggerDataFlow(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityRetriggeringDataFlow, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            serviceClient.getBudgetProposalTimeSeriesClient().deleteAllExecutions();

            return this.tryInitializeBudgetProposalTimeSeries(context);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>tryInitializeBudgetProposalTimeSeries</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState tryInitializeBudgetProposalTimeSeries(SysInstrumentationActivityContext context)
    {
        System.Exception exception;
        NonSuccessHttpResponseException commException;

        try
        {
            this.initializeBudgetProposalTimeSeries();

            return BudgetProposalInsightsStateMachineState::ExternalDataServicePolling;
        }
        catch (commException)
        {
            str exceptionMessage = strFmt('ReasonCode: %1, %2 %3', commException.RemoteStatusCode, commException.RemoteReasonPhrase, commException.ToString());
            logger.logError(exceptionMessage);
            context.addCustomProperty(FinanceInsightsConstants::InstrumentationCustomPropertyExceptionType, commException.RemoteServiceError != null ? commException.RemoteServiceError.ErrorCode : 'Unknown');

            return BudgetProposalInsightsStateMachineState::ExternalServiceCallFailed;
        }
        catch (exception)
        {
            logger.logError(exception.ToString());
            context.addCustomProperty(FinanceInsightsConstants::InstrumentationCustomPropertyExceptionType, exception.GetType().Name);

            return BudgetProposalInsightsStateMachineState::ExternalServiceCallFailed;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeBudgetProposalTimeSeries</Name>
				<Source><![CDATA[
    private void initializeBudgetProposalTimeSeries()
    {
        FinanceInsightsExecutionInitializationResponseDataContract initializationResponse = this.createBudgetProposalExecution();

        if (initializationResponse != null && initializationResponse.parmExecutionId() != emptyGuid())
        {
            #OCCRetryCount

            try
            {
                BudgetTimeSeriesParameters params = BudgetTimeSeriesParameters::findOrCreate(true);

                ttsbegin;
                params.FinanceInsightsExecutionId = initializationResponse.parmExecutionId();
                params.doUpdate();
                ttscommit;
            }
            #StandardRetryCatchBlock
        }
        else
        {
            throw new System.Exception('Failed to trigger the FinanceInsightsInitiatorService to start training. Response was empty.');
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createBudgetProposalExecution</Name>
				<Source><![CDATA[
    private FinanceInsightsExecutionInitializationResponseDataContract createBudgetProposalExecution()
    {
        FinanceInsightsBudgetProposalTimeSeriesRequestDataContract requestDataContract = this.createRequestDataContract();

        return serviceClient.getBudgetProposalTimeSeriesClient().createExecution(requestDataContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createRequestDataContract</Name>
				<Source><![CDATA[
    private FinanceInsightsBudgetProposalTimeSeriesRequestDataContract createRequestDataContract()
    {

        FinanceInsightsProductInfoProvider productInfo = FinanceInsightsProductInfoProvider::construct();
        str appVersion = productInfo.getApplicationVersion();

        logger.logInformation(strFmt('AppVersion: %1', appVersion));

        FinanceInsightsBudgetProposalTimeSeriesRequestDataContract requestDataContract = new FinanceInsightsBudgetProposalTimeSeriesRequestDataContract();
        requestDataContract.parmAxVersion(appVersion);

        return requestDataContract;
    }

]]></Source>
			</Method>
			<Method>
				<Name>onExternalServicePolled</Name>
				<Source><![CDATA[
    private PowerAIInsightsStateMachineState onExternalServicePolled(PowerAIInsightsStateMachineEvent _event)
    {
        using (SysInstrumentationActivityContext context = logger.activityContext(FinanceInsightsConstants::InstrumentationActivityExternalServicePolled, FinanceInsightsConstants::FinanceInsightsMetricNamespace))
        {
            context.addCustomProperty(FinanceInsightsConstants::InstrumentationCustomPropertyEventName,  _event);

            return BudgetProposalInsightsStateMachineState::ExternalDataServicePolling;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>