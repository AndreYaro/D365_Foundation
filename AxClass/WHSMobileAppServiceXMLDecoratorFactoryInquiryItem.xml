<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppServiceXMLDecoratorFactoryInquiryItem</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSMobileAppServiceXMLDecoratorFactoryInquiryItem</c> class is responsible for instantiating the correct decorator depending on the current step of the menu item.
/// </summary>
[WHSWorkExecuteMode(WHSWorkExecuteMode::InquiryItem)]
public class WHSMobileAppServiceXMLDecoratorFactoryInquiryItem implements WHSIMobileAppServiceXMLDecoratorFactory
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDecorator</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates the <c>WHSMobileAppServiceXMLDecorator</c> class.
    /// </summary>
    /// <param name = "_con">
    /// Contains information about the context that may be required for instantiating the <c>WHSMobileAppServiceXMLDecorator</c> class.
    /// </param>
    /// <returns>
    /// The <c>WHSMobileAppServiceXMLDecorator</c> class.
    /// </returns>
    public WHSMobileAppServiceXMLDecorator getDecorator(container _con)
    {
        if (this.inputInquiryScreen(_con))
        {
            return new WHSMobileAppServiceXMLDecoratorInquiryInput();
        }

        return new WHSMobileAppServiceXMLDecoratorInquiry();
    }

]]></Source>
			</Method>
			<Method>
				<Name>inputInquiryScreen</Name>
				<Source><![CDATA[
    private boolean inputInquiryScreen(container _con)
    {
        str currentStep = this.getCurrentStep(_con);

        return (currentStep == this.inputStepName() || currentStep == this.inputItemDimensionsStepName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentStep</Name>
				<Source><![CDATA[
    /// <summary>
    /// Extracts the current step from the container.
    /// the current step should be preceeded by a string "CurrentStep"
    /// </summary>
    /// <param name = "_con">
    /// Contains information about the context
    /// </param>
    /// <returns>
    /// The current step listed in the context
    /// </returns>
    private str getCurrentStep(container _con)
    {
        WhsrfPassthrough pass = WhsrfPassthrough::createFromFormContainer(_con);

        return pass.parmCurrentStep();
    }

]]></Source>
			</Method>
			<Method>
				<Name>inquiryItemInputStepDelegate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the input step for the item inquiry process guide.
    /// </summary>
    /// <param name = "_result">
    /// The <c>EventHandlerResult</c> containing the input step name.
    /// </param>
    delegate void inquiryItemInputStepDelegate(EventHandlerResult _result)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>inquiryItemDimensionsInputStepDelegate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the item dimension input step for the item inquiry process guide.
    /// </summary>
    /// <param name = "_result">
    /// The <c>EventHandlerResult</c> containing the item dimension input step name.
    /// </param>
    delegate void inquiryItemDimensionsInputStepDelegate(EventHandlerResult _result)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>inputStepName</Name>
				<Source><![CDATA[
    private str inputStepName()
    {
        EventHandlerResult result = new EventHandlerResult();
        this.inquiryItemInputStepDelegate(result);

        str inputStep;

        if (result.result() != null)
        {
            inputStep = result.result();
        }

        return inputStep;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inputItemDimensionsStepName</Name>
				<Source><![CDATA[
    private str inputItemDimensionsStepName()
    {
        EventHandlerResult result = new EventHandlerResult();
        this.inquiryItemDimensionsInputStepDelegate(result);

        str inputStep;

        if (result.result() != null)
        {
            inputStep = result.result();
        }

        return inputStep;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>