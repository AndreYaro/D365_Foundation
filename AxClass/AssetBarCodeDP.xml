<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetBarCodeDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>AssetBarCodeDP</c> class is the data provider for the <c>AssetBarCode</c> report.
/// </summary>
[
    SRSReportParameterAttribute(classStr(AssetBarCodeContract)),
    SRSReportQueryAttribute(queryStr(AssetBarCode))
]
public class AssetBarCodeDP extends SRSReportDataProviderPreProcessTempDB
{
    AssetBarCodeTmp assetBarCodeTmp;
    BarcodeSetupId  barcodeSetupId;
    BarcodeSetup    barcodeSetup;
    Barcode         barcode;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>barcode</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Gets the barcode string of the bar code.
    /// </summary>
    /// <param name="_assetBarCode">
    /// The asset bar code.
    /// </param>
    /// <returns>
    /// the bar code string.
    /// </returns>
    protected BarCodeString barcode(AssetBarCode _assetBarCode)
    {
        BarCodeString barCodeString;

        if (_assetBarCode)
        {
            if (barcodeSetup.validateBarcode(_assetBarCode))
            {
                barcode.string(true, _assetBarCode);
                barcode.encode();
                barCodeString = barcode.barcodeStr();
            }
            else
            {
                throw(error(strfmt("@SYS41409", barcode.barcodeType(), _assetBarCode)));
            }
        }

        return barCodeString;
    }

]]></Source>
			</Method>
			<Method>
				<Name>geAssetBarCodeTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets an <c>AssetBarCodeTmp</c> record.
    /// </summary>
    /// <returns>
    /// The <c>AssetBarCodeTmp</c> record.
    /// </returns>
    [
        SRSReportDataSetAttribute(tableStr(AssetBarCodeTmp))
    ]
    public AssetBarCodeTmp geAssetBarCodeTmp()
    {
        select assetBarCodeTmp;
        return assetBarCodeTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    public void processReport()
    {
        AssetTable assetTable;
        QueryRun queryRun;

        AssetBarCodeContract contract;

        contract = this.parmDataContract() as AssetBarCodeContract;

        barcodeSetup = BarcodeSetup::find(contract.parmBarCodeSetUpId());
        barcode = barcodeSetup.barcode();
        queryRun = new QueryRun(this.parmQuery());  
      
        RecordInsertList recordInsertList = new RecordInsertList(tableNum(AssetBarCodeTmp), true, true, true, false, true, assetBarCodeTmp);

        while (queryRun.next())
        {
            assetTable = queryRun.get(tableNum(AssetTable)) as AssetTable;
            assetBarCodeTmp.clear();
            assetBarCodeTmp.AssetId = assetTable.AssetId;
            assetBarCodeTmp.AssetName = assetTable.Name;
            assetBarCodeTmp.BarCode = this.barCode(assetTable.Barcode);

            recordInsertList.add(assetBarCodeTmp);
        }

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>