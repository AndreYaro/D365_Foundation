<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAResAssignAnalysis_EmployeeResource</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This derived class is used for handling worker wise assignment analysis.
/// </summary>
class PSAResAssignAnalysis_EmployeeResource extends PSAResAssignAnalysisBase
{
    #TimeConstants

    Query rootQuery;
    QueryBuildDataSource analysisDS;

    const real percent100 = 100.0;
    utcdatetime startDateTime = DateTimeUtil::minValue();
    utcdatetime endDateTime = DateTimeUtil::maxValue();

    boolean projBase;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Builds the query for getting worker wise resource assignment analysis records.
    /// </summary>
    void buildQuery()
    {
        if (m_dStartDate)
        {
            startDateTime = DateTimeUtil::newDateTime(m_dStartDate, 0);
        }

        if (m_dEndDate)
        {
            endDateTime = DateTimeUtil::newDateTime(m_dEndDate, 0);
        }

        rootQuery = new Query();

        if (m_tCommon && (m_tCommon.TableId == tableNum(ProjTable)))
        {
            projBase = true;
            ProjTable projTable = m_tCommon;
            ActivityRecId activity = ProjWBS::getRootActivityRecId(projTable.ProjId);

            analysisDS = rootQuery.addDataSource(tableNum(ResResourceAnalysisPerProjectDayView));            
            
            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Activity))
                .value(SysQuery::value(activity));

            if (m_Resource)
            {
                analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Resource))
                    .value(SysQuery::value(m_Resource));
            }
            else if (m_tCommon && (m_tCommon.TableId == tableNum(ResourceView)))
            {
                ResourceView resourceView = m_tCommon;
                analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Resource))
                    .value(SysQuery::value(resourceView.RecId));
            }

            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, RecipientCompany))
                .value(SysQuery::value(curExt()));
            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Timeframe))
                .value(queryRange(startDateTime, endDateTime));
            
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, Resource));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategoryId));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, Capacity), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, HardLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, SoftLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, HardAssigned), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, HardBooked), SelectionField::Sum);

            analysisDS.orderMode(OrderMode::GroupBy);
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, Resource));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategoryId));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, Activity));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, RecipientCompany));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, EffectiveDate));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, ExpirationDate));            
        }
        else
        {
            analysisDS = rootQuery.addDataSource(tableNum(ResResourceAnalysisDayView));
               
            if (m_Resource)
            {
                analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, Resource))
                    .value(SysQuery::value(m_Resource));
            }
            else if (m_tCommon && (m_tCommon.TableId == tableNum(ResourceView)))
            {
                ResourceView resourceView = m_tCommon;
                analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, Resource))
                    .value(SysQuery::value(resourceView.RecId));
            }

            analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, RecipientCompany))
                .value(SysQuery::value(curExt()));
            analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, Timeframe))
                .value(queryRange(startDateTime, endDateTime));
            
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, Resource));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, ResourceCategoryId));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, Capacity), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, HardLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, SoftLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, HardAssigned), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, HardBooked), SelectionField::Sum);
            
            analysisDS.orderMode(OrderMode::GroupBy);
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, Resource));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, ResourceCategoryId));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, RecipientCompany));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, EffectiveDate));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, ExpirationDate));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, RoleEffectiveDate));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, RoleExpirationDate));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes the query based on value provided for department wise resource assignment analysis.
    /// </summary>
    void initQuery()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssignmentAnalysis</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Calculates and populates the temporary table for worker wise assignment analysis.
    /// </summary>
    void insertAssignmentAnalysis()
    {
        m_oSummaryQueryRun = new QueryRun(rootQuery);

        ttsbegin;

        if (projBase)
        {
            this.insertRecordByActivityProcess();
        }
        else
        {
            this.insertRecordByResourceProcess();
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertRecordByActivityProcess</Name>
				<Source><![CDATA[
    private void insertRecordByActivityProcess()
    {       
        while (m_oSummaryQueryRun.next())
        {
            ResResourceAnalysisPerProjectDayView projectAnalysis = m_oSummaryQueryRun.get(tableNum(ResResourceAnalysisPerProjectDayView));
            if (projectAnalysis.EffectiveDate > endDateTime || projectAnalysis.ExpirationDate < startDateTime)
            {
                continue;
            }
            else
            {
                m_tTmpResourceAssignmentAnalysis.VersionId = ResourceFacade::getID(projectAnalysis.Resource);
                m_tTmpResourceAssignmentAnalysis.Resource = projectAnalysis.Resource;
                m_tTmpResourceAssignmentAnalysis.ResourceName = ResourceFacade::getName(projectAnalysis.Resource);
                m_tTmpResourceAssignmentAnalysis.ResourceCategoryId = projectAnalysis.ResourceCategoryId;
                m_tTmpResourceAssignmentAnalysis.Description = m_tTmpResourceAssignmentAnalysis.VersionId;
                m_tTmpResourceAssignmentAnalysis.Capacity = projectAnalysis.Capacity /  #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.HardCommit = projectAnalysis.HardLocked /  #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.SoftCommit = projectAnalysis.SoftLocked / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.Availability = m_tTmpResourceAssignmentAnalysis.Capacity
                    - m_tTmpResourceAssignmentAnalysis.HardCommit;
                m_tTmpResourceAssignmentAnalysis.HardAssignedHours = projectAnalysis.HardAssigned / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.HardBookedHours = projectAnalysis.HardBooked / #SecondsPerHour;

                if (projectAnalysis.Capacity == 0)
                {
                    m_tTmpResourceAssignmentAnalysis.PercentHard = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentSoft = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentTotal = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentAvailability = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentAssignedOverBooked = 0;
                }
                else
                {
                    m_tTmpResourceAssignmentAnalysis.PercentHard = percent100 * projectAnalysis.HardLocked / projectAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentSoft = percent100 * projectAnalysis.SoftLocked / projectAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentTotal = percent100 * (projectAnalysis.HardLocked + projectAnalysis.SoftLocked)
                        / projectAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentAvailability = percent100 * (projectAnalysis.Capacity - projectAnalysis.HardLocked)
                        / projectAnalysis.Capacity;
                    if (projectAnalysis.HardBooked)
                    {
                        m_tTmpResourceAssignmentAnalysis.PercentAssignedOverBooked = percent100 *
                            projectAnalysis.HardAssigned / projectAnalysis.HardBooked;
                    }
                }
            }
            m_tTmpResourceAssignmentAnalysis.insert();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertRecordByResourceProcess</Name>
				<Source><![CDATA[
    private void insertRecordByResourceProcess()
    {
        PSASchedRoleId defaultRoleId = PSASchedRole::findOrCreateTeamMemberRole().RoleId;

        while (m_oSummaryQueryRun.next())
        {
            ResResourceAnalysisDayView companyAnalysis = m_oSummaryQueryRun.get(tableNum(ResResourceAnalysisDayView));
            
            if (companyAnalysis.EffectiveDate > endDateTime || companyAnalysis.ExpirationDate < startDateTime ||
                (companyAnalysis.ResourceCategoryId != defaultRoleId
                    && (companyAnalysis.RoleEffectiveDate > DateTimeUtil::date(EndDateTime)
                        || companyAnalysis.RoleExpirationDate < DateTimeUtil::date(StartDateTime))))
            {
                continue;
            }
            else
            {
                m_tTmpResourceAssignmentAnalysis.VersionId = ResourceFacade::getID(companyAnalysis.Resource);
                m_tTmpResourceAssignmentAnalysis.Resource = companyAnalysis.Resource;
                m_tTmpResourceAssignmentAnalysis.ResourceName = ResourceFacade::getName(companyAnalysis.Resource);
                m_tTmpResourceAssignmentAnalysis.ResourceCategoryId = companyAnalysis.ResourceCategoryId;
                m_tTmpResourceAssignmentAnalysis.Description = m_tTmpResourceAssignmentAnalysis.VersionId;
                m_tTmpResourceAssignmentAnalysis.Capacity = companyAnalysis.Capacity /  #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.HardCommit = companyAnalysis.HardLocked /  #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.SoftCommit = companyAnalysis.SoftLocked / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.Availability = m_tTmpResourceAssignmentAnalysis.Capacity
                    - m_tTmpResourceAssignmentAnalysis.HardCommit;
                m_tTmpResourceAssignmentAnalysis.HardAssignedHours = companyAnalysis.HardAssigned / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.HardBookedHours = companyAnalysis.HardBooked / #SecondsPerHour;

                if (companyAnalysis.Capacity == 0)
                {
                    m_tTmpResourceAssignmentAnalysis.PercentHard = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentSoft = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentTotal = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentAvailability = 0;
                    m_tTmpResourceAssignmentAnalysis.PercentAssignedOverBooked = 0;
                }
                else
                {
                    m_tTmpResourceAssignmentAnalysis.PercentHard = percent100 * companyAnalysis.HardLocked / companyAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentSoft = percent100 * companyAnalysis.SoftLocked / companyAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentTotal = percent100 * (companyAnalysis.HardLocked + companyAnalysis.SoftLocked)
                        / companyAnalysis.Capacity;
                    m_tTmpResourceAssignmentAnalysis.PercentAvailability = percent100 * (companyAnalysis.Capacity - companyAnalysis.HardLocked)
                        / companyAnalysis.Capacity;
                    if (companyAnalysis.HardBooked)
                    {
                        m_tTmpResourceAssignmentAnalysis.PercentAssignedOverBooked = percent100 *
                            companyAnalysis.HardAssigned / companyAnalysis.HardBooked;
                    }
                }
            }

            m_tTmpResourceAssignmentAnalysis.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>