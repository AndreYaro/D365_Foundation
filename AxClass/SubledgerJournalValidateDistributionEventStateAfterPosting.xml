<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SubledgerJournalValidateDistributionEventStateAfterPosting</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Validates the accounting distribution data for the document to make sure the data is valid and we do not have erroneous entry for the document i.e. we have distribution with 0 
/// accounting event after posting.
/// </summary>
[ExportMetadataAttribute(classstr(SubledgerJournalValidateDistributionEventStateAfterPosting), identifierStr(''))]
[ExportMetadataAttribute(identifierStr('SourceDocumentDataConsistencyValidators'), identifierStr(''))]
[ExportAttribute('Microsoft.Dynamics.AX.Application.SubledgerJournalizerDocumentValidationIProvider')]
internal final class SubledgerJournalValidateDistributionEventStateAfterPosting implements SubledgerJournalizerDocumentValidationIProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the accounting distribution data for the document to make sure the data is valid and we do not have 
    /// erroneous entry for the document i.e. we have distribution with 0 accounting event after posting.
    /// </summary>
    /// <param name = "_accountingEvent">The accounting event for the journalization task using source document accounting processor</param>
    /// <param name = "_voucher">The voucher used for the posting to ledger; if no accounting is generated then this will be blank</param>
    /// <param name = "_sourceDocumentLineSubmittedForProcessing">All the source document lines that are submitted to source document accounting processor</param>
    /// <param name = "_accountingDistributionTmpForValidation">Accounting distribution detail for the document submitted for processing</param>
    /// <param name = "_subledgerJournalAccountEntryTmpForValidation">Detail accounting of the voucher that is getting posted</param>
    [Hookable(false)]
    void validateDocument(
        AccountingEvent _accountingEvent,
        Voucher _voucher,
        SourceDocumentLineRecIdTmp _sourceDocumentLineSubmittedForProcessing,
        AccountingDistributionTmpForValidation _accountingDistributionTmpForValidation,
        SubledgerJournalAccountEntryTmpForValidation _subledgerJournalAccountEntryTmpForValidation)
    {
        using (SysInstrumentationActivityContext activityContext = SourceDocumentAccountingFrameworkInstrumentationLogger::getActivityContext(
            SourceDocumentInstrumentationConstants::SourceDocumentValidationFrameworkNamespace,
            classStr(SubledgerJournalValidateDistributionEventStateAfterPosting),
            classStr(SubledgerJournalValidateDistributionEventStateAfterPosting)))
        {
            AccountingDistribution accountingDistribution;
            AccountingEvent accountingEvent, accountingEventNotExists;
            SourceDocumentHeader sourceDocumentHeader;
        
            select firstonly RecId from sourceDocumentHeader
                where sourceDocumentHeader.RecId == _accountingEvent.SourceDocumentHeader &&
                    (
                        sourceDocumentHeader.SourceRelationType == tableNum(CustInvoiceJour) ||
                        sourceDocumentHeader.SourceRelationType == tableNum(CustInvoiceTable) ||
                        sourceDocumentHeader.SourceRelationType == tableNum(VendInvoiceJour) ||
                        sourceDocumentHeader.SourceRelationType == tableNum(VendInvoiceInfoTable) ||
                        sourceDocumentHeader.SourceRelationType == tableNum(PurchReqTable)
                    )
            join accountingEvent
                where accountingEvent.SourceDocumentHeader == _accountingEvent.SourceDocumentHeader &&
                    accountingEvent.RecId == _accountingEvent.RecId
            join accountingDistribution
                where accountingDistribution.SourceDocumentHeader == accountingEvent.SourceDocumentHeader &&
                    accountingDistribution.AccountingEvent == 0
            exists join _sourceDocumentLineSubmittedForProcessing
                where _sourceDocumentLineSubmittedForProcessing.SourceDocumentLineRecId == accountingDistribution.SourceDocumentLine
            notexists join accountingEventNotExists
                where accountingEventNotExists.SourceDocumentHeader == accountingEvent.SourceDocumentHeader &&
                    accountingEventNotExists.State == AccountingEventState::Started;

            if (sourceDocumentHeader.RecId)
            {
                //log telemetry that the source document lines are in an invalid accounting status
                activityContext.addCustomProperty('ValidationResult', 'Fail');

                activityContext.addCustomProperty(
                    'Message', SourceDocumentInstrumentationConstants::ErrorMessageWhenAccountingDistributionIsInvalid);

                activityContext.addCustomProperty(
                    'SourceDocumentHeader', int642Str(_accountingEvent.SourceDocumentHeader));

                activityContext.addCustomProperty(
                    'AccountingEvent', int642Str(_accountingEvent.RecId));

                if (FeatureStateProvider::isFeatureEnabled(EnableSourceDocumentValidationFramework::instance()))
                {
                    throw error("@SourceDocumentAccountingFramework:ErrorMessageForValidatingAccountingDistributionEntriesInWrongStateDuringJournalization");;
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>