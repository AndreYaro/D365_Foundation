<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResProductReleaseAddProductForm</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>EcoResProductReleaseAddProductForm</c> class provides the methods to control the behavior of
///    the <c>EcoResProductReleaseAddProduct</c> form and handles form events.
/// </summary>
public class EcoResProductReleaseAddProductForm
{
    EcoResReleaseSessionRecId   releaseSessionRecId;

    FormRun                     productReleaseAddProductFormRun;

    FormDataSource              productFormDataSource;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addProductsToReleaseSession</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds selected products of a form data source to the release session.
    /// </summary>
    /// <param name="_productFormDataSource">
    ///    A form data source.
    /// </param>
    private void addProductsToReleaseSession(FormDataSource _productFormDataSource)
    {
        EcoResProductReleaseForm::addProductsFromDataSourceToRelease(releaseSessionRecId, _productFormDataSource);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buttonAddClicked</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Handles a clicked event of the add selected products button.
    /// </summary>
    public void buttonAddClicked()
    {
        this.addProductsToReleaseSession(productFormDataSource);

        productFormDataSource.research(true);
        this.refreshCaller();
    }

]]></Source>
			</Method>
			<Method>
				<Name>closeOk</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Handles a form close event triggered by clicking the OK button.
    /// </summary>
    public void closeOk()
    {
        this.addProductsToReleaseSession(productFormDataSource);
        this.refreshCaller();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProductFormDataSource</Name>
				<Source><![CDATA[
        public FormDataSource parmProductFormDataSource(FormDataSource _productFormDataSource = productFormDataSource)
    {
        productFormDataSource = _productFormDataSource;

        return productFormDataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProductReleaseAddProductFormRun</Name>
				<Source><![CDATA[
        public FormRun parmProductReleaseAddProductFormRun(FormRun _productReleaseAddProductFormRun = productReleaseAddProductFormRun)
    {
        productReleaseAddProductFormRun = _productReleaseAddProductFormRun;

        return productReleaseAddProductFormRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReleaseSessionRecId</Name>
				<Source><![CDATA[
        public EcoResReleaseSessionRecId parmReleaseSessionRecId(EcoResReleaseSessionRecId _releaseSessionRecId = releaseSessionRecId)
    {
        releaseSessionRecId = _releaseSessionRecId;

        return releaseSessionRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryReleaseSession</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves a query for the release session form data source.
    /// </summary>
    /// <returns>
    ///    A query for the release session form data source.
    /// </returns>
    public Query queryReleaseSession()
    {
        return EcoResReleaseSession::releaseSessionQuery(releaseSessionRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryReleaseSessionAddProducts</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves a query for all of the products displayed in the add products to a release form.
    /// </summary>
    /// <returns>
    ///    A query for all products to be displayed in the add products to a release form.
    /// </returns>
    public Query queryReleaseSessionAddProducts()
    {
        return EcoResReleaseSessionProduct::releaseSessionAddProductsQuery(releaseSessionRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshCaller</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Refreshes a calling form.
    /// </summary>
    private void refreshCaller()
    {
        Object  caller  = productReleaseAddProductFormRun.args().caller();

        if (    caller
            &&  caller is FormRun
            &&  formHasMethod(caller, identifierStr(refresh)))
        {
            caller.refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static EcoResProductReleaseAddProductForm construct()
    {
        return new EcoResProductReleaseAddProductForm();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes a new instance of the <c>EcoResProductReleaseAddProductForm</c> class.
    /// </summary>
    /// <param name="_args">
    ///    The input arguments.
    /// </param>
    /// <returns>
    ///    A new instance of the <c>EcoResProductReleaseAddProductForm</c> class.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    ///    An <c>EcoResReleaseSession</c> table buffer has not been passed.
    /// </exception>
    public static EcoResProductReleaseAddProductForm newFromArgs(Args _args)
    {
        EcoResProductReleaseAddProductForm  productReleaseAddProductForm;

        if (    _args
            &&  _args.record() is EcoResReleaseSession)
        {
            productReleaseAddProductForm    = EcoResProductReleaseAddProductForm::newFromReleaseSessionRecId(_args.record().RecId);
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return productReleaseAddProductForm;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromReleaseSessionRecId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes a new instance of the <c>EcoResProductReleaseAddProductForm</c> class for
    ///    an existing release session.
    /// </summary>
    /// <param name="_releaseSessionRecId">
    ///    A release session ID.
    /// </param>
    /// <returns>
    ///    An instance of the <c>EcoResProductReleaseAddProductForm</c> class.
    /// </returns>
    public static EcoResProductReleaseAddProductForm newFromReleaseSessionRecId(EcoResReleaseSessionRecId _releaseSessionRecId)
    {
        EcoResProductReleaseAddProductForm  productReleaseAddProductForm = EcoResProductReleaseAddProductForm::construct();

        if (!_releaseSessionRecId)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        productReleaseAddProductForm.parmReleaseSessionRecId(_releaseSessionRecId);

        return productReleaseAddProductForm;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>