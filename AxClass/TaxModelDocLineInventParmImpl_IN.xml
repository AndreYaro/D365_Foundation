<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLineInventParmImpl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for <c>inventTransferParmLine</c>.
/// </summary>
[TaxModelDocumentLineAttribute(tableStr(InventTransferParmLine))]
public class TaxModelDocLineInventParmImpl_IN extends TaxModelDocLineInventTransImpl_IN
{
    InventTransferParmLine      inventTransferParmLine;
    InventTransferParmTable     inventTransferParmTable;
    InventTransferUpdateType    inventTransferUpdateType;
    TransTaxInformation         fromTransTaxInformation;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDocumentLineMainTableId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>TableId</c> of the document line main table.
    /// </summary>
    /// <returns>
    /// The <c>TableId</c>
    /// </returns>
    public TableId getDocumentLineMainTableId()
    {
        return inventTransferParmLine.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentLineMainTableRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>RecId</c> of the document line main table.
    /// </summary>
    /// <returns>
    /// The <c>RecId</c> of the document line main table.
    /// </returns>
    public RecId getDocumentLineMainTableRecId()
    {
        return inventTransferParmLine.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExciseDirectSettlement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets direct settlement of this document line.
    /// </summary>
    /// <returns>
    /// The <c>DirectSettlement_IN</c>
    /// </returns>
    public DirectSettlement_IN getExciseDirectSettlement()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return DirectSettlement_IN::None;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExciseRecordType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>ExciseRecordType_IN</c> of the document line map.
    /// </summary>
    /// <returns>
    /// The <c>ExciseRecordType_IN</c>
    /// </returns>
    public ExciseRecordType_IN getExciseRecordType()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return toTransTaxInformation.ExciseRecordType;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExciseType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>ExciseType_IN</c> of the document line.
    /// </summary>
    /// <returns>
    /// The <c>ExciseType_IN</c>
    /// </returns>
    public ExciseType_IN getExciseType()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return toTransTaxInformation.ExciseType;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventTransferUpdateType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the invent transfer update type.
    /// </summary>
    /// <returns>
    /// The instance of the <c>InventTransferUpdateType</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public InventTransferUpdateType getInventTransferUpdateType()
    {
        return inventTransferUpdateType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the quantity from the transaction line.
    /// </summary>
    /// <returns>
    ///    The quantity.
    /// </returns>
    /// <remarks>
    ///    If the transaction does not support quantities, 0 may be passed as the quantity. This will be used
    ///    to calculate taxes by unit.
    /// </remarks>
    public Qty getQuantity()
    {
        if (this.isShipment())
        {
            return inventTransferParmLine.QtyShipNow;
        }
        else
        {
            return inventTransferParmLine.QtyReceiveNow;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax direction.
    /// </summary>
    /// <returns>
    /// The tax direction
    /// </returns>
    public TaxDirection getTaxDirection()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return TaxDirection::IncomingTax;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxLocationRefRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax location <c>RefRecId</c> on the transaction.
    /// </summary>
    /// <returns>
    /// The tax location of the transaction.
    /// </returns>
    /// <remarks>
    /// The method may be required to be overridden by different type of transaction.
    /// </remarks>
    public RefRecId getTaxLocationRefRecId()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return inventTransferTable.toLocation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction posting date
    /// </summary>
    /// <returns>
    /// The transaction posting date
    /// </returns>
    public TransDate getTransDate()
    {
        return inventTransferParmTable.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventTransId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>InventTransId</c> of the document line.
    /// </summary>
    /// <returns>
    /// The <c>InventTransId</c>.
    /// </returns>
    public InventTransId getInventTransId()
    {
        return inventTransferParmLine.InventTransId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        inventTransferParmLine = _documentLineMap;
        inventTransferParmTable = inventTransferParmLine.inventTransferParmTable();
        super(inventTransferParmLine.inventTransferLine());
        inventTransferUpdateType = inventTransferParmLine.inventTransferParmTable().UpdateType;

        fromTransTaxInformation =  TransTaxInformationHelper::newHelper().findTransTaxInformationByRecord(
            inventTransferLine,
            TransTaxInformationType_IN::InventTransferFrom);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isExicseDSA</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if the transaction should register DSA.
    /// </summary>
    /// <returns>
    /// True if the document line should register DSA; otherwise, false.
    /// </returns>
    public NoYes isExicseDSA()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return toTransTaxInformation.ExciseDSA;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isReceive</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks the invent transfer update type if the invent should be receive.
    /// </summary>
    /// <returns>
    /// Returns true if the invent should be receive.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean isReceive()
    {
        return inventTransferUpdateType == InventTransferUpdateType::Receive;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isShipment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the invent transfer update type is shipment.
    /// </summary>
    /// <returns>
    /// Returns true if the invent transfer update type is shipment.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean isShipment()
    {
        return inventTransferUpdateType == InventTransferUpdateType::Shipment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldPostTaxTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if tax transaction should be posted or not.
    /// </summary>
    /// <returns>
    /// True if the document line should post tax transaction; otherwise, false.
    /// </returns>
    public NoYes shouldPostTaxTrans()
    {
        if (inventTransferUpdateType == InventTransferUpdateType::PickingList)
        {
            return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssessableValueTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the assessable value.
    /// </summary>
    /// <returns>
    /// the assessable value.
    /// </returns>
    public AmountCur getAssessableValueTransactionCurrency()
    {
        return inventTransferParmLine.SalesPurchJournalLine::getSalesPurchJournalLineExtension_IN().getAssessableValue();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDeliveryDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction delivery date
    /// </summary>
    /// <returns>
    /// The transaction delivery date
    /// </returns>
    public date getDeliveryDate()
    {
        date    deliveryDate;

        if (inventTransferParmTable.UpdateType == InventTransferUpdateType::Shipment)
        {
            deliveryDate = inventTransferParmLine.inventTransferLine().ShipDate;
        }
        else if (inventTransferParmTable.UpdateType == InventTransferUpdateType::Receive)
        {
            deliveryDate = inventTransferParmLine.inventTransferLine().ReceiveDate;
        }

        return deliveryDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the line amount of the document line.
    /// </summary>
    /// <returns>
    /// The line amount.
    /// </returns>
    public LineAmount getLineAmount()
    {
        LineAmount  lineAmount;

        if (inventTransferParmTable.UpdateType == InventTransferUpdateType::Shipment)
        {
            lineAmount = inventTransferParmLine.inventTransferLine().inventTransferLine_IN().UnitPrice * inventTransferParmLine.QtyShipNow;
        }
        else
        {
            lineAmount = inventTransferParmLine.inventTransferLine().inventTransferLine_IN().UnitPrice * inventTransferParmLine.QtyReceiveNow;
        }

        return lineAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPartyGSTRegistrationNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the party GST Registration Number.
    /// </summary>
    /// <returns>
    /// The party <c>TaxRegistrationNumber_IN</c>
    /// </returns>
    public TaxRegistrationNumber_IN getPartyGSTRegistrationNumber()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return TaxInformation_IN::find(
                    fromTransTaxInformation.TaxInformation).getTaxRegistrationNumbersTable(
                        TaxType_IN::GST).RegistrationNumber;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMaximumRetailPriceTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the maximum retail price of the document line.
    /// </summary>
    /// <returns>
    /// The maximum retail price.
    /// </returns>
    public MaximumRetailPrice_IN getMaximumRetailPriceTransactionCurrency()
    {
        MaximumRetailPrice_IN   maximumRetailPrice;

        if (inventTransferLineIN && inventTransferLine.QtyTransfer)
        {
            maximumRetailPrice = inventTransferLineIN.MaximumRetailPrice * this.getQuantity() / inventTransferLine.QtyTransfer;
        }

        return maximumRetailPrice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPartyLogisticsPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the party <c>LogisticsPostalAddress</c> of this transaction document line.
    /// </summary>
    /// <returns>
    /// The tax <c>LogisticsPostalAddress</c>
    /// </returns>
    public LogisticsPostalAddress getPartyLogisticsPostalAddress()
    {
        LogisticsPostalAddress logisticsPostalAddress;

        if (this.isShipment())
        {
            logisticsPostalAddress = LogisticsPostalAddress::findByLocation(inventTransferTable.toLocation());
        }
        else
        {
            logisticsPostalAddress = LogisticsPostalAddress::findByLocation(inventTransferTable.fromLocation());
        }

        return logisticsPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getGSTRegistrationNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the GST Registration Number.
    /// </summary>
    /// <returns>
    /// The <c>TaxRegistrationNumber_IN</c>
    /// </returns>
    public TaxRegistrationNumber_IN getGSTRegistrationNumber()
    {
        if (this.isShipment())
        {
            return super();
        }
        else
        {
            return TransTaxInformationHelper::getTaxRegistrationNumbers(toTransTaxInformation, TaxType_IN::GST).RegistrationNumber;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentInvoiceDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction invoice date
    /// </summary>
    /// <returns>
    /// The transaction invoice date
    /// </returns>
    public InvoiceDate getDocumentInvoiceDate()
    {
        return inventTransferParmTable.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction document date
    /// </summary>
    /// <returns>
    /// The transaction document date
    /// </returns>
    public DocumentDate getDocumentDate()
    {
        return inventTransferParmTable.TransDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>