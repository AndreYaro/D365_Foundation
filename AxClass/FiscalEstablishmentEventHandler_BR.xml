<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FiscalEstablishmentEventHandler_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class FiscalEstablishmentEventHandler_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>onModifiedFiscalEstablishmentField</Name>
				<Source><![CDATA[
    [DataEventHandler(tablestr(FiscalEstablishment_BR), DataEventType::ModifiedField)]
    public static void onModifiedFiscalEstablishmentField(Common _sender, DataEventArgs _args)
    {
        ModifyFieldEventArgs mArgs = _args;
        FiscalEstablishment_BR fiscalEstab = _sender;

        switch (mArgs.parmFieldId())
        {
            case fieldNum(FiscalEstablishment_BR, DirPartyLocation) :
                var location = DirPartyLocation::findRec(fiscalEstab.DirPartyLocation).Location;

                fiscalEstab.Name = LogisticsLocation::find(location).Description;

                var logisticsLocationExt = LogisticsLocationExt::find(location);

                if (logisticsLocationExt)
                {
                    FiscalInformationCopy_BR::copyFiscalInfoFromLogisticsLocationExt(fiscalEstab);
                }
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onValidatedFiscalEstablishmentField</Name>
				<Source><![CDATA[
    [DataEventHandler(tablestr(FiscalEstablishment_BR), DataEventType::ValidatedField)]
    public static void onValidatedFiscalEstablishmentField(Common _sender, DataEventArgs _args)
    {
        if(!SystemParameters::find().UseAdvCertificateStore)
        {
            ValidateFieldEventArgs vArgs = _args;
            FiscalEstablishment_BR fiscalEstab = _sender;

            DigitalCertificateTmp   digitalCertificate;

            switch (vArgs.parmFieldId())
            {
                case fieldNum(FiscalEstablishment_BR, EFDocDigitalCertificateSubject):
                if (fiscalEstab.EFDocDigitalCertificateSubject)
                    {
                        digitalCertificate = DigitalCertificateManager::populateDigitalCertificates();

                        select Subject from digitalCertificate
                        where digitalCertificate.Subject == fiscalEstab.EFDocDigitalCertificateSubject;

                        if (!digitalCertificate)
                        {
                            vArgs.parmValidateResult(checkFailed("@SYS341169"));
                        }
                    }
                    break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onInitializedFiscalEstablishment</Name>
				<Source><![CDATA[
    [DataEventHandler(tablestr(FiscalEstablishment_BR), DataEventType::InitializedRecord)]
    public static void onInitializedFiscalEstablishment(Common _sender, DataEventArgs _args)
    {
        EFDReturnCode_BR::checkDefaultData();
        EFDocWebServiceParameters_BR::checkDefaultData();
        EFDocFormatClassInfo_BR::checkDefaultData();
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleFiscalEstablishmentInsertion</Name>
				<Source><![CDATA[
    [SubscribesTo(tablestr(FiscalEstablishment_BR), delegateStr(FiscalEstablishment_BR, insertingFiscalEstablishment))]
    public static void handleFiscalEstablishmentInsertion(FiscalEstablishment_BR _fiscalEstab)
    {
        FiscalInformationCopy_BR::copyFiscalInfoToLogisticsLocationExt(_fiscalEstab);
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleFiscalEstablishmentUpdate</Name>
				<Source><![CDATA[
    [SubscribesTo(tablestr(FiscalEstablishment_BR), delegateStr(FiscalEstablishment_BR, updatingFiscalEstablishment))]
    public static void handleFiscalEstablishmentUpdate(FiscalEstablishment_BR _fiscalEstab, boolean _dirPartyLocationIsChanged)
    {
        if (_dirPartyLocationIsChanged)
        {
            FiscalEstablishmentLocationHelper_BR::propagateFiscalEstablishmentLocation(_fiscalEstab);
        }

        FiscalInformationCopy_BR::copyFiscalInfoToLogisticsLocationExt(_fiscalEstab);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>