<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMJournalOrgUnitPanel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>CFMJournalOrgUnitPanel</c> class manages a list panel control to use to select the
///   cash flow management dimension attributes.
/// </summary>
public class CFMJournalOrgUnitPanel extends SysListPanel
{
    Common source;
    Set set;
    Map intRecordMap;
    Map aggregationField;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CFMJournalOrgUnitPanel construct()
    {
        return new CFMJournalOrgUnitPanel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize class parameters and create panel for form
    /// </summary>
    /// <param name = "_formRun"></param>
    /// <param name = "_parentId"></param>
    /// <returns> return <c>CFMJournalOrgUnitPanel</c> object </returns>
    public static CFMJournalOrgUnitPanel newForm(FormRun _formRun, int _parentId)
    {
        CFMJournalOrgUnitPanel listPanel = CFMJournalOrgUnitPanel::construct();
        listPanel.parmOnlyViewAccess(SecurityRights::construct().tableAccessRight(tableStr(CFMJournalOrgUnitList)) <= AccessRight::View);
        listPanel.parmFormRun(_formRun);
        listPanel.parmParentId(_parentId);
        listPanel.parmCaptionSelected("@CashFlowManagement:SelectedSources");
        listPanel.parmCaptionAvailable("@CashFlowManagement:AvailableSources");

        listPanel.parmHasAddAllButton(false);
        listPanel.parmHasRemoveAllButton(false);

        listPanel.build();

        return listPanel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a <c>CFMJournalOrgUnitList</c> record for the current configuration with the
    ///    specified dimension attribute.
    /// </summary>
    /// <param name="_data">
    ///    The item that was added to the list of enabled dimension attributes.
    /// </param>
    public void addData(container data)
    {
        ttsbegin;
        for (int i = conlen(data); i > 0; i--)
        {
            int key = conPeek(data, i);

            CFMJournalOrgUnitList record = intRecordMap.lookup(key);

            record.insert();

            intRecordMap.insert(key, record);
        }
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items for the left and right lists of the list panel.
    /// </summary>
    /// <returns>
    ///    A container with the items to add to both the left and right lists of the list panel.
    /// </returns>
    public container getData()
    {
        container fillData(List _list)
        {
            ListEnumerator listEnumerator = _list.getEnumerator();
            container data;

            while (listEnumerator.moveNext())
            {
                CFMJournalOrgUnitList record = listEnumerator.current();

                int key = intRecordMap.elements() + 1;
                intRecordMap.insert(key, record);

                data += [[key, record.description()]];
            }

            return data;
        }

        intRecordMap = new Map(Types::Integer, Types::Record);

        container leftData;
        container rightData;

        if (source)
        {
            rightData = fillData(CFMJournalOrgUnitList::orgUnitSelectedList(source));
            leftData = fillData(CFMJournalOrgUnitList::orgUnitNotSelectedList(source));
        }

        return [leftData, rightData];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSource</Name>
				<Source><![CDATA[
    public void parmSource(Common _source)
    {
        source = _source;
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Deletes the <c>CFMJournalOrgUnitList</c> record of the current configuration with the
    ///    specified dimension attribute.
    /// </summary>
    /// <param name="_data">
    /// The item that was removed from the list of enabled dimension attributes.
    /// </param>
    public void removeData(container data)
    {
        ttsbegin;

        for (int i = conlen(data); i > 0; i--)
        {
            int key = conPeek(data, i);

            CFMJournalOrgUnitList record = intRecordMap.lookup(key);

            if (record.RecId)
            {
                record.selectForUpdate(true);
                record.delete();
            }
            else
            {
                throw error("@SYS303332");
            }
        }
        ttsCommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>