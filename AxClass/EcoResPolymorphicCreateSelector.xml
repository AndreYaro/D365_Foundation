<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResPolymorphicCreateSelector</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is used for presenting a dialog in which a sub-table can be selected amongst a list of options.
/// </summary>
class EcoResPolymorphicCreateSelector extends RunBase
{
    str caption;
    str dropdownLabel;

    Map tables;
    TableId selectedTableId;
    DialogField fieldTables;

    Map mapIndexToTableId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmCaption</Name>
				<Source><![CDATA[
    public str parmCaption(str _caption = caption)
    {
        caption = _caption;
        return caption;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDropDownLabel</Name>
				<Source><![CDATA[
    public str parmDropDownLabel(str _dropdownLabel = dropdownLabel)
    {
        dropdownLabel = _dropdownLabel;
        return dropdownLabel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTables</Name>
				<Source><![CDATA[
    public Map parmTables(Map _tables = tables)
    {
        tables = _tables;
        return tables;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectedTableId</Name>
				<Source><![CDATA[
    public TableId selectedTableId()
    {
        return selectedTableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        int64 selectedComboIdx = fieldTables.value();
        selectedTableId = mapIndexToTableId.lookup(selectedComboIdx);

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>Dialog</Name>
				<Source><![CDATA[
    public Object Dialog()
    {
        Dialog dialog = super();

        dialog.caption(caption);

        // Manipulate the contents of the dropdown
        fieldTables = dialog.addField(enumStr(NoYesError));
        FormBuildComboBoxControl formComboBoxControl = fieldTables.control();
        formComboBoxControl.enumType(0);
        formComboBoxControl.label(dropdownLabel);
        formComboBoxControl.items(tables.elements());

        mapIndexToTableId = new Map(Types::Int64, Types::Int64);

        int comboIdx = 0;
        MapEnumerator tablesEnumerator = tables.getEnumerator();
        while (tablesEnumerator.moveNext())
        {
            formComboBoxControl.item(comboIdx + 1);
            formComboBoxControl.text(tablesEnumerator.currentValue());

            mapIndexToTableId.insert(comboIdx, tablesEnumerator.currentKey());

            comboIdx++;
        }

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptForTableId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prompts the user for which sub-table to select.
    /// </summary>
    /// <param name = "_caption">The caption of the dialog.</param>
    /// <param name = "_dropDownCaption">The caption of the drop down field.</param>
    /// <param name = "_tables">A map of table ids and the corresponding labels that will be shown in the dropdown.</param>
    /// <returns>The <c>TableId</c> of the selected subtable.</returns>
    public static TableId promptForTableId(str _caption, str _dropDownCaption, Map _tables)
    {
        EcoResPolymorphicCreateSelector dialog = new EcoResPolymorphicCreateSelector();
        dialog.parmCaption(_caption);
        dialog.parmDropDownLabel(_dropDownCaption);
        dialog.parmTables(_tables);

        if (dialog.prompt())
        {
            return dialog.selectedTableId();
        }

        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
