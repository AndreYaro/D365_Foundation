<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqParametersFormEventHandler_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the Indian related events for the <c>ReqParameters</c> form.
/// </summary>
internal static class ReqParametersFormEventHandler_IN
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>ReqParameters_Post_enableFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler for <c>ReqParameters</c> form's <c>enableFields</c> method.
    /// </summary>
    /// <param name="_args">A <c>XppPrePostArgs</c> instance.</param>
    [PostHandlerFor(formStr(ReqParameters), formMethodStr(ReqParameters, enableFields))]
    public static void ReqParameters_Post_enableFields(XppPrePostArgs _args)
    {
        FormRun         form                = _args.getThis();
        FormDataSource  reqParameters_ds    = form.dataSource(formDataSourceStr(ReqParameters, ReqParameters)) as FormDataSource;

        ReqParametersFormEventHandler_IN::enableFieldsReqParameters(reqParameters_ds);
    }

]]></Source>
			</Method>
			<Method>
				<Name>ReqParameters_Post_init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler for <c>ReqParameters</c> form's <c>init</c> method.
    /// </summary>
    /// <param name="_args">A <c>XppPrePostArgs</c> instance.</param>
    [PostHandlerFor(formStr(ReqParameters), formMethodStr(ReqParameters, init))]
    public static void ReqParameters_Post_init(XppPrePostArgs _args)
    {
        FormRun         form                = _args.getThis();
        FormDataSource  reqParameters_ds    = form.dataSource(formDataSourceStr(ReqParameters, ReqParameters)) as FormDataSource;

        if (!StockTransferPriceDefaultingInMasterPlanningFeature_IN::isEnabled())
        {
            reqParameters_ds.object(fieldNum(ReqParameters, PriceType_IN)).visible(false);
            reqParameters_ds.object(fieldNum(ReqParameters, TransferType_IN)).visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>TransferType_IN_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler for the ReqParameters.TransferType_IN field's OnModified event.
    /// </summary>
    /// <param name="_sender">The ReqParameters.TransferType_IN field.</param>
    /// <param name="_e">Args for the event.</param>
    [FormDataFieldEventHandler(formDataFieldStr(ReqParameters, ReqParameters, TransferType_IN), FormDataFieldEventType::Modified)]
    public static void TransferType_IN_OnModified(FormDataObject _sender, FormDataFieldEventArgs _e)
    {
        ReqParametersFormEventHandler_IN::enableFieldsReqParameters(_sender.dataSource());
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableFieldsReqParameters</Name>
				<Source><![CDATA[
    private static void enableFieldsReqParameters(FormDataSource _reqParameters_ds)
    {
        ReqParameters reqParameters = _reqParameters_ds.cursor();
        _reqParameters_ds.object(fieldNum(ReqParameters, PriceType_IN)).allowEdit(reqParameters.TransferType_IN == TransferType_IN::StockTransfer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>