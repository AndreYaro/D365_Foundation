<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesTableForm_SmallPackingShippingEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Event handlers for the sales table form specific to the small package shipping feature.
/// </summary>
internal final class SalesTableForm_SmallPackingShippingEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>CarrierCustomerAccountNumber_OnLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Lookup event handler for carrier customer account number field.
    /// </summary>
    /// <param name="sender">The form control raising the event.</param>
    /// <param name="_e">Args for the event.</param>
    [FormControlEventHandler(formControlStr(SalesTable, CarrierCustomerAccountNumber), FormControlEventType::Lookup)]
    public static void CarrierCustomerAccountNumber_OnLookup(FormControl _sender, FormControlEventArgs _e)
    {
        FormRun salesTableForm = _sender.formRun();

        FormDataSource salesTable_ds = salesTableForm.dataSource(formDataSourceStr(SalesTable, SalesTable)) as FormDataSource;
        SalesTable salesTable = salesTable_ds.cursor();

        FormDataSource tmsSalesTable_ds = salesTableForm.dataSource(formDataSourceStr(SalesTable, TMSSalesTable)) as FormDataSource;
        TMSSalesTable tmsSalesTable = tmsSalesTable_ds.cursor();

        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(TMSShippingCarrierCustomerAccount), _sender);        
        sysTableLookup.addLookupfield(fieldNum(TMSShippingCarrierCustomerAccount, CarrierCustomerAccountNumber));
        sysTableLookup.addLookupfield(fieldNum(TMSShippingCarrierCustomerAccount, CustAccount));
        sysTableLookup.addLookupfield(fieldNum(TMSShippingCarrierCustomerAccount, CarrierCode));

        Query query = new Query();
        QueryBuildDataSource qbds= query.addDataSource(tableNum(TMSShippingCarrierCustomerAccount));
        qbds.addRange(fieldNum(TMSShippingCarrierCustomerAccount, CustAccount)).value(queryValue(salesTable.CustAccount));

        if (tmsSalesTable.CarrierCode)
        {
            qbds.addRange(fieldNum(TMSShippingCarrierCustomerAccount, CarrierCode)).value(queryValue(tmsSalesTable.CarrierCode));
        }

        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();

        // Do not call super to avoid another lookup form.
        FormControlCancelableSuperEventArgs eventArgs = _e as FormControlCancelableSuperEventArgs;
        eventArgs.CancelSuperCall();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>