<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxWithholdSalesPurchJournalUIBuilder_TH</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// UI builder class for <c>TaxWithholdSalesPurchJournal</c> SSRS report.
/// </summary>
public class TaxWithholdSalesPurchJournalUIBuilder_TH extends SrsReportDataContractUIBuilder
{
    private DialogField dialogFieldIncludeZeroLines;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the UI
    /// </summary>
    public void build()
    {
        DialogField dialogFieldAccountType;
        DialogField dialogFieldDetailSummary;
        TaxWithholdSalesPurchJournalContract_TH contract;

        super();

        contract = this.dataContractObject() as TaxWithholdSalesPurchJournalContract_TH;

        dialogFieldAccountType = this.bindInfo().getDialogField(contract, methodStr(TaxWithholdSalesPurchJournalContract_TH, parmAccountType));

        if (dialogFieldAccountType)
        {
            if (contract.parmModuleType() == ModuleCustVend::Cust)
            {
                // Customer type
                dialogFieldAccountType.label("@SYS4082598");
            }
            else
            {
                // Vendor type
                dialogFieldAccountType.label("@SYS314159");
            }
        }

        dialogFieldDetailSummary = this.bindInfo().getDialogField(contract, methodStr(TaxWithholdSalesPurchJournalContract_TH, parmDetailSummary));

        if (dialogFieldDetailSummary)
        {
            dialogFieldDetailSummary.registerOverrideMethod(
                methodStr(FormComboBoxControl, SelectionChange),
                methodStr(TaxWithholdSalesPurchJournalUIBuilder_TH, detailSummaryControlSelectionChanged),
                this);

            dialogFieldIncludeZeroLines = this.bindInfo().getDialogField(contract, methodStr(TaxWithholdSalesPurchJournalContract_TH, parmIncludeZeroLines));

            if (dialogFieldIncludeZeroLines)
            {
                dialogFieldIncludeZeroLines.value(false);
                dialogFieldIncludeZeroLines.visible(contract.parmDetailSummary() == DetailSummary::Summary);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>detailSummaryControlSelectionChanged</Name>
				<Source><![CDATA[
    private void detailSummaryControlSelectionChanged(FormComboBoxControl _dialogFieldDetailSummary)
    {
        if (dialogFieldIncludeZeroLines)
        {
            dialogFieldIncludeZeroLines.value(false);
            dialogFieldIncludeZeroLines.visible(_dialogFieldDetailSummary.selection() == DetailSummary::Summary);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>