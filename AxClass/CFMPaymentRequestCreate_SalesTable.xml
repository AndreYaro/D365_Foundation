<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestCreate_SalesTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestSourceAttribute(tableStr(SalesTable))]
public class CFMPaymentRequestCreate_SalesTable extends CFMPaymentRequestCreate
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canCreateFromPlannedPayments</Name>
				<Source><![CDATA[
    protected boolean canCreateFromPlannedPayments(Common _sourceRecord)
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDistributionSourceQuery</Name>
				<Source><![CDATA[
    protected Query getDistributionSourceQuery(Common _sourceRecord)
    {
        SalesTable sourceSalesTable = _sourceRecord;

        Query invoiceQuery = new Query();

        QueryBuildDataSource invoiceDataSource = invoiceQuery.addDataSource(tableNum(CustInvoiceJour));

        QueryBuildDataSource invoiceSalesLinkDataSource = invoiceDataSource.addDataSource(tableNum(CustInvoiceSalesLink));

        invoiceSalesLinkDataSource.addLink(fieldNum(CustInvoiceJour, SalesId), fieldNum(CustInvoiceSalesLink, SalesId));
        invoiceSalesLinkDataSource.addLink(fieldNum(CustInvoiceJour, InvoiceId), fieldNum(CustInvoiceSalesLink, InvoiceId));
        invoiceSalesLinkDataSource.addLink(fieldNum(CustInvoiceJour, InvoiceDate), fieldNum(CustInvoiceSalesLink, InvoiceDate));
        invoiceSalesLinkDataSource.joinMode(JoinMode::ExistsJoin);

        QueryBuildRange invoiceRange = invoiceSalesLinkDataSource.addRange(fieldNum(CustInvoiceSalesLink, OrigSalesId));
        invoiceRange.value(queryValue(sourceSalesTable.SalesId));

        return invoiceQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDistributionSourceTableId</Name>
				<Source><![CDATA[
    protected TableId getDistributionSourceTableId()
    {
        return tableNum(CustInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPaymentRequestSourceQuery</Name>
				<Source><![CDATA[
    protected Query getPaymentRequestSourceQuery(Common _sourceRecord)
    {
        CustInvoiceJour sourceInvoiceJour = _sourceRecord;

        Query query = new Query();

        QueryBuildDataSource qbdsCustTrans = query.addDataSource(tableNum(CustTrans));

        QueryBuildRange qbr = qbdsCustTrans.addRange(fieldNum(CustTrans, Invoice));
        qbr.value(queryValue(sourceInvoiceJour.InvoiceId));

        qbr = qbdsCustTrans.addRange(fieldNum(CustTrans, Voucher));
        qbr.value(queryValue(sourceInvoiceJour.LedgerVoucher));

        qbr = qbdsCustTrans.addRange(fieldNum(CustTrans, TransDate));
        qbr.value(queryValue(sourceInvoiceJour.InvoiceDate));

        qbr = qbdsCustTrans.addRange(fieldNum(CustTrans, AccountNum));
        qbr.value(queryValue(sourceInvoiceJour.InvoiceAccount));

        QueryBuildDataSource qbdsTransOpen = qbdsCustTrans.addDataSource(tableNum(CustTransOpen));

        qbr = qbdsTransOpen.addRange(fieldNum(CustTransOpen, AmountCur));
        qbr.value(#negative);

        qbdsTransOpen.addLink(fieldNum(CustTrans, RecId), fieldNum(CustTransOpen, RefRecId));
        qbdsTransOpen.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustTransOpen, AccountNum));
        qbdsTransOpen.joinMode(JoinMode::InnerJoin);

        QueryBuildDataSource qbdsPaymentRequest = qbdsTransOpen.addDataSource(tableNum(CFMPaymentRequestTable));

        qbr = qbdsPaymentRequest.addRange(fieldNum(CFMPaymentRequestTable, SourceType));
        qbr.value(queryValue(CFMPaymentRequestSourceType::CustInvoice));

        qbdsPaymentRequest.addLink(fieldNum(CustTransOpen, RecId), fieldNum(CFMPaymentRequestTable, SourceRefRecId));
        qbdsPaymentRequest.joinMode(JoinMode::NoExistsJoin);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPaymentRequestSourceTableId</Name>
				<Source><![CDATA[
    protected TableId getPaymentRequestSourceTableId()
    {
        return tableNum(CustTransOpen);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPlannedPaymentSourceQuery</Name>
				<Source><![CDATA[
    protected Query getPlannedPaymentSourceQuery(Common _sourceRecord)
    {
        return null;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>