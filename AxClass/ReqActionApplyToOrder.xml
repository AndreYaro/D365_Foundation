<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqActionApplyToOrder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This <c>ReqActionApplyToOrder</c> class is responsible for applying actions to the orders referenced by the requirement transaction.
/// </summary>
abstract class ReqActionApplyToOrder
{
    ReqTrans            reqTrans;
    ReqActionApply      reqActionApply;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromReqTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs an instance of the <c>ReqActionApplyToOrder</c> class based on the <c>ReqTrans</c>.
    /// </summary>
    /// <param name = "_reqTrans">A <c>ReqTrans</c> record</param>
    /// <param name = "_reqActionApply">An instance of <c>ReqActionApply</c>.</param>
    /// <returns>An instance of <c>ReqActionApplyToOrder</c>.</returns>
    public static ReqActionApplyToOrder newFromReqTrans(ReqTrans _reqTrans, ReqActionApply _reqActionApply)
    {
        ReqRefTypeFactoryAttribute attribute = new ReqRefTypeFactoryAttribute(_reqTrans.RefType);

        ReqActionApplyToOrder instance = SysExtensionAppClassFactory::getClassFromSysAttribute(classStr(ReqActionApplyToOrder), attribute) as ReqActionApplyToOrder;

        if (!instance)
        {
            throw error(strFmt("@GLS3800070", _reqTrans.RefType));
        }

        instance.initialize(_reqTrans, _reqActionApply);
        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    private void initialize(ReqTrans _reqTrans, ReqActionApply _reqActionApply)
    {
        this.reqTrans = _reqTrans;
        this.reqActionApply = _reqActionApply;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyActionToReferencedOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies the action to the order referenced by the requirement transaction.
    /// </summary>
    /// <param name = "_reqTrans">A <c>ReqTrans</c> record.</param>
    public abstract void applyActionToReferencedOrder()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateReqTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the action date and quantity for the order.
    /// </summary>
    protected void updateReqTrans()
    {
        reqTrans.reread();

        if (reqTrans.ActionDays != 0)
        {
            reqTrans.ReqDate = reqTrans.ActionDate;
        }

        if (reqTrans.ActionQtyAdd)
        {
            reqTrans.Qty += reqTrans.ActionQtyAdd;
        }

        reqTrans.update(null, false);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>