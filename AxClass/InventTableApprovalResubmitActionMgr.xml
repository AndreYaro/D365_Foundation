<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTableApprovalResubmitActionMgr</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTableApprovalResubmitActionMgr</c> menu item action event handler.
/// </summary>
public class InventTableApprovalResubmitActionMgr 
{
    Args args;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    protected Args parmArgs(Args _args = args)
    {
        args = _args;
        return args;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens a dialog box to use for resubmitting a journal workflow.
    /// </summary>
    protected void run()
    {
        recID                           recID               = args.record().RecId;
        tableId                         tableId             = args.record().TableId;
        InventJournalTable              inventJournalTable  = args.record();
        WorkflowWorkItemTable           workItem            = args.caller().getActiveWorkflowWorkItem();
        WorkflowWorkItemActionDialog    workflowWorkItemActionDialog;

        // The method has not been called correctly.
        if (tableId != tablenum(InventJournalTable) ||
            recId == 0)
        {
            throw error(strfmt("@SYS19306", funcname()));
        }

        // The journal has an active workflow.
        if (workItem.RecId > 0)
        {
            try
            {
                // The journal does support workflow approvals.
                workflowWorkItemActionDialog = WorkflowWorkItemActionDialog::construct( workItem,
                                                                                        WorkflowWorkItemActionType::Resubmit,
                                                                                        new MenuFunction(args.menuItemName(), args.menuItemType()));
                workflowWorkItemActionDialog.run();

                if (workflowWorkItemActionDialog.parmIsClosedOK())
                {
                    InventJournalTable inventJournalTableLoc = InventJournalTable::findByRecId(recID);

                    if (inventJournalTableLoc.WorkflowApprovalStatus == InventJournalWorkflowApprovalStatus::ChangeRequested ||
                        inventJournalTableLoc.WorkflowApprovalStatus == InventJournalWorkflowApprovalStatus::Rejected)
                    {
                        workItem    = args.caller().getActiveWorkflowWorkItem();
                        WorkflowWorkItemActionManager::dispatchWorkItemAction(  workItem,
                                                                                workflowWorkItemActionDialog.parmWorkflowComment(),
                                                                                workflowWorkItemActionDialog.parmTargetUser(),
                                                                                WorkflowWorkItemActionType::Resubmit,
                                                                                args.menuItemName());

                        // Set the workflow status to Submitted.
                        InventJournalWorkflowApprovalStatusManager::resubmitFromForm(inventJournalTable);
                    }
                    else
                    {
                        throw Exception::Error;
                    }
                }
            }
            catch(Exception::Error)
            {
                throw error(strfmt("@SYS111652", inventJournalTable.JournalNameId, inventJournalTable.JournalId));
            }
        }
        // Make the form refresh its common workflow UI controls.
        args.caller().updateWorkflowControls();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
	{
        InventTableApprovalResubmitActionMgr inventTableApprovalResubmitActionMgr = new InventTableApprovalResubmitActionMgr();
        inventTableApprovalResubmitActionMgr.parmArgs(_args);
        inventTableApprovalResubmitActionMgr.run();
	}

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>