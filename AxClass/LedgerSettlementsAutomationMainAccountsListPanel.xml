<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerSettlementsAutomationMainAccountsListPanel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerSettlementsAutomationMainAccountsListPanel</c> class manages a list panel control to use to select the
///    main accounts to be used for the Ledger settlement automation process.
/// </summary>
public class LedgerSettlementsAutomationMainAccountsListPanel extends SysListPanel
{
    LedgerSettlementsAutomationCriteria ledgerSettlementsAutomationCriteria;
    private boolean listPanelInitialized;
    private const int listPanelColumnCount = 2;
    private const int mainAccountColumnWidth = 80;
    private const int mainAccountNameColumnWidth = 140;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>selectionCount</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the number of items populated in the "selected" list panel.
    /// </summary>
    /// <returns>
    ///    An integer representing the number of items populated in the "selected" list panel.
    /// </returns>
    internal int selectionCount()
    {
        return viewSelected.getCount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeAllSelected</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Removes all items that were populated in the "selected" list panel, returning them to the "available" list panel.
    /// </summary>
    internal void removeAllSelected()
    {
        viewSelected.deleteAll();
    }

]]></Source>
			</Method>
			<Method>
				<Name>commitSelectionToDatabase</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Save the selected Main account entries to the LedgerSettlementsAutomationMatchSelection table
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The parent record from the LedgerSettlementsAutomationCriteria table that the selected main accounts are bound to.
    /// </param>
    [Hookable(false)]
    public void commitSelectionToDatabase(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        RefRecId mainAccountId;
        LedgerSettlementsAutomationMatchSelection ledgerSettlementsAutomationMatchSelection;

        // Need to remove all previous selections that were chosen.
        this.deletePreviousSelection(_ledgerSettlementsAutomationCriteriaRecId);

        FormListItem formListItem;
        container data;
        int idx = viewSelected.getCount() - 1;
        int i = 1;

        while(idx != -1)
        {
            formListItem = viewSelected.getItem(idx);
            data += formListItem.data();

            mainAccountId = conpeek(data, i);

            ledgerSettlementsAutomationMatchSelection.LedgerSettlementsAutomationCriteria = _ledgerSettlementsAutomationCriteriaRecId;
            ledgerSettlementsAutomationMatchSelection.SelectionRecId = mainAccountId;
            ledgerSettlementsAutomationMatchSelection.SelectionCriteria = LedgerSettlementsAutomationCriteriaSelection::MainAccounts;
            ledgerSettlementsAutomationMatchSelection.insert();

            idx --;
            i ++;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deletePreviousSelection</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Remove all saved Main accounts selected that are bound to the LedgerSettlementsAutomationCriteria record.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The parent record from the LedgerSettlementsAutomationCriteria table that the selected main accounts are bound to.
    /// </param>
    [Hookable(false)]
    public void deletePreviousSelection(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        LedgerSettlementsAutomationMatchSelection ledgerSettlementsAutomationMatchSelection;

        delete_from ledgerSettlementsAutomationMatchSelection
            where ledgerSettlementsAutomationMatchSelection.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
            && ledgerSettlementsAutomationMatchSelection.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::MainAccounts;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Stub class to satisfy the implemention requirement of this abstract method from class SysListPanel
    /// </summary>
    /// <param name="_data">
    ///    The item that was added to the list of selected main accounts.
    /// </param>
    internal void addData(container _data)
    {
        //super(); No need to call super() as we aren't immediately adding data, but stil need the method to satisfy the abstract method's implementation.
    }

]]></Source>
			</Method>
			<Method>
				<Name>getData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items for the left and right lists of the list panel.
    /// </summary>
    /// <returns>
    ///    A container with the items to add to both the selected and available lists of the list panel.
    /// </returns>
    internal container getData()
    {
        container mainAccountsSelected, mainAccountsAvailable;

        RefRecId recId = ledgerSettlementsAutomationCriteria.RecId;

        if (!listPanelInitialized)
        {
            // Get the main accounts already selected.
            mainAccountsSelected = LedgerSettlementsAutomationMainAccountsListPanel::getMainAccountsSelected(recId);

            // Get the main accounts available to select.
            mainAccountsAvailable = LedgerSettlementsAutomationMainAccountsListPanel::getMainAccountsAvailable(recId);

            listPanelInitialized = true;
        }

       return [mainAccountsAvailable, mainAccountsSelected];
    }

]]></Source>
			</Method>
			<Method>
				<Name>numOfColumnsAvailable</Name>
				<Source><![CDATA[
    internal int numOfColumnsAvailable()
    {
        return listPanelColumnCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>numOfColumnsSelected</Name>
				<Source><![CDATA[
    internal int numOfColumnsSelected()
    {
        return listPanelColumnCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeData</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Typically, this method would remove a main account entry from the "Selected main accounts" list,
    ///    but seeing as we are not actually storing the interim selections in a table, this method is not used, but since
    ///    we inherited it, we still need to have it's stub in this class.
    /// </summary>
    /// <param name="_data">
    ///    The item that was selected from the list of selected main accounts.
    /// </param>
    internal void removeData(container _data)
    {
        //super(); No need to call super() as we aren't immediately updating data, but stil need the method to satisfy the abstract method's implementation.
    }

]]></Source>
			</Method>
			<Method>
				<Name>setLedgerSettlementsAutomationCriteria</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the LedgerSettlementsAutomationCriteria reference for the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>LedgerSettlementsAutomationCriteria</c> record.
    /// </param>
    internal void setLedgerSettlementsAutomationCriteria(RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        ledgerSettlementsAutomationCriteria = LedgerSettlementsAutomationCriteria::find(_ledgerSettlementsAutomationCriteriaRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>sortMethod</Name>
				<Source><![CDATA[
    protected FormListSort sortMethod()
    {
        // Do not sort the list as the data is returned in the correct order.
        return FormListSort::NoSort;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the selected item can be added or removed from the list.
    /// </summary>
    /// <param name="_data">
    ///    The selected item in the panel list.
    /// </param>
    /// <param name="_addRemove">
    ///    Specifies whether the selected item is being added or removed.
    /// </param>
    /// <returns>
    ///    true if the selected item can be added or removed; otherwise, false.
    /// </returns>
    [Hookable(false)]
    public boolean validate(anytype _data, AddRemove _addRemove)
    {
        // Active configurations or configurations locked for editing by another user are not allowed to be updated.
        return super(_data, _addRemove);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Constructs an instance of the <c>LedgerSettlementsAutomationMainAccountsListPanel</c> class.
    /// </summary>
    /// <returns>
    ///    An instance of the <c>LedgerSettlementsAutomationMainAccountsListPanel</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    internal static LedgerSettlementsAutomationMainAccountsListPanel construct()
    {
        return new LedgerSettlementsAutomationMainAccountsListPanel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMainAccountsAvailable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items for the left list of the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>ledgerSettlementsAutomationCriteria</c> record.
    /// </param>
    /// <returns>
    ///    A container with the items to add to left list of the list panel.
    /// </returns>
    private static container getMainAccountsAvailable(
        RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        container mainAccountsAvailable;

        LedgerTransSettlementAccount ledgerTransSettlementAccount;
        MainAccount mainAccount;
        LedgerSettlementsAutomationMatchSelection alreadySelectedMainAccounts;

        // Populate the list of main accounts that are available to be selected for main accounts.
        while select RecId, MainAccountId, Name from mainAccount
            order by MainAccountId
            where mainAccount.LedgerChartOfAccounts == LedgerChartOfAccounts::current()
            exists join ledgerTransSettlementAccount
                where ledgerTransSettlementAccount.MainAccount == mainAccount.RecId
            notexists join alreadySelectedMainAccounts
                where mainAccount.RecId == alreadySelectedMainAccounts.SelectionRecId
                && alreadySelectedMainAccounts.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::MainAccounts
                && alreadySelectedMainAccounts.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
 
        {
            mainAccountsAvailable += [[mainAccount.RecId, mainAccount.MainAccountId, mainAccount.Name]];
        }

        return mainAccountsAvailable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMainAccountsSelected</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the items of the right list of the list panel.
    /// </summary>
    /// <param name="_ledgerSettlementsAutomationCriteriaRecId">
    ///    The record ID of the <c>LedgerSettlementsAutomationCriteria</c> record.
    /// </param>
    /// <returns>
    ///    A container with the items to add to the right list of the list panel.
    /// </returns>
    private static container getMainAccountsSelected(
        RefRecId _ledgerSettlementsAutomationCriteriaRecId)
    {
        container mainAccountSelected;

        LedgerTransSettlementAccount ledgerTransSettlementAccount;
        MainAccount mainAccount;
        LedgerSettlementsAutomationMatchSelection alreadySelectedMainAccounts;

        // Populate the list of main accounts that are currently selected.
        while select RecId, MainAccountId, Name from mainAccount
            order by MainAccountId
            where mainAccount.LedgerChartOfAccounts == LedgerChartOfAccounts::current()
            join alreadySelectedMainAccounts
            where alreadySelectedMainAccounts.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::MainAccounts
                && alreadySelectedMainAccounts.SelectionRecId == mainAccount.RecId
                && alreadySelectedMainAccounts.LedgerSettlementsAutomationCriteria == _ledgerSettlementsAutomationCriteriaRecId
        {
            mainAccountSelected += [[mainAccount.RecId, mainAccount.MainAccountId, mainAccount.Name]];
        }

        return mainAccountSelected;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes a new instance of the <c>LedgerSettlementsAutomationMainAccountsListPanel</c> class.
    /// </summary>
    /// <param name="_formRun">
    ///    The form that will contain the list panel controls.
    /// </param>
    /// <param name="_parentId">
    ///    The form control that will contain the list panel controls.
    /// </param>
    /// <returns>
    ///    An instance of the <c>LedgerSettlementsAutomationMainAccountsListPanel</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    internal static LedgerSettlementsAutomationMainAccountsListPanel newForm(FormRun _formRun, int _parentId)
    {
        LedgerSettlementsAutomationMainAccountsListPanel listPanel = LedgerSettlementsAutomationMainAccountsListPanel::construct();

        listPanel.parmFormRun(_formRun);
        listPanel.parmParentId(_parentId);
        listPanel.parmCaptionSelected("@GeneralLedger:SelectedMainAccounts");
        listPanel.parmCaptionAvailable("@GeneralLedger:AvailableMainAccounts");

        listPanel.parmHasUpDownButton(false);
        listPanel.parmHasAddAllButton(false);
        listPanel.parmHasRemoveAllButton(false);
        listPanel.parmShowColumnHeader(true);
        listPanel.build();

        return listPanel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getColumnHeaders</Name>
				<Source><![CDATA[
    protected Array getColumnHeaders()
    {
        Array columnHeaders = new Array(Types::Class);

        columnHeaders.value(1, new SysListPanelColumnHeader("@GeneralLedger:MatchCriteriaMainAccount", mainAccountColumnWidth));
        columnHeaders.value(2, new SysListPanelColumnHeader("@GeneralLedger:LedgerSettlementsAccountName", mainAccountNameColumnWidth));

        return columnHeaders;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>