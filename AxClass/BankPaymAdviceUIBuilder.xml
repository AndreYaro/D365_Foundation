<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymAdviceUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BankPaymAdviceUIBuilder</c> class consumes the bank payment advice contract to generate
/// form controls for the bank payment advice dialog.
/// </summary>
public final class BankPaymAdviceUIBuilder extends SrsReportDataContractUIBuilder
{
    private DialogField dialogLedgerJournalId;
    private DialogField dialogPaymentStatus;
    private DialogField dialogAccount;
    private DialogField dialogPaymMode;
    private DialogField dialogBank;

    private ModuleCustVend moduleCustVend;
    private DataAreaId paymentCompany;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        super();

        BankPaymAdviceContract bankPaymAdviceContract = this.dataContractObject() as BankPaymAdviceContract;

        this.bindContractFieldsToDialogFields(bankPaymAdviceContract);

        this.disableFieldForExecutionContext(bankPaymAdviceContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>bindContractFieldsToDialogFields</Name>
				<Source><![CDATA[
    private void bindContractFieldsToDialogFields(BankPaymAdviceContract _bankPaymAdviceContract)
    {
        dialogLedgerJournalId = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmLedgerJournalId));
        dialogPaymentStatus = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmPaymentStatus));
        dialogAccount = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmAccountNum));
        dialogPaymMode = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmPaymMode));
        dialogBank = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmCompanyBankAccountId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    public void postRun()
    {
        super();

        LedgerJournalTrans ledgerJournalTrans;

        if (this.controller().parmArgs() && this.controller().parmArgs().record())
        {
            ledgerJournalTrans = this.controller().parmArgs().record() as LedgerJournalTrans;
        }

        moduleCustVend = this.findModule(ledgerJournalTrans);
        paymentCompany = this.findCompanyContext(ledgerJournalTrans);
        this.initControlsFromPayment(ledgerJournalTrans);

        this.subscribeDialogFieldEvents();

        BankPaymAdviceContract contract = this.dataContractObject();

        if (contract.parmHideSSRSDestination())
        {
            this.hideSSRSDestinationTab();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>hideSSRSDestinationTab</Name>
				<Source><![CDATA[
    private void hideSSRSDestinationTab()
    {
        const str destinationControlName = formControlStr(SysOperationTemplateForm, Destination);

        FormControl destinationControl = dialog.formRun().control(dialog.formRun().controlId(destinationControlName));

        if (destinationControl)
        {
            destinationControl.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findModule</Name>
				<Source><![CDATA[
    private ModuleCustVend findModule(LedgerJournalTrans _ledgerJournalTrans)
    {
        ModuleCustVend custVend;

        switch (_ledgerJournalTrans.AccountType)
        {
            case LedgerJournalACType::Cust:
                custVend = ModuleCustVend::Cust;
                break;

            case LedgerJournalACType::Vend:
                custVend = ModuleCustVend::Vend;
                break;
        }

        return custVend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findCompanyContext</Name>
				<Source><![CDATA[
    private DataAreaId findCompanyContext(LedgerJournalTrans _ledgerJournalTrans)
    {
        DataAreaId companyContext = curExt();

        if (_ledgerJournalTrans.OffsetCompany != '')
        {
            companyContext = _ledgerJournalTrans.OffsetCompany;
        }

        return companyContext;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initControlsFromPayment</Name>
				<Source><![CDATA[
    private void initControlsFromPayment(LedgerJournalTrans _ledgerJournalTrans)
    {
        dialogLedgerJournalId.value(_ledgerJournalTrans.JournalNum);
        dialogLedgerJournalId.allowEdit(false);

        dialogPaymentStatus.value(_ledgerJournalTrans.PaymentStatus);
        dialogPaymentStatus.allowEdit(false);

        dialogPaymMode.value(_ledgerJournalTrans.PaymMode);

        if (_ledgerJournalTrans.AccountType == LedgerJournalACType::Cust
            || _ledgerJournalTrans.AccountType == LedgerJournalACType::Vend)
        {
            dialogAccount.value(_ledgerJournalTrans.parmAccount());
        }

        if (_ledgerJournalTrans.OffsetAccountType == ledgerJournalACType::Bank)
        {
            dialogBank.value(_ledgerJournalTrans.parmOffsetAccount());
        }

        if (this.disallowEditBankField(_ledgerJournalTrans))
        {
            dialogBank.allowEdit(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>subscribeDialogFieldEvents</Name>
				<Source><![CDATA[
    private void subscribeDialogFieldEvents()
    {
        dialogAccount.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(BankPaymAdviceUIBuilder, lookupAccountByModule),
            this);

        dialogPaymMode.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(BankPaymAdviceUIBuilder, lookupMethodOfPaymentByModule),
            this);

        dialogBank.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(BankPaymAdviceUIBuilder, lookupBankAccount),
            this);

        dialogBank.registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(BankPaymAdviceUIBuilder, validateBankAccount),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupAccountByModule</Name>
				<Source><![CDATA[
    private void lookupAccountByModule(FormControl _formControl)
    {
        switch (moduleCustVend)
        {
            case ModuleCustVend::Vend:
                VendTable::lookupVendor(_formControl, paymentCompany);
                break;

            case ModuleCustVend::Cust:
                CustTable::lookupCustomer(_formControl, paymentCompany);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupMethodOfPaymentByModule</Name>
				<Source><![CDATA[
    private void lookupMethodOfPaymentByModule(FormControl _formControl)
    {
        switch (moduleCustVend)
        {
            case ModuleCustVend::Vend:
                VendPaymModeTable::lookupPaymMethod(_formControl, dialogPaymMode.value(), paymentCompany);
                break;

            case ModuleCustVend::Cust:
                CustPaymModeTable::lookupPaymMethod(_formControl, dialogPaymMode.value(), paymentCompany);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupBankAccount</Name>
				<Source><![CDATA[
    private void lookupBankAccount(FormControl _formControl)
    {
        BankAccountTable::lookupBankAccount(_formControl, paymentCompany);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateBankAccount</Name>
				<Source><![CDATA[
    private boolean validateBankAccount(FormControl _formControl)
    {
        BankAccountID bankAccountId = _formControl.valueStr();
        boolean isValidBankAccount = true;

        if (bankAccountId != '')
        {
            isValidBankAccount = BankAccountTable::checkIsActive(_formControl.valueStr());
        }

        return isValidBankAccount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>disallowEditBankField</Name>
				<Source><![CDATA[
    private boolean disallowEditBankField(LedgerJournalTrans _ledgerJournalTrans)
    {
        LedgerJournalType journalType = _ledgerJournalTrans.ledgerJournalTable().JournalType;
        return journalType == LedgerJournalType::CustBillOfExchangeDraw
            || journalType == LedgerJournalType::VendPromissoryNoteDraw
            || journalType == LedgerJournalType::VendPromissoryNoteRedraw;
    }

]]></Source>
			</Method>
			<Method>
				<Name>disableFieldForExecutionContext</Name>
				<Source><![CDATA[
    private void disableFieldForExecutionContext(BankPaymAdviceContract _bankPaymAdviceContract)
    {
        DialogField dialogExecutionContext = this.bindInfo().getDialogField(_bankPaymAdviceContract, methodStr(BankPaymAdviceContract, parmReportExecutionContext));
        dialogExecutionContext.allowEdit(false);
        dialogExecutionContext.visible(false);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>