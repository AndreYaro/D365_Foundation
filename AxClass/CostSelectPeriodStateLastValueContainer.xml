<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CostSelectPeriodStateLastValueContainer</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>CostSelectPeriodStateLastValueContainer</c> is responsible for saving the <c>CostSelectPeriodState</c>
/// instances in the <c>SysLastValue</c> storage.
/// </summary>
public class CostSelectPeriodStateLastValueContainer implements SysSaveable
{
    CostSelectPeriodState state;
    str                   elementName;

    const int currentVersion = 1;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmState</Name>
				<Source><![CDATA[
    public CostSelectPeriodState parmState(CostSelectPeriodState _state = state)
    {
        state = _state;
        return state;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmElementName</Name>
				<Source><![CDATA[
    public str parmElementName(str _elementName = elementName)
    {
        elementName = _elementName;
        return elementName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [currentVersion, state.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the state with default values.
    /// </summary>
    public void initParmDefault()
    {
        state = CostSelectPeriodState::newFromFiscalYearPeriod();
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer     version    = conPeek(_packedClass, 1);

        if (version == currentVersion)
        {
            container   packedState = conPeek(_packedClass, 2);
            state = CostSelectPeriodState::construct();
            state.unpack(packedState);

            return true;
        }
        else
        {
            return false;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueDataAreaId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last value for the data area ID.
    /// </summary>
    /// <returns>The last value for the data area ID.</returns>
    public DataAreaId lastValueDataAreaId()
    {
        return curExt();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueUserId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last value for the user ID.
    /// </summary>
    /// <returns>The last value for the user ID.</returns>
    public UserId lastValueUserId()
    {
        return curUserId();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last value for the element type.
    /// </summary>
    /// <returns>The last value for the element type.</returns>
    public UtilElementType lastValueType()
    {
        return UtilElementType::Class;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueElementName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last value for the element name.
    /// </summary>
    /// <returns>The last value for the element name.</returns>
    public str lastValueElementName()
    {
        return elementName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueDesignName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last value for the class name.
    /// </summary>
    /// <returns>The last value for the class name.</returns>
    public str lastValueDesignName()
    {
        return classStr(CostSelectPeriodState);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        state = CostSelectPeriodState::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveInLastValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the provided instance owned by given form in the <c>SysLastValue</c>
    /// </summary>
    /// <param name="_state">
    /// The <c>CostSelectPeriodState</c> class instance.
    /// </param>
    /// <param name="_parentFormName">
    /// The name of the owning form.
    /// </param>
    public static void saveInLastValue(CostSelectPeriodState _state, FormName _parentFormName)
    {
        var costSelectPeriodStateLastValueContainer = new CostSelectPeriodStateLastValueContainer();
        costSelectPeriodStateLastValueContainer.parmState(_state);
        costSelectPeriodStateLastValueContainer.parmElementName(_parentFormName);

        xSysLastValue::saveLast(costSelectPeriodStateLastValueContainer);
    }

]]></Source>
			</Method>
			<Method>
				<Name>readStateFromLastValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the instance saved in the <c>SysLastValue</c>
    /// </summary>
    /// <param name="_parentFormName">
    /// The name of the owning form.
    /// </param>
    /// <returns>
    /// The <c>CostSelectPeriodState</c> class instance.
    /// </returns>
    public static CostSelectPeriodState readStateFromLastValue(FormName _parentFormName)
    {
        var costSelectPeriodStateLastValueContainer = new CostSelectPeriodStateLastValueContainer();
        costSelectPeriodStateLastValueContainer.parmElementName(_parentFormName);
        xSysLastValue::getLast(costSelectPeriodStateLastValueContainer);
        return costSelectPeriodStateLastValueContainer.parmState();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>