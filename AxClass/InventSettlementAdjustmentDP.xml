<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventSettlementAdjustmentDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventSettlementAdjustmentDP</c> class is the RDP class for the
/// <c>InventSettlementAdjustment</c> report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(InventSettlementAdjustment))
]
public class InventSettlementAdjustmentDP extends SrsReportDataProviderPreProcessTempDB
{
    InventSettlementAdjustmentTmp inventSettlementAdjustmentTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getInventSettlementAdjustmentTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the <c>InventSettlementAdjustmentTmp</c> temporary table.
    /// </summary>
    /// <returns>
    /// The <c>InventSettlementAdjustmentTmp</c> temporary table.
    /// </returns>
    [SRSReportDataSetAttribute(tableStr(InventSettlementAdjustmentTmp))]
    public InventSettlementAdjustmentTmp getInventSettlementAdjustmentTmp()
    {
        select inventSettlementAdjustmentTmp;
        return inventSettlementAdjustmentTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initInventSettlementAdjustmentTmp</Name>
				<Source><![CDATA[
    private void initInventSettlementAdjustmentTmp (InventTrans _inventTrans, InventSettlement _inventSettlement, InventTransOrigin _inventTransOrigin)
    {
        inventSettlementAdjustmentTmp.clear();
        inventSettlementAdjustmentTmp.Cancelled                      = _inventSettlement.Cancelled;
        inventSettlementAdjustmentTmp.ItemId                         = _inventSettlement.ItemId;
        inventSettlementAdjustmentTmp.TransDate                      = _inventSettlement.TransDate;
        inventSettlementAdjustmentTmp.Voucher                        = _inventSettlement.Voucher;
        inventSettlementAdjustmentTmp.SettleModel                    = _inventSettlement.SettleModel;
        inventSettlementAdjustmentTmp.CostAmountAdjustmentSettlement = _inventSettlement.CostAmountAdjustment;
        inventSettlementAdjustmentTmp.DateFinancial                  = _inventTrans.DateFinancial;
        inventSettlementAdjustmentTmp.Qty                            = _inventTrans.Qty;
        inventSettlementAdjustmentTmp.CostAmountAdjustmentTrans      = _inventTrans.CostAmountAdjustment;
        inventSettlementAdjustmentTmp.CostAmountPosted               = _inventTrans.CostAmountPosted;
        inventSettlementAdjustmentTmp.ReferenceCategory              = _inventTransOrigin.ReferenceCategory;
        inventSettlementAdjustmentTmp.ReferenceId                    = _inventTransOrigin.ReferenceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes business logic used in the <c>InventSettlementAdjustment</c> report.
    /// </summary>
    public void processReport()
    {
        InventTrans inventTrans;
        InventSettlement inventSettlement;
        InventTransOrigin inventTransOrigin;

        QueryRun tmpQueryRun = new QueryRun(this.parmQuery());
        inventSettlementAdjustmentTmp.setConnection(this.parmUserConnection());
        RecordInsertList recordInsertList = new RecordInsertList(tableNum(InventSettlementAdjustmentTmp), true, true, true, false, true, inventSettlementAdjustmentTmp);

        while (tmpQueryRun.next())
        {
            inventTrans = tmpQueryRun.get(tablenum(InventTrans));
            inventSettlement = tmpQueryRun.get(tablenum(InventSettlement));
            inventTransOrigin = tmpQueryRun.get(tablenum(InventTransOrigin));

            this.initInventSettlementAdjustmentTmp(inventTrans, inventSettlement, inventTransOrigin);
            recordInsertList.add(inventSettlementAdjustmentTmp);
        }

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>