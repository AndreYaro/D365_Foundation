<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjPeriodPostingLedgerSales</Name>
	<SourceCode>
		<Declaration><![CDATA[
class   ProjPeriodPostingLedgerSales extends ProjPeriodPostingLedger
{
    #Define.CurrentVersion(16)

    #LOCALMACRO.CurrentList
        transActionDate,
        acknowledgementDate
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkPostTrans</Name>
				<Source><![CDATA[
    private boolean checkPostTrans(ProjTrans _projTrans)
    {
        if (this.parmReverseTurnover())
        {
            if (! _projTrans.ledgerSalesPosted())
            {
                return false;
            }
        }

        if (! this.parmReverseTurnover())
        {
            if (_projTrans.ledgerSalesPosted())
            {
                return false;
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    /// interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    /// A dialog box can be either built by using the <c>Dialog</c> class or by using a class that is
    /// created in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        DialogRunbase       dialogRunbase;

        dialogRunbase = new DialogRunbase(ProjPeriodPostingLedgerSales::description(),this);

        dialogTransDate = dialogRunbase.addFieldValue(extendedTypeStr(TransDate), transactionDate, "@SYS14475", "@SYS53214");
        if (LedgerParameters::find().AcknowledgementDate_IT)
        {
            dialogAcknowledgementDate = dialogRunbase.addFieldValue(extendedTypeStr(AcknowledgementDate), DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), "@SYS67", "@SYS99220");
        }

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>enterCost</Name>
				<Source><![CDATA[
    protected void enterCost(LedgerVoucher _ledgerVoucher)
    {
        ProjTrans           projTrans;
        ProjCostTrans       projCostTrans;
        ProjCostTransSale   projCostTransSale;

        projCostTransSale = ProjCostTransSale::find(tmpProjPeriodicSale.SaleRefRecId, true);
        projCostTrans = ProjCostTrans::find(projCostTransSale.TransId, true);
        projTrans = ProjTrans::newProjCostTransSale(projCostTrans, projCostTransSale);

        if (this.checkPostTrans(projTrans))
        {
            ProjPost projPost = ProjPost::newEnterSalesAccrued_TrxDetail(projCostTrans, projCostTransSale, _ledgerVoucher, reverseTurnover);
            projPost.parmProjTrans().parmDefaultAccruedRevenueExchangeRateFromPostingDate(ProjAccruedRevenueExchangeRateFromPostingDateFlight::instance().isEnabled());
            projPost.postTrans();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>enterEmpl</Name>
				<Source><![CDATA[
    protected void enterEmpl(LedgerVoucher _ledgerVoucher)
    {
        ProjTrans           projTrans;
        ProjEmplTrans       projEmplTrans;
        ProjEmplTransSale   projEmplTransSale;

        projEmplTransSale = ProjEmplTransSale::find(tmpProjPeriodicSale.SaleRefRecId, true);
        projEmplTrans = ProjEmplTrans::find(projEmplTransSale.TransId, true);
        projTrans = ProjTrans::newProjEmplTransSale(projEmplTrans, projEmplTransSale);

        if (this.checkPostTrans(projTrans))
        {
            ProjPost projPost = ProjPost::newEnterSalesAccrued_TrxDetail(projEmplTrans, projEmplTransSale, _ledgerVoucher, reverseTurnover);
            projPost.parmProjTrans().parmDefaultAccruedRevenueExchangeRateFromPostingDate(ProjAccruedRevenueExchangeRateFromPostingDateFlight::instance().isEnabled());
            projPost.postTrans();

            //  post indirect revenue amount
            if (projEmplTrans.psaIndirectComponentGroup && isConfigurationkeyEnabled(configurationKeyNum(Project)))
            {
                ProjPost::psaNewEnterIndirectSalesAccrued(projEmplTrans, _ledgerVoucher, reverseTurnover).postTrans();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>enterItem</Name>
				<Source><![CDATA[
    protected void enterItem(LedgerVoucher _ledgerVoucher)
    {
        ProjTrans           projTrans;
        ProjItemTrans       projItemTrans;
        ProjItemTransSale   projItemTransSale;

        projItemTransSale = ProjItemTransSale::find(tmpProjPeriodicSale.SaleRefRecId, true);
        projItemTrans = ProjItemTrans::find(projItemTransSale.ProjTransId, true);
        projTrans = ProjTrans::newProjItemTransSale(projItemTrans, projItemTransSale);

        if (this.checkPostTrans(projTrans))
        {
            ProjPost projPost = ProjPost::newEnterSalesAccrued_TrxDetail(projItemTrans, projItemTransSale, _ledgerVoucher, reverseTurnover);
            projPost.parmProjTrans().parmDefaultAccruedRevenueExchangeRateFromPostingDate(ProjAccruedRevenueExchangeRateFromPostingDateFlight::instance().isEnabled());
            projPost.postTrans();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>enterRevenue</Name>
				<Source><![CDATA[
    protected void enterRevenue(LedgerVoucher _ledgerVoucher)
    {
        ProjTrans               projTrans;
        ProjRevenueTrans        projRevenueTrans;
        ProjRevenueTransSale    projRevenueTransSale;

        projRevenueTransSale = ProjRevenueTransSale::find(tmpProjPeriodicSale.SaleRefRecId, true);
        projRevenueTrans = ProjRevenueTrans::find(projRevenueTransSale.TransId, true);
        projTrans = ProjTrans::newProjRevenueTransSale(projRevenueTrans, projRevenueTransSale);

        if (this.checkPostTrans(projTrans))
        {
            ProjPost projPost = ProjPost::newEnterSalesAccrued_TrxDetail(projRevenueTrans, projRevenueTransSale, _ledgerVoucher, reverseTurnover);
            projPost.parmProjTrans().parmDefaultAccruedRevenueExchangeRateFromPostingDate(ProjAccruedRevenueExchangeRateFromPostingDateFlight::instance().isEnabled());
            projPost.postTrans();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    protected void update(LedgerVoucher _ledgerVoucher)
    {
        ProjTable projTable;

        // Stops the processing of transactions where project stage is closed/finished.
        while select tmpProjPeriodic
			join tmpProjPeriodicSale
				where tmpProjPeriodicSale.TransId == tmpProjPeriodic.TransId
            exists join projTable
				where projTable.ProjId == tmpProjPeriodic.ProjId
					&& projTable.Status != ProjStatus::Completed
        {
            this.updateTrans(_ledgerVoucher);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        tmpSortedList      = new SysRecordSortedList(tableNum(TmpProjPeriodic));
        tmpSortedCostList  = new SysRecordSortedList(tableNum(TmpProjPeriodicCost));
        tmpSortedSaleList  = new SysRecordSortedList(tableNum(TmpProjPeriodicSale));

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        while select tmpProjPeriodic
        {
            tmpSortedList.ins(tmpProjPeriodic);
        }

        while select tmpProjPeriodicCost
        {
            tmpSortedCostList.ins(tmpProjPeriodicCost);
        }

        while select tmpProjPeriodicSale
        {
            tmpSortedSaleList.ins(tmpProjPeriodicSale);
        }

        return [#CurrentVersion, #CurrentList, tmpSortedList.pack(), tmpSortedCostList.pack(), tmpSortedSaleList.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount
        NumberSeq               numberSeq =  NumberSeq::newGetVoucher(ProjParameters::numRefProjPeriodVoucher());

        LedgerVoucher       ledgerVoucher;
        LedgerVoucherObject     ledgerVoucherObject;
        DetailSummary           detailSummary = ProjParameters::find().LineTotalAccrueRevenue == ProjLineTotal::Line ? DetailSummary::Detail : DetailSummary::Summary;
        TransactionTxt          transTxt;

        Voucher                 voucher;

        transTxt  = TransactionTxt::construct(LedgerTransTxt::ProjectPostingLedger);

        try
        {
            ttsbegin;
            voucher = numberSeq.voucher();

            transTxt.setDate(transactionDate);
            transTxt.setVoucher(voucher);

            ledgerVoucher = LedgerVoucher::newLedgerPost(detailSummary,
                                                        SysModule::Project,
                                                        numberSeq.parmVoucherSequenceCode(),
                                                        this.transactionLogType(),
                                                        this.transactionLogTxt());
            ledgerVoucherObject = LedgerVoucherObject::newVoucher(voucher,
                                                                  transactionDate,
                                                                  SysModule::Project,
                                                                  LedgerTransType::Project);
            ledgerVoucherObject.lastTransTxt(transTxt.txt());
            if (!acknowledgementDate)
            {
                acknowledgementDate = transactionDate;
            }
            ledgerVoucherObject.parmAcknowledgementDate(acknowledgementDate);
            ledgerVoucher.addVoucher(ledgerVoucherObject);

            this.update(ledgerVoucher);

            if (ledgerVoucherObject.getInternalCollection().elements() != 0)
            {
                ledgerVoucher.end();

                info(strFmt("@SYS9799",voucher));
            }

            ttscommit;
        }
        catch (Exception::Error)
        {
            throw error("@SYS21628");
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>transactionLogTxt</Name>
				<Source><![CDATA[
    TransTxt transactionLogTxt()
    {
        return "@SYS92313";
    }

]]></Source>
			</Method>
			<Method>
				<Name>transactionLogType</Name>
				<Source><![CDATA[
    TransactionLogType transactionLogType()
    {
        return TransactionLogType::ProjRevenue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer         version        = conPeek(_packedClass,1);
        container       packedQuery, packedQuery2, packedQuery3;

        #LOCALMACRO.List14
            transActionDate
        #ENDMACRO

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedQuery, packedQuery2, packedQuery3]      = _packedClass;
                if (packedQuery)
                {
                    tmpSortedList.unpack(packedQuery);
                    tmpSortedList.first(tmpProjPeriodic);

                    while (tmpProjPeriodic)
                    {
                        tmpProjPeriodic.insert();
                        if (!tmpSortedList.next(tmpProjPeriodic))
                        {
                            break;
                        }
                    }
                }
                if (packedQuery2)
                {
                    tmpSortedCostList.unpack(packedQuery2);
                    tmpSortedCostList.first(tmpProjPeriodicCost);

                    while (tmpProjPeriodicCost)
                    {
                        tmpProjPeriodicCost.insert();
                        if (!tmpSortedCostList.next(tmpProjPeriodicCost))
                        {
                            break;
                        }
                    }
                }
                if (packedQuery3)
                {
                    tmpSortedSaleList.unpack(packedQuery3);
                    tmpSortedSaleList.first(tmpProjPeriodicSale);

                    while (tmpProjPeriodicSale)
                    {
                        tmpProjPeriodicSale.insert();
                        if (!tmpSortedSaleList.next(tmpProjPeriodicSale))
                        {
                            break;
                        }
                    }
                }
                break;

            case 15:
                [version, #CurrentList, packedQuery]      = _packedClass;
                if (packedQuery)
                {
                    tmpSortedList.unpack(packedQuery);
                    tmpSortedList.first(tmpProjPeriodic);

                    while (tmpProjPeriodic)
                    {
                        tmpProjPeriodic.insert();
                        if (!tmpSortedList.next(tmpProjPeriodic))
                        {
                            break;
                        }
                    }
                }
                break;

            case 14:
                [version, #List14, packedQuery]      = _packedClass;
                if (packedQuery)
                {
                    tmpSortedList.unpack(packedQuery);
                    tmpSortedList.first(tmpProjPeriodic);

                    while (tmpProjPeriodic)
                    {
                        tmpProjPeriodic.insert();
                        if (!tmpSortedList.next(tmpProjPeriodic))
                        {
                            break;
                        }
                    }
                }
                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTrans</Name>
				<Source><![CDATA[
    private void updateTrans(LedgerVoucher _ledgerVoucher)
    {
        if (tmpProjPeriodic.TransType   == ProjTransType::Hour)
        {
            this.enterEmpl(_ledgerVoucher);
        }

        if (tmpProjPeriodic.TransType   == ProjTransType::Cost)
        {
            this.enterCost(_ledgerVoucher);
        }

        if (tmpProjPeriodic.TransType   == ProjTransType::Revenue)
        {
            this.enterRevenue(_ledgerVoucher);
        }

        if (tmpProjPeriodic.TransType   == ProjTransType::Item)
        {
            this.enterItem(_ledgerVoucher);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        if (!dialogTransDate.value())
        {
            return checkFailed("@SYS24455");
        }

        if (LedgerParameters::find().AcknowledgementDate_IT)
        {
            GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceIT00010, funcName());
            if (dialogTransDate.value() > dialogAcknowledgementDate.value())
            {
                // Posting date may not exceed competence date.
                return checkFailed("@SYS99223");
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS50421";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>