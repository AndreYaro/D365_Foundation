<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerBalanceMainAccountAmounts_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerBalanceMainAccountAmounts_W</c> class is an extension of <c>LedgerBalanceMainAccountAmounts</c>.
/// </summary>
class LedgerBalanceMainAccountAmounts_W extends LedgerBalanceMainAccountAmounts
{
    CurrencyType_RU currencyType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculateBalance</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the balance for the main account.
    /// </summary>
    /// <param name="_mainAccount">
    ///    The main account.
    /// </param>
    /// <remarks>
    ///    The main account can have a direct or indirect balance. Use the <c>getAccountingCurrencyBalance
    ///    </c>method and the <c>getTmpData </c>method to access the results.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    ///    The main account is null.
    /// </exception>
    public void calculateBalance(MainAccount _mainAccount)
    {
        DimensionHierarchy dimHierFocus;

        if (_mainAccount == null)
        {
            throw error(Error::missingParameter(this));
        }

        if (useGeneralJournal)
        {
            dimHierFocus  = null;
        }
        else
        {
            dimHierFocus = DimensionHierarchy::getMainAccountFocus();
        }

        if (dimHierFocus == null)
        {
            LedgerBalanceQueryGeneralJournal generalJournalBalanceQuery = LedgerBalanceQueryGeneralJournal::construct();
            generalJournalBalanceQuery.setJournalizeJournal(journalizeJournal);
            generalJournalBalanceQuery.setBaseQuery(generalJournalBaseQuery);
            balanceQuery = generalJournalBalanceQuery;
        }
        else
        {
            if (FeatureStateProvider::IsFeatureEnabled(GeneralLedgerBalanceFeature::instance()))
            {
                balanceQuery = LedgerBalanceQueryGeneralLedgerBalance::construct();
            }
            else
            {
                balanceQuery = LedgerBalanceQueryDimensionFocus::construct();
            }
        }

        balanceQuery.setLedgerRecId(ledgerRecId);
        balanceQuery.setAccountingDateRange(startAccountingDate, endAccountingDate);
        balanceQuery.setExcludeValues(excludeDebits, excludeCredits, separateDebitsAndCredits);
        balanceQuery.setIncludeValues(includeRegularPeriod, includeOpeningPeriod, includeClosingPeriod, includeClosingPeriodBySystem);
        balanceQuery.setPostingLayer(postingLayer);
        balanceQuery.setPostingLayerList(postingLayerList);

        if (this.parmCurrencyType() == CurrencyType_RU::Currency)
        {
            balanceQuery.setGroupByValues(true, false);
        }

        balanceQuery.calculateForMainAccount(_mainAccount);

        accountingCurrencyDebitBalance = balanceQuery.getAccountingCurrencyDebitBalance();
        accountingCurrencyCreditBalance = balanceQuery.getAccountingCurrencyCreditBalance();
        reportingCurrencyBalance = balanceQuery.getReportingCurrencyBalance();
        quantityBalance = balanceQuery.getQuantityBalance();
        tmpAccountSum = balanceQuery.getTmpData();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static LedgerBalanceMainAccountAmounts_W construct()
    {
        return new LedgerBalanceMainAccountAmounts_W();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCurrencyType</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets or sets the currency type.
    /// </summary>
    /// <param name="_currencyType">
    ///     The currency type; optional.
    /// </param>
    /// <returns>
    ///     The currency type.
    /// </returns>
    public CurrencyType_RU parmCurrencyType(CurrencyType_RU _currencyType = currencyType)
    {
        currencyType = _currencyType;
        return currencyType;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>