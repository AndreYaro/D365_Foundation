<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventJournalFormTrans_Mov_Count</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class controls the performance/appearance of the
/// InventJournalCount form.
/// </summary>
class InventJournalFormTrans_Mov_Count extends InventJournalFormTrans_Movement
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>datasourceActivePost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the datasource activation event.
    /// </summary>
    public void datasourceActivePost()
    {
        super();

        if (#PdsCatchWeightEnabled)
        {
            FormDataSource ds = FormDataUtil::getFormDataSource(inventJournalTrans);
            ds.object(fieldNum(InventJournalTrans,PdsCWQty)).allowEdit(PdsGlobal::pdsIsCWItem(inventJournalTrans.ItemId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldModifiedInventDimFieldsPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the update when a dimension is changed.
    /// </summary>
    /// <param name="_inventDim">
    /// <c>InventDim</c> record modified.
    /// </param>
    /// <param name="_dimFieldId">
    /// Id of field that was modified.
    /// </param>
    public void fieldModifiedInventDimFieldsPost(
        InventDim   _inventDim,
        FieldId     _dimFieldId)
    {
        //
        // The parent function sets the cw counted field properly,
        // if this should be necessary. However, it does not adjust the
        // associated quantity fields.
        //
        PdsCWInventQty cwQty = inventJournalTrans.PdsCWInventQtyCounted;
        super(_inventDim, _dimFieldId);
        if (inventJournalTrans.PdsCWInventQtyCounted != cwQty)
        {
            this.fieldModifiedPdsCWInventQtyCountedPost();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldModifiedItemIdPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the item modificiation event.
    /// </summary>
    public void fieldModifiedItemIdPost()
    {
        super();

        if (#PdsCatchWeightEnabled)
        {
            FormDataSource ds = FormDataUtil::getFormDataSource(inventJournalTrans);
            ds.object(fieldNum(InventJournalTrans,PdsCWQty)).allowEdit(PdsGlobal::pdsIsCWItem(inventJournalTrans.ItemId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldModifiedPdsCWQtyPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the catch weight counted field.
    /// </summary>
    public void fieldModifiedPdsCWQtyPost()
    {
        if (cwFormCtrl)
        {
            InventQty preQty = inventJournalTrans.Counted;
            cwFormCtrl.fieldModified(fieldNum(InventJournalTrans,PdsCWQty));
            inventJournalTrans.PdsCWInventQtyCounted =
                  inventJournalTrans.PdsCWInventOnHand
                + inventJournalTrans.PdsCWQty;
            cwFormCtrl.fieldModified(fieldNum(InventJournalTrans,PdsCWInventQtyCounted));
            cwFormCtrl.fieldModified(fieldNum(InventJournalTrans,Counted));
            if (inventJournalTrans.Counted != preQty)
            {
                inventJournalTrans.inventMovement().journalSetCounted();
                FormDataUtil::getFormDataSource(inventJournalTrans).refresh();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the design update of the form.
    /// </summary>
    /// <param name="_mode">
    /// The mode the form is in when method is called.
    /// </param>
    protected void updateDesign(InventDimFormDesignUpdate _mode)
    {
        InventMovement lclMovement = InventMovement::construct(inventJournalTrans);

        if (_mode == InventDimFormDesignUpdate::Init)
        {
            if (!inventDimFormSetup)
            {
                inventDimFormSetup = InventDimCtrl_Frm_Mov_Count::
                    newFromFormMovement(formRun,lclMovement);
            }
        }

        super(_mode);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
