<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetControlRemoveDeletePendingRecordsTask</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data maintenance task to cleanup delete pending records of budget control.
/// </summary>
internal class BudgetControlRemoveDeletePendingRecordsTask implements IDataMaintenanceTask
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public DataMaintenanceTaskResult run()
    {
        DataMaintenanceTaskResult taskResult = DataMaintenanceTaskResult::construct();

        this.removeDeletePendingBudgetRecords();
       
        return taskResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>caption</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public DataMaintenanceCaption caption()
    {
        return literalStr("@Budget:BudgetControlRemoveDeletePendingRecordsTaskCaption");
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeDeletePendingBudgetRecords</Name>
				<Source><![CDATA[
    private void removeDeletePendingBudgetRecords()
    {
        BudgetSourceTracking budgetSourceTracking;
        BudgetSourceTrackingDetail budgetSourceTrackingDetail;
        BudgetSourceTrackingRelievingDetail budgetSourceTrackingRelievingDetail;

        ttsbegin;

        this.skipMethods(budgetSourceTracking);
        this.skipMethods(budgetSourceTrackingDetail);
        this.skipMethods(budgetSourceTrackingRelievingDetail);

        delete_from budgetSourceTracking
            where budgetSourceTracking.IsDeletePending == NoYes::Yes;

        delete_from budgetSourceTrackingDetail
            where budgetSourceTrackingDetail.IsDeletePending == NoYes::Yes;

        delete_from budgetSourceTrackingRelievingDetail
            where budgetSourceTrackingRelievingDetail.IsDeletePending == NoYes::Yes;

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>skipMethods</Name>
				<Source><![CDATA[
    private void skipMethods(Common _common)
    {
        _common.skipDataMethods(true);
        _common.skipDeleteActions(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>