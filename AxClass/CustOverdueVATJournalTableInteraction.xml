<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOverdueVATJournalTableInteraction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustOverdueVATJournalTableInteraction</c> class manages controls on <c>CustOverdueVATJournalTable</c> form
/// </summary>
class CustOverdueVATJournalTableInteraction extends PageInteraction
{
    Page                            page;
    CustOverdueVATJournalTable_W    journalTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>enableHeaderActions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables buttons on form action pane.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void enableHeaderActions()
    {
        OverdueJournalTable_W  lastJournal = OverdueJournalTable_W::findLast(OverdueJournalType_W::CustVATJournal);

        page.actionPaneControlEnabled(formControlStr(CustOverdueVATJournalTable_W, OverdueVATJournalPost_W),
            journalTable && !journalTable.Posted);
        page.actionPaneControlEnabled(formControlStr(CustOverdueVATJournalTable_W, OverdueVATJournalCancel_W),
            journalTable && journalTable.Posted && journalTable.RecId == lastJournal.RecId && !journalTable.Cancelled);
        page.actionPaneControlEnabled(formControlStr(CustOverdueVATJournalTable_W, LedgerTransVoucher),
            journalTable.Posted);
        page.actionPaneControlEnabled(formControlStr(CustOverdueVATJournalTable_W, TaxTrans),
            journalTable.Posted);

        FormDataSource custOverdueVATJournalTable_DS;

        custOverdueVATJournalTable_DS = FormDataUtil::getFormDataSource(journalTable);
        if (custOverdueVATJournalTable_DS)
        {
            custOverdueVATJournalTable_DS.allowCreate(lastJournal.Posted || !lastJournal);
            custOverdueVATJournalTable_DS.allowDelete(!journalTable.Posted);
            custOverdueVATJournalTable_DS.allowEdit(!journalTable.Posted);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes based on the active records for the form.
    /// </summary>
    private void init()
    {
        page         = this.page();

        journalTable = page.activeRecord(identifierStr(OverdueVATJournalTable));
    }

]]></Source>
			</Method>
			<Method>
				<Name>OverdueVATJournalTbl_DS_selectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Enables the header action menu item buttons when the active journal table has been updated.
    /// </summary>
    [FormDataSourceInteractionAttribute('OverdueVATJournalTable', 'selectionChanged')]
    public void OverdueVATJournalTbl_DS_selectionChanged()
    {
        this.init();

        this.enableHeaderActions();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>