<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLinePurchLineReturnImpl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for <c>PurchLine</c>.
/// </summary>
[
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::Credit),
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::CreditOnly),
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::ReplaceCredit),
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::ReplaceScrap),
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::ReturnToCust),
TaxModelDocumentLineReturnAttribute(tableStr(PurchLine), DispositionAction::Scrap)
]
public class TaxModelDocLinePurchLineReturnImpl_IN extends TaxModelDocLinePurchLineImpl_IN
    implements TaxModelTaxableReturn
{
    TaxModelDocLineVendInvoiceTransImpl_IN orignalInvoiceTransModel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDispositionAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>DispositionAction</c> of the transaction.
    /// </summary>
    /// <returns>
    /// the <c>DispositionAction</c> of the transaction
    /// </returns>
    public DispositionAction getDispositionAction()
    {
        return DispositionAction::Credit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOrignalInvoiceTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the original invoice transaction.
    /// </summary>
    /// <returns>
    /// The orignal invoice.
    /// </returns>
    public TaxModelTaxable getOrignalInvoiceTrans()
    {
        return orignalInvoiceTransModel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxRateBasingDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax rate basing date for the given tax type.
    /// </summary>
    /// <param name="_taxType">
    /// The given tax type.
    /// </param>
    /// <returns>
    /// The tax rate date.
    /// </returns>
    public TransDate getTaxRateBasingDate(TaxType_IN _taxType)
    {
        TransDate       taxCalculationDate;

        if (this.isTaxPerOrignal())
        {
            taxCalculationDate =  orignalInvoiceTransModel.getTaxRateBasingDate(_taxType);
        }
        else
        {
            taxCalculationDate = super(_taxType);
        }

        return taxCalculationDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets <c>TaxModelTransactionType_IN</c> of the transaction
    /// </summary>
    /// <returns>
    /// The <c>TaxModelTransactionType_IN</c> of the transaction
    /// </returns>
    public TaxModelTransactionType getTransactionType()
    {
        if (this.isOrignalTaxSettled())
        {
            return TaxModelTransactionType::Return;
        }
        else
        {
            return TaxModelTransactionType::StrikeBalance;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        VendInvoiceTrans vendInvoiceTrans;
        PurchLine_IN     purchLine_IN;

        super(_documentLineMap);

        purchLine_IN = purchLine.purchLine_IN();
        if (purchLine_IN.TaxWithholdLineNum)
        {
            select firstOnly * from vendInvoiceTrans
                where vendInvoiceTrans.PurchID == purchLine.InventRefId
                    && vendInvoiceTrans.LineNum == purchLine_IN.TaxWithholdLineNum;

            orignalInvoiceTransModel = TaxModelDocLineFactory::newTaxModelDocLine(vendInvoiceTrans) as TaxModelDocLineVendInvoiceTransImpl_IN;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isOrignalTaxSettled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determine the orignal tax is settled or not.
    /// </summary>
    /// <returns>
    /// True, if the orignal tax is settled;Otherwise, false.
    /// </returns>
    public boolean isOrignalTaxSettled()
    {
        if (orignalInvoiceTransModel)
        {
            return TaxTrans_IN::isTaxSettledForTransaction(
                orignalInvoiceTransModel.getDocumentLineMainTableId(),
                orignalInvoiceTransModel.getDocumentLineMainTableRecId());
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isTaxPerOrignal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Is the tax rate using the same of the orignal document line.
    /// </summary>
    /// <returns>
    /// True, if the tax rate using the same of the orignal document line.
    /// </returns>
    public boolean isTaxPerOrignal()
    {
        return orignalInvoiceTransModel && purchLine.purchLine_IN().TaxAsPerOriginalInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipCustomsTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Customs Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipCustomsTax(TaxOnItem _taxOnItem)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipServiceTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Service Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipServiceTax(TaxOnItem _taxOnItem)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxLogisticsPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax <c>LogisticsPostalAddress</c> of this transaction document line.
    /// </summary>
    /// <param name = "_type">The transaction tax information type.</param>
    /// <returns>
    /// The tax <c>LogisticsPostalAddress</c>
    /// </returns>
    /// This method is used to init TransTaxInformation.CompanyLocation, which should be defaulted from company address.
    public LogisticsPostalAddress getTaxLogisticsPostalAddress(TransTaxInformationType_IN _type = TransTaxInformationType_IN::Normal)
    {
        if (this.isBasedOnReferenceTaxModelDocLine())
        {
            return this.getReferenceTaxModelDocLine().getTaxLogisticsPostalAddress(_type);
        }
        else
        {
            InventDim inventDim = purchLine.inventDim();
            return LogisticsPostalAddress::findByLocation(
                TaxLogisticsLocationControllerHelper_IN::getDefaultLogisticsLocation(inventDim.InventLocationId, inventDim.InventSiteId));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>