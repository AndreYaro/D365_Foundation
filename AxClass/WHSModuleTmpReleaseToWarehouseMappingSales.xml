<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSModuleTmpReleaseToWarehouseMappingSales</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSModuleTmpReleaseToWarehouseMappingSales</c> class is handling the mapping of methods, fields and other types for WHSModule::Sales.
/// </summary>
[WHSModuleTmpReleaseToWarehouseMappingFactory(WHSModule::Sales)]
public class WHSModuleTmpReleaseToWarehouseMappingSales extends WHSModuleTmpReleaseToWarehouseMapping
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeWHSTmpReleaseToWarehouse</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes <c>WHSTmpReleaseToWarehouse</c> record for production sales module.
    /// </summary>
    /// <param name="_releaseToWarehouse">
    /// The <c>WHSTmpReleaseToWarehouse</c> table record.
    /// </param>
    /// <param name="_releaseToWarehouseId">
    /// The release to warehouse identifier.
    /// </param>
    /// <param name="_inventTransId">
    /// The inventory transaction identifier.
    /// </param>
    /// <param name="_inventDimId">
    /// The inventory dimension identifier.
    /// </param>
    /// <param name="_populateFulfillmentPolicy">
    /// true if the fulfillmentpolicy field should be populated; otherwise, false.
    /// </param>
    /// <param name="_qtyToRelease">
    /// The quantity to release to the warehouse
    /// </param>
    public void initializeWHSTmpReleaseToWarehouse(
        WHSTmpReleaseToWarehouse    _releaseToWarehouse,
        WHSReleaseToWarehouseId     _releaseToWarehouseId,
        InventTransId               _inventTransId,
        InventDimId                 _inventDimId,        
        boolean                     _populateFulfillmentPolicy,
        Qty                         _qtyToRelease)
    {
        SalesLine salesLine = SalesLine::findInventTransId(_inventTransId);
        _releaseToWarehouse.OrderNum = salesLine.SalesId;
        _releaseToWarehouse.CreditMaxCheck = CustParameters::find().CreditMaxCheck;
                        
        if (_populateFulfillmentPolicy)
        {
            WHSFulfillmentPolicy fulfillmentPolicy = WHSFulfillmentRateRequirementsProvider::getFulfillmentPolicyForSalesOrder(SalesTable::find(salesLine.SalesId));
            _releaseToWarehouse.FulfillmentPolicyRecId = fulfillmentPolicy.RecId;
        }

        WHSTmpReleaseToWarehouseCreateNewLineContext tmpReleaseToWarehouseCreateNewLineContext = WHSTmpReleaseToWarehouseCreateNewLineContext::current();
        if (tmpReleaseToWarehouseCreateNewLineContext && tmpReleaseToWarehouseCreateNewLineContext.populateShipmentConsolidationPolicy)
        {
            _releaseToWarehouse.ShipConsolidationPolicy = WHSShipConsolidationPolicyProvider::getShipmentConsolidationPolicyForSalesLine(salesLine).RecId;
        }

        _releaseToWarehouse.ReleaseToWarehouseId = _releaseToWarehouseId;
        _releaseToWarehouse.InventTransId        = _inventTransId;
        _releaseToWarehouse.InventDimId          = _inventDimId;
        _releaseToWarehouse.Module               = WHSModule::Sales;
        _releaseToWarehouse.ReleasedQty          = _qtyToRelease;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>