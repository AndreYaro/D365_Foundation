<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankDocumentUpdateBankTransTypeUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class BankDocumentUpdateBankTransTypeUIBuilder extends SysOperationUIBuilder
{
    private BankDocumentUpdateBankTransTypeContract contract;

    private BankAccountID bankAccountId;
    private BankTransactionType bankTransType;

    private DialogField dialogBankAccount;
    private DialogField dialogBankTransType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void build()
    {
        super();

        Dialog dialogLocal = dialog as Dialog;

        dialog.addText("@CashManagement:UpdateBankTransTypeDescription");

        dialogBankAccount = dialog.addFieldValue(extendedTypeStr(BankAccountID), bankAccountId);
        dialogBankTransType = dialog.addFieldValue(extendedTypeStr(BankTransactionType), bankTransType, "@CashManagement:UpdateBankTransTypeNew");
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void postRun()
    {
        super();

        // Bank account
        dialogBankAccount.value(this.getContract().parmBankAccount());
        dialogBankAccount.fieldControl().mandatory(true);

        dialogBankAccount.registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(BankDocumentUpdateBankTransTypeUIBuilder, bankAccountModified),
            this);

        dialogBankAccount.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(BankDocumentUpdateBankTransTypeUIBuilder, lookupBankAccount),
            this);

        // Bank trans type
        dialogBankTransType.value(this.getContract().parmBankTransType());
        dialogBankTransType.fieldControl().mandatory(true);

        dialogBankTransType.registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(BankDocumentUpdateBankTransTypeUIBuilder, bankTransTypeModified),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupBankAccount</Name>
				<Source><![CDATA[
    private void lookupBankAccount(FormControl _formControl)
    {
        BankAccountTable::lookupBankReconEnabledBankAccount(_formControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>bankAccountModified</Name>
				<Source><![CDATA[
    private boolean bankAccountModified(FormStringControl _bankAccountControl)
    {
        this.getContract().parmBankAccount(_bankAccountControl.valueStr());

        boolean isValid = this.getContract().validateBankAccount();

        this.setDialogOkButtonEnabled(dialog, isValid);

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>bankTransTypeModified</Name>
				<Source><![CDATA[
    private boolean bankTransTypeModified(FormStringControl _bankTransTypeControl)
    {
        this.getContract().parmBankTransType(_bankTransTypeControl.valueStr());

        boolean isValid = this.getContract().validateBankTransType();

        this.setDialogOkButtonEnabled(dialog, isValid);

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDialogOkButtonEnabled</Name>
				<Source><![CDATA[
    private void setDialogOkButtonEnabled(SysOperationDialog _dialog, boolean _shouldEnable)
    {
        const str OkButton = formControlStr(SysOperationTemplateForm, CommandButton);

        FormControl okButtonControl = _dialog.formRun().control(_dialog.formRun().controlId(OkButton));

        okButtonControl.enabled(_shouldEnable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getContract</Name>
				<Source><![CDATA[
    private BankDocumentUpdateBankTransTypeContract getContract()
    {
        if (contract == null)
        {
            contract = this.dataContractObject() as BankDocumentUpdateBankTransTypeContract;
        }

        return contract;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>