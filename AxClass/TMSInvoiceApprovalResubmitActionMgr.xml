<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSInvoiceApprovalResubmitActionMgr</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class TMSInvoiceApprovalResubmitActionMgr
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[	/// <summary>
	/// Resubmits reconciled invoice for approval.
	/// </summary>
	/// <param name="args">
	/// Args containing <c>TMSInvoiceTable</c> record.
	/// </param>
	/// <param name="_workflowItem">
	/// Active workflow item.
	/// </param>
    public static void main(Args args, WorkflowWorkItemTable _workflowItem = null)
    {
        RecId                           recId = args.record().RecId;
        WorkflowComment                 note = "";
        WorkflowWorkItemActionDialog    workflowWorkItemActionDialog;
        TMSInvoiceTable                 invoiceTable;
        boolean                         reconciled;
        TMSInvoiceLine                  invoiceLine;
        boolean                         submit;
        WorkflowWorkItemTable           workflowWorkItemTable;

        recId = args.record().RecId;
        invoiceTable = args.record();

        reconciled = TMSInvoiceApproval::checkInvoice(invoiceTable);

        if (reconciled)
        {
            if (_workflowItem)
            {
                workflowWorkItemTable = _workflowItem;
            }

            else
            {
                workflowWorkItemTable = args.caller().getActiveWorkflowWorkItem();
            }

            // Opens the resubmit to workflow dialog.
            // This line of code is different in the submit and resubmit methods
            workflowWorkItemActionDialog = WorkflowWorkItemActionDialog::construct(workflowWorkItemTable,
                                                                                   WorkflowWorkItemActionType::Resubmit,
                                                                                   new MenuFunction(menuitemactionstr(TMSInvoiceApprovalResubmit),
                                                                                   MenuItemType::Action));
            workflowWorkItemActionDialog.run();

            if (WorkflowWorkItemActionDialog.parmIsClosedOK())
            {
                note = workflowWorkItemActionDialog.parmWorkflowComment();

                try
                {
                    while select invoiceLine
                        where (invoiceLine.InvoiceLineStatus == TMSInvoiceLineStatus::Open                ||
                              invoiceLine.InvoiceLineStatus  == TMSInvoiceLineStatus::PartiallyApproved   ||
                              invoiceLine.InvoiceLineStatus  == TMSInvoiceLineStatus::Rejected)  &&
                              invoiceLine.InternalInvNumber  == invoiceTable.InternalInvNumber
                    {
                        // Make sure invoice line has something matched or reconciled to submit
                        if (invoiceLine.amountReadyToSubmit())
                        {
                            submit = true;
                            break;
                        }
                    }

                    if (!submit)
                    {
                        throw error("@TRX1288");
                    }

                    ttsbegin;

                    // This line of code is different in the submit and resubmit methods
                    WorkflowWorkItemActionManager::dispatchWorkItemAction(workflowWorkItemTable,
                                                                          note,
                                                                          curUserId(),
                                                                          WorkflowWorkItemActionType::Resubmit,
                                                                          args.menuItemName());

                    invoiceTable.InvoiceStatus =  TMSInvoiceStatus::PendingAfterReject;
                    invoiceTable.update();

                    // Send a Resubmitted to Workflow Infolog message.
                    info("@TRX1243");

                    ttscommit;
                }

                catch(exception::Error)
                {
                    info("@TRX1227");
                }
            }
        }

        else
        {
            info("@TRX1311");
        }

        if (!_workflowItem)
        {
            args.caller().updateWorkFlowControls();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>