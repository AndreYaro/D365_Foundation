<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmActivityAdvancedPromptEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class handles <c>smmActivityAdvancedPrompt</c> events.
/// </summary>
class smmActivityAdvancedPromptEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>smmActivityAdvancedPrompt_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handler for form modification to be run on post initialization of the form.
    /// </summary>
    /// <param name="_sender">The formrun object raising the event.</param>
    /// <param name="_e">Args for the event.</param>
    [FormEventHandler(formStr(smmActivityAdvancedPrompt), FormEventType::Initialized)]
    public static void smmActivityAdvancedPrompt_OnInitialized(xFormRun _sender, FormEventArgs _e)
    {
        // Initialize the instance of this form extension handler now that the controls exist
        FormRun smmActivityAdvancedPromptForm = _sender as FormRun;
        TableId callerTableId;

        if (smmActivityAdvancedPromptForm.args().parm())
        {
            callerTableId = str2int(smmActivityAdvancedPromptForm.args().parm());
        }

        if (callerTableId == tablenum(SMAServiceOrderTable) || callerTableId == tablenum(SMAAgreementTable))
        {
            FormStringControl phaseId = _sender.design().controlName(formControlStr(smmActivityAdvancedPrompt, smmActivities_PhaseId));
            phaseId.visible(false);

            FormComboBoxControl taskPriority = _sender.design().controlName(formControlStr(smmActivityAdvancedPrompt, smmActivities_TaskPriority));
            taskPriority.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>Category_OnValidated</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the Category field.
    /// </summary>
    /// <param name="_sender">The form data object raising the event.</param>
    /// <param name="_e">Args for the event.</param>
    [FormDataFieldEventHandler(formDataFieldStr(smmActivityAdvancedPrompt, smmActivities, Category), FormDataFieldEventType::Validated)]
    public static void Category_OnValidated(FormDataObject _sender, FormDataFieldEventArgs _e)
    {
        FormRun smmActivityAdvancedPromptForm = _sender.datasource().formRun();
        smmActivities smmActivities  = smmActivityAdvancedPromptForm.dataSource().cursor();

        TableId callerTableId;

        if (smmActivityAdvancedPromptForm.args().parm())
        {
            callerTableId = str2int(smmActivityAdvancedPromptForm.args().parm());
        }

        if (smmActivities.Category == smmActivityCategory::Appointment)
        {
            if (callerTableId == tablenum(smmTMCallListTable))
            {
                throw error("@SYS90484");
            }

            if (callerTableId == tablenum(smmCampaignSelection))
            {
                throw error("@SYS90485");
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>