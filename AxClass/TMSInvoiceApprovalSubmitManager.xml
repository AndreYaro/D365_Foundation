<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSInvoiceApprovalSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[/// <summary>
/// The <c>TMSInvoiceApprovalSubmitManager</c> class is used to submit invoices for approval.
/// </summary>
public class TMSInvoiceApprovalSubmitManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[	/// <summary>
	/// Submits reconciled and matched invoice lines for approval.
	/// </summary>
	/// <param name="args">
	/// Args containing <c>TMSInvoiceTable</c> record.
	/// </param>
	/// <param name="_configTable">
	/// <c>WorkflowVersionTable</c> record.
	/// </param>
    void submit(Args args, WorkflowVersionTable _configTable = null)
    {
        RecId                   recId = args.record().RecId;
        WorkflowComment         note;
        WorkflowSubmitDialog    workflowSubmitDialog;
        TMSInvoiceTable         invoiceTable;
        boolean                 reconciled;
        TMSInvoiceLine          invoiceLine;
        boolean                 submit;
        WorkflowVersionTable    versionTable;

        recId = args.record().RecId;
        invoiceTable = args.record();

        reconciled = TMSInvoiceApproval::checkInvoice(invoiceTable);

        if (reconciled)
        {
            if (_configTable)
            {
                versionTable = _configTable;
            }

            else
            {
                versionTable = args.caller().getActiveWorkflowConfiguration();
            }

            workflowSubmitDialog = WorkflowSubmitDialog::construct(versionTable);
            workflowSubmitDialog.run();

            if (workflowSubmitDialog.parmIsClosedOK())
            {
                note = workflowSubmitDialog.parmWorkflowComment();

                while select invoiceLine
                    where (invoiceLine.InvoiceLineStatus == TMSInvoiceLineStatus::Open                ||
                          invoiceLine.InvoiceLineStatus  == TMSInvoiceLineStatus::PartiallyApproved   ||
                          invoiceLine.InvoiceLineStatus  == TMSInvoiceLineStatus::Rejected)           &&
                          invoiceLine.InternalInvNumber  == invoiceTable.InternalInvNumber
                {
                    // Make sure invoice line has something matched or reconciled to submit
                    if (invoiceLine.amountReadyToSubmit())
                    {
                        submit = true;
                        break;
                    }
                }

                if (!submit)
                {
                    throw error("@TRX1288");
                }

                try
                {
                    TMSCommProxy::submitInvoiceApproval(recId, note, true);
                }
                catch
                {
                    info("@TRX1227");
                }
            }
        }

        else
        {
            info("@TRX1311");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mainConfigTable</Name>
				<Source><![CDATA[
    public static void mainConfigTable(Args _args, WorkflowVersionTable _configTable)
    {
        TMSInvoiceApprovalSubmitManager invoiceApprovalSubmit = new TMSInvoiceApprovalSubmitManager();

        invoiceApprovalSubmit.submit(_args, _configTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[	/// <summary>
	/// Submits an invoice for approval.
	/// </summary>
	/// <param name="_args">
	/// Args containing <c>TMSInvoiceTable</c> record.
	/// </param>
	/// <param name="_configTable">
	/// Workflow version.
	/// </param>
    public static void main(Args _args)
    {
        TMSInvoiceApprovalSubmitManager::mainConfigTable(_args, null);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>