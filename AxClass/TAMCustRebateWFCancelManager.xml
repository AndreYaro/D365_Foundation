<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMCustRebateWFCancelManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMCustRebateWFCancelManager</c> class handles cancelling
/// a TAMCustRebate workflow.
/// </summary>
class TAMCustRebateWFCancelManager extends WorkflowCancelManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateDocument</Name>
				<Source><![CDATA[
    public void updateDocument(Args _args)
    {
        PdsRebateAgreement pdsRebateAgreement;

        if (_args.record() is PdsRebateAgreement)
        {
            pdsRebateAgreement = _args.record() as PdsRebateAgreement;
            pdsRebateAgreement.reread();
        }
        else if (_args.record() is WorkflowTrackingStatusTable)
        {
            WorkflowTrackingStatusTable workflowTrackingStatusTable = _args.record() as WorkflowTrackingStatusTable;
            workflowTrackingStatusTable.reread();

            if (workflowTrackingStatusTable.ContextTableId == tableNum(PdsRebateAgreement))
            {
                pdsRebateAgreement = PdsRebateAgreement::find(workflowTrackingStatusTable.ContextRecId);
            }
        }

        if (pdsRebateAgreement)
        {
            ttsbegin;
            pdsRebateAgreement.selectForUpdate(true);
            pdsRebateAgreement.TAMCustRebateApprovalStatus = TAMCustRebateApprovalStatus::NotSubmitted;
            pdsRebateAgreement.update();
            ttscommit;

            FormDataSource pdsRebateAgreement_ds = FormDataUtil::getFormDataSource(pdsRebateAgreement);
            // If a datasource was passed in, update the datasource and cause it to be refreshed on the form.
            if (pdsRebateAgreement_ds)
            {
                pdsRebateAgreement_ds.research(true);
            }
        }
        else
        {
            throw error(strFmt("@SCM:WorkflowMissingRecordError", tableStr(PdsRebateAgreement), tableId2Name(_args.record().TableId)));
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TAMCustRebateWFCancelManager construct()
    {
        return new TAMCustRebateWFCancelManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        TAMCustRebateWFCancelManager  workflowCancelManager = TAMCustRebateWFCancelManager::construct();
        workflowCancelManager.run(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>