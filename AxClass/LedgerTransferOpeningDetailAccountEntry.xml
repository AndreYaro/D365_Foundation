<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerTransferOpeningDetailAccountEntry</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerTransferOpeningDetailAccountEntry</c> class used to insert records in to the <c>GeneralJournalAccountEntry</c> table 
/// based on <c>GeneralJournalAccountEntryDetailsTmp</c> for opening transactions at year-end close.
/// </summary>
public class LedgerTransferOpeningDetailAccountEntry
{
    private GeneralJournalAccountEntryDetailsTmp entryDetails;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDetailsTransactions</Name>
				<Source><![CDATA[
    internal static void insertDetailsTransactions(
        RefRecId _journalEntryRecId,
        CurrentOperationsTax _postingLayer,
        int _processSession,
        UTCTimeStamp _processTimeStamp)
    {
        LedgerTransferOpeningDetailAccountEntry createAccountEntry = new LedgerTransferOpeningDetailAccountEntry();

        GeneralJournalAccountEntry accountEntry;
        accountEntry.skipDataMethods(true);
        accountEntry.skipEvents();

        SysDaInsertObject insertObject = createAccountEntry.getGeneralJournalAccountEntryInsertObject(accountEntry);
        SysDaQueryObject queryObject = createAccountEntry.getQueryObject(
            _journalEntryRecId,
            _postingLayer,
            _processSession,
            _processTimeStamp);

        insertObject.query(queryObject);

        SysDaInsertStatement generalJournalAccountInsertStatement = new SysDaInsertStatement();

        ttsbegin;
        generalJournalAccountInsertStatement.insert(insertObject);
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getGeneralJournalAccountEntryInsertObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the <c>GeneralJournalAccountEntry</c> table fields to an Insert object.
    /// </summary>
    /// <param name = "_generalJournalAccountEntry">
    /// The <c>GeneralJournalAccountEntry</c> buffer.
    /// </param>
    /// <returns>
    /// The insert object with the fields added.
    /// </returns>
    [Wrappable(true)]
    protected SysDaInsertObject getGeneralJournalAccountEntryInsertObject(GeneralJournalAccountEntry _generalJournalAccountEntry)
    {
        SysDaInsertObject generalJournalAccountEntryInsertObject = new SysDaInsertObject(_generalJournalAccountEntry);

        generalJournalAccountEntryInsertObject.fields()
            .add(fieldStr(GeneralJournalAccountEntry, AccountingCurrencyAmount))
            .add(fieldStr(GeneralJournalAccountEntry, GeneralJournalEntry))
            .add(fieldStr(GeneralJournalAccountEntry, IsCorrection))
            .add(fieldStr(GeneralJournalAccountEntry, IsCredit))
            .add(fieldStr(GeneralJournalAccountEntry, LedgerAccount))
            .add(fieldStr(GeneralJournalAccountEntry, LedgerDimension))
            .add(fieldStr(GeneralJournalAccountEntry, MainAccount))
            .add(fieldStr(GeneralJournalAccountEntry, PostingType))
            .add(fieldStr(GeneralJournalAccountEntry, OriginalAccountEntry))
            .add(fieldStr(GeneralJournalAccountEntry, ReportingCurrencyAmount))
            .add(fieldStr(GeneralJournalAccountEntry, Text))
            .add(fieldStr(GeneralJournalAccountEntry, TransactionCurrencyAmount))
            .add(fieldStr(GeneralJournalAccountEntry, TransactionCurrencyCode))
            .add(fieldStr(GeneralJournalAccountEntry, FinTag));

        return generalJournalAccountEntryInsertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryObject</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    protected SysDaQueryObject getQueryObject(
        RefRecId _journalEntryRecId,
        CurrentOperationsTax _postingLayer,
        int _processSession,
        UTCTimeStamp _processTimeStamp)
    {
        SysDaQueryObject entryQuery = new SysDaQueryObject(entryDetails);

        this.addFieldsToGeneralJournalAccountEntryDetailsTmp(entryQuery, _journalEntryRecId);
        this.addWhereClauseToGeneralJournalAccountEntryDetailsTmp(entryQuery, _postingLayer, _processSession, _processTimeStamp);

        return entryQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addFieldsToGeneralJournalAccountEntryDetailsTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>GeneralJournalAccountEntryDetailsTmp</c> fields to the projection of a query object.
    /// </summary>
    /// <param name = "_entryQuery">
    /// The query object to add the fields to.
    /// </param>
    /// <param name = "_journalEntryRecId">
    /// The RecId of general journal entry 
    /// </param>
    [Wrappable(true)]
    protected void addFieldsToGeneralJournalAccountEntryDetailsTmp(
        SysDaQueryObject _entryQuery,
        RefRecId _journalEntryRecId)
    {
        _entryQuery.projection()
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, AccountingCurrencyAmount))
            .addValue(_journalEntryRecId)
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, IsCorrection))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, IsCredit))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, LedgerAccount))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, LedgerDimension))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, MainAccount))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, PostingType))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, GeneralJournalAccountEntryRecId))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, ReportingCurrencyAmount))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, Text))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, TransactionCurrencyAmount))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, TransactionCurrencyCode))
            .add(fieldStr(GeneralJournalAccountEntryDetailsTmp, FinTag));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addWhereClauseToGeneralJournalAccountEntryDetailsTmp</Name>
				<Source><![CDATA[
    private void addWhereClauseToGeneralJournalAccountEntryDetailsTmp(
        SysDaQueryObject _entryQuery,
        CurrentOperationsTax _postingLayer,
        int _processSession,
        UTCTimeStamp _processTimeStamp)
    {
        _entryQuery.whereClause(new SysDaAndExpression(
            new SysDaEqualsExpression(new SysDaFieldExpression(entryDetails, fieldStr(GeneralJournalAccountEntryDetailsTmp, PostingLayer)),
                new SysDaValueExpression(_postingLayer)),
            new SysDaEqualsExpression(new SysDaFieldExpression(entryDetails, fieldStr(GeneralJournalAccountEntryDetailsTmp, ProcessSessionId)),
                new SysDaValueExpression(_processSession)))
            .and(new SysDaEqualsExpression(new SysDaFieldExpression(entryDetails, fieldStr(GeneralJournalAccountEntryDetailsTmp, ProcessUTCTimestamp)),
                new SysDaValueExpression(_processTimeStamp))));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>