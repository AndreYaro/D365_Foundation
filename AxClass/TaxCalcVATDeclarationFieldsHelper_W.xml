<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxCalcVATDeclarationFieldsHelper_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxCalcVATDeclarationFields</c> class calculates the fields used in VAT tax reporting export.
/// </summary>
public class TaxCalcVATDeclarationFieldsHelper_W
{
    TmpTaxReportById tmpTaxReportById;
    Query query;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmTmpTaxReportById</Name>
				<Source><![CDATA[
    public TmpTaxReportById parmTmpTaxReportById(TmpTaxReportById _tmpTaxReportById = tmpTaxReportById)
    {
        tmpTaxReportById = _tmpTaxReportById;
        return tmpTaxReportById;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQuery</Name>
				<Source><![CDATA[
    public Query parmQuery(Query _query = query)
    {
        query = _query;
        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calc</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the tax fields.
    /// </summary>
    public void calc()
    {
        TmpTaxReportById tmpTaxReportByIdLocal;
        TaxReportCollection taxReportCollection;
        TaxReportVoucher taxReportVoucher;

        QueryRun queryRun = new QueryRun(query);

        if (query)
        {
            while (queryRun.next())
            {
                taxReportVoucher = queryRun.get(tableNum(TaxReportVoucher));

                tmpTaxReportByIdLocal = TaxTrans::taxAmountByReportId_Table(
                    taxReportVoucher.FromDate,
                    taxReportVoucher.ToDate,
                    taxReportVoucher.TaxPeriod,
                    taxReportVoucher.TaxRepVersion,
                    taxReportVoucher.TaxRepCounter);

                tmpTaxReportById = TaxCalcVATDeclarationFieldsHelper_W::sumTaxAmountByReportId(tmpTaxReportByIdLocal, tmpTaxReportById);
            }

            while select tmpTaxReportById
                join taxReportCollection
                    where tmpTaxReportById.TaxReportField   == taxReportCollection.TaxReportField &&
                        taxReportCollection.TaxReportLayout == TaxReportLayout::Estonia
            {
                ttsbegin;
                tmpTaxReportById.Amount = -tmpTaxReportById.Amount;
                tmpTaxReportById.update();
                ttscommit;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>sumTaxAmountByReportId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the total tax amount by report id.
    /// </summary>
    /// <param name = "_tmpTaxReportById">The list of tax report.</param>
    /// <param name = "_tmpTaxReportByIdTotal">The total tax report.</param>
    /// <returns>The tax amount list in summary.</returns>
    public static TmpTaxReportById sumTaxAmountByReportId(TmpTaxReportById _tmpTaxReportById, TmpTaxReportById _tmpTaxReportByIdTotal)
    {
        ttsbegin;

        while select sum(Amount), sum(TaxInCostPrice)
            from _tmpTaxReportById
            group by TaxReportField
        {
            select TaxReportField, Amount, TaxInCostPrice from _tmpTaxReportByIdTotal
                where _tmpTaxReportByIdTotal.TaxReportField == _tmpTaxReportById.TaxReportField;

            if (_tmpTaxReportByIdTotal)
            {
                _tmpTaxReportByIdTotal.Amount += _tmpTaxReportById.Amount;
                _tmpTaxReportByIdTotal.TaxInCostPrice += _tmpTaxReportById.TaxInCostPrice;
                _tmpTaxReportByIdTotal.update();
            }
            else
            {
                _tmpTaxReportByIdTotal.TaxReportField = _tmpTaxReportById.TaxReportField;
                _tmpTaxReportByIdTotal.Amount = _tmpTaxReportById.Amount;
                _tmpTaxReportByIdTotal.TaxInCostPrice = _tmpTaxReportById.TaxInCostPrice;
                _tmpTaxReportByIdTotal.insert();
            }
        }

        ttscommit;

        return _tmpTaxReportByIdTotal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TaxCalcVATDeclarationFieldsHelper_W construct(Query _query)
    {
        TaxCalcVATDeclarationFieldsHelper_W taxCalcFields = new TaxCalcVATDeclarationFieldsHelper_W();

        taxCalcFields.parmQuery(_query);
        return taxCalcFields;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>