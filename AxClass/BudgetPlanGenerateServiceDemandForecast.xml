<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanGenerateServiceDemandForecast</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetPlanGenerateServiceDemandForecast</c> is used to generate budget plan data from demand forecast.
/// </summary>
public class BudgetPlanGenerateServiceDemandForecast extends BudgetPlanGenerateService
{
    BudgetPlanGenerateContractDemandForecast budgetPlanGenerateContractDemandForecast;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>BudgetPlanGenerateServiceDemandForecast</c> class.
    /// </summary>
    /// <returns>
    /// An instance of the <c>BudgetPlanGenerateServiceDemandForecast</c> class.
    /// </returns>
    public static BudgetPlanGenerateServiceDemandForecast construct()
    {
        return new BudgetPlanGenerateServiceDemandForecast();
    }

]]></Source>
			</Method>
			<Method>
				<Name>startProcess</Name>
				<Source><![CDATA[
    /// <summary>
    /// Starts the process of data generation.
    /// </summary>
    /// <param name="_contract">
    /// An instance of the <c>BudgetPlanGenerateContractDemandForecast</c> class.
    /// </param>
    public void startProcess(BudgetPlanGenerateContractDemandForecast _contract)
    {
        BudgetInstrumentationLogger budgetPlanLogger = BudgetInstrumentationLogger::createLogger(classStr(BudgetPlanGenerateServiceDemandForecast));
        using (SysInstrumentationActivityContext budgetPlanGenerateServiceDemandForecastContext = budgetPlanLogger.budgetPlanGeneration().demandForecastContext(_contract))
        {
            this.parmBudgetPlanGenerateContract(_contract);
            this.initialize();
            this.generateData();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateBudgetPlanLineForSales</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates the record in the <c>BudgetPlanLine</c> table for the sales amount.
    /// </summary>
    /// <param name = "_budgetPlanHeaderRecId">The record ID of the <c>BudgetPlanHeader</c> record.</param>
    /// <param name = "_forecastSales">The buffer of the <c>ForecastSales</c> table.</param>
    /// <param name = "_includeSalesOrder">The boolean value for the inclusion of the sales data.</param>
    protected void createOrUpdateBudgetPlanLineForSales(BudgetPlanHeaderRecId _budgetPlanHeaderRecId, ForecastSales _forecastSales, boolean _includeSalesOrder)
    {
        if (_includeSalesOrder)
        {
            LedgerDimensionDefaultAccount salesLedgerDimension = _forecastSales.accountSalesLedgerDimension();

            if (salesLedgerDimension)
            {
                BudgetPlanCurrencyAmount grossAmount = _forecastSales.grossAmount();

                this.createOrUpdateBudgetPlanLineForDiscount(_budgetPlanHeaderRecId, _forecastSales, grossAmount-_forecastSales.Amount);

                BudgetPlanCurrencyAmount budgetPlanLineAmount = -grossAmount;

                // Creates the budget plan line for the sales amount
                if (budgetPlanLineAmount)
                {
                    this.createOrUpdateBudgetPlanLine(
                        this.setBudgetPlanLineDataGenerationTmpForDemandForecast(
                            _budgetPlanHeaderRecId,
                            _forecastSales,
                            salesLedgerDimension,
                            budgetPlanLineAmount,
                            _forecastSales.Currency,
                            BudgetClass::Revenue));
                }
            }
            else
            {
                this.validateDefaultAccount(salesLedgerDimension, InventPosting::accountType2LedgerPostingType(InventAccountType::SalesRevenue));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmBudgetPlanGenerateContract</Name>
				<Source><![CDATA[
    public BudgetPlanGenerateContract parmBudgetPlanGenerateContract(BudgetPlanGenerateContract _budgetPlanGenerateContract = budgetPlanGenerateContract)
    {
        super(_budgetPlanGenerateContract);

        budgetPlanGenerateContractDemandForecast = _budgetPlanGenerateContract as BudgetPlanGenerateContractDemandForecast;

        return budgetPlanGenerateContract;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates the data in the budget plan tables.
    /// </summary>
    protected void generateData()
    {
        boolean isValidQuery;
        ForecastSales forecastSales;

        Query userDefinedQuery = this.createQuery();

        try
        {
            ttsbegin;

            BudgetPlanHeader budgetPlanHeader    = this.createOrUpdateBudgetPlanHeader();
            QueryRun         userDefinedQueryRun = new QueryRun(userDefinedQuery);

            while (userDefinedQueryRun.next())
            {
                isValidQuery = true;

                forecastSales = userDefinedQueryRun.get(tableNum(ForecastSales)) as ForecastSales;

                this.createOrUpdateBudgetPlanLineForSales(
                    budgetPlanHeader.RecId,
                    forecastSales,
                    budgetPlanGenerateContractDemandForecast.parmSourceDemandForecastSalesOrder());

                this.createOrUpdateBudgetPlanLineForConsumption(
                    budgetPlanHeader.RecId,
                    forecastSales,
                    budgetPlanGenerateContractDemandForecast.parmSourceDemandForecastConsumption());
            }

            if (isValidQuery)
            {
                this.bulkInsertOrUpdateBudgetPlanLines();
            }

            if (!isValidQuery || !isBudgetPlanLineCreated)
            {
                throw error(this.recordNotFoundErrorMessage());
            }

            if (budgetPlanHeader)
            {
                this.showTaskCompleteMessage();
            }

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= BudgetPlanningConstants::MaxRetryCount)
            {
                throw Exception::Deadlock;
            }
            else
            {
                retry;
            }
        }
        catch (Exception::DuplicateKeyException)
        {
            error("@Budget:BudgetPlanning_Validation_ErrorOccurredDuringUpdate");
        }
        catch (Exception::Error)
        {
            exceptionTextFallThrough();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateBudgetPlanLineForConsumption</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates the record in the <c>BudgetPlanLine</c> table for the consumption amount.
    /// </summary>
    /// <param name = "_budgetPlanHeaderRecId">The record ID of the <c>BudgetPlanHeader</c> record.</param>
    /// <param name = "_forecastSales">The buffer of the <c>ForecastSales</c> table.</param>
    /// <param name = "_includeConsumption">The boolean value for the inclusion of the consumption data.</param>
    protected void createOrUpdateBudgetPlanLineForConsumption(BudgetPlanHeaderRecId _budgetPlanHeaderRecId, ForecastSales _forecastSales, boolean _includeConsumption)
    {
        InventTable inventTable = _forecastSales.inventTable();

        if (_includeConsumption
            && (!inventTable || inventTable.modelGroup().PostOnhandFinancial))
        {
            LedgerDimensionDefaultAccount consumptionLedgerDimension = _forecastSales.accountConsumptionLedgerDimension();

            if (consumptionLedgerDimension && _forecastSales.CostPrice)
            {
                this.createOrUpdateBudgetPlanLine(
                    this.setBudgetPlanLineDataGenerationTmpForDemandForecast(
                        _budgetPlanHeaderRecId,
                        _forecastSales,
                        consumptionLedgerDimension,
                        _forecastSales.InventQty * _forecastSales.CostPrice,
                        Ledger::accountingCurrency(),
                        BudgetClass::Expense));
            }
            else
            {
                this.validateDefaultAccount(consumptionLedgerDimension, InventPosting::accountType2LedgerPostingType(InventAccountType::SalesConsump));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateBudgetPlanLineForDiscount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates the record in the <c>BudgetPlanLine</c> table for the discount amount.
    /// </summary>
    /// <param name = "_budgetPlanHeaderRecId">The record ID of the <c>BudgetPlanHeader</c> record.</param>
    /// <param name = "_forecastSales">The buffer of the <c>ForecastSales</c> table.</param>
    /// <param name = "_discountAmount">The value for the discount amount.</param>
    protected void createOrUpdateBudgetPlanLineForDiscount(BudgetPlanHeaderRecId _budgetPlanHeaderRecId, ForecastSales _forecastSales, BudgetPlanCurrencyAmount _discountAmount)
    {
        LedgerDimensionDefaultAccount discountLedgerDimension = _forecastSales.accountDiscLedgerDimension();

        if (_discountAmount)
        {
            if (discountLedgerDimension)
            {
                // Creates the budget plan line for the discount amount
                this.createOrUpdateBudgetPlanLine(
                    this.setBudgetPlanLineDataGenerationTmpForDemandForecast(
                        _budgetPlanHeaderRecId,
                        _forecastSales,
                        discountLedgerDimension,
                        _discountAmount,
                        _forecastSales.Currency,
                        BudgetClass::Revenue));
            }
            else
            {
                this.validateDefaultAccount(discountLedgerDimension, InventPosting::accountType2LedgerPostingType(InventAccountType::SalesDisc));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setBudgetPlanLineDataGenerationTmpForDemandForecast</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the values on a new <c>BudgetPlanLineDataGenerationTmp</c> record.
    /// </summary>
    /// <param name="_budgetPlanHeaderRecId">
    /// The record ID of the <c>BudgetPlanHeader</c> record.
    /// </param>
    /// <param name="_forecastSales">
    /// The buffer of the <c>ForecastSales</c> table.
    /// </param>
    /// <param name="_defaultAccount">
    /// The ledger dimension default account reference to be processed for inclusion in the budget ledger dimension.
    /// </param>
    /// <param name="_amount">
    /// The amount to be set on the <c>BudgetPlanLine</c> table record.
    /// </param>
    /// <param name="_currency">
    /// The currency to be set on the <c>BudgetPlanLine</c> table record.
    /// </param>
    /// <param name="_budgetClass">
    /// The budget class to be set on the <c>BudgetPlanLine</c> table record.
    /// </param>
    /// <exception cref="M:Exception::Error">
    /// The creation process encountered an error and was not completed.
    /// </exception>
    /// <returns>
    /// The <c>BudgetPlanLineDataGenerationTmp</c> buffer with demand forecast values.
    /// </returns>
    protected BudgetPlanLineDataGenerationTmp setBudgetPlanLineDataGenerationTmpForDemandForecast(
        BudgetPlanHeaderRecId         _budgetPlanHeaderRecId,
        ForecastSales                 _forecastSales,
        LedgerDimensionDefaultAccount _defaultAccount,
        BudgetPlanCurrencyAmount      _amount,
        CurrencyCode                  _currency,
        BudgetClass                   _budgetClass)
    {
        BudgetPlanLineDataGenerationTmp budgetPlanLineDataGenerationTmp;

        budgetPlanLineDataGenerationTmp = this.setBudgetPlanLineDataGenerationTmp(_budgetPlanHeaderRecId);

        budgetPlanLineDataGenerationTmp.EffectiveDate   = _forecastSales.StartDate;
        budgetPlanLineDataGenerationTmp.LedgerDimension = LedgerDimensionFacade::serviceCreateLedgerDimensionForType(
                                                                                        LedgerDimensionType::BudgetPlanning,
                                                                                        _defaultAccount,
                                                                                        _forecastSales.DefaultDimension);
        budgetPlanLineDataGenerationTmp.ForecastModelId = _forecastSales.ModelId;
        budgetPlanLineDataGenerationTmp.BudgetClass     = _budgetClass;

        budgetPlanLineDataGenerationTmp.TransactionCurrencyCode = _currency;
        budgetPlanLineDataGenerationTmp.TransactionCurrencyAmount = CurrencyExchangeHelper::amount(_amount, budgetPlanLineDataGenerationTmp.TransactionCurrencyCode);
        budgetPlanLineDataGenerationTmp.EstimateType = BudgetPlanEstimateType::Monetary;

        return budgetPlanLineDataGenerationTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUserDefinedQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the query object according to the settings which the user defined on the dialog.
    /// </summary>
    /// <returns>
    /// An instance of the <c>Query</c> class.
    /// </returns>
    public Query getUserDefinedQuery()
    {
        return this.createQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the query object according to the settings which the user defined on the dialog.
    /// </summary>
    /// <returns>
    /// An instance of the <c>Query</c> class.
    /// </returns>
    protected Query createQuery()
    {
        Query query;

        query = new Query(SysOperationHelper::base64Decode(budgetPlanGenerateContractDemandForecast.parmQueryBudgetPlanGenerateDemadForecast()));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteAllMatchingBudgetPlanLine</Name>
				<Source><![CDATA[
    protected void deleteAllMatchingBudgetPlanLine(BudgetPlanLineDataGenerationTmp _budgetPlanLineDataGenerationTmp)
    {
        BudgetPlanLine budgetPlanLine;

        delete_from budgetPlanLine
            where budgetPlanLine.BudgetPlanHeader       == _budgetPlanLineDataGenerationTmp.BudgetPlanHeader
                && budgetPlanLine.BudgetPlanScenario    == _budgetPlanLineDataGenerationTmp.BudgetPlanScenario
                && budgetPlanLine.LedgerDimension       == _budgetPlanLineDataGenerationTmp.LedgerDimension
                && budgetPlanLine.EffectiveDate         == _budgetPlanLineDataGenerationTmp.EffectiveDate
                && budgetPlanLine.BudgetClass           == _budgetPlanLineDataGenerationTmp.BudgetClass
                && budgetPlanLine.ForecastModelId       == _budgetPlanLineDataGenerationTmp.ForecastModelId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findRecordInTmpTable</Name>
				<Source><![CDATA[
    protected void findRecordInTmpTable(BudgetPlanLineDataGenerationTmp _budgetPlanLineDataGenerationTmp)
    {
        select firstonly forupdate budgetPlanLineDataGenerationTmpBulkInsert
            where budgetPlanLineDataGenerationTmpBulkInsert.BudgetPlanHeader    == _budgetPlanLineDataGenerationTmp.BudgetPlanHeader
                && budgetPlanLineDataGenerationTmpBulkInsert.BudgetPlanScenario == _budgetPlanLineDataGenerationTmp.BudgetPlanScenario
                && budgetPlanLineDataGenerationTmpBulkInsert.LedgerDimension    == _budgetPlanLineDataGenerationTmp.LedgerDimension
                && budgetPlanLineDataGenerationTmpBulkInsert.EffectiveDate      == _budgetPlanLineDataGenerationTmp.EffectiveDate
                && budgetPlanLineDataGenerationTmpBulkInsert.BudgetClass        == _budgetPlanLineDataGenerationTmp.BudgetClass
                && budgetPlanLineDataGenerationTmpBulkInsert.ForecastModelId    == _budgetPlanLineDataGenerationTmp.ForecastModelId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>