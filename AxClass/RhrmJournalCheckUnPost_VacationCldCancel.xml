<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RhrmJournalCheckUnPost_VacationCldCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RhrmJournalCheckUnPost_VacationCldCancel extends RHRMJournalCheckPost implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkTrans</Name>
				<Source><![CDATA[
        protected boolean checkTrans(JournalTransData _journalTransData)
    {
        boolean            ret;
        RHRMOrderTrans     rhrmOrderTrans = _journalTransData.journalTrans();
        RPayAverageEarnEmplPeriodTable  rPayAverageEarnEmplPeriodTable;
        TransLink_RU                    transLink;

        ret = super(_journalTransData);

        if ((select firstonly rpayTrans
                 where rpayTrans.Emplid    == rhrmOrderTrans.EmplId &&
                       rpayTrans.PayPeriod > endmth(rhrmOrderTrans.StartDate)).RecId)
        {
            ret = ret && checkFailed(strFmt("@RUP1834",
                                            rhrmOrderTrans.EmplId,
                                            RHRMEmplTable::find(rhrmOrderTrans.EmplId).name()));
        }

        select firstonly forupdate rPayAverageEarnEmplPeriodTable
            where rPayAverageEarnEmplPeriodTable.StartDate          == rhrmOrderTrans.PrevVacationStartDate     &&
                  rPayAverageEarnEmplPeriodTable.EmplId             == rhrmOrderTrans.EmplId                    &&
                  rPayAverageEarnEmplPeriodTable.Included           == NoYes::Yes                               &&
                  rPayAverageEarnEmplPeriodTable.RpayIllType        == RPayIllType::ChildVacation;
        if (rPayAverageEarnEmplPeriodTable.RecId != 0)
        {
            if (rPayAverageEarnEmplPeriodTable.EndDate != rhrmOrderTrans.StartDate - 1)
                ret = ret && checkFailed(strFmt("@RUP3195", rhrmOrderTrans.PrevVacationStartDate));
        }
        select firstonly RecId from rPayAverageEarnEmplPeriodTable
            where rPayAverageEarnEmplPeriodTable.EmplId == rhrmOrderTrans.EmplId
            &&    rPayAverageEarnEmplPeriodTable.StartDate <= rhrmOrderTrans.PrevVacationEndDate
            &&    rPayAverageEarnEmplPeriodTable.EndDate   >= rhrmOrderTrans.StartDate
            notexists join transLink
                        where transLink.RefTableId == rPayAverageEarnEmplPeriodTable.TableId &&
                              transLink.RefSec     == rPayAverageEarnEmplPeriodTable.RecId   &&
                              transLink.Reversed;
        if (rPayAverageEarnEmplPeriodTable.RecId != 0)
        {
            ret = checkFailed(strFmt("@RUP3194", rhrmOrderTrans.EmplId, rhrmOrderTrans.emplName()));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postTrans</Name>
				<Source><![CDATA[
        protected void postTrans(JournalTransData _journalTransData)
    {
        RHRMOrderTrans      rhrmOrderTrans = _journalTransData.journalTrans();
        RHRMOrderTable      rhrmOrderTable = journalTableData.journalTable();

        TransDate                         oldDate;
        RPayAverageEarnEmplPeriodTable    rPayAverageEarnEmplPeriodTable;

        super(_journalTransData);

        select firstonly forupdate rPayAverageEarnEmplPeriodTable
            where rPayAverageEarnEmplPeriodTable.StartDate          == rhrmOrderTrans.PrevVacationStartDate     &&
                  rPayAverageEarnEmplPeriodTable.EmplId             == rhrmOrderTrans.EmplId                    &&
                  rPayAverageEarnEmplPeriodTable.Included           == NoYes::Yes                               &&
                  rPayAverageEarnEmplPeriodTable.RpayIllType        == RPayIllType::ChildVacation;
        if (rPayAverageEarnEmplPeriodTable.RecId != 0)
        {
            oldDate = rPayAverageEarnEmplPeriodTable.EndDate;
            rPayAverageEarnEmplPeriodTable.EndDate = rhrmOrderTrans.PrevVacationEndDate;
            rPayAverageEarnEmplPeriodTable.update();

            RPayTblUpdater::updateTimesheetFromDocument(rPayAverageEarnEmplPeriodTable.StartDate,
                                                        oldDate,
                                                        rhrmOrderTrans.EmplId);
        }
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return false;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>