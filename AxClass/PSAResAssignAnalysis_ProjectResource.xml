<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAResAssignAnalysis_ProjectResource</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This derived class is used for handling project wise assignment analysis.
/// </summary>
class PSAResAssignAnalysis_ProjectResource extends PSAResAssignAnalysisBase
{
    Query                     rootQuery;
    QueryBuildDataSource      details;

    utcdatetime startDateTime;
    utcdatetime endDateTime;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Builds the query for getting project wise resource assignment analysis records.
    /// </summary>
    void buildQuery()
    {
        rootQuery = new Query();

        details = rootQuery.addDataSource(tableNum(ResProjectAnalysisDayView));
        

        if (m_tCommon && (m_tCommon.TableId == tableNum(ProjTable)))
        {
            ProjTable projTable = m_tCommon;

            details.addRange(fieldNum(ResProjectAnalysisDayView, ProjId))
                .value(SysQuery::value(projTable.ProjId));
        }

        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ProjType));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ProjId));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ActivityName));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ActivitySchedulingStatus));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ActivityTeamStartDate));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ActivityTeamEndDate));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, BookedCapacityHardHours));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, AssignedCapacityHardHours));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, ActivityEffort));
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, HardBooked), SelectionField::Sum);
        details.addSelectionField(fieldnum(ResProjectAnalysisDayView, HardAssigned), SelectionField::Sum);

        details.orderMode(OrderMode::GroupBy);
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ProjType));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ProjId));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ActivityName));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ActivitySchedulingStatus));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ActivityTeamStartDate));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ActivityTeamEndDate));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, BookedCapacityHardHours));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, AssignedCapacityHardHours));
        details.addGroupByField(fieldnum(ResProjectAnalysisDayView, ActivityEffort));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes the query based on value provided for project wise resource assignment analysis.
    /// </summary>
    void initQuery()
    {
        startDateTime = DateTimeUtil::minValue();
        endDateTime = DateTimeUtil::maxValue();

        if (m_dStartDate)
        {
            startDateTime = DateTimeUtil::newDateTime(m_dStartDate, 0);
        }

        if (m_dEndDate)
        {
            endDateTime = DateTimeUtil::newDateTime(m_dEndDate, 0);
        }

        details.addRange(fieldnum(ResProjectAnalysisDayView, Timeframe))
            .value(SysQuery::range(startDateTime, endDateTime));
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssignmentAnalysis</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Calculates and populates the temporary table for project wise assignment analysis.
    /// </summary>
    void insertAssignmentAnalysis()
    {
        #TimeConstants

        m_oSummaryQueryRun = new QueryRun(rootQuery);

        ttsbegin;

        while (m_oSummaryQueryRun.next())
        {
            ResProjectAnalysisDayView analysis = m_oSummaryQueryRun.get(tableNum(ResProjectAnalysisDayView));
            boolean hasWBS = ProjTable::activityExists(analysis.ProjId);
            boolean doesRecordExist = false;

            select firstOnly m_tTmpResourceAssignmentAnalysis
            where m_tTmpResourceAssignmentAnalysis.VersionId == analysis.ProjId;

            if (m_tTmpResourceAssignmentAnalysis)
            {
                doesRecordExist = true;
            }

            //
            //  Checks if the record already exist in the temporary table.
            //  If the record doesn't exist then insert it otherwise update it.
            //
            if (!doesRecordExist)
            {
                m_tTmpResourceAssignmentAnalysis.VersionId                  = analysis.ProjId;
                m_tTmpResourceAssignmentAnalysis.Description                = analysis.ActivityName;
                m_tTmpResourceAssignmentAnalysis.ProjectType                = analysis.ProjType;
                m_tTmpResourceAssignmentAnalysis.ProjectSchedulingStatus    = analysis.ActivitySchedulingStatus;
                m_tTmpResourceAssignmentAnalysis.ScheduleStartDate          = analysis.ActivityTeamStartDate;
                m_tTmpResourceAssignmentAnalysis.ScheduleEndDate            = analysis.ActivityTeamEndDate;
                m_tTmpResourceAssignmentAnalysis.ProjectBookedHours         = analysis.BookedCapacityHardHours;
                m_tTmpResourceAssignmentAnalysis.ProjectAssignedHours       = analysis.AssignedCapacityHardHours;
                m_tTmpResourceAssignmentAnalysis.HardBookedHours            = analysis.HardBooked / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.HardAssignedHours          = analysis.HardAssigned / #SecondsPerHour;
                m_tTmpResourceAssignmentAnalysis.ProjectUnassignedHardHours = hasWBS ? ((analysis.HardBooked - analysis.HardAssigned) / #SecondsPerHour) : 0;
                m_tTmpResourceAssignmentAnalysis.WBSUnassignedHours         = hasWBS ? ((analysis.ActivityEffort - (analysis.HardAssigned / #SecondsPerHour))) : 0;
                m_tTmpResourceAssignmentAnalysis.insert();
            }
            else
            {
                m_tTmpResourceAssignmentAnalysis.selectForUpdate(true);
                m_tTmpResourceAssignmentAnalysis.ScheduleStartDate = m_tTmpResourceAssignmentAnalysis.ScheduleStartDate > analysis.ActivityTeamStartDate ? 	analysis.ActivityTeamStartDate : m_tTmpResourceAssignmentAnalysis.ScheduleStartDate;
                m_tTmpResourceAssignmentAnalysis.ScheduleEndDate = m_tTmpResourceAssignmentAnalysis.ScheduleEndDate > analysis.ActivityTeamEndDate ? 	m_tTmpResourceAssignmentAnalysis.ScheduleEndDate : analysis.ActivityTeamEndDate;
                m_tTmpResourceAssignmentAnalysis.update();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>