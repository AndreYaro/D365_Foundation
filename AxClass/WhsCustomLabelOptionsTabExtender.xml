<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsCustomLabelOptionsTabExtender</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WhsCustomLabelOptionsTabExtender
{
    internal const FormControlName CustomLabelsOptionsButtonGroupControlName = 'WhsCustomLabelsOptionsButtonGroupControl';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>SysSystemDefinedButtons_onConstructFormOptionsTab</Name>
				<Source><![CDATA[
    [SubscribesTo(classStr(SysSystemDefinedButtons), delegateStr(SysSystemDefinedButtons, onConstructFormOptionsTab))]
    public static void SysSystemDefinedButtons_onConstructFormOptionsTab(FormRun _formRun, FormActionPaneTabControl _optionsTab)
    {
        WhsCustomLabelOptionsTabExtender extender = new WhsCustomLabelOptionsTabExtender();
        extender.addCustomLabelPrintButtons(_formRun, _optionsTab);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addCustomLabelPrintButtons</Name>
				<Source><![CDATA[
    private void addCustomLabelPrintButtons(FormRun _formRun, FormActionPaneTabControl _optionsTab)
    {
        if (_formRun && this.shouldDisplayCustomLabelPrintButtonsOnForm(_formRun) && _optionsTab)
        {
            // If button group exists, don't continue.
            FormButtonGroupControl buttonGroupControl = _formRun.design().controlName(CustomLabelsOptionsButtonGroupControlName) as FormButtonGroupControl;
            if (buttonGroupControl)
            {
                return;
            }

            buttonGroupControl = _optionsTab.addControl(FormControlType::ButtonGroup, CustomLabelsOptionsButtonGroupControlName);
            buttonGroupControl.allowUserSetup(FormAllowUserSetup::No);
            buttonGroupControl.caption("@WAX:CustomLabels");
            buttonGroupControl.hideIfEmpty(true);

            this.addCustomLabelPrintButtonsForRootDataSources(_formRun, buttonGroupControl);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addCustomLabelPrintButtonsForRootDataSources</Name>
				<Source><![CDATA[
    private void addCustomLabelPrintButtonsForRootDataSources(FormRun _formRun, FormButtonGroupControl _parentControl)
    {
        List rootFormDataSources = _formRun.rootFormDataSources();
        ListEnumerator rootFormDataSourcesEnumerator = rootFormDataSources.getEnumerator();
        Set rootTableIds = new Set(Types::Int64);

        while (rootFormDataSourcesEnumerator.moveNext())
        {
            FormDataSource rootFormDataSource = rootFormDataSourcesEnumerator.current();
            DictTable dictTable = new DictTable(rootFormDataSource.table());

            if (dictTable && !rootTableIds.in(dictTable.id()))
            {
                rootTableIds.add(dictTable.id());

                List labelDataSourcesForRootTable = WhsCustomLabelsMetadataCache::getCustomLabelLayoutDataSourcesForTableName(dictTable.name());
                ListEnumerator labelDataSourcesForRootTableEnumerator = labelDataSourcesForRootTable.getEnumerator();

                boolean isSingleLabelDataSourceForTable = (labelDataSourcesForRootTable.elements() == 1);

                while (labelDataSourcesForRootTableEnumerator.moveNext())
                {
                    WHSLabelLayoutDataSourceId labelLayoutDataSourceId = labelDataSourcesForRootTableEnumerator.current();

                    str printLabelButtonId = strFmt('WHSCustomLabelPrintButtonControl_%1_%2', rootFormDataSource.id(), labelLayoutDataSourceId);
                    str printLabelButtonText;
                    if (isSingleLabelDataSourceForTable)
                    {
                        printLabelButtonText = strFmt('%1 %2', "@SYS920", dictTable.label());
                    }
                    else
                    {
                        WHSLabelLayoutDataSource labelLayoutDataSource = WHSLabelLayoutDataSource::find(labelLayoutDataSourceId);
                        printLabelButtonText = strfmt('%1 %2 (%3)', "@SYS920", dictTable.label(), (labelLayoutDataSource.Description ? labelLayoutDataSource.Description : labelLayoutDataSource.LabelLayoutDataSourceId));
                    }

                    FormFunctionButtonControl printLabelForDSAndLayoutButton = _parentControl.addControl(FormControlType::MenuFunctionButton, printLabelButtonId);
                    printLabelForDSAndLayoutButton.needsRecord(true);
                    printLabelForDSAndLayoutButton.multiSelect(true);
                    printLabelForDSAndLayoutButton.dataSource(rootFormDataSource.id());
                    printLabelForDSAndLayoutButton.parameters(labelLayoutDataSourceId);                    
                    printLabelForDSAndLayoutButton.menuItemType(MenuItemType::Action);
                    printLabelForDSAndLayoutButton.menuItemName(menuItemActionStr(WhsCustomLabelPrint));                    
                    printLabelForDSAndLayoutButton.text(printLabelButtonText);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldDisplayCustomLabelPrintButtonsOnForm</Name>
				<Source><![CDATA[
    private boolean shouldDisplayCustomLabelPrintButtonsOnForm(FormRun _formRun)
    {
        switch (WHSParameters::find().CustomLabelsPrintButtonsDisplay)
        {
            case WhsCustomLabelsPrintButtonsDisplay::Never:
                return false;

            case WhsCustomLabelsPrintButtonsDisplay::AllForms:
                return this.existsCustomLabelLayoutForFormRootDataSource(_formRun);
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>existsCustomLabelLayoutForFormRootDataSource</Name>
				<Source><![CDATA[
    private boolean existsCustomLabelLayoutForFormRootDataSource(FormRun _formRun)
    {
        List rootFormDataSources = _formRun.rootFormDataSources();
        ListEnumerator rootFormDataSourcesEnumerator = rootFormDataSources.getEnumerator();
        Set rootTableIds = new Set(Types::Int64);

        while (rootFormDataSourcesEnumerator.moveNext())
        {
            FormDataSource rootFormDataSource = rootFormDataSourcesEnumerator.current();
            DictTable dictTable = new DictTable(rootFormDataSource.table());

            if (dictTable && !rootTableIds.in(dictTable.id()))
            {
                rootTableIds.add(dictTable.id());

                List labelDataSourcesForRootTable = WhsCustomLabelsMetadataCache::getCustomLabelLayoutDataSourcesForTableName(dictTable.name());

                if (labelDataSourcesForRootTable && labelDataSourcesForRootTable.elements() > 0)
                {
                    return true;
                }
            }
        }

        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>