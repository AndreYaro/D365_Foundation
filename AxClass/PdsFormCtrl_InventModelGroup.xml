<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsFormCtrl_InventModelGroup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class serves as a controller for the standard InventModelGroup
/// form to handle Process Distribution functionalities.
/// </summary>
public class PdsFormCtrl_InventModelGroup extends PmfFormCtrl
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fieldModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles logic for InventModelGroup field modification methods.
    /// </summary>
    /// <param name="_fieldId">
    /// Field modified on the InventModelGroup form.
    /// </param>
    public void fieldModified(FieldId _fieldId)
    {
        InventModelGroup inventModelGroup;

        super(_fieldId);

        switch (_fieldId)
        {
            case fieldNum(InventModelGroup,ReserveByDate) :
            case fieldNum(InventModelGroup,PdsSameLot) :
                this.updateDesign();
                break;
            case fieldNum(InventModelGroup,PdsCheck) :
                inventModelGroup = this.inventModelGroup();
                inventModelGroup.PdsPickCriteria = PdsPickCriteria::ExpiryDate;
                this.updateDesign();
                this.parmFormDataSource().refresh();
                break;
            case fieldNum(InventModelGroup,StockedProduct):
                inventModelGroup = this.inventModelGroup();
                if (!inventModelGroup.StockedProduct)
                {
                    inventModelGroup.PdsSameLot = false;
                    inventModelGroup.PdsConsReq = false;
                    inventModelGroup.PdsCheck   = false;
                    inventModelGroup.PdsPickCriteria = PdsPickCriteria::ExpiryDate;
                    inventModelGroup.PdsDispositionCode = '';
                    inventModelGroup.PdsReqVendBatchDetail = false;
                }
                this.updateDesign();
                break;

            default:
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventModelGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the form buffer.
    /// </summary>
    /// <returns>
    /// the form buffer as an InventModelGroup record
    /// </returns>
    public InventModelGroup inventModelGroup()
    {
        return buffer;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Protected 'new'.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    /// <summary>
    /// Refreshes the form design.
    /// </summary>
    public void updateDesign()
    {
        InventStockedProduct stockedProduct = this.inventModelGroup().StockedProduct;

        super();

        this.enableDSField(fieldNum(InventModelGroup, PdsSameLot),           stockedProduct);
        this.enableDSField(fieldNum(InventModelGroup, PdsDispositionCode),   stockedProduct);
        this.enableDSField(fieldNum(InventModelGroup, PdsReqVendBatchDetail),stockedProduct);

        if (#PdsSameLotEnabled)
        {
            this.enableDSField(fieldNum(InventModelGroup, PdsConsReq), stockedProduct && this.inventModelGroup().PdsSameLot);
        }

        if (#PdsShelfEnabled)
        {
            this.enableDSField(fieldNum(InventModelGroup, PdsPickCriteria), stockedProduct && this.inventModelGroup().PdsCheck);
            this.enableDSField(fieldNum(InventModelGroup, PdsCheck),stockedProduct && !this.inventModelGroup().ReserveByDate);
            this.enableDSField(fieldNum(InventModelGroup, ReserveByDate), stockedProduct && !this.inventModelGroup().PdsCheck);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// A simple construct.
    /// </summary>
    /// <returns>
    /// The new form control
    /// </returns>
    public static PdsFormCtrl_InventModelGroup construct()
    {
        return new PdsFormCtrl_InventModelGroup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a new instance of PdsFormCtrl_InventModelGroup.
    /// </summary>
    /// <param name="_fr">
    /// Form caller.
    /// </param>
    /// <returns>
    /// The new instance of the form control
    /// </returns>
    public static PdsFormCtrl_InventModelGroup newFromForm(FormRun _fr)
    {
        PmfFormCtrl frmCtrl;

        frmCtrl = PdsFormCtrl_InventModelGroup::construct();
        frmCtrl.parmFormRun(_fr);
        return frmCtrl;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>