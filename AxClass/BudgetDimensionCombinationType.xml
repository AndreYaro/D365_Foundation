<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetDimensionCombinationType</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>BudgetDimensionCombinationType</c> class determines the state applied to the budget dimension combination type.
/// </summary>
[ExportMetadataAttribute(enumstr(LedgerDimensionType), LedgerDimensionType::Budget),
 ExportAttribute(identifierstr(Dynamics.AX.Application.DimensionCombinationType))]
public class BudgetDimensionCombinationType extends DimensionCombinationType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isSingleHierarchy</Name>
				<Source><![CDATA[
    public boolean isSingleHierarchy()
    {
        // Budget combinations support rule hierarchy types.
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAllowedDimensionRuleTypeCollection</Name>
				<Source><![CDATA[
    public DimensionRuleTypeCollection getAllowedDimensionRuleTypeCollection()
    {
        DimensionRuleTypeCollection ruleTypeCollection = super();

        ruleTypeCollection.add(DimensionRuleType::GeneralLedger);
        ruleTypeCollection.add(DimensionRuleType::BudgetPlanning);

        return ruleTypeCollection;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAttributeListRestricted</Name>
				<Source><![CDATA[
    public boolean isAttributeListRestricted()
    {
        // Budget combinations restrict the list of dimension attributes to those that are enabled for budget.
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAttributeAllowed</Name>
				<Source><![CDATA[
    public boolean isAttributeAllowed(DimensionAttributeRecId _dimensionAttributeRecId)
    {
        boolean isAttributeAllowed = super(_dimensionAttributeRecId);

        if (isAttributeAllowed)
        {
            // Get the list of enabled budget dimension for the current primary ledger.
            List budgetDimensionsList = BudgetPrimaryLedgerDimensionAttribute::budgetDimensionsList(Ledger::current());

            isAttributeAllowed = BudgetPrimaryLedgerDimensionAttribute::isDimensionAttributeInList(_dimensionAttributeRecId, budgetDimensionsList);
        }

        return isAttributeAllowed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getComboDisplayValue</Name>
				<Source><![CDATA[
    public DimensionDisplayValue getComboDisplayValue(DimensionStorage _dimensionStorage, Map _segments, int _totalSegmentCount, str _accountDelimiter)
    {
        DimensionDisplayValue displayValue;
        boolean isFirstValueDisplayed = true;

        int hierarchyCount = _dimensionStorage.hierarchyCount();

        if (Debug::debugMode())
        {
            Debug::assert(hierarchyCount > 0);
            Debug::assert(_totalSegmentCount > 0);
        }

        if (_accountDelimiter == '')
        {
            _accountDelimiter = DimensionParameters::getDimensionSegmentDelimiter();
            Debug::assert(_accountDelimiter != '');
        }

        int i = 1;
        int hierarchyIndex;
        DimensionHierarchyId hierarchyId;
        DimensionHierarchyLevel dimHierarchyLevel;

        Set dimAttributes = new Set(Types::Int64);

        for (hierarchyIndex = 1; hierarchyIndex <= hierarchyCount; hierarchyIndex++)
        {
            hierarchyId = _dimensionStorage.getHierarchyId(hierarchyIndex);

            while select DimensionAttribute from dimHierarchyLevel
                order dimHierarchyLevel.Level
                where dimHierarchyLevel.DimensionHierarchy == hierarchyId
            {
                Debug::assert(dimHierarchyLevel.DimensionAttribute != 0);

                // Don't add segments that have already been added with the same dimension attribute
                if (!dimAttributes.in(dimHierarchyLevel.DimensionAttribute)
                    && this.isAttributeAllowed(dimHierarchyLevel.DimensionAttribute))
                {
                    dimAttributes.add(dimHierarchyLevel.DimensionAttribute);
                    displayValue += DimensionStorage::addSegmentToDisplayValue(displayValue, _segments, i, _accountDelimiter, isFirstValueDisplayed);
                    isFirstValueDisplayed = false;
                }

                i++;
            }
        }

        return displayValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getComboDisplayValueInitialSegmentIndex</Name>
				<Source><![CDATA[
    public int getComboDisplayValueInitialSegmentIndex(DimensionHierarchyId _dimensionHierarchyId)
    {
        int initialSegmentIndex;

        DimensionHierarchyLevelCollection levelCollection = DimensionHierarchyLevelCollection::newForHierarchy(_dimensionHierarchyId);
        levelCollection.initializeEnumerator();

        while (levelCollection.moveNext())
        {
            if (this.isAttributeAllowed(levelCollection.currentDimensionAttributeId()))
            {
                initialSegmentIndex = levelCollection.currentLevel();
                break;
            }
        }

        return initialSegmentIndex;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>