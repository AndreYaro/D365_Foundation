<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAccountStatementIntUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class CustAccountStatementIntUIBuilder extends SrsReportDataContractUIBuilder
{
    CustAccountStatementIntContract contract;

    DialogField billingClassificationField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>billingClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a Billing Classification lookup.
    /// </summary>
    public void billingClassificationLookup()
    {
        Query query = new Query(queryStr(CustBillingClassificationLookup));
        TableId multiSelectTableNum = tableNum(CustBillingClassification);
        container selectedFields = [multiSelectTableNum, fieldName2id(multiSelectTableNum, fieldStr(CustBillingClassification, BillingClassification))];

        SysLookupMultiSelectCtrl::constructWithQuery(this.dialog().dialogForm().formRun(), billingClassificationField.control(), query, false, selectedFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
        public void postBuild()
    {
        DialogField dialogField;

        boolean isBillingClassEnabled;
        DialogField inclTransWithNoBillingClassField;

        super();

        contract = this.dataContractObject();
        contract.parmBankLCExportEnable(BankLCExportFeatureChecker::checkBankLCExportEnabled());

        // after framework has built all fields, enable/disable the dialogfield for BankLCBankDocumentType
        dialogField = this.bindInfo().getDialogField(contract, methodStr(CustAccountStatementIntContract, parmBankLCBankDocumentType));

        // Get the parameter controls from dialog
        billingClassificationField = this.bindInfo().getDialogField(contract, methodStr(CustAccountStatementIntContract, parmBillingClassification));
        inclTransWithNoBillingClassField = this.bindInfo().getDialogField(contract, methodStr(CustAccountStatementIntContract, parmInclTransWithNoBillingClass));

        if (billingClassificationField && inclTransWithNoBillingClassField)
        {
            isBillingClassEnabled = CustParameters::isBillingClassificationEnabled();

            billingClassificationField.visible(isBillingClassEnabled);
            inclTransWithNoBillingClassField.visible(isBillingClassEnabled);
            billingClassificationField.lookupButton(FormLookupButton::Always);
        }

        if (dialogField && !contract.parmBankLCExportEnable())
        {
            dialogField.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implements the functionality that is supposed to execute after run.
    /// </summary>
    public void postRun()
    {
        if (CustParameters::isBillingClassificationEnabled())
        {
            this.billingClassificationLookup();
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>