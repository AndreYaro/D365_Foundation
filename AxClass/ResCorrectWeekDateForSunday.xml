<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResCorrectWeekDateForSunday</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This runnable class is used to validate and correct project reservation tables <c>ResBooking</c>, 
/// <c>ResAssignment</c> and <c>ResCalendarCapacity</c> records where Day is Sunday but Week value 
/// does not equate to Day value.
/// </summary>
class ResCorrectWeekDateForSunday
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs class to detect and correct tables data issue.
    /// </summary>
    /// <param name = "_args">No arguments will be needed.</param>
    public static void main(Args _args)
    {        
        ResCorrectWeekDateForSunday weekDateCorrection = new ResCorrectWeekDateForSunday();

        ResBooking booking;
        int bookingId = booking.TableId;
        str bookingIdTableName = tableId2PName(bookingId);

        ResAssignment assignment;
        int assignmentTableId = assignment.TableId;
        str assignmentTableName = tableId2PName(assignmentTableId);

        ResCalendarCapacity calendarCapacity;
        int calendarCapacityTableId = calendarCapacity.TableId;
        str calendarCapacityTableName = tableId2PName(calendarCapacityTableId);

        if (Box::yesNo((
                    strFmt("@Res:ValidateFixWeekFieldForSunday",
                        bookingIdTableName,
                        assignmentTableName,
                        calendarCapacityTableName)
                        ), DialogButton::No) == DialogButton::Yes)
        {
            str correctStatement = weekDateCorrection.getCorrectWeekDateForSundaySQLStatement(tableStr(ResBooking))
                + weekDateCorrection.getCorrectWeekDateForSundaySQLStatement(tableStr(ResAssignment))
                + weekDateCorrection.getCorrectWeekDateForSundaySQLStatement(tableStr(ResCalendarCapacity));
            weekDateCorrection.executeSQLStatement(correctStatement);

            info("@Res:WeekFieldForSundayFixSucceed");
        }   
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCorrectWeekDateForSundaySQLStatement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get correct week date for sunday SQL statement for the specific table.
    /// </summary>
    /// <param name = "_tableName">Specific table name to be corrected.</param>
    /// <returns>Correct week date for Sunday SQL statement.</returns>
    private str getCorrectWeekDateForSundaySQLStatement(str _tableName)
    {
        return 'UPDATE T1 SET Week = T1.Day FROM ' + _tableName + 
            ' T1 WHERE datename(dw, T1.Day) = \'Sunday\' AND T1.Day != T1.Week; ';
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeSQLStatement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Execute SQL statement.
    /// </summary>
    /// <param name = "_statement">SQL statement string.</param>
    private void executeSQLStatement(str _statement)
    {
        ttsbegin;
        Connection userConnection = new Connection();
        Statement stmt = userConnection.createStatement();
        new SqlStatementExecutePermission(_statement).assert();
        stmt.executeUpdate(_statement);
        CodeAccessPermission::revertAssert();
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>