<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSalesLineWFApprResubmitActionMgr</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailSalesLineWFApprResubmitActionMgr</c> menu item action event handler.
/// </summary>
public class RetailSalesLineWFApprResubmitActionMgr 
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
	{
        WorkflowWorkItemActionManager	workflowWorkItemActionManager = new WorkflowWorkItemActionManager();
        WorkflowWorkItemTable			workItemCurrent;
        WorkflowWorkItemTable           workItem;
        WorkflowWorkItemActionDialog	workflowWorkItemActionDialog;
        MCRSalesLine					mcrSalesLine;
        SalesLine                       salesLineTemp;
        SalesLine                       salesLine;
        
        if (_args.record() && _args.caller())
        {   
            workItemCurrent = _args.caller().getActiveWorkflowWorkItem();

            if (workItemCurrent.RecId > 0)
            {
                try
                {
                    workflowWorkItemActionDialog = WorkflowWorkItemActionDialog::construct(workItemCurrent,
																						    WorkflowWorkItemActionType::Resubmit,
                                                                                            new MenuFunction(_args.menuItemName(),_args.menuItemType()));

                    workflowWorkItemActionDialog.run();

                    if (workflowWorkItemActionDialog.parmIsClosedOK())
                    {
                        salesLineTemp = _args.record();
                        if (salesLineTemp)
                        {
							//loop lines to resubmit all rejected lines
                            while select salesLine
                                where salesLine.SalesId == salesLineTemp.SalesId
                            {                      
                                mcrSalesLine = salesLine.mcrSalesLine();
                                if (mcrSalesLine && mcrSalesLine.RetailPriceOverrideWorkflowState == RetailPriceOverrideWorkflowState::Rejected)
                                { 
                                    select firstonly workItem
                                        where workItem.ConfigurationId == workItemCurrent.ConfigurationId
											&& workItem.UserId == workItemCurrent.UserId
                                            && workItem.Status == WorkflowWorkItemStatus::Pending
                                            && workItem.Type == WorkflowWorkItemType::Return
                                            && workItem.RefTableId == workItemCurrent.RefTableId
                                            && workItem.RefRecId == salesLine.RecId;

                                    if (workItem)
                                    {
                                        WorkflowWorkItemActionManager::dispatchWorkItemAction(workItem,
                                                                            workflowWorkItemActionDialog.parmWorkflowComment(),
                                                                            workflowWorkItemActionDialog.parmTargetUser(),
                                                                            WorkflowWorkItemActionType::Resubmit,
                                                                            _args.menuItemName());

                                        MCRSalesLine::updateWorkflowState(salesLine.RecId, RetailPriceOverrideWorkflowState::Submitted);
                                    } 
                                }
                            }
                        }
                        _args.caller().updateWorkflowControls();
                        _args.caller().doRefresh();               
                    }
                }
                catch(Exception::Error)
                {
                    throw error(strfmt('Cannot resubmit retail sales line workflow.'));
                }
            }
        }
	}

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>