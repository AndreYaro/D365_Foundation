<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestFormViewDetails</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class shows detailed form for different sources.
/// </summary>
abstract class CFMPaymentRequestFormViewDetails
{
    CFMPaymentRequestTable paymentRequest;
    FormControl formControl;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates instance of <c>CFMPaymentRequestFormViewDetails</c> based on source parameters.
    /// </summary>
    /// <param name="_paymentRequest">
    /// Source payment request.
    /// </param>
    /// <param name="_fieldId">
    /// Source field number.
    /// </param>
    /// <param name="_tableId">
    /// Source table id.
    /// </param>
    /// <param name="_formControl">
    /// Source form control.
    /// </param>
    /// <returns>
    /// Instance of <c>CFMPaymentRequestFormViewDetails</c>.
    /// </returns>
    public static CFMPaymentRequestFormViewDetails construct(CFMPaymentRequestTable _paymentRequest, fieldId _fieldId, tableId _tableId, FormControl _formControl = null)
    {
        CFMPaymentRequestFormAttribute extensionAttribute = new CFMPaymentRequestFormAttribute(fieldId2name(_tableId, _fieldId), tableId2Name(_tableId));

        CFMPaymentRequestFormViewDetails form = SysExtensionAppClassFactory::getClassFromSysAttribute(classStr(CFMPaymentRequestFormViewDetails), extensionAttribute);

        form.parmPaymentRequest(_paymentRequest);
        form.parmFormControl(_formControl);

        return form;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addLookupFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds lookup fields.
    /// </summary>
    /// <param name="_tableLookup">
    /// Lookup table.
    /// </param>
    /// <returns>
    /// <c>SysTableLookup</c> object or null.
    /// </returns>
    protected SysTableLookup addLookupFields(SysTableLookup _tableLookup)
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formDataAreaId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets company id.
    /// </summary>
    /// <returns>
    /// Company where detailed form should be open.
    /// </returns>
    abstract protected SelectableDataArea formDataAreaId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>formDetailsName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets form name.
    /// </summary>
    /// <returns>
    /// Name of form which should be open.
    /// </returns>
    abstract protected str formDetailsName()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>formMenuItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets menu item.
    /// </summary>
    /// <returns>
    /// Menu item.
    /// </returns>
    abstract protected str formMenuItem()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>formRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets table record.
    /// </summary>
    /// <returns>
    /// Table record which should be open on detailed form.
    /// </returns>
    abstract protected Common formRecord()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>name</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns name of this class as string.
    /// </summary>
    /// <returns>
    /// Returns name of this class as string.
    /// </returns>
    /// <remarks>
    /// This method needed to prevent crash in called forms. In forms caller is not validated for type.
    /// </remarks>
    public str name()
    {
        return classStr(CFMPaymentRequestFormViewDetails);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFormControl</Name>
				<Source><![CDATA[
    public FormControl parmFormControl(FormControl _formControl = formControl)
    {
        formControl = _formControl;

        return formControl;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymentRequest</Name>
				<Source><![CDATA[
    public CFMPaymentRequestTable parmPaymentRequest(CFMPaymentRequestTable _paymentRequest = paymentRequest)
    {
        paymentRequest = _paymentRequest;

        return paymentRequest;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Shows detailed form in company.
    /// </summary>
    public void showForm()
    {
        this.validate();

        changecompany(this.formDataAreaId())
        {
            Args args = new Args(this.formDetailsName());

            if (this.formRecord())
            {
                args.record(this.formRecord());
            }

            args.menuItemName(this.formMenuItem());
            args.caller(this);

            FormRun formRun = classfactory.formRunClass(args);
            formRun.init();
            formRun.run();
            formRun.wait();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showLookup</Name>
				<Source><![CDATA[
    public void showLookup()
    {
        Query query = new Query();

        query.allowCrossCompany(true);
        query.addCompanyRange(this.formDataAreaId());

        query.addDataSource(this.tableId());

        SysTableLookup sysTableLookup = SysTableLookup::newParameters(this.tableId(), this.parmFormControl());
        sysTableLookup.parmQuery(query);

        this.addLookupFields(sysTableLookup);

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>tableId</Name>
				<Source><![CDATA[
    protected TableId tableId()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks data before form opens.
    /// </summary>
    protected void validate()
    {
        if (!hasMenuItemAccess(this.formMenuItem(), MenuItemType::Display))
        {
            throw error("@SYS342243");
        }

        if (! xDataArea::exist(this.formDataAreaId()))
        {
            throw error(strFmt("@SYS10666",  this.formDataAreaId()));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>