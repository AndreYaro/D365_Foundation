<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchEditLinesInteraction</Name>
	<SourceCode>
		<Declaration><![CDATA[
class PurchEditLinesInteraction
extends PageInteraction
{
    PurchLine       purchLine;
    PurchTableType  purchTableType;
    boolean         isStocked;
    boolean         isNotFinalized;
    boolean         doNotHaveMultipleDeliveries;
    boolean         isOpenStockedLineWthSingleDlv;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>enableLineActions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Enables or disables the line level menu items.
    /// </summary>
    public void enableLineActions()
    {
        boolean hasWHS = this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, WHSHasWhs));

        // Invent -> Reservation
        // Update line -> Process
        if (!hasWHS)
        {
            this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventReserve), isOpenStockedLineWthSingleDlv);
        }

        // Invent -> View
        this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventTrans), isOpenStockedLineWthSingleDlv);
        this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventOnhand), isStocked && purchTableType.inventOnhandInvoke());
        this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventLot), isStocked && doNotHaveMultipleDeliveries && purchTableType.inventLotInvoke());

        // Update line -> Process

        if (!hasWHS)
        {
            this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventTransRegister), doNotHaveMultipleDeliveries && isNotFinalized && purchLine.PurchQty > 0 && purchLine.isTangible());

            this.page().actionPaneControlEnabled(formControlStr(PurchEditLines, inventTransPick), isStocked && doNotHaveMultipleDeliveries && isNotFinalized && purchLine.PurchQty < 0);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the instance variables based on the active records for the form.
    /// </summary>
    private void init()
    {
        PurchParmLine purchParmLine = this.page().activeRecord('PurchParmLine');

        purchLine = purchParmLine.purchLine();

        purchTableType = purchLine.purchTable().type();
        isStocked = purchLine.isStocked();
        isNotFinalized = !purchLine.IsFinalized;
        doNotHaveMultipleDeliveries = purchLine.LineDeliveryType != LineDeliveryType::OrderLineWithMultipleDeliveries;
        isOpenStockedLineWthSingleDlv = isStocked && !purchLine.IsFinalized && doNotHaveMultipleDeliveries;
    }

]]></Source>
			</Method>
			<Method>
				<Name>purchLine_DS_selectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Enables the line action menu item buttons on selection changed.
    /// </summary>
    [FormDataSourceInteractionAttribute('PurchParmLine', 'selectionChanged')]
    public void purchLine_DS_selectionChanged()
    {
        this.init();

        this.enableLineActions();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>