<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSWaveFinalizeLabelPrinting</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSWaveFinalizeLabelPrinting</c> class is responsible for making sure all
/// post label printing wave processes are run.
/// </summary>
internal class WHSWaveFinalizeLabelPrinting extends RunBaseBatch implements SysErrorMessageHelpScopeIBatchProvider, BatchRetryable
{
    private WHSWaveId               waveId;
    private WHSWaveExecutionID      waveExecutionId;
    private WHSReleaseToWarehouseId releaseToWarehouseId;
    private WHSWaveStepRefRecId     currentRunningWaveStepRecId;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        waveId,
        waveExecutionId,
        releaseToWarehouseId,
        currentRunningWaveStepRecId
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WHSWaveFinalizeLabelPrinting construct()
    {
        return new WHSWaveFinalizeLabelPrinting();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromPostEngine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the parameters of the class based on the _postEngine parameters.
    /// </summary>
    /// <param name = "_postEngine">The post engine.</param>
    [Hookable(false)]
    public void initFromPostEngine(WHSPostEngine _postEngine)
    {
        this.parmWaveId(_postEngine.parmWaveTable().WaveId);
        this.parmWaveExecutionId(_postEngine.parmWaveExecutionId());
        this.parmReleaseToWarehouseId(_postEngine.parmReleaseToWarehouseId());
        this.parmCurrentRunningWaveStep(_postEngine.parmCurrentRunningWaveStep());
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCurrentRunningWaveStep</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public WHSWaveStepRefRecId parmCurrentRunningWaveStep(WHSWaveStepRefRecId _currentRunningWaveStep = currentRunningWaveStepRecId)
    {
        currentRunningWaveStepRecId = _currentRunningWaveStep;

        return currentRunningWaveStepRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReleaseToWarehouseId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public WHSReleaseToWarehouseId parmReleaseToWarehouseId(WHSReleaseToWarehouseId _releaseToWarehouseId = releaseToWarehouseId)
    {
        releaseToWarehouseId = _releaseToWarehouseId;

        return releaseToWarehouseId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWaveExecutionId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public WHSWaveExecutionID parmWaveExecutionId(WHSWaveExecutionID _waveExecutionId = waveExecutionId)
    {
        waveExecutionId = _waveExecutionId;

        return waveExecutionId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWaveId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public WHSWaveId parmWaveId(WHSWaveId _waveId = waveId)
    {
        waveId = _waveId;

        return waveId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>ensureWaveProcessContinues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a batch job to run waves steps after label printing.
    /// </summary>
    private void ensureWaveProcessContinues()
    {
        var nextWaveStepTaskCreator = WHSNextWaveStepTaskCreator::newStandard(waveId, waveExecutionId, currentRunningWaveStepRecId, releaseToWarehouseId);

        if (!nextWaveStepTaskCreator.createTask())
        {
            throw error("@WAX:WaveNextBatchTaskCreationError");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void run()
    {        
        using (SysErrorMessageHelpScope scope = SysErrorMessageHelpScope::newErrorContextBatchServer(this))
        {
            WHSWaveTable waveTable = WHSWaveTable::findEnsureRecordIsReadInsideTTS(waveId);

            using (var perfTask = WHSInstrumentationWaveProcessingPerformanceTask::newFinalizeLabelPrinting(waveTable, waveExecutionId, funcName()))
            {
                if (appl.ttsLevel() != 0)
                {
                    WHSInstrumentationWaveLabelPrintingPerformanceTask::logWaveFinalizeLabelPrintingError(
                            "@WAX:WaveProcessingFinalLabelPrintingStepUnexpectedErrorTTSNotZero",
                            waveTable.RecId,
                            waveExecutionId,
                            releaseToWarehouseId,
                            currentRunningWaveStepRecId);

                    // this should never happen because this is the finalize batch task for the label printing step
                    throw error("@WAX:WaveProcessingFinalLabelPrintingStepUnexpectedErrorTTSNotZero");
                }

                try
                {
                    ttsbegin;

                    WHSWaveStepController lockedControlRecord = WHSWaveStepController::createAndLockControlRecord(waveId, waveExecutionId, classStr(WHSWaveFinalizeLabelPrinting), waveTable.InventLocationId);
                    this.ensureWaveProcessContinues();
                    lockedControlRecord.delete();

                    ttscommit;
                }
                catch
                {
                    this.handleException(waveTable);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleException</Name>
				<Source><![CDATA[
    private void handleException(WHSWaveTable _waveTable)
    {
        this.cleanupErroredWave();

        WHSInstrumentationWaveLabelPrintingPerformanceTask::logWaveFinalizeLabelPrintingError(
                    "@WAX:WaveProcessingFinalLabelPrintingStepUnexpectedError",
                    _waveTable.RecId,
                    waveExecutionId,
                    releaseToWarehouseId,
                    currentRunningWaveStepRecId);

        error("@WAX:WaveProcessingFinalLabelPrintingStepUnexpectedError");

        this.notifyOnWaveExecutionFailure();
    }

]]></Source>
			</Method>
			<Method>
				<Name>cleanupErroredWave</Name>
				<Source><![CDATA[
    private void cleanupErroredWave()
    {
        var waveProcessingDataCleanup = WHSWaveProcessingDataCleanup::newForWaveExecution(waveId, waveExecutionId);
        waveProcessingDataCleanup.parmWaveCleanupType('FinalWaveLabelPrintingStep');
        waveProcessingDataCleanup.cleanupErroredWave();
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyOnWaveExecutionFailure</Name>
				<Source><![CDATA[
    private void notifyOnWaveExecutionFailure()
    {
        WHSWaveTable erroredWaveTable = WHSWaveTable::find(waveId);
        str infologText = infolog.text();

        WhsWaveNotificationManager::instance().handleNotificationsOnWaveExecuted(erroredWaveTable, waveExecutionId, 0, infologText);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean unpack(container _packedClass)
    {
        Version version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedClass;
                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@WAX:WavePostLabelPrintingBatchJobDescription";
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>errorMessageHelpScopeFormName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the form name used for the error context using a <c>SysErrorMessageHelpScope</c> class.
    /// </summary>
    /// <returns>The form name used for the error context.</returns>
    [Hookable(false)]
    public FormName errorMessageHelpScopeFormName()
    {
        return strFmt('%1_%2', formStr(Dialog), classStr(WhsWaveExecute));
    }

]]></Source>
			</Method>
			<Method>
				<Name>errorMessageHelpScopeActionName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the action name used for the error context using a <c>SysErrorMessageHelpScope</c> class.
    /// </summary>
    /// <returns>The action name used for the error context.</returns>
    [Hookable(false)]
    public str errorMessageHelpScopeActionName()
    {
        return menuItemActionStr(WHSWaveExecute);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    final public boolean isRetryable()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>batchInfo</Name>
				<Source><![CDATA[
    [Hookable(false)]
    final public BatchInfo batchInfo()
    {
        BatchInfo ret = super();

        ret.parmRetryable(false);
        ret.parmRetriesOnFailure(0);

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>