<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerFiscalCloseTaskApplicationSuite_CN_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerFiscalCloseTaskApplicationSuite_CN_Extension</c> class executes pre-processing year-end close job tasks for the CN country region.
/// </summary>
[ExtensionOf(classStr(LedgerFiscalCloseServiceJobEvents))]
internal final static class LedgerFiscalCloseTaskApplicationSuite_CN_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preJobExecution</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs prerequisite steps for CN country region functionality called prior to company year-end close processing.
    /// </summary>
    /// <param name="_ledgerFiscalCloseServiceHistory">The service job history record for the completed job.</param>
    /// <param name="_jobContract">The year-end close job contract.</param>
    internal static void preJobExecution(
        LedgerFiscalCloseServiceHistory _ledgerFiscalCloseServiceHistory,
        LedgerFiscalCloseServiceJobContract _jobContract)
    {
        if (_ledgerFiscalCloseServiceHistory.LedgerFiscalCloseHistory)
        {
            LedgerFiscalCloseHistory ledgerFiscalCloseHistory = LedgerFiscalCloseHistory::find(_ledgerFiscalCloseServiceHistory.LedgerFiscalCloseHistory);
            FiscalCalendarYear fiscalCalendarYear = FiscalCalendarYear::find(ledgerFiscalCloseHistory.FiscalCalendarYear);
            
            if (LedgerParameters::isChineseVoucher_CN())
            {
                LedgerParameters ledgerParameters = LedgerParameters::find();

                if (ledgerParameters.PeriodClosingPosting)
                {
                    LedgerVoucherTypeRefRecId_CN ledgerVoucherTypeRecId_CN = LedgerVoucherType_CN::findByNaturalKey(_jobContract.parmVoucherTypeId_CN()).RecId;

                    LedgerFiscalCloseChineseVoucher_CN ledgerFiscalCloseChineseVoucher_CN = LedgerFiscalCloseChineseVoucher_CN::newForFiscalYearClose(
                        _jobContract.parmVoucher_CN(),
                        _jobContract.parmVoucherTypeId_CN(),
                        fiscalCalendarYear,
                        ledgerParameters);

                    ledgerFiscalCloseChineseVoucher_CN.setChineseVoucherUsedForClosingPeriod();
                }
            }
        }

        next preJobExecution(_ledgerFiscalCloseServiceHistory, _jobContract);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>