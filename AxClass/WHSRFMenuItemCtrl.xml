<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSRFMenuItemCtrl</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSRFMenuItemCtrl</c> class is used to create tree control for RF menu items.
/// </summary>
class WHSRFMenuItemCtrl
{
    WHSRFMenuTable          menuTable;
    FormTreeControl         tree;

    #WHSRF
	}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildMenuItemNodes</Name>
				<Source><![CDATA[
    /// <summary>
	/// Builds menu item nodes in tree.
	/// </summary>
	/// <param name="_treeNode">
	/// Root tree node.
	/// </param>
    void buildMenuItemNodes(TreeItemIdx _treeNode)
    {
        WHSRFMenuItemTable  menuItemTable;

        while select MenuItemName from menuItemTable
        {
            if (!WHSRFMenuLine::isInMenu(menuTable.MenuName, menuItemTable.MenuItemName))
            {
                SysFormTreeControl::addTreeItem(tree,
                                                menuItemTable.MenuItemName,
                                                _treeNode,
                                                strFmt('%1,%2', #MenuItemCtrl, menuItemTable.MenuItemName));  // MenuItem,%1
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildMenuNodes</Name>
				<Source><![CDATA[
    /// <summary>
	/// Builds menu nodes in tree.
	/// </summary>
	/// <param name="_treeNode">
	/// Root tree node.
	/// </param>
    void buildMenuNodes(TreeItemIdx _treeNode)
    {
        WHSRFMenuTable      locMenuTable;

        while select MenuName from locMenuTable
            where locMenuTable.MenuName != menuTable.MenuName
        {
            if (!WHSRFMenuLine::isInMenu(menuTable.MenuName, '', locMenuTable.MenuName))
            {
                SysFormTreeControl::addTreeItem(tree,
                                                locMenuTable.MenuName,
                                                _treeNode,
                                                strFmt('%1,%2', #MenuCtrl, locMenuTable.MenuName));   // Menu,%1
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildTree</Name>
				<Source><![CDATA[
    /// <summary>
	/// Builds tree with menu items and menus.
	/// </summary>
    void buildTree()
    {
        tree.deleteAll();

        TreeItemIdx menuItemsIdx = SysFormTreeControl::addTreeItem(tree,
                                                                   "@WAX1038",
                                                                   FormTreeAdd::Root,
                                                                   "@WAX1063");

        this.buildMenuItemNodes(menuItemsIdx);

        SysFormTreeControl::expandTree(tree, menuItemsIdx, 3);

        menuItemsIdx = SysFormTreeControl::addTreeItem(tree,
                                                      "@WAX1036",
                                                      FormTreeAdd::Root,
                                                      "@WAX1036");

        this.buildMenuNodes(menuItemsIdx);

        SysFormTreeControl::expandTree(tree, menuItemsIdx, 3);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRFMenuTable</Name>
				<Source><![CDATA[
    WHSRFMenuTable parmRFMenuTable(WHSRFMenuTable _menuTable = menuTable)
    {
        menuTable = _menuTable;

        return menuTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTree</Name>
				<Source><![CDATA[
    FormTreeControl parmTree(FormTreeControl _tree = tree)
    {
        tree = _tree;

        return tree;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
