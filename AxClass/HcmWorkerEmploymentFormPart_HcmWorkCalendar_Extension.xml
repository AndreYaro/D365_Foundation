<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmWorkerEmploymentFormPart_HcmWorkCalendar_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(HcmWorkerEmploymentFormPart))]
internal final class HcmWorkerEmploymentFormPart_HcmWorkCalendar_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        WorkCalendarWorkerAffiliationGroup_CalendarId.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(HcmWorkerEmploymentFormPart_HcmWorkCalendar_Extension, onCalendarLookup),
            this);

        WorkCalendarWorkerAffiliationGroup_CalendarId.registerOverrideMethod(
            methodStr(FormStringControl, validate),
            methodStr(HcmWorkerEmploymentFormPart_HcmWorkCalendar_Extension, onCalendarValidate),
            this);

        WorkCalendarWorkerAffiliationGroup_CalendarId.registerOverrideMethod(
            methodStr(FormStringControl, jumpRef),
            methodStr(HcmWorkerEmploymentFormPart_HcmWorkCalendar_Extension, onCalendarJumpRef),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCalendarLookup</Name>
				<Source><![CDATA[
    private void onCalendarLookup(FormStringControl _calendarId)
    {
        DataAreaId dataAreaId = CompanyInfo::getDataArea(hcmEmployment.legalEntity);

        changecompany (dataAreaId)
        {
            _calendarId.lookup();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCalendarValidate</Name>
				<Source><![CDATA[
    private boolean onCalendarValidate(FormStringControl _calendarId)
    {
        DataAreaId dataAreaId = CompanyInfo::getDataArea(hcmEmployment.legalEntity);

        changecompany (dataAreaId)
        {
            return _calendarId.validate();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCalendarJumpRef</Name>
				<Source><![CDATA[
    private void onCalendarJumpRef(FormStringControl _calendarId)
    {
        DataAreaId dataAreaId = CompanyInfo::getDataArea(hcmEmployment.legalEntity);

        if (dataAreaId == curExt())
        {
            _calendarId.jumpRef();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>