<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmCampaignSelectionCaseActivitiesProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case activities
/// </summary>
[ExportMetadataAttribute('CaseIActivities', tableStr(smmCampaignSelection)), ExportAttribute('Dynamics.AX.Application.CaseIActivities')]
class smmCampaignSelectionCaseActivitiesProvider implements CaseIActivities
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromCommon</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommon(smmActivities _activities, Common _table)
    {
        smmCampaignSelection smmCampaignSelection = _table;
        this.initFromCampaignTargetParameters(_activities, smmCampaignSelection);
        return smmActivityParentType::Campaign;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parentRecId</Name>
				<Source><![CDATA[
    public RefRecId parentRecId(Common _table)
    {
        smmCampaignSelection smmCampaignSelection = _table;
        return smmCampaignTable::find(smmCampaignSelection.CampaignId).RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCommonUsingTemplate</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommonUsingTemplate(smmActivities _activities, Common _table, smmActivities _templateActivity)
    {
        return smmActivityParentType::None;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateExternalMemo</Name>
				<Source><![CDATA[
    public smmOutlookMemo updateExternalMemo(RefRecId _parentLink)
    {
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldActivityBeCreated</Name>
				<Source><![CDATA[
    public CaseYesNoPrompt shouldActivityBeCreated(Common _table, boolean _isBatch)
    {
        if (_isBatch)
        {
            return (smmParametersTable::find().RunBaseBatchCreateActivity == smmRunBaseBatchCreateActivity::Yes) ? (CaseYesNoPrompt::Yes) : (CaseYesNoPrompt::No);
        }
        else
        {
            return smmActivities::shouldActivityBeCreatedGeneric(smmParametersTable::find().CampaignTargetsCreateActivity);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCampaignTargetParameters</Name>
				<Source><![CDATA[
    private void initFromCampaignTargetParameters(smmActivities _activities, smmCampaignSelection _campaignTarget)
    {
        smmParametersTable parameters = smmParametersTable::find();

        _activities.initValue(parameters.CampaignTargetActivityCategory);

        // Get default values from CRM parameters
        _activities.PhaseId                = parameters.CampaignTargetActivityPhase;
        _activities.TypeId                 = parameters.CampaignTargetActivityType;
        _activities.Purpose                = parameters.CampaignTargetActivityPurpose;

        // Copy values from campaign target to activity

        _activities.setStartDate(_campaignTarget.PlannedDate);
        _activities.setEndDate(_campaignTarget.PlannedDate);
        _activities.setReminderDate(_campaignTarget.PlannedDate);
        _activities.ResponsibleWorker    = _campaignTarget.FollowUpRespWorker;

        _activities.initFromWorkerTable(_activities.ResponsibleWorker);

        if (_activities.ReminderMinutes > 0)
        {
            _activities.ReminderActive     = NoYes::Yes;
        }
        else
        {
            _activities.ReminderActive     = NoYes::No;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getParentReferenceFieldNum</Name>
				<Source><![CDATA[
    public FieldId getParentReferenceFieldNum(TableId _tableId, smmActivityParentRefTableId _parentTableId)
    {
        FieldId fieldId;

        switch (_parentTableId)
        {
            case tableNum(DirPartyTable) :
                fieldId = fieldNum(smmCampaignSelection,Party);
                break;

            case tableNum(ContactPerson) :
                fieldId = fieldNum(smmCampaignSelection,ContactPersonId);
                break;
        }

        return fieldId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>