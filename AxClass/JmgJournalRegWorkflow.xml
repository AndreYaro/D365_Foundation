<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgJournalRegWorkflow</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgJournalRegistrationWorkflow</c> class is used to activate a journal registration workflow
///    by calling the <c>activateFromWorkflowType</c> method of the <c>Workflow</c> class.
/// </summary>
public class JmgJournalRegWorkflow
{
    JmgStampJournalTrans        jmgStampJournalTrans;
    WorkflowVersionTable        workflowVersionTable;
    WorkflowComment             workflowComment;
    boolean                     submit;
    WorkflowWorkItemTable       workflowWorkItemTable;
    userId                      userId;
    menuItemName                menuItemName;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogOk</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Opens a dialog box for submitting a journal registration record.
    /// </summary>
    /// <returns>
    ///    true if the dialog box was closed by pressing the Ok button; otherwise, false.
    /// </returns>
    public boolean dialogOk()
    {
        WorkflowSubmitDialog            workflowSubmitDialog;
        WorkflowWorkItemActionDialog    workflowWorkItemActionDialog;
        boolean                         ok = workflowComment != '';

        if (menuItemName == menuitemactionstr(JmgJournalRegSubmitWorkflow) && !workflowComment)
        {
            workflowSubmitDialog = WorkflowSubmitDialog::construct(this.parmWorkflowVersionTable());
            workflowSubmitDialog.run();
            this.parmWorkflowComment(workflowSubmitDialog.parmWorkflowComment());
            ok = workflowSubmitDialog.parmIsClosedOK();
        }
        else if (menuItemName == menuitemactionstr(JmgJournalRegResubmitWorkflow) && !workflowComment)
        {
            workflowWorkItemActionDialog = WorkflowWorkItemActionDialog::construct( workflowWorkItemTable,
                                                                                    WorkflowWorkItemActionType::Resubmit,
                                                                                    new MenuFunction(menuitemactionstr(JmgJournalRegReSubmitWorkflow), MenuItemType::Action));
            workflowWorkItemActionDialog.run();
            this.parmWorkflowComment(workflowWorkItemActionDialog.parmWorkflowComment());
            ok = workflowWorkItemActionDialog.parmIsClosedOK();
            userId = workflowWorkItemActionDialog.parmTargetUser();
        }
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmJmgStampJournalTrans</Name>
				<Source><![CDATA[
    public JmgStampJournalTrans parmJmgStampJournalTrans(JmgStampJournalTrans _jmgStampJournalTrans = jmgStampJournalTrans)
    {
        jmgStampJournalTrans = _jmgStampJournalTrans;

        return jmgStampJournalTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMenuItemName</Name>
				<Source><![CDATA[
    public menuItemName parmMenuItemName(menuItemName _menuItemName = menuItemName)
    {
        menuItemName = _menuItemName;

        return menuItemName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSubmit</Name>
				<Source><![CDATA[
    public boolean parmSubmit(boolean _submit = submit)
    {
        submit = _submit;

        return submit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowComment</Name>
				<Source><![CDATA[
    public WorkflowComment parmWorkflowComment(WorkflowComment _workflowComment = workflowComment)
    {
        workflowComment = _workflowComment;

        return workflowComment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowVersionTable</Name>
				<Source><![CDATA[
    public WorkflowVersionTable parmWorkflowVersionTable(WorkflowVersionTable _workflowVersionTable = workflowVersionTable)
    {
        workflowVersionTable = _workflowVersionTable;

        return workflowVersionTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowWorkItemtable</Name>
				<Source><![CDATA[
    public WorkflowWorkItemTable parmWorkflowWorkItemtable(WorkflowWorkItemTable _workflowWorkItemTable = workflowWorkItemTable)
    {
        workflowWorkItemTable = _workflowWorkItemTable;
        return workflowWorkItemTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>resubmit</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Resubmits the journal registration workflow.
    /// </summary>
    /// <remarks>
    ///    This method resubmits the workflow configuration by calling the <c>activateFromWorkflowType</c>
    ///    method on the <c>Workflow</c> class with information about the journal registration record.
    /// </remarks>
    public void resubmit()
    {
        Object jmgStampJournalTrans_ds;

        ttsbegin;
        WorkflowWorkItemActionManager::dispatchWorkItemAction(workflowWorkItemTable,
                                                              workflowComment,
                                                              userId,
                                                              WorkflowWorkItemActionType::Resubmit,
                                                              menuItemName);

        jmgStampJournalTrans_ds = FormDataUtil::getFormDataSource(jmgStampJournalTrans);
        jmgStampJournalTrans.WorkflowStatus = JmgJournalRegWorkflowStatus::Submitted;
        if (jmgStampJournalTrans_ds)
        {
            jmgStampJournalTrans_ds.write();
            jmgStampJournalTrans_ds.refresh();
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Activates journal registration workflow.
    /// </summary>
    /// <remarks>
    ///    This method starts the workflow configuration by calling the <c>activateFromWorkflowType</c> method
    ///    on the <c>Workflow</c> class with information about the journal registration record.
    /// </remarks>
    public void submit()
    {
        Object jmgStampJournalTrans_ds;

        Workflow::activateFromWorkflowType(workflowtypestr(JmgJournalRegType),
                                           jmgStampJournalTrans.RecId,
                                           this.parmWorkflowComment(),
                                           false);

        jmgStampJournalTrans_ds = FormDataUtil::getFormDataSource(jmgStampJournalTrans);
        jmgStampJournalTrans.WorkflowStatus = JmgJournalRegWorkflowStatus::Submitted;

        if (jmgStampJournalTrans_ds)
        {
            jmgStampJournalTrans_ds.write();
            jmgStampJournalTrans_ds.refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static JmgJournalRegWorkflow construct()
    {
        return new JmgJournalRegWorkflow();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Submits or resubmits a journal registration document and starts corresponding workflow.
    /// </summary>
    /// <param name="args">
    ///    An <c>Args</c> value from the caller.
    /// </param>
    public static void main(Args args)
    {
        JmgJournalRegWorkflow   jmgJournalRegWorkflow;
        JmgStampJournalTrans    jmgStampJournalTrans;

        jmgStampJournalTrans = args.record();
        jmgJournalRegWorkflow = JmgJournalRegWorkflow::construct();
        jmgJournalRegWorkflow.parmJmgStampJournalTrans(args.record());
        jmgJournalRegWorkflow.parmMenuItemName(args.menuItemName());
        jmgJournalRegWorkflow.parmWorkflowVersionTable(args.caller().getActiveWorkflowConfiguration());
        jmgJournalRegWorkflow.parmWorkflowWorkItemtable(args.caller().getActiveWorkflowWorkItem());
        jmgJournalRegWorkflow.parmSubmit(args.menuItemName() == menuitemactionstr(JmgJournalRegSubmitWorkflow));
        jmgJournalRegWorkflow.parmWorkflowComment(args.parm());

        if (jmgJournalRegWorkflow.dialogOk())
        {
            if (jmgJournalRegWorkflow.parmSubmit())
            {
                jmgJournalRegWorkflow.submit();
            }
            else
            {
                jmgJournalRegWorkflow.resubmit();
            }

            args.caller().updateWorkflowControls();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>