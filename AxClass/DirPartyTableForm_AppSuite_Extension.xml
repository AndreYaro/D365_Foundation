<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DirPartyTableForm_AppSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the ApplicationSuite module form extension events for the <c>DirPartyTable</c> form.
/// </summary>
[ExtensionOf(formStr(DirPartyTable))]
public final class DirPartyTableForm_AppSuite_Extension
{
    #ISOCountryRegionCodes
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>CFMDirPartyTable_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler for the Initialized event of the DirPartyTable form.
    /// </summary>
    /// <param name="_sender">The <c>CFMDirPartyTable</c> data source.</param>
    /// <param name="_e">The args for the event.</param>
    [FormDataSourceEventHandler(formDataSourceStr(DirPartyTable, CFMDirPartyTable), FormDataSourceEventType::Initialized)]
    public static void CFMDirPartyTable_OnInitialized(FormDataSource sender, FormDataSourceEventArgs e)
    {
        if (!SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            if (sender)
            {
                QueryBuildDataSource dirPartyQbds = sender.queryBuildDataSource();
                dirPartyQbds.enabled(false);
                sender.linkType(FormLinkType::Passive);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>CFMDirPartyTable_OnValidatingWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Hooks on ValidatingWrite event of the <c>CFMDirPartyTable</c> form data source and cancels the validation in case .
    /// </summary>
    /// <param name = "_sender">The data source object that triggers the event.</param>
    /// <param name = "_e">Event arguments.</param>
    [FormDataSourceEventHandler(formDataSourceStr(DirPartyTable, CFMDirPartyTable), FormDataSourceEventType::ValidatingWrite)]
    public static void CFMDirPartyTable_OnValidatingWrite(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            FormDataSource dirPartyTable_ds = _sender.formRun().dataSource(formDataSourceStr(DirPartyTable, DirPartyTable)) as FormDataSource;
            CFMDirPartyTable cfmDirPartyTable = _sender.cursor();

            if (!dirPartyTable_ds.cursor().RecId && dirPartyTable_ds.validateWrite())
            {
                dirPartyTable_ds.write();
                
                cfmDirPartyTable.DirPartyTable = dirPartyTable_ds.cursor().RecId;

                FormDataSourceCancelEventArgs cancelable = _e as FormDataSourceCancelEventArgs;
                cancelable.cancel(!cfmDirPartyTable.RecId && !cfmDirPartyTable.PaymentPriority);
            }

            if (!cfmDirPartyTable.DirPartyTable)
            {
                cfmDirPartyTable.DirPartyTable = dirPartyTable_ds.cursor().RecId;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>