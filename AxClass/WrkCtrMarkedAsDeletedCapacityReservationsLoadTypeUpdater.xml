<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater</c> updates the load type of the marked as deleted <c>WrkCtrCapRes</c> records.
/// </summary>
public class WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater extends SysQueryUpdateRecordSet
{
    private WrkCtrCapRes wrkCtrCapResRange;
    private WrkCtrCapacityType targetLoadType;
    private boolean isRangeInitialized;
    private boolean isTargetLoadTypeInitialized;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeFieldValueMap</Name>
				<Source><![CDATA[
    protected void initializeFieldValueMap()
    {
        if (!isTargetLoadTypeInitialized)
        {
            throw error(Error::WrongUseOfFunction(funcName()));
        }

        this.insertFieldValueMapPair(tableNum(WrkCtrCapRes), fieldNum(WrkCtrCapRes, LoadType), strFmt('"%1"', targetLoadType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    protected Query initQuery()
    {
        if (!isRangeInitialized)
        {
            throw error(Error::WrongUseOfFunction(funcName()));
        }

        Query query = new Query();
        query.recordLevelSecurity(false);

        QueryBuildDataSource qbdsWrkCtrCapRes = query.addDataSource(tableNum(WrkCtrCapRes));
        qbdsWrkCtrCapRes.addRange(fieldNum(WrkCtrCapRes, PlanVersion)).value(queryValue(wrkCtrCapResRange.PlanVersion));
        qbdsWrkCtrCapRes.addRange(fieldNum(WrkCtrCapRes, RefType)).value(queryValue(wrkCtrCapResRange.RefType));
        qbdsWrkCtrCapRes.addRange(fieldNum(WrkCtrCapRes, RefId)).value(queryValue(wrkCtrCapResRange.RefId));
        qbdsWrkCtrCapRes.addRange(fieldNum(WrkCtrCapRes, LoadType)).value(queryValue(WrkCtrCapacityType::Deletemarked));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>skipDataMethods</Name>
				<Source><![CDATA[
    protected boolean skipDataMethods()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>withWrkCtrCapResRange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies the <c>WrkCtrCapRes</c> which is used to specify the range for the update query.
    /// </summary>
    /// <param name = "_range">The <c>WrkCtrCapRes</c> which is used to specify the range for the update query.</param>
    /// <returns>The instance with which this method was called.</returns>
    public WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater withWrkCtrCapResRange(WrkCtrCapRes _range)
    {
        wrkCtrCapResRange = _range;
        isRangeInitialized = true;

        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>withTargetLoadType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies the <c>WrkCtrCapacityType</c> which is used as the new value for the load type field.
    /// </summary>
    /// <param name = "_targetLoadTypeValue">The <c>WrkCtrCapacityType</c> which is used as the new value for the load type field.</param>
    /// <returns>The instance with which this method was called.</returns>
    public WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater withTargetLoadType(WrkCtrCapacityType _targetLoadType)
    {
        targetLoadType = _targetLoadType;
        isTargetLoadTypeInitialized = true;

        return this;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates a new instance of the <c>WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater</c> class.
    /// </summary>
    /// <returns>A new instance of <c>WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater</c>.</returns>
    public static WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater construct()
    {
        return new WrkCtrMarkedAsDeletedCapacityReservationsLoadTypeUpdater();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>