<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The CFMPaymentRequestSubmitManager menu item action event handler.
/// </summary>
public class CFMPaymentRequestSubmitManager
{
    CFMPaymentRequestTable paymentRequestTable;
    WorkflowComment submitionNote;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CFMPaymentRequestSubmitManager construct(CFMPaymentRequestTable _paymentRequestTable)
    {
        CFMPaymentRequestSubmitManager submitManager = new CFMPaymentRequestSubmitManager();

        submitManager.init(_paymentRequestTable);

        return submitManager;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        if (args.record() &&
            args.dataset() == tableNum(CFMPaymentRequestTable))
        {
            CFMPaymentRequestTable paymentRequestTable = args.record() as CFMPaymentRequestTable;

            if (paymentRequestTable.canSubmitToWorkflow())
            {
                CFMPaymentRequestSubmitManager submitManager = CFMPaymentRequestSubmitManager::construct(paymentRequestTable);
                submitManager.run();
            }
            else
            {
                checkFailed(strFmt("@Cashflowmanagement:WorkflowIsNotConfiguredForPaymRequest", paymentRequestTable.PaymentRequestId));
            }
        }
        else
        {
            throw  error(Error::wrongUseOfFunction(funcName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init(CFMPaymentRequestTable _paymentRequestTable)
    {
        paymentRequestTable = _paymentRequestTable;
        submitionNote = '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        if (this.showSubmitDialog())
        {
            this.submitWorkflow();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showSubmitDialog</Name>
				<Source><![CDATA[
    private boolean showSubmitDialog()
    {
        WorkflowSubmitDialog workflowSubmitDialog = WorkflowSubmitDialog::construct(paymentRequestTable.getWorkflowVersionTable());
        workflowSubmitDialog.run();

        submitionNote = workflowSubmitDialog.parmWorkflowComment();

        return workflowSubmitDialog.parmIsClosedOK();
    }

]]></Source>
			</Method>
			<Method>
				<Name>submitWorkflow</Name>
				<Source><![CDATA[
    private void submitWorkflow()
    {
        try
        {
            ttsbegin;
            WorkflowCorrelationId workflowCorrelationId = CFMPaymentRequestWorkflowManager::submitWorkflow(paymentRequestTable, submitionNote);
            info("@Cashflowmanagement:SubmittedToWorkflow");

            ttscommit;
        }
        catch(Exception::Error)
        {
            info("@SYS303438");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>