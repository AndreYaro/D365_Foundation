<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPRPeriodicCreateQueryBuilder_Cust</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestSourceTypeAttribute(CFMPaymentRequestSourceType::CustInvoice)]
public class CFMPRPeriodicCreateQueryBuilder_Cust extends CFMPRPeriodicCreateQueryBuilder
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPaymentRequestSourceQuery</Name>
				<Source><![CDATA[
    public Query getPaymentRequestSourceQuery()
    {
        Query sourceQuery = new Query();
        
        QueryBuildDataSource qbds = sourceQuery.addDataSource(tableNum(CustTrans));

        QueryBuildRange qbr = qbds.addRange(fieldNum(CustTrans, Invoice));
        qbr.value(SysQuery::valueNotEmptyString());

        qbds = qbds.addDataSource(tableNum(CustTransOpen));

        qbr = qbds.addRange(fieldNum(CustTransOpen, AmountCur));
        qbr.value(#negative);

        qbr = qbds.addRange(fieldNum(CustTransOpen, DueDate));

        if (dateTo == dateNull())
        {
            qbr.value(SysQuery::range(dateFrom, maxDate()));
        }
        else
        {
            qbr.value(SysQuery::range(dateFrom, dateTo));
        }

        qbds.addLink(fieldNum(CustTrans, RecId), fieldNum(CustTransOpen, RefRecId));
        qbds.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustTransOpen, AccountNum));
        qbds.joinMode(JoinMode::InnerJoin);

        qbds = qbds.addDataSource(tableNum(CFMPaymentRequestTable));

        qbr = qbds.addRange(fieldNum(CFMPaymentRequestTable, SourceType));
        qbr.value(queryValue(CFMPaymentRequestSourceType::CustInvoice));

        qbds.addLink(fieldNum(CFMPaymentRequestTable, SourceRefRecId), fieldNum(CustTransOpen, RecId));
        qbds.joinMode(JoinMode::NoExistsJoin);

        this.addQueryDataAreaFiltering(sourceQuery);

        return sourceQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryDataSourceTableId</Name>
				<Source><![CDATA[
    public TableId getQueryDataSourceTableId()
    {
        return tableNum(CustTransOpen);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>