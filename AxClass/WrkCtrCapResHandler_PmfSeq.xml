<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WrkCtrCapResHandler_PmfSeq</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>WrkCtrCapResHandler_PmfSeq</c> class can be used to retrieve information about resource
/// capacity reservations that were sequenced.
/// </summary>
class WrkCtrCapResHandler_PmfSeq extends WrkCtrCapResHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addCapResQueryIndex</Name>
				<Source><![CDATA[
    protected void addCapResQueryIndex(QueryBuildDataSource _capResDataSource)
    {
        _capResDataSource.addSortIndex(indexNum(PmfSeqWrkCtrCapRes, WrkCtrIdx));
    }

]]></Source>
			</Method>
			<Method>
				<Name>capResPropertyTableFieldId</Name>
				<Source><![CDATA[
    protected FieldId capResPropertyTableFieldId(FieldId _fieldId)
    {
        return fieldName2id(tableNum(PMFSeqWrkCtrCapResProperty), fieldId2name(tableNum(WrkCtrCapResProperty), _fieldId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>capResPropertyTableId</Name>
				<Source><![CDATA[
    protected TableId capResPropertyTableId()
    {
        return tableNum(PmfSeqWrkCtrCapResProperty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>capResTableFieldId</Name>
				<Source><![CDATA[
    protected FieldId capResTableFieldId(FieldId _fieldId)
    {
        return fieldName2id(tableNum(PMFSeqWrkCtrCapRes), fieldId2name(tableNum(WrkCtrCapRes), _fieldId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>capResTableId</Name>
				<Source><![CDATA[
    protected TableId capResTableId()
    {
        return tableNum(PMFSeqWrkCtrCapRes);
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteOrderReservations</Name>
				<Source><![CDATA[
    public void deleteOrderReservations(
        WrkCtrCapRefType        _refType,
        WrkCtrRefId             _refNum,
        OprNum                  _oprNum         = 0,
        RefRecId                _planVersion    = 0,
        ProdJobId               _jobId          = ''
        )
    {
        PmfSeqWrkCtrCapRes            wrkCtrCapRes;
        PmfSeqWrkCtrCapResProperty    wrkCtrCapResProperty;

        wrkCtrCapRes.skipDataMethods(true);
        wrkCtrCapRes.skipDeleteActions(true);

        if (_refType != WrkCtrCapRefType::PlannedOrder)
        {
            _planVersion = 0;
        }

        ttsbegin;

        delete_from wrkCtrCapResProperty
        exists join wrkCtrCapRes
            where   wrkCtrCapRes.RecId              == wrkCtrCapResProperty.CapResRefRecId
                &&  wrkCtrCapRes.PlanVersion        == _planVersion
                &&  wrkCtrCapRes.RefType            == _refType
                &&  wrkCtrCapRes.RefId              == _refNum
                &&  (wrkCtrCapRes.OprNum            == _oprNum      || !_oprNum)
                &&  (wrkCtrCapRes.JobId             == _jobId       || !_jobId)
                &&  wrkCtrCapRes.LoadType           != WrkCtrCapacityType::Deletemarked;

        delete_from wrkCtrCapRes
            where   wrkCtrCapRes.PlanVersion            == _planVersion
                &&  wrkCtrCapRes.RefType                == _refType
                &&  wrkCtrCapRes.RefId                  == _refNum
                &&  (wrkCtrCapRes.OprNum                == _oprNum || !_oprNum)
                &&  (wrkCtrCapRes.JobId                 == _jobId  || !_jobId)
                &&  wrkCtrCapRes.LoadType               != WrkCtrCapacityType::Deletemarked;

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasNewCapacityReservation</Name>
				<Source><![CDATA[
    public boolean hasNewCapacityReservation(guid _conflictCheckSessionId)
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WrkCtrCapResHandler_PmfSeq construct()
    {
        return new WrkCtrCapResHandler_PmfSeq();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParams</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>WrkCtrCapResHandler_PmfSeq</c> class.
    /// </summary>
    /// <param name="_planVersion">
    /// A <c>RecId</c> value that identifies the plan version to use in the capacity reservation queries.
    /// </param>
    /// <param name="_limitCapPO">
    /// A Boolean that indicates whether planned production order capacity reservations are considered in
    /// the queries.
    /// </param>
    /// <param name="_limitCapProd">
    /// A Boolean that indicates whether production order capacity reservations are considered in the
    /// queries.
    /// </param>
    /// <param name="_limitCapProject">
    /// A Boolean that indicates whether the project capacity reservations are considered in the queries.
    /// </param>
    /// <returns>
    /// A new <c>WrkCtrCapResHandler_PmfSeq</c> class instance.
    /// </returns>
    public static WrkCtrCapResHandler_PmfSeq newParams(
        RecId   _planVersion,
        boolean _limitCapPO         = true,
        boolean _limitCapProd       = true,
        boolean _limitCapProject    = true)
    {
        WrkCtrCapResHandler_PmfSeq capResHandler = new WrkCtrCapResHandler_PmfSeq();

        capResHandler.parmPlanVersion(_planVersion);
        capResHandler.parmLimitCapPO(_limitCapPO);
        capResHandler.parmLimitCapProd(_limitCapProd);
        capResHandler.parmLimitCapProject(_limitCapProject);

        return capResHandler;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>