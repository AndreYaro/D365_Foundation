<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestUpdateSource_Vend</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestSourceTypeAttribute(CFMPaymentRequestSourceType::VendInvoice)]
class CFMPaymentRequestUpdateSource_Vend extends CFMPaymentRequestUpdateSource
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doUpdate</Name>
				<Source><![CDATA[
    protected void doUpdate()
    {
        ttsBegin;

        if (paymentRequestTable.HeaderRefRecId)
        {
            VendTrans vendTrans = VendTrans::find(paymentRequestTable.HeaderRefRecId, true);
            vendTrans.PaymMode = paymentRequestTable.PaymMode;
            vendTrans.PaymSpec = paymentRequestTable.PaymSpec;
            vendTrans.ThirdPartyBankAccountId = paymentRequestTable.RecepientBankAccountId;
            vendTrans.CompanyBankAccountId = (paymentRequestTable.PaymentAccountType == CFMPaymentAccountType::Bank) ? paymentRequestTable.PaymentAccountNum : vendTrans.CompanyBankAccountId;
            vendTrans.update();
        }

        if (paymentRequestTable.SourceRefRecId)
        {
            VendTransOpen vendTransOpen = VendTransOpen::find(paymentRequestTable.SourceRefRecId, true);
            if (vendTransOpen)
            {
                vendTransOpen.DueDate = paymentRequestTable.PaymentDate;
                vendTransOpen.ThirdPartyBankAccountId = paymentRequestTable.RecepientBankAccountId;
                vendTransOpen.update();
            }
        }

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doValidateField</Name>
				<Source><![CDATA[
    protected boolean doValidateField(FieldId _fieldId)
    {
        boolean ret = super(_fieldId);

        if (ret)
        {
            switch(_fieldId)
            {
                case fieldNum(CFMPaymentRequestTable, PaymMode):

                    if (paymentRequestTable.PaymMode && !VendPaymModeTable::exist(paymentRequestTable.PaymMode))
                    {
                        ret = checkFailed(strFmt("@Cashflowmanagement:SpecifiedValueDoesNotExistSourceCompanyPaymRequest", paymentRequestTable.PaymMode));
                    }
                    break;

                case fieldNum(CFMPaymentRequestTable, PaymSpec):

                    if (paymentRequestTable.PaymSpec && !VendPaymModeSpec::existForPaymMode(paymentRequestTable.PaymSpec, paymentRequestTable.PaymMode))
                    {
                        ret = checkFailed(strFmt("@Cashflowmanagement:SpecifiedValueDoesNotExistSourceCompanyPaymRequest", paymentRequestTable.PaymSpec));
                    }
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>