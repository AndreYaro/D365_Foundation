<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PlVendCorrectJourPost</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PlVendCorrectJourPost</c> class is used to post a vendor invoice
/// correction note.
/// </summary>
class PlVendCorrectJourPost
{
    private PlVendCorrectJour vendCorrectJour;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferencePL00030, funcName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendCorrectJour</Name>
				<Source><![CDATA[
    final public PlVendCorrectJour parmVendCorrectJour(PlVendCorrectJour _vendCorrectJour = vendCorrectJour)
    {
        vendCorrectJour = _vendCorrectJour;

        return vendCorrectJour;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Posts a correction note.
    /// </summary>
    public void run()
    {
        RefRecId numSeqRef;

        if (vendCorrectJour.Posted)
        {
            return;
        }

        if (!vendCorrectJour.OriginalText)
        {
            throw error("@GLS110057");
        }

        ttsbegin;

        if (!vendCorrectJour.CorrNoteId)
        {
            numSeqRef = NumberSeqReference::findReference(extendedTypeNum(PlCorrNoteId)).NumberSequenceId;

            if (numSeqRef)
            {
                vendCorrectJour.CorrNoteId = NumberSeq::newGetNumFromId(numSeqRef).num();
            }
        }

        if (!vendCorrectJour.CorrNoteId)
        {
            throw error("@GLS110054");
        }

        if (!vendCorrectJour.DocumentDate)
        {
            vendCorrectJour.DocumentDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        }

        vendCorrectJour.Posted = NoYes::Yes;

        vendCorrectJour.update();

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postCorrectionNote</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Posts a correction note.
    /// </summary>
    /// <param name="_vendCorrectJour">
    ///     <c>PlVendCorrectJour</c> to post.
    /// </param>
    public static void postCorrectionNote(PlVendCorrectJour _vendCorrectJour)
    {
        PlVendCorrectJourPost vendCorrectJourPost = new PlVendCorrectJourPost();

        vendCorrectJourPost.parmVendCorrectJour(_vendCorrectJour);
        vendCorrectJourPost.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>