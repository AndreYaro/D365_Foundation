<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanSupplyForecastDrillThroughHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetPlanSupplyForecastDrillThroughHelper</c> class will be used as a helper for the drill through functionality
/// from budget planning to supply forecast.
/// </summary>
public class BudgetPlanSupplyForecastDrillThroughHelper extends BudgetPlanDrillThroughHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static BudgetPlanSupplyForecastDrillThroughHelper construct()
    {
        return new BudgetPlanSupplyForecastDrillThroughHelper();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQueryForInventoryDrillThrough</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the range criteria based on invent posting profile for the given account types and
    /// adds the ranges of different attributes of supply forecast in the query.
    /// </summary>
    /// <param name = "_query">The <c>Query</c> instance.</param>
    /// <param name = "_inventAccountTypeConsump">The value of the <c>InventAccountType</c> for consumption.</param>
    /// <param name = "_inventAccountTypeDisc">The value of the <c>InventAccountType</c> for discount.</param>
    /// <param name = "_inventAccountTypeDiscExpense">The value of the <c>InventAccountType</c> for discount expense.</param>
    public void updateQueryForInventoryDrillThrough(
        Query _query,
        InventAccountType _inventAccountTypeConsump,
        InventAccountType _inventAccountTypeDisc,
        InventAccountType _inventAccountTypeDiscExpense)
    {
        if (this.parmForecastModelId())
        {
            _query.dataSourceTable(tableNum(ForecastPurch)).addRange(fieldnum(ForecastPurch, ModelId)).value(this.parmForecastModelId());
        }

        this.createRangeCriteriaFromInventPosting(
            _inventAccountTypeConsump,
            _inventAccountTypeDisc,
            _inventAccountTypeDiscExpense,
            true);

        this.addRangesInQuery(_query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addRangesInQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the ranges of different attributes of supply forecast in the query.
    /// </summary>
    /// <param name = "_query">The value of the <c>ForecastPurch</c> query.</param>
    private void addRangesInQuery(Query _query)
    {
        str rangeCriteria;
        str rangePlaceHolder = '(%1.%2 == "%3")';

        QueryBuildDataSource forecastPurchDataSource = _query.dataSourceTable(tableNum(ForecastPurch));

        Name forecastPurchDataSourceName = forecastPurchDataSource.name();

        if (!isItemRelationAll)
        {
            if (items)
            {
                rangeCriteria = strfmt(rangePlaceHolder, forecastPurchDataSourceName, fieldstr(ForecastPurch, ItemId), strDel(items, strlen(items), 1));
            }

            rangeCriteria = BudgetPlanUtility::addRangeCriteria(
                                itemGroups,
                                rangeCriteria,
                                forecastPurchDataSourceName,
                                fieldstr(ForecastPurch, ItemGroupId));
        }

        if (!isAccountRelationAll)
        {
            rangeCriteria = BudgetPlanUtility::addRangeCriteria(
                                accounts,
                                rangeCriteria,
                                forecastPurchDataSourceName,
                                fieldstr(ForecastPurch, VendAccountId));

            rangeCriteria = BudgetPlanUtility::addRangeCriteria(
                                accountGroups,
                                rangeCriteria,
                                forecastPurchDataSourceName,
                                fieldstr(ForecastPurch, VendGroupId));
        }

        rangeCriteria = BudgetPlanUtility::addRangeCriteria(
                                saleTaxGroups,
                                rangeCriteria,
                                forecastPurchDataSourceName,
                                fieldstr(ForecastPurch, TaxGroupId));

        if (rangeCriteria)
        {
            forecastPurchDataSource.addRange(fieldnum(ForecastPurch, ItemId)).value(rangeCriteria);
        }
        else if (!isAccountRelationAll && !isItemRelationAll)
        {
            forecastPurchDataSource.addRange(fieldnum(ForecastPurch, ItemId)).value(SysQuery::valueEmptyString());
        }

        BudgetPlanDrillThroughHelper::appendDimensionRanges(forecastPurchDataSource, fieldNum(ForecastPurch, DefaultDimension), dimensionAttributeValueMap);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>