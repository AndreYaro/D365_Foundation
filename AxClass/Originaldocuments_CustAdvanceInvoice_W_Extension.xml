<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>Originaldocuments_CustAdvanceInvoice_W_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>Originaldocuments_CustAdvanceInvoice_W_Extension</c> extension class
/// provides wrapper methods for <c>Originaldocuments</c> class
/// to include Advance invoices into the Original documents data collection scope.
/// </summary>
[ExtensionOf(classStr(Originaldocuments))]
internal final class Originaldocuments_CustAdvanceInvoice_W_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findFromGeneralJournal</Name>
				<Source><![CDATA[
    void findFromGeneralJournal()
    {
        #ISOCountryRegionCodes

        next findFromGeneralJournal();

        GeneralJournalAccountEntry generalJournalAccountEntry_Caller = commonArg as GeneralJournalAccountEntry;

        if (generalJournalAccountEntry_Caller
            && SysCountryRegionCode::isLegalEntityInCountryRegion([#isoCZ, #isoHU, #isoPL]))
        {
            CzCustAdvanceInvoiceTable czCustAdvanceInvoiceTable;
            GeneralJournalEntry generalJournalEntry;
            CustTable custTable;

            while select InvoiceAmount, InvoiceId, InvoiceAccount from czCustAdvanceInvoiceTable
                outer join Party from custTable
                    where custTable.AccountNum == czCustAdvanceInvoiceTable.InvoiceAccount
                exists join generalJournalEntry
                    where generalJournalEntry.RecId == generalJournalAccountEntry_Caller.GeneralJournalEntry
                        && czCustAdvanceInvoiceTable.Voucher == generalJournalEntry.SubledgerVoucher
                        && czCustAdvanceInvoiceTable.InvoiceDate == generalJournalEntry.AccountingDate
            {
                CustName custName = custTable.name();

                this.insertDocument(
                    OriginalDocument::CustAdvanceInvoice_W,
                    czCustAdvanceInvoiceTable.TableId,
                    czCustAdvanceInvoiceTable.RecId,
                    SysModule::Cust,
                    czCustAdvanceInvoiceTable.InvoiceAmount,
                    czCustAdvanceInvoiceTable.InvoiceId,
                    czCustAdvanceInvoiceTable.InvoiceAccount,
                    custName);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>