<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsRebateTableStatusType_Approved</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class that holds the rebate processes specific to rebate with status 'Approved'.
/// </summary>
public class PdsRebateTableStatusType_Approved extends PdsRebateTableStatusType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>inserted</Name>
				<Source><![CDATA[
    public void inserted()
    {
        super();

        if (pdsRebateTable.pdsRebateAgreement().PdsCumulateSalesBy == PdsCumulationPeriod::Invoice)
        {
            this.runProcessed();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pdsRebateStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the rebate status as 'Approved'.
    /// </summary>
    /// <returns>
    /// The rebate status.
    /// </returns>
    public PdsRebateStatus pdsRebateStatus()
    {
        return PdsRebateStatus::Approved;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runProcessed</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the rebate processing.
    /// </summary>
    /// <param name="_forAdjustment">
    /// A boolean value that determines if the rebate is an adjustment.
    /// </param>
    /// <exception cref="Exception::UpdateConflictNotRecovered">
    /// Update conflict is not recovered.
    /// </exception>
    /// <exception cref="Exception::UpdateConflict">
    /// Update conflict is present.
    /// </exception>
    public void runProcessed(
        boolean _forAdjustment  = false)
    {
        #OCCRetryCount

        try
        {
            ttsbegin;

            pdsRebateTable.selectForUpdate(true);

            pdsRebateTable.type().runProcessed(_forAdjustment);

            pdsRebateTable.update();

            ttscommit;

            info(strfmt("@PRO1431", pdsRebateTable.PdsRebateId));
        }
        catch (Exception::Error)
        {
            exceptionTextFallThrough();
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setNextStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the rebate status as 'MarkForCredit'.
    /// </summary>
    public void setNextStatus()
    {
        pdsRebateTable.PdsRebateStatus = PdsRebateStatus::MarkForCredit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateFieldValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates changes to a field in the <c>PdsRebateTable</c> table.
    /// </summary>
    /// <param name="_fieldName">
    /// The field name to Validate.
    /// </param>
    /// <param name="_arrayIndex">
    /// The array index of the field to validate; optional.
    /// </param>
    /// <returns>
    /// true if the value in the field change is valid; otherwise, false.
    /// </returns>
    public boolean validateFieldValue(fieldName _fieldName, int _arrayIndex = 1)
    {
        boolean ret = true;

        if (_fieldName == fieldStr(PdsRebateTable, PdsMarkForCredit))
        {
            ret = checkFailed(strfmt("@PRO1902", pdsRebateTable.PdsRebateId));
        }

        ret = ret && super(_fieldName, _arrayIndex);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the class.
    /// </summary>
    /// <returns>
    /// A class instance.
    /// </returns>
    public static PdsRebateTableStatusType_Approved construct()
    {
        return new PdsRebateTableStatusType_Approved();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>