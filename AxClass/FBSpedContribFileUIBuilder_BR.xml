<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBSpedContribFileUIBuilder_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>FBSpedContribFileUIBuilder_BR</c> class is the UI builder class for the sped contributions file generation operation.
/// </summary>
class FBSpedContribFileUIBuilder_BR extends SysOperationAutomaticUIBuilder
{
    DialogField dlgFiscalEstablishmentId;
    DialogField dlgFiscalEstablishmentDataArea;
    DialogField dlgMonth;
    DialogField dlgYear;
    DialogField dlgSpedFileType;
    protected DialogField dlgContribLateSubmission;
    protected DialogField dlgContribVersion;

    private FBSpedContribFileContract_BR contract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Captures dialog events.
    /// </summary>
    public void postBuild()
    {
        super();

        contract = this.dataContractObject();

        dlgFiscalEstablishmentDataArea = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmFiscalEstablishmentDataArea));
        dlgFiscalEstablishmentDataArea.allowEdit(false);

        dlgFiscalEstablishmentId = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmFiscalEstablishmentId));
        dlgFiscalEstablishmentId.allowEdit(false);

        dlgMonth = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmMonth));
        dlgMonth.allowEdit(false);

        dlgYear = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmYr));
        dlgYear.allowEdit(false);

        dlgContribVersion = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmContribVersion));
        dlgContribVersion.registerOverrideMethod(methodStr(FormComboBoxControl, modified), methodStr(FBSpedContribFileUIBuilder_BR, contribVersionModified), this);

        dlgContribLateSubmission = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmContribLateSubmission));
        dlgContribLateSubmission.visible(contract.parmContribVersion() == FBContribVersion_BR::v6);

        dlgSpedFileType = this.bindInfo().getDialogField(contract, methodStr(FBSpedContribFileContract_BR, parmSpedFileType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>contribVersionModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the file type change event.
    /// </summary>
    /// <param name="_formComboBoxControl">
    /// The file type control.
    /// </param>
    /// <returns>
    /// True if the value was modified successfully; otherwise, false.
    /// </returns>
    private boolean contribVersionModified(FormComboBoxControl _formComboBoxControl)
    {
        boolean ret = _formComboBoxControl.modified();

        FormComboBoxControl fileTypeControl = dlgContribVersion.control() as FormComboBoxControl;
        boolean visibleEditable = fileTypeControl && fileTypeControl.isVisible() && fileTypeControl.selection() >= FBContribVersion_BR::v6;

        dlgContribLateSubmission.visible(visibleEditable);

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>