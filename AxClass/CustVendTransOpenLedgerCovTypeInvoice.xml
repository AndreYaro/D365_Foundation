<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendTransOpenLedgerCovTypeInvoice</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class CustVendTransOpenLedgerCovTypeInvoice extends CustVendTransOpenLedgerCovType
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateLedgerCov</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    internal void updateLedgerCov()
    {
        this.updateLedgerCovForInvoice();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLedgerCovForInvoice</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected void updateLedgerCovForInvoice(AmountCur _amountCur = contract.getCustVendTransOpen().AmountCur)
    {
        TransDate dueDate = this.getDueDate();

        date systemDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());

        CustVendTrans trans = contract.getCustVendTrans();

        LedgerCovAvgAverageDaysSelector selector = (trans.TableId == tableNum(CustTrans) ? 
            LedgerCovAvgAverageDaysSelector::CustAverageDaysFromInvoiceToPayment : 
            LedgerCovAvgAverageDaysSelector::VendAverageDaysFromInvoiceToPayment);

        TransDate paymentDate = ledgerCoverage.determinePaymentDueDateWithAverages(selector, trans.AccountNum, dueDate, trans.clearingPeriod());
        paymentDate = CustVendTransOpenLedgerCovType::calculateMinimumPaymentDate(paymentDate);

        this.updateLedgerCovForInvoiceDetail(paymentDate, _amountCur);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLedgerCovForInvoiceDetail</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected void updateLedgerCovForInvoiceDetail(TransDate _paymentDate, AmountCur _amountCur)
    {
        DimensionDefault custVendTableDefaultDimension = contract.getCustVendTableDefaultDimension();

        //Payment summary
        ledgerCoverage.updateSum(
            _paymentDate,
            contract.getSummaryPostingType(),
            LedgerDimensionFacade::getMainAccountFromLedgerDimension(contract.getCustVendTrans().summaryLedgerDimension()).RecId,
            custVendTableDefaultDimension,
            contract.getCustVendTrans().CurrencyCode,
            -_amountCur);

        //Payment clearing
        ledgerCoverage.updateSum(
            _paymentDate,
            contract.getClearingPostingType(),
            LedgerDimensionFacade::getMainAccountFromLedgerDimension(contract.getCustVendTrans().clearingLedgerDimension()).RecId,
            custVendTableDefaultDimension,
            contract.getCustVendTrans().CurrencyCode,
            _amountCur);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>