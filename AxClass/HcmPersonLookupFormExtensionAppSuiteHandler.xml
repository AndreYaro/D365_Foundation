<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmPersonLookupFormExtensionAppSuiteHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the Application Suite form extension events for the HcmPersonLookup form.
/// </summary>
class HcmPersonLookupFormExtensionAppSuiteHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>ContactPerson_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the ContactPerson datasource's OnInitialized event.
    /// </summary>
    /// <param name="_sender">The ContactPerson datasource in <c>HcmPersonLookup.ExtensionAppSuite</c> form extension.</param>
    /// <param name="_e">Args for the event.</param>
    [FormDataSourceEventHandler(formDataSourceStr(HcmPersonLookup, ContactPerson), FormDataSourceEventType::Initialized)]
    public static void ContactPerson_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        QueryBuildDataSource qbds;

        // Clear any existing Dynalinks to make sure the correct data source query is created.
        qbds = _sender.queryBuildDataSource();
        qbds.clearDynalinks();
        qbds.clearLinks();

        // Create the correct link between a contact person and a person.
        qbds.addLink(fieldnum(DirPerson, RecId), fieldnum(ContactPerson, Party));
    }

]]></Source>
			</Method>
			<Method>
				<Name>VendTable_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the VendTable datasource's OnInitialized event.
    /// </summary>
    /// <param name="_sender">The VendTable datasource in <c>HcmPersonLookup.ExtensionAppSuite</c> form extension.</param>
    /// <param name="_e">Args for the event.</param>
    [FormDataSourceEventHandler(formDataSourceStr(HcmPersonLookup, VendTable), FormDataSourceEventType::Initialized)]
    public static void VendTable_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        QueryBuildDataSource qbds;

        qbds = _sender.queryBuildDataSource();
        qbds.clearLinks();
        qbds.addLink(fieldnum(VendTable,Party),fieldnum(ContactPerson,ContactForParty));
        qbds.fields().clearFieldList();
        qbds.fields().addField(fieldnum(VendTable, AccountNum));
        qbds.fields().addField(fieldnum(VendTable,Party));
    }

]]></Source>
			</Method>
			<Method>
				<Name>CustTable_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the CustTable datasource's OnInitialized event.
    /// </summary>
    /// <param name="_sender">The CustTable datasource in <c>HcmPersonLookup.ExtensionAppSuite</c> form extension.</param>
    /// <param name="_e">Args for the event.</param>
    [FormDataSourceEventHandler(formDataSourceStr(HcmPersonLookup, CustTable), FormDataSourceEventType::Initialized)]
    public static void CustTable_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        QueryBuildDataSource qbds;

        qbds = _sender.queryBuildDataSource();
        qbds.clearLinks();
        qbds.addLink(fieldnum(CustTable,Party), fieldnum(ContactPerson,ContactForParty));
        qbds.fields().clearFieldList();
        qbds.fields().addField(fieldnum(CustTable, AccountNum));
        qbds.fields().addField(fieldnum(CustTable,Party));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentRecIdHandler_Contact</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event Handler for the Contact tab page and datasource. Returns the recid of the row the user currently has selected.
    /// </summary>
    /// <param name = "_page">The currently active tab page on the HcmPersonLookup form. The handler uses this to determine if it should handle the event.</param>
    /// <param name = "_result">The EventHandlerResult the handler will use to return the current recid.</param>
    [SubscribesTo(formstr(HcmPersonLookup), delegateStr(HcmPersonLookup, getCurrentRecId))]
    public static void getCurrentRecIdHandler_Contact(FormTabPageControl _page, EventHandlerResult _result)
    {
        // this handler is for the Contact tab and datasource
        if (_page.name() == formControlStr(HcmPersonLookup, ContactPersonTab))
        {
            var gridControl = _page.formRun().control(_page.formRun().controlId(formControlStr(HcmPersonLookup, PersonContact)));

            if (gridControl != null)
            {
                _result.result(gridControl.dataSourceObject().cursor().RecId);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>