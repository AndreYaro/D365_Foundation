<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailFiscalIntegrationPost_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailFiscalIntegrationPost_BR</c> class is used to post fiscal integration.
/// </summary>
internal abstract class RetailFiscalIntegrationPost_BR
{
    protected FiscalDocument_BR businessDocument;
    protected RetailFiscalIntegrationRegisterResponse_BR registerResponse;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>post</Name>
				<Source><![CDATA[
    /// <summary>
    /// Posts the fiscal integration for the fiscal document.
    /// </summary>
    /// <param name = "_fiscalDocument">The fiscal document.</param>
    public void post(FiscalDocument_BR _fiscalDocument)
    {
        if (!this.canBePosted())
        {
            return;
        }

        businessDocument = _fiscalDocument;

        BusinessDocumentSubmission businessDocumentSubmission;
        businessDocumentSubmission.RefTableId = businessDocument.TableId;
        businessDocumentSubmission.RefRecId = businessDocument.RecId;
        businessDocumentSubmission.StageDescription = this.getStageDescription();
        businessDocumentSubmission.UID = registerResponse.parmDocumentId();
        businessDocumentSubmission.State = registerResponse.parmDocumentState();
        businessDocumentSubmission.ParentRefRecId = this.getParentDocumentId();

        this.initKeys(businessDocumentSubmission);

        businessDocumentSubmission.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getStageDescription</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the stage description.
    /// </summary>
    /// <returns>The stage description.</returns>
    protected abstract Description getStageDescription()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getParentDocumentId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the parent document id.
    /// </summary>
    /// <returns>The parent document id.</returns>
    protected RefRecId getParentDocumentId()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canBePosted</Name>
				<Source><![CDATA[
    private boolean canBePosted()
    {
        return registerResponse && registerResponse.parmAccessKey() != '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>initKeys</Name>
				<Source><![CDATA[
    private void initKeys(BusinessDocumentSubmission _businessDocumentSubmission)
    {
        Map identificationKeys = this.getIdentificationKeys();

        _businessDocumentSubmission.KeyData = identificationKeys.pack();

        int idx = 1;
        MapEnumerator me = identificationKeys.getEnumerator();
        while (me.moveNext())
        {
            _businessDocumentSubmission.KeyDataValues[idx] = me.currentValue();
            idx++;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getIdentificationKeys</Name>
				<Source><![CDATA[
    private Map getIdentificationKeys()
    {
        Map identificationKeys = new Map(Types::String, Types::String);
        identificationKeys.insert(fieldPName(FiscalDocument_BR, FiscalDocumentSeries), businessDocument.FiscalDocumentSeries);
        identificationKeys.insert(fieldPName(FiscalDocument_BR, FiscalDocumentNumber), businessDocument.FiscalDocumentNumber);

        return identificationKeys;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>