<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBSpedRecordDeterminer_Fiscal_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>FBSpedRecordDeterminer_Fiscal_BR</c> class determines the type of record for a fiscal document related to sped fiscal.
/// </summary>
class FBSpedRecordDeterminer_Fiscal_BR extends FBSpedRecordDeterminer_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>determineRecord</Name>
				<Source><![CDATA[
/// <summary>
    ///     Determines the type of record the fiscal document should be exported to in the Sped Fiscal file.
    /// </summary>
    /// <returns>
    ///     The <c>FBSpedFileRecordIdentification_BR</c> representing the Sped Fiscal record where the fiscal document should be exported to.
    /// </returns>
    public FBSpedFileRecordIdentification_BR determineRecord()
    {
        const container modelsForRecordC100 = [
            FiscalBooksConstants_BR::FiscalDocModel_01,
            FiscalBooksConstants_BR::FiscalDocModel_1B,
            FiscalBooksConstants_BR::FiscalDocModel_04,
            FiscalBooksConstants_BR::FiscalDocModel_55,
            FiscalBooksConstants_BR::FiscalDocModel_65];
        const container modelsForRecordC500 = [
            FiscalBooksConstants_BR::FiscalDocModel_06,
            FiscalBooksConstants_BR::FiscalDocModel_28,
            FiscalBooksConstants_BR::FiscalDocModel_29,
            FiscalBooksConstants_BR::FiscalDocModel_66];
        const container modelsForRecordD100 = [
            FiscalBooksConstants_BR::FiscalDocModel_07,
            FiscalBooksConstants_BR::FiscalDocModel_08,
            FiscalBooksConstants_BR::FiscalDocModel_8B,
            FiscalBooksConstants_BR::FiscalDocModel_09,
            FiscalBooksConstants_BR::FiscalDocModel_10,
            FiscalBooksConstants_BR::FiscalDocModel_11,
            FiscalBooksConstants_BR::FiscalDocModel_26,
            FiscalBooksConstants_BR::FiscalDocModel_27,
            FiscalBooksConstants_BR::FiscalDocModel_57,
            FiscalBooksConstants_BR::FiscalDocModel_67];
        const container modelsForRecordD500 = [
            FiscalBooksConstants_BR::FiscalDocModel_21,
            FiscalBooksConstants_BR::FiscalDocModel_22];

        FBSpedFileRecordIdentification_BR record;
        FiscalDocumentModel_BR model = fiscalDocument.Model;

        if (fbBookingPeriodFiscalDocument.HasIcms || fbBookingPeriodFiscalDocument.HasIpi || this.parmHasIcmsIpi())
        {
            if (conFind(modelsForRecordC100, model))
            {
                record = FiscalBooksConstants_BR::SpedRecord_C100;
            }
            else if (model == FiscalBooksConstants_BR::FiscalDocModel_02 && fiscalDocument.Direction == FiscalDocDirection_BR::Outgoing)
            {
                if (fiscalDocument.Status != FiscalDocumentStatus_BR::Cancelled)
                {
                    record = FiscalBooksConstants_BR::SpedRecord_C350;
                }
            }
            else if (conFind(modelsForRecordC500, model))
            {
                record = FiscalBooksConstants_BR::SpedRecord_C500;
            }
            else if (conFind(modelsForRecordD100, model))
            {
                record = FiscalBooksConstants_BR::SpedRecord_D100;
            }
            else if  (conFind(modelsForRecordD500, model))
            {
                record = FiscalBooksConstants_BR::SpedRecord_D500;
            }
            else if (model == FiscalBooksConstants_BR::FiscalDocModel_59)
            {
                record = FiscalBooksConstants_BR::SpedRecord_C800;
            }
        }

        return record;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>