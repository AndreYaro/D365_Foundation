<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LeaveTeamsAppDataverseUtility</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Xrm.Sdk.Query;
using System;
using System.Linq;
using System.Web;

internal static class LeaveTeamsAppDataverseUtility
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isLeaveAppInstalled</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static boolean isLeaveAppInstalled()
    {
        boolean doesSolutionExist = false;

        try
        {
            doesSolutionExist = SysDataverseUtility::CheckSolutionExistence(SysDataverseUtility::GetOrganizationServiceForCurrentUser(LeaveTeamsAppConstants::OrgServiceName), LeaveTeamsAppConstants::AnchorSolutionName);
        }
        catch
        {
            SysInstrumentationLoggerFactory::CreateLogger(classStr(LeaveTeamsAppDataverseUtility)).logInformation('User does not have permissions to query solutions');
        }

        return doesSolutionExist;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLeaveTeamsAppId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static str getLeaveTeamsAppId()
    {
        try
        {
            var query = new QueryExpression('appmodule');
            query.TopCount = 1;
            query.ColumnSet = new ColumnSet('appmoduleid');

            var criteria = query.Criteria;
            criteria.AddCondition('uniquename', ConditionOperator::Equal, LeaveTeamsAppConstants::ModelDrivenAppName);

            var result = SysDataverseUtility::GetOrganizationServiceForCurrentUser(LeaveTeamsAppConstants::OrgServiceName).RetrieveMultiple(query).Entities;

            if (result.Count == 1)
            {
                return result.get_Item(0).get_Item('appmoduleid');
            }
        }
        catch
        {
            SysInstrumentationLoggerFactory::CreateLogger(classStr(LeaveTeamsAppDataverseUtility)).logInformation('User does not have permissions to query apps');
        }

        return '';
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>