<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WrkCtrWorkingTimes</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>WrkCtrWorkingTimes</c> class is used to fetch working times for a resource and a specified
///    interval of time.
/// </summary>
class WrkCtrWorkingTimes
{
    date        startDate;
    TimeOfDay   startTime;
    date        endDate;
    TimeOfDay   endTime;
    boolean     forward;
    int         workingTimesCount;
    // workingTimes works as the return value for processCalendarTimeInterval
    container   workingTimes;

    // workingTimesCumulative is a 2-level stack of calculated working times. The contents of this structure is cached.
    [SysObsolete('This variable is obsolete, and is no longer actively used.', false, 30\06\2020)]
    container   workingTimesCumulative; // No longer used, keeping for backwards compatibility
    [SysObsolete('This variable is obsolete, and is no longer actively used.', false, 30\06\2020)]
    int         slotsToReturn; // No longer used, keeping for backwards compatibility

    #TimeConstants
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addWorkCalendarLineToResult</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a <c>WorkCalendarDateLine</c> record to the result set.
    /// </summary>
    /// <param name="_workCalendarDateLine">
    /// A <c>WorkCalendarDateLine</c> record that contains the working time interval to add to the results.
    /// </param>
    /// <param name="_limitStartDate">
    /// The date that specifies the start date that should be used to clip the working time.
    /// </param>
    /// <param name="_limitStartTime">
    /// A <c>TimeOfDay</c> value that specifies the start time that should be used to clip the working time.
    /// </param>
    /// <param name="_limitEndDate">
    /// A date that specifies the end date that should be used to clip the working time.
    /// </param>
    /// <param name="_limitEndTime">
    /// A <c>TimeOfDay</c> value that specifies the end time that should be used to clip the working time.
    /// </param>
    /// <param name="_effectivityPct">
    /// A percentage value that is used to compute the effectiveness of the working times.
    /// </param>
    /// <param name="_properties">
    /// A list of properties that should be added to the calendar line property.
    /// </param>
    protected void addWorkCalendarLineToResult(WorkCalendarDateLine     _workCalendarDateLine,
                                               date                     _limitStartDate,
                                               TimeOfDay                _limitStartTime,
                                               date                     _limitEndDate,
                                               TimeOfDay                _limitEndTime,
                                               EffectivityPct           _effectivityPct = 100,
                                               container                _properties = conNull())
    {
        utcdatetime     resultStartTime;
        utcdatetime     resultEndTime;
        container       dateResult;

        int             listenerLength;
        int             listenerIterator;
        container       listener;

        // align start time
        resultStartTime = DateTimeUtil::newDateTime(_workCalendarDateLine.TransDate, _workCalendarDateLine.FromTime);
        if (_workCalendarDateLine.TransDate     <   _limitStartDate   ||
            (_workCalendarDateLine.TransDate    ==  _limitStartDate   &&
             _workCalendarDateLine.FromTime     <   _limitStartTime))
        {
            resultStartTime = DateTimeUtil::newDateTime(_limitStartDate, _limitStartTime);
        }

        // align end time
        resultEndTime = DateTimeUtil::newDateTime(_workCalendarDateLine.TransDate, _workCalendarDateLine.ToTime);
        if (_workCalendarDateLine.TransDate     >   _limitEndDate   ||
            (_workCalendarDateLine.TransDate    ==  _limitEndDate   &&
             _workCalendarDateLine.ToTime       >   _limitEndTime))
        {
            resultEndTime = DateTimeUtil::newDateTime(_limitEndDate, _limitEndTime);
        }

        // only add if there is a time difference between start and end
        if (resultStartTime < resultEndTime)
        {
            // add the working time to the results
            dateResult   = [[resultStartTime,
                resultEndTime,
                (_workCalendarDateLine.Effectivity / 100) * (_effectivityPct / 100),
                _workCalendarDateLine.PropertyId ?
                    (_properties + [WrkCtrSchedulingProperty::propertySchedulingProperty(_workCalendarDateLine.PropertyId), 0.0]) :
                    _properties]];

            workingTimes        += dateResult;
            listenerLength      =  conlen(workingTimesCumulative);
            for (listenerIterator = 1; listenerIterator <= listenerLength; listenerIterator++)
            {
                listener = conPeek(workingTimesCumulative, listenerIterator);
                listener += dateResult;
                workingTimesCumulative = conPoke(workingTimesCumulative, listenerIterator, listener);
            }

            slotsToReturn ++;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkingTimes</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the working times for the resource specified by the <c>wrkCtrId</c> field and the interval
    ///    that was set up when the object was created.
    /// </summary>
    /// <returns>
    ///    A container that stores the working time records. Each element in the container is another
    ///    container that holds the <c>startDateTime</c>, <c>endDateTime</c>, <c>effectivity</c>, and
    ///    <c>propertyId</c> values.
    /// </returns>
    /// <remarks>
    ///    This class always returns an empty container. Override in derived classes.
    /// </remarks>
    public container getWorkingTimes()
    {
        container result;

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEndDate</Name>
				<Source><![CDATA[
    protected date parmEndDate(date _endDate = endDate)
    {
        endDate = _endDate;
        return endDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEndTime</Name>
				<Source><![CDATA[
    protected TimeOfDay parmEndTime(TimeOfDay _endTime = endTime)
    {
        endTime = _endTime;
        return endTime;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmForward</Name>
				<Source><![CDATA[
    protected boolean parmForward(boolean _forward = forward)
    {
        forward = _forward;
        return forward;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmStartDate</Name>
				<Source><![CDATA[
    protected date parmStartDate(date _startDate = startDate)
    {
        startDate = _startDate;
        return startDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmStartTime</Name>
				<Source><![CDATA[
    protected TimeOfDay parmStartTime(TimeOfDay _startTime = startTime)
    {
        startTime = _startTime;
        return startTime;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkingTimesCount</Name>
				<Source><![CDATA[
    protected int parmWorkingTimesCount(int _workingTimesCount = workingTimesCount)
    {
        workingTimesCount = _workingTimesCount;
        return workingTimesCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processCalendarTimeInterval</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes working times for a calendar by considering the base calendar information.
    /// </summary>
    /// <param name="_calendarId">
    ///    A <c>CalendarId</c> object that specifies the calendar for which to fetch the working times.
    /// </param>
    /// <param name="_startDate">
    ///    The Date value that specifies the start date of the interval for which to fetch working times.
    /// </param>
    /// <param name="_startTime">
    ///    A <c>TimeOfDay</c> object that specifies the start time of the interval for which to fetch working
    ///    times.
    /// </param>
    /// <param name="_endDate">
    ///    The date value that specifies the end date of the interval for which to fetch working times.
    /// </param>
    /// <param name="_endTime">
    ///    A <c>TimeOfDay</c> object that specifies the end time of the interval for which to fetch working
    ///    times.
    /// </param>
    /// <param name="_effectivityPct">
    ///    A percentage value that will be used to compute the effectiveness of the resulting working times.
    ///    This value is multiplied with the efficiency of each working time.
    /// </param>
    /// <param name="_properties">
    ///    A list of properties that should be added to the calendar line property.
    /// </param>
    /// <returns>
    ///    true if fetching working times should continue; otherwise, false.
    /// </returns>
    protected boolean processCalendarTimeInterval(CalendarId        _calendarId,
                                                  date              _startDate,
                                                  TimeOfDay         _startTime,
                                                  date              _endDate,
                                                  TimeOfDay         _endTime,
                                                  EffectivityPct    _effectivityPct = 100,
                                                  Map               _properties = new Map(Types::String, Types::Real))
    {
        utcdatetime utcStart = this.dateAndTimeToUtcdatetimeWithClipping(_startDate, _startTime);
        utcdatetime utcEnd   = this.dateAndTimeToUtcdatetimeWithClipping(_endDate,   _endTime);
        workingTimes = WrkCtrWorkingTimesCache::getValues(_calendarId, utcStart, utcEnd, _effectivityPct, _properties, workingTimesCount, forward);
        return conLen(workingTimes) < workingTimesCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dateAndTimeToUtcdatetimeWithClipping</Name>
				<Source><![CDATA[
    private utcdatetime dateAndTimeToUtcdatetimeWithClipping(date _date, TimeOfDay _time)
    {
        if (_date == dateMax() && _time >= #SecondsPerDay)
        {
            return DateTimeUtil::maxValue();
        }
        
        if (_date == dateNull() && _time <= 0)
        {
            return DateTimeUtil::minValue();
        }

        return DateTimeUtil::newDateTime(_date, _time);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processTimeInterval</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes working times for a calendar without considering any base calendar.
    /// </summary>
    /// <param name="_calendarId">
    ///    A <c>CalendarId</c> object that specifies the calendar for which to fetch the working times.
    /// </param>
    /// <param name="_startDate">
    ///    A date value that specifies the start date of the interval for which to fetch working times.
    /// </param>
    /// <param name="_startTime">
    ///    A <c>TimeOfDay</c> value that specifies the start time of the interval for which to fetch working
    ///    times.
    /// </param>
    /// <param name="_endDate">
    ///    A date value that specifies the end date of the interval for which to fetch working times.
    /// </param>
    /// <param name="_endTime">
    ///    A <c>TimeOfDay</c> value that specifies the end time of the interval for which to fetch working
    ///    times.
    /// </param>
    /// <param name="_effectivityPct">
    ///    A percentage value that will be used in computing the effectiveness of the resulting working times.
    ///    This value is multiplied with the efficiency of each working time.
    /// </param>
    /// <param name="_properties">
    ///    A list of properties that should be added to the calendar line property.
    /// </param>
    /// <returns>
    ///    true if fetching working times should continue; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    The method just fetches the working times from the <c>WorkCalendarDateLine</c> table.
    /// </remarks>
    protected boolean processTimeInterval(CalendarId        _calendarId,
                                          date              _startDate,
                                          TimeOfDay         _startTime,
                                          date              _endDate,
                                          TimeOfDay         _endTime,
                                          EffectivityPct    _effectivityPct = 100,
                                          Map               _properties = new Map(Types::String, Types::Real))
    {
        WorkCalendarDateLine    workCalendarDateLine;

        Query                   query;
        QueryRun                queryRun;
        QueryBuildDataSource    dsWorkCalendarDateLine;
        container               propertiesContainer;

        if (_startDate == _endDate &&
            _startTime == _endTime)
        {
            return true;
        }

        query = new Query();
        dsWorkCalendarDateLine = query.addDataSource(tableNum(WorkCalendarDateLine));
        dsWorkCalendarDateLine.addRange(fieldNum(WorkCalendarDateLine, CalendarId)).value(queryValue(_calendarId));
        dsWorkCalendarDateLine.addRange(fieldNum(WorkCalendarDateLine, TransDate)).value(SysQuery::range(_startDate, _endDate));

        dsWorkCalendarDateLine.addOrderByField(fieldNum(WorkCalendarDateLine, TransDate), forward ? SortOrder::Ascending : SortOrder::Descending);

        propertiesContainer = WrkCtrWorkingTimes::propertiesMap2Container(_properties);

        queryRun = new QueryRun(query);
        while (queryRun.next())
        {
            workCalendarDateLine    = queryRun.get(tableNum(WorkCalendarDateLine));

            if ((workCalendarDateLine.TransDate == _startDate   && workCalendarDateLine.ToTime <= _startTime) ||
                (workCalendarDateLine.TransDate == _endDate     && workCalendarDateLine.FromTime >= _endTime))
            {
                continue;
            }

            this.addWorkCalendarLineToResult(workCalendarDateLine,
                                             _startDate,
                                             _startTime,
                                             _endDate,
                                             _endTime,
                                             _effectivityPct,
                                             propertiesContainer);
            if (slotsToReturn >= workingTimesCount && workingTimesCount !=0 )
            {
                return false;
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPropertiesFromContainer</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds a property string to the container if it does not already exists.
    /// </summary>
    /// <param name="_mapProperties">
    ///    A container with the properties to which the new property has to be added.
    /// </param>
    /// <param name="_propertiesToAdd">
    ///    A real value that defines the property grade.
    /// </param>
    protected static void addPropertiesFromContainer(Map        _mapProperties,
                                                     container  _propertiesToAdd)
    {
        int             i;

        for (i = 0; i < conLen(_propertiesToAdd) / 2; i++)
        {
            WrkCtrWorkingTimes::addProperty(_mapProperties,
                                            conPeek(_propertiesToAdd, (i * 2) + 1),
                                            conPeek(_propertiesToAdd, (i * 2) + 2));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addProperty</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds a property string to the container if it does not already exists.
    /// </summary>
    /// <param name="_mapProperties">
    ///    A <c>Map</c> object that has the properties to which the new property has to be added.
    /// </param>
    /// <param name="_property">
    ///    A string defining the property ID.
    /// </param>
    /// <param name="_propertyGrade">
    ///    A real value that defines the property grade.
    /// </param>
    protected static void addProperty(Map  _mapProperties,
                                      str  _property,
                                      real _propertyGrade = 0.0)
    {
        if (_mapProperties.exists(_property))
        {
            if (_mapProperties.lookup(_property) < _propertyGrade)
            {
                _mapProperties.insert(_property, _propertyGrade);
            }
        }
        else
        {
            _mapProperties.insert(_property, _propertyGrade);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WrkCtrWorkingTimes construct()
    {
        return new WrkCtrWorkingTimes();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getGlobalObjectCacheScope</Name>
				<Source><![CDATA[
    private static GlobalObjectCacheScope getGlobalObjectCacheScope()
    {
        return classStr(WrkCtrWorkingTimes) + getcurrentpartition() + curext();
    }

]]></Source>
			</Method>
			<Method>
				<Name>invalidateGlobalCacheScope</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Clears the global working time cache.
    /// </summary>
    public static void invalidateGlobalCacheScope()
    {
        SysGlobalObjectCache    cache = new SysGlobalObjectCache();

        cache.clear(WrkCtrWorkingTimes::getGlobalObjectCacheScope());
    }

]]></Source>
			</Method>
			<Method>
				<Name>propertiesMap2Container</Name>
				<Source><![CDATA[
    /// <summary>
    /// Converts a map of properties to a container of properties.
    /// </summary>
    /// <param name="_properties">
    /// A map of properties.
    /// </param>
    /// <returns>
    /// A container of properties.
    /// </returns>
    protected static container propertiesMap2Container(Map _properties)
    {
        MapEnumerator   mapEnumerator;
        container       result;

        mapEnumerator = _properties.getEnumerator();
        while (mapEnumerator.moveNext())
        {
            result += [mapEnumerator.currentKey(), mapEnumerator.currentValue()];
        }

        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>