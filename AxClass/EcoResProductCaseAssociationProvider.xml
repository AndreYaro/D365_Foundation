<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResProductCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Product'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class EcoResProductCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        EcoResProduct ecoResProduct;
        select DataAreaId from ecoResProduct where ecoResProduct.RecId == _parentRecId;

        return ecoResProduct.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        EcoResProduct ecoResProduct;
        select DisplayProductNumber from ecoResProduct where ecoResProduct.RecId == _parentRecId;

        return ecoResProduct.DisplayProductNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        EcoResProduct ecoResProduct;
        select RecId from ecoResProduct where ecoResProduct.DisplayProductNumber == _naturalKey;

        return ecoResProduct.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        EcoResProduct ecoResProduct;
        select * from ecoResProduct where ecoResProduct.RecId == _parentRecId;

        return ecoResProduct.productName();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        EcoResProduct ecoResProduct;
        
        //We need this code in order not to lose the localization data in the opened form.
        EcoResProduct ecoResProductCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from ecoResProductCrossCompany
            where ecoResProductCrossCompany.RecId == _association.RefRecId;

        if (ecoResProductCrossCompany.Recid)
        {
            changecompany (ecoResProductCrossCompany.DataAreaId)
            {
                select firstonly ecoResProduct where ecoResProduct.RecId == _association.RefRecId;
            }
        }

        _args.record(ecoResProduct);
        _args.copyCallerQuery(CopyCallerQuery::Yes);
        return menuitemdisplaystr(EcoResProductDetails);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(EcoResProduct), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(EcoResProduct));

        //Building range
        Range productRange = queryValue(DictTable::getRelationTypeFromTableName(tableStr(EcoResProductMaster)));
        productRange = queryRangeConcat(productRange, DictTable::getRelationTypeFromTableName(tableStr(EcoResDistinctProduct)));
        QueryBuildRange queryBuildRange = oqbds.addRange(fieldNum(EcoResProduct,InstanceRelationType));
        queryBuildRange.value(productRange);
        queryBuildRange.status(RangeStatus::Hidden);

        oSysTableLookup.addLookupfield(fieldnum(EcoResProduct, DisplayProductNumber));
        oSysTableLookup.addLookupMethod(tablemethodstr(EcoResProduct, displayProductName));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(EcoResProduct)
                || _refTableId == tablenum(EcoResDistinctProduct)
                || _refTableId == tablenum(EcoResProductMaster))
        {
            _eventArgs.parmEntityType(CaseEntityType::Product);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>