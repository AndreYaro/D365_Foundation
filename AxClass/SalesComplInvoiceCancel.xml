<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesComplInvoiceCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class creates a copy of a sales complementary invoice and its lines with the amounts reversed.
/// </summary>
class SalesComplInvoiceCancel
{
    SalesComplementaryInvoiceHeader originalInvoice;
    ReasonTableRefRecId             reasonTableRef;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the copy of the sales complementary invoice and its lines.
    /// </summary>
    /// <returns>
    /// The RecID of the new invoice.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public RecId copy()
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR00052, funcName());

        SalesComplementaryInvoiceHeader newInvoice;
        SalesComplementaryInvoiceLine   newInvoiceLine, oldInvoiceLine;

        ttsBegin;

        originalInvoice.selectForUpdate(true);
        originalInvoice.Status = ComplementaryInvoiceStatus::CancelInProgress;
        originalInvoice.update();

        newInvoice.data(originalInvoice);
        newInvoice.Status = ComplementaryInvoiceStatus::Open;
        newInvoice.CanceledInvoice = originalInvoice.RecId;
        newInvoice.SourceDocumentHeader = 0;
        newInvoice.RecId = 0;
        newInvoice.DocumentDate = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone());
        newInvoice.LedgerVoucher = '';
        newInvoice.FiscalDocumentNumber = '';
        newInvoice.ReasonTableRef_BR = reasonTableRef;
        newInvoice.insert();

        while select oldInvoiceLine
            where oldInvoiceLine.SalesComplementaryInvoiceHeader == originalInvoice.RecId
        {
            newInvoiceLine.data(oldInvoiceLine);
            newInvoiceLine.Amount = newInvoiceLine.Amount * -1;
            newInvoiceLine.CorrectedAmount += newInvoiceLine.Amount;
            newInvoiceLine.AdditionalICMSAmount = newInvoiceLine.AdditionalICMSAmount * -1;
            newInvoiceLine.AdditionalIPIAmount = newInvoiceLine.AdditionalIPIAmount * -1;
            newInvoiceLine.SalesComplementaryInvoiceHeader = newInvoice.RecId;
            newInvoiceLine.SourceDocumentLine = 0;
            newInvoiceLine.RecId = 0;
            newInvoiceLine.insert();
        }

        FiscalDocument_BR originalFiscalDocument = FiscalDocument_BR::findByRef(originalInvoice.TableId, originalInvoice.RecId);
        FiscalDocumentTextUpdate_BR::newFromRecord(newInvoice).copy(originalFiscalDocument);

        ttsCommit;

        return newInvoice.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>SalesComplInvoiceCancel</c>
    /// </summary>
    /// <param name="_originalInvoice">
    /// Sales complementary invoice to be copied.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void new(SalesComplementaryInvoiceHeader _originalInvoice)
    {
        Debug::assert(_originalInvoice.RecId);
        originalInvoice = _originalInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReasonTableRef</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public ReasonTableRefRecId parmReasonTableRef(ReasonTableRefRecId _reasonTableRef = reasonTableRef)
    {
        reasonTableRef = _reasonTableRef;
        return reasonTableRef;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        SalesComplementaryInvoiceHeader invoiceToBeCanceled = _args.record();
        SalesComplInvoiceCancel invoiceCancel = new SalesComplInvoiceCancel(invoiceToBeCanceled);
        invoiceCancel.copy();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>