<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjInvoiceItem_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(ProjInvoiceItem))]
internal final class ProjInvoiceItem_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initInvoiceFromProposale</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retains Intrastat transaction code from the original transaction ProjInvoiceItem or ProjItemTrans entities
    /// in the case of project invoice created from an adjusted transaction.
    /// </summary>
    /// <param name = "_projProposalItem">
    /// An invoice proposal line from which to initialize the invoice line.
    /// </param>
    /// <returns>
    /// The new invoice line.
    /// </returns>
    public static ProjInvoiceItem initInvoiceFromProposale(ProjProposalItem _projProposalItem)
    {
        ProjInvoiceItem projInvoiceItem = next initInvoiceFromProposale(_projProposalItem);

        container countryRegionCodes = newÂ DictTable(tableNum(Intrastat)).getCountryRegionCodes();

        if (!projInvoiceItem.TransactionCode &&
           (!countryRegionCodes || SysCountryRegionCode::isLegalEntityInCountryRegion(countryRegionCodes)))
        {
            ProjItemTrans projItemTrans = ProjItemTrans::find(projInvoiceItem.ProjTransId);
            if (projItemTrans.AdjRefTransId)
            {
                ProjInvoiceItem origProjInvoiceItem;

                select firstonly TransactionCode from origProjInvoiceItem
                    where origProjInvoiceItem.ProjTransId == projItemTrans.AdjRefTransId;
                
                projInvoiceItem.TransactionCode = origProjInvoiceItem.TransactionCode;

                if (!projInvoiceItem.TransactionCode)
                {
                    ProjItemTrans origProjItemTrans = ProjItemTrans::find(projItemTrans.AdjRefTransId);
                    projInvoiceItem.TransactionCode = SalesLine::findInventTransId(origProjItemTrans.InventTransId).TransactionCode;
                }
            }
        }

        return projInvoiceItem;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>