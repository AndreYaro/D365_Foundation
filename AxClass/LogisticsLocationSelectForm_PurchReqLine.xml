<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_PurchReqLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
class LogisticsLocationSelectForm_PurchReqLine extends LogisticsLocationSelectForm
{
    PurchReqLine purchReqLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates the entities from which addresses can be selected.
    /// </summary>
    /// <returns>
    ///    A set of <c>LogisticsLocationSelectSourceTypes</c> enumeration types from which addresses can be
    ///    selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret;

        ret = super();

        ret.add(LogisticsLocationSelectSourceType::Customer);
        ret.add(LogisticsLocationSelectSourceType::Vendor);
        ret.add(LogisticsLocationSelectSourceType::PurchReqLineRequsterHCMWorker);
        ret.add(LogisticsLocationSelectSourceType::Company);
        ret.add(LogisticsLocationSelectSourceType::Site);
        ret.add(LogisticsLocationSelectSourceType::Warehouse);
        ret.add(LogisticsLocationSelectSourceType::PurchReqLine);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling purchase requisition.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        TaxGroup taxGroup;
        purchReqLine.DeliveryPostalAddress = _addressRecId;
        purchReqLine.AddressRefRecId = _transferFromTable.RecId;
        purchReqLine.AddressRefTableId = _transferFromTable.TableId;
        purchReqLine.setDeliveryName();

        taxGroup = purchReqLine.getTaxGroup(VendTable::find(purchReqLine.VendAccount));
        if (taxGroup)
        {
            purchReqLine.TaxGroup               = taxGroup;
            purchReqLine.TaxGroupDataArea       = appl.company().dataArea(tableNum(TaxGroupHeading));            
        }

        // <GBR>
        if (BrazilParameters::isEnabled())
        {
            purchReqLine.cfopTable_BR = this.getDefaultCFOP_BR(purchReqLine);
        }
        // </GBR>
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the source that is selected first for the addresses on the form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticsLocationSelectSourceType</c> enumeration type that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (purchReqLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchReqLine.AddressRefTableId,
                purchReqLine.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    purchReqLine.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> table that is selected.
    /// </summary>
    /// <returns>
    /// The <c>LogisticsPostalAddress</c> record ID.
    /// </returns>
    public RecId getLogisticsPostalAddressRecId()
    {
        return purchReqLine.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of a target form that will be selected upon opening.
    /// </summary>
    /// <returns>
    /// The record that will be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (purchReqLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchReqLine.AddressRefTableId,
                purchReqLine.AddressRefRecId
            );
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(PurchReqLine _purchReqLine)
    {
        purchReqLine = _purchReqLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_PurchReqLine</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A <c>LogisticsLocationSelectForm_PurchReqLine</c> record to use to initialize the object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_PurchReqLine</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_PurchReqLine construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_PurchReqLine(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>