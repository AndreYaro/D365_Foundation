<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TradeCDSQuotationCalculateTotalsMessageType</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TradeCDSQuotationCalculateTotalsMessageType</c> class represents business logic for calculating totals for sales quotations using message processor.
/// </summary>
[SysMessageTypeFactoryAttribute(SysMessageType::SalesD365SalesQuotationCalculateTotals)]
internal class TradeCDSQuotationCalculateTotalsMessageType extends TradeCDSSalesPriceUpdateMessageTypeBase
{
    #OCCRetryCount

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>processMessageStateTransition</Name>
				<Source><![CDATA[
    internal void processMessageStateTransition(SysMessage _message, SysMessageStateTransition _messageStateTransition)
    {
        if (_messageStateTransition.isEqual(SysMessageStateTransition::newFromStates(SysMessageState::Queued, SysMessageState::Processed)))
        {
            this.process(this.getContract(_message));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>process</Name>
				<Source><![CDATA[
    private void process(TradeCDSQuotationCalculateTotalsDataContract _contract)
    {
        try
        {
            ttsbegin;
            SalesQuotationTotalsCalculate::calculateTotalsForQuotation(_contract.parmSalesQuotationId());
            ttscommit;
        }
        catch (Exception::UpdateConflict)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw Exception::UpdateConflictNotRecovered;
            }
            else
            {
                retry;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getContract</Name>
				<Source><![CDATA[
    private TradeCDSQuotationCalculateTotalsDataContract getContract(SysMessage _message)
    {
        TradeCDSQuotationCalculateTotalsDataContract contract = FormJsonSerializer::deserializeObject(classNum(TradeCDSQuotationCalculateTotalsDataContract), _message.Content);

        return contract;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>