<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdOrderCreateOpen</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Creates a production or batch order, and opens the card form for the created record.
/// </summary>
public class ProdOrderCreateOpen
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        Args args = new Args();
        args.caller(_args.caller());

        if (_args.parmEnumType() == enumNum(PmfOrderType))
        {
            if (_args.parmEnum() == PmfOrderType::ProdOrder)
            {
                ProdOrderCreateOpen::createAndOpenProdTable(
                    new MenuFunction(menuItemDisplayStr(ProdTableNew),MenuItemType::Display),
                    args);
                return;
            }
            else if (_args.parmEnum() == PmfOrderType::ProcessBatch)
            {
                ProdOrderCreateOpen::createAndOpenProdTable(
                    new MenuFunction(menuItemDisplayStr(PmfBatchOrderNew),MenuItemType::Display),
                    args);
                return;
            }
        }

        throw error(Error::wrongUseOfFunction(funcName()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>createAndOpenProdTable</Name>
				<Source><![CDATA[
    private static void createAndOpenProdTable(MenuFunction _menuFunctionToUse, Args _args)
    {
        FormRun formProduction = _menuFunctionToUse.create(_args);

        formProduction.run();
        formProduction.wait();

        if (formProduction.closedOK())
        {
            Object createForm = formProduction;
            ProdId orderId = createForm.createdOrderId();

            ProdTable prodTable = ProdTable::find(orderId);

            Args orderArgs = new Args();
            orderArgs.record(prodTable);
            orderArgs.caller(_args.caller());

            new MenuFunction(menuItemDisplayStr(ProdTable), MenuItemType::Display).run(orderArgs);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>