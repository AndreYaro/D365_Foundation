<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSLocationDirectiveStrategyLocationAgingFIFO</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSLocationDirectiveStrategyLocationAgingFIFO</c> class handles validations for FIFO location directive strategies.
/// </summary>
[WhsLocationDirectiveStrategyFactory(WhsLocDirStrategy::LocationAgingFIFO)]
final public class WHSLocationDirectiveStrategyLocationAgingFIFO extends WhsLocationDirectiveStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean validate(
        WHSLocDirTable  _locDirTable,
        WHSLocDirLine   _locDirLine,
        WHSLocDirAction _locDirAction)
    {
        boolean ret = true;

        if (this.isInvalidWorkTransType(_locDirTable.WorkTransType))
        {
            ret = checkFailed(strFmt("@WAX5583", _locDirAction.LocDirStrategy, _locDirTable.WorkTransType));
        }

        if (this.isInvalidWorkType(_locDirTable.WorkType))
        {
            ret = checkFailed(strFmt("@WAX4602", _locDirAction.LocDirStrategy, _locDirTable.WorkType));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInvalidWorkTransType</Name>
				<Source><![CDATA[
    private boolean isInvalidWorkTransType(WHSWorkTransType _workTransType)
    {
        return !WhsWorkTransactionType::singletonFromTypeOrDefault(_workTransType).isItemRelatedPicking();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInvalidWorkType</Name>
				<Source><![CDATA[
    private boolean isInvalidWorkType(WHSWorkType _workType)
    {
        return _workType != WHSWorkType::Pick;
    }

]]></Source>
			</Method>
			<Method>
				<Name>performStrategy</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean performStrategy(WhsLocationDirective _locationDirective, WhsLocationDirectiveStrategyParameters _parameters)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyPickLocDirActionQuery</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean modifyPickLocDirActionQuery(WhsLocationDirectiveActionQuery _actionQuery, Query _query)
    {
        QueryBuildDataSource qbdsLocAggrStatus = _query.dataSourceTable(tableNum(WMSLocation)).addDataSource(tableNum(WMSLocationAggregateStatus));
        qbdsLocAggrStatus.fetchMode(QueryFetchMode::One2One);
        qbdsLocAggrStatus.joinMode(JoinMode::OuterJoin);
        qbdsLocAggrStatus.relations(true);
        
        this.addFirstOrderByField(_query,
            tableNum(WMSLocationAggregateStatus),
            fieldNum(WMSLocationAggregateStatus, AgingDate),
            SortOrder::Ascending);

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>