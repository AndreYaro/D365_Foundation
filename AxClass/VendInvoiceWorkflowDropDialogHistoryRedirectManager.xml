<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceWorkflowDropDialogHistoryRedirectManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Manages redirecting of workflow history buttons on vendor invoice workflow drop dialog forms.
/// </summary>
internal static class VendInvoiceWorkflowDropDialogHistoryRedirectManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>shouldWorkflowHistoryRedirectToHistoryTimeline</Name>
				<Source><![CDATA[
    internal static boolean shouldWorkflowHistoryRedirectToHistoryTimeline(Common _workflowDocument)
    {
        MenuFunction workflowHistoryTimelineMenuFunction =
            VendInvoiceWorkflowDropDialogHistoryRedirectManager::createMenuFunctionForInvoiceHistoryTimeline();

        return VendAutomateVendorInvoicesFeature::isEnabled()
            && _workflowDocument
            && _workflowDocument is VendInvoiceInfoTable
            && workflowHistoryTimelineMenuFunction
            && workflowHistoryTimelineMenuFunction.checkAccessRights();
    }

]]></Source>
			</Method>
			<Method>
				<Name>redirectButtonToWorkflowHistoryTimeline</Name>
				<Source><![CDATA[
    internal static void redirectButtonToWorkflowHistoryTimeline(FormFunctionButtonControl _formButton)
    {
        if (_formButton)
        {
            MenuFunction workflowHistoryTimelineMenuFunction = 
                VendInvoiceWorkflowDropDialogHistoryRedirectManager::createMenuFunctionForInvoiceHistoryTimeline();

            VendInvoiceWorkflowDropDialogHistoryRedirectManager::redirectWorkflowHistoryButtonToWorkflowHistoryDialog(
                _formButton,
                workflowHistoryTimelineMenuFunction);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>redirectWorkflowHistoryButtonToWorkflowHistoryDialog</Name>
				<Source><![CDATA[
    private static void redirectWorkflowHistoryButtonToWorkflowHistoryDialog(
        FormFunctionButtonControl _workflowHistoryButton, 
        MenuFunction _workflowHistoryDialogMenuFunction)
    {
        _workflowHistoryButton.menuItemType(_workflowHistoryDialogMenuFunction.type());
        _workflowHistoryButton.menuItemName(_workflowHistoryDialogMenuFunction.name());
        _workflowHistoryButton.text(_workflowHistoryDialogMenuFunction.label());
        _workflowHistoryButton.caption(_workflowHistoryDialogMenuFunction.label());
        _workflowHistoryButton.helpText(_workflowHistoryDialogMenuFunction.helptext());
    }

]]></Source>
			</Method>
			<Method>
				<Name>createMenuFunctionForInvoiceHistoryTimeline</Name>
				<Source><![CDATA[
    private static MenuFunction createMenuFunctionForInvoiceHistoryTimeline()
    {
        return new MenuFunction(
            menuItemDisplayStr(VendInvoiceWorkflowHistoryTimelineDialog),
            MenuItemType::Display);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>