<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxIntegrationSalesTableDataPersistence</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxIntegrationSalesTableDataPersistence</c> class persists data to <c>SalesTable</c>, etc.
/// </summary>
[TaxIntegrationDataPersistence(tableStr(SalesTable))]
public class TaxIntegrationSalesTableDataPersistence
    extends TaxIntegrationAbstractDataPersistenceTemplate
{
    protected SalesTable salesTable;
    protected SalesLine salesLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDocumentQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the document.
    /// </summary>
    /// <returns>The query for the document.</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentQueryObject()
    {
        if (TaxIntegrationConstructWithSourceRecordFlight::instance().isEnabled() && document.getLocalRecord())
        {
            salesTable = document.getLocalRecord();
            salesTable.selectForUpdate(true);
            return null;
        }
        else
        {
            return SysDaQueryObjectBuilder::from(this.salesTable)
                .forUpdate()
                .where(this.salesTable, fieldStr(SalesTable, RecId))
                    .isEqualToLiteral(this.document.getLocalRecId())
                .toSysDaQueryObject();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the lines of the document.
    /// </summary>
    /// <returns>The query for the lines of the document</returns>
    [Replaceable]
    protected SysDaQueryObject getLineQueryObject()
    {
        return SysDaQueryObjectBuilder::from(this.salesLine)
            .forUpdate()
            .where(this.salesLine, fieldStr(SalesLine, SalesId))
                .isEqualToLiteral(this.salesTable.SalesId)
            .toSysDaQueryObject();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of the document.
    /// </summary>
    /// <returns>The query for the charges of the document</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentChargeQueryObject()
    {
        return SysDaQueryObjectBuilder::from(this.markupTransOfDocument)
            .forUpdate()
            .where(this.markupTransOfDocument, fieldStr(MarkupTrans, TransTableId))
                .isEqualToLiteral(this.salesTable.TableId)
            .where(this.markupTransOfDocument, fieldStr(MarkupTrans, TransRecId))
                .isEqualToLiteral(this.salesTable.RecId)
            .toSysDaQueryObject();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of each line.
    /// </summary>
    /// <returns>The query for the charges of each line.</returns>
    [Replaceable]
    protected SysDaQueryObject getLineChargeQueryObject()
    {
        return SysDaQueryObjectBuilder::from(this.markupTransOfLine)
            .forUpdate()
            .where(this.markupTransOfLine, fieldStr(MarkupTrans, TransTableId))
                .isEqualToLiteral(this.salesLine.TableId)
            .where(this.markupTransOfLine, fieldStr(MarkupTrans, TransRecId))
                .isEqualToLiteral(this.salesLine.RecId)
            .toSysDaQueryObject();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the current line of the document.
    /// </summary>
    /// <returns>The current line of the document</returns>
    protected TaxIntegrationLineObject getLine()
    {
        TaxIntegrationLocalId localId = TaxIntegrationAbstractRecordObject::getLocalIdStatic(this.salesLine.TableId, this.salesLine.RecId);
        if (!document.checkLine(localId))
        {
            document.setException(new System.InvalidOperationException(
                strFmt("@TaxIntegrationException:LineOnDocumentNotFound", localId, document.getLocalId())));
            return null;
        }

        return document.getLine(localId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the document.
    /// </summary>
    /// <returns>Always true.</returns>
    protected boolean saveDocument()
    {
        if (isTaxIntegrationDataPersistenceSkipFetchDB)
        {
            this.salesTable = document.getLocalRecord();
            if (!salesTable)
            {
                throw error(Error::missingRecord(funcName()));
            }
            salesTable.selectForUpdate(true);
        }
        TaxIntegrationTaxIdUtility::saveTaxIDFromDocumentToTable(document, salesTable);
        TaxIntegrationListCodeUtility::saveListCodeFromDocumentToTable(document, salesTable);
        if (TaxIntegrationNumberSequenceInDataPersistenceFlight::instance().isEnabled()
            && TaxIntegrationUtils::isMultipleTaxIdEnabled())
        {
            this.saveNumberSequenceGroupToTable();
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the current line of the document.
    /// </summary>
    /// <param name = "_line">The current line of the document.</param>
    /// <returns>true if the operation is successful; otherwise, false.</returns>
    protected boolean saveLine(TaxIntegrationLineObject _line)
    {
        TaxGroup taxGroup = _line.getTaxGroup();
        TaxItemGroup taxItemGroup = _line.getTaxItemGroup();

        if (isTaxIntegrationDataPersistenceSetLineRecord)
        {
            this.salesLine = _line.getLocalRecord();
            if (!salesLine)
            {
                throw error(Error::missingRecord(funcName()));
            }
            salesLine.selectForUpdate(true);
        }

        TaxRegistrationRecId taxId = _line.getTaxId();
        if (_line.getNeedUpdateRecord()
            || (TaxIntegrationMultipleTaxIdForSalesOrderFlight::instance().isEnabled()
                && salesLine.TaxID != taxId))
        {
            if (salesLine)
            {
                salesLine.TaxGroup = taxGroup;
                salesLine.TaxItemGroup = taxItemGroup;
                salesLine.TaxID = taxId;
                salesLine.doUpdate();
            }
            else
            {
                SalesLine salesLineLocal;
                salesLineLocal.skipDataMethods(true);
                salesLineLocal.skipEvents(true);
                update_recordset salesLineLocal
                    setting
                        TaxGroup = taxGroup,
                        TaxItemGroup = taxItemGroup,
                        TaxID = taxId
                    where salesLineLocal.RecId == _line.getLocalRecId();
            }
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveNumberSequenceGroupToTable</Name>
				<Source><![CDATA[
    private void saveNumberSequenceGroupToTable()
    {
        NumberSequenceGroupId numberSequenceGroupId = document.getNumberSequenceGroupId();

        if (numberSequenceGroupId && salesTable.numberSequenceGroup != numberSequenceGroupId)
        {
            salesTable.numberSequenceGroup = numberSequenceGroupId;
            salesTable.doUpdate();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>