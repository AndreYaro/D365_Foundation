<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvExpTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'TrvExpTable'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class TrvExpTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        TrvExpTable trvExpTable;
        select crosscompany CreatingWorker from trvExpTable where trvExpTable.RecId == _caseAssociation.RefRecId;

        if (trvExpTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Employee, trvExpTable.CreatingWorker);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        TrvExpTable trvExpTable;
        select crosscompany DataAreaId from trvExpTable where trvExpTable.RecId == _parentRecId;

        return trvExpTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        TrvExpTable trvExpTable;
        select crosscompany ExpNumber from trvExpTable where trvExpTable.RecId == _parentRecId;

        return trvExpTable.ExpNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        TrvExpTable trvExpTable;
        select RecId from trvExpTable where trvExpTable.ExpNumber == _naturalKey;

        if (!trvExpTable)
        {
            // Since this table doesn't have a single field natural key
            // RecId might be sent instead.  See performLookup().
            select RecId from trvExpTable where trvExpTable.RecId == str2Int64(_naturalKey);
        }

        return trvExpTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        TrvExpTable trvExpTable;
        select crosscompany CreatingWorker from trvExpTable where trvExpTable.RecId == _parentRecId;

        return HcmWorker::worker2Name(trvExpTable.CreatingWorker);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        TrvExpTable trvExpTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        TrvExpTable trvExpTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from trvExpTableCrossCompany
            where trvExpTableCrossCompany.RecId == _association.RefRecId;

        if (trvExpTableCrossCompany.Recid)
        {
            changecompany (trvExpTableCrossCompany.DataAreaId)
            {
                select firstonly trvExpTable where trvExpTable.RecId == _association.RefRecId;
            }
        }

        _args.record(trvExpTable);
        return menuitemdisplaystr(TrvExpenses);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(TrvExpTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(TrvExpTable));

        oSysTableLookup.addLookupfield(fieldNum(TrvExpTable, RecId), true);
        oSysTableLookup.addLookupfield(fieldnum(TrvExpTable, ReferenceDataAreaId));
        oSysTableLookup.addLookupfield(fieldnum(TrvExpTable, ExpNumber));
        oSysTableLookup.addLookupfield(fieldnum(TrvExpTable, CreatingWorker));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(TrvExpTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::TrvExpTable);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>