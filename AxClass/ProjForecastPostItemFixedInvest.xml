<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjForecastPostItemFixedInvest</Name>
	<SourceCode>
		<Declaration><![CDATA[
class   ProjForecastPostItemFixedInvest extends ProjForecastPostItem
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkCost</Name>
				<Source><![CDATA[
    protected boolean checkCost()
    {
        boolean ret = super();

        if (ret)
        {
            switch (this.forecastUpdateType())
            {
                case ProjForecastUpdateType::Elimination:
                    if (! projTrans.forecastEliminationDate())
                    {
                        ret = checkFailed("@SYS89885");
                    }
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if Cost amounts should be posted.
    /// </summary>
    /// <returns>
    /// true if Cost amounts should be posted; otherwise, false.
    /// </returns>
    protected boolean checkEnterCost()
    {
        boolean ret = true;

        ret = super();

        if (ret)
        {
            switch (this.forecastUpdateType())
            {
                case ProjForecastUpdateType::Journals:
                    ret = this.checkEnterCostForJournalsType();
                    break;

                case ProjForecastUpdateType::Invoice:
                    ret = this.checkEnterCostForInvoiceType();
                    break;

                case ProjForecastUpdateType::Estimate:
                    ret = this.checkEnterCostForEstimateType();
                    break;

                case ProjForecastUpdateType::Elimination:
                    ret = this.checkEnterCostForEliminationType();
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCostForJournalsType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if Cost amounts should be posted for the Journals type.
    /// </summary>
    /// <returns>
    /// true if Cost amounts should be posted; otherwise, false.
    /// </returns>
    protected boolean checkEnterCostForJournalsType()
    {
        return ProjForecastPostItemCostNonStockedFlight::instance().isEnabled() || this.postOnhandFinancial();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCostForInvoiceType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if Cost amounts should be posted for the Invoice type.
    /// </summary>
    /// <returns>
    /// true if Cost amounts should be posted; otherwise, false.
    /// </returns>
    protected boolean checkEnterCostForInvoiceType()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCostForEstimateType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if Cost amounts should be posted for the Estimate type.
    /// </summary>
    /// <returns>
    /// true if Cost amounts should be posted; otherwise, false.
    /// </returns>
    protected boolean checkEnterCostForEstimateType()
    {
        boolean ret = true;
        boolean isFixedPriceBillingMethod = ProjMultipleContractLinesForProjectFeatureHelper::isFixedPriceBillingMethodForContractLine(projTrans.projTable(), this.forecastSales.ContractLineId, ProjTransType::Item, isProjectOperationsIntegrationEnabled);

        if ((isFixedPriceBillingMethod && !ForecastModel::find(HeadingSub::Heading, this.modelId()).ProjForecastWIPFixedPrice) ||
            ((projTable.Type == ProjType::Investment || isMultipleContractLineFeatureEnabled) &&
            !ForecastModel::find(HeadingSub::Heading, this.modelId()).ProjForecastWIPInvestment))
        {
            ret = false;
        }

        if (! projTrans.projLineProperty().ToBeCapitalize)
        {
            ret = false;
        }

        if (isFixedPriceBillingMethod)
        {
            ProjCompletePrincip complPrinciple = isMultipleContractLineFeatureEnabled ? projRevProfile.CompletePrinciple : projGroup.CompletePrincip;
            if (complPrinciple != ProjCompletePrincip::CompletedContract)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCostForEliminationType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if Cost amounts should be posted for the Elimination type.
    /// </summary>
    /// <returns>
    /// true if Cost amounts should be posted; otherwise, false.
    /// </returns>
    protected boolean checkEnterCostForEliminationType()
    {
        boolean ret = true;
        boolean isProjFixedPriceBillingMethod = ProjMultipleContractLinesForProjectFeatureHelper::isFixedPriceBillingMethodForContractLine(projTrans.projTable(), this.forecastSales.ContractLineId, ProjTransType::Item, isProjectOperationsIntegrationEnabled);

        if ((isProjFixedPriceBillingMethod && !ForecastModel::find(HeadingSub::Heading, this.modelId()).ProjForecastWIPFixedPrice) ||
            ((projTable.Type == ProjType::Investment || isMultipleContractLineFeatureEnabled) &&
            !ForecastModel::find(HeadingSub::Heading, this.modelId()).ProjForecastWIPInvestment))
        {
            ret = false;
        }

        if (! projTrans.projLineProperty().ToBeCapitalize)
        {
            ret = false;
        }

        if (isProjFixedPriceBillingMethod)
        {
            ProjCompletePrincip complPrinciple = isMultipleContractLineFeatureEnabled ? projRevProfile.CompletePrinciple : projGroup.CompletePrincip;
            if (complPrinciple != ProjCompletePrincip::CompletedContract)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterTurnover</Name>
				<Source><![CDATA[
    protected boolean checkEnterTurnover()
    {
        boolean ret = true;

        if (this.forecastUpdateType() != ProjForecastUpdateType::Invoice)
        {
            ret = false;
        }

        if (ret)
        {
            // Test if model is used in project budgeting or standard forecasting
            ForecastModel forecasteodel = ForecastModel::findForecastModelByName(this.modelId());
            if (forecasteodel.ProjBudgetType == ProjBudgetType::None)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postCost</Name>
				<Source><![CDATA[
    protected void postCost()
    {
        MapEnumerator   fundingAllocationEnumerator;
        Map             splitTransQty;

        switch (this.forecastUpdateType())
        {
            case ProjForecastUpdateType::Estimate:
                this.postingType(LedgerPostingType::ProjOffsetAccountItem);
                this.costSales(ProjCostSales::Cost);
                this.ledgerTransDate(projTrans.transDate());

                fundingAllocationEnumerator = ProjFundingEngine::ledgerDistributions(fundingSourceList, -projTrans.transCost(), projTrans.currencyIdCost()).getEnumerator();
                splitTransQty = ProjFundingEngine::distributeQty(fundingSourceList, -projTrans.qty());
                while (fundingAllocationEnumerator.moveNext())
                {
                    this.qty(splitTransQty.exists(fundingAllocationEnumerator.currentKey()) ? splitTransQty.lookup(fundingAllocationEnumerator.currentKey()) * 1 : 0.0);
                    this.accountNum(projTrans.offsetAccountItemLedgerDimension(fundingAllocationEnumerator.currentKey()));
                    this.amountMST(fundingAllocationEnumerator.currentValue());
                    this.createForecastPosting(fundingAllocationEnumerator.currentKey());
                }

                this.postingType(LedgerPostingType::ProjStatusAccountItem);
                this.costSales(ProjCostSales::Cost);
                this.ledgerTransDate(projTrans.transDate());

                fundingAllocationEnumerator = ProjFundingEngine::ledgerDistributions(fundingSourceList, projTrans.transCost(), projTrans.currencyIdCost()).getEnumerator();
                splitTransQty = ProjFundingEngine::distributeQty(fundingSourceList, projTrans.qty());
                while (fundingAllocationEnumerator.moveNext())
                {
                    this.qty(splitTransQty.exists(fundingAllocationEnumerator.currentKey()) ? splitTransQty.lookup(fundingAllocationEnumerator.currentKey()) * 1 : 0.0);
                    this.accountNum(projTrans.statusAccountItemLedgerDimension(fundingAllocationEnumerator.currentKey()));
                    this.amountMST(fundingAllocationEnumerator.currentValue());
                    this.createForecastPosting(fundingAllocationEnumerator.currentKey());
                }
                break;

            case ProjForecastUpdateType::Elimination:
                this.postingType(LedgerPostingType::ProjStatusAccountItem);
                this.costSales(ProjCostSales::Cost);
                this.ledgerTransDate(projTrans.forecastEliminationDate());

                fundingAllocationEnumerator = ProjFundingEngine::ledgerDistributions( fundingSourceList,-projTrans.transCost(),projTrans.currencyIdCost()).getEnumerator();
                splitTransQty = ProjFundingEngine::distributeQty(fundingSourceList, -projTrans.qty());
                while (fundingAllocationEnumerator.moveNext())
                {
                    this.qty(splitTransQty.exists(fundingAllocationEnumerator.currentKey()) ? splitTransQty.lookup(fundingAllocationEnumerator.currentKey()) * 1 : 0.0);
                    this.accountNum(projTrans.statusAccountItemLedgerDimension(fundingAllocationEnumerator.currentKey()));
                    this.amountMST(fundingAllocationEnumerator.currentValue());
                    this.createForecastPosting(fundingAllocationEnumerator.currentKey());
                }

                if (ProjMultipleContractLinesForProjectFeatureHelper::isFixedPriceBillingMethodForContractLine(projTrans.projTable(), this.forecastSales.ContractLineId, ProjTransType::Item, isProjectOperationsIntegrationEnabled))
                {
                    this.postingType(LedgerPostingType::ProjOffsetAccountItem);
                    this.costSales(ProjCostSales::Cost);
                    this.ledgerTransDate(projTrans.forecastEliminationDate());

                    fundingAllocationEnumerator = ProjFundingEngine::ledgerDistributions( fundingSourceList,projTrans.transCost(),projTrans.currencyIdCost()).getEnumerator();
                    splitTransQty = ProjFundingEngine::distributeQty(fundingSourceList, projTrans.qty());
                    while (fundingAllocationEnumerator.moveNext())
                    {
                        this.qty(splitTransQty.exists(fundingAllocationEnumerator.currentKey()) ? splitTransQty.lookup(fundingAllocationEnumerator.currentKey()) * 1 : 0.0);
                        this.accountNum(projTrans.offsetAccountItemLedgerDimension(fundingAllocationEnumerator.currentKey()));
                        this.amountMST(fundingAllocationEnumerator.currentValue());
                        this.createForecastPosting(fundingAllocationEnumerator.currentKey());
                    }
                }
                break;

            default:
                super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>