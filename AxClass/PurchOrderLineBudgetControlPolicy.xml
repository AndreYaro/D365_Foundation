<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchOrderLineBudgetControlPolicy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchOrderLineBudgetControlPolicy</c> class provides information that pertains to checking
/// the budget for purchase orders.
/// </summary>
[SourceDocumentEnumExtensionAttribute(enumStr(BusinessEvent_ProductOrder), BusinessEvent_ProductOrder::ObligateBudgetFundsForExpensedProducts)]
class PurchOrderLineBudgetControlPolicy extends BudgetControlSourceDocumentLinePolicy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canCheckBudget</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the budget can be checked for the purchase source document line that is
    /// associated with the purchase order line or purchase order header.
    /// </summary>
    /// <returns>
    /// true if budget can be checked; otherwise, false.
    /// </returns>
    public boolean canCheckBudget()
    {
        boolean canCheckBudget;
        PurchaseOrderSourceDocument purchaseOrderSourceDocument;

        canCheckBudget = super();

        if (canCheckBudget)
        {
            if (isDraftReservation &&
                !isOnDemandBudgetCheck &&
                !(budgetSourceSourceDocumentLineItem is PurchOrderLineSourceDocumentLineItem))
            {
                // Budget check for draft status should never include header amounts
                canCheckBudget = false;
            }
            else if (budgetSourceSourceDocumentLineItem.parmSourceDocument() is PurchaseOrderSourceDocument)
            {
                purchaseOrderSourceDocument = budgetSourceSourceDocumentLineItem.parmSourceDocument();

                if (purchaseOrderSourceDocument.parmPurchaseType() == PurchaseType::Journal)
                {
                    // Purchase orders of type journal should never be checked for budget
                    canCheckBudget = false;
                }
            }
        }

        return canCheckBudget;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>