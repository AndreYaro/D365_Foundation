<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendOpenTrans_Bank_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(VendOpenTrans))]
internal final class VendOpenTrans_Bank_Extension
{
    internal boolean isCallerBankStatementGeneratePaymentDialog;
    internal FormRun callingForm;
    private List bankStmtISOReportEntryRecIdList;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        callingForm = this.args().caller();
        isCallerBankStatementGeneratePaymentDialog = callingForm && callingForm.name() == formStr(BankStatementGeneratePaymentDialog);

        if (isCallerBankStatementGeneratePaymentDialog)
        {
            bankStmtISOReportEntryRecIdList = this.args().parmObject();
            VendTransOpen_ds.linkActive();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesignDynamic</Name>
				<Source><![CDATA[
    public void updateDesignDynamic()
    {
        next updateDesignDynamic();

        if (isCallerBankStatementGeneratePaymentDialog && this.manager.getSpecTransCount() > 0)
        {
            UpdateNow.enabled(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>markBankStatementPaymentTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Marks the payment transactions of statement lines to be settled.
    /// </summary>
    internal void markBankStatementPaymentTrans()
    {
        if (bankStmtISOReportEntryRecIdList)
        {
            VendTransOpen vendTransOpen;
            LedgerJournalTrans ledgerJournalTrans;
            BankStatementLinePaymentInformation paymInfo;
            ListEnumerator statementLineEnumerator = bankStmtISOReportEntryRecIdList.getEnumerator();

            while (statementLineEnumerator.moveNext())
            {
                select firstonly vendTransOpen
                    join RecId from paymInfo
                        where paymInfo.BankStmtISOReportEntry == statementLineEnumerator.current()
                    join RecId from ledgerJournalTrans
                        where ledgerJournalTrans.RecId == paymInfo.LedgerJournalTrans
                            && ledgerJournalTrans.VendTransId == vendTransOpen.RefRecId;

                VendTransOpen_ds.positionToRecord(vendTransOpen);
                VendTransOpen_ds.editMarkTrans(true, vendTransOpen, NoYes::Yes);
            }
        }

        this.updateMarkedTotals();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>