<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSSourceDocument</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Encapsulates document entity, which is a subject of transportation management system processing.
/// </summary>
abstract class TMSSourceDocument
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>currency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the <c>CurrencyCode</c> for the particular source document.
    /// </summary>
    /// <returns>
    /// The <c>CurrencyCode</c> for the source document.
    /// </returns>
    public abstract CurrencyCode currency()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDestinationAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves destination address of source document.
    /// </summary>
    /// <returns>
    /// Destination address.
    /// </returns>
    public TMSAddressPhone getDestinationAddress()
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOriginAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the origin address from source document.
    /// </summary>
    /// <returns>
    /// The origin address.
    /// </returns>
    public TMSAddressPhone getOriginAddress()
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeAddressesOnTMSProcessXML</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes address related fields on <c>TMSProcessXML_Base</c> from source document.
    /// </summary>
    /// <param name="_tmsProcessXML">
    /// An instance of <c>TMSProcessXML_Base</c> class to be initialized.
    /// </param>
    /// <param name="_shipWarehouseAddress">
    /// The warehouse address of related inbound or outbound shipment.
    /// </param>
    /// <param name="_externalAddress">
    /// A 3rd party address of related inboung or outbound shipment; optional.
    /// </param>
    public void initializeAddressesOnTMSProcessXML(
        TMSProcessXML_Base  _tmsProcessXML,
        TMSAddressPhone     _shipWarehouseAddress,
        TMSAddressPhone     _externalAddress = null)
    {
        TMSAddressPhone         fromAdd;
        TMSAddressPhone         toAdd;

        switch (this.transportDirection())
        {
            case TMSDirection::Inbound:
                fromAdd = this.getOriginAddress();
                toAdd = _shipWarehouseAddress;
                break;
            case TMSDirection::Outbound:
                fromAdd = _shipWarehouseAddress;
                toAdd = this.getDestinationAddress();
                break;
        }

        _tmsProcessXML.parmFromAddress(fromAdd);
        _tmsProcessXML.parmToAddress(toAdd);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessXML</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of <c>TMSProcessXML_Base</c> with basic data.
    /// </summary>
    /// <param name="_processXML">
    /// An instance of <c>TMSProcessXML_Base</c> class to be initialized.
    /// </param>
    public abstract void initializeProcessXML(TMSProcessXML_base  _processXML)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>transportDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines the direction of transportation operation.
    /// </summary>
    /// <returns>
    /// The direction of transportation operation.
    /// </returns>
    public abstract TMSDirection transportDirection()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a new instance of <c>TMSSourceDocument</c> class.
    /// </summary>
    /// <param name="_sourceRecord">
    /// The source record from which to construct a source document encapsulation.
    /// </param>
    /// <returns>
    /// An instance of <c>TMSSourceDocument</c> class.
    /// </returns>
    /// <exception cref="Exception::Error">
    /// _sourceRecord does not refer to existing AX table.
    /// </exception>
    public static TMSSourceDocument construct(Common _sourceRecord)
    {
        if (_sourceRecord.TableId == 0)
        {
            throw error("@TRX2707");
        }

        if (_sourceRecord.RecId == 0)
        {
            return TMSSourceDocument_Default::construct();
        }

        switch (_sourceRecord.TableId)
        {
            case (tableNum(SalesTable)):
                return TMSSourceDocument_SalesTable::construct(_sourceRecord as SalesTable);

            case (tableNum(PurchTable)):
                return TMSSourceDocument_PurchTable::construct(_sourceRecord as PurchTable);

            case (tableNum(InventTransferTable)):
                return TMSSourceDocument_InventTransferTable::construct(_sourceRecord as InventTransferTable);

            case (tableNum(SalesLine)):
                SalesLine salesLine = _sourceRecord as SalesLine;
                return TMSSourceDocument_SalesTable::construct(salesLine.salesTable());

            case (tableNum(PurchLine)):
                PurchLine purchLine = _sourceRecord as PurchLine;
                return TMSSourceDocument_PurchTable::construct(purchLine.purchTable());

            case (tableNum(InventTransferLine)):
                InventTransferLine transferLine = _sourceRecord as InventTransferLine;
                return TMSSourceDocument_InventTransferTable::construct(transferLine.inventTransferTable());

            default:
                return TMSSourceDocument::defaultConstruct(_sourceRecord);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSourceRecord</Name>
				<Source><![CDATA[
    protected void setSourceRecord(Common _sourceRecord)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultConstruct</Name>
				<Source><![CDATA[
    public static TMSSourceDocument defaultConstruct(Common _sourceRecord)
    {
        SysTableNameFactoryAttribute attribute = new SysTableNameFactoryAttribute(tableId2Name(_sourceRecord.TableId));
        TMSSourceDocument instance  = SysExtensionAppClassFactory::getClassFromSysAttribute(classStr(TMSSourceDocument), attribute) as TMSSourceDocument;

        if (!instance)
        {
            instance = TMSSourceDocument_Default::construct();
        }
        instance.setSourceRecord(_sourceRecord);
        
        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>