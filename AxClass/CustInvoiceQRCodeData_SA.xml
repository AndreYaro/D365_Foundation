<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceQRCodeData_SA</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>CustInvoiceQRCodeData_SA</c> represents the data to be encoded to QR code.
/// </summary>
internal class CustInvoiceQRCodeData_SA implements CustInvoiceQRCodeDataInterface_SA
{
    protected CustInvoiceQRCodeDataContextInterface_SA context;
    protected Common journal;
    protected CompanyInfo companyInfo;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromJournal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>CustInvoiceQRCodeData_SA</c> class from journal.
    /// </summary>
    /// <param name = "_journal">The buffer of a journal.</param>
    /// <param name = "_companyInfo">The buffer of a <c>CompanyInfo</c> table.</param>
    /// <returns>A new instance of the <c>CustInvoiceQRCodeData_SA</c> class from journal.</returns>
    public static CustInvoiceQRCodeData_SA newFromJournal(Common _journal, CompanyInfo _companyInfo)
    {
        CustInvoiceQRCodeData_SA instance = new CustInvoiceQRCodeData_SA();
        instance.journal = _journal;
        instance.companyInfo = _companyInfo;
        instance.context = CustInvoiceQRCodeDataContextFactory_SA::newFromTableName(tableId2Name(_journal.TableId));

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>concatBytes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Concatenates the list of byte arrays.
    /// </summary>
    /// <param name = "_byteArrayList">The list of byte arrays.</param>
    /// <returns>Concatenates list of byte arrays.</returns>
    protected static System.Byte[] concatBytes(List _byteArrayList)
    {
        return GenerateQRCode_SA::concatBytes(_byteArrayList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createTLVField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a byte array in a TLV structure.
    /// </summary>
    /// <param name = "_tag">The tag value.</param>
    /// <param name = "_value">The value of TLV element.</param>
    /// <returns>A byte array in a TLV structure.</returns>
    protected final System.Byte[] createTLVField(System.Byte _tag, str _value)
    {
        return GenerateQRCode_SA::createTLVField(_tag, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getByteArray</Name>
				<Source><![CDATA[
    public System.Byte[] getByteArray()
    {
        System.Byte[] encodedSellerName = this.createTLVField(GenerateQRCode_SA::SellerNameTag, companyInfo.name());
        System.Byte[] encodedSellerRegNum = this.createTLVField(GenerateQRCode_SA::SellerRegNumTag, companyInfo.CoRegNum);
        System.Byte[] encodedTimeStamp = this.createTLVField(GenerateQRCode_SA::TimeStampTag, DateTimeUtil::toStr(
            DateTimeUtil::newDateTime(
                context.getIssueDate(journal),
                context.getIssueTime(journal))));
        System.Byte[] encodedTotalAmount = this.createTLVField(GenerateQRCode_SA::TotalAmountTag, this.numFormat(context.getInvoiceAmount(journal)));
        System.Byte[] encodedTotalTaxAmount = this.createTLVField(GenerateQRCode_SA::TotalTaxAmountTag, this.numFormat(context.getTaxAmount(journal)));

        List byteArrayList = new List(Types::AnyType);
        byteArrayList.addEnd(encodedSellerName);
        byteArrayList.addEnd(encodedSellerRegNum);
        byteArrayList.addEnd(encodedTimeStamp);
        byteArrayList.addEnd(encodedTotalAmount);
        byteArrayList.addEnd(encodedTotalTaxAmount);

        return CustInvoiceQRCodeData_SA::concatBytes(byteArrayList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>numFormat</Name>
				<Source><![CDATA[
    /// <summary>
    /// Cconverts the number to string in the proper format.
    /// </summary>
    /// <param name = "_num">The number.</param>
    /// <returns>The formated number.</returns>
    protected final str numFormat(real _num)
    {
        return GenerateQRCode_SA::numFormat(_num);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getString</Name>
				<Source><![CDATA[
    public final str getString()
    {
        System.Byte[] byteArray = this.getByteArray();

        return System.Convert::ToBase64String(byteArray);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>