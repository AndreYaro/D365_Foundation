<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqPeggingTreeNodeSalesLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ReqPeggingTreeNodeSalesLine</c> class represents a sales order line node in the pegging tree.
/// </summary>
public class ReqPeggingTreeNodeSalesLine extends ReqPeggingTreeNode
{
    SalesLine  salesLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the textual description of the node.
    /// </summary>
    /// <returns>
    /// A string containing the description text of the node.
    /// </returns>
    public str description()
    {
        return strFmt("@SYS320508",salesLine.SalesId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>eventType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the event type for the current tree node.
    /// </summary>
    /// <returns>
    /// A <c>LeanPeggedEventType</c> instance.
    /// </returns>
    public LeanPeggedEventType eventType()
    {
        return LeanPeggedEventType::SalesLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeDetails</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>ReqPeggingTreeDetails</c> instance with data.
    /// </summary>
    /// <param name="_details">
    /// The object that holds the details.
    /// </param>
    public void initializeDetails(ReqPeggingTreeDetails _details)
    {
        _details.initFromSalesLine(salesLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>listChildren</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the list of children record buffers (kanbans).
    /// </summary>
    /// <returns>
    /// A list containing the kanbans that supply the sales order line of the node.
    /// </returns>
    public List listChildren()
    {
        List      list;
        container cachedResult = reqPeggingListCache.find(classStr(ReqPeggingTreeNodeSalesLine), [salesLine.SourceDocumentLine]);

        if (cachedResult != conNull())
        {
            return List::create(cachedResult);
        }

        list = ReqPeggingTreeNode::peggedKanbans(salesLine.SourceDocumentLine);
        cachedResult = list.pack();
        reqPeggingListCache.insert(classStr(ReqPeggingTreeNodeSalesLine), [salesLine.SourceDocumentLine], cachedResult);
        return list;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parent</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the sales order record buffer of the parent node.
    /// </summary>
    /// <returns>
    /// A <c>SalesTable</c> record buffer.
    /// </returns>
    public Common parent()
    {
        return SalesTable::find(salesLine.SalesId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSalesLine</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public SalesLine parmSalesLine(SalesLine _salesLine = salesLine)
    {
        salesLine = _salesLine;
        return salesLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceDocNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the source document number.
    /// </summary>
    /// <returns>
    /// A source document number.
    /// </returns>
    /// <remarks>
    /// Implemented only for root level nodes.
    /// </remarks>
    public str sourceDocNumber()
    {
        return salesLine.SalesId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static ReqPeggingTreeNodeSalesLine construct()
    {
        return new ReqPeggingTreeNodeSalesLine();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the class.
    /// </summary>
    /// <param name="_salesLine">
    /// The <c>SalesLine</c> record buffer represented by the node.
    /// </param>
    /// <returns>
    /// A new instance of the class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ReqPeggingTreeNodeSalesLine newStandard(SalesLine _salesLine)
    {
        ReqPeggingTreeNodeSalesLine    reqPeggingTreeNodeSalesLine = ReqPeggingTreeNodeSalesLine::construct();

        reqPeggingTreeNodeSalesLine.parmSalesLine(_salesLine);

        return reqPeggingTreeNodeSalesLine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>