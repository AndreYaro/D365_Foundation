<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppServiceDecoratorRuleInquiryDisplayArea</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSMobileAppServiceDecoratorInquiryDisplayAreaRule</c> class populates the DisplayArea for the Inquiry pattern.
/// </summary>
public class WHSMobileAppServiceDecoratorRuleInquiryDisplayArea implements WHSIMobileAppServiceXMLDecoratorRule
{
    #WHSRF
    #WHSWorkExecuteControlElements
    #XmlDocumentation

    private const str Footer1 = 'Qty';
    private const str Footer2 = 'Inventory status';
    private const str newLine = '\n';
    private str prevLPLabel;

    private const ExtendedTypeId LPExtendedType             = extendedTypeNum(WHSLicensePlateId);
    private const ExtendedTypeId LocationExtendedType       = extendedTypeNum(WMSLocationId);
    private const ExtendedTypeId ItemInfoExtendedType       = extendedTypeNum(WHSRFItemInformation);
    private const ExtendedTypeId QuantityInfoExtendedType   = extendedTypeNum(WHSRFQuantityInformation);

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WHSMobileAppServiceDecoratorRuleInquiryDisplayArea construct()
    {
        return new WHSMobileAppServiceDecoratorRuleInquiryDisplayArea();
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustSetFooters</Name>
				<Source><![CDATA[
    private boolean mustSetFooters(ExtendedTypeId _controlInputType)
    {
        switch (_controlInputType)
        {
            case ItemInfoExtendedType:
                return true;
        }
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isNewLPHeaderGroup</Name>
				<Source><![CDATA[
    private boolean isNewLPHeaderGroup(str _currentLabel)
    {
        if (prevLPLabel != _currentLabel)
        {
            prevLPLabel = _currentLabel;
            return true;
        }
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDisplayArea</Name>
				<Source><![CDATA[
    private str getDisplayArea(ExtendedTypeId _controlInputType, str _currentLabel)
    {
        switch (_controlInputType)
        {
            case LPExtendedType:
                if (this.isNewLPHeaderGroup(_currentLabel))
                {
                    return WHSMobileAppXMLDisplayArea::GroupHeaderArea;
                }
                break;
            case LocationExtendedType:
                return WHSMobileAppXMLDisplayArea::GroupHeaderArea;
            case ItemInfoExtendedType,
                 QuantityInfoExtendedType:
                return WHSMobileAppXMLDisplayArea::BodyArea;
            default:
                return WHSMobileAppXMLDisplayArea::SubHeaderArea;
        }
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method to run the transformation.
    /// </summary>
    /// <param name = "_pageInfo">
    /// The <c>WHSMobileAppPageInfo</c> containing controls and information to carry out the transformation.
    /// </param>
    public void run(WHSMobileAppPageInfo _pageInfo)
    {
        ListEnumerator le = _pageInfo.parmControlsEnumerator();

        while (le.moveNext())
        {
            Map controlMap = le.current();
            if (this.mustDecorateControl(controlMap))
            {
                this.decorateControl(controlMap);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>decorateControl</Name>
				<Source><![CDATA[
    private void decorateControl(Map _controlMap)
    {
        ExtendedTypeId controlInputType = _controlMap.lookup(#XMLControlInputType);
        str currentLabel = _controlMap.lookup(#XMLControlLabel);

        str displayArea = this.getDisplayArea(controlInputType, currentLabel);
        if (displayArea)
        {
            this.setDisplayArea(_controlMap, displayArea);
        }

        if (this.mustSetFooters(controlInputType))
        {
            this.setFooters(_controlMap);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustDecorateControl</Name>
				<Source><![CDATA[
    private boolean mustDecorateControl(Map _controlMap)
    {
        return _controlMap.lookup(#XMLControlCtrlType) == #RFLabel &&
               !this.newLineControl(_controlMap.lookup(#XMLControlLabel));
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDisplayArea</Name>
				<Source><![CDATA[
    private void setDisplayArea(Map _controlMap, str _displayArea)
    {
        _controlMap.insert(#XMLControlDisplayArea, _displayArea);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFooters</Name>
				<Source><![CDATA[
    private void setFooters(Map _controlMap)
    {
        _controlMap.insert(#XMLControlFooter1, Footer1);
        _controlMap.insert(#XMLControlFooter2, Footer2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newLineControl</Name>
				<Source><![CDATA[
    private boolean newLineControl(str _controlLabel)
    {
        return _controlLabel == newLine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>