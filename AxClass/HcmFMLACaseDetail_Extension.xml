<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmFMLACaseDetail_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(HcmFMLACaseDetail))]
public final class HcmFMLACaseDetail_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getHoursWorked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the productive work hours.
    /// </summary>
    /// <returns>
    /// Returns the productive hours required for meeting
    /// the eligibility criteria for FMLA leave.
    /// </returns>
    public PayrollEarningQuantity getHoursWorked()
    {
        PayrollEarningCode          payrollEarningCode;
        PayrollEarningStatementLine earningStatementLine;
        PayrollEarningStatement     earningStatement;
        HcmWorker                   worker;

        select sum(Quantity) from earningStatementLine
            where earningStatementLine.EarningDate >= prevYr(this.LeaveStartDate)
                && earningStatementLine.EarningDate <= this.LeaveStartDate
            join RecId from worker
                where worker.Person == this.Party
            join RecId from earningStatement
                where earningStatement.RecId == earningStatementLine.EarningStatement
                    && earningStatement.Worker == worker.RecId
            exists join payrollEarningCode
                where payrollEarningCode.QuantityUnit == PayrollQuantityUnit::Hours
                    && payrollEarningCode.IsProductive == NoYes::Yes
                    && payrollEarningCode.RecId == earningStatementLine.EarningCode;

        return earningStatementLine.Quantity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>InsertHoursWorked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>HcmFMLACaseDetail.SetHoursWorkedDelegate()</c> event.
    /// </summary>
    /// <param name="_fmlaCaseDetail">
    /// The table buffer being used by the record.
    /// </param>
    [SubscribesTo(tableStr(HcmFMLACaseDetail), delegatestr(HcmFMLACaseDetail, SetHoursWorkedDelegate))]
    public static void InsertHoursWorked(HcmFMLACaseDetail _fmlaCaseDetail)
    {
        _fmlaCaseDetail.HoursWorked = _fmlaCaseDetail.getHoursWorked();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>