<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EFDocParametersFormValidateBenefitCodesField_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formDataFieldStr(EFDocParameters_BR, EFDocAuthorityState_BR, ValidateBenefitCodes))]
public final class EFDocParametersFormValidateBenefitCodesField_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>modified</Name>
				<Source><![CDATA[
    public void modified()
    {
        next modified();

        FormDataObject fdo = any2Object(this) as FormDataObject;
        FormDataSource fds = fdo.datasource();
        EFDocAuthorityState_BR efDocAuthorityState = fds.cursor();

        if (efDocAuthorityState.ValidateBenefitCodes == NoYes::Yes)
        {
            LogisticsAddressStateId stateId = efDocAuthorityState.getStateId();
            if (FiscalEstablishment_BR::isTechnicalNoteEnabledForState(EFDocConstants_BR::NFeNT2019001v110, stateId))
            {
                TaxBenefitCodeTaxationCodes_BR taxBenefitCodeTaxationCodes;
                FBFiscalDocumentAdjustmentCodeICMS_BR fbFiscalDocumentAdjustmentCodeICMS;

                select firstonly RecId from taxBenefitCodeTaxationCodes
                    exists join fbFiscalDocumentAdjustmentCodeICMS
                        where fbFiscalDocumentAdjustmentCodeICMS.State == stateId
                            && fbFiscalDocumentAdjustmentCodeICMS.RecId == taxBenefitCodeTaxationCodes.FBFiscalDocumentAdjustmentCodeICMS;

                if (!taxBenefitCodeTaxationCodes)
                {
                    DialogButton diagBut = Box::yesNo(
                        strFmt("@NFE:TaxBenefitCodeTaxationCodesBoxMessage", stateId),
                        DialogButton::No,
                        "@NFE:TaxBenefitCodeTaxationCodesBoxTitle");

                    if (diagBut == DialogButton::Yes)
                    {
                        TaxBenefitCodeType_BR taxBenefitCodeType;

                        insert_recordset taxBenefitCodeTaxationCodes(TaxationCode, FBFiscalDocumentAdjustmentCodeICMS)
                            select TaxationCode from taxBenefitCodeType
                                join RecId from fbFiscalDocumentAdjustmentCodeICMS
                                    where fbFiscalDocumentAdjustmentCodeICMS.TaxBenefitCodeType == taxBenefitCodeType.RecId
                                        && fbFiscalDocumentAdjustmentCodeICMS.State == stateId;
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>