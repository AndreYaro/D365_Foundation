<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSASNItemReceiptHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSASNItemReceiptHandler</c> class is responsible for handling operations related to the ASN item receipts.
/// </summary>
internal class WHSASNItemReceiptHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateWorkCreatedQtyForLoadLine</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static WHSWorkCreatedQty calculateWorkCreatedQtyForLoadLine(WHSLoadLine _loadLine)
    {
        WHSWorkCreatedQty workCreatedQty;

        WHSASNItem asnItem;
        
        select sum(WorkCreatedQty) from asnItem
            where asnItem.LoadLineRefRecId == _loadLine.RecId;

        workCreatedQty = asnItem.WorkCreatedQty;

        WHSASNItemReceipt asnItemReceipt;
        WHSASNItem asnItemForLoadLine;

        select sum(WorkCreatedQty) from asnItemReceipt
            exists join asnItemForLoadLine
            where asnItemForLoadLine.RecId == asnItemReceipt.ASNItem
                && asnItemForLoadLine.LoadLineRefRecId == _loadLine.RecId;

        workCreatedQty += asnItemReceipt.WorkCreatedQty;

        return workCreatedQty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>registerASNItemWorkCreatedQty</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static void registerASNItemWorkCreatedQty(
        WHSASNItem          _asnItem,
        WHSWorkCreatedQty   _workCreatedQty)
    {
        WHSASNItemReceipt asnItemReceipt;

        select forupdate asnItemReceipt
            where asnItemReceipt.ASNItem == _asnItem.RecId;

        if (asnItemReceipt)
        {
            asnItemReceipt.WorkCreatedQty += _workCreatedQty;
            asnItemReceipt.update();
        }
        else
        {
            asnItemReceipt.ASNItem = _asnItem.RecId;
            asnItemReceipt.WorkCreatedQty = _workCreatedQty;
            asnItemReceipt.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>