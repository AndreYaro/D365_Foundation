<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepreciationScheduleUIBuilder_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// <c>AssetDepreciationScheduleUIBuilder_IN</c> is the UI Builder class for
/// <c>AssetDepreciationSchedule_IN</c> SSRS Report.
/// </summary>
public class AssetDepreciationScheduleUIBuilder_IN extends SrsReportDataContractUIBuilder
{
    AssetDepScheduleContract_IN contract;

    DialogField valueModel;
    DialogField currentOperationsTaxStringField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds dialog with basic parameters.
    /// </summary>
    public void build()
    {
        contract = this.dataContractInfo().dataContractObject() as AssetDepScheduleContract_IN;

        super();

        valueModel = this.bindInfo().getDialogField(
           contract,
           methodStr(AssetDepScheduleContract_IN, parmValueModel));

        switch (contract.parmReportType())
        {
            case AssetDepreciationScheduleReportType_IN::DepreciationSchedule:
                valueModel.visible(false);
                break;

            case AssetDepreciationScheduleReportType_IN::ComponentDepreciationSchedule:
                valueModel.visible(true);
                break;

            default:
                break;
        }

        valueModel.enabled(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyOverrideMethod</Name>
				<Source><![CDATA[
    /// <summary>
    /// Register the overrided lookup method
    /// </summary>
    protected void modifyOverrideMethod()
    {
        valueModel.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(AssetDepreciationScheduleUIBuilder_IN, valueModelLookup),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post build actions.
    /// </summary>
    public void postBuild()
    {
        super();

        this.modifyOverrideMethod();
    }

]]></Source>
			</Method>
			<Method>
				<Name>valueModelLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides the lookup method
    /// </summary>
    /// <param name="_valueModelDialogControl">
    /// The form string control
    /// </param>
    public void valueModelLookup(FormStringControl _valueModelDialogControl)
    {
        QueryBuildDataSource            qbdsValueModel;
        Query                           query = new Query();
        SysTableLookup                  lookup;
        Args                            args = controller.parmArgs();

        qbdsValueModel  = query.addDataSource(tableNum(AssetBookTable));
        qbdsValueModel.addRange(fieldNum(AssetBookTable, CurrentOperationsTax)).value(queryValue(currentOperationsTaxStringField.value()));

        lookup = SysTableLookup::newParameters(tableNum(AssetBookTable), _valueModelDialogControl);
        lookup.addLookupfield(fieldNum(AssetBookTable, BookId), true);
        lookup.addLookupfield(fieldNum(AssetBookTable, Description));
        lookup.parmQuery(query);

        // Perform the lookup.
        lookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>addDialogField</Name>
				<Source><![CDATA[
    protected DialogField addDialogField(identifierName _methodName, Object _dataContract = this.dataContractObject())
    {
        DialogField ret;

        if (_methodName == methodStr(AssetDepScheduleContract_IN, parmCurrentOperationsTax))
        {
            AssetDepScheduleContract_IN depreciationScheduleContract = this.dataContractObject();
            Dialog dialogForm = this.dialog();

            currentOperationsTaxStringField = dialogForm.addFieldValue(
                extendedTypeStr(AssetPostingLayerString),
                AssetPostingLayerXdsTmp::getStringByValue(depreciationScheduleContract.parmCurrentOperationsTax()));
        }
        else
        {
            ret = super(_methodName, _dataContract);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public void getFromDialog()
    {
        super();

        AssetPostingLayerString currentOperationsTaxString = currentOperationsTaxStringField.value();

        if (!currentOperationsTaxString)
        {
            throw warning("@FixedAssets:FA_MessageError_PostingLayerCannotBeEmpty");
        }

        CurrentOperationsTax currentOperationsTax = AssetPostingLayerXdsTmp::getValueByString(currentOperationsTaxString);

        AssetDepScheduleContract_IN depreciationScheduleContract = this.dataContractObject();
        depreciationScheduleContract.parmCurrentOperationsTax(currentOperationsTax);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>