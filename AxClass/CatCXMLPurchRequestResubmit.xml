<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CatCXMLPurchRequestResubmit</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class CatCXMLPurchRequestResubmit
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resubmitPR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Resubmit the Purchase record
    /// </summary>
    /// <param name = "_cxmlPurchRequest">
    /// An instance of <c>cXMLPurchRequest</c> data type
    /// </param>
    static void resubmitPR(CatCXMLPurchRequest _cxmlPurchRequest)
    {
        CatCXMLPurchRequestHistory cxmlPurchRequestHistory;

        //find all the history records for the PurchId and where the record hasn't been archived.
        //As this would be the candidate for the resubmitting.
        cxmlPurchRequestHistory = CatCXMLPurchRequestHistory::findByCurrent(_cxmlPurchRequest.PurchId,true);

        if (cxmlPurchRequestHistory.PurchReqSendStatus == PurchReqSendStatus::Sent
            && cxmlPurchRequestHistory.PurchReqVendStatus == PurchReqVendStatus::VendorAcknowledged)
        {
            throw error("@CXM:ResubmitPurchaseRequestError");
        }

        ttsBegin;

        //The PurchRequest will be re-set to waiting.
        _cxmlPurchRequest.PurchReqSendStatus = PurchReqSendStatus::Waiting;
        _cxmlPurchRequest.PurchReqVendStatus = PurchReqVendStatus::Waiting;
        _cxmlPurchRequest.update();

        cxmlPurchRequestHistory.PurchReqSendStatus = PurchReqSendStatus::Waiting;
        cxmlPurchRequestHistory.PurchReqStatusChangedDateTime = DateTimeUtil::getSystemDateTime();
        cxmlPurchRequestHistory.PurchReqVendStatus = PurchReqVendStatus::Waiting;

        cxmlPurchRequestHistory.PurchReqResubmitCount = cxmlPurchRequestHistory.PurchReqResubmitCount + 1;
        cxmlPurchRequestHistory.PurchResubmitDateTime = DateTimeUtil::getSystemDateTime();

        cxmlPurchRequestHistory.UserID = curUserId();

        cxmlPurchRequestHistory.update();

        ttsCommit;

    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        
        if (_args.record())
        {
            CatCXMLPurchRequestResubmit::resubmitPR(_args.record());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>