<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_ServiceOrder</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_ServiceOrder extends LogisticsLocationSelectForm
{
    SMAServiceOrderTable smaServiceOrderTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates the entities from which the addresses can be selected.
    /// </summary>
    /// <returns>
    /// A set of <c>LogisticsLocationSelectSourceType</c> enumeration values from which addresses can be
    /// selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret = new Set(Types::Enum);

        ret.add(LogisticsLocationSelectSourceType::Customer);
        ret.add(LogisticsLocationSelectSourceType::Vendor);
        ret.add(LogisticsLocationSelectSourceType::Site);
        ret.add(LogisticsLocationSelectSourceType::Warehouse);
        ret.add(LogisticsLocationSelectSourceType::ServiceOrder);
        ret.add(LogisticsLocationSelectSourceType::Project);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling Service order.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        smaServiceOrderTable.ServicePostalAddress = _addressRecId;
        smaServiceOrderTable.AddressRefRecId = _transferFromTable.RecId;
        smaServiceOrderTable.AddressRefTableId = _transferFromTable.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    The initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    ///    A <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (smaServiceOrderTable.AddressRefTableId)
        {
            sourceType = LogisticsLocationSelectSourceType::Customer;

            addressOwner = LogisticsLocationSelectForm::findByRecId(
                smaServiceOrderTable.AddressRefTableId,
                smaServiceOrderTable.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    smaServiceOrderTable.AddressRefTableId,
                    sourceType);
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> record that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of the <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public recId getLogisticsPostalAddressRecId()
    {
        return smaServiceOrderTable.ServicePostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of the target form should have selected upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (smaServiceOrderTable.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                smaServiceOrderTable.AddressRefTableId,
                smaServiceOrderTable.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            if (smaServiceOrderTable.CustAccount)
            {
                addressOwner = CustTable::find(smaServiceOrderTable.CustAccount);
            }
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(SMAServiceOrderTable _smaServiceOrderTable)
    {
        smaServiceOrderTable = _smaServiceOrderTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_ServiceOrder</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A record to use to initialize the <c>LogisticsLocationSelectForm_ServiceOrder</c> object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_ServiceOrder</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_ServiceOrder construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_ServiceOrder(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>