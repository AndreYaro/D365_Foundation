<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BusinessDocumentSubmissionUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class BusinessDocumentSubmissionUIBuilder extends SysOperationAutomaticUIBuilder
{
    private boolean allowBatchSubmission;
    private boolean allowResubmitFromFailedAction;
    private DialogField batchSubmissionField;
    private DialogField resubmitField;
    private DialogField resubmitFromFailedActionField;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void postBuild()
    {
        super();

        allowBatchSubmission = 
            FeatureStateProvider::isFeatureEnabled(BusinessDocumentBatchSubmissionFeature::instance())
            && BusinessDocumentParameters::isFeatureEnabled([EInvoicingFeaturesForService::ID00001]);

        batchSubmissionField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(BusinessDocumentSubmissionContract, parmBatchSubmission));
        batchSubmissionField.visible(allowBatchSubmission);

        allowResubmitFromFailedAction = FeatureStateProvider::isFeatureEnabled(BusinessDocumentResubmitFromFailedActionFeature::instance());

        resubmitField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(BusinessDocumentSubmissionContract, parmResubmit));
        resubmitFromFailedActionField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(BusinessDocumentSubmissionContract, parmResubmitFromFailedAction));
        resubmitFromFailedActionField.visible(allowResubmitFromFailedAction);

        this.updateDesign();
        resubmitField.registerOverrideMethod(methodStr(FormComboBoxControl, modified),
                                                methodStr(BusinessDocumentSubmissionUIBuilder, modifiedResubmit),
                                                this);
        batchSubmissionField.registerOverrideMethod(methodStr(FormComboBoxControl, modified),
                                                methodStr(BusinessDocumentSubmissionUIBuilder, modifiedBatchSubmission),
                                                this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void getFromDialog()
    {
        super();
        if (!allowBatchSubmission)
        {
            var submissionDataContact = this.dataContractObject() as BusinessDocumentSubmissionContract;
            if (submissionDataContact != null)
            {
                submissionDataContact.parmBatchSubmission(NoYes::No);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedBatchSubmission</Name>
				<Source><![CDATA[
    private boolean modifiedBatchSubmission(FormControl _formControl)
    {
        this.updateDesign();
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedResubmit</Name>
				<Source><![CDATA[
    private boolean modifiedResubmit(FormControl _formControl)
    {
        this.updateDesign();
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    private void updateDesign()
    {
        boolean resubmitFromFailedActionEnabled = allowResubmitFromFailedAction 
            && resubmitField.value() == NoYes::Yes 
            && (!allowBatchSubmission || batchSubmissionField.value() == NoYes::No);

        if (!resubmitFromFailedActionEnabled)
        {
            resubmitFromFailedActionField.value(NoYes::No);
        }
        resubmitFromFailedActionField.enabled(resubmitFromFailedActionEnabled);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>