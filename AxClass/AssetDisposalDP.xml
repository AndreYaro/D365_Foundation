<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDisposalDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The Report Data Provider <c>AssetDisposalDP</c> class declares table for the <c>AssetDisposal</c> Report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(AssetDisposal)),
    SRSReportParameterAttribute(classstr(AssetDisposalContract))
]
public class AssetDisposalDP extends SRSReportDataProviderPreProcessTempDb
{
    AssetDisposalTmp assetDisposalTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssetDisposalTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Fetches data from <c>AssetDisposalTmp</c> table.
    /// </summary>
    /// <returns>
    ///     <c>AssetDisposalTmp</c> data for the report.
    /// </returns>
    [
        SRSReportDataSetAttribute(tableStr(AssetDisposalTmp))
    ]
    public AssetDisposalTmp getAssetDisposalTmp()
    {
        select assetDisposalTmp;
        return assetDisposalTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssetDisposalTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Inserts all the values into <c>AssetDisposalTmp</c> temp table.
    /// </summary>
    /// <param name="_assetDisposal">
    ///     The <c>AssetDisposal</c> field to which to assign a value.
    /// </param>
    public void insertAssetDisposalTmp(AssetDisposal _assetDisposal)
    {
        assetDisposalTmp.clear();

        assetDisposalTmp.AssetGroup             = _assetDisposal.AssetGroup;
        assetDisposalTmp.AssetId                = _assetDisposal.AssetId;
        assetDisposalTmp.BookId                 = _assetDisposal.BookId;
        assetDisposalTmp.DisposalDate           = _assetDisposal.DisposalDate;
        assetDisposalTmp.AssetName              = _assetDisposal.Name;
        assetDisposalTmp.Status                 = _assetDisposal.Status;
        assetDisposalTmp.NetBookValue           = _assetDisposal.assetNetBookValue();
        assetDisposalTmp.ProfitLossValue        = _assetDisposal.assetProfitLossValue();
        assetDisposalTmp.SaleValue              = _assetDisposal.assetSaleValue();
        assetDisposalTmp.NetBookValueReportingCurrency = _assetDisposal.assetNetBookValueReportingCurrency();
        assetDisposalTmp.ProfitLossValueReportingCurrency = _assetDisposal.assetProfitLossValueReportingCurrency();
        assetDisposalTmp.SaleValueReportingCurrency = _assetDisposal.assetSaleValueReportingCurrency();

        assetDisposalTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Fetches the required data for the report.
    /// </summary>
    public void processReport()
    {
        Query query;
        QueryRun queryRun;
        AssetDisposal assetDisposal;

        query = this.parmQuery();
        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            assetDisposal = queryRun.get(tableNum(AssetDisposal)) as AssetDisposal;

            this.insertAssetDisposalTmp(assetDisposal);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>