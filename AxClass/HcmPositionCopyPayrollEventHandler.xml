<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmPositionCopyPayrollEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
class HcmPositionCopyPayrollEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmPositionCopy_copyingPayroll</Name>
				<Source><![CDATA[
    /// <summary>
    /// The copyingPayroll event handler for the HcmPositionCopy class.
    /// </summary>
    /// <param name = "_positionRecId">The RecId of the position being copied.</param>
    /// <param name = "_destinationPositionRecId">The RecId of the new position being created.</param>
    /// <param name = "_asOfDateTime">The point in time to be used when reading the date effective information from the source table.</param>
    /// <param name = "_validFrom">The beginning utcDateTime value for the time state.</param>
    /// <param name = "_validTo">The ending utcDateTime value for the time state.</param>
    [SubscribesTo(classStr(HcmPositionCopy), delegateStr(HcmPositionCopy, copyingPayroll))]
    public static void HcmPositionCopy_copyingPayroll(HcmPositionRecId _positionRecId, HcmPositionRecId _destinationPositionRecId, utcdatetime _asOfDateTime, utcdatetime _validFrom, utcdatetime _validTo)
    {
        HcmPositionCopyPayrollEventHandler::copyPayrollPositionDetail(_positionRecId, _destinationPositionRecId, _asOfDateTime, _validFrom, _validTo);
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyPayrollPositionDetail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copy the <c>PayrollPositionDetails</c> data from the source position to destination position record.
    /// </summary>
    /// <param name = "_positionRecId">The RecId of the position being copied.</param>
    /// <param name = "_destinationPositionRecId">The RecId of the new position being created.</param>
    /// <param name = "_asOfDateTime">The point in time to be used when reading the date effective information from the source table.</param>
    /// <param name = "_validFrom">The beginning utcDateTime value for the time state.</param>
    /// <param name = "_validTo">The ending utcDateTime value for the time state.</param>
    private static void copyPayrollPositionDetail(HcmPositionRecId _positionRecId, HcmPositionRecId _destinationPositionRecId, utcdatetime _asOfDateTime, utcDateTime _validFrom, utcDateTime _validTo)
    {
        PayrollPositionDetails   payrollPositionDetails;
        PayrollPositionDetails   payrollPositionDetailsCopy;

        while select validTimeState(_asOfDateTime) payrollPositionDetails
            where payrollPositionDetails.Position == _positionRecId
        {
            buf2Buf(payrollPositionDetails, payrollPositionDetailsCopy);

            payrollPositionDetailsCopy.Position = _destinationPositionRecId;
            payrollPositionDetailsCopy.ValidFrom = DateTimeUtil::date(_validFrom);
            payrollPositionDetailsCopy.ValidTo = DateTimeUtil::date(_validTo);
            payrollPositionDetailsCopy.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>