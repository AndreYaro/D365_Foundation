<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSFulfillmentPolicySelectForm</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSFulfillmentPolicySelectForm</c> class contains the business logic of the <c>WHSFulfillmentPolicySelect</c> form.
/// </summary>
public class WHSFulfillmentPolicySelectForm
{
    WHSFulfillmentPolicyRecId selectedPolicy;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateFulfillmentPolicyData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the selected set of release lines with the new policy selected from the dialog 
    /// named select fulfillment policy in the release to warehouse form.
    /// </summary>
    /// <param name="_args">
    /// The WHSFulfillmentPolicySelect form related data.
    /// </param>
    /// <param name="_selectedFulfillmentPolicyRecId">
    /// The recId of the selected fulfillment policy which will be assigned to the selected lines.
    /// </param>
    public void updateFulfillmentPolicyData(Args _args, WHSFulfillmentPolicyRecId _selectedFulfillmentPolicyRecId)
    {
        selectedPolicy = _selectedFulfillmentPolicyRecId;

        if (FormDataUtil::isFormDataSource(_args.record()))
        {
            FormDataSource formDataSource = _args.record().dataSource();

            if (formDataSource.table() == tableNum(WHSTmpReleaseToWarehouse))
            {
                for (WHSTmpReleaseToWarehouse releaseToWarehouse = this.getBufferFromDataSource(formDataSource);
                    releaseToWarehouse; 
                    releaseToWarehouse = formDataSource.getNext())
                {
                    if (this.validateCanAssignPolicy(releaseToWarehouse))
                    {
                        WHSTmpReleaseToWarehouse::assignFulfillmentPolicy(selectedPolicy, releaseToWarehouse);
                    }
                    else
                    {
                        throw error(strfmt("@WAX:Table_ErrorFulfillmentTypeNotSupportedForTransferOrders_Label", WHSFulfillmentPolicy::find(selectedPolicy).FulfillmentType));
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBufferFromDataSource</Name>
				<Source><![CDATA[
    private Common getBufferFromDataSource(FormDataSource _dataSource)
    {
        return _dataSource.getFirst(true) ? _dataSource.getFirst(true) : _dataSource.cursor();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCanAssignPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates whether the fulfillment type related to fulfillment policy associated 
    /// to the release line belonging to transfer order in release to warehouse form 
    /// is valid or not.
    /// </summary>
    /// <param name="_releaseToWarehouse">
    /// The buffer of the line to be released.
    /// </param>
    /// <returns>
    /// true if at least one line got released; otherwise, false.
    /// </returns>
    private boolean validateCanAssignPolicy(WHSTmpReleaseToWarehouse _releaseToWarehouse)
    {
        if (_releaseToWarehouse.Module == WHSModule::Transfer
            && !WHSFulfillmentPolicy::find(selectedPolicy).canTypeBeUsedForTransferOrders())
        {
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>