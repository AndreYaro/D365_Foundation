<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TsTimesheetAddFavorites</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TsTimesheetAddFavorites</c> class adds multiple selected timesheet line records to the
/// <c>TSTimesheetFavorites</c> table.
/// </summary>
public class TsTimesheetAddFavorites
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromTSTimesheetLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initialize the <c>TSTimesheetFavorites</c>. 
    /// </summary>
    /// <param name = "_timesheetLine">
    ///     A <c>TSTimesheetLine</c> record.
    /// </param>
    /// <returns>
    ///     The <c>TSTimesheetFavorites</c> buffer.
    /// </returns>
    protected static TSTimesheetFavorites initFromTSTimesheetLine(TSTimesheetLine _timesheetLine)
    {
        TSTimesheetFavorites timesheetFavorites;

        timesheetFavorites.ProjId            = _timesheetLine.ProjId;
        timesheetFavorites.ActivityNumber    = _timesheetLine.ActivityNumber;
        timesheetFavorites.CategoryId        = _timesheetLine.CategoryId;
        timesheetFavorites.Resource          = _timesheetLine.Resource;
        timesheetFavorites.TableGroupAll     = TableGroupAll::Table;
        timesheetFavorites.ProjectDataAreaId = _timesheetLine.ProjectDataAreaId;
        return timesheetFavorites;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addToFavorites</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the <c>TSTimesheetLine</c> record information to the <c>TSTimesheetFavorites</c> table.
    /// </summary>
    /// <param name="_timesheetLine">
    /// A <c>TSTimesheetLine</c> record.
    /// </param>
    /// <returns>
    /// true if the <c>TSTimesheetFavorites</c> record is added; otherwise, false.
    /// </returns>
    public static boolean addToFavorites(TSTimesheetLine _timesheetLine)
    {
        if (!TSTimesheetFavorites::existFavorite(_timesheetLine))
        {
            TSTimesheetFavorites timesheetFavorites = TSTimesheetAddFavorites::initFromTSTimesheetLine(_timesheetLine);

            if (TSResource::resourceIsWorkerInCurrentCompany(_timesheetLine.Resource))
            {
                timesheetFavorites.doInsert();
                return true;
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        Common common;
        int counter;

        if (_args)
        {
            if (_args.multiSelectionContext() != null && _args.multiSelectionContext().getFirst() != null)
            {
                for (common = _args.multiSelectionContext().getFirst(); common; common = _args.multiSelectionContext().getNext())
                {
                    if (TSTimesheetAddFavorites::addToFavorites(common))
                    {
                        counter++;
                    }
                }
            }
            else
            {
                if (_args.record())
                {
                    if (TSTimesheetAddFavorites::addToFavorites(_args.record()))
                    {
                        counter++;
                    }
                }
            }
        }

        info(strFmt("@SYS333663", counter));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>