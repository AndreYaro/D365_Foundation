<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchVendorPortalPurchTableAllVersionsFilterStrategy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///   The <c>PurchVendorPortalPurchTableAllVersionsFilterStrategy</c> class applies a correct range to
///   the <c>PurchTableAllVersions</c> table.
/// </summary>
[
    SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::String, formStr(PurchVendorPortalRequests)),
    SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::String, formStr(PurchVendorPortalConfirmedOrders)),
    SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::String, queryStr(PurchVendorPortalRequestsPending)),
    SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::String, queryStr(PurchVendorPortalConfirmedOpenOrders))
]
public class PurchVendorPortalPurchTableAllVersionsFilterStrategy extends SysFilterStructureStrategyQuery implements SysIFilterStructureStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resolve</Name>
				<Source><![CDATA[
    /// <summary>
    /// Resolves the <c>SysIFilterValueType</c> class and adds ranges to a <c>Query</c> class.
    /// </summary>
    /// <param name="_valueType">
    /// The <c>SysIFilterValueType</c> class to be resolved.
    /// </param>
    /// <returns>
    /// true if ranges have been added; otherwise, false.
    /// </returns>
    protected boolean resolve(SysIFilterValueType _valueType)
    {
        if (_valueType.parmTypeIdentifier() == extendedTypeNum(VendAccount))
        {
            QueryBuildDataSource purchTableAllVersionsDataSource = query.dataSourceTable(tableNum(PurchTableAllVersions));

            if (PurchVendorPortalFilterStrategyExcludeOrFrameworkConditionFlight::instance().isEnabled())
            {
                QueryBuildRange qbr = SysQueryBase::findOrCreateRange(purchTableAllVersionsDataSource, fieldNum(PurchTableAllVersions, OrderAccount));

                qbr.name(fieldId2Name(purchTableAllVersionsDataSource.table(), fieldNum(PurchTableAllVersions, OrderAccount))
                    + SysFilterConstants::FrameworkDatasourceSuffix);
                qbr.enabled(true);
                qbr.status(RangeStatus::Hidden);

                // (targetTable.TargetField <operator> sysFilterTable.ValueField)
                qbr.value(strfmt('(%1.%2 %3 %4.%5)',
                    purchTableAllVersionsDataSource.name(),
                    fieldId2Name(purchTableAllVersionsDataSource.table(), fieldNum(PurchTableAllVersions, OrderAccount)),
                    SysFilterOperatorHelper::operator2SqlStr(filterStructureItem.parmOperator()),
                    filterDatasource.name(),
                    fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId())));
            }
            else
            {
                this.addLinkFromTableToFilter(purchTableAllVersionsDataSource, fieldNum(PurchTableAllVersions, OrderAccount));
            }

            return true;
        }
        
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>