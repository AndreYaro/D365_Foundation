<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchaseOrderVendorCollaborationValidator</Name>
	<SourceCode>
		<Declaration><![CDATA[
public static class PurchaseOrderVendorCollaborationValidator
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateVendorCollaborationResponse</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the latest purchase order version is confirmed by a vendor.
    /// </summary>
    /// <param name = "_purchTable">Purchase order header record.</param>
    /// <returns>
    ///  true if the latest purchase order version is confirmed by a vendor; otherwise, false.
    /// </returns>
    public static boolean validateVendorCollaborationResponse(PurchTable _purchTable)
    {
        boolean ok = true;

        if (VendTable::find(_purchTable.OrderAccount).VendVendorCollaborationType != VendVendorCollaborationType::Disabled)
        {
            ok = false;
            if ( _purchTable.DocumentState == VersioningDocumentState::InExternalReview)
            {
                PurchaseOrderResponseVersion response =
                    PurchTableVersion::findLatest(_purchTable.PurchId, _purchTable.DataAreaId).findLatestResponseVersion();
                ok = response.RecId 
                    && (response.ResponseState == PurchaseOrderResponseState::Confirmed
                    || (response.ResponseState == PurchaseOrderResponseState::AcknowledgedWithChange
                        && _purchTable.responseHeader().ConsumptionState == ConsumptionState::Consumed));
            }
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateVendorCollaborationResponses</Name>
				<Source><![CDATA[
    public static boolean validateVendorCollaborationResponses(ParmId _parmId)
    {
        boolean                 ok = true;
        PurchParmTable          purchParmTable;
        PurchTable              purchTable;

        while select purchTable
            join purchParmTable
                where purchParmTable.ParmId == _parmId
                     && purchParmTable.PurchId == purchTable.PurchId
                     && purchParmTable.DataAreaId == purchTable.DataAreaId
        {
            if (!PurchaseOrderVendorCollaborationValidator::validateVendorCollaborationResponse(purchTable))
            {
                ok = false;
                break;
            }
        }

        return ok;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>