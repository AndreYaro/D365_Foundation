<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSLoadTenderController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TMSLoadTenderController</c> class is used to run print management for <c>TMSLoadTender</c> report.
/// </summary>
class TMSLoadTenderController extends SrsPrintMgmtController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        this.setRanges();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPrintMgmt</Name>
				<Source><![CDATA[
    protected void runPrintMgmt()
    {
        printMgmtReportRun = PrintMgmtReportRun::construct(PrintMgmtHierarchyType::TMS, PrintMgmtNodeType::TMS, PrintMgmtDocumentType::TMSLoadTender);

        printMgmtReportRun.parmReportRunController(this);

        printMgmtReportRun.load(this.parmArgs().record(), this.parmArgs().record(), Global::currentUserLanguage());

        this.outputReports();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    private void setRanges()
    {
        var q                       = this.getFirstQuery();
        var queryBuildDataSource    = SysQuery::findOrCreateDataSource(q, tableNum(WHSLoadTable));
        var queryBuildRange         = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSLoadTable, LoadId));

        if (this.parmArgs().parm())
        {
            queryBuildRange.value(queryValue(this.parmArgs().parm()));
        }
        else if (this.parmArgs().record())
        {
            switch (this.parmArgs().dataset())
            {
                case tableNum(TMSTransportationTender):
                    TMSTransportationTender transportationTender = this.parmArgs().record();
                    str loadRange;

                    switch (transportationTender.TenderRefType)
                    {
                        case TMSTenderRefType::Load:
                            loadRange = queryValue(transportationTender.RefId);
                            break;
                        case TMSTenderRefType::RouteSegLoad:
                            loadRange = queryValue(transportationTender.LoadId);
                            break;
                        case TMSTenderRefType::Route:
                        case TMSTenderRefType::RouteSegment:
                            TMSRouteSegmentLoad routeSegmentLoad;
                            while select Id from routeSegmentLoad
                                where routeSegmentLoad.RouteCode == transportationTender.RefId
                                    && (transportationTender.TenderRefType == TMSTenderRefType::Route
                                        || routeSegmentLoad.Sequence == transportationTender.Sequence)
                            {
                                loadRange = queryRangeConcat(loadRange, routeSegmentLoad.Id);
                            }
                            break;
                        default:
                            throw error(Error::wrongUseOfFunction(enumStr(TMSTenderRefType)));
                    }

                    queryBuildRange.value(loadRange);
                    break;
                case tableNum(WHSLoadTable):
                    WHSLoadTable whsLoadTable = this.parmArgs().record();
                    queryBuildRange.value(whsLoadTable.LoadId);
                    break;
                default:
                    throw error(Error::wrongUseOfFunction(tableId2Name(this.parmArgs().dataset())));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TMSLoadTenderController construct()
    {
        return new TMSLoadTenderController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>