<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPRPeriodicCreateQueryBuilder_Vend</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestSourceTypeAttribute(CFMPaymentRequestSourceType::VendInvoice)]
public class CFMPRPeriodicCreateQueryBuilder_Vend extends CFMPRPeriodicCreateQueryBuilder
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPaymentRequestSourceQuery</Name>
				<Source><![CDATA[
    public Query getPaymentRequestSourceQuery()
    {
        Query sourceQuery = new Query();

        QueryBuildDataSource qbds = sourceQuery.addDataSource(tableNum(VendTrans));

        QueryBuildRange qbr = qbds.addRange(fieldNum(VendTrans, Invoice));
        qbr.value(SysQuery::valueNotEmptyString());

        qbds = qbds.addDataSource(tableNum(VendTransOpen));

        qbr = qbds.addRange(fieldNum(VendTransOpen, AmountCur));
        qbr.value(#negative);

        qbr = qbds.addRange(fieldNum(VendTransOpen, DueDate));

        if (dateTo == dateNull())
        {
            qbr.value(SysQuery::range(dateFrom, maxDate()));
        }
        else
        {
            qbr.value(SysQuery::range(dateFrom, dateTo));
        }

        qbds.addLink(fieldNum(VendTrans, RecId), fieldNum(VendTransOpen, RefRecId));
        qbds.addLink(fieldNum(VendTrans, AccountNum), fieldNum(VendTransOpen, AccountNum));
        qbds.joinMode(JoinMode::InnerJoin);

        qbds = qbds.addDataSource(tableNum(CFMPaymentRequestTable));

        qbr = qbds.addRange(fieldNum(CFMPaymentRequestTable, SourceType));
        qbr.value(queryValue(CFMPaymentRequestSourceType::VendInvoice));

        qbds.addLink(fieldNum(VendTransOpen, RecId), fieldNum(CFMPaymentRequestTable, SourceRefRecId));
        qbds.joinMode(JoinMode::NoExistsJoin);

        this.addQueryDataAreaFiltering(sourceQuery);

        return sourceQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryDataSourceTableId</Name>
				<Source><![CDATA[
    public TableId getQueryDataSourceTableId()
    {
        return tableNum(VendTransOpen);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>