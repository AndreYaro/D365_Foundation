<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMDistributionPercentCalc_CustVendTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class calculates an amount distribution percent for vendor Trans or customer trans.
/// </summary>
[CFMDistributionPercentSourceAttribute(NoYes::No, tableStr(VendTrans)),
CFMDistributionPercentSourceAttribute(NoYes::No, tableStr(CustTrans))]
public class CFMDistributionPercentCalc_CustVendTrans extends CFMDistributionPercentCalc
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>custVendTrans</Name>
				<Source><![CDATA[
    private CustVendTrans custVendTrans()
    {
        return source;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillDistribution</Name>
				<Source><![CDATA[
    protected void fillDistribution()
    {
        AmountCur amountToDistribute = this.paymentSign() * this.custVendTrans().AmountCur;
        CFMLedgerDimension cfmLedgerDimensionHeader = cfmDimensionConverter.convertDefaultDimension(this.custVendTrans().DefaultDimension);
        TaxTrans taxTrans;

        while select TaxDirection, TaxGroup, TaxItemGroup, SourceRegulateAmountCur, SourceBaseAmountCur from taxTrans
            where taxTrans.Voucher == this.custVendTrans().Voucher
               && taxTrans.TransDate == this.custVendTrans().TransDate
        {
            AmountCur amountCur = taxTrans.SourceBaseAmountCur;

            if (taxTrans.TaxDirection != TaxDirection::UseTax)
            {
                amountCur += taxTrans.SourceRegulateAmountCur;
            }

            this.addAmount(cfmLedgerDimensionHeader,
                taxTrans.TaxGroup,
                taxTrans.TaxItemGroup,
                amountCur);

            amountToDistribute -= amountCur;
        }

        //add the part without taxes left in transaction
        this.addAmount(cfmLedgerDimensionHeader,
            '',
            '',
            amountToDistribute);
    }

]]></Source>
			</Method>
			<Method>
				<Name>paymentSign</Name>
				<Source><![CDATA[
    protected Sign paymentSign()
    {
        return -1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceCurrencyCode</Name>
				<Source><![CDATA[
    protected CurrencyCode sourceCurrencyCode()
    {
        return this.custVendTrans().CurrencyCode;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>