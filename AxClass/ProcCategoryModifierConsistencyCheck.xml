<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProcCategoryModifierConsistencyCheck</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class ProcCategoryModifierConsistencyCheck extends SysConsistencyCheck
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        boolean doFix = this.checkFix() == CheckFix::Fix;
        int numFixed = this.updateEcoResCategoryReferences(doFix);

        if (doFix)
        {
            this.updateCorrected(numFixed);
        }

        this.updateNotCorrected();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS321171";
    }

]]></Source>
			</Method>
			<Method>
				<Name>helpText</Name>
				<Source><![CDATA[
    public static HelpTxt helpText()
    {
        return "@SCM:HelpText_ProcCategoryModifierConsistencyCheck";
    }

]]></Source>
			</Method>
			<Method>
				<Name>treeParentClassId</Name>
				<Source><![CDATA[
    public ClassId treeParentClassId()
    {
        return classNum(EcoResCategoryConsistencyCheck);
    }

]]></Source>
			</Method>
			<Method>
				<Name>configurationKey</Name>
				<Source><![CDATA[
    public ConfigurationKeyId configurationKey()
    {
        return configurationKeyNum(LogisticsBasic);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateEcoResCategoryReferences</Name>
				<Source><![CDATA[
    private int updateEcoResCategoryReferences(boolean _doFix)
    {
        int numFixed = 0;
        EcoResCategory ecoResCategory;
        ProcCategoryModifier procCategoryModifier;
        RecId procurementHierarchyRecId = EcoResCategoryHierarchyRole::getHierarchiesByRole(EcoResCategoryNamedHierarchyRole::Procurement).CategoryHierarchy;
        
        while select ecoResCategory
            where ecoResCategory.CategoryHierarchy == procurementHierarchyRecId
            notexists join procCategoryModifier where ecoResCategory.RecId == procCategoryModifier.Category
        {
            if (_doFix)
            {
                this.fixCategoryReference(ecoResCategory);
                numFixed++;
            }
            else
            {
                warning(strFmt("@SCM:ProcCategoryModifierConsistencyCheckWarning", tablePName(ProcCategoryModifier), tablePName(EcoResCategory), ecoResCategory.Name));
            }
        }

        return numFixed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fixCategoryReference</Name>
				<Source><![CDATA[
    private void fixCategoryReference(EcoResCategory _ecoResCategory)
    {
        ProcCategoryModifier procCategoryModifierInsert;
        
        procCategoryModifierInsert.initValue();
        procCategoryModifierInsert.Category = _ecoResCategory.RecId;
        procCategoryModifierInsert.insert();

        warning(strFmt("@SCM:ProcCategoryModifierConsistencyCheckWarningFixed", tablePName(ProcCategoryModifier), _ecoResCategory.Name));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>