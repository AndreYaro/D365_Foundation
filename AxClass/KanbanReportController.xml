<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>KanbanReportController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    This <c>KanbanReportController</c> class is a controller base class for kanban reports.
/// </summary>
public abstract class KanbanReportController extends SrsReportRunController
{
    SRSPrintDestinationSettings     defaultPrintSettings;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkPrintMediumType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether the print medium can be used.
    /// </summary>
    /// <returns>
    /// true if the print medium can be used; otherwise, false.
    /// </returns>
    protected boolean checkPrintMediumType()
    {
        boolean result = true;

        if (   defaultPrintSettings
            && defaultPrintSettings.printMediumType() == SRSPrintMediumType::Screen
            && !hasGUI())
        {
            result = checkFailed(strFmt("@SYS345089",defaultPrintSettings.printMedium().label()));
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDefaultPrintSettings</Name>
				<Source><![CDATA[
        public SRSPrintDestinationSettings parmDefaultPrintSettings(SRSPrintDestinationSettings _defaultPrintSettings = defaultPrintSettings)
    {
        defaultPrintSettings = _defaultPrintSettings;
        return defaultPrintSettings;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the ability to modify the report contract before dialog box is shown to user.
    /// </summary>
    /// <remarks>
    /// Use this method to change the report contract. A common use case is to add or change a query.It is
    /// important to notice that this method is called only during an interactive scenario before rendering
    /// the UI to a user.For a batch scenario, we do not call this method because there is no user
    /// interaction and the contract values are already saved in the batch.Do not instantiate any instance
    /// variables in this code if they are used during the lifetime of a controller because they will fail
    /// in batch cases.
    /// </remarks>
    protected void prePromptModifyContract()
    {
        SRSPrintDestinationSettings srsPrintDestinationSettings = this.parmDefaultPrintSettings();

        super();

        if (srsPrintDestinationSettings)
        {
            this.addSysOperationDataContractInfo(srsPrintDestinationSettings,dataContractInfoObjects);
            this.parmReportContract().parmPrintSettings(srsPrintDestinationSettings);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>print</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Prints a report.
    /// </summary>
    protected void print()
    {
        if (this.checkPrintMediumType())
        {
            this.startOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrintDestinationSettings</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets print destination settings for the report.
    /// </summary>
    /// <param name="_location">
    ///    Information about where to print the data.
    /// </param>
    /// <param name="_workCell">
    ///    The name of the work cell from which records are selected.
    /// </param>
    /// <returns>
    ///    The print destination settings.
    /// </returns>
    public static SRSPrintDestinationSettings getPrintDestinationSettings(
        KanbanPrintLocation _location,
        WrkCtrIdBase        _workCell = '')
    {
        SRSPrintDestinationSettings         settings;
        WrkCtrResourceGroup                 wrkCtrResourceGroup;
        WrkCtrResourceGroupPrinterSettings  wrkCtrResourceGroupPrinterSettings;

        switch (_location)
        {
            case KanbanPrintLocation::WorkCell:
                select firstonly wrkCtrResourceGroupPrinterSettings
                    exists join  wrkCtrResourceGroup
                        where wrkCtrResourceGroup.RecId    == wrkCtrResourceGroupPrinterSettings.WrkCtrResourceGroup
                           && wrkCtrResourceGroup.WrkCtrId == _workCell;

                if (   wrkCtrResourceGroupPrinterSettings
                    && wrkCtrResourceGroupPrinterSettings.PrinterSettings != conNull())
                {
                    settings = new SRSPrintDestinationSettings(wrkCtrResourceGroupPrinterSettings.PrinterSettings);
                }
                else
                {
                    settings = new SRSPrintDestinationSettings();
                }
                break;

            case KanbanPrintLocation::Default:
                settings = null;    // Set to null to retrieve last user values
                break;

            case KanbanPrintLocation::UnitTest:
                settings = new SRSPrintDestinationSettings();
                settings.printMediumType(SRSPrintMediumType::Custom);
                break;

            default:
                throw error(Error::wrongUseOfFunction(funcName()));
        }

        return settings;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>