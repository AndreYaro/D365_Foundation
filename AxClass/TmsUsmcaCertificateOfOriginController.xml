<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TmsUsmcaCertificateOfOriginController</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class TmsUsmcaCertificateOfOriginController extends SrsReportRunController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        this.setShowDialogFromArgs();
        this.setRanges();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setShowDialogFromArgs</Name>
				<Source><![CDATA[
    public void setShowDialogFromArgs()
    {
        if (this.parmArgs())
        {
            this.parmShowDialog(this.parmArgs().parmEnum());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    private void setRanges()
    {
        Query q = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = SysQuery::findOrCreateDataSource(q, tableNum(WHSShipmentTable));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSShipmentTable, ShipmentId));

        if (this.parmArgs().parm())
        {
            queryBuildRange.value(queryValue(this.parmArgs().parm()));
        }
        else if (this.parmArgs().record() && this.parmArgs().dataset() == tableNum(WHSShipmentTable))
        {
            WHSShipmentTable whsShipmentTable = this.parmArgs().record();
            queryBuildRange.value(whsShipmentTable.ShipmentId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initailzes the controller, sets the print management, assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        #ISOCountryRegionCodes
        GlobalizationInstrumentationHelper::featureRunByCountryRegionCodes([
            [#isoUS, GlobalizationConstants::FeatureReferenceUS00002] ],
            funcName()
        );

        TmsUsmcaCertificateOfOriginController TmsUsmcaCertificateOfOriginController;

        if (_args.parm()
            || (_args.record() && _args.dataset() == tableNum(WHSShipmentTable)))
        {
            TmsUsmcaCertificateOfOriginController = new TmsUsmcaCertificateOfOriginController();
            TmsUsmcaCertificateOfOriginController.parmArgs(_args);
            TmsUsmcaCertificateOfOriginController.parmReportName(ssrsReportStr(TmsUsmcaCertificateOfOrigin, Report));
            TmsUsmcaCertificateOfOriginController.parmShowDialog(true);
            TmsUsmcaCertificateOfOriginController.startOperation();
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>