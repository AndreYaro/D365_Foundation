<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendOutPaym_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
abstract class VendOutPaym_RU extends VendOutPaym
{
    BankPaymentOrderJour_RU      paymentOrderJour;
    NoYes                        printWarrantText;
    DialogField                  dialogPrintWarrantText;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        printWarrantText
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBankAccountNumber</Name>
				<Source><![CDATA[
        public boolean checkBankAccountNumber()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>close</Name>
				<Source><![CDATA[
        public void close()
    {
        if (outPaymRecordList.elements())
        {
            this.printDocument();
        }
        else
        {
            info(strFmt("@GLS106940", this.interfaceName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillPaymentOrder</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        protected void fillPaymentOrder(LedgerJournalTrans    _ledgerJournalTrans,
                                    CustVendOutPaymRecord _vendOutPaymRecord)
    {
        BankGroup       bankGroup;
        BankAccountMap  bankAccountMap      = _vendOutPaymRecord.parmCustVendPaym().bankAccountMap();
        AccountNum      accountNum          = _vendOutPaymRecord.parmCustVendPaym().custVendTable().AccountNum;
        AccountNum      bankPrintAccountNum = _vendOutPaymRecord.parmCustVendPaym().ledgerJournalTrans().BankPrintAccountNum_RU;
        ;

        paymentOrderJour.PaymentOrderNum    = this.paymentOrderNum(_ledgerJournalTrans);

        paymentOrderJour.initFromLedgerJournalTrans(_ledgerJournalTrans);

        paymentOrderJour.AmountCur           = _vendOutPaymRecord.parmCustVendPaym().paymAmountInForeignCurrency();
        paymentOrderJour.CurrencyCode        = Currency::findCurrencyCodeISO(_vendOutPaymRecord.parmCustVendPaym().paymCurrencyCode()).CurrencyCode;
        paymentOrderJour.BankIdPayer         = _vendOutPaymRecord.parmCustVendPaym().bankAccountTable().AccountID;
        paymentOrderJour.PurposeOfPayment    = _vendOutPaymRecord.parmCustVendPaym().paymPurposeText();
        paymentOrderJour.AccountNumRecipient = bankPrintAccountNum;
        paymentOrderJour.CompanyRecipient    = _vendOutPaymRecord.parmCustVendPaym().ledgerJournalTrans().Company;
        paymentOrderJour.CustVendPaymAccount = accountNum;
        paymentOrderJour.BankIdRecipient     = bankAccountMap.AccountID;
        paymentOrderJour.ElectronicPayment   = this.parmClientBank_RU();

        if (printWarrantText && ! this.parmClientBank_RU())
        {
            if (paymentOrderJour.CompanyRecipient != curext())
            {
                changecompany(paymentOrderJour.CompanyRecipient)
                {
                    paymentOrderJour.PurposeOfPayment += strfmt("@GLS112132", CompanyInfo::find().Name);
                }
            }
            else if (bankPrintAccountNum)
            {
                paymentOrderJour.PurposeOfPayment += strfmt("@GLS112132", VendTable::find(accountNum).name());
            }
        }

        if (! paymentOrderJour.AccountNumRecipient)
        {
            paymentOrderJour.AccountNumRecipient = accountNum;
        }

        if (bankAccountMap.ForeignBank_RU)
        {
            paymentOrderJour.AccountNumForeignRecipient = accountNum;

            bankGroup = BankGroup::findByCompany_W(bankAccountMap.company(), bankAccountMap.ForeignBank_RU);
            paymentOrderJour.CorrBankRecipient = paymentOrderJour.AccountNumRecipient == bankGroup.VendAccountNum_RU;

            if (bankPrintAccountNum && ! paymentOrderJour.CorrBankRecipient)
            {
                paymentOrderJour.AccountNumForeignRecipient = bankPrintAccountNum;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
        public boolean getFromDialog()
    {
        boolean ret = super();

        if (ret)
        {
            printWarrantText  = dialogPrintWarrantText.value();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>numSeqTypeId</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        abstract public int numSeqTypeId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>open</Name>
				<Source><![CDATA[
        public void open()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
        public container pack()
    {
        return [#CurrentVersion, #CurrentList, super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>paymentOrderNum</Name>
				<Source><![CDATA[
        private BankPaymentOrder_RU paymentOrderNum(LedgerJournalTrans  _ledgerJournalTrans)
    {
        NumberSeq           numberSeq;
        BankAccountTable    bankAccountTable;
        BankPaymentOrder_RU paymentOrderNum;

        if (! _ledgerJournalTrans.DocumentNum)
        {
            bankAccountTable = BankAccountTable::find(_ledgerJournalTrans.parmOffsetAccount());
            if (bankAccountTable.PaymOrderNumSeqId_W)
            {
                numberSeq = NumberSeq::newGetNumFromCode(bankAccountTable.PaymOrderNumSeqId_W);
            }
            else
            {
                numberSeq = NumberSeq::newGetNum(NumberSeqReference::findReference(this.numSeqTypeId()));
            }

            paymentOrderNum = numberSeq.num();
        }
        else
        {
            paymentOrderNum = _ledgerJournalTrans.DocumentNum;
        }
        return paymentOrderNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>printDocument</Name>
				<Source><![CDATA[
        public void printDocument()
    {
        ;

        if (printDocument)
        {
            this.runReport(menuitemOutputStr(CustVendOutPaymReport_RU));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>releasePaymentOrderNum_RU</Name>
				<Source><![CDATA[
        public void releasePaymentOrderNum_RU(LedgerJournalTrans   _ledgerJournalTrans,
                                          BankPaymentOrder_RU  _orderNum)
    {
        NumberSequenceCode  numberSequenceCode;
        BankAccountTable    bankAccountTable;

        super(_ledgerJournalTrans, _orderNum);

        if (!_ledgerJournalTrans.DocumentNum)
        {
            bankAccountTable = BankAccountTable::find(_ledgerJournalTrans.parmOffsetAccount());
            if (bankAccountTable.PaymOrderNumSeqId_W)
            {
                numberSequenceCode = bankAccountTable.PaymOrderNumSeqId_W;
            }
            else
            {
                numberSequenceCode = NumberSeqReference::findReference(this.numSeqTypeId()).numberSequenceTable().NumberSequence;
            }

            try
            {
                NumberSeq::release(numberSequenceCode, _orderNum);
            }
            catch (Exception::Error)
            {
                error(strFmt("@GLS105137", numberSequenceCode, _orderNum));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
        public boolean unpack(container packedClass)
    {
        Integer   version = RunBase::getVersion(packedClass);
        container con;

        switch (version)
        {
            case #CurrentVersion :

                [version, #CurrentList, con] = packedClass;
                super(con);

                break;

            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLedgerJournalTrans</Name>
				<Source><![CDATA[
        protected void updateLedgerJournalTrans(LedgerJournalTrans    _ledgerJournalTrans,
                                            CustVendOutPaymRecord _vendOutPaymRecord,
                                            boolean               _generateRemittanceFeeTrans)
    {
        LedgerJournalTrans ledgerJournalTrans = _ledgerJournalTrans;
        ;

        this.fillPaymentOrder(ledgerJournalTrans, _vendOutPaymRecord);
        paymentOrderJour.insert();

        if (! ledgerJournalTrans.PaymReference)
        {
            ledgerJournalTrans.PaymReference    = paymentOrderJour.PaymentOrderNum;
        }
        if (! ledgerJournalTrans.BankChequeNum)
        {
            ledgerJournalTrans.BankChequeNum    = paymentOrderJour.PaymentOrderNum;
        }

        ledgerJournalTrans.DocumentDate = paymentOrderJour.PaymentTransDate;
        ledgerJournalTrans.DocumentNum  = paymentOrderJour.PaymentOrderNum;

        super (_ledgerJournalTrans, _vendOutPaymRecord, _generateRemittanceFeeTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
        public boolean validate(Object calledFrom = null)
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>