<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppServiceDecoratorRuleLabelListDisplayArea</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSMobileAppServiceDecoratorRuleLabelListDisplayArea</c> class populates the DisplayArea and AttachedTo Attributes for the WorkList pattern.
/// </summary>
public class WHSMobileAppServiceDecoratorRuleLabelListDisplayArea implements WHSIMobileAppServiceXMLDecoratorRule
{
    #WHSRF
    private const ExtendedTypeId WaveLabelBuildExtendedType = extendedTypeNum(WHSWaveLabelBuildId);

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WHSMobileAppServiceDecoratorRuleLabelListDisplayArea construct()
    {
        return new WHSMobileAppServiceDecoratorRuleLabelListDisplayArea();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isLastControl</Name>
				<Source><![CDATA[
    private boolean isLastControl(Map _controlMap)
    {
        return this.compareControlName(_controlMap, WHSMobileAppControlNames::RFPrev);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isHeaderControl</Name>
				<Source><![CDATA[
    private boolean isHeaderControl(Map _controlMap)
    {
        return this.compareControlName(_controlMap, "@WAX:WaveLabelList");
    }

]]></Source>
			</Method>
			<Method>
				<Name>isLabelGroupControl</Name>
				<Source><![CDATA[
    private boolean isLabelGroupControl(Map _controlMap)
    {
        return _controlMap.lookup(#XMLControlInputType) == WaveLabelBuildExtendedType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>compareControlName</Name>
				<Source><![CDATA[
    private boolean compareControlName(Map _controlMap, WHSMobileAppControlName _targetName)
    {
        return _controlMap.lookup(#XMLControlName) == _targetName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSubHeaderControls</Name>
				<Source><![CDATA[
    private boolean setSubHeaderControls(ListEnumerator _listEnum)
    {
        while (_listEnum.moveNext())
        {
            Map controlMap = _listEnum.current();
            controlMap.insert(#XMLControlDisplayArea, WHSMobileAppXMLDisplayArea::SubHeaderArea);
            if (this.isHeaderControl(controlMap))
            {
                return true;
            }
        }
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findFirstLabelGroupControl</Name>
				<Source><![CDATA[
    private boolean findFirstLabelGroupControl(ListEnumerator _listEnum)
    {
        while (_listEnum.moveNext())
        {
            Map controlMap = _listEnum.current();
            if (this.isLabelGroupControl(controlMap))
            {
                return true;
            }
        }
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>decorateBody</Name>
				<Source><![CDATA[
    private void decorateBody(ListEnumerator _listEnum)
    {
        str currentLabelBuildId = '';
        do
        {
            Map controlMap = _listEnum.current();
            if (this.isLastControl(controlMap))
            {
                break;
            }
            if (this.isLabelGroupControl(controlMap))
            {
                currentLabelBuildId = controlMap.lookup(#XMLControlName);
            }
            controlMap.insert(#XMLControlAttachedTo, currentLabelBuildId);
        }
        while (_listEnum.moveNext());
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method to run the transformation.
    /// </summary>
    /// <param name = "_pageInfo">
    /// The <c>WHSMobileAppPageInfo</c> containing controls and information to carry out the transformation.
    /// </param>
    public void run(WHSMobileAppPageInfo _pageInfo)
    {
        ListEnumerator le = _pageInfo.parmControlsEnumerator();

        boolean labelListHeaderFound = this.setSubHeaderControls(le);

        if (labelListHeaderFound)
        {
            boolean labelGroupControlFound = this.findFirstLabelGroupControl(le);

            if (labelGroupControlFound)
            {
                this.decorateBody(le);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>