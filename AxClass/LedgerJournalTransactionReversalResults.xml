<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalTransactionReversalResults</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Journal Header provider for the transaction reversal results form.
/// </summary>
[ExportMetadataAttribute(formStr(TransactionReversalResults), TransactionReversalSourceType::LedgerJournalTrans)]
[ExportAttribute('Dynamics.AX.Application.TransactionReversalResultsBase')]
public final class LedgerJournalTransactionReversalResults extends TransactionReversalResultsBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getReversalExecutionId</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public guid getReversalExecutionId()
    {
        guid executionId;

        TransactionReversalContract callingFormContract = callingForm.args().parmObject() as TransactionReversalContract;

        if (callingFormContract)
        {
            executionId = callingFormContract.parmExecutionId();
        }
        else if (callingForm.args().record())
        {
            LedgerJournalTrans callingJournalLine = callingForm.args().record() as LedgerJournalTrans;

            if (callingJournalLine)
            {
                LedgerJournalTable callingJournalHeader = callingJournalLine.ledgerJournalTable();
                executionId = TransactionReversalExecutionDetail::getReversalExecutionIdForRecord(callingJournalHeader);
            }
        }

        Debug::assert(executionId != emptyGuid());

        return executionId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeTransactionReversalExecutionDetailDataSource</Name>
				<Source><![CDATA[
    public void initializeTransactionReversalExecutionDetailDataSource(FormDataSource _formDataSource)
    {
        super(_formDataSource);

        // Certain fields are not relevant to journal reversals and should not be shown
        _formDataSource.object(fieldNum(TransactionReversalExecutionDetail, JournalNumber)).visible(false);
        _formDataSource.object(fieldNum(TransactionReversalExecutionDetail, PostingType)).visible(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldFilterForReversalFailures</Name>
				<Source><![CDATA[
    protected boolean shouldFilterForReversalFailures()
    {
        return callingForm.args().menuItemName() == menuItemDisplayStr(LedgerJournalTransactionReversalFailures);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>