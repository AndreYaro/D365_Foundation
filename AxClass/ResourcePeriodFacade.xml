<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResourcePeriodFacade</Name>
	<SourceCode>
		<Declaration><![CDATA[
class ResourcePeriodFacade
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPeriodDateRange</Name>
				<Source><![CDATA[
    public static container getPeriodDateRange(
    ResourceRecId _resource,
    date _transactionDate,
    LegalEntityRecId _legalEntity = CompanyInfo::current())
{
    ProjPeriodTable projPeriodTable;
    date startDate;
    date endDate;

    PeriodRecId period = ResourceSetup::find(_resource).Period;
    if (period)
    {
        select firstonly crossCompany DataAreaId
            from projPeriodTable
            where projPeriodTable.RecId == period;

        if (projPeriodTable)
        {
            changecompany (projPeriodTable.DataAreaId)
            {
                ProjPeriodEmpl projPeriodEmpl = ProjPeriodEmpl::findTransdate(_resource, _transactionDate);
                startDate = projPeriodEmpl.PeriodFrom;
                endDate = projPeriodEmpl.periodTo();
            }
        }
    }

    return [startDate, endDate];
}

]]></Source>
			</Method>
			<Method>
				<Name>updatePeriod</Name>
				<Source><![CDATA[
    public static void updatePeriod(
    ResourceRecId _resource,
    date _transactionDate,
    LegalEntityRecId _legalEntity = CompanyInfo::current())
{
    ProjPeriodTable projPeriodTable;

    // If the worker tied to the transaction has a period code setup
    PeriodRecId period = ResourceSetup::find(_resource).Period;
    if (period)
    {
        select firstonly  crossCompany DataAreaId
            from projPeriodTable
            where projPeriodTable.RecId == period;

        if (projPeriodTable)
        {
            changecompany (projPeriodTable.DataAreaId)
            {
                // Get the worker period in which this transaction being posted falls in
                ProjPeriodEmpl projPeriodEmpl = ProjPeriodEmpl::findTransdate(_resource, _transactionDate);
                if (projPeriodEmpl.periodTo() >= _transactionDate)
                {
                    // Set the worker period record to posted on the current date, which is when this transaction is being posted
                    ProjPeriodEmpl::setPosted(_resource, projPeriodEmpl.PeriodFrom, DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()));
                }
            }
        }
    }
}

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
