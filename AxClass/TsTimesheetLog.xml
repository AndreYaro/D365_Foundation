<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TsTimesheetLog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Utility class for creating logs for timesheet.
/// </summary>
class TsTimesheetLog
{
    boolean preExistingLines[];
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>logPostLineInserts</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create logs for the lines created since preLineInserts were called
    /// </summary>
    /// <param name="_timesheetTableLog">
    /// the table log to link the line logs to.
    /// </param>
    public void logPostLineInserts(TsTimesheetTableLog _timesheetTableLog)
    {
        TsTimesheetLine     timesheetLine;
        TsTimesheetLineWeek timesheetLineWeek;

        if (ProjParameters::find().TimesheetAuditTrail)
        {
            while select timesheetLine where
                timesheetLine.TimesheetNbr              == _timesheetTableLog.TimesheetNbr &&
                timesheetLine.createdBy                 == curUserId()
                join timesheetLineWeek where
                    timesheetLineWeek.TSTimesheetLine   == timesheetLine.recid
            {
                if (preExistingLines[timesheetLine.RecId])
                {
                    TSTimesheetLineLog::CreateLineLog(_timesheetTableLog, timesheetLine, timesheetLineWeek, '', TsTimesheetChangeType::NoChange);
                }
                else
                {
                    TSTimesheetLineLog::CreateLineLog(_timesheetTableLog, timesheetLine, timesheetLineWeek, '', TsTimesheetChangeType::Create);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>preLineInserts</Name>
				<Source><![CDATA[
    /// <summary>
    /// Call this before starting to insert a batch of lines to enable tracking what lines were created
    /// </summary>
    /// <param name="_timesheetTable">
    /// The timesheet to track lines for
    /// </param>
    public void preLineInserts(TSTimesheetTable _timesheetTable)
    {
        TsTimesheetLine timesheetLine;

        while select RecId from timesheetLine where
            timesheetLine.TimesheetNbr == _timesheetTable.TimesheetNbr
        {
            preExistingLines[timesheetLine.RecId] = true;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>