<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmOpportunityCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Opportunity'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class smmOpportunityCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        smmOpportunityTable opportunityTable;
        select crosscompany Party from opportunityTable where opportunityTable.RecId == _caseAssociation.RefRecId;

        CustTable custTable;
        select firstonly RecId from custTable where custTable.Party == opportunityTable.Party;
        if (custTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Customer, custTable.RecId);
        }
        else
        {
            smmBusRelTable smmBusRelTable;
            select firstonly RecId from smmBusRelTable where smmBusRelTable.Party == opportunityTable.Party;

            _caseAssociation.insertCaseAssociation(CaseEntityType::BusinessRelation, smmBusRelTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        smmOpportunityTable opportunityTable;
        select crosscompany DataAreaId from opportunityTable where opportunityTable.RecId == _parentRecId;

        return opportunityTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        smmOpportunityTable opportunityTable;
        select crosscompany OpportunityId from opportunityTable where opportunityTable.RecId == _parentRecId;

        return opportunityTable.OpportunityId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        smmOpportunityTable opportunityTable;
        select RecId from opportunityTable where opportunityTable.OpportunityId == _naturalKey;

        return opportunityTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        smmOpportunityTable opportunityTable;
        select crosscompany Subject from opportunityTable where opportunityTable.RecId == _parentRecId;

        return opportunityTable.Subject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        smmOpportunityTable opportunityTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        smmOpportunityTable opportunityTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from opportunityTableCrossCompany
            where opportunityTableCrossCompany.RecId == _association.RefRecId;

        if (opportunityTableCrossCompany.Recid)
        {
            changecompany (opportunityTableCrossCompany.DataAreaId)
            {
                select firstonly opportunityTable where opportunityTable.RecId == _association.RefRecId;
            }
        }

        _args.record(opportunityTable);
        return menuitemdisplaystr(smmOpportunityTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(smmOpportunityTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(smmOpportunityTable));

        oSysTableLookup.addLookupfield(fieldnum(smmOpportunityTable, OpportunityId));
        oSysTableLookup.addLookupfield(fieldnum(smmOpportunityTable, Subject));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(smmOpportunityTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Opportunity);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>