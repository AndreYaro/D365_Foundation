<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventPhysicalOnhandChangingHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class InventPhysicalOnhandChangingHandler
{
    Map         inventQtyMap;
    private Map inventCWQtyMap;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        inventQtyMap   = new Map(Types::Container, Types::Real);
        inventCWQtyMap = new Map(Types::Container, Types::Real);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventPhysicalOnhanddetaQty</Name>
				<Source><![CDATA[
    [SysObsolete('Method is not used anymore, use getInventPhysicalOnhandDeltaAllQty()', false, 31\03\2019)]
    public Map parmInventPhysicalOnhanddetaQty(Map _inventQtyMap = inventQtyMap)
    {
        inventQtyMap = _inventQtyMap;
        return inventQtyMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventPhysicalOnhandDeltaAllQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a map containing both <c>InventQty</c> and <c>PdsCWInventQty</c> quantities.
    /// </summary>
    /// <returns>
    /// Returns a map containing both <c>InventQty</c> and <c>PdsCWInventQty</c> quantities.
    /// </returns>
    [Hookable(false)]
    internal Map getInventPhysicalOnhandDeltaAllQty()
    {
        Map           inventAllQtyMap        = new Map(Types::Container, Types::Container);
        MapEnumerator inventQtyMapEnumerator = inventQtyMap.getEnumerator();

        while (inventQtyMapEnumerator.moveNext())
        {
            inventAllQtyMap.insert(inventQtyMapEnumerator.currentKey(), [inventQtyMapEnumerator.currentValue(), inventCWQtyMap.lookup(inventQtyMapEnumerator.currentKey())]);
        }

        return inventAllQtyMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>trackPhysicalOnHandChanges</Name>
				<Source><![CDATA[
    /// <summary>
    /// Tracks the physical on hand changes.
    /// </summary>
    /// <param name = "_context">
    /// The context of the physical on hand change.
    /// </param>
    /// <param name = "_args">
    /// The information describes the changes to the physical on hand.
    /// </param>
    public void trackPhysicalOnHandChanges(Object _context, InventPhysicalOnHandChangeArgs _args)
    {
        InventDimId         inventDimId       = _args.parmInventDimId();
        InventQty           inventQty         = _args.parmDeltaQuantity();
        PdsCWInventQty      cwQty             = _args.parmDeltaCWQuantity();
        WMSPickingRouteID   wmsPickingRouteID = _args.parmWMSPickingRouteID();

        if (inventQtyMap.exists([inventDimId, wmsPickingRouteID]))
        {   
            InventQty      mapQty;
            PdsCWInventQty mapCWQty;

            mapQty   = inventQtyMap.lookup([inventDimId, wmsPickingRouteID]);
            mapCWQty = inventCWQtyMap.lookup([inventDimId, wmsPickingRouteID]);

            inventQty += mapQty;
            cwQty += mapCWQty;
        }

        inventQtyMap.insert([inventDimId, wmsPickingRouteID], inventQty);
        inventCWQtyMap.insert([inventDimId, wmsPickingRouteID], cwQty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventPhysicalOnhandChangingHandler construct()
    {
        return new InventPhysicalOnhandChangingHandler();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>