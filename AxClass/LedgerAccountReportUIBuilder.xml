<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerAccountReportUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerAccountReportUIBuilder</c> class is the base UI builder for a ledger account analysis
/// by dimension and ledger account balance sheet by dimension SSRS report.
/// </summary>
abstract class LedgerAccountReportUIBuilder extends SrsReportDataContractExtendUIBuilder_CN
{
    DialogField     dialogFieldAttribute;
    DialogField     dialogFieldFromDimension;
    DialogField     dialogFieldToDimension;
    DialogField     dialogFieldAccount;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dimensionAttributeLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs a dimension attribute lookup.
    /// </summary>
    /// <param name="_dimensionAttributeDialogControl">
    /// A dimension attribute control.
    /// </param>
    public void dimensionAttributeLookup(FormStringControl _dimensionAttributeDialogControl)
    {
        DimensionHierarchy::lookupDimensionAttributeByLocalizedName(_dimensionAttributeDialogControl, 0, true, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dimensionAttributeValidate</Name>
				<Source><![CDATA[

    /// <summary>
    /// Performs a dimension attribute validation.
    /// </summary>
    /// <param name="_dimensionAttribute">
    /// A dimension attribute control to validate.
    /// </param>
    /// <returns>
    /// true if the dimension attribute is valid; otherwise, false.
    /// </returns>
    public boolean dimensionAttributeValidate(FormStringControl _dimensionAttribute)
    {
        Name    dimensionAttributeLocal;

        boolean ret = true;

        dimensionAttributeLocal = DimensionAttribute::findByLocalizedName(dialogFieldAttribute.value(), false, SystemParameters::systemLanguageId()).Name;
        if (!dimensionAttributeLocal)
        {
            //The value '%1' in field Dimension type is not found in the related table DimensionAttribute.
            ret = checkFailed(strFmt(
                "@GLS180006",
                dialogFieldAttribute.value(),
                _dimensionAttribute.label(),
                tableId2name(tableNum(DimensionAttribute))));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dimensionValueLookup</Name>
				<Source><![CDATA[

    /// <summary>
    /// Provides a look-up interaction for the individual dimension attribute lookup controls.
    /// </summary>
    /// <param name="_dimensionValueControl">
    /// The <c>FormStringControl</c> enumeration value that triggers this event.
    /// </param>
    public void dimensionValueLookup(FormStringControl _dimensionValueControl)
    {
        Name dimensionAttributeName;

        dimensionAttributeName = dialogFieldAttribute.value();

        if (dimensionAttributeName == '')
        {
            //Please choose a value for "Dimension type" first!
            warning("@GLS100015");
            return;
        }

        if (_dimensionValueControl != null)
        {
            DimensionValueLookupHelper::lookupDimensionValues(DimensionAttribute::findByLocalizedName(dimensionAttributeName, false, SystemParameters::systemLanguageId()), _dimensionValueControl);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dimensionValueValidate</Name>
				<Source><![CDATA[

    /// <summary>
    /// Performs a dimension value validation.
    /// </summary>
    /// <param name="_dimensionValue">
    /// A dimension value control to validate.
    /// </param>
    /// <returns>
    /// true if the dimension value is valid; otherwise, false.
    /// </returns>
    public boolean dimensionValueValidate(FormStringControl _dimensionValue)
    {
        DimensionAttribute  dimensionAttributeTable;
        DimensionValue      dimensionValue;
        Common              common;

        boolean     ret = true;

        if (dialogFieldAttribute.value())
        {
            dimensionValue = _dimensionValue.valueStr();

            if (dimensionValue)
            {
                dimensionAttributeTable = DimensionAttribute::findByLocalizedName(
                    dialogFieldAttribute.value(),
                    false,
                    SystemParameters::systemLanguageId());

                DimensionAttributeValue dimAttrValue = DimensionAttributeValue::findByDimensionAttributeAndValueNoError(dimensionAttributeTable, dimensionValue);
                if (dimAttrValue)
                {
                    common = dimAttrValue.getEntityInstance();
                    if (!common)
                    {
                        //The value '%1' in field '%2' is not found in the related table '%3'.
                        ret = checkFailed(strFmt(
                            "@GLS180006",
                            dimensionValue,
                            _dimensionValue.label(),
                            dimensionAttributeTable.BackingEntityTableName));
                    }
                }
            }
        }
        else
        {
            //Please choose a value for "Dimension type" first!
            ret = checkFailed("@GLS100015");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyProperty</Name>
				<Source><![CDATA[

    /// <summary>
    /// Modifies a property of field control.
    /// </summary>
    protected void modifyProperty()
    {
        this.overrideDialogFieldAlwaysShowLookup(dialogFieldAttribute);
        this.overrideDialogFieldAlwaysShowLookup(dialogFieldFromDimension);
        this.overrideDialogFieldAlwaysShowLookup(dialogFieldToDimension);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>