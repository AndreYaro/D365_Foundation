<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestUpdateSource_Cust</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestSourceTypeAttribute(CFMPaymentRequestSourceType::CustInvoice)]
class CFMPaymentRequestUpdateSource_Cust extends CFMPaymentRequestUpdateSource
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doUpdate</Name>
				<Source><![CDATA[
    protected void doUpdate()
    {
        ttsBegin;

        if (paymentRequestTable.HeaderRefRecId)
        {
            CustTrans custTrans = custTrans::find(paymentRequestTable.HeaderRefRecId, true);
            custTrans.PaymMode = paymentRequestTable.PaymMode;
            custTrans.PaymSpec = paymentRequestTable.PaymSpec;
            custTrans.ThirdPartyBankAccountId = paymentRequestTable.RecepientBankAccountId;
            custTrans.CompanyBankAccountId = (paymentRequestTable.PaymentAccountType == CFMPaymentAccountType::Bank) ? paymentRequestTable.PaymentAccountNum : custTrans.CompanyBankAccountId;
            custTrans.update();
        }

        if (paymentRequestTable.SourceRefRecId)
        {
            CustTransOpen custTransOpen = custTransOpen::find(paymentRequestTable.SourceRefRecId, true);
            custTransOpen.DueDate = paymentRequestTable.PaymentDate;
            custTransOpen.update();
        }

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doValidateField</Name>
				<Source><![CDATA[
    protected boolean doValidateField(FieldId _fieldId)
    {
        boolean ret = super(_fieldId);

        if (ret)
        {
            switch(_fieldId)
            {
                case fieldNum(CFMPaymentRequestTable, PaymMode):
                    if (paymentRequestTable.PaymMode && !CustPaymModeTable::exist(paymentRequestTable.PaymMode))
                    {
                        ret = checkFailed(strFmt("@Cashflowmanagement:SpecifiedValueDoesNotExistSourceCompanyPaymRequest", paymentRequestTable.PaymMode));
                    }
                    break;

                case fieldNum(CFMPaymentRequestTable, PaymSpec):
                    if (paymentRequestTable.PaymSpec && !CustPaymModeSpec::existForPaymMode(paymentRequestTable.PaymSpec, paymentRequestTable.PaymMode))
                    {
                        ret = checkFailed(strFmt("@Cashflowmanagement:SpecifiedValueDoesNotExistSourceCompanyPaymRequest", paymentRequestTable.PaymSpec));
                    }
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>