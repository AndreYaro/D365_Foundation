<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PCXmlSessionWriterCompAttributeGroup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The PCXmlSessionWriterCompAttributeGroup class writes session XML for a component attribute group.
/// </summary>
class PCXmlSessionWriterCompAttributeGroup
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>write</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates the XML representation of this record that is compatible with the product configuration.
    /// </summary>
    /// <param name="_attributeGroup">
    /// An attribute group.
    /// </param>
    /// <param name="_attributeTypeWriter">
    /// The XML writer used to store the types of attributes.
    /// </param>
    /// <param name="_componentTreeWriter">
    /// The XML writer used to store the tree of components.
    /// </param>
    /// <param name="_component">
    /// The component that owns this configuration group.
    /// </param>
    /// <param name="_parsedTypes">
    /// A collection containing the record IDs of the attribute types already parsed.
    /// </param>
    /// <param name="_parsedSystemTableConstraintTypes">
    /// A collection containing the record IDs of the system table constraint types already parsed.
    /// </param>
    /// <param name="_parsedIntegerDomains">
    /// A map containing integer domains that have already been encountered during the XML generation.
    /// </param>
    /// <param name="_sessionLanguage">
    /// The language ID of the current session.
    /// </param>
    /// <param name="_tableConstraintRelationsMap">
    /// A map containing any table constraint relations the component may have.
    /// </param>
    /// <param name="_attributeDefaultValuesMap">
    /// A map containing attribute default values.
    /// </param>
    /// <param name="_attributeValuesMap">
    /// A map containing attribute values.
    /// </param>
    /// <param name="_attributeModifiersMap">
    /// A map containing attribute modifiers.
    /// </param>
    /// <param name="_xmlSessionComponentAttributeBindings">
    /// A collection containing record IDs of the attributes which have an attribute binding to a BOM or route.
    /// </param>
    /// <param name="_runtimeInstanceIdManager">
    /// A runtime instance ID manager.
    /// </param>
    public void write(
        PCComponentAttributeGroup               _attributeGroup,
        PCXmlWriter                             _attributeTypeWriter,
        PCXmlWriter                             _componentTreeWriter,
        PCClass                                 _component,
        Set                                     _parsedTypes,
        PCXmlSessionDatabaseRelationTypes       _parsedSystemTableConstraintTypes,
        Map                                     _parsedIntegerDomains,
        LanguageId                              _sessionLanguage,
        Map                                     _tableConstraintRelationsMap,
        Map                                     _attributeDefaultValuesMap,
        Map                                     _attributeValuesMap,
        Map                                     _attributeModifiersMap,
        PCXmlSessionComponentAttributeBindings  _xmlSessionComponentAttributeBindings,
        PCRuntimeInstanceIdManager              _runtimeInstanceIdManager)
    {
        #PC
        EcoResCategoryAttribute                 categoryAttribute;
        PCComponentAttributeGroupCategoryAttr   attributeGroup;
        EcoResAttribute                         attribute;
        EcoResAttributeType                     domain;
        PCSolverVariable                        solverVariable;

        PCXmlSessionWriterComponentAttribute    xmlSessionWriterComponentAttribute = PCXmlSessionWriterComponentAttribute::construct();

        PCContractNotNull::assertObject(_attributeTypeWriter, funcName());
        PCContractNotNull::assertObject(_componentTreeWriter, funcName());
        PCContractNotNull::assertRecord(_component, funcName());
        PCContractNotNull::assertObject(_parsedTypes, funcName());
        PCContractNotNull::assertObject(_parsedSystemTableConstraintTypes, funcName());
        PCContractNotNull::assertObject(_parsedIntegerDomains, funcName());
        PCContractNotNull::assertObject(_tableConstraintRelationsMap, funcName());
        PCContractNotNull::assertObject(_attributeDefaultValuesMap, funcName());
        PCContractNotNull::assertObject(_attributeValuesMap, funcName());
        PCContractNotNull::assertObject(_attributeModifiersMap, funcName());
        PCContractNotNull::assertObject(_xmlSessionComponentAttributeBindings, funcName());
        PCContractNotNull::assertObject(_runtimeInstanceIdManager, funcName());

        _componentTreeWriter.writeStartElement(#attributeGroupElement);
        _componentTreeWriter.writeAttributeString(#nameAttribute, _attributeGroup.getTranslatedName());

        //Grouped Attributes
        while   select RecId from attributeGroup
        order   by attributeGroup.uiOrder
        where   attributeGroup.ComponentAttributeGroup == _attributeGroup.RecId
        join    categoryAttribute
        where   attributeGroup.CategoryAttribute == categoryAttribute.RecId
        &&      categoryAttribute.Category == _component.RecId
        join    RecId from attribute
        where   attribute.RecId == categoryAttribute.Attribute
        join    solverVariable
        where   solverVariable.Attribute == categoryAttribute.Attribute
        join    domain
        where   domain.RecId == attribute.AttributeType
        {
            xmlSessionWriterComponentAttribute.write(
                categoryAttribute,
                _attributeTypeWriter,
                _componentTreeWriter,
                _parsedTypes,
                _parsedSystemTableConstraintTypes,
                _parsedIntegerDomains,
                domain,
                solverVariable,
                _sessionLanguage,
                _tableConstraintRelationsMap,
                _attributeDefaultValuesMap,
                _attributeValuesMap,
                _attributeModifiersMap,
                _xmlSessionComponentAttributeBindings,
                _runtimeInstanceIdManager);
        }
        _componentTreeWriter.writeEndElement();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PCXmlSessionWriterCompAttributeGroup construct()
    {
        return new PCXmlSessionWriterCompAttributeGroup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>