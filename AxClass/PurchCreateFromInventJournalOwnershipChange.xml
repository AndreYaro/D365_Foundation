<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchCreateFromInventJournalOwnershipChange</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchCreateFromInventJournalOwnershipChange</c> class contains information about inventory ownership change journals used when creating purchase order header and lines.
/// </summary>
public class PurchCreateFromInventJournalOwnershipChange
{
    JournalTransList journalTransList;
    TmpPurchLinePrice tmpPurchLinePrice;
    InventJournalOwnershipChangePurchaseOrderCollection purchaseOrders;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();

        purchaseOrders = InventJournalOwnershipChangePurchaseOrderCollection::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPurchaseOrders</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a collection of purchase orders and lines.
    /// </summary>
    /// <returns>A collection of purchase orders and lines.</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public InventJournalOwnershipChangePurchaseOrderCollection getPurchaseOrders()
    {
        return purchaseOrders;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJournalHeader</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the inventory ownership change journal header.
    /// </summary>
    /// <returns>The inventory ownership change journal header</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public InventJournalTable getJournalHeader()
    {
        return journalTransList.journalTableData().journalTable();
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasTransError</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if any errors has occurred when posting the inventory ownership change journal lines.
    /// </summary>
    /// <param name = "_journalTransList">A list of inventory ownership change journal lines.</param>
    /// <returns>true if there are errors; otherwise, false.</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected boolean hasTransError(JournalTransList _journalTransList)
    {
        return _journalTransList.journalTableData().journalErrorTransList().find(_journalTransList.item().LineNum) ||
               _journalTransList.journalTableData().journalErrorVoucherList().find(_journalTransList.item().Voucher);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createPurchaseOrderAndLineStructure</Name>
				<Source><![CDATA[
    private void createPurchaseOrderAndLineStructure()
    {
        journalTransList.reset();

        while (journalTransList.next())
        {
            if (!this.hasTransError(journalTransList))
            {
                InventJournalTrans ownershipChangeJournalLine = journalTransList.item();
                ownershipChangeJournalLine.reread(); // The line could have been changed since journalTransList was created

                purchaseOrders.addPurchaseOrder(ownershipChangeJournalLine);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTmpPurchLinePrice</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public TmpPurchLinePrice parmTmpPurchLinePrice(TmpPurchLinePrice _tmpPurchLinePrice = tmpPurchLinePrice)
    {
        tmpPurchLinePrice = _tmpPurchLinePrice;
        return tmpPurchLinePrice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of the <c>PurchCreateFromInventJournalOwnershipChange</c> class.
    /// </summary>
    /// <param name = "_journalTransList">The inventory ownership change journal line to add.</param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void initialize(JournalTransList _journalTransList)
    {
        journalTransList = _journalTransList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static PurchCreateFromInventJournalOwnershipChange construct()
    {
        return new PurchCreateFromInventJournalOwnershipChange();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromJournalLines</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>PurchCreateFromInventJournalOwnershipChange</c> class.
    /// </summary>
    /// <param name = "_journalTransList">The inventory ownership change journal lines to add.</param>
    /// <returns>An instance of the <c>PurchCreateFromInventJournalOwnershipChange</c> class.</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public PurchCreateFromInventJournalOwnershipChange newFromJournalLines(JournalTransList _journalTransList)
    {
        PurchCreateFromInventJournalOwnershipChange journal = PurchCreateFromInventJournalOwnershipChange::construct();

        journal.initialize(_journalTransList);

        journal.createPurchaseOrderAndLineStructure();

        return journal;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>