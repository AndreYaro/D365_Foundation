<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvExpTableQueryHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Helper class for expense module that provides utility methods to filter the expense report query by certain attributes.
/// </summary>
static class TrvExpTableQueryHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>filterByCurrentCompany</Name>
				<Source><![CDATA[
    /// <summary>
    /// Filter by current company and lock the range.
    /// </summary>
    /// <param name = "_query">A Query object with <c>TrvExpTable</c> as data source</param>
    public static void filterByCurrentCompany(Query _query)
    {
        QueryBuildDataSource qbds;
        QueryBuildRange      legalEntityRange;

        qbds = _query.dataSourceTable(tablenum(TrvExpTable));

        if (qbds)
        {
            legalEntityRange = SysQuery::findOrCreateRange(qbds, fieldnum(TrvExpTable, LegalEntity));
            legalEntityRange.value(queryValue(CompanyInfo::current()));
            legalEntityRange.status(RangeStatus::Locked);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>filterByCurrentWorker</Name>
				<Source><![CDATA[
    /// <summary>
    /// Filter by current user and lock the range.
    /// </summary>
    /// <param name = "_query">A Query object with <c>TrvExpTable</c> as data source</param>
    public static void filterByCurrentWorker(Query _query)
    {
        QueryBuildDataSource qbds;
        QueryBuildRange      currentWorkerRange;

        qbds = _query.dataSourceTable(tablenum(TrvExpTable));

        if (qbds)
        {
            currentWorkerRange = SysQuery::findOrCreateRange(qbds, fieldnum(TrvExpTable, CreatingWorker));
            currentWorkerRange.value(queryValue(HcmWorkerLookup::currentWorker()));
            currentWorkerRange.status(RangeStatus::Locked);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>filterByCurrentUsersDelegatingWorker</Name>
				<Source><![CDATA[
    /// <summary>
    /// Filter by current user's context and lock the range.
    /// </summary>
    /// <param name = "_query">A Query object with <c>TrvAppEmplSub</c> as data source.</param>
    public static void filterByCurrentUsersDelegatingWorker(Query _query)
    {
        QueryBuildDataSource qbds;
        QueryBuildRange      currentWorkerRange;

        qbds = _query.dataSourceTable(tablenum(TrvAppEmplSub));

        if (qbds)
        {
            currentWorkerRange = SysQuery::findOrCreateRange(qbds, fieldnum(TrvAppEmplSub, DelegatedUser));
            currentWorkerRange.value(queryValue(curUserId()));
            currentWorkerRange.status(RangeStatus::Locked);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>