<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WMSWorkloadProcessorPlannedForecast</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Processes the master plan planned transactions and creates forecast data for the work load report
/// based on the planned data.
/// </summary>
class WMSWorkloadProcessorPlannedForecast extends WMSWorkloadProcessor
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>executeSupplySchedule</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the <c>ReqSupplyDemandScheduleModel</c> API.
    /// </summary>
    /// <param name="_workLoadUnit">
    /// An instance of the <c>WMSWorkLoadUnit</c> class.
    /// </param>
    /// <param name="_itemId">
    /// An <c>ItemID</c> value.
    /// </param>
    /// <param name="_inventDim">
    /// An <c>InventDim</c> buffer.
    /// </param>
    /// <param name="_status">
    /// A <c>WMSReqTransMovementStatus</c> value.
    /// </param>
    /// <returns>
    /// An instance of the <c>ReqSupplyDemandScheduleModel</c> class.
    /// </returns>
    public ReqSupplyDemandScheduleModel executeSupplySchedule(WMSWorkLoadUnit _workLoadUnit, ItemId _itemId, InventDim _inventDim, WMSReqTransMovementStatus _status)
    {
        ReqSupplyDemandScheduleModel    reqSupplyDemandScheduleModel = this.initSupplySchedule(_itemId, _inventDim);
        InventDimParm                   inventDimParm;

        inventDimParm.InventSiteIdFlag = NoYes::Yes;
        inventDimParm.InventLocationIdFlag = NoYes::Yes;

        reqSupplyDemandScheduleModel.parmMovementStatus(_status);
        reqSupplyDemandScheduleModel.parmSkipBacklogPeriod(true);
        reqSupplyDemandScheduleModel.parmSkipOutlookPeriod(true);
        reqSupplyDemandScheduleModel.parmInventDimParm(inventDimParm);
        reqSupplyDemandScheduleModel.parmInboundTransTypes(_workLoadUnit.getInboundTransTypes());
        reqSupplyDemandScheduleModel.parmOutboundTransTypes(_workLoadUnit.getOutboundTransTypes());

        reqSupplyDemandScheduleModel.calcPeriods();

        return reqSupplyDemandScheduleModel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateForecastForWorkUnit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates work load unit forecast based on the input from the <c>ReqSupplyDemandScheduleModel</c>
    /// class.
    /// </summary>
    /// <param name="_workLoadUnit">
    /// An instance of the <c>WMSWorkLoadUnit</c> class.
    /// </param>
    /// <param name="_reqSupplyDemandScheduleModel">
    /// An instance of the <c>ReqSupplyDemandScheduleModel</c> class.
    /// </param>
    /// <param name="_reportServiceItemProvider">
    /// An instance of the <c>WMSReportServiceItemProvider</c> class.
    /// </param>
    protected void updateForecastForWorkUnit(WMSWorkLoadUnit _workLoadUnit, ReqSupplyDemandScheduleModel _reqSupplyDemandScheduleModel, WMSReportServiceItemProvider _reportServiceItemProvider)
    {
        int calculatedPeriodsCount;
        int currentPeriod;

        calculatedPeriodsCount = conLen(_reqSupplyDemandScheduleModel.calculatedPeriods());

        for (currentPeriod = 1 ; currentPeriod <= calculatedPeriodsCount; currentPeriod++)
        {
            this.populateWorkloadUnitFromModel(_workLoadUnit, _reqSupplyDemandScheduleModel, _reportServiceItemProvider, currentPeriod);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static WMSWorkloadProcessorPlannedForecast construct()
    {
        return new WMSWorkloadProcessorPlannedForecast();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromReportSetup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>WMSWorkloadProcessorPlannedForecast</c> class, based on the work load
    /// report data.
    /// </summary>
    /// <param name="_reqPlanId">
    /// The master plan reference.
    /// </param>
    /// <param name="_fromDate">
    /// The specified start date.
    /// </param>
    /// <param name="_toDate">
    /// The specified end date.
    /// </param>
    /// <param name="_periodTemplateName">
    /// The specified period template name.
    /// </param>
    /// <returns>
    /// An instance of the <c>WMSWorkloadProcessorPlannedForecast</c> class.
    /// </returns>
    public static WMSWorkloadProcessorPlannedForecast newFromReportSetup(ReqPlanId _reqPlanId, TransDate _fromDate, TransDate _toDate, PeriodTemplateName _periodTemplateName)
    {
        WMSWorkloadProcessorPlannedForecast  workloadProcessorPlannedForecast = WMSWorkloadProcessorPlannedForecast::construct();

        workloadProcessorPlannedForecast.parmReqPlanId(_reqPlanId);
        workloadProcessorPlannedForecast.parmFromDate(_fromDate);
        workloadProcessorPlannedForecast.parmToDate(_toDate);
        workloadProcessorPlannedForecast.parmPeriodTemplateName(_periodTemplateName);

        return workloadProcessorPlannedForecast;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>