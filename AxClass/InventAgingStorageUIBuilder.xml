<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventAgingStorageUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventAgingStorageUIBuilder</c> is a UIBuilder class for <c>InventAging</c> report.
/// </summary>
class InventAgingStorageUIBuilder extends SysOperationAutomaticUIBuilder
{
    DialogGroup     inventoryDimensionGroup;

    DialogField     dialogFieldAgingPeriodUnit;
    DialogField     dialogFieldAgingDaysOne;
    DialogField     dialogFieldAgingDaysTwo;
    DialogField     dialogFieldAgingDaysThree;
    DialogField     dialogFieldAgingDaysFour;
    DialogField     dialogFieldAgingDatesOne;
    DialogField     dialogFieldAgingDatesTwo;
    DialogField     dialogFieldAgingDatesThree;
    DialogField     dialogFieldAgingDatesFour;
    DialogField     dialogFieldAgingDatePeriodOne;
    DialogField     dialogFieldAgingDatePeriodTwo;
    DialogField     dialogFieldAgingDatePeriodThree;
    DialogField     dialogFieldAgingDatePeriodFour;

    #Define.SelectionChangeMethodName('selectionChange')
    #Define.ValidateMethodName('validate')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>enableAgingGroups</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables date field related to aging period.
    /// </summary>
    /// <param name="_periodUnit">
    /// A value index of enum <c>InventAgingPeriod</c> indicating the aging period selected.
    /// </param>
    protected void enableAgingGroups(InventAgingPeriod _periodUnit)
    {
        dialogFieldAgingDaysOne.visible(_periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysTwo.visible(_periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysThree.visible(_periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysFour.visible(_periodUnit == InventAgingPeriod::Days);

        dialogFieldAgingDatesOne.visible(_periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesTwo.visible(_periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesThree.visible(_periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesFour.visible(_periodUnit == InventAgingPeriod::Dates);

        dialogFieldAgingDatePeriodOne.visible(_periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodTwo.visible(_periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodThree.visible(_periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodFour.visible(_periodUnit == InventAgingPeriod::DateIntervals);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers method override in order to capture dialog events.
    /// </summary>
    public void postRun()
    {
        super();

        dialog.dialogForm().formRun().controlMethodOverload(false);

        dialogFieldAgingPeriodUnit.registerOverrideMethod(#SelectionChangeMethodName, methodStr(InventAgingStorageUIBuilder, selectionChangeAging), this);
        dialogFieldAgingDaysOne.registerOverrideMethod(#ValidateMethodName, methodStr(InventAgingStorageUIBuilder, validateDays), this);
        dialogFieldAgingDaysTwo.registerOverrideMethod(#ValidateMethodName, methodStr(InventAgingStorageUIBuilder, validateDays), this);
        dialogFieldAgingDaysThree.registerOverrideMethod(#ValidateMethodName, methodStr(InventAgingStorageUIBuilder, validateDays), this);
        dialogFieldAgingDaysFour.registerOverrideMethod(#ValidateMethodName, methodStr(InventAgingStorageUIBuilder, validateDays), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method initializes the dialog fields after the fields have been built.
    /// </summary>
    public void postBuild()
    {
        dialogFieldAgingPeriodUnit = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmAgingPeriodUnit));

        dialogFieldAgingDaysOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDaysOne));
        dialogFieldAgingDaysTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDaysTwo));
        dialogFieldAgingDaysThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDaysThree));
        dialogFieldAgingDaysFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDaysFour));
        dialogFieldAgingDatesOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDateOne));
        dialogFieldAgingDatesTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDateTwo));
        dialogFieldAgingDatesThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDateThree));
        dialogFieldAgingDatesFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDateFour));
        dialogFieldAgingDatePeriodOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDatePeriodOne));
        dialogFieldAgingDatePeriodTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDatePeriodTwo));
        dialogFieldAgingDatePeriodThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDatePeriodThree));
        dialogFieldAgingDatePeriodFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingStorageContract, parmIntervalDatePeriodFour));

        this.enableAgingGroups((this.dataContractObject() as InventAgingStorageContract).parmAgingPeriodUnit());

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectionChangeAging</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the aging period selection change event.
    /// </summary>
    /// <param name="_formControl">
    /// The form control that raised the event.
    /// </param>
    /// <returns>
    /// The changed selection.
    /// </returns>
    final protected int selectionChangeAging(FormComboBoxControl _formControl)
    {
        int ret = _formControl.selectionChange();

        DictEnum dictEnum = new DictEnum(enumNum(InventAgingPeriod));
        this.enableAgingGroups(dictEnum.index2Value(_formControl.selection()));

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDays</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the aging days validate event.
    /// </summary>
    /// <param name="_formControl">
    /// The form control that raised the event.
    /// </param>
    /// <returns>
    /// True if the days are greater or equals zero; otherwise false.
    /// </returns>
    protected boolean validateDays(FormIntControl   _formControl)
    {
        boolean ret = _formControl.validate();

        if (ret && _formControl.value() < 0)
        {
            throw error("@SYS102923");
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>