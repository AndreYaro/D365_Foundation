<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WMSBOLItemProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WMSBOLItemProvider</c> class is used for the construction of bills of lading from various
/// sources like <c>WMSOrderTrans</c> records.
/// </summary>
abstract class WmsbolItemProvider
{
    // <GEERU>
    #ISOCountryRegionCodes
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canUseBillOfLading</Name>
				<Source><![CDATA[
    // </GEERU>

    /// <summary>
    /// Indicates whether a <c>WMSBillOfLading</c> record is as the current bill of lading for the provider.
    /// </summary>
    /// <param name="_existingWMSBillOfLading">
    /// A <c>WMSBillOfLading</c> buffer that is used as the bill of lading for the provider; optional.
    /// </param>
    /// <returns>
    /// true if the <c>WMSBillOfLading</c> record is used as the current bill of lading for the provider;
    /// otherwise, false.
    /// </returns>
    /// <remarks>
    /// This method is used to avoid multiple database operations for the same bill of lading.
    /// </remarks>
    protected boolean canUseBillOfLading(WMSBillOfLading _existingWMSBillOfLading)
    {
        if (_existingWMSBillOfLading.RecId
        &&  _existingWMSBillOfLading.ShipmentId             == this.getShipmentID()
        &&  _existingWMSBillOfLading.CustAccount            == this.getCustAccount()
        &&  _existingWMSBillOfLading.DeliveryPostalAddress  == this.getDeliveryPostalAddress())
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canUseBillOfLadingCarrier</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether a <c>WMSBillOfLadingCarrier</c> record is used as the current bill of lading for
    /// the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingId">
    /// A <c>WMSBillOfLadingId</c> value that is used for comparison to the <c>WMSBillOfLadingCarrier</c>
    /// record.
    /// </param>
    /// <param name="_wmsbolAdditionalInfo">
    /// A <c>WMSBOLAdditionalInfo</c> value that is used for comparison to the
    /// <c>WMSBillOfLadingCarrier</c> record.
    /// </param>
    /// <param name="_existingWMSBillOfLadingCarrier">
    /// A <c>WMSBillOfLadingCarrier</c> buffer that is used as the bill of lading carrier for the provider;
    /// optional.
    /// </param>
    /// <returns>
    /// true if the <c>WMSBillOfLadingCarrier</c> record is used as the current bill of lading carrier for
    /// the provider; otherwise, <paramref name="false" />.
    /// </returns>
    /// <remarks>
    /// This method is used to avoid multiple database operations for the same bill of lading carrier.
    /// </remarks>
    protected boolean canUseBillOfLadingCarrier(
        WMSBillOfLadingId       _wmsBillOfLadingId,
        WMSBOLAdditionalInfo    _wmsbolAdditionalInfo,
        WMSBillOfLadingCarrier  _existingWMSBillOfLadingCarrier)
    {
        if (_existingWMSBillOfLadingCarrier.RecId
        &&  _existingWMSBillOfLadingCarrier.BillOfLadingId  == _wmsBillOfLadingId
        &&  _existingWMSBillOfLadingCarrier.AdditionalInfo  == _wmsbolAdditionalInfo)
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canUseBillOfLadingOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the <c>WMSBillOfLadingOrder</c> record is used as the current bill of lading for
    /// the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingId">
    /// A <c>WMSBillOfLadingId</c> value that is used for the comparison to the
    /// <c>WMSBillOfLadingCarrier</c> record.
    /// </param>
    /// <param name="_existingBillOfLadingOrder">
    /// A <c>WMSBillOfLadingOrder</c> buffer that is used as the bill of lading order for the provider;
    /// optional.
    /// </param>
    /// <returns>
    /// true if the <c>WMSBillOfLadingOrder</c> record is used as the current bill of lading order for the
    /// provider; otherwise, false.
    /// </returns>
    /// <remarks>
    /// This method is used to avoid multiple database operations for the same bill of lading order.
    /// </remarks>
    protected boolean canUseBillOfLadingOrder(
        WMSBillOfLadingId    _wmsBillOfLadingId,
        WMSBillOfLadingOrder _existingBillOfLadingOrder)
    {
        if (_existingBillOfLadingOrder.RecId
            && _existingBillOfLadingOrder.BillOfLadingId    == _wmsBillOfLadingId
            && _existingBillOfLadingOrder.InventTransRefId  == this.getBillOfLadingOrderReferenceId()
            && _existingBillOfLadingOrder.wmsReferenceType  == this.getWMSReferenceType())
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateWMSBillOfLadingCarrier</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates a <c>WMSBillOfLadingCarrier</c> record that is related to a
    /// <c>WMSBillOfLading</c> record.
    /// </summary>
    /// <param name="_wmsBillOfLading">
    /// The <c>WMSBillOfLading</c> record to which the <c>WMSBillOfLadingCarrier</c> record is related.
    /// </param>
    /// <param name="_existingWMSBillOfLadingCarrier">
    /// An existing <c>WMSBillOfLadingCarrier</c> record that potentially can be updated.
    /// </param>
    /// <returns>
    /// A record in the <c>WMSBillOfLadingCarrier</c> table; otherwise; an empty record.
    /// </returns>
    public WMSBillOfLadingCarrier createOrUpdateWMSBillOfLadingCarrier(
        WMSBillOfLading         _wmsBillOfLading,
        WMSBillOfLadingCarrier  _existingWMSBillOfLadingCarrier)
    {
        WMSBillOfLadingCarrier wmsBillOfLadingCarrier;
        WMSBOLAdditionalInfo   wmsbolAdditionalInfo;

        wmsbolAdditionalInfo = this.getBillOfLadingAdditionalInfo(_wmsBillOfLading.LanguageId);

        if (this.canUseBillOfLadingCarrier(_wmsBillOfLading.BillOfLadingId,wmsbolAdditionalInfo,_existingWMSBillOfLadingCarrier))
        {
            wmsBillOfLadingCarrier = _existingWMSBillOfLadingCarrier;
        }
        else
        {
            select forupdate firstonly wmsBillOfLadingCarrier
                where wmsBillOfLadingCarrier.BillOfLadingId == _wmsBillOfLading.BillOfLadingId
                &&    wmsBillOfLadingCarrier.AdditionalInfo == wmsbolAdditionalInfo;
        }

        if (wmsBillOfLadingCarrier.RecId)
        {
            this.updateWMSBillOfLadingCarrierFields(wmsBillOfLadingCarrier);
            wmsBillOfLadingCarrier.update();
        }
        else
        {
            this.initWMSBillOfLadingCarrier(wmsBillOfLadingCarrier);
            wmsBillOfLadingCarrier.BillOfLadingId   = _wmsBillOfLading.BillOfLadingId;
            wmsBillOfLadingCarrier.AdditionalInfo   = wmsbolAdditionalInfo;
            wmsBillOfLadingCarrier.insert();
        }

        return wmsBillOfLadingCarrier;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateWMSBillOfLadingOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates a <c>WMSBillOfLadingOrder</c> record that is related to a <c>WMSBillOfLading</c>
    /// record.
    /// </summary>
    /// <param name="_wmsBillOfLading">
    /// A <c>WMSBillOfLading</c> record to which the <c>WMSBillOfLadingCarrier</c> record is related.
    /// </param>
    /// <param name="_existingWMSBillOfLadingOrder">
    /// A <c>WMSBillOfLadingOrder</c> record that potentially can be updated.
    /// </param>
    /// <returns>
    /// A record in the <c>WMSBillOfLadingOrder</c> table; otherwise, an empty record.
    /// </returns>
    public WMSBillOfLadingOrder createOrUpdateWMSBillOfLadingOrder(
        WMSBillOfLading         _wmsBillOfLading,
        WMSBillOfLadingOrder    _existingWMSBillOfLadingOrder)
    {
        WMSBillOfLadingOrder wmsBillOfLadingOrder;

        if (this.canUseBillOfLadingOrder(_wmsBillOfLading.BillOfLadingId,_existingWMSBillOfLadingOrder))
        {
            wmsBillOfLadingOrder = _existingWMSBillOfLadingOrder;
        }
        else
        {
            select forupdate firstonly wmsBillOfLadingOrder
                where wmsBillOfLadingOrder.BillOfLadingId   == _wmsBillOfLading.BillOfLadingId
                &&    wmsBillOfLadingOrder.InventTransRefId == this.getBillOfLadingOrderReferenceId()
                &&    wmsBillOfLadingOrder.wmsReferenceType == this.getWMSReferenceType();
        }

        if (wmsBillOfLadingOrder.RecId)
        {
            this.updateWMSBillOfLadingOrderFields(wmsBillOfLadingOrder);
            wmsBillOfLadingOrder.update();
        }
        else
        {
            this.initWMSBillOfLadingOrder(wmsBillOfLadingOrder);
            wmsBillOfLadingOrder.BillOfLadingId = _wmsBillOfLading.BillOfLadingId;
            wmsBillOfLadingOrder.insert();
        }

        return wmsBillOfLadingOrder;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createWMSBillOfLading</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a  <c>WMSBillOfLading</c> record.
    /// </summary>
    /// <returns>
    /// A <c>WMSBillOfLading</c> record.
    /// </returns>
    public WMSBillOfLading createWMSBillOfLading()
    {
        WMSBillOfLading wmsBillOfLading;

        this.initWMSBillOfLading(wmsBillOfLading);

        wmsBillOfLading.insert();

        return wmsBillOfLading;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findOrCreateWMSBillOfLading</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds or creates a <c>WMSBillOfLading</c> record.
    /// </summary>
    /// <param name="_lastUsedWMSBillOfLading">
    /// A <c>WMSBillOfLading</c> buffer that potentially can be used as the bill of lading for the provider.
    /// </param>
    /// <returns>
    /// A <c>WMSBillOfLading</c> record.
    /// </returns>
    public WMSBillOfLading findOrCreateWMSBillOfLading(WMSBillOfLading _lastUsedWMSBillOfLading)
    {
        WMSBillOfLading wmsBillOfLading;

        if (this.canUseBillOfLading(_lastUsedWMSBillOfLading))
        {
            return _lastUsedWMSBillOfLading;
        }
        else
        {
           wmsBillOfLading = this.findWMSBillOfLading();
        }

        if (!wmsBillOfLading.RecId)
        {
            wmsBillOfLading = this.createWMSBillOfLading();
        }

        return wmsBillOfLading;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findWMSBillOfLading</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a <c>WMSBillOfLading</c> record that can be used as the bill of lading for the provider.
    /// </summary>
    /// <returns>
    /// A <c>WMSBillOfLading</c> record that can be used as the bill of lading for the provider.
    /// </returns>
    protected WMSBillOfLading findWMSBillOfLading()
    {
        WMSBillOfLading wmsBillOfLading;

        select firstonly wmsBillOfLading
            where wmsBillOfLading.ShipmentId                == this.getShipmentID()
            &&    wmsBillOfLading.CustAccount               == this.getCustAccount()
            &&    wmsBillOfLading.DeliveryPostalAddress     == this.getDeliveryPostalAddress();

        return wmsBillOfLading;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingAdditionalInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets additional information for the <c>WMSBillOfLading</c> record from the bill of lading for a
    /// given language.
    /// </summary>
    /// <param name="_languageId">
    /// A language ID that is used to find the addition information.
    /// </param>
    /// <returns>
    /// More information that can be used for bill of ladings.
    /// </returns>
    public WMSBOLAdditionalInfo getBillOfLadingAdditionalInfo(LanguageId _languageId)
    {
        InventTable         inventTable         = InventTable::find(this.getItemID());
        EcoResDescription   ecoResDescription   = inventTable.productDescription(_languageId);
        str                 additionalInfo;

        if (ecoResDescription)
        {
            additionalInfo = firstLine(ecoResDescription);
        }
        else
        {
            additionalInfo = inventTable.defaultProductName();
        }

        return strFmt("%1:%2", inventTable.ItemId, additionalInfo);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingOrderAdditionalInfo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets additional information for the <c>WMSBillOfLadingOrder</c> record from the bill of lading
    /// order for a given language.
    /// </summary>
    /// <returns>
    /// More information that can be used for a bill of lading order.
    /// </returns>
    public WMSBOLAdditionalInfo getBillOfLadingOrderAdditionalInfo()
    {
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingOrderReferenceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a reference ID for a bill of lading order.
    /// </summary>
    /// <returns>
    /// A reference ID for a bill of lading order.
    /// </returns>
    public abstract InventTransRefId getBillOfLadingOrderReferenceId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingOrderWeight</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the weight for a bill of lading order.
    /// </summary>
    /// <returns>
    /// Always returns 0.
    /// </returns>
    /// <remarks>
    /// This method is intended to be overridden.
    /// </remarks>
    public Weight getBillOfLadingOrderWeight()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingReferenceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a reference ID for the bill of lading.
    /// </summary>
    /// <returns>
    /// A reference ID for the bill of lading.
    /// </returns>
    public abstract InventTransRefId getBillOfLadingReferenceId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCorrect_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if WMS Bill of Lading is correction
    /// </summary>
    /// <returns>
    /// False, but overridden in descendants
    /// </returns>
    public Correct_RU getCorrect_RU()
    {
        return NoYes::No;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustAccount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the customer account that is used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The customer account that is used for the bill of lading.
    /// </returns>
    public abstract CustAccount getCustAccount()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDeliveryPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the delivery postal address ID that is used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The delivery postal address ID that is used for the bill of lading.
    /// </returns>
    public abstract LogisticsPostalAddressRecId getDeliveryPostalAddress()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getHandlingQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the handling quantity that is used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The handling quantity that is used for the bill of lading.
    /// </returns>
    public abstract InventQty getHandlingQuantity()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventLocationId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets an <c>InventLocationID</c> value for the bill of lading.
    /// </summary>
    /// <returns>
    /// The <c>InventLocationID</c> value for the bill of lading.
    /// </returns>
    public abstract InventLocationId getInventLocationId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getItemID</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets an <c>ItemId</c> value for the bill of lading order.
    /// </summary>
    /// <returns>
    /// The <c>ItemId</c> value for the bill of lading order.
    /// </returns>
    protected abstract ItemId getItemID()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the quantity that is used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The quantity that is used for the bill of lading.
    /// </returns>
    public abstract InventQty getQuantity()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getShipmentID</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a <c>WMSShipmentId</c> value for the bill of lading.
    /// </summary>
    /// <returns>
    /// The <c>WMSShipmentId</c> value for the bill of lading.
    /// </returns>
    public WMSShipmentId getShipmentID()
    {
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWMSReferenceType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the WMS reference type of the order.
    /// </summary>
    /// <returns>
    /// The WMS reference type of the order.
    /// </returns>
    public abstract WMSReferenceType getWMSReferenceType()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initWMSBillOfLading</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>WMSBillOfLading</c> record.
    /// </summary>
    /// <param name="_wmsBillOfLading">
    /// The <c>WMSBillOfLading</c> record to be initialized.
    /// </param>
    protected void initWMSBillOfLading(WMSBillOfLading _wmsBillOfLading)
    {
        _wmsBillOfLading.initValue();

        // <GEERU>
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([ #isoRU ]))
        {
            _wmsBillOfLading.BillOfLadingId          = WMSBOLIdGenerate::nextBOLId(this.getCorrect_RU());
        }
        else
        {
        // </GEERU>
            _wmsBillOfLading.BillOfLadingId          = WMSBOLIdGenerate::nextBOLId();
        // <GEERU>
        }
        // </GEERU>

        _wmsBillOfLading.wmsReferenceType       = this.getWMSReferenceType();
        _wmsBillOfLading.InventTransRefId       = this.getBillOfLadingReferenceId();
        _wmsBillOfLading.CustAccount            = this.getCustAccount();
        _wmsBillOfLading.DeliveryPostalAddress  = this.getDeliveryPostalAddress();
        _wmsBillOfLading.ShipmentId             = this.getShipmentID();
        _wmsBillOfLading.InventLocationId       = this.getInventLocationId();

        _wmsBillOfLading.initShippingPostalAddress();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initWMSBillOfLadingCarrier</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>WMSBillOfLadingCarrier</c> bill of lading argument based on the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingCarrier">
    /// The bill of lading carrier that should be initialized.
    /// </param>
    protected void initWMSBillOfLadingCarrier(WMSBillOfLadingCarrier  _wmsBillOfLadingCarrier)
    {
        _wmsBillOfLadingCarrier.HandlingQty         = this.getHandlingQuantity();
        _wmsBillOfLadingCarrier.Weight              = this.getBillOfLadingCarrierWeight();
        _wmsBillOfLadingCarrier.pdsCWHandlingQty    = this.pdsGetCWHandlingQuantity();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initWMSBillOfLadingOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>WMSBillOfLadingOrder</c> bill of lading order argument based on the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingOrder">
    /// The bill of lading order that should be initialized.
    /// </param>
    protected void initWMSBillOfLadingOrder(WMSBillOfLadingOrder _wmsBillOfLadingOrder)
    {
        _wmsBillOfLadingOrder.Qty               = this.getQuantity();
        _wmsBillOfLadingOrder.pdsCWQty          = this.pdsGetCWQuantity();
        _wmsBillOfLadingOrder.wmsReferenceType  = this.getWMSReferenceType();
        _wmsBillOfLadingOrder.InventTransRefId  = this.getBillOfLadingOrderReferenceId();
        _wmsBillOfLadingOrder.Weight            = this.getBillOfLadingOrderWeight();
        _wmsBillOfLadingOrder.AdditionalInfo    = this.getBillOfLadingOrderAdditionalInfo();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pdsGetCWHandlingQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the handling catch weight quantity used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The handling catch weight quantity used for the bill of lading.
    /// </returns>
    public PdsCWInventQty pdsGetCWHandlingQuantity()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pdsGetCWQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the catch weight quantity used for the bill of lading.
    /// </summary>
    /// <returns>
    /// The catch weight quantity used for the bill of lading.
    /// </returns>
    public PdsCWInventQty pdsGetCWQuantity()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateWMSBillOfLadingCarrierFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates field values on the existing <c>WMSBillOfLadingCarrier</c> argument based on the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingCarrier">
    /// The bill of lading carrier that should have fields updated.
    /// </param>
    protected void updateWMSBillOfLadingCarrierFields(WMSBillOfLadingCarrier  _wmsBillOfLadingCarrier)
    {
        _wmsBillOfLadingCarrier.HandlingQty         += this.getHandlingQuantity();
        _wmsBillOfLadingCarrier.Weight              += this.getBillOfLadingCarrierWeight();
        _wmsBillOfLadingCarrier.pdsCWHandlingQty    += this.pdsGetCWHandlingQuantity();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateWMSBillOfLadingOrderFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates field values on the existing <c>WMSBillOfLadingOrder</c> argument based on the provider.
    /// </summary>
    /// <param name="_wmsBillOfLadingOrder">
    /// The bill of lading order that should have fields updated.
    /// </param>
    protected void updateWMSBillOfLadingOrderFields(WMSBillOfLadingOrder  _wmsBillOfLadingOrder)
    {
        _wmsBillOfLadingOrder.Qty    += this.getQuantity();
        _wmsBillOfLadingOrder.Weight += this.getBillOfLadingOrderWeight();
        _wmsBillOfLadingOrder.pdsCWQty += this.pdsGetCWQuantity();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBillOfLadingCarrierWeight</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the weight for a bill of lading carrier.
    /// </summary>
    /// <returns>
    /// Always returns 0.
    /// </returns>
    /// <remarks>
    /// This method is intended to be overridden.
    /// </remarks>
    public Weight getBillOfLadingCarrierWeight()
    {
        return 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>