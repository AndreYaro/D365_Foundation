<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailZReportExportArchiveDestinationFactory</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;
using Microsoft.Dynamics365.LocalizationFramework.Format.FileGeneration;

/// <summary>
/// The <c>RetailZReportExportArchiveDestinationFactory</c> class is a destination factory for using <c>RetailZReportExportArchiveDestination</c>.
/// </summary>
final internal class RetailZReportExportArchiveDestinationFactory implements ERIFileDestinationFactory, ERIFileDestinationFactoryPostProcessor
{
    private ERIFileDestinationFactory originDestinationFactory;
    private RetailZReportExportArchiveContract zReportContract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>createPrintMgmtDestination</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates file destination for print management.
    /// </summary>
    /// <param name = "_fileDestination">A default file destination.</param>
    /// <param name = "_identification">An identification strategy.</param>
    /// <param name = "_rootContext">A root context.</param>
    /// <param name = "_root">A root element.</param>
    /// <returns>A file destination.</returns>
    public ERIDataDrivenFileDestination createPrintMgmtDestination(
        ERIFileDestination _fileDestination,
        ERIObjectIdentificationStrategy _identification,
        ERTextFormatDataContext _rootContext,
        ERTextFormatIFileComponent _root)
    {
        ERIDataDrivenFileDestination dataDrivenDestination = originDestinationFactory.createPrintMgmtDestination(
            _fileDestination,
            _identification,
            _rootContext,
            _root);

        IFileDestinationHost fileDestinationHost = dataDrivenDestination as IFileDestinationHost;
        if (fileDestinationHost)
        {
            fileDestinationHost.FileDestination = RetailZReportExportArchiveDestination::construct(
                fileDestinationHost.get_FileDestination(),
                zReportContract);
        }

        return dataDrivenDestination;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processDestinationAfterCreation</Name>
				<Source><![CDATA[
    public ERIFileDestination processDestinationAfterCreation(ERIFileDestination _fileDestination)
    {
        return RetailZReportExportArchiveDestination::construct(_fileDestination, zReportContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs the Z-report export archive destination.
    /// </summary>
    /// <param name = "_originDestinationFactory">The original destination.</param>
    /// <param name = "_zReportContract">The z-report contract.</param>
    /// <returns>The archive destination.</returns>
    public static RetailZReportExportArchiveDestinationFactory construct(ERIFileDestinationFactory _originDestinationFactory, RetailZReportExportArchiveContract _zReportContract)
    {
        RetailZReportExportArchiveDestinationFactory destinationFactory = new RetailZReportExportArchiveDestinationFactory();
        destinationFactory.originDestinationFactory = _originDestinationFactory;
        destinationFactory.zReportContract = _zReportContract;

        ERIFileDestinationFactoryPostProcessorsHost factoryPostProcessing = ERCast::asObject(destinationFactory.originDestinationFactory) as ERIFileDestinationFactoryPostProcessorsHost;

        if (factoryPostProcessing)
        {
            factoryPostProcessing.addDestinationPostProcessor(destinationFactory);
        }

        return destinationFactory;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>