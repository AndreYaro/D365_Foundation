<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TradeTotalsBuildNoFetchMarkupTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TradeTotalsBuildNoFetchMarkupTrans</c> class provides access to a <c>MarkupTrans</c> select
/// statement that has been created with a noFetch keyword. The select statement will return records
/// for a specific parent record.
/// </summary>
class TradeTotalsBuildNoFetchMarkupTrans
{
    MarkupTransRefTableId   sourceTableId;
    MarkupTransRefRecId     sourceRecId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildMarkupTransNoFetchSelect</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a <c>MarkupTrans</c> specific select statement for a specific parent record. The select statement is created
    ///    with the noFetch keyword to defer execution of the statement.
    /// </summary>
    /// <param name="_goodsInRouteId">
    ///    Identifier of journal of passing of property.
    /// </param>
    /// <returns>
    ///    A <c>MarkupTrans</c> select statement with a noFetch keyword.
    /// </returns>
    public MarkupTrans buildMarkupTransNoFetchSelect(
        GoodsInRouteId_RU _goodsInRouteId = ''
    )
    {
        MarkupTrans markupTransNoFetchSelect;

        if (AutoChargesSetupSequenceCompoundFeature::instance().IsEnabled())
        {
            select nofetch markupTransNoFetchSelect
                index hint TableRecIdIdx
                order by Position asc
                where markupTransNoFetchSelect.TransTableId      == this.parmSourceTableId()  &&
                      markupTransNoFetchSelect.TransRecId        == this.parmSourceRecId()    &&
                      // <GEERU>
                      markupTransNoFetchSelect.IsDeleted         == NoYes::No                 &&
                      (! _goodsInRouteId || markupTransNoFetchSelect.GoodsInRouteId_RU == _goodsInRouteId);
            // </GEERU>
        }
        else
        {
            select nofetch markupTransNoFetchSelect
                index hint TableRecIdIdx
                where markupTransNoFetchSelect.TransTableId      == this.parmSourceTableId()  &&
                      markupTransNoFetchSelect.TransRecId        == this.parmSourceRecId()    &&
                      // <GEERU>
                      markupTransNoFetchSelect.IsDeleted         == NoYes::No                 &&
                      (! _goodsInRouteId || markupTransNoFetchSelect.GoodsInRouteId_RU == _goodsInRouteId);
                // </GEERU>
        }

        return markupTransNoFetchSelect;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceRecId</Name>
				<Source><![CDATA[
        protected MarkupTransRefRecId parmSourceRecId(MarkupTransRefRecId _sourceRecId = sourceRecId)
    {
        sourceRecId = _sourceRecId;

        return sourceRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceTableId</Name>
				<Source><![CDATA[
        protected MarkupTransRefTableId parmSourceTableId(MarkupTransRefTableId _sourceTableId = sourceTableId)
    {
        sourceTableId = _sourceTableId;

        return sourceTableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        protected static TradeTotalsBuildNoFetchMarkupTrans construct()
    {
        return new TradeTotalsBuildNoFetchMarkupTrans();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new instance of the <c>TradeTotalsBuildNoFetchMarkupTrans</c> class.
    /// </summary>
    /// <param name="_sourceTableId">
    ///    The table ID of the parent of the <c>MarkupTrans</c> records.
    /// </param>
    /// <param name="_sourceRecId">
    ///    The record ID of the parent of the <c>MarkupTrans</c> records.
    /// </param>
    /// <returns>
    ///    An instance of the <c>TradeTotalsBuildNoFetchMarkupTrans</c> class.
    /// </returns>
    public static TradeTotalsBuildNoFetchMarkupTrans newFromSource(MarkupTransRefTableId _sourceTableId, MarkupTransRefRecId _sourceRecId)
    {
        TradeTotalsBuildNoFetchMarkupTrans tradeTotalsBuildNoFetchMarkupTrans = TradeTotalsBuildNoFetchMarkupTrans::construct();

        tradeTotalsBuildNoFetchMarkupTrans.parmSourceTableId(_sourceTableId);
        tradeTotalsBuildNoFetchMarkupTrans.parmSourceRecId(_sourceRecId);

        return tradeTotalsBuildNoFetchMarkupTrans;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>