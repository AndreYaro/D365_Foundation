<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSRSReportHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendSRSReportHelper</c> class is the helper class for the <c>Vend</c> and the
/// <c>VendBaseData</c> reports.
/// </summary>
class VendSRSReportHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDefaultDimensionValueDescription</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get dimension value and dimension description for a default system dimension.
    /// </summary>
    /// <param name="_vendTable">
    /// <c>VendTable</c> record which contains dimension information.
    /// </param>
    /// <param name="_sysDimension">
    /// Indicates target system dimension.
    /// </param>
    /// <returns>
    /// A container with two elements. The first is dimension value, and the
    /// second is dimension description.
    /// </returns>
    public static container getDefaultDimensionValueDescription(VendTable _vendTable, SysDimension _sysDimension)
    {
        DimensionAttributeValueSetItem dimensionAttributeValueSetItem;
        DimensionAttributeValue dimensionAttributeValue;
        DimensionAttributeRecId dimensionAttributeRecId;
        container valueAndDescription;
        DimensionDefault dimensionDefault = _vendTable.DefaultDimension;

        dimensionAttributeRecId = DimensionAttribute::findByName(VendSRSReportHelper::sysDimensionEnglishName(_sysDimension)).RecId;

        select firstOnly * from dimensionAttributeValue
                where dimensionAttributeValue.DimensionAttribute == dimensionAttributeRecId
            exists join dimensionAttributeValueSetItem
                where dimensionAttributeValueSetItem.DimensionAttributeValue == dimensionAttributeValue.RecId
                   && dimensionAttributeValueSetItem.DimensionAttributeValueSet == dimensionDefault;

        valueAndDescription = [dimensionAttributeValue.getValue(), dimensionAttributeValue.getName()];

        return valueAndDescription;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sysDimensionEnglishName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the en-US localized label for the supplied <c>SysDimension</c> value.
    /// </summary>
    /// <param name="_sysDimension">
    ///    The <c>SysDimension</c> value.
    /// </param>
    /// <returns>
    ///    The en-US localized label.
    /// </returns>
    private static str sysDimensionEnglishName(SysDimension _sysDimension)
    {
        str labelName;
        str labelString;
        SysDictEnum sysDictEnum;
        SysLabel sysLabel;

        // Get the label ID (@SYS123456) for the dimension value enum
        sysDictEnum = new SysDictEnum(enumnum(SysDimension));
        labelName = sysDictEnum.index2LabelId(_sysDimension);

        // Get the EN-US localization for the label string
        sysLabel = new SysLabel('EN-US');
        labelString = sysLabel.extractString(labelName);

        return labelString;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>