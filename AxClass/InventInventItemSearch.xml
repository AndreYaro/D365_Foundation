<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventInventItemSearch</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>InventInventItemSearch</c> class provides an interface to search for an Product or Variant
/// </summary>
public class InventInventItemSearch
{
    List    searchProcessors;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createSearchProcessors</Name>
				<Source><![CDATA[
    protected void createSearchProcessors()
    {
        searchProcessors = new List(Types::Class);

        searchProcessors.addEnd(new InventInventItemSearchItemId());
        searchProcessors.addEnd(new InventInventItemSearchItemAlias());
        searchProcessors.addEnd(new InventInventItemSearchBarcode());
        searchProcessors.addEnd(new InventInventItemSearchExternal());
        searchProcessors.addEnd(new InventInventItemSearchGTIN());

        return;
    }
]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }
]]></Source>
			</Method>
			<Method>
				<Name>search</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Performs a search for an item matching the passed <c>_searchValue</c> parameter.
    /// </summary>
    /// <param name="_searchValue">
    ///     Value used to search for a matching item.
    /// </param>
    /// <returns>
    ///     <c>InventInventItemSearchResult</c> record containing the information found (if any).
    ///     If the client requested variant information to be returned, this will be populated in the
    ///     <c>InventInventItemSearchResult</c> record.
    /// </returns>
    public InventInventItemSearchResult search(InventInventItemSearchItem _searchValue)
    {
        InventInventItemSearchResult    result;

        if (searchProcessors == null)
        {
            this.createSearchProcessors();
        }

        ListEnumerator listEnumerator = searchProcessors.getEnumerator();
        while (!result && listEnumerator.moveNext())
        {
            InventInventItemSearchProcessor processor = listEnumerator.current();
            result = processor.search(_searchValue);
        }
        return result;
    }
]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates an <c>InventInventItemSearch</c> object.
    /// </summary>
    /// <returns>
    ///     An initialized <c>InventInventItemSearch</c> object.
    /// </returns>
    public static InventInventItemSearch construct()
    {
        return new InventInventItemSearch();
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
