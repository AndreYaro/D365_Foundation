<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqDemPlanForecastAttributeListPanel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ReqDemPlanForecastAttributeListPanel</c> class handles list panels
/// used for defining forecast dimensions that are used in forecast aggregation process.
/// </summary>
class ReqDemPlanForecastAttributeListPanel extends SysListPanel
{
    ReqDemPlanForecastAttributeManager  attributeManager = ReqDemPlanForecastAttributeManager::construct();

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a container which holds two containers, one with data for the left list panel and one with data for the right list panel.
    /// </summary>
    /// <returns>
    /// A container which holds two containers, one with data for the left list panel and one with data for the right list panel.
    /// </returns>
    public container getData()
    {
        ReqDemPlanForecastAttribute attribute;
        container                   available, selected;

        while select attribute order by attribute.Attribute where attribute.Enabled && attribute.Configurable
        {
            selected = this.addPivotAttributeToContainer(attribute, selected);
        }

        while select attribute order by attribute.Attribute where attribute.Enabled == false && attribute.Configurable
        {
            available = this.addPivotAttributeToContainer(attribute, available);
        }

        return [available, selected];
    }

]]></Source>
			</Method>
			<Method>
				<Name>addData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method that is called when item is added to selected list.
    /// </summary>
    /// <param name="_data">
    /// A container with the data to add.
    /// </param>
    public void addData(container _data)
    {
        int i, containerLength;

        containerLength = conlen(_data);

        for (i=1; i <= containerLength; i++)
        {
            attributeManager.enableAttribute(conpeek(_data,i));
        }

        this.fill();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method that is called when item is removed from selected list.
    /// </summary>
    /// <param name="_data">
    /// A container with the Id's of the elements that should be removed.
    /// </param>
    public void removeData(container _data)
    {
        int i, containerLength;

        containerLength = conlen(_data);

        for (i=1; i <= containerLength; i++)
        {
            attributeManager.disableAttribute(conpeek(_data,i));
        }

        this.fill();
    }

]]></Source>
			</Method>
			<Method>
				<Name>persistSelectedPivotAttributes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Persists selected pivot attributes.
    /// </summary>
    public void persistSelectedPivotAttributes()
    {
        ReqDemPlanForecastAttributeType attribute;
        ReqDemPlanPivotAttributes       pivotAttribute;
        Counter                         countOfItemsLeft;
        FormListItem                    formListItem;

        var itemCount = this.parmViewSelected().getCount();

        ttsbegin;

        delete_from pivotAttribute;

        for (var i = 0; i < itemCount; i++)
        {
            var itemText = this.parmViewSelected().getItem(i).text();
            attribute = itemText == "@SYS12836" ? ReqDemPlanForecastAttributeType::ItemId : str2Enum(attribute, itemText);

            ReqDemPlanPivotAttributes::insertByAttributeType(attribute);
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static ReqDemPlanForecastAttributeListPanel construct()
    {
        return new ReqDemPlanForecastAttributeListPanel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a new instance of the <c>ReqDemPlanForecastAttributeListPanel</c> class.
    /// </summary>
    /// <param name="_formRun">
    /// A formrun that should have the list panels added.
    /// </param>
    /// <param name="_parentId">
    /// Id of the parent element.
    /// </param>
    /// <returns>
    /// A new instance of the <c>ReqDemPlanForecastAttributeListPanel</c> class.
    /// </returns>
    public static ReqDemPlanForecastAttributeListPanel newFromForm(
        FormRun     _formRun,
        int         _parentId)
    {
        ReqDemPlanForecastAttributeListPanel panel = ReqDemPlanForecastAttributeListPanel::construct();

        panel.parmFormRun(_formRun);
        panel.parmParentId(_parentId);
        panel.parmCaptionAvailable("@DMP1422");
        panel.parmCaptionSelected("@DMP1423");
        panel.parmHasUpDownButton(false);
        panel.parmUseImageListOverlay(false);
        panel.build();
        return panel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPivotAttributeToContainer</Name>
				<Source><![CDATA[
    private container addPivotAttributeToContainer(ReqDemPlanForecastAttribute _pivotAttribute, container _container)
    {
        _container += [[_pivotAttribute.Attribute, enum2Str(_pivotAttribute.Attribute), enum2Str(_pivotAttribute.Attribute)]];
        return _container;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>