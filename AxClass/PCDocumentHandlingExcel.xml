<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PCDocumentHandlingExcel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PCDocumentHandlingExcel</c> class helps to attach the sales price breakdown to the source line.
/// </summary>
class PCDocumentHandlingExcel
{
    private const str ExcelFileMimeType = 'application/vnd.ms-excel';

    Name documentName;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createOrUpdateDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates a document with a file attachment.
    /// </summary>
    /// <param name="_sourceDocumentLine">
    /// A source document line.
    /// </param>
    /// <param name="_fileName">
    /// A path to the file.
    /// </param>
    /// <param name="_fileStream">
    /// The stream with a document content.
    /// </param>
    public void createOrUpdateDocument(Common _sourceDocumentLine, str _fileName, System.IO.Stream _fileStream)
    {
        DocuRef document;
        EcoResProductParameters productParameters;

        if (_sourceDocumentLine)
        {
            changeCompany(_sourceDocumentLine.dataAreaId)
            {
                document = this.findDocumentBySourceLine(_sourceDocumentLine, true);

                if (document)
                {
                    document.delete();
                }

                productParameters = EcoResProductParameters::find();

                document = DocumentManagement::attachFile(_sourceDocumentLine.TableId, _sourceDocumentLine.RecId, _sourceDocumentLine.DataAreaId, productParameters.PriceBreakdownDocuTypeId , _fileStream, _fileName, ExcelFileMimeType, documentName, '');
                document.Restriction = DocuRestriction::External;
                document.update();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findDocumentBySourceLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds the attached document..
    /// </summary>
    /// <param name="_sourceLine">
    /// The product configuration source line.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <returns>
    /// A record in the <c>DocuRef</c> table; otherwise, an empty record.
    /// </returns>
    public DocuRef findDocumentBySourceLine(Common _sourceLine, boolean _forUpdate =false)
    {
        DocuRef documentReference;
        TableId baseTableId = DocuRef::GetRootTableID(_sourceLine.TableId);

        documentReference.selectForUpdate(_forUpdate);

        select  firstonly documentReference
        where   documentReference.RefRecId      == _sourceLine.RecId
        &&      documentReference.RefTableId    == baseTableId
        &&      documentReference.Restriction   == DocuRestriction::External
        &&      documentReference.Name          == documentName;

        return documentReference;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDocumentName</Name>
				<Source><![CDATA[
    public Name parmDocumentName(Name _documentName = documentName)
    {
        documentName = _documentName;

        return documentName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PCDocumentHandlingExcel construct()
    {
        return new PCDocumentHandlingExcel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createForPriceBreakdown</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs new instance of <c>PCDocumentHandlingExcel</c> dedicated to process price breakdown document.
    /// </summary>
    /// <returns>
    /// An instance of <c>PCDocumentHandlingExcel</c> class.
    /// </returns>
    public static PCDocumentHandlingExcel createForPriceBreakdown()
    {
        PCDocumentHandlingExcel documentHandlingExcel = PCDocumentHandlingExcel::construct();

        documentHandlingExcel.parmDocumentName("@SYS4001897");

        return  documentHandlingExcel;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>