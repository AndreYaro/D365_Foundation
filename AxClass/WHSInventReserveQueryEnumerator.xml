<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSInventReserveQueryEnumerator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSInventReserveQueryEnumerator</c> class is used to iterate the <c>WHSInventReserveQueryCollection</c> class.
/// </summary>
class WHSInventReserveQueryEnumerator extends WHSInventOnHandCollectionEnumerator
{
    Query                                   queryInventReserve;
    QueryRun                                queryRunInventReserve;
    WHSInventOnHandData                     whsInventOnHandData;
    boolean                                 atBeginning;
    boolean                                 atTheEnd;
    TableId                                 sumTableId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createQueryRunFromQuery</Name>
				<Source><![CDATA[
        private void createQueryRunFromQuery(Query _query)
    {
        queryRunInventReserve = new QueryRun(_query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>current</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the current element in the collection.
    /// </summary>
    /// <returns>
    /// The current element in the collection.
    /// </returns>
    /// <exception cref="Exception::Error">
    /// The enumerator does not point to any element in the collection.
    /// </exception>
    public anyType current()
    {
        if (atBeginning || atTheEnd)
        {
            throw error("@SYS125456");
        }

        whsInventOnHandData = WHSInventOnHandData::newFromCommon(queryRunInventReserve.get(sumTableId),queryRunInventReserve.get(tableNum(inventDim)));

        return whsInventOnHandData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>definitionString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the description for the <c>WHSInventReserveQueryEnumerator</c> class.
    /// </summary>
    /// <returns>
    /// The description for the <c>WHSInventReserveQueryEnumerator</c> class.
    /// </returns>
    public str definitionString()
    {
        return classStr(WHSInventReserveQueryEnumerator);
    }

]]></Source>
			</Method>
			<Method>
				<Name>moveNext</Name>
				<Source><![CDATA[
    /// <summary>
    /// Advances the enumerator to the next element of the collection.
    /// </summary>
    /// <returns>
    /// true if the enumerator was advanced; otherwise, false.
    /// </returns>
    public boolean moveNext()
    {
        whsInventOnHandData = null;
        if (!queryRunInventReserve.next())
        {
            atTheEnd = true;
        }

        atBeginning = false;

        return !atTheEnd;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        super();
        atBeginning = true;
        atTheEnd = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQueryRunInventReserve</Name>
				<Source><![CDATA[
        public queryRun parmQueryRunInventReserve(queryRun _queryRunInventReserve = queryRunInventReserve)
    {
        queryRunInventReserve = _queryRunInventReserve;

        return queryRunInventReserve;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmsumTableId</Name>
				<Source><![CDATA[
        public tableId parmsumTableId(tableId _sumTableId = sumTableId)
    {
        sumTableId = _sumTableId;

        return sumTableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reset</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the enumerator to its initial position, which is before the first element in the collection.
    /// </summary>
    public void reset()
    {
        queryRunInventReserve.reset();

        atBeginning = true;
        atTheEnd = false;
        whsInventOnHandData = null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQueryInventReserve</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the query that will be used to enumerate the reservation information.
    /// </summary>
    /// <param name="_queryInventReserve">
    /// The query that will be used to enumerate the reservation information.
    /// </param>
    public void setQueryInventReserve(Query _queryInventReserve )
    {
        queryInventReserve = _queryInventReserve;

        this.createQueryRunFromQuery(queryInventReserve);
    }

]]></Source>
			</Method>
			<Method>
				<Name>toString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the string representation of the collection element that enumerator points to.
    /// </summary>
    /// <returns>
    /// The string representation of the collection element that enumerator points to.
    /// </returns>
    public str toString()
    {
        str toString;

        if (whsInventOnHandData)
        {
            toString = whsInventOnHandData.toString();
        }
        else
        {
            toString = "@SYS344541";
        }

        return toString;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        private static WHSInventReserveQueryEnumerator construct()
    {
        return new WHSInventReserveQueryEnumerator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromQueryCollection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a new instance of this class based on the parameter.
    /// </summary>
    /// <param name="_whsInventReserveQueryCollection">
    /// The collection that should be enumerated.
    /// </param>
    /// <returns>
    /// A new instance of this class
    /// </returns>
    public static WHSInventReserveQueryEnumerator newFromQueryCollection(
        WHSInventReserveQueryCollection _whsInventReserveQueryCollection)
    {
        WHSInventReserveQueryEnumerator whsInventReserveQueryEnumerator = WHSInventReserveQueryEnumerator::construct();
        whsInventReserveQueryEnumerator.setQueryInventReserve(_whsInventReserveQueryCollection.parmOnHandQuery());
        whsInventReserveQueryEnumerator.parmsumTableId(_whsInventReserveQueryCollection.parmSumTableId());

        return whsInventReserveQueryEnumerator;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
