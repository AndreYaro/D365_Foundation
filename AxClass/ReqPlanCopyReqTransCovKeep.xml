<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqPlanCopyReqTransCovKeep</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ReqPlanCopyReqTransCovKeep</c> class copies the preserved requirements coverage from the source master plan version
/// to the target master plan version.
/// </summary>
class ReqPlanCopyReqTransCovKeep extends SysQueryInsertRecordSet
{
    int reqTransCovDatasourceId;
    int reqTransDataSourceId;

    ReqPlanVersion reqPlanVersionFrom;
    ReqPlanVersion reqPlanVersionTo;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFieldMapping</Name>
				<Source><![CDATA[
    protected void initFieldMapping()
    {
        this.mapField(reqTransCovDatasourceId, fieldStr(ReqTransCov, Qty), fieldStr(ReqTransCov, Qty));
        this.mapField(reqTransCovDatasourceId, fieldStr(ReqTransCov, ActionQtyAdd), fieldStr(ReqTransCov, ActionQtyAdd));
        this.mapField(reqTransCovDatasourceId, fieldStr(ReqTransCov, ItemId), fieldStr(ReqTransCov, ItemId));
        this.mapField(reqTransCovDatasourceId, fieldStr(ReqTransCov, IsDerivedDirectly), fieldStr(ReqTransCov, IsDerivedDirectly));
        this.mapField(reqTransCovDatasourceId, fieldStr(ReqTransCov, IssueRecId), fieldStr(ReqTransCov, IssueRecId));

        this.mapField(reqTransDataSourceId, fieldStr(ReqTrans, RecId), fieldStr(ReqTransCov, ReceiptRecId));
        this.mapField(reqTransDataSourceId, fieldStr(ReqTrans, PlanVersion), fieldStr(ReqTransCov, PlanVersion));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    protected Query initQuery()
    {
        Query query = new Query();

        QueryBuildDataSource qbdsReqTransCovSelect = query.addDataSource(tableNum(ReqTransCov));
        qbdsReqTransCovSelect.addRange(fieldNum(ReqTransCov, PlanVersion)).value(queryValue(reqPlanVersionFrom.RecId));
        reqTransCovDatasourceId = qbdsReqTransCovSelect.uniqueId();
        
        QueryBuildDataSource qbdsReqTransReceipt = qbdsReqTransCovSelect.addDataSource(tableNum(ReqTrans));
        qbdsReqTransReceipt.addLink(fieldNum(ReqTransCov, ReceiptRecId), fieldNum(ReqTrans, LastPlanRecId));
        qbdsReqTransReceipt.addRange(fieldNum(ReqTrans, PlanVersion)).value(queryValue(reqPlanVersionTo.RecId));
        reqTransDataSourceId = qbdsReqTransReceipt.uniqueId();

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initTargetCursor</Name>
				<Source><![CDATA[
    protected Common initTargetCursor()
    {
        ReqTransCov reqTransCovInsert;
        reqTransCovInsert.skipDataMethods(true);
        reqTransCovInsert.skipAosValidation(true);

        return reqTransCovInsert;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromReqPlanVersions</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates a new instance of the <c>ReqPlanCopyReqTransCovKeep</c> class.
    /// </summary>
    /// <param name="_reqPlanVersionFrom">
    ///     The version of the source master plan.
    /// </param>
    /// <param name="_reqPlanVersionTo">
    ///     The version of the target master plan.
    /// </param>
    /// <returns>A new instance.</returns>
    public static ReqPlanCopyReqTransCovKeep newFromReqPlanVersions(ReqPlanVersion _reqPlanVersionFrom, ReqPlanVersion _reqPlanVersionTo)
    {
        ReqPlanCopyReqTransCovKeep instance = new ReqPlanCopyReqTransCovKeep();
        instance.reqPlanVersionFrom = _reqPlanVersionFrom;
        instance.reqPlanVersionTo = _reqPlanVersionTo;

        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>