<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendCreatePaymJournalBillSettle_CustApplicationSuite_IT_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(CustVendCreatePaymJournalBillSettle_Cust))]
public final class CustVendCreatePaymJournalBillSettle_CustApplicationSuite_IT_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>searchTransactions</Name>
				<Source><![CDATA[
    protected void searchTransactions()
    {
        next searchTransactions();

        if (FeatureStateProvider::isFeatureEnabled(ConfigurablePostingProfilesForBanksAndRemittanceTypesFeature::instance()))
        {
            Map postingProfilesMap = new Map(Types::String, Types::String);
            CustVendPaymProposalLine custVendPaymProposalLine;
            CustVendPaymProposalLine tmpPostingProfiles;
            tmpPostingProfiles.setTempDB();

            while select custVendPaymProposalLine
                where custVendPaymProposalLine.JournalId == paymProposal.ledgerJournalId()
            {
                tmpPostingProfiles.clear();
                tmpPostingProfiles.TmpRecId = custVendPaymProposalLine.RecId;

                if (postingProfilesMap.exists(custVendPaymProposalLine.CompanyBankAccountIdNegInst))
                {
                    tmpPostingProfiles.PostingProfile = postingProfilesMap.lookup(custVendPaymProposalLine.CompanyBankAccountIdNegInst);
                }
                else
                {
                    tmpPostingProfiles.PostingProfile = ConfigurablePostingProfilesForBanksAndRemittanceTypes::getPostingProfile(
                        custVendPaymProposalLine.CompanyBankAccountIdNegInst,
                        remittanceType,
                        ModuleCustVend::Cust);

                    postingProfilesMap.insert(custVendPaymProposalLine.CompanyBankAccountIdNegInst, tmpPostingProfiles.PostingProfile);
                }

                tmpPostingProfiles.insert();
            }

            update_recordset custVendPaymProposalLine
                setting PostingProfile = tmpPostingProfiles.PostingProfile
                join tmpPostingProfiles
                    where tmpPostingProfiles.TmpRecId == custVendPaymProposalLine.RecId;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>