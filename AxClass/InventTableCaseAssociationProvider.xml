<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Item'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class InventTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        InventTable inventTable;
        select crosscompany DataAreaId from inventTable where inventTable.RecId == _parentRecId;

        return inventTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        InventTable inventTable;
        select crosscompany ItemId from inventTable where inventTable.RecId == _parentRecId;

        return inventTable.ItemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        InventTable inventTable;
        select RecId from inventTable where inventTable.ItemId == _naturalKey;

        return inventTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        InventTable inventTable;
        select crosscompany NameAlias from inventTable where inventTable.RecId == _parentRecId;

        return inventTable.NameAlias;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        InventTable inventTable;
        InventTable inventTableBufferCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from inventTableBufferCrossCompany
            where inventTableBufferCrossCompany.RecId == _association.RefRecId;

        if (inventTableBufferCrossCompany.Recid)
        {
            changecompany (inventTableBufferCrossCompany.DataAreaId)
            {
                select firstonly inventTable where inventTable.RecId == _association.RefRecId;
            }
        }

        _args.record(inventTable);
        return menuitemdisplaystr(EcoResProductDetailsExtended);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        InventTable::lookupItem(callerControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(InventTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Item);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>