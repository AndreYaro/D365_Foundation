<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgShopSupervisorWorkspaceConfigurationFormController</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class JmgShopSupervisorWorkspaceConfigurationFormController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>modifiedProdUnit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears <c>ResourceGroupFilter</c> and/or <c>ResourceFilter</c> if needed
    /// after <c>ProdUnitFilter</c> field has been modified.
    /// </summary>
    /// <param name = "_config">A <c>JmgShopSupervisorWorkspaceConfiguration</c> table buffer.</param>
    /// <returns>true if at least one field was cleared; false otherwise.</returns>
    public static boolean modifiedProdUnit(JmgShopSupervisorWorkspaceConfiguration _config)
    {
        if (!_config.ProdUnitFilter)
        {
            return false;
        }

        boolean needRefresh = false;

        var selectedResource       = WrkCtrTable::find(_config.ResourceFilter);
        var selectedResourceGroup  = WrkCtrResourceGroup::findWrkCtrId(_config.ResourceGroupFilter);
        var actualResourceGroup    = WrkCtrTable::resourceGroup(_config.ResourceFilter);

        if (selectedResourceGroup.RecId && selectedResourceGroup.ProdUnitId != _config.ProdUnitFilter)
        {
            _config.ResourceGroupFilter = '';
            _config.ResourceFilter = '';
            needRefresh = true;
        }
        else if (selectedResource.RecId && actualResourceGroup.ProdUnitId != _config.ProdUnitFilter)
        {
            _config.ResourceFilter = '';
            needRefresh = true;
        }

        return needRefresh;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedResourceGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears <c>ResourceFilter</c> if needed after <c>ResourceGroupFilter</c> field has been modified.
    /// </summary>
    /// <param name = "_config">A <c>JmgShopSupervisorWorkspaceConfiguration</c> table buffer.</param>
    /// <returns>true if <c>ResourceFilter</c> field was cleared; false otherwise.</returns>
    public static boolean modifiedResourceGroup(JmgShopSupervisorWorkspaceConfiguration _config)
    {
        boolean needRefresh = false;

        if (!_config.ResourceGroupFilter)
        {
            return needRefresh;
        }

        var selectedResource    = WrkCtrTable::find(_config.ResourceFilter);
        var actualResourceGroup = WrkCtrTable::resourceGroup(_config.ResourceFilter);

        if (selectedResource.RecId && actualResourceGroup.WrkCtrId != _config.ResourceGroupFilter)
        {
            _config.ResourceFilter = '';
            needRefresh = true;
        }

        return needRefresh;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateResourceGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the <c>ResourceGroupFilter</c> is valid according to the <c>ProdUnitFilter</c> field's value.
    /// </summary>
    /// <param name = "_config">A <c>JmgShopSupervisorWorkspaceConfiguration</c> table buffer.</param>
    /// <returns>true if <c>ResourceGroupFilter</c> is valid; false otherwise.</returns>
    public static boolean validateResourceGroup(JmgShopSupervisorWorkspaceConfiguration _config)
    {
        if (!_config.ResourceGroupFilter || !_config.ProdUnitFilter)
        {
            return true;
        }

        var selectedResourceGroup = WrkCtrResourceGroup::findWrkCtrId(_config.ResourceGroupFilter);
        if (selectedResourceGroup.ProdUnitId != _config.ProdUnitFilter)
        {
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateResource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the <c>ResourceFilter</c> is valid according to
    /// the <c>ResourceGroupFilter</c> and <c>ProdUnitFilter</c> fields' values.
    /// </summary>
    /// <param name = "_config">A <c>JmgShopSupervisorWorkspaceConfiguration</c> table buffer.</param>
    /// <returns>true if <c>ResourceFilter</c> is valid; false otherwise.</returns>
    public static boolean validateResource(JmgShopSupervisorWorkspaceConfiguration _config)
    {
        if (!_config.ResourceFilter)
        {
            return true;
        }

        var selectedResource = WrkCtrTable::find(_config.ResourceFilter);
        if (!selectedResource.RecId)
        {
            return false;
        }

        var selectedResourceGroup = WrkCtrResourceGroup::findWrkCtrId(_config.ResourceGroupFilter);
        var actualResourceGroup   = WrkCtrTable::resourceGroup(selectedResource.WrkCtrId);
        if (selectedResourceGroup.RecId && selectedResourceGroup.RecId != actualResourceGroup.RecId)
        {
            return false;
        }

        var selectedProductionUnit = ProdUnitTable::find(_config.ProdUnitFilter);
        if (selectedProductionUnit.RecId && actualResourceGroup.ProdUnitId != selectedProductionUnit.ProdUnitId)
        {
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>