<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SettlementPair_VendPaym</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Represents a single debit-credit relationship during settlement. Debit is usually an invoice while credit is usually a payment.
/// Relationship between debits and credits is many-to-many, therefore single invoice or single payment can be partially present in multiple SettlementPair relationships.
/// </summary>
/// <remarks>
/// This class should be instantiated and used only from inside <c>CustVendSettle</c> class.
/// Additional functionality currently present on <c>CustVendSettle</c> class will be moved to <c>SettlementPair</c> class in further releases.
/// Get and set methods on this class exposing individual table buffers are going to be removed in subsequent releases in favor of more coarse operations.
/// </remarks>
public class SettlementPair_VendPaym extends SettlementPair_Vend
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fetchInvoice</Name>
				<Source><![CDATA[
    public boolean fetchInvoice(dataAreaId _company, tableId _tableId, RecId _recId)
    {
        boolean recordFound;

        if (!specTransDebit)
        {
            select forupdate crossCompany specTransDebit
                where specTransDebit.SpecCompany == _company
                   && specTransDebit.SpecTableId == _tableId
                   && specTransDebit.SpecRecId   == _recId
                   && !specTransDebit.Payment
            join forupdate custVendTransOpenDebit
                order by AmountCur desc, CashDiscDate, DueDate, RecId
                where custVendTransOpenDebit.DataAreaId == specTransDebit.RefCompany
                   && custVendTransOpenDebit.TableId    == specTransDebit.RefTableId
                   && custVendTransOpenDebit.RecId      == specTransDebit.RefRecId;
        }
        else
        {
            next specTransDebit;
        }

        // This can't be part of join because it causes problems if several CustVendTransOpen records
        // have a RefRecId to the same CustVendTrans (this happens if there is a payment schedule and
        // are settling two or more transactions on the payment schedule).  The problems will be an
        // update conflict error message and the CustVendTrans not having the updated fields.
        changecompany(custVendTransOpenDebit.company())
        {
            // Check if both CustVendTransOpenDebit and CustVendTransOpenCredit records point to the
            // same CustVendTrans record
            if (custVendTransOpenDebit.RefRecId == custVendTransOpenCredit.RefRecId)
            {
                custVendTransDebit = custVendTransCredit;
            }
            else
            {
                 custVendTransDebit = VendTrans::find(custVendTransOpenDebit.RefRecId, true);
            }
        }

        SettlementUtility::reverseSpecTransAmounts(specTransDebit);
        SettlementUtility::reverseTransAmounts(custVendTransDebit);
        SettlementUtility::reverseTransOpenAmounts(custVendTransOpenDebit);

        if (specTransDebit.RecId != 0)
        {
            recordFound = true;
            this.initDebitCompanyCurrency();
            SettlementUtility::roundTransactionAmounts(custVendTransDebit, specTransDebit, debitCompanyCurrency);
        }

        this.forceExchRateCalculation();

        return recordFound;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetchPayment</Name>
				<Source><![CDATA[
    public boolean fetchPayment(dataAreaId _company, tableId _tableId, RecId _recId)
    {
        boolean recordFound;

        if (!specTransCredit)
        {
            select forupdate crossCompany specTransCredit
                where specTransCredit.SpecCompany == _company
                   && specTransCredit.SpecTableId == _tableId
                   && specTransCredit.SpecRecId   == _recId
                   && specTransCredit.Payment
            join forupdate custVendTransOpenCredit
                order by TransDate, RecId
                where custVendTransOpenCredit.DataAreaId == specTransCredit.RefCompany
                   && custVendTransOpenCredit.TableId   == specTransCredit.RefTableId
                   && custVendTransOpenCredit.RecId     == specTransCredit.RefRecId;
        }
        else
        {
            next specTransCredit;
        }

        if (specTransCredit.RecId == 0)
        {
            this.throwIfOversettled();
        }

        // This can't be part of join because it causes problems if several CustVendTransOpen records
        // have a RefRecId to the same CustVendTrans (this happens if there is a payment schedule and
        // are settling two or more transactions on the payment schedule).  The problems will be an
        // update conflict error message and the CustVendTrans not having the updated fields.
        changecompany(custVendTransOpenCredit.company())
        {
            // Check if both CustVendTransOpenCredit and settlementPair.getCustVendTransOpenDebit() records point to the
            // same CustVendTrans record
            if (custVendTransOpenCredit.RefRecId == custVendTransOpenDebit.RefRecId)
            {
                custVendTransCredit = custVendTransDebit;
            }
            else
            {
                custVendTransCredit = VendTrans::find(custVendTransOpenCredit.RefRecId, true);
            }
        }

        SettlementUtility::reverseSpecTransAmounts(specTransCredit);
        SettlementUtility::reverseTransAmounts(custVendTransCredit);
        SettlementUtility::reverseTransOpenAmounts(custVendTransOpenCredit);

        if (specTransCredit.RecId != 0)
        {
            recordFound = true;
            this.initCreditCompanyCurrency();
            SettlementUtility::roundTransactionAmounts(custVendTransCredit, specTransCredit, creditCompanyCurrency);
        }

        this.forceExchRateCalculation();

        return recordFound;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>