<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GBTExportDataToXML_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class GBTExportDataToXML_CN
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validatePeriodName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the fiscal calendar period user chooses is valid.
    /// </summary>
    /// <param name="_fiscalPeriodName">
    /// The fiscal period name of current dialog field.
    /// </param>
    /// <param name="_fiscalYearname">
    /// The fiscal year name of current dialog field.
    /// </param>
    /// <returns>
    /// True when user inputs for fiscal calendar period is valid, else, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static boolean validatePeriodName(
        PeriodName      _fiscalPeriodName,
        FiscalYearName  _fiscalYearname)
    {
        boolean                 ret = true;
        FiscalCalendarPeriod    fiscalCalendarPeriodLocal;
        FiscalCalendarYear      fiscalCalendarYearLocal;

        select firstOnly RecId from fiscalCalendarPeriodLocal
            where fiscalCalendarPeriodLocal.Name            == _fiscalPeriodName
            exists join fiscalCalendarYearLocal
                where fiscalCalendarYearLocal.FiscalCalendar    == Ledger::fiscalCalendar(CompanyInfo::current())
                    && fiscalCalendarYearLocal.Name             == _fiscalYearname
                    && fiscalCalendarYearLocal.RecId            == fiscalCalendarPeriodLocal.FiscalCalendarYear;

        if (!fiscalCalendarPeriodLocal.RecId)
        {
            //The period %1 is not valid for the fiscal year. Change the fiscal year or select another fiscal period.
            ret = checkFailed(strFmt("@GLS70437", _fiscalPeriodName));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateYearName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the fiscal calendar year user chooses is valid.
    /// </summary>
    /// <param name="_fiscalYearName">
    /// The fiscal year name of current dialog field.
    /// </param>
    /// <returns>
    /// True when user inputs for fiscal calendar year is valid, else, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static boolean validateYearName(FiscalYearName _fiscalYearName)
    {
        boolean existCheck = true;

        if (_fiscalYearName)
        {
            existCheck = FiscalCalendarYear::existByCalendarYearName(
                Ledger::fiscalCalendar(CompanyInfo::current()),
                _fiscalYearName);
        }

        if (!existCheck)
        {
            return checkFailed(
                strFmt(
                    FiscalCalendarYear::txtNotExistByCalendarYear(),
                    FiscalCalendar::find(Ledger::fiscalCalendar(CompanyInfo::current())).CalendarId,
                    _fiscalYearName));
        }
        else
        {
            return true;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>