<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendEditInvoiceCBD_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>VendEditInvoiceCBD_Extension</c> is an extension class of the <c>VendEditInvoice</c>
/// that supports ER runtime print destinations.
/// </summary>
[ExtensionOf(formStr(VendEditInvoice))]
internal final class VendEditInvoiceCBD_Extension
{
    private boolean isERPrintSetupExists = false;
    private boolean isSSRSPrintSetupExists = true;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        if (purchFormLetter.isSupportedCBDReport())
        {
            PrintElectronicReportingOptions.visible(true);
            this.analyzePrintManagementSetup();
            this.setEnabledEROptions();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>reArrange</Name>
				<Source><![CDATA[
    public boolean reArrange(boolean _clearTotals)
    {
        boolean ret = next reArrange(_clearTotals);

        if (purchFormLetter.isSupportedCBDReport())
        {
            this.analyzePrintManagementSetup();
            this.setEnabledEROptions();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>printOptionsClickedPost</Name>
				<Source><![CDATA[
    public void printOptionsClickedPost()
    {
        next printOptionsClickedPost();

        if (purchFormLetter.isSupportedCBDReport())
        {
            this.setEnabledEROptions();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setPrintVariables</Name>
				<Source><![CDATA[
    public void setPrintVariables(
        boolean _printInvoice,
        Printout _printout,
        boolean _usePrintManagement,
        boolean _printSalesFormLetter,
        boolean _printEUVatInvoice,
        boolean _printFacture,
        boolean _printAcceptance,
        boolean _printInventBailee,
        boolean _printExciseInvoice,
        ExciseInvoiceAddressType_IN _exciseInvoiceAddress)
    {
        next setPrintVariables(
            _printInvoice,
            _printout,
            _usePrintManagement,
            _printSalesFormLetter,
            _printEUVatInvoice,
            _printFacture,
            _printAcceptance,
            _printInventBailee,
            _printExciseInvoice,
            _exciseInvoiceAddress);

        if (purchFormLetter.isSupportedCBDReport())
        {
            this.setEnabledEROptions();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printerSettingsClickedPost</Name>
				<Source><![CDATA[
    public void printerSettingsClickedPost()
    {
        next printerSettingsClickedPost();

        if (purchFormLetter.isSupportedCBDReport())
        {
            buttonPrintFormletter.enabled(purchFormLetter.printFormLetter() && isSSRSPrintSetupExists);
            buttonPrintFormletterCopy.enabled(purchFormLetter.printFormLetter() && isSSRSPrintSetupExists);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>analyzePrintManagementSetup</Name>
				<Source><![CDATA[
    private void analyzePrintManagementSetup()
    {
        [isSSRSPrintSetupExists, isERPrintSetupExists] = purchFormLetter.analyzePrintManagementSetup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setEnabledEROptions</Name>
				<Source><![CDATA[
    private void setEnabledEROptions()
    {
        PrintElectronicReportingOptions.enabled(purchFormLetter.printFormLetter() && isERPrintSetupExists);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>