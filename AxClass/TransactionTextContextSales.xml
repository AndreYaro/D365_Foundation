<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TransactionTextContextSales</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TransactionTextContextCust</c> class enables additional parameter value functionality for
/// <c>SalesCreditNoteCust</c>, <c>SalesCreditNoteLedger</c>, <c>SalesInvoiceCust</c>, and
/// <c>SalesInvoiceLedger</c> documents.
/// </summary>
[
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, SalesCreditNoteCust)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, SalesCreditNoteLedger)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, SalesInvoiceCust)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, SalesInvoiceLedger))
]
class TransactionTextContextSales extends TransactionTextContext
{
    CustInvoiceJour     custInvoiceJour;
    SalesTable          salesTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSupportedTables</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides a list of tables that are supported for use with the specified transaction type.
    /// </summary>
    /// <returns>
    /// A container that contains the <c>TableId</c> collection of tables that are supported by specified
    /// transaction type.
    /// </returns>
    /// <remarks>
    /// The container that contains only those tables that are associated by the specified transaction type.
    /// </remarks>
    public container getSupportedTables()
    {
        container   tableList;

        tableList += tableNum(CustInvoiceJour);
        tableList += tableNum(SalesTable);

        return tableList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the parameter variable of the object to contain the table buffers that are used by the
    /// specified transaction type.
    /// </summary>
    /// <remarks>
    /// This must be called before you attempt to map the table fields and values for the transaction text
    /// setup parameters.
    /// </remarks>
    public void setParameters()
    {
        parameters = [custInvoiceJour, salesTable];
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTableBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set a particular table buffer for the specified transaction type.
    /// </summary>
    /// <param name="_common">
    /// The <c>Common</c> record that is passed in and set based on the <c>TableId</c> of the buffer.
    /// </param>
    /// <returns>
    /// true if the table is supported and buffer is saved; otherwise, false.
    /// </returns>
    /// <remarks>
    /// The common record buffer is cast to the appropriate table through switch logic. This method can be
    /// called repeatedly to assign multiple buffers.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    /// Thrown if table is not supported by this transaction type.
    /// </exception>
    public boolean setTableBuffer(Common _common)
    {
        boolean tableBufferSet;

        switch (_common.TableId)
        {
            case tableNum(CustInvoiceJour):
                custInvoiceJour = _common as CustInvoiceJour;
                tableBufferSet = true;
                break;
            case tableNum(SalesTable):
                salesTable = _common as SalesTable;
                tableBufferSet = true;
                break;
        }

        return tableBufferSet;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>