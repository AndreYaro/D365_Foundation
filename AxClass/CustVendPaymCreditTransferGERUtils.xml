<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendPaymCreditTransferGERUtils</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class CustVendPaymCreditTransferGERUtils
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateElectronicPaymentNumber</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static str updateElectronicPaymentNumber(RecId _ledgerJournalTransRecId)
    {
        LedgerJournalTrans ledgerJournalTrans = LedgerJournalTrans::findRecId(_ledgerJournalTransRecId, true);
        LedgerJournalType journalType = LedgerJournalTable::find(ledgerJournalTrans.JournalNum).JournalType;
        str electronicPaymentNumber;

        if (CustVendPaymCreditTransferGERUtils::shouldUpdateElectronicPaymentNumber(ledgerJournalTrans, journalType))
        {
            ttsbegin;
            NumberSequenceReference numberSequenceReference = journalType == LedgerJournalType::Payment ?
                VendParameters::numRefElectronicPaymentReference() :
                PayrollParameters::numRefElectronicPaymentReference();
            if (numberSequenceReference.numberSequenceTable().Manual == NoYes::Yes && ledgerJournalTrans.ElectronicPaymentNumber)
            {
                electronicPaymentNumber = ledgerJournalTrans.ElectronicPaymentNumber;
            }
            else if (numberSequenceReference.numberSequenceTable().Manual == NoYes::No && !ledgerJournalTrans.ElectronicPaymentNumber)
            {
                VendorElectronicPaymentReference newPaymentReference = NumberSeq::newGetNum(numberSequenceReference).num();
                if (newPaymentReference)
                {
                    electronicPaymentNumber = newPaymentReference;
                    ledgerJournalTrans.ElectronicPaymentNumber = newPaymentReference;
                }
            }
            ledgerJournalTrans.doUpdate();
            ttscommit;
        }

        return electronicPaymentNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldUpdateElectronicPaymentNumber</Name>
				<Source><![CDATA[
    private static boolean shouldUpdateElectronicPaymentNumber(LedgerJournalTrans _ledgerJournalTrans, LedgerJournalType _journalType)
    {
        return VendPaymERGenerateElectronicPaymNumFlight::instance().isEnabled()
            && VendPaymGenerateElectronicPaymNumInERFlight::instance().isEnabled()
            && VendPaymModeTable::find(_ledgerJournalTrans.PaymMode).PaymentType == PaymentType::ElectronicPayment
            && ((_journalType == LedgerJournalType::Payment && VendParameters::find().EnableElectronicPaymentNumber == NoYes::Yes)
                || (_journalType == LedgerJournalType::PayrollDisbursement && PayrollParameters::find().EnableElectronicPaymentNumber == NoYes::Yes));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>