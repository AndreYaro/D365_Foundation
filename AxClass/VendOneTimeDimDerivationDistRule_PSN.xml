<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendOneTimeDimDerivationDistRule_PSN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendOneTimeDimDerivationDistRule_PSN</c> class is a delegate class that is used in the
/// <c>DimensionDerivationRule</c> subclasses to handle accounting distribution generation for one time vendor invoices.
/// </summary>
class VendOneTimeDimDerivationDistRule_PSN
{
    DimensionDerivationRule dimensionDerivationRule;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void new(DimensionDerivationRule _dimensionDerivationRule)
    {
        dimensionDerivationRule = _dimensionDerivationRule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateDimAllocationList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Binds the populated dimension allocation list event handler delegate to the populateDimAllocList method.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void populateDimAllocationList()
    {
        boolean isOneTimeVendor;

        isOneTimeVendor = SysTransactionScopeCache::get(identifierStr(VendProcessOneTimeVendorService_PSN),
                                                        identifierStr(VendProcessOneTimeVendorService_PSN), false);

        if (isOneTimeVendor)
        {
            dimensionDerivationRule.populatingDimAllocListEventHandler += eventhandler(this.populateDimAllocList);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateDimAllocList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Populates the ledger dimension allocation list by using staging distribution data from the <c>VendOneTimeVendInvoiceStaging_PSN</c> table.
    /// </summary>
    /// <param name="_ledgerDimensionAllocationList">
    /// The ledger dimension allocation list to populate.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void populateDimAllocList(LedgerDimensionAllocationList _ledgerDimensionAllocationList)
    {
        VendOneTimeVendInvoiceStaging_PSN  vendOneTimeVendInvoiceStaging;
        LedgerDimensionAllocation          ledgerDimensionAllocation;
        AccountingRule                     accountingRule;
        VendorInvoiceSourceDoc             vendorInvoiceSourceDoc;
        VendorInvoiceLineSourceDocLineItem vendorInvoiceLineSourceDocLineItem;
        DebitCredit                        debitCredit;

        accountingRule                     = dimensionDerivationRule.parmAccountingRule();
        vendorInvoiceLineSourceDocLineItem = accountingRule.parmSourceDocumentLineItem();
        debitCredit                        = accountingRule.parmSide();
        vendorInvoiceSourceDoc             = vendorInvoiceLineSourceDocLineItem.parmSourceDocument();

            while select LedgerDimension, Percent from vendOneTimeVendInvoiceStaging
                where vendOneTimeVendInvoiceStaging.InvoiceNumber == vendorInvoiceSourceDoc.parmDocumentNumber()
                    && vendOneTimeVendInvoiceStaging.LineNumber  == vendorInvoiceLineSourceDocLineItem.parmLineNum()
                    && vendOneTimeVendInvoiceStaging.UserID == curUserId()
            {
                ledgerDimensionAllocation = LedgerDimensionAllocation::newFromParameters(vendOneTimeVendInvoiceStaging.LedgerDimension,
                                                                                            vendOneTimeVendInvoiceStaging.Percent / 100,
                                                                                            debitCredit,
                                                                                            false);

                _ledgerDimensionAllocationList.addEnd(ledgerDimensionAllocation);
            }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromDimensionDerivationRule</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>VendOneTimeDimDerivationDistRule_PSN</c> class.
    /// </summary>
    /// <param name="_dimensionDerivationRule">
    /// An instance of the <c>DimensionDerivationRule</c> class.
    /// </param>
    /// <returns>
    /// An instance of the <c>VendOneTimeDimDerivationDistRule_PSN</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static VendOneTimeDimDerivationDistRule_PSN newFromDimensionDerivationRule(DimensionDerivationRule _dimensionDerivationRule)
    {
        return new VendOneTimeDimDerivationDistRule_PSN(_dimensionDerivationRule);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>