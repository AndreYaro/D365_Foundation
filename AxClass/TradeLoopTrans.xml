<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TradeLoopTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
class TradeLoopTrans
{
    Common      journalTable;
    tableId     journalTableId;
    tableId     transTableId;
    boolean     update;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    Query buildQuery()
    {
        Query           query;
        DictRelation    dictRelation;
        Counter         c;

        dictRelation = new DictRelation(transTableId);
        dictRelation.loadTableRelation(journalTableId);
        if (dictRelation.lines())
        {
            query = new Query();
            query.addDataSource(transTableId);
            for (c=1; c <= dictRelation.lines(); c++)
            {
                switch (dictRelation.lineType(c))
                {
                    case TableRelation::Field:
                        query.dataSourceTable(transTableId).addRange(dictRelation.lineTableValue(c)).value(queryValue(journalTable.(dictRelation.lineExternTableValue(c))));
                        break;
                    case TableRelation::ExternFixed:
                        if (journalTable.(dictRelation.lineExternTableValue(c)) != dictRelation.lineTableValue(c))
                        {
                            query.dataSourceTable(transTableId).addRange(FieldNum(Common, tableId)).value(queryValue(0));
                        }
                        break;
                    case TableRelation::ThisFixed:
                        query.dataSourceTable(transTableId).addRange(dictRelation.lineTableValue(c)).value(queryValue(dictRelation.lineExternTableValue(c)));
                        break;
                }
            }
        }
        else
            throw error(strfmt("@SYS59540",tableid2name(journalTableId),tableid2name(transTableId)));

        query.dataSourceTable(transTableId).update(update);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildQueryRun</Name>
				<Source><![CDATA[
    QueryRun buildQueryRun()
    {
        return new QueryRun(this.buildQuery());
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new (Common  journal,
              tableId _transTableId,
              boolean _update = false)
    {
        update         = _update;
        journalTable   = journal;
        journalTableId = journal.tableId;
        transTableId   = _transTableId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>