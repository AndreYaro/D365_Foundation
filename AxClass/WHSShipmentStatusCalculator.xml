<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSShipmentStatusCalculator</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSShipmentStatusCalculator
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>determineInboundShipmentStatusFromLoadStatus</Name>
				<Source><![CDATA[
    internal static WHSShipmentStatus determineInboundShipmentStatusFromLoadStatus(WHSLoadStatus _loadStatus)
    {
        switch (_loadStatus)
        {
            case WHSLoadStatus::Received:
                return WHSShipmentStatus::Received;
                break;

            case WHSLoadStatus::InProcess:
                return WHSShipmentStatus::InProcess;
                break;

            case WHSLoadStatus::Shipped:
                return WHSShipmentStatus::Shipped;
                break;

            case WHSLoadStatus::Open:
                return WHSShipmentStatus::Open;
                break;
            
            default:
                throw error(error::wrongUseOfFunction(funcName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getShipmentStatusFromWork</Name>
				<Source><![CDATA[
    internal static WHSShipmentStatus getShipmentStatusFromWork(WHSShipmentId _shipConfirmShipmentId)
    {
        WHSShipmentStatus shipmentStatus = WHSShipmentStatus::Open;

        WHSWorkTable workTableWithMaxStatusInShipment = WHSShipmentStatusCalculator::getWorkTableForShipmentStatus(_shipConfirmShipmentId);

        if (workTableWithMaxStatusInShipment.RecId != 0)
        {
            if (workTableWithMaxStatusInShipment.WorkStatus == WHSWorkStatus::Open)
            {
                shipmentStatus = WHSShipmentStatus::Waved;
            }
            else
            {
                shipmentStatus = WHSShipmentStatus::InProcess;
            }
        }

        return shipmentStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getWorkTableForShipmentStatus</Name>
				<Source><![CDATA[
    private static WHSWorkTable getWorkTableForShipmentStatus(WHSShipmentId _shipConfirmShipmentId)
    {
        WHSWorkTable workTable;

        select firstonly RecId, WorkStatus from workTable
            order by workTable.WorkStatus desc
            where workTable.ShipmentId == _shipConfirmShipmentId
                &&  workTable.WorkStatus < WHSWorkStatus::Closed;

        return workTable;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>