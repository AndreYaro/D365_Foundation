<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLCNumberReferenceLookup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLCNumberReferenceLookup</c> class is the reference lookup helper class for the
///    <c>BankLCNumber</c> field of the <c>BankLC</c> table.
/// </summary>
class BankLCNumberReferenceLookup extends SysReferenceTableLookup
{
    FormBuildDataSource    selectionBuildDataSource;
    BankLCNumberController controller;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildAdditionalFormDataSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Builds additional form data sources.
    /// </summary>
    /// <param name="_form">
    ///    The form.
    /// </param>
    /// <remarks>
    ///    This method overrides the super method.
    /// </remarks>
    protected void buildAdditionalFormDataSources(Form _form)
    {
        super(_form);

        if (controller.parmReturnAsBankLC())
        {
            if (controller is BankLCImportNumberController)
            {
                selectionBuildDataSource = _form.addDataSource(tablestr(BankLCImport));
                selectionBuildDataSource.table(tablenum(BankLCImport));
            }
            else
            {
                selectionBuildDataSource = _form.addDataSource(tablestr(BankLCExport));
                selectionBuildDataSource.table(tablenum(BankLCExport));
            }
            selectionBuildDataSource.joinSource(_form.dataSource(1).name());
            selectionBuildDataSource.linkType(FormLinkType::InnerJoin);
        }
        else
        {
            selectionBuildDataSource = _form.dataSource(1);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGrid</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Builds the lookup grid.
    /// </summary>
    /// <param name="_formBuildGridControl">
    ///    The form grid control.
    /// </param>
    /// <param name="_formBuildDataSource">
    ///    The form build data source.
    /// </param>
    /// <returns>
    ///    The build grid.
    /// </returns>
    /// <remarks>
    ///    This method overrides the super method.
    /// </remarks>
    protected FormBuildGridControl buildGrid(
        FormBuildGridControl _formBuildGridControl,
        FormBuildDataSource  _formBuildDataSource)
    {
        FormBuildGridControl ret;

        ret = super(_formBuildGridControl, _formBuildDataSource);

        if (controller is BankLCImportNumberController)
        {
            _formBuildGridControl.addDataField(selectionBuildDataSource.id(), fieldnum(BankLCImport, BankLCNumber));
            _formBuildGridControl.addDataField(selectionBuildDataSource.id(), fieldnum(BankLCImport, PurchTable));
            _formBuildGridControl.addDataField(selectionBuildDataSource.id(), fieldnum(BankLCImport, ApplicationDateTime));
        }
        else
        {
            _formBuildGridControl.addDataField(selectionBuildDataSource.id(), fieldnum(BankLCExport, BankLCNumber));
            _formBuildGridControl.addDataField(selectionBuildDataSource.id(), fieldnum(BankLCExport, SalesTable));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmController</Name>
				<Source><![CDATA[
        public BankLCNumberController parmController(BankLCNumberController _controller = controller)
    {
        controller = _controller;
        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Constructs an instance of the <c>BankLCNumberReferenceLookup</c> class.
    /// </summary>
    /// <param name="_controller">
    ///    An instance of the helper class for the <c>BankLCNumber</c> field.
    /// </param>
    /// <param name="_tableId">
    ///    The table ID that is required by the <c>SysReferenceTableLookup</c> class.
    /// </param>
    /// <param name="_callingControl">
    ///    The calling form control.
    /// </param>
    /// <param name="_useLookupValue">
    ///    A Boolean value that indicates whether to use the lookup value.
    /// </param>
    /// <returns>
    ///    The new instance of the <c>BankLCNumberReferenceLookup</c> class.
    /// </returns>
    public static BankLCNumberReferenceLookup construct(
        BankLCNumberController _controller,
        tableId                _tableId,
        FormReferenceControl   _callingControl,
        boolean                _useLookupValue = true)
    {
        BankLCNumberReferenceLookup lookup;

        lookup = new BankLCNumberReferenceLookup();

        lookup.parmController(_controller);
        lookup.parmTableId(_tableId);
        lookup.parmCallingControl(_callingControl);
        lookup.parmUseLookupValue(_useLookupValue);

        return lookup;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>