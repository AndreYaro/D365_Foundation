<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetReductionEntryMassUpdate_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class for asset document reduction entry mass update function.
/// </summary>
class AssetReductionEntryMassUpdate_JP extends RunBase
{
    QueryRun                            queryRun;
    AssetReductionEntryMassUpdateTmp_JP assetReductionEntryMassUpdateTmp;
    Object                              formREMassUpdate;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialog = super();

        dialog.caption("@SYS4006234");
        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Args _args)
    {
        #ISOCountryRegionCodes
        Query query = new Query(queryStr(AssetReductionEntryMassUpdateCriteria_JP));
        QueryBuildRange qr;

        super();

        GlobalizationInstrumentationHelper::featureRunByCountryRegionCodes([
            [#isoJP, GlobalizationConstants::FeatureReferenceJP00016] ],
            funcName()
        );

        if (_args != null && _args.caller())
        {
            formREMassUpdate = _args.caller();
        }

        qr = query.dataSourceTable(tableNum(AssetBook)).addRange(fieldNum(AssetBook, Status));
        qr.value(queryValue(AssetStatus::Open) + ',' + queryValue(AssetStatus::NoAcquisition));
        qr.status(1);

        qr = query.dataSourceTable(tableNum(AssetBookTable)).addRange(fieldNum(AssetBookTable, CurrentOperationsTax));
        qr.value(queryValue(CurrentOperationsTax::Current) + ',' + queryValue(CurrentOperationsTax::Tax));
        qr.status(1);

        queryRun = new QueryRun(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        AssetTable                              assetTable;
        AssetBook                               assetBook;
        AssetDocumentEntryTableView_JP          assetDocumentEntryTableView;
        AssetDocumentReductionEntryTable_JP     assetDocumentReductionEntry;
        AssetBookMergeId                        bookMergeId;

        ttsBegin;
        while (queryRun.next())
        {
            assetTable = queryRun.get(tableNum(AssetTable)) as AssetTable;
            assetBook = queryRun.get(tableNum(AssetBook)) as AssetBook;

            if (assetBook.BookId)
            {
                assetReductionEntryMassUpdateTmp.clear();
                assetReductionEntryMassUpdateTmp.AssetId = assetTable.AssetId;
                assetReductionEntryMassUpdateTmp.AssetGroup = assetTable.AssetGroup;
                bookMergeId = '';
                if (assetBook.BookId)
                {
                    bookMergeId = assetBook.BookId;
                    assetReductionEntryMassUpdateTmp.BookId = assetBook.BookId;
                }
                assetReductionEntryMassUpdateTmp.AssetBookMergeId = bookMergeId;
                select DocumentId from assetDocumentEntryTableView
                where assetDocumentEntryTableView.BookId == bookMergeId &&
                    assetDocumentEntryTableView.AssetId == assetTable.AssetId;
                assetReductionEntryMassUpdateTmp.DocumentId = assetDocumentEntryTableView.DocumentId;
                assetDocumentReductionEntry = AssetDocumentReductionEntryTable_JP::findByDocumentEntry(assetDocumentEntryTableView.RecId);
                assetReductionEntryMassUpdateTmp.DocumentStatus = assetDocumentReductionEntry.Status;
                assetReductionEntryMassUpdateTmp.insert();
            }
        }
        ttsCommit;

        if (formREMassUpdate != null
            && formHasMethod(formREMassUpdate, 'updateTmpTable'))
        {
            formREMassUpdate.updateTmpTable(assetReductionEntryMassUpdateTmp);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        AssetReductionEntryMassUpdate_JP assetREMassUpdate = new AssetReductionEntryMassUpdate_JP(args);

        assetREMassUpdate.getLast();

        if (assetREMassUpdate.prompt())
        {
            assetREMassUpdate.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>