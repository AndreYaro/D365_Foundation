<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OMHierarchyTypeEx</Name>
	<SourceCode>
		<Declaration><![CDATA[
class OMHierarchyTypeEx
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findRetailReplenishment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a retail replenishment hierarchy by using the record ID value.
    /// </summary>
    /// <param name="_hierarchyId">
    /// The record ID of the hierarchy to find.
    /// </param>
    /// <returns>
    /// A hierarchy type if found; otherwise, null.
    /// </returns>
    public static RetailReplenishmentHierarchyView findRetailReplenishment(RetailReplenishmentHierarchyId _hierarchyId)
    {
        RetailReplenishmentHierarchyView replenishmentHierarchy;

        if (isConfigurationkeyEnabled(configurationKeyNum(Retail)))
        {
            select replenishmentHierarchy
                where replenishmentHierarchy.RecId == _hierarchyId;

            return replenishmentHierarchy;
        }
        else
        {
            return null;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupRetailReplenishment</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides a lookup for controls that must show a list of hierarchies associated with Retail
    /// replenishment purpose.
    /// </summary>
    /// <param name="_callingControl">
    /// A calling control.
    /// </param>
    /// <param name="_showRecId">
    /// A Boolean value that indicates whether the lookup shows the record ID field.
    /// </param>
    public static void lookupRetailReplenishment(FormControl _callingControl, NoYes _showRecId = false)
    {
        Query                   query = new Query();
        QueryBuildDataSource    dsHierarchyType;
        QueryBuildDataSource    dsHierarchyPurpose;

        SysTableLookup          sysTableLookup;

        if (isConfigurationkeyEnabled(configurationKeyNum(Retail)))
        {
            sysTableLookup = SysTableLookup::newParameters(tableNum(OMHierarchyType), _callingControl);

            dsHierarchyType = query.addDataSource(tableNum(OMHierarchyType));

            dsHierarchyPurpose = dsHierarchyType.addDataSource(tableNum(OMHierarchyPurpose));
            dsHierarchyPurpose.relations(true);
            dsHierarchyPurpose.joinMode(JoinMode::ExistsJoin);
            dsHierarchyPurpose.addRange(fieldNum(OMHierarchyPurpose, HierarchyPurpose)).value(queryValue(HierarchyPurpose::RetailReplenishment));

            sysTableLookup.parmQuery(query);

            if (_showRecId)
            {
                sysTableLookup.addLookupfield(fieldNum(OMHierarchyType, RecId));
            }
            sysTableLookup.addLookupfield(fieldNum(OMHierarchyType, Name));

            sysTableLookup.performFormLookup();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>