<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DeliveryReasonForm_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class complements the logic of <c>DeliveryReason</c> form datasources.
/// </summary>
public static class DeliveryReasonForm_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DlvReason_OnActivated</Name>
				<Source><![CDATA[
    /// <summary>
    /// This event handler enable or disable fields related to goods delivered for free.
    /// </summary>
    /// <param name = "_sender">Delivery Reason form</param>
    /// <param name = "_e">Event arguments</param>
    [FormDataSourceEventHandler(formDataSourceStr(DeliveryReason, DlvReason), FormDataSourceEventType::Activated)]
    public static void DlvReason_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        if (FeatureStateProvider::isFeatureEnabled(SeparateInvoiceOnGoodsDeliveredForFreeFeature::instance()))
        {
            DeliveryReasonForm_ApplicationSuite_Extension::enableSalesForFreeFields(_sender, _sender.cursor());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>Free_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// This event handler enable or disable fields related to goods delivered for free.
    /// </summary>
    /// <param name = "_sender">Delivery Reason form</param>
    /// <param name = "_e">Event arguments</param>
    [FormDataFieldEventHandler(formDataFieldStr(DeliveryReason, DlvReason, Free_IT), FormDataFieldEventType::Modified)]
    public static void Free_OnModified(FormDataObject _sender, FormDataFieldEventArgs _e)
    {
        if (FeatureStateProvider::isFeatureEnabled(SeparateInvoiceOnGoodsDeliveredForFreeFeature::instance()))
        {
            DeliveryReasonForm_ApplicationSuite_Extension::enableSalesForFreeFields(_sender.datasource(), _sender.datasource().cursor());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InvoiceAccount_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// This event handler enable or disable fields related to goods delivered for free.
    /// </summary>
    /// <param name = "_sender">Delivery Reason form</param>
    /// <param name = "_e">Event arguments</param>
    [FormDataFieldEventHandler(formDataFieldStr(DeliveryReason, DlvReason, InvoiceAccount_IT), FormDataFieldEventType::Modified)]
    public static void InvoiceAccount_OnModified(FormDataObject _sender, FormDataFieldEventArgs _e)
    {
        if (FeatureStateProvider::isFeatureEnabled(SeparateInvoiceOnGoodsDeliveredForFreeFeature::instance()))
        {
            DeliveryReasonForm_ApplicationSuite_Extension::enableSalesForFreeFields(_sender.datasource(), _sender.datasource().cursor());
            _sender.datasource().refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableSalesForFreeFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enable or disable fields related to goods delivered for free.
    /// </summary>
    /// <param name = "_dlvReason_ds">The form datasource.</param>
    /// <param name = "_dlvReason">
    /// The table of Delivery Reason.
    /// </param>
    public static void enableSalesForFreeFields(FormDataSource _dlvReason_ds, DlvReason _dlvReason)
    {
        if (_dlvReason_ds)
        {
            _dlvReason_ds.object(fieldnum(dlvReason, InvoiceAccount_IT)).enabled(_dlvReason.Free_IT);
            _dlvReason_ds.object(fieldnum(dlvReason, PaymTermId_IT)).allowEdit(_dlvReason.InvoiceAccount_IT != '');
            _dlvReason_ds.object(fieldnum(dlvReason, PaymTermId_IT)).mandatory(_dlvReason.InvoiceAccount_IT != '');
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>