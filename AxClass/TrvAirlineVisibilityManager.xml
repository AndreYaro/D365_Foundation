<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvAirlineVisibilityManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class controls visibility for airline expense type.
/// </summary>
class TrvAirlineVisibilityManager extends TrvCommonExpenseTypeVisibilityManager
{
    Map                         transactionAirline;
    const str                   transactionAirlineGroupName = 'ItineraryData';
    str                         dataSourceNameAirline;
    TrvEnhancedItineraryData    trvEnhancedItineraryData;

    Map                         transactionTripLeg;
    const str                   transactionTripLegGroupName = 'TripLegData';
    str                         dataSourceNameTripLeg;
    TrvEnhancedTripLegDetail    trvEnhancedTripLegDetail;

    Map                         airlineTransData;
    const str                   airlineTransDataGroupName   = 'AirlineTransData';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmdataSourceNameAirline</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets and sets the name of the datasource for airline enhanced data to set field vibilities on for the class.
    /// </summary>
    /// <param name = "_dataSourceNameAirline">
    /// The name of the airline enhanced fields dataSource to set field visibilities on the form.
    /// </param>
    /// <returns>
    /// The name of the airline enhanced fields dataSource to set field visibilities on the form.
    /// </returns>
    public str parmdataSourceNameAirline(str _dataSourceNameAirline = dataSourceNameAirline)
    {
        dataSourceNameAirline = _dataSourceNameAirline;
        return dataSourceNameAirline;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmdataSourceNameTripLeg</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets and sets the name of the datasource for airline enhanced trip leg details data to set field vibilities on for the class.
    /// </summary>
    /// <param name = "_dataSourceNameTripLeg">
    /// The name of the airline enhanced trip leg details fields dataSource to set field visibilities on the form.
    /// </param>
    /// <returns>
    /// The name of the airline enhanced trip leg details fields dataSource to set field visibilities on the form.
    /// </returns>
    public str parmdataSourceNameTripLeg(str _dataSourceNameTripLeg = dataSourceNameTripLeg)
    {
        dataSourceNameTripLeg = _dataSourceNameTripLeg;
        return dataSourceNameTripLeg;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionAirline</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for airline field group.
    /// </summary>
    /// <returns>
    /// A map of airline fields.
    /// </returns>
    Map getTransactionAirline()
    {
        if (!transactionAirline)
        {
            transactionAirline = this.getFieldGroup(trvEnhancedItineraryData, transactionAirlineGroupName);
        }

        return transactionAirline;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionTripLeg</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for airline field group.
    /// </summary>
    /// <returns>
    /// A map of airline fields.
    /// </returns>
    Map getTransactionTripLeg()
    {
        if (!transactionTripLeg)
        {
            transactionTripLeg = this.getFieldGroup(trvEnhancedTripLegDetail, transactionTripLegGroupName);
        }

        return transactionTripLeg;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAirlineTransData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for the AirlineTransData field group.
    /// </summary>
    /// <returns>
    /// A map of airline fields from TrvExpTrans to set visibility.
    /// </returns>
    Map getAirlineTransData()
    {
        if (!airlineTransData)
        {
            airlineTransData = this.getFieldGroup(trvExpTrans, airlineTransDataGroupName);
        }

        return airlineTransData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructor for an new instance of TrvAirlineVisibilityManager.
    /// </summary>
    /// <param name = "_trvExpTrans">
    /// The current record instance of the <c>TrvExpensesEntity</c>.
    /// </param>
    /// <param name = "_trvExpensesForm">
    /// An instance of the calling form object.
    /// </param>
    /// <param name = "_dataSourceName">
    /// Name of the datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameAirline">
    /// Name of the airline enhanced datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameTripLeg">
    /// Name of the airline enhanced trip leg details datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameHeader">
    /// Name of the expense header datasource on the form for which we set the visibility.
    /// </param>
    void new(Common _trvExpTrans, FormRun _trvExpensesForm, str _dataSourceName, str _dataSourceNameAirline, str _dataSourceNameTripLeg, str _dataSourceNameHeader = "")
    {
        super(_trvExpTrans, _trvExpensesForm, _dataSourceName, _dataSourceNameHeader);
        this.parmdataSourceNameAirline(_dataSourceNameAirline);
        this.parmdataSourceNameTripLeg(_dataSourceNameTripLeg);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldVisibilities</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets visibility for airline fields.
    /// </summary>
    /// <param name = "_isVisible">
    /// Determines if we show or hide airline fields.
    /// </param>
    void setFieldVisibilities(boolean _isVisible = false)
    {
        FormDataSource formDataSource = trvExpenseForm.dataSource(dataSourceName);
        FormDataSource formDataSourceAirline = trvExpenseForm.dataSource(dataSourceNameAirline);
        FormDataSource formDataSourceTripLeg = trvExpenseForm.dataSource(dataSourceNameTripLeg);

        if (!this.parmExpenseVisibilityManager())
        {
            this.setVisibilityForFieldGroup(this.getTransactionAirline(), dataSourceNameAirline, _isVisible);
            this.setVisibilityForFieldGroup(this.getTransactionTripLeg(), dataSourceNameTripLeg, _isVisible);
            this.setVisibilityForFieldGroup(this.getAirlineTransData(), dataSourceName,          _isVisible);

            if (_isVisible)
            {
                this.setVisibilityForAdminCustomFields(trvAdminCustomFieldsContainer, formDataSourceAirline, trvExpTrans.isEditable());
                this.setVisibilityForAdminCustomFields(trvAdminCustomFieldsContainer, formDataSourceTripLeg, trvExpTrans.isEditable());
            }
        }
        else
        {
            this.parmExpenseVisibilityManager().setVisibilityForAirlineRelatedFields(formDataSource.table(), formDataSource, _isVisible);
            this.parmExpenseVisibilityManager().setVisibilityForAirlineRelatedFields(formDataSourceAirline.table(), formDataSourceAirline, _isVisible);
            this.parmExpenseVisibilityManager().setVisibilityForAirlineRelatedFields(formDataSourceTripLeg.table(), formDataSourceTripLeg, _isVisible);
        }

        super(_isVisible);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>