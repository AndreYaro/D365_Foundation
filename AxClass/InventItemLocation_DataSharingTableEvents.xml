<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventItemLocation_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>InventItemLocation_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class InventItemLocation_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static InventItemLocation_DataSharingTableEvents construct()
    {
        return new InventItemLocation_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = InventItemLocation_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemLocation), DataEventType::Inserted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventItemLocation origInventItemLocation = this.dataSharingTableEventParameters.OrigDataSharingRecord;
                
                if (origInventItemLocation.RecId)
                {
                    InventItemLocation inventItemLocation;
                    buf2Buf(origInventItemLocation, inventItemLocation);
                    inventItemLocation.inventDimId = InventDim::inventDimIdBlank();
                    if (!inventItemLocation.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotInsertRecordInTable", tablePName(InventItemLocation), _dataSharingCompany));
                    }
                    inventItemLocation.insert();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        // deleting is handled in each company, if item is deleted cleanup is done from InventTable delete method.
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemLocation), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventItemLocation origInventItemLocation = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                // Only update the default records, other updates should be handle in each company.
                InventItemLocation inventItemLocation = inventItemLocation::findDefault(origInventItemLocation.ItemId,true);
                if (inventItemLocation.RecId)
                {
                    buf2Buf(origInventItemLocation, inventItemLocation);
                    inventItemLocation.inventDimId = InventDim::inventDimIdBlank();
                    if (!inventItemLocation.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotUpdateRecordInTable", tablePName(InventItemLocation), _dataSharingCompany));
                    }
                    inventItemLocation.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemLocation_onInserted</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of InventItemLocation table 
    [DataEventHandler(tableStr(InventItemLocation), DataEventType::Inserted)]
    public static void InventItemLocation_onInserted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            InventItemLocation inventItemLocation = _dataSharingRecord;
            if (inventItemLocation.inventDimId == InventDim::inventDimIdBlank())
            {
                var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemLocation), DataEventType::Inserted);
                if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
                {
                    SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemLocation = InventItemLocation_DataSharingTableEvents::construct();
                    tableEventsProcessorInventItemLocation.initializeProcessor(_dataSharingRecord);

                    SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemLocation);
                    using (var contextInsertCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemLocation), DataEventType::Inserted))
                    {
                        processor.insertCrossCompany();
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemLocation_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(InventItemLocation), DataEventType::Updated)]
    public static void InventItemLocation_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            InventItemLocation inventItemLocation = _dataSharingRecord;
            if (inventItemLocation.inventDimId == InventDim::inventDimIdBlank())
            {
                var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemLocation), DataEventType::Updated);
                if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
                {
                    SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemLocation = new InventItemLocation_DataSharingTableEvents();
                    tableEventsProcessorInventItemLocation.initializeProcessor(_dataSharingRecord);

                    SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemLocation);
                    using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemLocation), DataEventType::Updated))
                    {
                        processor.updateCrossCompany();
                    }
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>