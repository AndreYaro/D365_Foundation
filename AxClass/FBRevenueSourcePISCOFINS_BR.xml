<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBRevenueSourcePISCOFINS_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
[FBRevenueSourceExtension_BR('Default')]
public class FBRevenueSourcePISCOFINS_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getRevenueSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// For PIS/COFINS taxes finds the revenue source code specified for current taxation code 
    /// and combination of item ID, item group ID or tax fiscal classification.
    /// </summary>
    /// <param name="_taxType">
    /// Transaction's tax type.
    /// </param>
    /// <param name="_taxationCode">
    /// Transaction's taxation code.
    /// </param>
    /// <returns>
    /// Revenue source code.
    /// </returns>
    /// <remarks>
    /// These values are definied in Sped tables and may change when new versions are released.
    /// </remarks>
    public FBRevenueSource_BR getRevenueSource(TaxType_BR _taxType, TaxationCode_BR _taxationCode)
    {
        FBRevenueSourceSetup_BR revenueSourceSetup;
        ItemId itemId = '';
        ItemGroupId itemGroup = '';
        TaxFiscalClassificationId_BR taxFiscalClassificationId = '';

        if (_taxType != TaxType_BR::PIS && _taxType != TaxType_BR::COFINS)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        FBRevenueSourcePISCOFINSContext_BR revSourceContext = FBRevenueSourcePISCOFINSContext_BR::instance();

        if (revSourceContext)
        {
            itemId = revSourceContext.parmItemId();
            itemGroup = InventTable::find(itemId).itemGroupId();
            taxFiscalClassificationId = revSourceContext.parmTaxFiscalClassificationId_BR();
        }

        select firstOnly RevenueSourceCode
            from revenueSourceSetup
            order by revenueSourceSetup.ItemCode, revenueSourceSetup.TaxFiscalClassificationID desc
            where revenueSourceSetup.TaxationCode == _taxationCode
                && ((revenueSourceSetup.ItemCode == TableGroupAll::Table && revenueSourceSetup.ItemRelation == itemId)
                || (revenueSourceSetup.ItemCode == TableGroupAll::GroupId && revenueSourceSetup.ItemRelation == itemGroup)
                || (revenueSourceSetup.ItemCode == TableGroupAll::All
                    && (revenueSourceSetup.TaxFiscalClassificationID == taxFiscalClassificationId || !revenueSourceSetup.TaxFiscalClassificationID)));

        return revenueSourceSetup.RevenueSourceCode;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>