<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppDefaultDataCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class for defaulting the setup for running warehouse app.
/// </summary>
public static class WHSMobileAppDefaultDataCreator
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createDefaultMobileAppFlowSteps</Name>
				<Source><![CDATA[
    public static void createDefaultMobileAppFlowSteps()
    {
        WHSMobileAppStepInstructionDefaultDataCreator::construct().createDefaultMobileAppFlowSteps();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createDefaultMobileAppFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates default <c>WHSMobileAppField</c> records.
    /// </summary>
    public static void createDefaultMobileAppFields()
    {
        Set defaultAppFields = new Set(Types::String);

        ttsbegin;

        ListEnumerator listEnumerator = WHSMobileAppDefaultDataCreator::getMobileAppFieldClassesListEnum();
        while (listEnumerator.moveNext())
        {
            WHSFieldClassName childClassName = classId2Name(listEnumerator.current());
            WHSMobileAppDefaultDataCreator::createMobileAppFieldRecord(childClassName);
            defaultAppFields.add(childClassName);
        }
        WHSMobileAppDefaultDataCreator::deleteNotExistingMobileAppFields(defaultAppFields);

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMobileAppFieldClassesListEnum</Name>
				<Source><![CDATA[
    private static ListEnumerator getMobileAppFieldClassesListEnum()
    {
        SysDictClass dictClass = new SysDictClass(className2Id(classStr(WHSField)));
        List extendedClassesList = dictClass.extendedBy();
        return extendedClassesList.getEnumerator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteNotExistingMobileAppFields</Name>
				<Source><![CDATA[
    private static void deleteNotExistingMobileAppFields(Set _defaultAppFields)
    {
        WHSMobileAppField mobileAppField;
        if (_defaultAppFields.empty())
        {
            delete_from mobileAppField;
            return ;
        }

        while select FieldClassName from mobileAppField
        {
            if (!WHSMobileAppDefaultDataCreator::isDefaultAppField(_defaultAppFields, mobileAppField.FieldClassName))
            {
                mobileAppField.selectForUpdate(true);
                mobileAppField.delete();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMobileAppFieldClassObject</Name>
				<Source><![CDATA[
    private static WHSField getMobileAppFieldClassObject(ClassId _classID)
    {
        SysDictClass dictClass = new SysDictClass(_classID);
        if (dictClass)
        {
            return dictClass.makeObject();
        }
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isDefaultAppField</Name>
				<Source><![CDATA[
    private static boolean isDefaultAppField(Set _defaultData, WHSFieldClassName _appFieldClassName)
    {
        return _defaultData.in(_appFieldClassName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createDefaultMobileAppFieldPriority</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates default <c>WHSMobileAppFieldPriority</c> records.
    /// </summary>
    public static void createDefaultMobileAppFieldPriority()
    {
        WHSMobileAppFieldPriority mobileAppFieldPriority;

        ttsbegin;

        delete_from mobileAppFieldPriority;

        ListEnumerator listEnumerator = WHSMobileAppDefaultDataCreator::getMobileAppFieldClassesListEnum();
        while (listEnumerator.moveNext())
        {
            ClassId childClassId = listEnumerator.current();
            WHSField mobileAppFieldBase = WHSMobileAppDefaultDataCreator::getMobileAppFieldClassObject(childClassId);
            WHSMobileAppDefaultDataCreator::createmobileAppFieldPriorityRecord(classId2Name(childClassId),
                                                                               mobileAppFieldBase.getPriority(),
                                                                               mobileAppFieldBase.getSubPriority());
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createMobileAppFieldRecord</Name>
				<Source><![CDATA[
    private static void createMobileAppFieldRecord(WHSFieldClassName _appFieldClassName)
    {
        WHSField mobileAppFieldBase = WHSMobileAppDefaultDataCreator::getMobileAppFieldClassObject(className2Id(_appFieldClassName));
        WHSMobileAppField mobileAppField = WHSMobileAppField::findByClassName(_appFieldClassName, true);

        if (!mobileAppField)
        {
            mobileAppField.FieldClassName   = _appFieldClassName;
            mobileAppField.InputMode        = mobileAppFieldBase.getInputMode();
            mobileAppField.InputType        = mobileAppFieldBase.getInputType();
            mobileAppField.NumOfDecimals    = mobileAppFieldBase.getNumOfDecimals();
            mobileAppField.insert();
        }
        else if (!mobileAppField.NumOfDecimals)
        {
            // Set new non-default values on demo data and existing environments
            mobileAppField.NumOfDecimals    = mobileAppFieldBase.getNumOfDecimals();
            mobileAppField.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createmobileAppFieldPriorityRecord</Name>
				<Source><![CDATA[
    private static void createmobileAppFieldPriorityRecord(WHSFieldClassName _appFieldClassName, WHSFieldDisplayPriority _priority, WHSFieldDisplaySubpriority _subPriority)
    {
        WHSMobileAppField mobileAppField = WHSMobileAppField::findByClassName(_appFieldClassName);

        if (mobileAppField.RecId != 0)
        {
            if (!WHSMobileAppFieldPriority::existGroupRecord(_priority))
            {
                WHSMobileAppDefaultDataCreator::createMobileAppFieldPriorityGroupRecord(_priority);
            }

            WHSFieldDisplaySubpriority subpriority = WHSMobileAppDefaultDataCreator::fixSubpriorityConflict(mobileAppField, _priority, _subPriority);

            WHSMobileAppFieldPriority mobileAppFieldPriority = WHSMobileAppFieldPriority::findByMobileAppField(mobileAppField, true);

            if (mobileAppFieldPriority.RecId == 0)
            {
                WHSMobileAppFieldPriority::insertItemRecord(mobileAppField, _priority, subpriority);
            }
            else
            {
                mobileAppFieldPriority.Priority    = _priority;
                mobileAppFieldPriority.SubPriority = subpriority;
                mobileAppFieldPriority.update();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fixSubpriorityConflict</Name>
				<Source><![CDATA[
    private static WHSFieldDisplaySubpriority fixSubpriorityConflict(WHSMobileAppField _mobileAppField, WHSFieldDisplayPriority _priority, WHSFieldDisplaySubpriority _subPriority)
    {
        WHSFieldDisplaySubpriority newSubpriority = _subPriority;

        WHSMobileAppFieldPriority existingPriority;
        select firstonly existingPriority
                where existingPriority.Priority == _priority
                    && existingPriority.SubPriority == _subPriority
                    && existingPriority.WHSMobileAppField != _mobileAppField.RecId;
            
        if (existingPriority.RecId != 0)
        {
            // Example: if there's already an item with subpriority 10, the next one might be 11.
            // Play it safe and move the one with 11 and all below it down. Use 11 for the new one.
            WHSMobileAppFieldPriority::moveDownFollowingItemsInGroup(_priority, _subPriority);
            newSubpriority++;
        }

        return newSubpriority;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createMobileAppFieldPriorityGroupRecord</Name>
				<Source><![CDATA[
    private static void createMobileAppFieldPriorityGroupRecord(WHSFieldDisplayPriority _priority)
    {
        WHSMobileAppFieldPriority::insertGroupRecord(_priority);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WHSMobileAppDefaultDataCreator::createDefaultMobileAppFields();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>