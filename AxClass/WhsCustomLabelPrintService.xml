<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsCustomLabelPrintService</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class WhsCustomLabelPrintService extends SysOperationServiceBase
{    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>printLabels</Name>
				<Source><![CDATA[
    public void printLabels(WhsCustomLabelPrintContract _printLabelsContract)
    {
        WHSLabelLayout labelLayout = WHSLabelLayout::find(WHSLabelLayoutType::CustomLabel, _printLabelsContract.parmCustomLabelLayout());
     
        if (!labelLayout || labelLayout.LabelLayoutDataSource != _printLabelsContract.parmLabelLayoutDataSourceId())
        {
            throw Error(Error::wrongUseOfFunction(funcName()));
        }
        
        int labelCount;
                        
        using (var batchPrintingContext = WhsBatchedDocumentRoutingContext::construct())
        {
            ListEnumerator rootDataSourceRecIdsEnum = _printLabelsContract.parmRootDataSourceRecIds().getEnumerator();

            while (rootDataSourceRecIdsEnum.moveNext())
            {
                RefRecId rootDataSourceRecId = rootDataSourceRecIdsEnum.current();
                
                if (_printLabelsContract.parmCustomLabelLayoutParameters() && WhsCustomLabelPrintCommandGenerator::IsParameterSupportEnabled)
                {
                    var generator = this.initLabelPrintCommandGenerator(_printLabelsContract, labelLayout, rootDataSourceRecId);
                    generator.withLabelLayoutDataSourceParameters(_printLabelsContract.parmCustomLabelLayoutParameters());
                    labelCount += generator.printLabels();
                }
                else
                {
                    labelCount += WHSCustomLabelPrintCommandGenerator::printLabelsForRecord(labelLayout, _printLabelsContract.parmPrinterName(), rootDataSourceRecId);
                }
            }

            batchPrintingContext.printBatchedLabels();
        }

        info(strFmt("@WAX:CustomLabelsSentToPrinter", labelCount, _printLabelsContract.parmPrinterName()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initLabelPrintCommandGenerator</Name>
				<Source><![CDATA[
    protected WHSCustomLabelPrintCommandGenerator initLabelPrintCommandGenerator(WhsCustomLabelPrintContract _printLabelsContract, WHSLabelLayout _labelLayout, RefRecId _rootDataSourceRecId)
    {
        return WhsCustomLabelPrintCommandGenerator::newFromParameters(_labelLayout, _printLabelsContract.parmPrinterName(), _rootDataSourceRecId);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>