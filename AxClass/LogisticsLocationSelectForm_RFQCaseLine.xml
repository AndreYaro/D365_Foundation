<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_RFQCaseLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_RFQCaseLine extends LogisticsLocationSelectForm
{
    PurchRFQCaseLine purchRFQCaseLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates the entities from which addresses can be selected.
    /// </summary>
    /// <returns>
    ///    A set of <c>LogisticsLocationSelectSourceTypes</c> enumeration values from which addresses can be
    ///    selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret;

        ret = super();

        ret.add(LogisticsLocationSelectSourceType::RequestForQuote);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuoteLine);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling RFQ line.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        purchRFQCaseLine.DeliveryPostalAddress = _addressRecId;
        purchRFQCaseLine.AddressRefRecId   = _transferFromTable.RecId;
        purchRFQCaseLine.AddressRefTableId = _transferFromTable.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// The initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    /// LogisticslocationSelectSourceType corresponding to the address source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (purchRFQCaseLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchRFQCaseLine.AddressRefTableId,
                purchRFQCaseLine.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    purchRFQCaseLine.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the record ID of the <c>LogisticsPostalAddress</c> that is currently selected.
    /// </summary>
    /// <returns>
    ///   The <c>LogisticsPostalAddress</c> record ID.
    /// </returns>
    public recId getLogisticsPostalAddressRecId()
    {
        return purchRFQCaseLine.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the record the target form's grid should have selected upon openning.
    /// </summary>
    /// <returns>
    ///   The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (purchRFQCaseLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchRFQCaseLine.AddressRefTableId,
                purchRFQCaseLine.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            addressOwner = CompanyInfo::find();
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMainTableBuffer_BR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns <c>PurchRFQCaseLine</c> table.
    /// </summary>
    /// <returns>
    /// Table buffer PurchRFQCaseLine.
    /// </returns>
    public PurchRFQCaseLine getMainTableBuffer_BR()
    {
        return purchRFQCaseLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(PurchRFQCaseLine _purchRFQCaseLine)
    {
        purchRFQCaseLine = _purchRFQCaseLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_RFQCaseLine</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A buffer to use to initialize the object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_RFQCaseLine</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_RFQCaseLine construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_RFQCaseLine(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>