<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOMTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'BOM'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class BOMTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        BOMTable bomTable;
        select crosscompany DataAreaId from bomTable where bomTable.RecId == _parentRecId;

        return bomTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        BOMTable bomTable;
        select crosscompany BOMId from bomTable where bomTable.RecId == _parentRecId;

        return bomTable.BOMId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        BOMTable bomTable;
        select RecId from bomTable where bomTable.BOMId == _naturalKey;

        return bomTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        BOMTable bomTable;
        select crosscompany Name from bomTable where bomTable.RecId == _parentRecId;

        return bomTable.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        BOMTable bomTable;

        //We need this code in order not to lose the localization data in the opened form.
        BOMTable bomTableBufferCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from bomTableBufferCrossCompany
            where bomTableBufferCrossCompany.RecId == _association.RefRecId;

        if (bomTableBufferCrossCompany.Recid)
        {
            changecompany (bomTableBufferCrossCompany.DataAreaId)
            {
                select firstonly bomTable where bomTable.RecId == _association.RefRecId;
            }
        }

        _args.record(bomTable);
        return menuitemdisplaystr(BOMTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        BOMTable bomTable = BOMTable::findRecId(_caseAssociation.RefRecId);
        BOMTable::lookupBOMId(callerControl, bomTable.ItemGroupId, bomTable.SiteId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(BOM)
                || _refTableId == tablenum(BOMTable)
                || _refTableId == tablenum(BOMVersion))
        {
            _eventArgs.parmEntityType(CaseEntityType::BOM);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>