<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PcDeliveryDateHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
class PcDeliveryDateHelper
{
    RefRecId activeDynamicPlanRecId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deliveryDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the expected delivery date of the sales line from the database.
    /// </summary>
    /// <param name="_salesId">
    /// The relating sales order.
    /// </param>
    /// <param name="_inventTransId">
    /// The inventory transaction of the sales line.
    /// </param>
    /// <returns>
    /// The expected delivery date for the sales line.
    /// </returns>
    /// <remarks>
    /// The delivery date is set up by the <c>ReqCalc</c> engine.
    /// </remarks>
    public TransDate deliveryDate(
        SalesId         _salesId,
        InventTransId   _inventTransId)
    {
        ReqTrans                reqTrans;
        InventTransOriginId     originId;

        originId = InventTransOrigin::findByInventTransId(_inventTransId).RecId;

        return (select maxof(FuturesDate) from reqTrans
                where reqTrans.PlanVersion            ==  this.parmActiveDynamicPlan() &&
                      reqTrans.RefType                ==  ReqRefType::Sales            &&
                      reqTrans.RefId                  == _salesId                      &&
                      reqTrans.InventTransOrigin      == originId).FuturesDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        this.parmActiveDynamicPlan(ReqPlanVersion::findActiveReqPlanIdDynamic().RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmActiveDynamicPlan</Name>
				<Source><![CDATA[
        public RefRecId parmActiveDynamicPlan(RefRecId _activeDynamicPlanRecID = activeDynamicPlanRecID)
    {
        activeDynamicPlanRecID = _activeDynamicPlanRecID;

        return activeDynamicPlanRecID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static PCDeliveryDateHelper construct()
    {
        return new PCDeliveryDateHelper();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>