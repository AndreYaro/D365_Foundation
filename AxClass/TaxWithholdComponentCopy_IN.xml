<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxWithholdComponentCopy_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is used to create new copy of Tax withhold component .
/// </summary>
public final class TaxWithholdComponentCopy_IN extends RunBase
{
    RefRecId            fromComponentGroup;
    RefRecId            toComponentGroup;
    TaxWithholdType_IN  taxType;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates  and initializes instance of  <c>TaxWithholdComponentCopy_IN </c>
    /// </summary>
    /// <returns>
    /// An instance of <c>TaxWithholdComponentCopy_IN </c> form
    /// </returns>
    public Object dialog()
    {
        Args     args = new  Args();
        FormRun  taxWithholdComponentCopy;

        args.name(formstr(TaxWithholdComponentCopy_IN));
        args.caller(this);

        taxWithholdComponentCopy = classfactory.formRunClass(args);

        taxWithholdComponentCopy.init();

        return taxWithholdComponentCopy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertComponentGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts a record into <c> TaxWithholdComponentTable_IN </c>
    /// </summary>
    /// <param name="_fromComponentGroup">
    /// Record Id of <c> TaxWithholdComponentGroup_IN </c>
    /// </param>
    /// <param name="_toComponentGroup">
    /// Record Id of <c> TaxWithholdComponentGroup_IN </c>
    /// </param>
    public void insertComponentGroup(
        RefRecId    _fromComponentGroup,
        RefRecId    _toComponentGroup)
    {
        TaxWithholdComponentTable_IN    taxWithholdComponentTable,
                                        taxWithholdComponentTableLoc;

        ttsbegin;
        while select taxWithholdComponentTable
            index TypeComponentGroupIdx
            where taxWithholdComponentTable.TaxWithholdComponentGroup == _fromComponentGroup
        {
            taxWithholdComponentTableLoc = TaxWithholdComponentTable_IN::findByComponentGroup(taxWithholdComponentTable.TaxType,
                                                                                              taxWithholdComponentTable.Component,
                                                                                              TaxWithholdComponentGroupTable_IN::find(_toComponentGroup).ComponentGroup);

            if (!taxWithholdComponentTableLoc)
            {
                taxWithholdComponentTable.TaxWithholdComponentGroup = _toComponentGroup;
                taxWithholdComponentTable.insert();
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return connull();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFromComponentGroup</Name>
				<Source><![CDATA[
    public RefRecId parmFromComponentGroup(RefRecId _fromComponentGroup = fromComponentGroup)
    {
        fromComponentGroup = _fromComponentGroup;
        return fromComponentGroup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTaxWithholdType</Name>
				<Source><![CDATA[
    public TaxWithholdType_IN parmTaxWithholdType(TaxWithholdType_IN _taxType = taxType)
    {
        taxType = _taxType;
        return taxType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmToComponentGroup</Name>
				<Source><![CDATA[
    public RefRecId parmToComponentGroup(RefRecId _toComponentGroup = toComponentGroup)
    {
        toComponentGroup = _toComponentGroup;
        return toComponentGroup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCopy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates whether particular TDS or TCS parameters active or not for India.
    /// </summary>
    /// <param name="_fromComponentGroup">
    /// Record Id of <c> TaxWithholdComponentGroup_IN </c>
    /// </param>
    /// <param name="_toComponentGroup">
    /// Record Id of <c> TaxWithholdComponentGroup_IN </c>
    /// </param>
    /// <param name="_taxType">
    ///  Withhold tax type.
    /// </param>
    public void validateCopy(
        RefRecId               _fromComponentGroup,
        RefRecId               _toComponentGroup,
        TaxWithholdType_IN     _taxType)
    {
        if (TaxWithholdParameters_IN::find().tdsActivate == NoYes::No && _taxType == TaxWithholdType_IN::TDS)
        {
            throw error(strfmt("@GLS5131", _taxType));
        }

        if (TaxWithholdParameters_IN::find().tcsActivate == NoYes::No && _taxType == TaxWithholdType_IN::TCS)
        {
            throw error(strfmt("@GLS5131", _taxType));
        }

        if (_fromComponentGroup == _toComponentGroup)
        {
            throw error("@SYS19123");
        }

        if (!TaxWithholdComponentGroupTable_IN::exist(_taxType, TaxWithholdComponentGroupTable_IN::find(_fromComponentGroup).ComponentGroup))
        {
            throw error(strfmt("@GLS5460", TaxWithholdComponentGroupTable_IN::find(_fromComponentGroup).ComponentGroup, _taxType));
        }

        if (!TaxWithholdComponentGroupTable_IN::exist(_taxType, TaxWithholdComponentGroupTable_IN::find(_toComponentGroup).ComponentGroup))
        {
            throw error(strfmt("@GLS5460", TaxWithholdComponentGroupTable_IN::find(_toComponentGroup).ComponentGroup, _taxType));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        TaxWithholdComponentCopy_IN    componentCopy;

        switch (args.dataset())
        {
            case tablenum(TaxWithholdComponentTable_IN) :
                componentCopy = TaxWithholdComponentCopy_IN::newComponentCopy(args.record());
                break;
        }
        if (componentCopy.prompt())
        {
            componentCopy.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newComponentCopy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new record for TaxWithholdComponentCopy_IN.
    /// </summary>
    /// <param name="_componentTable">
    /// A <c>TaxWithholdComponentTable_IN</c> table buffer.
    /// </param>
    /// <returns>
    /// Returns a <c>TaxWithholdComponentCopy_IN</c> table buffer.
    /// </returns>
    public static TaxWithholdComponentCopy_IN newComponentCopy(TaxWithholdComponentTable_IN _componentTable)
    {
        TaxWithholdComponentCopy_IN taxWithholdComponentCopyLoc;

        taxWithholdComponentCopyLoc = new TaxWithholdComponentCopy_IN();
        taxWithholdComponentCopyLoc.getLast();

        taxWithholdComponentCopyLoc.parmTaxWithholdType(_componentTable.TaxType);
        taxWithholdComponentCopyLoc.parmFromComponentGroup(_componentTable.TaxWithholdComponentGroup);
        taxWithholdComponentCopyLoc.parmToComponentGroup();

        return taxWithholdComponentCopyLoc;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>