<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AmountAdjustEngineVendInvoice4Paym_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class AmountAdjustEngineVendInvoice4Paym_W extends AmountAdjustEngine_W
{
    VendInvoice4PaymJour_RU vendInvoiceJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcLine_MarkupTrans</Name>
				<Source><![CDATA[
    protected container calcLine_MarkupTrans(MarkupTrans _line)
    {
        AmountMST amountCalculated, amountLocked;

        if (_line.CurrencyCode == CompanyInfoHelper::standardCurrency())
        {
            amountLocked     = _line.CalculatedAmount;
        }
        else
        {
            amountCalculated = CurrencyExchangeHelper::mstAmount(_line.CalculatedAmount, _line.CurrencyCode, vendInvoiceJour.Invoice4PaymDate, Currency::noYes2UnknownNoYes(vendInvoiceJour.Triangulation), _line.CurrencyCode == vendInvoiceJour.CurrencyCode ? vendInvoiceJour.ExchRate : 0, _line.CurrencyCode == vendInvoiceJour.CurrencyCode ? vendInvoiceJour.ExchRateSecondary : 0);
        }

        return [ amountCalculated, amountLocked ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcLine_VendInvoice4PaymTrans</Name>
				<Source><![CDATA[
    protected container calcLine_VendInvoice4PaymTrans(VendInvoice4PaymTrans_RU _line)
    {
        AmountMST amountCalculated, amountLocked;

        if (vendInvoiceJour.CurrencyCode == CompanyInfoHelper::standardCurrency())
        {
            amountLocked     = _line.LineAmount;
        }
        else
        {
            amountCalculated = vendInvoiceJour.amountMST(_line.LineAmount);
        }

        return [ amountCalculated, amountLocked ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendInvoice4PaymJour</Name>
				<Source><![CDATA[
    protected void parmVendInvoice4PaymJour(VendInvoice4PaymJour_RU _vendInvoiceJour)
    {
        ;
        vendInvoiceJour = _vendInvoiceJour;
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustInvoiceJour</Name>
				<Source><![CDATA[
    #ISOCountryRegionCodes
    public static void adjustInvoiceJour(VendInvoice4PaymJour_RU _invoiceJour)
    {
        AmountAdjustEngineVendInvoice4Paym_W adjustEngine;
        MarkupTrans                          markupTrans;
        VendInvoice4PaymTrans_RU             invoiceTrans;

        if (! SysCountryRegionCode::isLegalEntityInCountryRegion([ #isoRU ]) &&
            ! isConfigurationkeyEnabled(configurationkeynum(LogisticsBasic)))
            return;

        adjustEngine = new AmountAdjustEngineVendInvoice4Paym_W();
        adjustEngine.parmVendInvoice4PaymJour(_invoiceJour);
        adjustEngine.parmTotalMST(_invoiceJour.amountMST(_invoiceJour.Invoice4PaymAmount) -
                                  _invoiceJour.amountMST(_invoiceJour.SumTax)             -
                                  _invoiceJour.amountMST(_invoiceJour.RoundOff)           -
                                  _invoiceJour.amountMST(_invoiceJour.EndDisc));

        ttsbegin;

        while select forupdate markupTrans
            where markupTrans.TransTableId == _invoiceJour.TableId &&
                  markupTrans.TransRecId   == _invoiceJour.RecId
        {
            adjustEngine.addLine(markupTrans);
        }

        while select forupdate invoiceTrans
            where invoiceTrans.PurchId                == _invoiceJour.PurchId                &&
                  invoiceTrans.Invoice4PaymId         == _invoiceJour.Invoice4PaymId         &&
                  invoiceTrans.Invoice4PaymDate       == _invoiceJour.Invoice4PaymDate       &&
                  invoiceTrans.InternalInvoice4PaymId == _invoiceJour.InternalInvoice4PaymId &&
                  invoiceTrans.NumberSequenceGroupId  == _invoiceJour.NumberSequenceGroupId
        {
            adjustEngine.addLine(invoiceTrans);

            while select forupdate markupTrans
                where markupTrans.TransTableId == invoiceTrans.TableId &&
                      markupTrans.TransRecId   == invoiceTrans.RecId
            {
                adjustEngine.addLine(markupTrans);
            }
        }

        adjustEngine.run();

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
