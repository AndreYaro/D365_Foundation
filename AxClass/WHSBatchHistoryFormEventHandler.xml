<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSBatchHistoryFormEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class contains warehouse managment specific event handlers for the <c>BatchHistory</c> form.
/// </summary>
internal final class WHSBatchHistoryFormEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>BatchHistory_OnInitialized</Name>
				<Source><![CDATA[
        /// <summary>
        /// Handles event of BatchHistory datasource init on the BatchHistory form.
        /// </summary>
        /// <param name="_sender">The BatchHistory datasource.</param>
        /// <param name="_e">The event arguments.</param>
        [FormDataSourceEventHandler(formDataSourceStr(BatchHistory, BatchHistory), FormDataSourceEventType::Initialized)]
        public static void BatchHistory_OnInitialized(FormDataSource _sender, FormDataSourceEventArgs _e)
        {
            WHSBatchHistoryFormEventHandler::modifyBatchHistoryFormQuery(_sender.query(), _sender.formRun().args());
        }

]]></Source>
			</Method>
			<Method>
				<Name>modifyBatchHistoryFormQuery</Name>
				<Source><![CDATA[
        private static Query modifyBatchHistoryFormQuery(
            Query   _currentQuery,
            Args    _args)
        {
            if (_args.record()
                &&  _args.dataset() == tableNum(WHSWorkDeferredPutProcessingTask))
            {
                WHSWorkDeferredPutProcessingTask workDeferredPutProcessingTask = _args.record();
                QueryBuildDataSource workDeferredPutProcessingTaskQbds =  SysQuery::findOrCreateDataSource(_currentQuery, tableNum(WHSWorkDeferredPutProcessingTaskBatch), tableNum(BatchHistory));
                workDeferredPutProcessingTaskQbds.joinMode(JoinMode::ExistsJoin);
                workDeferredPutProcessingTaskQbds.addLink(fieldNum(BatchHistory, BatchId), fieldNum(WHSWorkDeferredPutProcessingTaskBatch, Batch));
                workDeferredPutProcessingTaskQbds.addRange(fieldNum(WHSWorkDeferredPutProcessingTaskBatch, WorkId)).value(queryValue(workDeferredPutProcessingTask.WorkId));
                workDeferredPutProcessingTaskQbds.addRange(fieldNum(WHSWorkDeferredPutProcessingTaskBatch, WorkLineNumber)).value(queryValue(workDeferredPutProcessingTask.WorkLineNumber));
            }

            return _currentQuery;
        }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>