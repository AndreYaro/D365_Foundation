<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDimCtrl_Frm_Mov_VendInvoice</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventDimCtrl_Frm_Mov_VendInvoice</c> class is used by non-PO invoice lines to lock or
///    unlock item dimension fields.
/// </summary>
public class InventDimCtrl_Frm_Mov_VendInvoice extends InventDimCtrl_Frm_Mov_OrderLine
{
    VendInvoiceInfoLine vendInvoiceInfoLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>formActiveSetup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Overrides the <c>formActiveSetup</c> method to set the inventory dimension lock option for vendor
    ///    invoices.
    /// </summary>
    /// <param name="_inventDimGroupSetup">
    ///    An instance of the <c>InventDimGroupSetup</c> class that provides information about dimension setup.
    /// </param>
    /// <param name="_movement">
    ///    The inventory movement that is associated with the line; optional.
    /// </param>
    public void formActiveSetup(InventDimGroupSetup _inventDimGroupSetup = null, InventMovement _movement = null)
    {
        InventDimParm inventDimParmEnabled;

        movement = InventMovement::construct(vendInvoiceInfoLine);

        if (!vendInvoiceInfoLine.isNonPO())
        {
            super(movement.inventDimGroupSetup(), movement);
        }
        else
        {
            editProductDimensionsAllowed = true;

            this.parmInventDimGroupSetup(_inventDimGroupSetup);

            this.initDimParmEnabled();

            // Enable only location and site for category based lines
            if (vendInvoiceInfoLine.isCategoryBased())
            {
                inventDimParmEnabled.setActiveSiteAndWarehouseDimensions();
                this.parmDimParmEnabled(inventDimParmEnabled);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ignoreOrderLineSpecificFieldEnablementRules</Name>
				<Source><![CDATA[
    protected boolean ignoreOrderLineSpecificFieldEnablementRules()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustEnableField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Enables or disables the field by using the setup in the <c>VendInvoiceInfoLine</c> table and the
    ///    inventory dimension lock setting.
    /// </summary>
    /// <param name="_dimFieldId">
    ///    The field ID to enable or disable.
    /// </param>
    /// <returns>
    ///    true if the field must be enabled; otherwise, false.
    /// </returns>
    protected NoYes mustEnableField(fieldId _dimFieldId)
    {
        if (!vendInvoiceInfoLine.isNonPO())
        {
            // Inventory dimensions for PO-based lines are not editable on the invoice
            return NoYes::No;
        }

        return super(_dimFieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendInvoiceInfoLine</Name>
				<Source><![CDATA[
    public VendInvoiceInfoLine parmVendInvoiceInfoLine(VendInvoiceInfoLine _vendInvoiceInfoLine = vendInvoiceInfoLine)
    {
        vendInvoiceInfoLine = _vendInvoiceInfoLine;

        return vendInvoiceInfoLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventDimCtrl_Frm_Mov_VendInvoice construct()
    {
        return new InventDimCtrl_Frm_Mov_VendInvoice();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromFormInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes an instance of this class to use in forms.
    /// </summary>
    /// <param name="_formRun">
    ///    The form where controls are maintained.
    /// </param>
    /// <param name="_vendInvoiceInfoLine">
    ///    An instance of the <c>VendInvoiceInfoLine</c> for which the class is used.
    /// </param>
    /// <param name="_validInventDimDS">
    ///    A set of valid <c>InventDim</c> data sources for which the class is used.
    /// </param>
    /// <returns>
    ///    An instance of this class to be used in a form.
    /// </returns>
    public static InventDimCtrl_Frm_Mov_VendInvoice newFromFormInvoice(FormRun _formRun, VendInvoiceInfoLine _vendInvoiceInfoLine = null, Set _validInventDimDS = null)
    {
        InventMovement movement = InventMovement::construct(_vendInvoiceInfoLine);
        InventDimCtrl_Frm_Mov_VendInvoice inventDimCtrl = InventDimCtrl_Frm_Mov_VendInvoice::construct();
        InventDimAxFormAdapter adapter = InventDimAxFormAdapter::newFromForm(_formRun);

        inventDimCtrl.parmCallingElement(adapter);
        inventDimCtrl.parmMovement(movement);
        inventDimCtrl.parmVendInvoiceInfoLine(_vendInvoiceInfoLine);
        inventDimCtrl.parmValidDataSources(_validInventDimDS);
        inventDimCtrl.init();

        return inventDimCtrl;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>