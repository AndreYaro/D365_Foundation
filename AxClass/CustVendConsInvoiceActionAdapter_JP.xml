<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendConsInvoiceActionAdapter_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class delegates the actions on Consolidation Invoice forms.
/// </summary>
public class CustVendConsInvoiceActionAdapter_JP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkParameterFailed</Name>
				<Source><![CDATA[
    /// <summary>
    /// Throws the exception for the failure of the parameter validation.
    /// </summary>
    /// <exception cref="Exception::Error">
    /// The error of the parameter validation.
    /// </exception>
    private static void checkParameterFailed()
    {
        // Consolidation invoice actions must be called with right parameter.
        throw error("@GLS63290");
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void  main(Args  args)
    {
        #ISOCountryRegionCodes
        GlobalizationInstrumentationHelper::featureRunByCountryRegionCodes([
            [#isoJP, GlobalizationConstants::FeatureReferenceJP00011] ],
            funcName()
        );
        GlobalizationInstrumentationHelper::featureRunByCountryRegionCodes([
            [#isoJP, GlobalizationConstants::FeatureReferenceJP00007] ],
            funcName()
        );

        Common record;
        int    paramEnumType;
        int    paramEnum;

        if (!args)
        {
            CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }

        record = args.record();
        if (!record || record.RecId == 0)
        {
            CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }

        paramEnumType   = args.parmEnumType();
        paramEnum       = args.parmEnum();

        if (paramEnumType != enumnum(CustVendConsInvoiceActionType_JP)
            || paramEnum  == 0)
        {
            CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }

        switch (record.TableId)
        {
            case tablenum(CustConsInvoice_JP) :
                CustVendConsInvoiceActionAdapter_JP::performCustActions(record, paramEnum);
                break;
            case tablenum(VendConsInvoice_JP) :
                CustVendConsInvoiceActionAdapter_JP::performVendActions(record, paramEnum);
                break;
            default :
                CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>performCustActions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs actions for Customer Consolidation Invoice.
    /// </summary>
    /// <param name="_consInvoice">
    /// The consolidation invoice record
    /// </param>
    /// <param name="_paramEnum">
    /// The type of action
    /// </param>
    private static void performCustActions(
        CustConsInvoice_JP  _consInvoice,
        int                 _paramEnum)
    {
        CustConsInvoiceType_JP consInvoiceType;

        consInvoiceType = CustConsInvoiceType_JP::construct(_consInvoice);

        switch (_paramEnum)
        {
            case CustVendConsInvoiceActionType_JP::Confirm:
                consInvoiceType.confirm();
                break;

            case CustVendConsInvoiceActionType_JP::Unconfirm:
                consInvoiceType.unconfirm();
                break;

            case CustVendConsInvoiceActionType_JP::Post:
                consInvoiceType.post();
                break;

            default :
                CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>performVendActions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs actions for Vender Consolidation Invoice.
    /// </summary>
    /// <param name="_consInvoice">
    /// The consolidation invoice record
    /// </param>
    /// <param name="_paramEnum">
    /// The type of action
    /// </param>
    private static void performVendActions(
        VendConsInvoice_JP  _consInvoice,
        int                 _paramEnum)
    {
        VendConsInvoiceType_JP consInvoiceType;

        consInvoiceType = VendConsInvoiceType_JP::construct(_consInvoice);

        switch (_paramEnum)
        {
            case CustVendConsInvoiceActionType_JP::Confirm:
                consInvoiceType.confirm();
                break;
            case CustVendConsInvoiceActionType_JP::Unconfirm:
                consInvoiceType.unconfirm();
                break;
            case CustVendConsInvoiceActionType_JP::Post:
                consInvoiceType.post();
                break;
            default :
                CustVendConsInvoiceActionAdapter_JP::checkParameterFailed();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>