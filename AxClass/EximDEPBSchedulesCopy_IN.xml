<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EximDEPBSchedulesCopy_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The exim DEPB schedules copy class.
/// </summary>
class  EximDEPBSchedulesCopy_IN extends RunBase
{
    DialogField              fieldFromDateTime;
    DialogField              fieldToDateTime;
    EximDEPBScheduleTable_IN eximDEPBSchedulesOrig;
    TransDateTime            fromDateTime;
    TransDateTime            toDateTime;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        fromDateTime,
        toDateTime
    #endMacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialog = new DialogRunbase("@GLS61465",this);

        fieldFromDateTime   = dialog.addField(identifierstr(FromDateTime), "@SYS109394", "@GLS61813");
        fieldToDateTime     = dialog.addField(identifierstr(ToDateTime), "@SYS109395", "@GLS61814");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets fields value from the dialog.
    /// </summary>
    /// <returns>
    /// If 'True', the value of the fields is got successfully.
    /// </returns>
    public boolean getFromDialog()
    {
        fromDateTime    = fieldFromDateTime.value();
        toDateTime      = fieldToDateTime.value();
        fromDateTime    = DateTimeUtil::removeTimeZoneOffset(fromDateTime, DateTimeUtil::getUserPreferredTimeZone());
        toDateTime      = DateTimeUtil::removeTimeZoneOffset(toDateTime, DateTimeUtil::getUserPreferredTimeZone());

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEximDEPBSchedules</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public EximDEPBScheduleTable_IN parmEximDEPBSchedules(EximDEPBScheduleTable_IN _eximDEPBSchedules = eximDEPBSchedulesOrig)
    {
        eximDEPBSchedulesOrig = _eximDEPBSchedules;

        return eximDEPBSchedulesOrig;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        #OCCRetryCount

        super();

        try
        {
            ttsbegin;
            EximDEPBSchedulesCopy_IN::runOnServer(eximDEPBSchedulesOrig, fromDateTime, toDateTime);
            ttscommit;
        }

        catch (Exception::Deadlock)
        {
            retry;
        }

        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        retry;
                    }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion :
                [version, #CurrentList] = _packedClass;
                break;

            default :
                return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the instance of the <c>EximDEPBSchedulesCopy_IN</c> class.
    /// </summary>
    /// <param name="calledFrom">
    /// The called from instance.
    /// </param>
    /// <returns>
    /// Returns true if the validation succeed.
    /// </returns>
    public boolean validate(Object calledFrom = null)
    {
        boolean ret;

        ret = super(calledFrom);

        if (ret && (!fromDateTime || !toDateTime))
        {
            ret = checkFailed("@SYS71110");
        }
        else if (ret && (fromDateTime > toDateTime))
        {
            ret =  checkFailed("@SYS16982");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static ClassDescription description()
    {
        return "@GLS61465";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void  main(Args  args)
    {
        EximDEPBSchedulesCopy_IN eximDEPBSchedulesCopy;

        eximDEPBSchedulesCopy = new EximDEPBSchedulesCopy_IN();

        if (eximDEPBSchedulesCopy.dialog())
        {
            eximDEPBSchedulesCopy.parmEximDEPBSchedules(args.record());

            if (eximDEPBSchedulesCopy.prompt())
            {
                eximDEPBSchedulesCopy.runOperation();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runOnServer</Name>
				<Source><![CDATA[
    /// <summary>
    /// The method run on the server side.
    /// </summary>
    /// <param name="_eximDEPBSchedules">
    /// The exim DEPB schedules.
    /// </param>
    /// <param name="_fromDateTime">
    /// The from date time.
    /// </param>
    /// <param name="_toDateTime">
    /// The to date time.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void runOnServer(
        EximDEPBScheduleTable_IN _eximDEPBSchedules,
        ValidFromDateTime        _fromDateTime,
        ValidToDateTime          _toDateTime)
    {
        EximDEPBScheduleTable_IN  eximDEPBSchedules;
        EximDEPBScheduleValues_IN eximDEPBScheduleValues;
        EximDEPBScheduleValues_IN eximDEPBScheduleValuesOrig;

        ttsbegin;
        eximDEPBSchedules.clear();
        eximDEPBSchedules.ProductGroupTableRecId = _eximDEPBSchedules.ProductGroupTableRecId;
        eximDEPBSchedules.ProductCode = _eximDEPBSchedules.ProductCode;
        eximDEPBSchedules.ValidFrom   = _fromDateTime;
        eximDEPBSchedules.ValidTo     = _toDateTime;

        if (eximDEPBSchedules.validateWrite())
        {
            eximDEPBSchedules.insert();
            while select eximDEPBScheduleValuesOrig
                where eximDEPBScheduleValuesOrig.ScheduleRecId == _eximDEPBSchedules.RecId
            {
                eximDEPBScheduleValues.clear();
                eximDEPBScheduleValues.data(eximDEPBScheduleValuesOrig);
                eximDEPBScheduleValues.ScheduleRecId = eximDEPBSchedules.RecId;
                eximDEPBScheduleValues.insert();
            }
        }
        eximDEPBSchedules.clear();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>