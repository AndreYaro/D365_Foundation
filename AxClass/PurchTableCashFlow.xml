<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableCashFlow</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// This class is the implementation between open purchase orders and cash flow.
/// </summary>
[ExportMetadata(classStr(LedgerCovIDocument), 'PurchTable'), ExportAttribute('Dynamics.AX.Application.LedgerCovIDocument')]
public class PurchTableCashFlow implements LedgerCovIDocument, LedgerCovIDocumentSingleTransactionScope
{
    private LedgerCovContext ledgerCovContext;
    private SysInstrumentationProgressLogger progressLogger;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateCashFlow</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates cash flow.
    /// </summary>
    /// <param name = "_allNew">
    /// Identifies when cash flow is generated for only new documents or all documents.
    /// </param>
    public void generateCashFlow(AllNew _allNew)
    {
        this.init();

        if (LedgerCovDocumentSingleTransactionScopeKillSwitchFlight::isEnabled())
        {
            this.generateCashFlowSetBased(_allNew);
        }
        else
        {
            this.generateCashFlowForProcessSet(_allNew);
            this.deleteCompletedRows();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    private void init()
    {
        progressLogger = this.createProgressLogger();
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateCashFlowForProcessSet</Name>
				<Source><![CDATA[
    private void generateCashFlowForProcessSet(AllNew _allNew)
    {
        const int SetSize = 1000;

        PurchId lastPurchId;
        PurchId fromPurchId;
        PurchId toPurchId;

        Query purchIdQuery = this.buildPurchIdQuery(_allNew);
        QueryRun purchIdQueryRun = new QueryRun(purchIdQuery);
        int i = 0;

        while (purchIdQueryRun.next())
        {
            i++;

            PurchTable purchTable = purchIdQueryRun.get(tableNum(PurchTable));

            PurchId purchId = purchTable.PurchId;

            if (i == 1)
            {
                fromPurchId = purchId;
            }

            if (i == SetSize)
            {
                toPurchId = purchId;

                this.processCashFlowForProcessSet(_allNew, fromPurchId, toPurchId);

                i = 0;
                fromPurchId = '';
                toPurchId = '';
            }

            lastPurchId = purchId;

        }

        if (i > 0)
        {
            toPurchId = lastPurchId;

            this.processCashFlowForProcessSet(_allNew, fromPurchId, toPurchId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>processCashFlowForProcessSet</Name>
				<Source><![CDATA[
    private void processCashFlowForProcessSet(AllNew _allNew, PurchId _fromPurchId, PurchId _toPurchId)
    {
        #OCCRetryCount
        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextForStage(CustVendInstrumentationConstants::ActivityNameCashFlowPurchaseOrder))
        {
            System.Exception exception;
            RecId purchTableRecId;
            try
            {
                PurchTable purchTable;

                ttsbegin;

                LedgerCoverage ledgerCoverage = LedgerCoverage::construct(purchTable, NoYes::Yes);

                Query q = this.buildGenerateCashFlowQueryForSet(_allNew, _fromPurchId, _toPurchId);
                QueryRun qr = new QueryRun(q);
                while (qr.next())
                {
                    purchTable = qr.get(tableNum(PurchTable));
                    purchTableRecId = purchTable.RecId;
                    purchTable.updateLedgerCov(ledgerCoverage);
                }

                LedgerCov::deleteForTransTableIdAndFilter(tableNum(PurchTable), ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                ledgerCoverage.insertDatabase();

                LedgerCovDocument::deleteForTransTableIdAndFilter(tableNum(PurchTable), ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                this.insertLedgerCovDocumentRows(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                this.updatePurchTableCovStatus(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::Deadlock;
                }
                else
                {
                    int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                    sleep(xSession::currentRetryCount() * backOffWait);
                    retry;
                }
            }
            catch (Exception::TransientSqlConnectionError)
            {
                if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                {
                    retry;
                }
                else
                {
                    throw;
                }
            }

            #StandardRetryCatchBlock

            catch (exception)
            {
                this.logProcessExecutionMessage(Exception::Error, strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedError", this.getDocumentName(), this.getDocumentNum(tableNum(PurchTable), purchTableRecId), exception.Message));
                SysInstrumentationLoggerFactory::CreateLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting).logError(strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedErrorLocked", identifierStr(PurchTable), purchTableRecId, exception.Message));

                exceptionTextFallThrough();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGenerateCashFlowQueryForSet</Name>
				<Source><![CDATA[
    private Query buildGenerateCashFlowQueryForSet(AllNew _allNew, PurchId _fromPurchId, PurchId _toPurchId)
    {
        Query query = this.buildGenerateCashFlowQuery(_allNew);
        QueryBuildDataSource qbds = query.dataSourceTable(tableNum(PurchTable));

        QueryBuildRange qbr = SysQuery::findOrCreateRange(qbds, fieldNum(PurchTable, PurchId));
        qbr.value(_fromPurchId + '..' + _toPurchId);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildPurchIdQuery</Name>
				<Source><![CDATA[
    private Query buildPurchIdQuery(AllNew _allNew)
    {
        Query query = this.buildGenerateCashFlowQuery(_allNew);
        QueryBuildDataSource qbds = query.dataSourceTable(tableNum(PurchTable));

        qbds.addSelectionField(fieldNum(PurchTable, PurchId));
        qbds.addOrderByField(fieldNum(PurchTable, PurchId), SortOrder::Ascending);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initLedgerCovContext</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void initLedgerCovContext(LedgerCovContext _ledgerCovContext)
    {
        this.ledgerCovContext = _ledgerCovContext;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createProgressLogger</Name>
				<Source><![CDATA[
    private SysInstrumentationProgressLogger createProgressLogger()
    {
        return SysInstrumentationProgressLogger::createLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting, CustVendInstrumentationConstants::InstrumentationNamespace);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logProcessExecutionMessage</Name>
				<Source><![CDATA[
    private void logProcessExecutionMessage(Exception _exceptionType, SysInfoLogStr _message, URL _helpUrl = '', SysInfoAction _sysInfoAction = null)
    {
        if (ledgerCovContext != null && ledgerCovContext.parmProcessExecutionSourceLinkRecId() != 0)
        {
            ProcessExecutionMessageLogItem messageLogItem = ProcessExecutionMessageLogItem::construct();
            messageLogItem.parmException(_exceptionType);
            messageLogItem.parmProcessExecutionSourceLink(ledgerCovContext.parmProcessExecutionSourceLinkRecId());
            messageLogItem.parmMessage(_message);

            ProcessExecutionMessageLog::insertMessage(messageLogItem);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedRows</Name>
				<Source><![CDATA[
    private void deleteCompletedRows()
    {
        #OCCRetryCount
        try
        {
            ttsbegin;
        
            this.deleteCompletedLedgerCovRows();
            this.deleteCompletedLedgerCovDocumentRows();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw Exception::Deadlock;
            }
            else
            {
                int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                sleep(xSession::currentRetryCount() * backOffWait);
                retry;
            }
        }
        catch (Exception::TransientSqlConnectionError)
        {
            if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
            {
                retry;
            }
            else
            {
                throw;
            }
        }

        #StandardRetryCatchBlock
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedLedgerCovRows</Name>
				<Source><![CDATA[
    private void deleteCompletedLedgerCovRows()
    {
        PurchTable purchTable;
        LedgerCov ledgerCov = LedgerCov::initializeForSetBasedDelete();

        delete_from ledgerCov
                where ledgerCov.TransTableId == tableNum(PurchTable)
                    exists join purchTable
                        where (purchTable.PurchStatus == PurchStatus::Invoiced
                            || purchTable.PurchStatus == PurchStatus::Canceled
                            || purchTable.DocumentState == VersioningDocumentState::Finalized)
                            && ledgerCov.TransRecId == purchTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void deleteCompletedLedgerCovDocumentRows()
    {
        PurchTable purchTable;
        LedgerCovDocument ledgerCovDocument = LedgerCovDocument::initializeForSetBasedDelete();

        delete_from ledgerCovDocument
                where ledgerCovDocument.TransTableId == tableNum(PurchTable)
                    exists join purchTable
                        where (purchTable.PurchStatus == PurchStatus::Invoiced
                            || purchTable.PurchStatus == PurchStatus::Canceled
                            || purchTable.DocumentState == VersioningDocumentState::Finalized)
                            && ledgerCovDocument.TransRecId == purchTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateCashFlowSetBased</Name>
				<Source><![CDATA[
    private void generateCashFlowSetBased(AllNew _allNew)
    {
        #OCCRetryCount
        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextForStage(CustVendInstrumentationConstants::ActivityNameCashFlowPurchaseOrder))
        {
            System.Exception exception;
            RecId purchTableRecid;
            try
            {
                PurchTable purchTable;
                ttsbegin;

                LedgerCoverage ledgerCoverage = LedgerCoverage::construct(purchTable, NoYes::Yes);

                Query q = this.buildGenerateCashFlowQuery(_allNew);
                QueryRun qr = new QueryRun(q);
                while (qr.next())
                {
                    purchTable = qr.get(tableNum(PurchTable));
                    purchTableRecId = purchTable.RecId;
                    purchTable.updateLedgerCov(ledgerCoverage);
                }

                this.deleteLedgerCovRows(_allNew, ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                ledgerCoverage.insertDatabase();

                this.deleteLedgerCovDocumentRows(_allNew, ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                this.insertLedgerCovDocumentRows(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                this.updatePurchTableCovStatus(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::Deadlock;
                }
                else
                {
                    int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                    sleep(xSession::currentRetryCount() * backOffWait);
                    retry;
                }
            }
            catch (Exception::TransientSqlConnectionError)
            {
                if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                {
                    retry;
                }
                else
                {
                    throw;
                }
            }

            #StandardRetryCatchBlock

            catch (exception)
            {
                this.logProcessExecutionMessage(Exception::Error, strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedError", this.getDocumentName(), this.getDocumentNum(tableNum(PurchTable), purchTableRecId), exception.Message));
                SysInstrumentationLoggerFactory::CreateLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting).logError(strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedErrorLocked", identifierStr(PurchTable), purchTableRecId, exception.Message));

                exceptionTextFallThrough();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGenerateCashFlowQuery</Name>
				<Source><![CDATA[
    private Query buildGenerateCashFlowQuery(AllNew _allNew)
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(PurchTable));

        qbds.addRange(fieldNum(PurchTable, PurchStatus)).value(SysQuery::valueNot(PurchStatus::Invoiced));
        qbds.addRange(fieldNum(PurchTable, DocumentState)).value(SysQuery::valueNot(VersioningDocumentState::Finalized));

        if (_allNew == AllNew::New)
        {
            qbds.addRange(fieldNum(PurchTable, CovStatus)).value(queryValue(0));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updatePurchTableCovStatus</Name>
				<Source><![CDATA[
    protected void updatePurchTableCovStatus(TmpDataAreaRecIdFilter _updatedPurchTableRows)
    {
        PurchTable purchTable;

        purchTable.skipDataMethods(true);
        purchTable.skipDatabaseLog(true);
        purchTable.skipEvents(true);
        purchTable.skipAosValidation(true);

        update_recordset purchTable
        setting CovStatus = 1
        exists join _updatedPurchTableRows
            where purchTable.RecId == _updatedPurchTableRows.RefRecId
                && _updatedPurchTableRows.RefTableId == tableNum(PurchTable)
                && _updatedPurchTableRows.RefDataAreaId == curExt();
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteLedgerCovRows</Name>
				<Source><![CDATA[
    private void deleteLedgerCovRows(AllNew _allNew, TmpDataAreaRecIdFilter _filter)
    {
        if (_allNew == AllNew::Total)
        {
            LedgerCov::deleteForTransTableId(tableNum(PurchTable));
        }
        else
        {
            LedgerCov::deleteForTransTableIdAndFilter(tableNum(PurchTable), _filter);

            PurchTable purchTable;
            LedgerCov ledgerCov = LedgerCov::initializeForSetBasedDelete();

            delete_from ledgerCov
                where ledgerCov.TransTableId == tableNum(PurchTable)
                    exists join purchTable
                        where (purchTable.PurchStatus == PurchStatus::Invoiced
                            || purchTable.PurchStatus == PurchStatus::Canceled
                            || purchTable.DocumentState == VersioningDocumentState::Finalized)
                            && ledgerCov.TransRecId == purchTable.RecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void deleteLedgerCovDocumentRows(AllNew _allNew, TmpDataAreaRecIdFilter _filter)
    {
        if (_allNew == AllNew::Total)
        {
            LedgerCovDocument::deleteForTransTableId(tableNum(PurchTable));
        }
        else
        {
            LedgerCovDocument::deleteForTransTableIdAndFilter(tableNum(PurchTable), _filter);

            PurchTable purchTable;
            LedgerCovDocument ledgerCovDocument = LedgerCovDocument::initializeForSetBasedDelete();

            delete_from ledgerCovDocument
                where ledgerCovDocument.TransTableId == tableNum(PurchTable)
                    exists join purchTable
                        where (purchTable.PurchStatus == PurchStatus::Invoiced
                            || purchTable.PurchStatus == PurchStatus::Canceled
                            || purchTable.DocumentState == VersioningDocumentState::Finalized)
                            && ledgerCovDocument.TransRecId == purchTable.RecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void insertLedgerCovDocumentRows(TmpDataAreaRecIdFilter _filter)
    {
        LedgerCovDocument ledgerCovDocument;
        PurchTable purchTable;
        VendTable vendTable;
        DirPartyTable dirPartyTable;

        TableId purchTableTableId = tableNum(PurchTable);
        TransDate transDate;
        LedgerCovDocumentType type = LedgerCovDocumentType::PurchTable;
        Voucher voucher;
        DueDate dueDate;
        CurrencyCode accountingCurrency = Ledger::accountingCurrency();

        insert_recordset ledgerCovDocument
            (TransTableId,
            TransRecId,
            TransDate,
            Type,
            AccountingCurrency,
            Number,
            Voucher,
            AccountNumber,
            DueDate,
            AccountName)
            select purchTableTableId,
                RecId,
                transDate,
                type,
                accountingCurrency,
                PurchId,
                voucher,
                OrderAccount,
                dueDate
                from purchTable
            join _filter
                where purchTable.TableId == _filter.RefTableId
                    && purchTable.RecId == _filter.RefRecId
            outer join vendTable
                where vendTable.AccountNum == purchTable.OrderAccount
            outer join Name
                from dirPartyTable
                    where dirPartyTable.RecId == vendTable.Party;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentName</Name>
				<Source><![CDATA[
    /// <summary>
    /// The document name.
    /// </summary>
    /// <returns>The document name.</returns>
    public str getDocumentName()
    {
        return "@SYS25545";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentNum</Name>
				<Source><![CDATA[
    /// <summary>
    /// The number of the document.
    /// </summary>
    /// <param name = "_tableId">Identifies the document table.</param>
    /// <param name = "_recId">Identifies the row of the document.</param>
    /// <returns>The number of the document.</returns>
    public Name getDocumentNum(TableId _tableId, RecId _recId)
    {
        Name documentNum;

        if (_tableId == tableNum(PurchTable))
        {
            PurchTable purchTable = PurchTable::findRecId(_recId);

            documentNum = purchTable.PurchId;
        }

        return documentNum;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>