<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsRebateBatchCalcDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class that serves to calculate multiple rebates at once.
/// </summary>
class PdsRebateBatchCalcDialog extends RunBaseBatch implements BatchRetryable
{
    PdsStartDate            pdsStartDate;
    PdsExpiryDate           pdsExpiryDate;

    DialogField             diagPdsStartDate;
    DialogField             diagPdsExpiryDate;

    #DEFINE.CurrentVersion(3)
    #LOCALMACRO.CurrentList
        pdsStartDate,
        pdsExpiryDate
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds date range fields to dialog box.
    /// </summary>
    /// <returns>Any object.</returns>
    public Object dialog()
    {
        DialogRunbase dialogRunbase = super();

        diagPdsStartDate = dialogRunbase.addFieldValue(
            extendedtypestr(PdsStartDate), pdsStartDate, "@SYS6255", "@SYS6255");
        diagPdsExpiryDate = dialogRunbase.addFieldValue(
            extendedtypestr(PdsExpiryDate), pdsExpiryDate, "@SYS22882", "@SYS22882");

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets dialog date range values.
    /// </summary>
    /// <returns>Return flag.</returns>
    public boolean getFromDialog()
    {
        pdsStartDate = diagPdsStartDate.value();
        pdsExpiryDate = diagPdsExpiryDate.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPdsStartDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets dialog start date value.
    /// </summary>
    /// <returns>Start date.</returns>
    public date getPdsStartDate()
    {
        return pdsStartDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets application module value.
    /// </summary>
    /// <returns>Returns vendor application module value.</returns>
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// substantiates the object
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// stores the dialog object parameters.
    /// </summary>
    /// <returns>Returns list of stored parameters.</returns>
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Placeholder.
    /// </summary>
    public void run()
    {
        PdsRebateTable::applyBatchRebates(pdsStartDate, pdsExpiryDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// restore the last parameters saved.
    /// </summary>
    /// <param name="_packedClass">list of stored parameters.</param>
    /// <returns>Returns if restore succeded or failed.</returns>
    public boolean unpack(container _packedClass)
    {
        Integer     version = conpeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case #CurrentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            default :
                ret = false;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns an instance of the class
    /// </summary>
    /// <returns>returns instance</returns>
    public static PdsRebateBatchCalcDialog construct()
    {
        return new PdsRebateBatchCalcDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns class description.
    /// </summary>
    /// <returns>class description.</returns>
    public static ClassDescription description()
    {
        return "@PRO1189";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method constructs an instance and runs the class
    /// </summary>
    /// <param name="_args">Any arguments passed</param>
    public static void main(Args _args)
    {
        #PdsNumbers

        PdsRebateBatchCalcDialog pdsRebateBatchCalcDialog = PdsRebateBatchCalcDialog::construct();

        try
        {
            if (pdsRebateBatchCalcDialog.prompt())
            {
                pdsRebateBatchCalcDialog.runOperation();
            }
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::Error)
        {
            warning("@PRO1190");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>