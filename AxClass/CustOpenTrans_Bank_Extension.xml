<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOpenTrans_Bank_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(CustOpenTrans))]
internal final class CustOpenTrans_Bank_Extension
{
    internal boolean isCallerBankStatementGeneratePaymentDialog;
    private List bankStmtISOReportEntryRecIdList;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        isCallerBankStatementGeneratePaymentDialog = this.callingForm && this.callingForm.name() == formStr(BankStatementGeneratePaymentDialog);

        if (isCallerBankStatementGeneratePaymentDialog)
        {
            bankStmtISOReportEntryRecIdList = this.args().parmObject();
            CustTransOpen_ds.linkActive();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesignDynamic</Name>
				<Source><![CDATA[
    public void updateDesignDynamic()
    {
        next updateDesignDynamic();

        if (isCallerBankStatementGeneratePaymentDialog && this.manager.getSpecTransCount() > 0)
        {
            UpdateNow.enabled(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>markBankStatementPaymentTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Marks the payment transactions of statement lines to be settled.
    /// </summary>
    internal void markBankStatementPaymentTrans()
    {
        if (bankStmtISOReportEntryRecIdList)
        {
            CustTransOpen custTransOpen;
            LedgerJournalTrans ledgerJournalTrans;
            BankStatementLinePaymentInformation paymInfo;
            ListEnumerator statementLineEnumerator = bankStmtISOReportEntryRecIdList.getEnumerator();

            while (statementLineEnumerator.moveNext())
            {
                select firstonly custTransOpen
                    join RecId from paymInfo
                        where paymInfo.BankStmtISOReportEntry == statementLineEnumerator.current()
                    join RecId from ledgerJournalTrans
                        where ledgerJournalTrans.RecId == paymInfo.LedgerJournalTrans
                            && ledgerJournalTrans.CustTransId == custTransOpen.RefRecId;

                CustTransOpen_ds.positionToRecord(custTransOpen);
                CustTransOpen_ds.editMarkTrans(true, custTransOpen, NoYes::Yes);
            }
        }

        this.updateMarkedTotals();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>