<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymAdviceCustPrint</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BankPaymAdvicePrint</c> class provides the ability to generate and print
/// payment advice reports for customers.
/// </summary>
[SysObsolete('This class is obsolete. Please use the BankPaymAdviceCustControllerV2 or BankPaymAdviceVendControllerV2 classes.', true, 10\5\2023)]
public class BankPaymAdviceCustPrint extends BankPaymAdvicePrint implements CustVendPaymIPrinterSettingsProvider
{
    private CustAccount custAccount;

    private DialogField dialogCustAccount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogAddAccount</Name>
				<Source><![CDATA[
    protected void dialogAddAccount()
    {
        dialogCustAccount = dialog.addFieldValue(extendedTypeStr(CustAccount), custAccount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPaymentAdviceMenuItemActionStr</Name>
				<Source><![CDATA[
    protected str getPaymentAdviceMenuItemActionStr()
    {
        return menuitemActionStr(BankPaymAdviceCustPrintOption);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        custAccount = dialogCustAccount.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustAccount</Name>
				<Source><![CDATA[
    public CustAccount parmCustAccount(CustAccount _custAccount = custAccount)
    {
        custAccount = _custAccount;
        return custAccount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    protected Query buildQuery()
    {
        Query query = super();

        if (custAccount)
        {
            QueryBuildDataSource qbds = query.dataSourceTable(tableNum(LedgerJournalTrans));

            SysQuery::addDimensionAttributeRange(query, qbds.name(), fieldStr(LedgerJournalTrans, LedgerDimension), DimensionComponent::LedgerDimensionDisplayValue, custAccount);
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPayments</Name>
				<Source><![CDATA[
    protected List getPayments()
    {
        QueryRun queryRun = new QueryRun(this.buildQuery());
        queryRun.setRecord(this.generatePaymentLineAccountNames());

        LedgerJournalTrans ledgerJournalTrans;
        List paymList = new List(Types::Record);

        while (queryRun.next())
        {
            ledgerJournalTrans = queryRun.get(tableNum(LedgerJournalTrans));

            ledgerJournalTrans ledgerJournalTransCopy;
            ledgerJournalTransCopy.data(ledgerJournalTrans);

            paymList.addEnd(ledgerJournalTransCopy);
        }

        return paymList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateReport</Name>
				<Source><![CDATA[
    protected void generateReport(List _paymList)
    {
        Args args = new Args();

        args.caller(this);
        args.parmObject(_paymList);
        args.parmEnumType(enumNum(NoYes));
        args.parmEnum(printPaymAdvicePrintMgmt);

        new MenuFunction(menuitemoutputstr(BankPaymAdviceCust), MenuItemType::Output).run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        BankPaymAdviceCustPrint bankPaymAdviceCustPrint;

        if (args.menuItemName() == menuitemActionStr(BankPaymAdviceCustPrintOption))
        {
            bankPaymAdviceCustPrint = args.caller().runBase();
            bankPaymAdviceCustPrint.callPrinterSettings();
            return;
        }

        if (args.dataset() != tableNum(LedgerJournalTrans))
        {
            throw error(Error::wrongUseOfFunction("@SYS55191"));
        }

        LedgerJournalTrans ledgerJournalTrans = args.record();

        bankPaymAdviceCustPrint = new BankPaymAdviceCustPrint();
        bankPaymAdviceCustPrint.parmLedgerJournalId(ledgerJournalTrans.JournalNum);
        bankPaymAdviceCustPrint.parmPaymMode(ledgerJournalTrans.PaymMode);
        if (ledgerJournalTrans.AccountType == LedgerJournalACType::Cust)
        {
            bankPaymAdviceCustPrint.parmCustAccount(ledgerJournalTrans.parmAccount());
            bankPaymAdviceCustPrint.parmAccountType(ledgerJournalTrans.AccountType);
        }
        if (ledgerJournalTrans.OffsetAccountType == ledgerJournalACType::Bank)
        {
            bankPaymAdviceCustPrint.parmBankAccountId(ledgerJournalTrans.parmOffsetAccount());
        }

        Debug::assert(args.parmEnumType(enumNum(UserInteractive)));
        UserInteractive userInteractive = args.parmEnum();
        boolean promptResult = true;
        if (userInteractive == UserInteractive::Yes)
        {
            promptResult = bankPaymAdviceCustPrint.prompt();
        }

        if (promptResult)
        {
            bankPaymAdviceCustPrint.getPaymentsAndGenerateReport();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>