<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgIpcBarcodeDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>JmgIpcBarcodeDP</c> class declares the variables and tables for the <c>JmgIpcBarcode</c>
///    report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(JmgIpcBarcode))
]
class JmgIpcBarcodeDP extends SRSReportDataProviderBase
{
    JmgTmpIpcBarcode    jmgTmpIpcBarcode;
    JmgBarcodeHelper    barcodeHelper;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getJmgTmpIpcBarcode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the data from the <c>JmgTmpIpcBarcode</c> temporary table.
    /// </summary>
    /// <returns>
    /// The data from the <c>JmgTmpIpcBarcode</c> temporary table.
    /// </returns>
    [
        SRSReportDataSetAttribute('JmgTmpIpcBarcode')
    ]
    public JmgTmpIpcBarcode getJmgTmpIpcBarcode()
    {
        select * from jmgTmpIpcBarcode;
        return jmgTmpIpcBarcode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertIntoTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts data into the temporary table.
    /// </summary>
    /// <param name="_ipcCategory">
    /// A <c>JmgIpcCategory</c> record.
    /// </param>
    /// <param name="_ipcActivity">
    /// A <c>JmgIpcActivity</c> record.
    /// </param>
    protected void insertIntoTmp(JmgIpcCategory _ipcCategory, JmgIpcActivity _ipcActivity)
    {
        // ipc category fields
        jmgTmpIpcBarcode.Category               = _ipcCategory.Category;
        jmgTmpIpcBarcode.CategoryDescription    = _ipcCategory.Description;

        // ipc activity fields
        jmgTmpIpcBarcode.Activity               = _ipcActivity.Activity;
        jmgTmpIpcBarcode.ActivityJobId          = _ipcActivity.JobId;
        jmgTmpIpcBarcode.ActivityDescription    = _ipcActivity.Description;

        // barcode fields
        jmgTmpIpcBarcode.BarcodeString = barcodeHelper.encodeBarcode(_ipcActivity.JobId);

        jmgTmpIpcBarcode.insert();
        jmgTmpIpcBarcode.clear();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fetches records based on the parameter values entered and inserts the records into the temporary
    /// table.
    /// </summary>
    public void processReport()
    {
        QueryRun                    queryRun = new QueryRun(this.parmQuery());

        JmgIpcCategory              ipcCategory;
        JmgIpcActivity              ipcActivity;

        barcodeHelper = JmgBarcodeHelper::construct();

        while (queryRun.next())
        {
            ipcCategory = queryRun.get(tablenum(JmgIpcCategory));
            ipcActivity = queryRun.get(tablenum(JmgIpcActivity));

            this.insertIntoTmp(ipcCategory, ipcActivity);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
