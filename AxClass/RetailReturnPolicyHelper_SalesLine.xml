<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailReturnPolicyHelper_SalesLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Helpep for applying policy to <c>SalesLine</c> record inventory dimensions.
/// </summary>
class RetailReturnPolicyHelper_SalesLine extends RetailReturnPolicyHelper
{
    SalesLine salesLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>apply</Name>
				<Source><![CDATA[
        public boolean apply(InventDimId _lineInventDimId)
    {
        InventDimId inventDimId = this.getMergedInventDimId(_lineInventDimId);

        if (!inventDimId)
        {
            return false;
        }

        salesLine.InventDimId = inventDimId;
        if (isBlocked)
        {
            salesLine.RetailBlockQty = -salesLine.SalesQty;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromReturnReasonCodeIdAndSalesLine</Name>
				<Source><![CDATA[
        private void initFromReturnReasonCodeIdAndSalesLine(ReturnReasonCodeId _reasonCode, SalesLine _salesLine)
    {
        retailReturnPolicyLine  = RetailReturnReasonCodePolicyLine::find(policyRefRecId, _reasonCode, retailStoreTableRecId, retailStoreTableRecId);
        salesLine               = _salesLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs new instance of <c>RetailReturnPolicyHelper_SalesLine</c>
    /// </summary>
    /// <param name="_retailStoreTableRecId">
    /// <c>RetailStoreTable</c> record RecId
    /// </param>
    /// <param name="_itemId">
    /// Released product id
    /// </param>
    /// <param name="_reasonCode">
    /// Return reason code id
    /// </param>
    /// <param name="_salesLine">
    /// <c>SalesLine</c> record to update
    /// </param>
    /// <returns>
    /// New instance of <c>RetailReturnPolicyHelper_SalesLine</c>
    /// </returns>
    public static RetailReturnPolicyHelper_SalesLine construct(RefRecId _retailStoreTableRecId, ItemId _itemId, ReturnReasonCodeId _reasonCode, SalesLine _salesLine)
    {
        RetailReturnPolicyHelper_SalesLine instance = new RetailReturnPolicyHelper_SalesLine();
        instance.initFromRetailStoreTableRecIdAndItemId(_retailStoreTableRecId, _itemId);
        instance.initFromReturnReasonCodeIdAndSalesLine(_reasonCode, _salesLine);

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInBlockedLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if the inventory dimension of saleline should be blocked.
    /// </summary>
    /// <param name = "_salesLine">
    /// <c>SalesLine</c> record to be checked.
    /// </param>
    /// <returns>True if blocked; otherwise, false.</returns>
    public static boolean isInBlockedLocation(SalesLine _salesLine)
    {
        RetailReturnInfocodePolicyLine    policyLine;
        InventDim    localInventDim = _salesLine.InventDim();
        RecId    retailReturnPolicy = RetailAttributesLegalEntityLookup::findReturnPolicyByCategory(EcoResProductCategory::findRetailCategoryByItem(_salesLine.itemId)).ReturnPolicy;

        boolean ret = false;

        if (retailReturnPolicy && _salesLine.isStocked() && _salesLine.SalesQty > 0)
        {
            select firstOnly RecId from policyLine
                where  policyLine.Policy           == retailReturnPolicy
                    && policyLine.InventLocationId == localInventDim.InventLocationId
                    && policyLine.WMSLocationId    == localInventDim.WMSLocationId
                    && policyLine.BlockItem;
            
            if (policyLine.RecId)
            {
                ret = true;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>