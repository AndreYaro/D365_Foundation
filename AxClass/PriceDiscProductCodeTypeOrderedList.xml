<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PriceDiscProductCodeTypeOrderedList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Represents the ordered list of enumeration values for <c>PriceDiscProductCodeType</c> enum.
/// Allows to add values to the list to support extension.
/// </summary>
public class PriceDiscProductCodeTypeOrderedList implements Enumerable
{
    List priceDiscProductCodeTypeOrderedList;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEnumerator</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the enumerator of the ordered list.
    /// </summary>
    /// <returns>An instance of <c>ListEnumerator</c> class.</returns>
    public Enumerator getEnumerator()
    {
        return priceDiscProductCodeTypeOrderedList.getEnumerator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>addAfter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>PriceDiscProductCodeType</c> enum to the ordered list after the specified value.
    /// </summary>
    /// <param name = "_newPriceDiscProductCodeTypeValue">Value to be added.</param>
    /// <param name = "_addAfterValue">Value to add after.</param>
    /// <exception>Could not find _addAfterValue in the ordered list.</exception>
    public void addAfter(PriceDiscProductCodeType _newPriceDiscProductCodeTypeValue, PriceDiscProductCodeType _addAfterValue)
    {
        ListIterator listIterator = new ListIterator(priceDiscProductCodeTypeOrderedList);
        while (listIterator.more())
        {
            if (listIterator.value() == _addAfterValue)
            {
                listIterator.next();
                listIterator.insert(_newPriceDiscProductCodeTypeValue);
                return;
            }
            listIterator.next();
        }
        throw error(strFmt("@SCM:CouldNotAddValueXAfterValueY_YnotFound", _newPriceDiscProductCodeTypeValue, _addAfterValue));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addStart</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>PriceDiscProductCodeType</c> enum to the start of the ordered list.
    /// </summary>
    /// <param name = "_priceDiscProductCodeType">Value to be added.</param>
    public void addStart(PriceDiscProductCodeType _priceDiscProductCodeType)
    {
        priceDiscProductCodeTypeOrderedList.addStart(_priceDiscProductCodeType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addEnd</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>PriceDiscProductCodeType</c> enum to the end of the ordered list.
    /// </summary>
    /// <param name = "_priceDiscProductCodeType">Value to be added.</param>
    public void addEnd(PriceDiscProductCodeType _priceDiscProductCodeType)
    {
        priceDiscProductCodeTypeOrderedList.addEnd(_priceDiscProductCodeType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        priceDiscProductCodeTypeOrderedList = new List(Types::Enum);

        this.initializePriceDiscProductCodeTypeOrderedList();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setEnumerateInOrder</Name>
				<Source><![CDATA[
    private void setEnumerateInOrder(SortOrder _enumerateInOrder)
    {
        if (_enumerateInOrder == SortOrder::Descending)
        {
            this.reverseOrderedList();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>reverseOrderedList</Name>
				<Source><![CDATA[
    private void reverseOrderedList()
    {
        List reversedPriceDiscProductCodeTypeOrderedList = new List(Types::Enum);
        ListEnumerator enumerator = this.getEnumerator() as ListEnumerator;
        
        while (enumerator.moveNext())
        {
            reversedPriceDiscProductCodeTypeOrderedList.addStart(enumerator.current());
        }

        priceDiscProductCodeTypeOrderedList = reversedPriceDiscProductCodeTypeOrderedList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the <c>PriceDiscProductCodeTypeOrderedList</c> class to be enumerated in the specified order.
    /// </summary>
    /// <param name="_enumerateInOrder">Enumerate the values in the enum in the specified order.</param>
    /// <returns>An instance of <c>PriceDiscProductCodeTypeOrderedList</c> class.</returns>
    public static PriceDiscProductCodeTypeOrderedList newFromOrder(SortOrder _enumerateInOrder = SortOrder::Ascending)
    {
        PriceDiscProductCodeTypeOrderedList orderedList = new PriceDiscProductCodeTypeOrderedList();
        orderedList.setEnumerateInOrder(_enumerateInOrder);
        return orderedList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializePriceDiscProductCodeTypeOrderedList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize the list with all the applicable values of <c>PriceDiscProductCodeType</c> enumeration in ascending order.
    /// </summary>
    protected void initializePriceDiscProductCodeTypeOrderedList()
    {
        priceDiscProductCodeTypeOrderedList.addEnd(PriceDiscProductCodeType::Table);
        priceDiscProductCodeTypeOrderedList.addEnd(PriceDiscProductCodeType::GroupId);
        priceDiscProductCodeTypeOrderedList.addEnd(PriceDiscProductCodeType::All);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>