<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSGS1DefaultPolicySetupCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class WHSGS1DefaultPolicySetupCreator
{
    RecordInsertList recordInsertList;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WHSGS1DefaultPolicySetupCreator::createDefaultPolicySetup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createDefaultPolicySetup</Name>
				<Source><![CDATA[
    private static void createDefaultPolicySetup()
    {     
        if (!WHSGS1ParsingSetup::isParsingSetupMade())
        {
            throw error("@WAX:GS1MakeParsingSetupWarning");
        }

        ttsbegin;

        WHSGS1DefaultPolicySetupCreator::clearPolicySetup();
        WHSGS1DefaultPolicySetupCreator defaultPolicySetupCreator = new WHSGS1DefaultPolicySetupCreator();
        defaultPolicySetupCreator.insertDefaultValues();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearPolicySetup</Name>
				<Source><![CDATA[
    private static void clearPolicySetup()
    {
        WHSGS1PolicyLine policyLine;
        WHSGS1PolicyTable policyTable;

        delete_from policyLine;
        delete_from policyTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDefaultValues</Name>
				<Source><![CDATA[
    protected void insertDefaultValues()
    {
        this.insertDefaultPolicies();
        this.insertDefaultPolicyLines();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDefaultPolicies</Name>
				<Source><![CDATA[
    private void insertDefaultPolicies()
    {
        recordInsertList = new RecordInsertList(tableNum(WHSGS1PolicyTable));

        this.populateRecordInsertListWithPolicies();

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDefaultPolicyLines</Name>
				<Source><![CDATA[
    private void insertDefaultPolicyLines()
    {
        recordInsertList = new RecordInsertList(tableNum(WHSGS1PolicyLine));

        this.populateRecordInsertListWithPolicyLines();

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateRecordInsertListWithPolicies</Name>
				<Source><![CDATA[
    private void populateRecordInsertListWithPolicies()
    {
        this.addPolicyRecord("UserDirected", 'Policy for user directed processes.');
        this.addPolicyRecord("Batch", 'Policy for scanning batch along with other fields.');
        this.addPolicyRecord("Purchase receiving", 'Policy for processing purchase receiving.');
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateRecordInsertListWithPolicyLines</Name>
				<Source><![CDATA[
    private void populateRecordInsertListWithPolicyLines()
    {
        this.addPolicyLineRecord("UserDirected", WHSGS1DefaultMDIdentifiers::Id, WHSGS1DefaultIdentifiers::SSCCIdentifier);
        this.addPolicyLineRecord("UserDirected", WHSGS1DefaultMDIdentifiers::InventBatchId, WHSGS1DefaultIdentifiers::BatchIdentifier);

        this.addPolicyLineRecord("Batch", WHSGS1DefaultMDIdentifiers::InventBatchId, WHSGS1DefaultIdentifiers::BatchIdentifier);

        this.addPolicyLineRecord("Purchase receiving", WHSGS1DefaultMDIdentifiers::InventBatchId, WHSGS1DefaultIdentifiers::BatchIdentifier);
        this.addPolicyLineRecord("Purchase receiving", WHSGS1DefaultMDIdentifiers::ExpDate, WHSGS1DefaultIdentifiers::ExpDateIdentifier);
        this.addPolicyLineRecord("Purchase receiving", WHSGS1DefaultMDIdentifiers::Qty, WHSGS1DefaultIdentifiers::QtyIdentifier);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPolicyRecord</Name>
				<Source><![CDATA[
    private void addPolicyRecord(   WHSGS1PolicyName    _policyName,
                                    Description         _description)
    {
        WHSGS1PolicyTable policy;

        policy.initValue();

        policy.PolicyName   = _policyName;
        policy.Description  = _description;

        if (policy.validateWrite())
        {
            recordInsertList.add(policy);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPolicyLineRecord</Name>
				<Source><![CDATA[
    private void addPolicyLineRecord( WHSGS1PolicyName    _policyName,
                                        WHSGS1MDIdentifier  _mdIdentifier,
                                        WHSGS1Identifier    _identifier,
                                        WHSGS1Sorting       _sorting = 0)
    {
        WHSGS1PolicyLine policyLine;

        if (WHSGS1ParsingSetup::exist(_identifier))
        {
            policyLine.initValue();

            policyLine.PolicyName     = _policyName;
            policyLine.Identifier     = _identifier;
            policyLine.MDIdentifier   = _mdIdentifier;
            policyLine.Sorting        = _sorting;

            if (policyLine.validateWrite())
            {
                recordInsertList.add(policyLine);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>