<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>KanbanPickListReportController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>KanbanPickListReportController</c> class is a controller class for the <c>KanbanPickList</c>
///    report.
/// </summary>
public class KanbanPickListReportController extends KanbanReportController implements BatchRetryable
{
    #define.ReportName('KanbanPickList.Report')
    List            jobList;
    WrkCtrIdBase    workCell;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmJobList</Name>
				<Source><![CDATA[
        public List parmJobList(List _jobList = jobList)
    {
        jobList = _jobList;
        return jobList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkCell</Name>
				<Source><![CDATA[
        public WrkCtrIdBase parmWorkCell(WrkCtrIdBase _workCell = workCell)
    {
        workCell = _workCell;
        return workCell;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preparePrint</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Prepares the printing of kanban picking lists.
    /// </summary>
    /// <param name="_listParameter">
    ///    A list that contains jobs to be printed.
    /// </param>
    /// <param name="_workCell">
    ///    The name of the work cell from which records were selected.
    /// </param>
    /// <param name="_showDialog">
    ///    A value that determines whether a dialog with printing options is opened.
    /// </param>
    /// <param name="_location">
    ///    The information about where data is printed.
    /// </param>
    /// <returns>
    ///    true if the printing was successful; otherwise, false.
    /// </returns>
    protected boolean preparePrint(
        List                _listParameter,
        WrkCtrIdBase        _workCell,
        boolean             _showDialog,
        KanbanPrintLocation _location = KanbanPrintLocation::Default)
    {
        boolean     result = _listParameter != null;

        if (   _listParameter
            && _listParameter.elements())
        {
            this.parmReportName(#ReportName);
            this.parmDialogCaption("@SYS338699");
            this.parmShowDialog(_showDialog);
            this.parmDefaultPrintSettings(KanbanReportController::getPrintDestinationSettings(_location,_workCell));

            this.parmJobList(_listParameter);
            this.parmWorkCell(_workCell);

            Args newArgs = this.parmArgs();
            if (newArgs == null)
            {
                newArgs = new Args();
            }
            newArgs.menuItemType(MenuItemType::Output);
            newArgs.menuItemName(menuItemOutputStr(KanbanPickList));

            this.parmArgs(newArgs);
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Changes the report contract before the report is run.
    /// </summary>
    protected void preRunModifyContract()
    {
        KanbanPickListContract contract = this.parmReportContract().parmRdpContract() as KanbanPickListContract;

        super();
        contract.parmJobList(jobList);
        contract.parmWorkCell(workCell);
    }

]]></Source>
			</Method>
			<Method>
				<Name>printReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Prints kanban picking lists.
    /// </summary>
    /// <param name="_listParameter">
    ///    A list that contains kanban IDs or card IDs of records to be printed.
    /// </param>
    /// <param name="_workCell">
    ///    The name of the work cell from which records were selected.
    /// </param>
    /// <param name="_showDialog">
    ///    A value that determines whether a dialog with printing options is opened.
    /// </param>
    /// <param name="_location">
    ///    The information about where data is printed.
    /// </param>
    /// <returns>
    ///    true if the printing was successful; otherwise, false.
    /// </returns>
    public boolean printReport(
        List                _listParameter,
        WrkCtrIdBase        _workCell,
        boolean             _showDialog,
        KanbanPrintLocation _location = KanbanPrintLocation::Default)
    {
        boolean result = this.preparePrint(_listParameter,_workCell,_showDialog,_location);

        if (result)
        {
            this.print();
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static KanbanPickListReportController construct()
    {
        return new KanbanPickListReportController();
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>