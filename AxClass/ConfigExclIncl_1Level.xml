<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ConfigExclIncl_1Level</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class ConfigExclIncl_1Level extends ConfigExcludeInclude
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>configGroupIdLookupQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a query container to use in a lookup form.
    /// </summary>
    /// <param name="_bomId">
    ///    The <c>BOMid</c> value.
    /// </param>
    /// <returns>
    ///    The container that has a query to use in a lookup.
    /// </returns>
    container configGroupIdLookupQuery(BOMId _bomId)
    {
        Query                   q;
        QueryBuildDataSource    qB;
        BOM                     bom;
        container               configGroups;

        q  = new Query();
        qB = q.addDataSource(tableNum(ConfigGroup));

        while select bom group ConfigGroupId
            where bom.bomId         == _bomId &&
                  bom.ConfigGroupId != ''
        {
            if (! conFind(configGroups,bom.ConfigGroupId))
            {
                configGroups += bom.ConfigGroupId;
                qB.addRange(fieldNum(ConfigGroup,ConfigGroupId)).value(queryValue(bom.ConfigGroupId));
            }
        }

        return q.pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>configItemIdLookupQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a query container to use in a look up form.
    /// </summary>
    /// <param name="_bomId">
    /// A <c>BOMid</c> value.
    /// </param>
    /// <param name="_configGroupId">
    /// A <c>ConfigGroupId</c> value.
    /// </param>
    /// <returns>
    /// A container that has a query to use in a look up.
    /// </returns>
    container configItemIdLookupQuery(
        BOMId           _bomId,
        ConfigGroupId   _configGroupId)
    {
        Query                   q;
        QueryBuildDataSource    qB;
        BOM                     bom;
        container           items;

        if (! _configGroupId)
        {
            throw error(strFmt("@SYS25638",funcName()));
        }

        q  = new Query();
        qB = q.addDataSource(tableNum(BOM));
        qB.orderMode(OrderMode::GroupBy);

        while select bom group ItemId
            where bom.bomId         == _bomId
               && bom.ConfigGroupId == _configGroupId
        {
            if (! conFind(items,bom.ItemId))
            {
                items += bom.ItemId;
                qB.addRange(fieldNum(BOM,ItemId)).value(queryValue(bom.ItemId));
            }
        }

        if (! items)
        {
            qB.addRange(fieldNum(BOM,RecId)).value(int2str(0));
        }

        qB.addSortField(fieldNum(BOM,ItemId));

        return q.pack();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>