<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxJournalLineEntityHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxJournalLineEntityHelper</c> class is a helper class for the entities associated with journal line tax scenarios.
/// </summary>
internal class TaxJournalLineEntityHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setLedgerJournalTransTaxExtension</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the value for table <c>LedgerJournalTransTaxExtension</c> when it is imported via journal line data entity.
    /// </summary>
    /// <param name = "_dataSourceCtx">The context of the datasource being mapped.</param>
    /// <returns>The <c>LedgerJournalTransTaxExtension</c> record.</returns>
    public static LedgerJournalTransTaxExtension setLedgerJournalTransTaxExtension(DataEntityDataSourceRuntimeContext _dataSourceCtx)
    {
        LedgerJournalTransTaxExtension ledgerJournalTransTaxExtension;
        boolean isExtensionEnabled;

        switch (_dataSourceCtx.name())
        {
            case dataEntityDataSourceStr(LedgerJournalLineEntity, LedgerJournalTransTaxExtension):
                isExtensionEnabled = Tax::isTaxIntegrationEnabledForBusinessProcess(TaxIntegrationBusinessProcess::Journal);
                break;

            case dataEntityDataSourceStr(ExpenseJournalLineEntity, LedgerJournalTransTaxExtension):
                isExtensionEnabled = Tax::isTaxIntegrationEnabledForBusinessProcess(TaxIntegrationBusinessProcess::Project);
                break;

            default:
                isExtensionEnabled = false;
                break;
        }

        if (isExtensionEnabled)
        {
            LedgerJournalTransTaxExtension ledgerJournalTransTaxExtBuffer = _dataSourceCtx.getBuffer();

            // When override sales tax is empty, it needs to get the value via default logic.
            ledgerJournalTransTaxExtBuffer.defaultRow();

            // Checks if extension table is inserted already since it may be inserted via LedgerJournalTrans.insert() method.
            // If yes, it should copy the data entity buffer to the extension table so that it can be updated later;
            // otherwise, it will set the data entity buffer to the empty extension table so that it can be inserted later.
            ledgerJournalTransTaxExtension =
                LedgerJournalTransTaxExtension::findByLedgerJournalTrans(ledgerJournalTransTaxExtBuffer.ledgerJournalTrans);
            buf2Buf(ledgerJournalTransTaxExtBuffer, ledgerJournalTransTaxExtension);
        }

        return ledgerJournalTransTaxExtension;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>