<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetCache</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BudgetCache</c> class caches budget information.
/// </summary>
public class BudgetCache
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearAllScopes</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Clears the budget cache for all budget cache scopes.
    /// </summary>
    public static void clearAllScopes()
    {
        DictEnum dictEnum = new DictEnum(enumnum(BudgetCacheScope));
        int i;

        for (i = 0; i <= dictEnum.values(); i++)
        {
            BudgetCache::clearScope(dictEnum.index2Value(i));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearScope</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Clears the budget cache for the specified budget cache scope.
    /// </summary>
    /// <param name="_scope">
    ///    The budget cache scope for which to clear.
    /// </param>
    public static void clearScope(BudgetCacheScope _scope)
    {
        SysGlobalObjectCache objectCache = ClassFactory.globalObjectCache();

        objectCache.clear(BudgetCache::getCacheScopeStr(_scope));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCacheScopeStr</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the budget cache scope identifier for the specified budget cache scope.
    /// </summary>
    /// <param name="_scope">
    ///    The budget cache scope for which to get the identifier.
    /// </param>
    /// <returns>
    ///    The identifier of the budget cache scope.
    /// </returns>
    private static str getCacheScopeStr(BudgetCacheScope _scope)
    {
        // Int2Str is used instead of Enum2Str to get the int
        // value like '2' instead of the string value for
        // the scope to ensure uniqueness. The _scope enum
        // is implicitly cast to an int by this call. This
        // avoids calling strfmt() which causes a kernel
        // callback running under IL and is therefore significantly
        // slower.
        return 'BudgetCache_' + int2str(_scope);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the budget cache value for the specified budget cache scope and value key.
    /// </summary>
    /// <param name="_scope">
    ///    The budget cache scope for which to obtain the cache value.
    /// </param>
    /// <param name="_key">
    ///    The value key for which to obtain the cache value.
    /// </param>
    /// <returns>
    ///    The budget cache value.
    /// </returns>
    public static container getValue(BudgetCacheScope _scope, container _key)
    {
        SysGlobalObjectCache objectCache = ClassFactory.globalObjectCache();

        return objectCache.find(BudgetCache::getCacheScopeStr(_scope), _key);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Inserts the budget cache value.
    /// </summary>
    /// <param name="_scope">
    ///    The budget cache scope to insert.
    /// </param>
    /// <param name="_key">
    ///    The value key to insert.
    /// </param>
    /// <param name="_value">
    ///    The value to insert.
    /// </param>
    public static void insertValue(BudgetCacheScope _scope, container _key, container _value)
    {
        SysGlobalObjectCache objectCache = ClassFactory.globalObjectCache();

        objectCache.insert(BudgetCache::getCacheScopeStr(_scope), _key, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>valueExists</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the specified value key in the budget cache exists.
    /// </summary>
    /// <param name="_scope">
    ///    The budget cache scope of the budget cache to check for existence.
    /// </param>
    /// <param name="_key">
    ///    The value key of the budget cache to check for existence.
    /// </param>
    /// <returns>
    ///    true if the specified value key exists; otherwise, false.
    /// </returns>
    public static boolean valueExists(BudgetCacheScope _scope, container _key)
    {
        SysGlobalObjectCache objectCache = ClassFactory.globalObjectCache();

        return (objectCache.find(BudgetCache::getCacheScopeStr(_scope), _key) != connull());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>