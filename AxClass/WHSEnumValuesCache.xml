<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSEnumValuesCache</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSEnumValuesCache
{
    private SysGlobalObjectCache cache = classfactory.globalObjectCache();
    
    private const str cacheScope = classStr(WHSEnumValuesCache);
    private container cacheKey;
    private EnumName enumName;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromEnumName</Name>
				<Source><![CDATA[
    internal static WHSEnumValuesCache newFromEnumName(EnumName _enumName)
    {
        WHSEnumValuesCache cache = new WHSEnumValuesCache();
        cache.cacheKey = [_enumName];
        cache.enumName = _enumName;
        return cache;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEnumValues</Name>
				<Source><![CDATA[
    internal container getEnumValues()
    {
        container enumValues = this.getFromCache();
        if (!enumValues)
        {
            this.fillCache();
            enumValues = this.getFromCache();
        }

        return enumValues;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromCache</Name>
				<Source><![CDATA[
    private container getFromCache()
    {
        return cache.find(cacheScope, cacheKey);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillCache</Name>
				<Source><![CDATA[
    private void fillCache()
    {
        container enumValues = this.getEnumValuesUsingReflection();
        cache.insert(cacheScope, cacheKey, enumValues);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEnumValuesUsingReflection</Name>
				<Source><![CDATA[
    private container getEnumValuesUsingReflection()
    {
        container enumValues;

        SysDictEnum sysDictEnum = new SysDictEnum(enumName2Id(enumName));

        for (int enumValueIndex = 1; enumValueIndex <= sysDictEnum.values(); enumValueIndex++)
        {
            anytype enumValue = sysDictEnum.index2Value(enumValueIndex);
            enumValues += enumValue;
        }

        return enumValues;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>