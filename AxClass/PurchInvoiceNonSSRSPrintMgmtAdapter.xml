<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchInvoiceNonSSRSPrintMgmtAdapter</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchInvoice4PaymNonSSRSPrintMgmtAdapter</c> class used to allow
/// use of print management for non-SSRS (for example, Microsoft Office Excel)
/// invoice report printed by invoice from purchase order.
/// </summary>
[NonSSRSPrintMgmtAdapterAttribute(PrintMgmtDocumentType::PurchaseOrderInvoice)]
class PurchInvoiceNonSSRSPrintMgmtAdapter extends FormLetterNonSSRSPrintMgmtAdapter
{
    PurchInvoiceJournalPrint purchInvoiceJournalPrint;
    RecordSortedList journalList;
    VendInvoiceJour vendInvoiceJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFormLetterReport</Name>
				<Source><![CDATA[
        protected void initFormLetterReport()
    {
        printCopyOriginal = this.parmArgs().parmEnum();

        if (classIdGet(this.parmArgs().caller()) == classNum(PurchInvoiceJournalPrint))//PurchInvoiceJournalPrint class is also used for approvals
        {
            purchInvoiceJournalPrint = this.parmArgs().caller();
        }

        if (this.parmArgs().record())
        {
            journalList = FormLetter::createJournalListCopy(this.parmArgs().record());
        }
        else
        {
            journalList = this.parmArgs().object();
        }

        formLetterReport = FormLetterReport::construct(PrintMgmtDocumentType::PurchaseOrderInvoice);

        if (purchInvoiceJournalPrint)
        {
            formLetterReport.parmDefaultCopyPrintJobSettings(new SRSPrintDestinationSettings(purchInvoiceJournalPrint.parmPrinterSettingsFormLetterCopy()));
            formLetterReport.parmDefaultOriginalPrintJobSettings(new SRSPrintDestinationSettings(purchInvoiceJournalPrint.parmPrinterSettingsFormLetter()));
            formLetterReport.parmUsePrintMgmtDestinations(purchInvoiceJournalPrint.parmUsePrintManagement());
        }
        else if (printCopyOriginal == PrintCopyOriginal::OriginalPrint)
        {
            // Always use the print mgmt destinations when reprinting for the OriginalPrint case.
            formLetterReport.parmUsePrintMgmtDestinations(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPrintMgmt</Name>
				<Source><![CDATA[
    /// <summary>
    /// Searches for print management settings and then executes them
    /// for each record in list of invoices being printed.
    /// </summary>
    protected void runPrintMgmt()
    {
        if (!journalList)
        {
            throw error("@SYS26348");
        }

        while (journalList.next(vendInvoiceJour))
        {
            if (!vendInvoiceJour)
            {
                throw error("@SYS26348");
            }

            formLetterReport.parmSSRS(PrintMgmtSSRS::NonSSRS);
            formLetterReport.parmPrintType(printCopyOriginal);

            formLetterReport.loadPrintSettings(vendInvoiceJour,
                                               vendInvoiceJour.purchTable(),
                                               vendInvoiceJour.LanguageId);
            this.outputReports();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRecordId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method used to set up recId of invoice being printed.
    /// </summary>
    protected void setRecordId()
    {
        recordId = vendInvoiceJour.RecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>