<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesPurchFindHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Helper class to search and show specific records from a datasource only after a value has been provided in the quickfilter.
/// </summary>
public class SalesPurchFindHelper
{
    boolean needToQuery = true;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>hideAndDisableControls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Hides the grid and disables an optional button
    /// </summary>
    /// <param name = "_grid">The grid to hide</param>
    /// <param name = "_button">An optional button to disable</param>
    public void hideAndDisableControls(FormGridControl _grid, FormControl _button = null)
    {
        if (_button)
        {
            _button.enabled(false);
        }
        _grid.visible(false);
        needToQuery = true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showAndEnableControls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Shows the grid and enables or disables an optional button
    /// </summary>
    /// <param name = "_grid">The grid to show</param>
    /// <param name = "_button">An optional button to enable or disable</param>
    /// <param name = "_enableButton">Controls if the button should be enabled or disabled</param>
    public void showAndEnableControls(FormGridControl _grid, FormControl _button = null, boolean _enableButton = false)
    {
        if (_button)
        {
            _button.enabled(_enableButton);
        }
        _grid.visible(true);
        needToQuery = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies the filter specified in the quickfilter and retrieves the corresponding records.
    /// </summary>
    /// <param name = "_filter">The quickfilter control</param>
    /// <param name = "_ds">The datasource associated to the records</param>
    public void applyFilter(QuickFilterControl _filter, FormDataSource _ds)
    {
        if (needToQuery)
        {
            str filterField = _filter.parmFilterField();
            str filterValue = _filter.parmFilterValue();

            _ds.executeQuery();

            _filter.parmFilterField(filterField);
            _filter.parmFilterValue(filterValue);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SalesPurchFindHelper construct()
    {
        return new SalesPurchFindHelper();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>