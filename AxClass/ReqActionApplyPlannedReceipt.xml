<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqActionApplyPlannedReceipt</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class applies an action to a <c>ReqTrans</c> record which is a planned receipt.
/// </summary>
[ReqRefTypeFactoryAttribute(ReqRefType::ItemPlannedOrder)]
[ReqRefTypeFactoryAttribute(ReqRefType::BOMPlannedOrder)]
[ReqRefTypeFactoryAttribute(ReqRefType::TransferPlannedOrder)]
[ReqRefTypeFactoryAttribute(ReqRefType::PlannedKanban)]
class ReqActionApplyPlannedReceipt extends ReqActionApplyToOrder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applyActionToReferencedOrder</Name>
				<Source><![CDATA[
    public void applyActionToReferencedOrder()
    {
        ReqPO reqPO;

        reqPO = reqTrans.reqPo(true);

        if (!reqPO.RecId)
        {
            throw error(strFmt("@SYS24774", reqTrans.RefId));
        }

        if (reqPO.ReqPOStatus == ReqPOStatus::Approved && !reqActionApply.parmApplyToApprovedPlannedOrders())
        {
            throw error(strFmt("@GLS3800081", reqPO.RefId));
        }

        if (reqTrans.ActionType == ActionType::Cancel)
        {
            if (!reqPO.validateDelete())
            {
                throw error("@SYS18447");
            }

            reqPO.delete();
        }
        else
        {
            if (reqTrans.ActionDays != 0)
            {
                reqPO.ReqDate = reqTrans.ActionDate;
                reqPO.initFromReqDate(reqPO.reqSetupDim(), ReqPlanData::newReqPlanVersion(reqPO.PlanVersion), false);
            }

            if (reqTrans.ActionQtyAdd)
            {
                reqPO.Qty += reqTrans.ActionQtyAdd;
                reqPO.PdsCWReqQty += PdsCatchWeight::cwQty(reqPO.ItemId, reqTrans.ActionQtyAdd);
            }

            reqPO.ReqPOStatus = reqActionApply.parmNewReqPOStatus();

            if (!reqPO.validateWrite())
            {
                throw error("@SYS18447");
            }

            reqPO.update();

            if (reqTrans.ActionDays != 0 && reqPO.ReqDate != reqTrans.ActionDate)
            {
                warning(strFmt("@GLS3800092", reqTrans.ActionDate, reqPO.ReqDate));
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>