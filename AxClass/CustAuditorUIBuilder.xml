<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAuditorUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>CustAuditorUIBuilder</c> class is the UI Builder class for the <c>CustAuditor</c> report.
/// </summary>
public class CustAuditorUIBuilder extends SrsReportDataContractUIBuilder
{
    CustAuditorContract custAuditorContract;
    DialogGroup dialogGroupDateInterval;
    DialogGroup dialogGroupDateIntervalCode;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Builds the dialog for the <c>CustAuditor</c> SSRS report.
    /// </summary>
    public void build()
    {
        // Gets the underlying CustAuditorContract, since this UI Builder is associated to the Contract by the attribute on contract.
        custAuditorContract = this.dataContractObject();
        this.addDialogField(methodstr(CustAuditorContract, parmShowDetails), custAuditorContract);

        dialogGroupDateInterval = dialog.addGroup("@SYS22526");
        dialogGroupDateInterval.frameOptionButton(FormFrameOptionButton::Radio);
        dialogGroupDateInterval.optionValue(true);
        this.addDialogField(methodstr(CustAuditorContract, parmFromDate), custAuditorContract);
        this.addDialogField(methodstr(CustAuditorContract, parmToDate), custAuditorContract);

        dialogGroupDateIntervalCode = dialog.addGroup("@SYS60051");
        dialogGroupDateIntervalCode.frameOptionButton(FormFrameOptionButton::Radio);
        dialogGroupDateIntervalCode.optionValue(false);
        this.addDialogField(methodstr(CustAuditorContract, parmPeriodCode), custAuditorContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Transfers data from the dialog into the data contract object.
    /// </summary>
    public void getFromDialog()
    {
        super();

        if (!dialogGroupDateIntervalCode.optionValue())
        {
            custAuditorContract.parmPeriodCode('');
        }
        else if (!dialogGroupDateInterval.optionValue())
        {
            custAuditorContract.parmFromDate(dateNull());
            custAuditorContract.parmToDate(dateNull());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>