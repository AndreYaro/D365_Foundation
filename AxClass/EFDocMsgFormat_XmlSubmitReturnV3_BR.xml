<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EFDocMsgFormat_XmlSubmitReturnV3_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Creates and interprets the XML to be sent to the submit return web service. (layout Version 3.10)
/// </summary>
class EFDocMsgFormat_XmlSubmitReturnV3_BR extends EFDocMsgFormat_XmlSubmitReturn_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>eFiscalDocVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the version of the NF-e manual that this class adheres to.
    /// </summary>
    /// <returns>The version of the NF-e manual from <c>EFiscalDocVersion_BR</c> enumerator.</returns>
    public EFiscalDocVersion_BR eFiscalDocVersion()
    {
        return EFiscalDocVersion_BR::V31;
    }
]]></Source>
			</Method>
			<Method>
				<Name>read</Name>
				<Source><![CDATA[
    /// <summary>
    /// Reads the XML returned by the submit return web service.
    /// </summary>
    /// <param name="_stream">The stream from which to read the XML.</param>
    /// <param name="_returnInfo">A parm object to fill the return information.</param>
    public void read(IEFDocMsgStream_BR _stream, EFiscalDocumentReturnInfo_BR _returnInfo)
    {
        #ExportFiscalDocument_BR

        str         content;
        str         batchReturnCode;
        str         returnCode;
        str         returnCodeDescription;
        str         returnReceptNumber;
        str         returnProtocolNumber;
        str         returnDateTime;
        str         returnMessage;

        utcdatetime dateTime;

        content = conpeek(_stream.read(),1);
        xmlDoc.loadXml(content);

        batchReturnCode         = this.getNodeContent('/x:retConsReciNFe/x:cStat');
        returnReceptNumber      = this.getNodeContent('/x:retConsReciNFe/x:nRec');

        returnMessage           = this.existsNode('/x:retConsReciNFe/x:xMsg') ? this.getNodeContent('/x:retConsReciNFe/x:xMsg') : '';

        if (batchReturnCode == #BatchProcessed)
        {
            returnCode              = this.getNodeContent('/x:retConsReciNFe/x:protNFe/x:infProt/x:cStat');
            returnCodeDescription   = this.getNodeContent('/x:retConsReciNFe/x:protNFe/x:infProt/x:xMotivo');
            returnDateTime          = this.getNodeContent('/x:retConsReciNFe/x:protNFe/x:infProt/x:dhRecbto');

            if (returnCode == #Authorized)
            {
                returnProtocolNumber    = this.getNodeContent('/x:retConsReciNFe/x:protNFe/x:infProt/x:nProt');
            }
        }
        else
        {
            returnCode              = this.getNodeContent('/x:retConsReciNFe/x:cStat');
            returnCodeDescription   = this.getNodeContent('/x:retConsReciNFe/x:xMotivo');
        }

        dateTime = this.getUtcDateTimeFromDateStrWithTZD(returnDateTime);

        _returnInfo.parmReturnCodeStatus(returnCode);
        _returnInfo.parmReturnCodeDescription(returnCodeDescription);
        _returnInfo.parmReceiptNumber(returnReceptNumber);
        _returnInfo.parmProtocolNumber(returnProtocolNumber);
        _returnInfo.parmProcessDateTime(dateTime);
        _returnInfo.parmReturnMessage(returnMessage);
    }
]]></Source>
			</Method>
			<Method>
				<Name>version</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the version of the XML that this class creates and interprets.
    /// </summary>
    /// <returns>The XML version in string format.</returns>
    public str version()
    {
        return #EFDocLayoutVersion310;
    }
]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a <c>EFDocMsgFormat_XmlSubmitReturnV3_BR</c> object.
    /// </summary>
    /// <returns>A <c>EFDocMsgFormat_XmlSubmitReturnV3_BR</c> object.</returns>
    public static EFDocMsgFormat_XmlSubmitReturnV3_BR construct()
    {
        return new EFDocMsgFormat_XmlSubmitReturnV3_BR();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>