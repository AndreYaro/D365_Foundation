<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetOverviewBEUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class AssetOverviewBEUIBuilder extends SrsReportDataContractUIBuilder
{
    private DialogField reportYear;
    private DialogField fiscalCalendarIDf;
    private DialogField fiscalYearName;
    private DialogField reportYearName;
    AssetOverviewContractBE contract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        super();

        contract = this.dataContractObject();
        
        reportYear = this.bindInfo().getDialogField(contract, methodStr(AssetOverviewContractBE, parmReportYear));
        fiscalCalendarIDf = this.bindInfo().getDialogField(contract, methodStr(AssetOverviewContractBE, parmFiscalCalendar));
        fiscalYearName = this.bindInfo().getDialogField(contract, methodStr(AssetOverviewContractBE, parmFiscalYearName));

        reportYearName = this.bindInfo().getDialogField(contract, methodStr(AssetOverviewContractBE, parmReportYearName));
        reportYearName.visible(false);

        if (FeatureStateProvider::isFeatureEnabled(AssetOverviewBEUseFiscalCalendarFeature::instance()))
        {
            fiscalCalendarIDf.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(AssetOverviewBEUIBuilder, fiscalCalendarLookup), this);
            fiscalCalendarIDf.registerOverrideMethod(methodStr(FormStringControl, modified), methodStr(AssetOverviewBEUIBuilder, fiscalCalendarIDModified), this);

            fiscalYearName.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(AssetOverviewBEUIBuilder, fiscalCalendarYearLookup), this);

            reportYear.visible(false);
        }
        else
        {
            fiscalCalendarIDf.visible(false);
            fiscalYearName.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    public void postRun()
    {
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalCalendarIDModified</Name>
				<Source><![CDATA[
    public boolean fiscalCalendarIDModified(FormStringControl _formStringControl)
    {
        fiscalYearName = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetOverviewContractBE, parmFiscalYearName));
        fiscalYearName.value('');

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalCalendarLookup</Name>
				<Source><![CDATA[
    private void fiscalCalendarLookup(FormStringControl _formStringControl)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(FiscalCalendar), _formStringControl);
        sysTableLookup.addLookupfield(fieldnum(FiscalCalendar, CalendarId));

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(FiscalCalendar));
        sysTableLookup.parmQuery(query);
        sysTableLookup.parmUseLookupValue(false);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalCalendarYearLookup</Name>
				<Source><![CDATA[
    private void fiscalCalendarYearLookup(FormStringControl _formStringControl)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(AssetOverviewBEFiscalCalendarYearTmp), _formStringControl);
        sysTableLookup.addLookupfield(fieldnum(AssetOverviewBEFiscalCalendarYearTmp, FiscalYearName));
        
        fiscalCalendarIDf = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(AssetOverviewContractBE, parmFiscalCalendar));

        AssetOverviewBEFiscalCalendarYearTmp assetOverviewBEFiscalCalendarYearTmp;
        FiscalCalendar fiscalCalendar;
        FiscalCalendarYear fiscalCalendarYear;

        while select Name, RecId from fiscalCalendarYear
            exists join fiscalCalendar
            where fiscalCalendar.CalendarId == fiscalCalendarIDf.value()
            && fiscalCalendar.RecId == fiscalCalendarYear.FiscalCalendar
        {
            assetOverviewBEFiscalCalendarYearTmp.clear();
            assetOverviewBEFiscalCalendarYearTmp.FiscalYearName = fiscalCalendarYear.Name;
            assetOverviewBEFiscalCalendarYearTmp.insert();
        }

        Query query = new Query();
        query.addDataSource(tableNum(AssetOverviewBEFiscalCalendarYearTmp));

        sysTableLookup.parmQuery(query);
        sysTableLookup.parmTmpBuffer(assetOverviewBEFiscalCalendarYearTmp);
        sysTableLookup.parmUseLookupValue(false);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>