<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DefaultDimensionDefaultingStrategyMerge</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>DefaultDimensionDefaultingStrategyMerge</c> class is responsible for defaulting dimension based on merge.
/// </summary>
public final class DefaultDimensionDefaultingStrategyMerge extends DefaultDimensionDefaultingStrategy
{
    private DimensionMerge dimensionMerge;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createDefaultDimension</Name>
				<Source><![CDATA[
    [Replaceable]
    protected DimensionDefault createDefaultDimension()
    {
        var dimensionEnumerator = defaultDimensionCollection.getEnumerator();

        DimensionDefault targetDefaultDimension = defaultDimensionCollection.getNextDimensionSource(dimensionEnumerator).getDefaultDimension();

        while (dimensionEnumerator.moveNext())
        {
            DefaultDimensionDefaultingDimensionISource mergeDefaultDimension = dimensionEnumerator.current();

            targetDefaultDimension = dimensionMerge.merge(targetDefaultDimension, mergeDefaultDimension.getDefaultDimension());
        }

        return targetDefaultDimension;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newMerge</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>DefaultDimensionDefaultingStrategyMerge</c> class.
    /// </summary>
    /// <param name = "_dimensionMerge">A <c>DimensionMerge</c> object used when default dimension is merged.</param>
    /// <returns>An instance of the <c>DefaultDimensionDefaultingStrategyMerge</c> class.</returns>
    [Hookable(false)]
    public static DefaultDimensionDefaultingStrategyMerge newMerge(DimensionMerge _dimensionMerge)
    {
        if (_dimensionMerge == null)
        {
            throw error(strFmt("@SYS22828",funcName()));
        }

        DefaultDimensionDefaultingStrategyMerge strategy = DefaultDimensionDefaultingStrategyMerge::construct();

        strategy.dimensionMerge = _dimensionMerge;

        return strategy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static DefaultDimensionDefaultingStrategyMerge construct()
    {
        return new DefaultDimensionDefaultingStrategyMerge();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>