<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Vendor'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class VendTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        VendTable vendTable;
        select crosscompany DataAreaId from vendTable where vendTable.RecId == _parentRecId;

        return vendTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        VendTable vendTable;
        select crosscompany AccountNum from vendTable where vendTable.RecId == _parentRecId;

        return vendTable.AccountNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        VendTable vendTable;
        select RecId from vendTable where vendTable.AccountNum == _naturalKey;

        return vendTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        VendTable vendTable;
        select crosscompany * from vendTable where vendTable.RecId == _parentRecId;

        return vendTable.name();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        VendTable vendTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        VendTable vendTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from vendTableCrossCompany
            where vendTableCrossCompany.RecId == _association.RefRecId;

        if (vendTableCrossCompany.Recid)
        {
            changecompany (vendTableCrossCompany.DataAreaId)
            {
                select firstonly vendTable where vendTable.RecId == _association.RefRecId;
            }
        }

        _args.record(vendTable);
        return menuitemdisplaystr(VendTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(VendTable), callerControl, true,
                        VendorDataInteractorFactory::useExistingOrCreateResolver(callerControl));

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(VendTable));

        oSysTableLookup.addLookupfield(fieldnum(VendTable, AccountNum));
        oSysTableLookup.addLookupfield(fieldnum(VendTable, Party));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(VendTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Vendor);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>