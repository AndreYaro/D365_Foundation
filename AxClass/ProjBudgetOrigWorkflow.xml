<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjBudgetOrigWorkflow</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProjBudgetOrigWorkflow</c> class is used to perform actions on submitting project original budget
/// </summary>
public class ProjBudgetOrigWorkflow
{
    RecId                           recId;
    ProjId                          rootProjId;
    WorkflowComment                 wfComment;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogOk</Name>
				<Source><![CDATA[
    private boolean dialogOk(Args _args)
    {
        WorkflowSubmitDialog    workflowSubmitDialog;
        boolean                 dialogOk;

        // Launch workflow comments dialog
        workflowSubmitDialog = WorkflowSubmitDialog::construct(_args.caller().getActiveWorkflowConfiguration());
        workflowSubmitDialog.run();

        if (workflowSubmitDialog.parmIsClosedOK())
        {
            wfComment = workflowSubmitDialog.parmWorkflowComment();
            dialogOk = true;
        }

        return dialogOk;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRecId</Name>
				<Source><![CDATA[
    public RecId parmRecId(RecId _recId = recId)
    {
        recId = _recId;
        return recId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRootProjId</Name>
				<Source><![CDATA[
    public ProjId parmRootProjId(ProjId _rootProjId = rootProjId)
    {
        rootProjId = _rootProjId;
        return rootProjId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[
    private void submit(Args _args)
    {
        ProjBudget projBudget;

        if (_args.dataset() == tableNum(ProjBudget))
        {
            // Query to pick up latest buffer from server
            projBudget =  ProjBudget::find(_args.record().RecId, true);

            // Check the state
            if (projBudget.BudgetWorkflowStatus == ProjBudgetWorkflowStatus::Created)
            {
                // Verify if total project budget value exceeds contract value
                if (ProjBudgetOrigWorkflow::canSubmit(rootProjId))
                {
                    if (this.dialogOk(_args))
                    {
                        ttsbegin;
                        projBudget.BudgetWorkflowStatus = ProjBudgetWorkflowStatus::Submitted;
                        projBudget.doUpdate();
                        ttscommit;

                        Workflow::activateFromWorkflowType(workFlowTypeStr(ProjBudgetOrig), recId, wfComment, NoYes::No);
                    }
                }
                else
                {
                    throw error("@SYS326805");
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canSubmit</Name>
				<Source><![CDATA[
public static boolean canSubmit(ProjId _rootProjId)
    {
        ProjBudget  projBudget;

        select firstonly projBudget
        where projBudget.RootProjId == _rootProjId;

        return ProjBudgetManager::checkProfitablityForFixedPrice(_rootProjId) && projBudget.canSubmitToWorkflow();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static ProjBudgetOrigWorkflow construct()
    {
        return new ProjBudgetOrigWorkflow();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        ProjBudgetOrigWorkflow  projBudgetOrigWF = ProjBudgetOrigWorkflow::construct();
        ProjBudget              projBudget;

        projBudgetOrigWF.parmRecId(_args.record().RecId);

        if (_args.record().TableId == tablenum(ProjBudget))
        {
            projBudget = _args.record();
            projBudgetOrigWF.parmRootProjId(projBudget.RootProjId);

            projBudgetOrigWF.submit(_args);

            ProjBudgetOrigWorkflow::refreshCaller(_args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshCaller</Name>
				<Source><![CDATA[
    public static void refreshCaller(Args _args)
    {
        FormRun                 callerForm;
        FormDataSource          projBudgetDS;

        if (_args.caller())
        {
            if (SysDictClass::isEqualOrSuperclass(classIdGet(_args.caller()), classNum(FormRun)))
            {
                callerForm = _args.caller();

                callerForm.updateWorkflowControls();

                projBudgetDS = FormDataUtil::getFormDataSource(_args.record());
                if (projBudgetDS)
                {
                    projBudgetDS.executeQuery();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>