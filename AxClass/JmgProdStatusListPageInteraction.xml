<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgProdStatusListPageInteraction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>JmgProdStatusListPageInteraction</c> class is used to provide the running logic for the
/// Production status list page to the Web and to the client.
/// </summary>
class JmgProdStatusListPageInteraction extends ListPageInteraction
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeQuery</Name>
				<Source><![CDATA[
    public void initializeQuery(Query _query)
    {
        QueryBuildDataSource    jmgJobTableQbds, prodRouteJobQbds, wrkCtrResGroupQbds;

        super(_query);

            jmgJobTableQbds = _query.dataSourceName(queryDataSourceStr(JmgProdStatusListPageQuery, JmgJobTable));
        wrkCtrResGroupQbds = _query.dataSourceTable(tableNum(WrkCtrResourceAndGroupView));

        if (JmgProdParameters::find().StampLevel == JmgStampLevel::Job)
        {
            prodRouteJobQbds = jmgJobTableQbds.addDataSource(tableNum(ProdRouteJob));

            prodRouteJobQbds.joinMode(JoinMode::ExistsJoin);

            prodRouteJobQbds.addLink(fieldNum(JmgJobTable, JobId),fieldNum(ProdRouteJob, JobId));
            prodRouteJobQbds.addLink(fieldNum(JmgJobTable, OprNum),fieldNum(ProdRouteJob, OprNum));
            prodRouteJobQbds.addLink(fieldNum(JmgJobTable, ModuleRefid),fieldNum(ProdRouteJob, ProdId));
            prodRouteJobQbds.addLink(fieldNum(JmgJobTable, Priority),fieldNum(ProdRouteJob, OprPriority));

            prodRouteJobQbds.addRange(fieldNum(ProdRouteJob, JobType)).value(queryRange(RouteJobType::Setup, RouteJobType::Process));
            prodRouteJobQbds.addRange(fieldNum(ProdRouteJob, JobControl)).value(queryValue(NoYes::Yes));
        }

        wrkCtrResGroupQbds.addRange(fieldNum(WrkCtrResourceAndGroupView, ValidFrom)).value(
                strFmt('(%1.%2 <= %3.%4)',
                        wrkCtrResGroupQbds.name(),
                        fieldStr(WrkCtrResourceAndGroupView, ValidFrom),
                        jmgJobTableQbds.name(),
                        fieldStr(JmgJobTable, FromDate))
        );

        wrkCtrResGroupQbds.addRange(fieldNum(WrkCtrResourceAndGroupView, ValidTo)).value(
                strFmt('(%1.%2 >= %3.%4)',
                        wrkCtrResGroupQbds.name(),
                        fieldStr(WrkCtrResourceAndGroupView, ValidTo),
                        jmgJobTableQbds.name(),
                        fieldStr(JmgJobTable, FromDate))
        );
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables controls on the production status list page, when the selection on the page is changed.
    /// </summary>
    public void selectionChanged()
    {
        ProdTable   prodTable;
        JmgJobTable jmgJobTable;
        boolean     isFormulaEnabled;

        super();
        prodTable   = this.listPage().activeRecord(querydatasourcestr(JmgProdStatusListPageQuery, ProdTable)) as ProdTable;
        jmgJobTable = this.listPage().activeRecord(querydatasourcestr(JmgProdStatusListPageQuery, JmgJobTable)) as JmgJobTable;
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, MaintainGroup),prodTable.RecId);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, processGroup),prodTable.RecId);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, ResourceGroup),prodTable.RecId);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, ProdJobDetailsGroup),prodTable.RecId);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, RegistrationsGroup),prodTable.RecId);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, FeedbackGroup),prodTable.RecId);
        if (jmgJobTable)
        {
            this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, JmgProdCancelFinished), jmgJobTable.jobStatus() == JmgJobStatus::Completed);
        }

        isFormulaEnabled = prodTable.inventTable().isFormulaAllowed();
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, PmfFormulaMenuItem),isFormulaEnabled);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, ProdBOM),!isFormulaEnabled);
        this.listPage().actionPaneControlEnabled(formcontrolstr(JmgProdStatusListPage, PmfConsOrder),(prodTable.PmfConsOrdId != ''));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>