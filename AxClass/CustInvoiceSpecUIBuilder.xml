<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceSpecUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustInvoiceSpecUIBuilder</c> class builds the UI for the <c>CustInvoiceSpec</c> SSRS report parameters.
/// </summary>
class CustInvoiceSpecUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField billingClassificationField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>billingClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a multi-select Billing Classification lookup.
    /// </summary>
    public void billingClassificationLookup()
    {
        Query query = new Query(queryStr(CustBillingClassificationLookup));
        TableId multiSelectTableNum = tableNum(CustBillingClassification);
        container selectedFields = [multiSelectTableNum, fieldName2id(multiSelectTableNum, fieldStr(CustBillingClassification, BillingClassification))];

        SysLookupMultiSelectCtrl::constructWithQuery(this.dialog().dialogForm().formRun(), billingClassificationField.control(), query, false, selectedFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the <c>CustInvoiceSpec</c> report parameter form that is based on billing classification.
    /// </summary>
    public void postBuild()
    {
        boolean isBillingClassEnabled;
        CustInvoiceSpecContract custInvoiceSpecContract;
        DialogField inclTransWithNoBillingClassField;

        // initializing data contract object
        custInvoiceSpecContract = this.dataContractObject() as CustInvoiceSpecContract;

        // Get the parameter controls from dialog
        billingClassificationField = this.bindInfo().getDialogField(custInvoiceSpecContract, methodStr(CustInvoiceSpecContract, parmBillingClassification));
        inclTransWithNoBillingClassField = this.bindInfo().getDialogField(custInvoiceSpecContract, methodStr(CustInvoiceSpecContract, parmInclTransWithNoBillingClass));

        // Controls the visibility of report parameters
        if (billingClassificationField && inclTransWithNoBillingClassField)
        {
            isBillingClassEnabled = CustParameters::isBillingClassificationEnabled();
            billingClassificationField.visible(isBillingClassEnabled);
            inclTransWithNoBillingClassField.visible(isBillingClassEnabled);
            billingClassificationField.lookupButton(FormLookupButton::Always);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implements the functionality that is supposed to execute after run.
    /// </summary>
    public void postRun()
    {
        if (CustParameters::isBillingClassificationEnabled())
        {
            this.billingClassificationLookup();
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>