<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSReleaseLoadToWarehouseContract</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class gets the parameters used to define which loads get released to the warehouses.
/// </summary>
[DataContractAttribute]
public class WHSReleaseLoadToWarehouseContract implements SysOperationInitializable
{
    private str					packedReleaseQuery;
    private Query				query;
    private CustCreditMaxCheck	creditMaxCheck;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setLoadIdRange</Name>
				<Source><![CDATA[
	/// <summary>
    /// Sets the load ID range on the query.
    /// </summary>
    /// <param name = "_loadIdRange">The selected load IDs that need to be queried.</param>
    public void setLoadIdRange(WHSLoadIdRange _loadIdRange)
    {
		if (!query)
        {
            query = this.newQuery();
		}
        
        SysQuery::findOrCreateRange(query.dataSourceTable(tableNum(WHSLoadTable)), fieldNum(WHSLoadTable, LoadId)).value(_loadIdRange);
        this.parmQuery(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCreditMaxCheck</Name>
				<Source><![CDATA[
	[DataMemberAttribute,
     SysOperationControlVisibilityAttribute(false)]
    public CustCreditMaxCheck parmCreditMaxCheck(CustCreditMaxCheck _creditMaxCheck = creditMaxCheck)
    {
        creditMaxCheck = _creditMaxCheck;
        return creditMaxCheck;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newQuery</Name>
				<Source><![CDATA[
	private Query newQuery()
    {
        return new Query(queryStr(WHSReleaseLoadToWarehouse));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the contract.
    /// </summary>
    public void initialize()
    {
        this.parmQuery(this.newQuery());
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPackedReleaseQuery</Name>
				<Source><![CDATA[
    [
        DataMemberAttribute,
        AifQueryTypeAttribute('_packedReleaseQuery', queryStr(WHSReleaseLoadToWarehouse))
    ]
    public str parmPackedReleaseQuery(str _packedReleaseQuery = packedReleaseQuery)
    {
        packedReleaseQuery = _packedReleaseQuery;

        return packedReleaseQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQuery</Name>
				<Source><![CDATA[
    public Query parmQuery(Query _query = query)
    {
        if (prmisDefault(_query)
		&&	!query)
        {
            query = new Query(SysOperationHelper::base64Decode(packedReleaseQuery));
        }
        else
        {
            packedReleaseQuery  = SysOperationHelper::base64Encode(_query.pack());
            query               = _query;
        }

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>