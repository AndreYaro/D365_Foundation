<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResProductInformation</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>EcoResProductInformation</c> class selects the correct form to show depending on the content of the related field.
/// </summary>
public class EcoResProductInformation
{
    EcoResProductInformationParameters productInformationParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method selects and runs the correct form to show depending if the Item number field is set.
    /// </summary>
    /// <param name="_args">An <c>Args</c> object containining the passed arguments.</param>
    public static void main(Args _args)
    {
        EcoResProductInformation productInformation = EcoResProductInformation::newFromArgs(_args);
        productInformation.runMenuItem();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates an object of the <c>EcoResProductInformation</c> class.
    /// </summary>
    /// <param name = "_args">An <c>Args</c> object containing the arguments used for instantiating the object.</param>
    /// <returns>
    /// The instantiated <c>EcoResProductInformation</c> object.
    /// </returns>
    protected static EcoResProductInformation newFromArgs(Args _args)
    {
        EcoResProductInformation productInformation = new EcoResProductInformation();

        if (EcoResProductInformation::isEmptyItemReference(_args))
        {
            productInformation.productInformationParameters = EcoResProductInformation::selectReleasedProductMenuItem();
        }
        else
        {
            productInformation.productInformationParameters = EcoResProductInformation::selectProductInformationDialog(_args);
        }

        return productInformation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isEmptyItemReference</Name>
				<Source><![CDATA[
	private static boolean isEmptyItemReference(Args _args)
    {
        return (!_args.record() && !_args.lookupRecord() && (!_args.lookupTable() || _args.lookupValue() == ''));
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectReleasedProductMenuItem</Name>
				<Source><![CDATA[
    private static EcoResProductInformationParameters selectReleasedProductMenuItem()
    {
        // If user selects "View details" option on the empty "Item number" field - Released products form should open.
        MenuFunction menuItem = new MenuFunction(menuItemDisplayStr(EcoResProductDetailsExtended), MenuItemType::Display);
        menuItem.formViewOption(FormViewOption::Grid);
        return EcoResProductInformationParameters::newFromMenuItemAndArgs(menuItem, null);
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectProductInformationDialog</Name>
				<Source><![CDATA[
    private static EcoResProductInformationParameters selectProductInformationDialog(Args _args)
    {
        InventDim callerInventDim = InventDimLocator::findAnyFromArgsCaller(_args);
        EcoResProductInformationController controller = EcoResProductInformationController::newFromArgs(_args, callerInventDim);

        Args args = new Args();
        args.parmObject(controller);

        MenuFunction menuItem = new MenuFunction(menuItemDisplayStr(EcoResProductInformationDialog), MenuItemType::Display);

        return EcoResProductInformationParameters::newFromMenuItemAndArgs(menuItem, args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>runMenuItem</Name>
				<Source><![CDATA[
    private void runMenuItem()
    {
        MenuFunction menuItem = productInformationParameters.menuItem;
        if (menuItem)
        {
            menuItem.run(productInformationParameters.args);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>