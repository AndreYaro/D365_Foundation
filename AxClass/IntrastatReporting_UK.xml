<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntrastatReporting_UK</Name>
	<SourceCode>
		<Declaration><![CDATA[
#ISOCountryRegionCodes
/// <summary>
/// The <c>IntrastatReporting_UK</c> class is responsible for generating file and report for Intrastat United Kingdom.
/// </summary>
[IntrastatCountryRegionAttribute(#isoGB)]
class IntrastatReporting_UK extends IntrastatReporting
{
    IntrastatDirection direction;

    DialogField dialogDirection;
    DialogField dialogCorrectionOnly;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        correctionOnly,
        direction
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        dialog = super();

        dialogCorrectionOnly = dialog.addFieldValue(extendedTypeStr(NoYesId), correctionOnly, "@SYS3966", "@SYS83434");
        dialogDirection = dialog.addFieldValue(enumStr(IntrastatDirection), direction);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = super();

        correctionOnly = dialogCorrectionOnly.value();
        direction = dialogDirection.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList, super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>prepareQuery</Name>
				<Source><![CDATA[
    protected QueryBuildDataSource prepareQuery()
    {
        QueryBuildDataSource dataSource = super();

        if (dataSource)
        {
            dataSource.addRange(fieldNum(Intrastat, Direction)).value(queryValue(direction));

            if (correctionOnly)
            {
                dataSource.addRange(fieldNum(Intrastat, Correct)).value(queryValue(NoYes::Yes));
            }
        }

        return dataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer version = RunBase::getVersion(packedClass);
        container packedSuper;

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedSuper] = packedClass;
                if (packedSuper)
                {
                    return super(packedSuper);
                }
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret = super(_calledFrom);

        if (generateFile)
        {
            if (!TaxIntegrationUtils::isMultipleTaxIdEnabledForIntrastat())
            {
                if ((IntrastatParameters::find().AgentName && !IntrastatParameters::find().AgentVATNum) ||
                    (!IntrastatParameters::find().AgentName && IntrastatParameters::find().AgentVATNum))
                {
                    ret = checkFailed("@SYS65250");
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDirection</Name>
				<Source><![CDATA[
    public IntrastatDirection parmDirection(IntrastatDirection _direction = direction)
    {
        direction = _direction;
        return direction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRun</Name>
				<Source><![CDATA[
    protected void preRun()
    {
        super();
        reportDirection = IntrastatReporting::direction2ReportDirection(direction);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>