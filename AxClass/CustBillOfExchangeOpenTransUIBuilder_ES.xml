<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBillOfExchangeOpenTransUIBuilder_ES</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Builds and handles the UI dialog for the <c>CustBillOfExchangeOpenTrans_ES</c> report.
/// </summary>
public class CustBillOfExchangeOpenTransUIBuilder_ES extends SrsReportDataContractUIBuilder
{
    CustBillOfExchOpenTransContract_ES contract;

    DialogField dialogFromBillId;
    DialogField dialogToBillId;
    DialogField dialogCustBillOfExchangeOpenTrans_ES;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Adds controls to the <c>Dialog</c> object.
    /// </summary>
    public void build()
    {
        FormBuildGroupControl grp = this.dialog().curFormGroup();
        grp.columns(2);

        super();

        dialogCustBillOfExchangeOpenTrans_ES = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(CustBillOfExchOpenTransContract_ES, parmGroupBy));
        dialogFromBillId = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(CustBillOfExchOpenTransContract_ES, parmFromBillId));
        dialogFromBillId.label(strfmt("@SYS86132", "@SYS71453"));
        dialogToBillId = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(CustBillOfExchOpenTransContract_ES, parmToBillId));
        dialogToBillId.label(strfmt("@SYS92684", "@SYS71453"));
        this.showControls(this.dialog(), dialogCustBillOfExchangeOpenTrans_ES.value());
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogSelectCtrl</Name>
				<Source><![CDATA[
    public void dialogSelectCtrl()
    {
        super();
        dialog.allowUpdateOnSelectCtrl(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>groupByModified</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Handles the modified event for the <c>GroupBy</c> dialog field.
    /// </summary>
    /// <param name="_control">
    ///     The control behind the <c>GroupBy</c> dialog field.
    /// </param>
    /// <returns>
    ///     Always returns <c>true</c>.
    /// </returns>
    public boolean groupByModified(FormComboBoxControl _control)
    {
        if (_control.modified())
        {
            CustBillOfExchangeOpenTrans_ES custBillOfExchangeOpenTrans_ES;
            this.showControls(this.dialog(), str2enum(custBillOfExchangeOpenTrans_ES,_control.valueStr()));
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Registers a method override in order to capture dialog events.
    /// </summary>
    public void postRun()
    {
        super();

        this.dialog().dialogForm().formRun().controlMethodOverload(false);
        dialogCustBillOfExchangeOpenTrans_ES.registerOverrideMethod(methodstr(FormComboBoxControl, modified), methodstr(CustBillOfExchangeOpenTransUIBuilder_ES, groupByModified), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>groupBillVisibility</Name>
				<Source><![CDATA[
    private void groupBillVisibility(boolean _isVisible, Dialog _dialogLocal)
    {
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS71453").visible(_isVisible);
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS80095").visible(_isVisible);
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS12371").visible(_isVisible);
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS25587").visible(_isVisible);
    }

]]></Source>
			</Method>
			<Method>
				<Name>groupOtherVisibility</Name>
				<Source><![CDATA[
    private void groupOtherVisibility(boolean _isVisible, Dialog _dialogLocal)
    {
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS14588").visible(_isVisible);
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS92690").visible(_isVisible);
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS21530").visible(_isVisible);
    }

]]></Source>
			</Method>
			<Method>
				<Name>showControls</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Shows or hides dialog controls.
    /// </summary>
    /// <param name="_dialogLocal">
    ///     A dialog object.
    /// </param>
    /// <param name="_groupBy">
    ///     The drop down value that is selected.
    /// </param>
    private void showControls(Dialog _dialogLocal, CustBillOfExchangeOpenTrans_ES _groupBy)
    {
        SRSReportHelper::getDialogGroupByCaption(_dialogLocal,"@SYS7149").visible(true);

        if (_groupBy == CustBillOfExchangeOpenTrans_ES::BillId)
        {
            this.groupBillVisibility(true, _dialogLocal);
            this.groupOtherVisibility(false, _dialogLocal);
        }
        else
        {
            this.groupBillVisibility(false, _dialogLocal);
            this.groupOtherVisibility(true, _dialogLocal);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>