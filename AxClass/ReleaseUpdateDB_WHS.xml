<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReleaseUpdateDB_WHS</Name>
	<SourceCode>
		<Declaration><![CDATA[
public abstract class ReleaseUpdateDB_WHS extends ReleaseUpdateDB
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateLocationDirectiveLineSequences</Name>
				<Source><![CDATA[
    /// <summary>
    /// Resequence location directive lines that have the same sequence number for the same location directive.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void updateLocationDirectiveLineSequences()
    {
        WHSLocDirLine locationDirectiveLine;
        WHSLocDirLine duplicate;

        // Find duplicates
        while select RefRecId from locationDirectiveLine
                group by RefRecId
                exists join duplicate
                    where duplicate.RefRecId == locationDirectiveLine.RefRecId
                    &&    duplicate.LocSeqNum == locationDirectiveLine.LocSeqNum
                    &&    duplicate.RecId != locationDirectiveLine.RecId
        {
            this.updateSingleLocDirectiveLinesSequence(locationDirectiveLine.RefRecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateSingleLocDirectiveLinesSequence</Name>
				<Source><![CDATA[
    /// <summary>
    /// Resequence the numbers for all <c>WHSLocDirLine</c> lines with the same location directive id.
    /// </summary>
    /// <param name = "_id">The lines location directive id</param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void updateSingleLocDirectiveLinesSequence(RefRecId _id)
    {
        ttsBegin;
    
        WHSLocDirLine locationDirectiveLine;
    
        locationDirectiveLine.disableCache(true); // if caching is not disabled then the while select query will only fetch the first record
        this.callInsertUpdateRelatedSkipMethods(locationDirectiveLine);
    
        WHSLocSeqNum seqCounter = 1;
    
        // Resequence lines for location directive from 1
        while select forUpdate LocSeqNum from locationDirectiveLine
                order by LocSeqNum, RecId
                where locationDirectiveLine.RefRecId == _id
        {
            locationDirectiveLine.LocSeqNum = seqCounter;
            locationDirectiveLine.doUpdate();
            seqCounter++;
        }
    
        ttsCommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>