<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceSimulatedPost</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics.AX.Platform.Extensibility;

/// <summary>
/// The <c>VendInvoiceSimulatedPost</c> is the base class for simulated post of vendor invoices.
/// </summary>
[ExportInterface()]
internal abstract class VendInvoiceSimulatedPost
{
    protected VendInvoiceInfoTable vendInvoiceInfoTable;
    protected PurchFormLetter_Invoice purchFormLetterInvoice;
    protected VendInvoiceSimulatedPostResult vendInvoiceSimulatedPostResult;
    protected boolean originalParmIsSimulatedPosting;
    protected str originalParmInfologPrefix;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmVendInvoiceInfoTable</Name>
				<Source><![CDATA[
    internal VendInvoiceInfoTable parmVendInvoiceInfoTable(VendInvoiceInfoTable _vendInvoiceInfoTable = this.vendInvoiceInfoTable)
    {
        if (!prmIsDefault(_vendInvoiceInfoTable))
        {
            this.vendInvoiceInfoTable = _vendInvoiceInfoTable;
        }

        return this.vendInvoiceInfoTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPurchFormLetterInvoice</Name>
				<Source><![CDATA[
    internal PurchFormLetter_Invoice parmPurchFormLetterInvoice(PurchFormLetter_Invoice _purchFormLetterInvoice = this.purchFormLetterInvoice)
    {
        if (!prmIsDefault(_purchFormLetterInvoice))
        {
            this.purchFormLetterInvoice = _purchFormLetterInvoice;
        }

        return this.purchFormLetterInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runSimulatedPost</Name>
				<Source><![CDATA[
    internal abstract void runSimulatedPost()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>simulatePosting</Name>
				<Source><![CDATA[
    internal VendInvoiceSimulatedPostResult simulatePosting()
    {
        vendInvoiceSimulatedPostResult = VendInvoiceSimulatedPostResult::construct();
        vendInvoiceSimulatedPostResult.parmInvoiceIdentifier(vendInvoiceInfoTable.RecId);
        this.setSimulatePostingParams(true, "@AccountsPayable:SimulatePosting");
        int infologLineStart = infologLine();
        Microsoft.Dynamics.Ax.Xpp.ErrorException errorException;

        try
        {
            this.runSimulatedPost();

            VendInstrumentation::logTelemetry(
                VendInvoicePostTaskConstants::VendInvoiceSimulatedPost,
                this.toString(),
                funcName(),
                VendInvoicePostTaskConstants::VendInvoiceSimulatePostingResult_Passed,
                strFmt("%1", vendInvoiceInfoTable.RecId));
        }
        catch (errorException)
        {
            vendInvoiceSimulatedPostResult.parmResult(VendInvoiceSimulatedPostStatus::Failed);

            VendInstrumentation::logTelemetry(
                VendInvoicePostTaskConstants::VendInvoiceSimulatedPost,
                this.toString(),
                funcName(),
                VendInvoicePostTaskConstants::VendInvoiceSimulatePostingResult_Failed,
                strFmt("%1", errorException.ToString()));
        }
        finally
        {
            this.setSimulatePostingParams(originalParmIsSimulatedPosting, originalParmInfologPrefix);
            this.getSimulationResult(infologLineStart);
            infolog.clear(infologLineStart);
        }

        return vendInvoiceSimulatedPostResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSimulationResult</Name>
				<Source><![CDATA[
    private void getSimulationResult(int _infologLineStart)
    {
        VendInvoiceSimulatedPostResult vendInvoiceSimulatedPostResultLocal = VendInvoiceSimulatedPostResultParser::construct().getErrorsAndWarningsFromInfolog(
            _infologLineStart,
            infologLine());

        vendInvoiceSimulatedPostResult.parmResult(vendInvoiceSimulatedPostResultLocal.parmResult());
        vendInvoiceSimulatedPostResult.appendMessageList(vendInvoiceSimulatedPostResultLocal.parmMessageList());
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSimulatePostingParams</Name>
				<Source><![CDATA[
    private void setSimulatePostingParams(boolean _isSimulatePosting, str _infologPrefix)
    {
        purchFormLetterInvoice.parmIsSimulatePosting(_isSimulatePosting);
        purchFormLetterInvoice.parmInfologPrefix(_infologPrefix);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>