<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendRequestNVJWorkflowAction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>VendRequestNVJWorkflowAction</c> class provides services to the submit command action of the
///    new vendor request.
/// </summary>
class VendRequestNVJWorkflowAction
{
    VendRequest                 vendRequest;
    WorkflowVersionTable        workflowConfigurationTable;
    WorkflowComment             workflowComment;
    boolean                     submit;
    WorkflowWorkItemTable       workflowWorkItemTable;
    userId                      userId;
    menuItemName                menuItemName;
    workflowTypeName            workflowTemplateName;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialogOk</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Opens a dialog box to submit a vendor request.
    /// </summary>
    /// <returns>
    ///    true if the dialog box was closed by pressing the Ok button; otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean dialogOk()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the workflow objects.
    /// </summary>
    /// <param name="_documentRecord">
    ///    The workflow document record that is used in the workflow.
    /// </param>
    /// <param name="_menuItemName">
    ///    The submit command that is used for the new vendor request workflow.
    /// </param>
    /// <param name="_workflowConfigurationTable">
    ///    The workflow configuration table that is used for the workflow.
    /// </param>
    /// <param name="_workflowWorkItemTable">
    ///    The workflow item table that is used for the workflow.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void init(   Common                      _documentRecord,
                        menuItemName                _menuItemName,
                        WorkflowVersionTable        _workflowConfigurationTable,
                        WorkflowWorkItemTable       _workflowWorkItemTable
                        )
    {
        this.parmVendRequest(_documentRecord as VendRequest);
        //TODO: Valid menu item needed.
        //this.parmSubmit(_menuItemName == menuitemactionstr(VendRequestNVJSubmit));
        this.parmMenuItemName(_menuItemName);

        this.parmWorkflowConfigurationTable(_workflowConfigurationTable);
        this.parmWorkflowWorkItemtable(_workflowWorkItemTable);
        this.parmWorkflowTemplateName(this.parmWorkflowConfigurationTable().workflowTable().TemplateName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMenuItemName</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public menuItemName parmMenuItemName(menuItemName _menuItemName = menuItemName)
    {
        menuItemName = _menuItemName;

        return menuItemName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSubmit</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean parmSubmit(boolean _submit = submit)
    {
        submit = _submit;
        return submit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendRequest</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or Sets the vendor request record.
    /// </summary>
    /// <param name="_vendRequest">
    ///    The <c>VendRequest</c> record to set.
    /// </param>
    /// <returns>
    ///    The current <c>VendRequest</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public VendRequest parmVendRequest(VendRequest _vendRequest = vendRequest)
    {
        vendRequest = _vendRequest;
        return vendRequest;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowComment</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or Sets the <c>WorkflowComment</c> record
    /// </summary>
    /// <param name="_workflowComment">
    ///    The <c>WorkflowComment</c> record to set.
    /// </param>
    /// <returns>
    ///    The current <c>WorkflowComment</c> record
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowComment parmWorkflowComment(WorkflowComment _workflowComment = workflowComment)
    {
        workflowComment = _workflowComment;
        return workflowComment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowConfigurationTable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or Sets the <c>WorkflowConfigurationTable</c> record.
    /// </summary>
    /// <param name="_workflowConfigurationTable">
    ///    The <c>WorkflowConfigurationTable</c> record to set.
    /// </param>
    /// <returns>
    ///    The current <c>WorkflowConfigurationTable</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowVersionTable parmWorkflowConfigurationTable(WorkflowVersionTable _workflowConfigurationTable = workflowConfigurationTable)
    {
        workflowConfigurationTable = _workflowConfigurationTable;
        return workflowConfigurationTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowTemplateName</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public workflowTypeName parmWorkflowTemplateName(workflowTypeName _workflowTemplateName = workflowTemplateName)
    {
        workflowTemplateName = _workflowTemplateName;
        return workflowTemplateName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWorkflowWorkItemtable</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public WorkflowWorkItemTable parmWorkflowWorkItemtable(WorkflowWorkItemTable _workflowWorkItemTable = workflowWorkItemTable)
    {
        workflowWorkItemTable = _workflowWorkItemTable;
        return workflowWorkItemTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>submit</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Activates the new vendor company request workflow template.The submit method is called from the
    ///    main static method.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void submit()
    {
        boolean             checkVendor = false;
        VendRequestCompany  vendRequestCompany;

        Workflow::activateFromWorkflowType(
            this.parmWorkflowTemplateName(),
            vendRequest.RecId,
            this.parmWorkflowComment(),
            NoYes::No);

        vendRequestCompany = VendRequestCompany::findRecId(vendRequest.RecId, true);
        ttsbegin;
        vendRequestCompany.VendRequestWorkflowState = VendRequestWorkflowState::Submitted;
        vendRequestCompany.VendRequestCompanyStatus = VendRequestCompanyStatus::RequestSubmitted;
        vendRequestCompany.update();
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canSubmit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the vendor request can be submitted by the user.
    /// </summary>
    /// <param name="_vendRequest">
    /// The <c>VendRequest</c> record.
    /// </param>
    /// <returns>
    /// true if the vendor request can be submitted by the user; otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static boolean canSubmit(VendRequest _vendRequest)
    {
        boolean                             canSubmit = _vendRequest.canSubmitToWorkflow(workFlowTypeStr(VendRequestNVJTemplate));
        VendRequestProfile                  vendRequestProfile;
        VendRequestCompany                  vendRequestCompany;
        VendFieldMetadataGroupType          fieldMetadataGroupType;
        VendRequestCategory                 vendRequestCategory;
        LogisticsPostalAddress              primaryPostalAddress;
        DirPartyContactInfoView             contactInfo;
        VendRequestBusinessJustification    businessJustification;

        vendRequestCompany = _vendRequest as VendRequestCompany;
        businessJustification = VendRequestBusinessJustification::find(vendRequestCompany.BusinessJustification);

        if (vendRequestCompany.NoOnlineAccess == NoYes::No)
        {
            // Has online access
            select firstonly RecId from contactInfo
                where contactInfo.Party == vendRequestCompany.ContactParty
                && contactInfo.Type == LogisticsElectronicAddressMethodType::Email;

            if (contactInfo.RecId == 0)
            {
                // You must enter an email address for the contact person in the '%1' fast tab
                canSubmit = checkFailed(strFmt("@SYS343974", "@SYS304356"));
            }
        }

        primaryPostalAddress = DirParty::primaryPostalAddress(_vendRequest.VendParty);

        if (primaryPostalAddress.RecId == 0)
        {
            // You must enter a primary postal address in the '%1' fast tab
            canSubmit = checkFailed(strFmt("@SYS343975", "@SYS304353"));
        }

        if (vendRequestCompany.IsExtension)
        {
            fieldMetadataGroupType = VendFieldMetadataGroupType::VendRequestExtensionJustification;
        }
        else
        {
            fieldMetadataGroupType = VendFieldMetadataGroupType::VendRequestNewVendorJustification;
        }

        vendRequestProfile = VendRequestProfileHelper::findByDataArea(fieldMetadataGroupType);

        canSubmit = VendRequestProfileHelper::checkMandatoryFields(vendRequestProfile.RecId, businessJustification) && canSubmit;

        if (vendRequestProfile.ShowAcceptAgreement == NoYes::Yes && vendRequestCompany.AcceptChecked == NoYes::No)
        {
            // Terms and conditions must be accepted
            canSubmit = checkFailed("@SYS135278");
        }

        if (vendRequestProfile.RequireCategory == NoYes::Yes)
        {
            select firstonly RecId from vendRequestCategory where vendRequestCategory.VendRequest == _vendRequest.RecId;

            // At least one category must be selected before the request is submitted.
            if (vendRequestCategory.RecId == 0)
            {
                // Category must be selected.
                canSubmit = checkFailed("@SYS135560");
            }
        }

        if (vendRequestProfile.CheckEmbargoCountries == NoYes::Yes)
        {
            if (VendRequestDuplicateCheck::matchEmbargoCountryRegion(vendRequestCompany))
            {
                // Requested vendor in country/region is located within the system embargo country/region list. The request cannot be submitted at this time.
                canSubmit = checkFailed("@SYS306766");
            }
        }

        return canSubmit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static VendRequestNVJWorkflowAction construct()
    {
        return new VendRequestNVJWorkflowAction();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        VendRequestNVJWorkflowAction    vendRequestWorkflow;
        VendRequest                     vendRequest;

        vendRequest = args.record() as VendRequest;
        vendRequestWorkflow = VendRequestNVJWorkflowAction::construct();
        vendRequestWorkflow.init(args.record(), args.menuItemName(), null, null);

        if (VendRequestNVJWorkflowAction::canSubmit(vendRequest))
        {
            if (vendRequestWorkflow.dialogOk())
            {
                if (vendRequestWorkflow.parmSubmit())
                {
                    vendRequestWorkflow.submit();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>