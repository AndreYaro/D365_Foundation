<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocPurchRFQDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider for purchase request for quote.
/// </summary>
public class TaxableDocPurchRFQDataProvider extends TaxableDocumentDataProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to load detail data for taxable document. And setup the row definitions.
    /// </summary>
    /// <returns>
    /// The query to load detail data for taxable document.
    /// </returns>
    /// <remarks>
    /// The row definitions must be setup here. It defines the mapping between row and data source,
    /// the child/parent relations between rows, and the row data provider class.
    /// </remarks>
    public Query buildQuery()
    {
        Query                   query;
        QueryBuildDataSource    purchRFQTableDataSource;
        QueryBuildDataSource    purchRFQLineDataSource;
        QueryBuildDataSource    markupTransLineDataSource;

        query = new Query();

        purchRFQTableDataSource = query.addDataSource(tableNum(PurchRFQTable));
        purchRFQTableDataSource.addRange(fieldNum(PurchRFQTable, RecId)).value(queryValue(rootTable.RecId));

        purchRFQLineDataSource = purchRFQTableDataSource.addDataSource(tableNum(PurchRFQLine));
        purchRFQLineDataSource.joinMode(JoinMode::OuterJoin);
        purchRFQLineDataSource.relations(true);

        markupTransLineDataSource = purchRFQLineDataSource.addDataSource(tableNum(MarkupTrans));
        markupTransLineDataSource.joinMode(JoinMode::OuterJoin);
        markupTransLineDataSource.relations(true);

        rowDefinitions.empty();
        this.registerRow(purchRFQTableDataSource.id(), 0, TaxEngineIntegrationConstants::Header, classStr(TaxableDocPurchRFQTableRowDataProvider));
        this.registerRow(purchRFQLineDataSource.id(), purchRFQTableDataSource.id(), TaxEngineModelFieldsConstants::Lines, classStr(TaxableDocPurchRFQLineRowDataProvider));
        this.registerRow(markupTransLineDataSource.id(), purchRFQLineDataSource.id(), TaxEngineModelFieldsConstants::Sublines, classStr(TaxableDocMarkupTransRowDataProvider));

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>