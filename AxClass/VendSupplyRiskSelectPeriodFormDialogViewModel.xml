<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSupplyRiskSelectPeriodFormDialogViewModel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>VendSupplyRiskSelectPeriodFormDialogViewModel</c> is a form view model for selecting a date interval code, from date and to date.
/// </summary>
internal final class VendSupplyRiskSelectPeriodFormDialogViewModel
{
    protected VendSupplyRiskSelectPeriodModel   selectPeriodModel;
    private FormStringControl                   dateCodeControl;
    private FormDateControl                     fromDateControl;
    private FormDateControl                     toDateControl;

    private const str                           blankMaxDateLabel = '\t';  // A tab will display a max data as blank

    private VendSupplyRiskISelectPeriodStorage  periodStorage;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>redrawFormDateControl</Name>
				<Source><![CDATA[
    /// <summary>
    /// Redraws FormDateControl controls.
    /// </summary>
    /// <param name = "control">The instance of the <c>FormDateControl</c> to redraw.</param>
    /// <remarks>
    /// This method is used to remove "Warning" icon displayed inside the <c>FormDateControl</c> control. "Warning" icon appears when
    /// user sets invalid date period. After he corrects the period, <c>FormDateControl</c> control should be drawn without this icon.
    /// </remarks>
    private static void redrawFormDateControl(FormDateControl control)
    {
        // Re-setting value enforces redrawing control.
        control.dateValue(control.dateValue());
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDateCode</Name>
				<Source><![CDATA[
    internal void parmDateCode(DateCode _dateCode)
    {
        dateCodeControl.text(_dateCode);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFromDate</Name>
				<Source><![CDATA[
    internal void parmFromDate(FromDate _fromDate)
    {
        fromDateControl.dateValue(_fromDate);
        // If the date period was previously invalid, "Warning" icon is displayed inside one of the date period controls.
        // When user sets new period properly, "Warning" icon should disappear (only toDateControl field has to be redrawn, because
        // value of fromDateControl has just been set).
        VendSupplyRiskSelectPeriodFormDialogViewModel::redrawFormDateControl(toDateControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmToDate</Name>
				<Source><![CDATA[
    internal void parmToDate(ToDate _toDate)
    {
        toDateControl.dateValue(_toDate);
        // If the date period was previously invalid, "Warning" icon is displayed inside one of the date period controls.
        // When user sets new period properly, "Warning" icon should disappear (only fromDateControl field has to be redrawn, because
        // value of toDateControl has just been set).
        VendSupplyRiskSelectPeriodFormDialogViewModel::redrawFormDateControl(fromDateControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dateCodeOnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the OnModified event for the <c>DateCode</c>.
    /// </summary>
    /// <param name = "_sender">The instance of the <c>FormControl</c> rasing the event.</param>
    /// <param name = "_e">The instance of the <c>FormControlEventArgs</c>.</param>
    private void dateCodeOnModified(FormControl _sender, FormControlEventArgs _e)
    {
        selectPeriodModel.parmDateCode(_sender.valueStr());
    }

]]></Source>
			</Method>
			<Method>
				<Name>fromDateOnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the OnModified event for the <c>FromDate</c>.
    /// </summary>
    /// <param name = "_sender">The instance of the <c>FormControl</c> rasing the event.</param>
    /// <param name = "_e">The instance of the <c>FormControlEventArgs</c>.</param>
    private void fromDateOnModified(FormControl _sender, FormControlEventArgs _e)
    {
        var sender = _sender as FormDateControl;

        selectPeriodModel.parmFromDate(sender.dateValue());
    }

]]></Source>
			</Method>
			<Method>
				<Name>toDateOnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the OnModified event for the <c>ToDate</c>.
    /// </summary>
    /// <param name = "_sender">The instance of the <c>FormControl</c> raising the event.</param>
    /// <param name = "_e">The instance of the <c>FormControlEventArgs</c>.</param>
    private void toDateOnModified(FormControl _sender, FormControlEventArgs _e)
    {
        var sender = _sender as FormDateControl;

        selectPeriodModel.parmToDate(sender.dateValue());
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateOnClicked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the form.
    /// </summary>
    /// <param name = "_sender">The instance of the <c>FormControl</c> raising the event.</param>
    /// <param name = "_e">The instance of the <c>FormControlEventArgs</c>.</param>
    internal void validateOnClicked(FormControl _sender, FormControlEventArgs _e)
    {
        this.saveState();
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveState</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the current state.
    /// </summary>
    private void saveState()
    {
        selectPeriodModel.validate();
        periodStorage.setPeriod(selectPeriodModel.getState());
    }

]]></Source>
			</Method>
			<Method>
				<Name>dispose</Name>
				<Source><![CDATA[
    /// <summary>
    /// Disposes the resources that are held by the view model.
    /// </summary>
    internal void dispose()
    {
        dateCodeControl.OnModified -= eventhandler(this.dateCodeOnModified);
        fromDateControl.OnModified -= eventhandler(this.fromDateOnModified);
        toDateControl.OnModified   -= eventhandler(this.toDateOnModified);

        selectPeriodModel.notifyDateCodeChange-=eventhandler(this.parmDateCode);
        selectPeriodModel.notifyFromDateChange-=eventhandler(this.parmFromDate);
        selectPeriodModel.notifyToDateChange-=eventhandler(this.parmToDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the view model.
    /// </summary>
    /// <param name = "_periodStorage">The implementation of the <c>VendSupplyRiskISelectPeriodStorage</c> interface used to provide the initial state and notify about state changes.</param>
    /// <param name = "_dateCodeControl">The instance of the <c>FormStringControl</c> class implementing the <c>DateCode</c>.</param>
    /// <param name = "_fromDateControl">The instance of the <c>FormDateControl</c> class implementing the <c>FromDate</c>.</param>
    /// <param name = "_toDateControl">The instance of the <c>FormDateControl</c> class implementing the <c>ToDate</c>.</param>
    private void initialize(
        VendSupplyRiskISelectPeriodStorage  _periodStorage,
        FormStringControl                   _dateCodeControl,
        FormDateControl                     _fromDateControl,
        FormDateControl                     _toDateControl)
    {
        periodStorage = _periodStorage;

        dateCodeControl = _dateCodeControl;
        fromDateControl = _fromDateControl;
        toDateControl   = _toDateControl;
        dateCodeControl.OnModified += eventhandler(this.dateCodeOnModified);
        fromDateControl.OnModified += eventhandler(this.fromDateOnModified);
        toDateControl.OnModified   += eventhandler(this.toDateOnModified);

        toDateControl.maxDateLabel(blankMaxDateLabel);

        selectPeriodModel = VendSupplyRiskSelectPeriodModel::construct();
        selectPeriodModel.notifyDateCodeChange+=eventhandler(this.parmDateCode);
        selectPeriodModel.notifyFromDateChange+=eventhandler(this.parmFromDate);
        selectPeriodModel.notifyToDateChange+=eventhandler(this.parmToDate);

        if (periodStorage)
        {
            selectPeriodModel.setState(periodStorage.getPeriod());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>VendSupplyRiskSelectPeriodFormViewModel</c> class.
    /// </summary>
    /// <param name = "_formCaller">The instance of the caller form.</param>
    /// <param name = "_dateCodeControl">The instance of the <c>FormStringControl</c> implementing the <c>DateCode</c>.</param>
    /// <param name = "_fromDateControl">The instance of the <c>FormDateControl</c> implementing the <c>FromDate</c>.</param>
    /// <param name = "_toDateControl">The instance of the <c>FormDateControl</c> implementing the <c>ToDate</c>.</param>
    /// <returns>An instance of <c>VendSupplyRiskSelectPeriodFormViewModel</c> class.</returns>
    internal static VendSupplyRiskSelectPeriodFormDialogViewModel construct(
        VendSupplyRiskISelectPeriodStorage  _formCaller,
        FormStringControl                   _dateCodeControl,
        FormDateControl                     _fromDateControl,
        FormDateControl                     _toDateControl)
    {
        VendSupplyRiskSelectPeriodFormDialogViewModel viewModel = new VendSupplyRiskSelectPeriodFormDialogViewModel();

        viewModel.initialize(_formCaller,
                             _dateCodeControl,
                             _fromDateControl,
                             _toDateControl);

        return viewModel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>periodToDateInterval</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a date interval representating the period.
    /// </summary>
    /// <returns>A <c>DateInterval</c> representation of the period.</returns>
    internal DateInterval periodToDateInterval()
    {
        return periodStorage.getPeriod().asDateInterval();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>