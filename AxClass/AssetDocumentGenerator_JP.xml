<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentGenerator_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Asset document transaction line generator.
/// </summary>
class AssetDocumentGenerator_JP
{
    FormDataSource                formDataSource;
    AssetDocumentEntry_JP         assetDocumentEntry;
    AssetDocumentTable_JP         assetDocumentTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create specific asset document transaction line.
    /// </summary>
    protected void createTrans()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>generate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates asset document transaction line and refresh to UI.
    /// </summary>
    public void generate()
    {
        // create transaction
        this.createTrans();
        // refresh UI
        this.refresh();
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes asset document generator instance based on imported <c>Args</c> instance.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public void init(Args _args)
    {
        FormDataSource                  formDataSourceLoc        = FormDataUtil::getFormDataSource(_args.record());

        this.parmFormDataSource(formDataSourceLoc);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAssetDocumentEntry</Name>
				<Source><![CDATA[
    public AssetDocumentEntry_JP parmAssetDocumentEntry(AssetDocumentEntry_JP _assetDocumentEntry = assetDocumentEntry)
    {
        assetDocumentEntry = _assetDocumentEntry;
        return assetDocumentEntry;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAssetDocumentTable</Name>
				<Source><![CDATA[
    public AssetDocumentTable_JP parmAssetDocumentTable(AssetDocumentTable_JP _assetDocumentTable = assetDocumentTable)
    {
        assetDocumentTable = _assetDocumentTable;
        return assetDocumentTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFormDataSource</Name>
				<Source><![CDATA[
    public FormDataSource parmFormDataSource(FormDataSource _formDataSource = formDataSource)
    {
        formDataSource = _formDataSource;
        return formDataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>refresh</Name>
				<Source><![CDATA[
    /// <summary>
    /// Refreshes asset document transaction line  to UI.
    /// </summary>
    protected void refresh()
    {
        if (formDataSource != null)
        {
            formDataSource.executeQuery();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the asset document and generator.
    /// </summary>
    /// <returns>
    /// True if it passed validation; otherwise, false.
    /// </returns>
    protected boolean validate()
    {
        boolean ret = true;

        if (!this.parmAssetDocumentEntry() ||
            !this.parmAssetDocumentTable() ||
            this.parmFormDataSource() == null)
        {
            ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constucts an asset document generator.
    /// </summary>
    /// <param name="_documentGenerateType">
    /// The asset document generation type.
    /// </param>
    /// <returns>
    /// An asset document generator instance.
    /// </returns>
    public static AssetDocumentGenerator_JP construct(AssetDocumentGenerateType_JP _documentGenerateType)
    {
        AssetDocumentGenerator_JP transGenerator;

        transGenerator = SysExtensionAppClassFactory::getClassFromSysAttribute(
            classStr(AssetDocumentGenerator_JP),
            new AssetDocumentGenerateTypeAttribute_JP(_documentGenerateType));

        if (transGenerator == null)
        {
            transGenerator = new AssetDocumentGenerator_JP();
        }

        return transGenerator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        AssetDocumentGenerator_JP transGenerator  = AssetDocumentGenerator_JP::construct(args.parmEnum());

        transGenerator.init(args);
        if (transGenerator.validate())
        {
            transGenerator.generate();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>