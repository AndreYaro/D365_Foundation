<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetAdvRuleExe_PreTaxDedCarryFwd_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The asset advanced rule executor for pre-tax deduction carrying foward
/// </summary>
[AssetAdvancedRuleTypeAttribute_JP(enumStr(AssetAdvancedRuleType_PreTaxDeduction_JP), AssetAdvancedRuleType_PreTaxDeduction_JP::DepOverDownCarryForward)]
class AssetAdvRuleExe_PreTaxDedCarryFwd_JP extends AssetAdvRuleExecutor_JP
{
    AssetAdvRuleContext_PreTaxDedCarryFwd_JP    assetAdvRuleContext_PreTaxDedCarryFwd;

    #define.DepOverUnderTypeItemId(1)
    #define.DepTypeItemId(2)
    #define.CarryForwardYearNumberItemId(3)
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addLog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds log
    /// </summary>
    protected void addLog()
    {
        AssetAdvRuleLogger_JP   logger;

        logger = this.getLogger();

        if (logger)
        {
            logger.save();
        }
    }
]]></Source>
			</Method>
			<Method>
				<Name>doRuleAction</Name>
				<Source><![CDATA[
    public boolean doRuleAction()
    {
        int             carryForwardYearNumber;
        AssetTransDate  newValidTo;
        AssetTransDate  lastDayOfCreatedYear;

        carryForwardYearNumber  = assetAdvRuleType.getItemValue(#CarryForwardYearNumberItemId);
        lastDayOfCreatedYear    = assetAdvRuleContext_PreTaxDedCarryFwd.parmLastDayOfCreatedYear();
        newValidTo              = mkDate(dayOfMth(lastDayOfCreatedYear), mthOfYr(lastDayOfCreatedYear), year(lastDayOfCreatedYear) + carryForwardYearNumber);

        // The new newValidTo may exceed the dateMax() in AX and then become dateNull(). Adjust to make it dateMax() in this case.
        // Note:
        //   1. compare with dateNull to identify this case is not safe. Instead, compare with lastDayOfCreatedYear is better.
        //   2. When running in automation, it is possible that newValidTo is bigger than dateMax(), and become dateNull() later when saving into DB.
        if (newValidTo < lastDayOfCreatedYear || newValidTo > dateMax())
        {
            newValidTo = dateMax();
        }

        assetAdvRuleContext_PreTaxDedCarryFwd.parmValidTo(newValidTo);

        this.addLog();

        return true;
    }
]]></Source>
			</Method>
			<Method>
				<Name>initForConstruct</Name>
				<Source><![CDATA[
    protected void initForConstruct(AssetAdvRuleType_JP _assetAdvRuleType, AssetAdvRuleContext_JP _assetAdvRuleContext)
    {
        super(_assetAdvRuleType, _assetAdvRuleContext);

        assetAdvRuleContext_PreTaxDedCarryFwd = _assetAdvRuleContext as AssetAdvRuleContext_PreTaxDedCarryFwd_JP;
    }
]]></Source>
			</Method>
			<Method>
				<Name>isRuleMatched</Name>
				<Source><![CDATA[
    public boolean isRuleMatched()
    {
        boolean ret = true;

        // Check the depreciation type
        if (assetAdvRuleType.getItemValue(#DepTypeItemId) != assetAdvRuleContext_PreTaxDedCarryFwd.parmDepType())
        {
            ret = false;
        }

        if (ret && assetAdvRuleType.getItemValue(#DepOverUnderTypeItemId) != assetAdvRuleContext_PreTaxDedCarryFwd.parmOverUnderType())
        {
            ret = false;
        }

        return ret;
    }
]]></Source>
			</Method>
			<Method>
				<Name>calcValidToDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates valid to date
    /// </summary>
    /// <param name="_depType">
    /// The depreciation type
    /// </param>
    /// <param name="_overUnderType">
    /// The over under type
    /// </param>
    /// <param name="_lastDayOfCreatedYear">
    /// The last day of created year
    /// </param>
    /// <param name="_assetDepPreTaxDedProcessContext">
    /// The context
    /// </param>
    /// <returns>
    /// Returns valid to date
    /// </returns>
    public static AssetTransDate calcValidToDate(
        AssetDepType_JP                         _depType,
        AssetDepPreTaxDeductionOverUnderType_JP _overUnderType,
        AssetTransDate                          _lastDayOfCreatedYear,
        AssetDepPreTaxDedProcessContext_JP      _assetDepPreTaxDedProcessContext = null)
    {
        AssetAdvRuleContext_PreTaxDedCarryFwd_JP    assetAdvRuleContext_PreTaxDedCarryFwd;

        assetAdvRuleContext_PreTaxDedCarryFwd = new AssetAdvRuleContext_PreTaxDedCarryFwd_JP();
        assetAdvRuleContext_PreTaxDedCarryFwd.parmDepType(_depType);
        assetAdvRuleContext_PreTaxDedCarryFwd.parmOverUnderType(_overUnderType);
        assetAdvRuleContext_PreTaxDedCarryFwd.parmLastDayOfCreatedYear(_lastDayOfCreatedYear);
        assetAdvRuleContext_PreTaxDedCarryFwd.parmValidTo(_lastDayOfCreatedYear);
        assetAdvRuleContext_PreTaxDedCarryFwd.parmAssetDepPreTaxDedProcessContext(_assetDepPreTaxDedProcessContext);

        AssetAdvRuleExecutor_JP::executeRuleSet(
            assetAdvRuleContext_PreTaxDedCarryFwd,
            AssetAdvancedRuleGroupType_JP::AssetDepPreTaxDeductionCarryForward);

        return assetAdvRuleContext_PreTaxDedCarryFwd.parmValidTo();
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>