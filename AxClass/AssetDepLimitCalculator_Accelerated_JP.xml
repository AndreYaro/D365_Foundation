<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepLimitCalculator_Accelerated_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The Japan fixed asset accelerated depreciation limit calculator.
/// </summary>
[AssetDepMethodAttribute_JP(AssetDepreciationMethod::Accelerated_JP)]
class AssetDepLimitCalculator_Accelerated_JP extends AssetDepLimitCalculator_JP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getValidAcceleratedDocumentEntry</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get container of <c>AssetDocumentEntry_JP</c> for current asset book.
    ///</summary>
    protected container getValidAcceleratedDocumentEntry()
    {
        container                           conEntry                 = conNull();
        AssetDocumentEntry_JP               assetDocumentEntry_JP;
        AssetDocumentTable_JP               assetDocumentTable_JP;
        AssetDocumentAcceleratedDepTable_JP assetDocumentAcceleratedDepTable;
        AssetTransDate                      acceleratedDepStartDate;

        acceleratedDepStartDate = assetBook.LastDepreciationDateAccelerated_JP?
            assetBook.LastDepreciationDateAccelerated_JP + 1 : assetBook.DepreciationStartDate;

        while select *
            from assetDocumentEntry_JP
            order by assetDocumentAcceleratedDepTable.ValidTo
            where assetDocumentEntry_JP.AssetId  == assetBook.AssetId
                && assetDocumentEntry_JP.BookId  == assetBook.BookId
            join RecId from assetDocumentTable_JP
            where assetDocumentTable_JP.MainType == AssetDocumentMainType_JP::AssetAcceleratedDepreciation
                && assetDocumentTable_JP.RecId   == assetDocumentEntry_JP.AssetDocumentTable_JP
            join ValidFrom, ValidTo from assetDocumentAcceleratedDepTable
            where assetDocumentAcceleratedDepTable.AssetDocumentTable_JP == assetDocumentTable_JP.RecId
                && (assetDocumentAcceleratedDepTable.Status == AssetAcceleratedDepDocumentStatus_JP::Confirmed
                    || assetDocumentAcceleratedDepTable.Status == AssetAcceleratedDepDocumentStatus_JP::PartiallyPosted
                    || assetDocumentAcceleratedDepTable.Status == AssetAcceleratedDepDocumentStatus_JP::Posted)
                && assetDocumentAcceleratedDepTable.ValidTo >= acceleratedDepStartDate
        {
            conEntry += [[assetDocumentAcceleratedDepTable.ValidFrom, assetDocumentAcceleratedDepTable.ValidTo, assetDocumentEntry_JP]];
        }

        return conEntry;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAssetTableMethod</Name>
				<Source><![CDATA[
    protected void initAssetTableMethod()
    {
        AssetTableMethod_JP     assetTableMethod_JP;
        AssetDocumentEntry_JP   acceleratedDepDocumentEntry;
        container               acceleratedEntryContainer;

        assetTableMethod = AssetTableMethod::construct(assetBook, dateMax(), assetBudgetModelId, false, false, false, true);

        Debug::assert(assetTableMethod IS AssetTableMethod_JP);
        assetTableMethod_JP = assetTableMethod AS AssetTableMethod_JP;

        acceleratedEntryContainer = this.getValidAcceleratedDocumentEntry();

        acceleratedDepDocumentEntry = AssetAcceleratedDepCalcManager_JP::GetValidAcceleratedDocumentEntry(
            acceleratedEntryContainer,
            (assetBook.LastDepreciationDateAccelerated_JP?
                assetBook.LastDepreciationDateAccelerated_JP + 1 : assetBook.DepreciationStartDate));

        assetTableMethod_JP.parmAssetDocumentEntry_JP(acceleratedDepDocumentEntry);
        assetTableMethod_JP.parmAcceleratedEntryContainer_JP(acceleratedEntryContainer);

        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>