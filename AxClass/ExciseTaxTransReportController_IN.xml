<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ExciseTaxTransReportController_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is a controller class for <c>ExciseTaxTransReport_IN</c> report.
/// </summary>
class ExciseTaxTransReportController_IN extends SRSReportRunController implements BatchRetryable
{
    TmpExciseInquiryTable_IN tmpExciseInquiryTable;
    RecId                    tmpTableDataRecId;
    container                fieldValues;
    str                      headerNote;
    str                      footerNote;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getParameterNameByIndex</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getting selected parameter value based on index.
    /// </summary>
    /// <param name="_parameterIndex">
    /// <c>_parameterIndex</c> to which it is assigned to.
    /// </param>
    /// <returns>
    /// Parameter name based on index.
    /// </returns>
    public str getParameterNameByIndex(int _parameterIndex)
    {
        #define.FieldName('ParamSort')

        return (#FieldName + int2str(_parameterIndex));
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
        protected void preRunModifyContract()
    {
        this.setParameters(this.parmArgs().record(), this.parmReportContract().parmRdpContract());
    }

]]></Source>
			</Method>
			<Method>
				<Name>setLabelsCaption</Name>
				<Source><![CDATA[
    /// <summary>
    /// This will set field captions.
    /// </summary>
    /// <param name="_fieldValues">
    /// <c>_fieldValues</c> to which it is assigned to.
    /// </param>
    /// <param name="_headerNote">
    /// <c>_headerNote</c> to which it is assigned to.
    /// </param>
    /// <param name="_footerNote">
    /// <c>_footerNote</c> to which it is assigned to.
    /// </param>
    public void setLabelsCaption(
        container _fieldValues,
        str _headerNote,
        str _footerNote)
    {
        fieldValues = _fieldValues;
        headerNote  = _headerNote;
        footerNote  = _footerNote;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the class objects and processes the report business logic.
    /// </summary>
    /// <param name="_tmpExciseInquiryTable">
    /// An instance of <c>TmpExciseInquiryTable_IN</c> temporary table.
    /// </param>
    /// <param name="_contract">
    /// An instance of contract <c>SrsTmpTblMarshallerContract</c> class.
    /// </param>
    public void setParameters(
        TmpExciseInquiryTable_IN    _tmpExciseInquiryTable,
        SrsTmpTblMarshallerContract _contract)
    {
        SrsReportRunPermission      permission;
        int                         idx;
        SrsReportRdlDataContract    rdlContract;

        #define.HeaderNote('HeaderNote')
        #define.FooterNote('FooterNote')

        rdlContract = this.parmReportContract().parmRdlContract();

        rdlContract.setValue(#HeaderNote, headerNote);
        rdlContract.setvalue(#FooterNote, footerNote);

        for (idx = 1; idx <= conlen(fieldValues); idx++)
        {
           rdlContract.setvalue(this.getParameterNameByIndex(idx), conpeek(fieldValues, idx));
        }

        //storing the temp table data using SRSTmpTblMarshaller
        permission = new SrsReportRunPermission();
        permission.assert();

        tmpTableDataRecId = SRSTmpTblMarshaller::SendTmpTblToDataProvider(_tmpExciseInquiryTable);
        delete_from tmpExciseInquiryTable;
        CodeAccessPermission::revertAssert();

        _contract.parmTmpTableDataRecId(tmpTableDataRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args _args)
    {
        ExciseTaxTransReportController_IN controller  = _args.parmObject();

        if (controller != null)
        {
            controller.parmReportName(ssrsReportStr(ExciseTaxTransReport_IN, Report));
            controller.parmArgs(_args);
            controller.parmShowDialog(false);
            controller.startOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns true.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>