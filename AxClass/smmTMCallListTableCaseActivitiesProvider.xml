<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmTMCallListTableCaseActivitiesProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case activities
/// </summary>
[ExportMetadataAttribute('CaseIActivities', tableStr(smmTMCallListTable)), ExportAttribute('Dynamics.AX.Application.CaseIActivities')]
class smmTMCallListTableCaseActivitiesProvider implements CaseIActivities
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromCommon</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommon(smmActivities _activities, Common _table)
    {
        smmTMCallListTable smmTMCallListTable = _table;
        this.initFromCallListTargetParameters(_activities, smmTMCallListTable);
        return smmActivityParentType::CallList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parentRecId</Name>
				<Source><![CDATA[
    public RefRecId parentRecId(Common _table)
    {
        smmTMCallListTable smmTMCallListTable = _table;
        return smmTMCallListGroup::find(smmTMCallListTable.CallListId).RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCommonUsingTemplate</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommonUsingTemplate(smmActivities _activities, Common _table, smmActivities _templateActivity)
    {
        return smmActivityParentType::None;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateExternalMemo</Name>
				<Source><![CDATA[
    public smmOutlookMemo updateExternalMemo(RefRecId _parentLink)
    {
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldActivityBeCreated</Name>
				<Source><![CDATA[
    public CaseYesNoPrompt shouldActivityBeCreated(Common _table, boolean _isBatch)
    {
        if (_isBatch)
        {
            return (smmParametersTable::find().RunBaseBatchCreateActivity == smmRunBaseBatchCreateActivity::Yes) ? (CaseYesNoPrompt::Yes) : (CaseYesNoPrompt::No);
        }
        else
        {
            return this.shouldActivityBeCreatedSmmTMCallListTable(_table);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldActivityBeCreatedSmmTMCallListTable</Name>
				<Source><![CDATA[
    CaseYesNoPrompt shouldActivityBeCreatedSmmTMCallListTable(smmTMCallListTable _smmTMCallListTable)
    {
        CaseYesNoPrompt localRetVal = CaseYesNoPrompt::No;

        if (_smmTMCallListTable.CallStatus == smmTMCallStatus::CallBack)
        {
            localRetVal = smmActivities::shouldActivityBeCreatedGeneric(smmParametersTable::find().CallBackCreateActivity);
        }
        else
        {
            localRetVal = smmActivities::shouldActivityBeCreatedGeneric(smmParametersTable::find().TelemarketingTargetsCreateActivity);
        }

        return localRetVal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCallListTargetParameters</Name>
				<Source><![CDATA[
    private void initFromCallListTargetParameters(smmActivities _activities, smmTMCallListTable _callList)
    {
        smmParametersTable parameters = smmParametersTable::find();

        _activities.initValue((_callList.CallStatus == smmTMCallStatus::CallBack)?parameters.tmCallBackActivityCategory:parameters.TelemarketingTargetActivityCategory);

        // Get default values from CRM parameters
        // If it is call back
        if (_callList.CallStatus == smmTMCallStatus::CallBack)
        {
            _activities.PhaseId                = parameters.tmCallBackActivityPhase;
            _activities.TypeId                 = parameters.tmCallbackActivityType;
            _activities.Purpose                = parameters.tmCallBackActivityPurpose;
        }
        // If it is not call back
        else
        {
            _activities.PhaseId                = parameters.TelemarkeringTargetActivityPhase;
            _activities.TypeId                 = parameters.TelemarketingTargetActivityType;
            _activities.Purpose                = parameters.TelemarketingActivityPurpose;
        }

        // Copy values from call list to activity
        _activities.StartDateTime          = _callList.CallPlannedDateTime;
        _activities.EndDateTime            = _callList.CallPlannedDateTime;
        _activities.ReminderDateTime       = _callList.CallPlannedDateTime;
        _activities.ResponsibleWorker      = _callList.CallRespWorker;

        _activities.initFromWorkerTable(_activities.ResponsibleWorker);

        if (_activities.ReminderMinutes > 0)
        {
            _activities.ReminderActive     = NoYes::Yes;
        }
        else
        {
            _activities.ReminderActive     = NoYes::No;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getParentReferenceFieldNum</Name>
				<Source><![CDATA[
    public FieldId getParentReferenceFieldNum(TableId _tableId, smmActivityParentRefTableId _parentTableId)
    {
        FieldId fieldId;

        switch (_parentTableId)
        {
            case tableNum(DirPartyTable) :
                fieldId = fieldNum(smmTMCallListTable,Party);
                break;

            case tableNum(ContactPerson) :
                fieldId = fieldNum(smmTMCallListTable,ContactPersonId);
                break;
        }

        return fieldId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>