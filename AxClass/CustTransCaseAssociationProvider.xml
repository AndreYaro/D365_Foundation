<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTransCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'CustTrans'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class CustTransCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        CustTrans custTrans;
        select crosscompany AccountNum from custTrans where custTrans.RecId == _caseAssociation.RefRecId;

        CustTable custTable;
        select firstonly RecId from custTable where custTable.AccountNum == custTrans.AccountNum;
        if (custTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Customer, custTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        CustTrans custTrans = CustTrans::find(_parentRecId);

        return custTrans.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        CustTrans custTrans;
        select crosscompany Voucher from custTrans where custTrans.RecId == _parentRecId;

        return custTrans.Voucher;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        CustTrans custTrans;
        select RecId from custTrans where custTrans.Voucher == _naturalKey;

        return custTrans.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        CustTrans custTrans;
        select crosscompany Txt from custTrans where custTrans.RecId == _parentRecId;

        return custTrans.Txt;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        CustTrans custTrans;
        
        //We need this code in order not to lose the localization data in the opened form.
        CustTrans custTransCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from custTransCrossCompany
            where custTransCrossCompany.RecId == _association.RefRecId;

        if (custTransCrossCompany.Recid)
        {
            changecompany (custTransCrossCompany.DataAreaId)
            {
                select firstonly custTrans where custTrans.RecId == _association.RefRecId;
            }
        }

        _args.record(custTrans);
        return menuitemdisplaystr(CustTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(CustTrans), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(CustTrans));

        oSysTableLookup.addLookupfield(fieldnum(CustTrans, Voucher));
        oSysTableLookup.addLookupfield(fieldnum(CustTrans, AccountNum));
        oSysTableLookup.addLookupfield(fieldnum(CustTrans, TransDate));
        oSysTableLookup.addLookupfield(fieldnum(CustTrans, Txt));

        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(CustTrans))
        {
            _eventArgs.parmEntityType(CaseEntityType::CustTrans);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>