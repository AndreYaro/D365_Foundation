<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PCActiveVersionOpenOrdersQuotation</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class PCActiveVersionOpenOrdersQuotation extends RunBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>convertCallerQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Converts the query used on the calling form to a query that can be passed to the
    ///    <c>SalesQuotationListPage</c> form.
    /// </summary>
    /// <param name="_args">
    ///    The initial query that is passed by the class.
    /// </param>
    /// <returns>
    ///    A converted <c>Query</c> object.
    /// </returns>
    protected static InitialQueryParameter convertCallerQuery(Args _args)
    {
        ItemId                      itemId;
        PCProductConfigurationModel productConfigurationModel;
        QueryBuildDataSource        qbds;
        Query                       query;

        productConfigurationModel   = PCModelingLibrary::getCueDataSourceActiveRecord(_args.caller(), tablenum(PCProductConfigurationModel));
        itemId                      = productConfigurationModel.getActiveVersionItem();

        query       = new Query(querystr(SalesQuotationListPage));
        qbds        = query.dataSourceName(tablestr(SalesQuotationTable));
        qbds.addRange(fieldnum(SalesQuotationTable, QuotationStatus)).value(queryvalue(SalesQuotationStatus::Created));
        qbds        = SysQuery::findOrCreateDataSource(query, tablenum(SalesQuotationLine), tablenum(SalesQuotationTable));
        qbds.joinMode(JoinMode::InnerJoin);
        qbds.addLink(fieldnum(SalesQuotationTable, QuotationId), fieldnum(SalesQuotationLine, QuotationId));
        qbds.addRange(fieldnum(SalesQuotationLine, QuotationStatus)).value(queryvalue(SalesQuotationStatus::Created));
        qbds.addRange(fieldnum(SalesQuotationLine, Itemid)).value(queryvalue(itemId));

        _args.initialQuery().modeledQueryName('');
        _args.initialQuery().query(query);
        return _args.initialQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        //validate that we have a query to work with
        if (!_args.caller())
        {
            throw error(Error::missingParameter(_args.caller()));
        }

        //convert it
        _args.initialQuery(PCActiveVersionOpenOrdersQuotation::convertCallerQuery(_args));

        //run the target form
        if (_args.caller() is FormRun)
        {
            new MenuFunction(menuitemdisplaystr(SalesQuotationListPage), MenuItemType::Display).run(_args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>