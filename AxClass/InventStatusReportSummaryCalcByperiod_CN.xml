<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventStatusReportSummaryCalcByPeriod_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class calculates the data for InventStatus_CN report - Period
/// </summary>
public class InventStatusReportSummaryCalcByPeriod_CN extends InventStatusReportSummaryCalc_CN
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcReportData</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method inserts selected records in temporary table
    /// </summary>
    public void calcReportData()
    {
        InventValueReportTmpLine inventValueReportLine;
        InventStatusReportTmp_CN inventStatusReportTmp;

        void initReportData()
        {
            InventQty beginingQty;
            CostAmount beginingAmount;
            InventQty receviedQty;
            CostAmount receviedAmount;
            InventQty issuedQty;
            CostAmount issuedAmount;
            InventQty endQty;
            CostAmount endAmount;
            TransDate beginDate;
            str reportGroupKey;

            this.initInventDim(inventValueReportLine);
            reportGroupKey = inventValueReportLine.ResourceGroupId + inventValueReportLine.ResourceId + InventDim.inventDimId;

            if (setItemDim.in(reportGroupKey))
            {
                return;
            }
            else
            {
                setItemDim.add(reportGroupKey);
            }

            this.initInventStatusReport(inventStatusReportTmp, inventValueReportLine);

            // Init the begining qty and amount
            [beginingQty, beginingAmount] = this.calcBalanceQtyAmountByItemDim(InventValueReportLineType::BeginningBalance,
                inventValueReportLine.ResourceGroupId,
                inventValueReportLine.ResourceId,
                InventDim,
                InventDimParm);

            inventStatusReportTmp.BeginningQty = beginingQty;
            inventStatusReportTmp.BeginningAmount = beginingAmount;

            // Init the transaction qty and amount by transaction date for Recevived
            [receviedQty, receviedAmount] = this.calcBalanceQtyAmountByItemDim(InventValueReportLineType::Transaction,
                inventValueReportLine.ResourceGroupId,
                inventValueReportLine.ResourceId,
                InventDim,
                InventDimParm,
                false,
                InventTransType::Sales,
                true,
                false);

            inventStatusReportTmp.ReceivedQty = receviedQty;
            inventStatusReportTmp.ReceivedAmount = receviedAmount;

            // Init the transaction qty and amount by transaction date for Recevived
            [issuedQty, issuedAmount] = this.calcBalanceQtyAmountByItemDim(InventValueReportLineType::Transaction,
                inventValueReportLine.ResourceGroupId,
                inventValueReportLine.ResourceId,
                InventDim,
                InventDimParm,
                false,
                InventTransType::Sales,
                false,
                true);

            inventStatusReportTmp.IssuedQty = issuedQty;
            inventStatusReportTmp.IssuedAmount = issuedAmount;

            // Init the ending qty and amount by transaction date
            [endQty, endAmount] = this.calcBalanceQtyAmountByItemDim(InventValueReportLineType::Transaction,
                inventValueReportLine.ResourceGroupId,
                inventValueReportLine.ResourceId,
                InventDim,
                InventDimParm,
                false,
                InventTransType::Sales,
                false,
                false);

            inventStatusReportTmp.EndingQty = beginingQty + endQty;
            inventStatusReportTmp.EndingAmount = beginingAmount + endAmount;
            inventStatusReportTmp.insert();
            inventStatusReportTmp.clear();
        }

        super();

        inventValueReportLine.setConnection(userConnection);
        inventStatusReportTmp.setConnection(userConnection);

        while select inventValueReportLine
            #InventValueReportTmpLineGroupBy(inventValueReportLine)
            where inventValueReportLine.CreatedTransactionId == transactionID
        {
            initReportData();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventStatusReportSummaryCalcByperiod_CN construct(
        UserConnection _userConnection,
        InventStatusContract_CN _contract,
        createdTransactionId _transactionId)
    {
        InventStatusReportSummaryCalcByPeriod_CN inventStatusReportSummaryCalcByPeriod = new InventStatusReportSummaryCalcByPeriod_CN();
        inventStatusReportSummaryCalcByPeriod.parmUserConnection(_userConnection);
        inventStatusReportSummaryCalcByPeriod.parmContract(_contract);
        inventStatusReportSummaryCalcByPeriod.parmTransactionID(_transactionId);
        inventStatusReportSummaryCalcByPeriod.init();

        return inventStatusReportSummaryCalcByPeriod;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>