<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LeanCommandCreateCards</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LeanCommandCreateCards</c> class encapsulates the business logic around the
/// creation of circulating kanban cards for rules that use those.
/// </summary>
class LeanCommandCreateCards extends LeanCommandBase
{
    LeanRuleData    leanRuleData;
    boolean         printCards;
    int             newCardsCount;

    List            listCreatedCards;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>execute</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the circulating kanban cards.
    /// </summary>
    /// <returns>
    ///    true if the creation was successful; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    The method is executed in a transaction scope that is provided by the <c>LeanRulesEngine</c> class.
    /// </remarks>
    protected boolean execute()
    {
        KanbanRule kanbanRule = leanRuleData.parmKanbanRule();

        listCreatedCards = KanbanCard::createForRule(kanbanRule, newCardsCount);
        return listCreatedCards != null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>listCreatedCards</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves a list of card IDs of the created cards.
    /// </summary>
    /// <returns>
    ///    A list that has card IDs of the created cards.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public List listCreatedCards()
    {
        return listCreatedCards;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLeanRuleData</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public LeanRuleData parmLeanRuleData(LeanRuleData _leanRuleData = leanRuleData)
    {
        leanRuleData = _leanRuleData;

        return leanRuleData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNewCardsCount</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public int parmNewCardsCount(int _newCardsCount = newCardsCount)
    {
        newCardsCount = _newCardsCount;

        return newCardsCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPrintCards</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean parmPrintCards(boolean _printCards = printCards)
    {
        printCards = _printCards;

        return printCards;
    }

]]></Source>
			</Method>
			<Method>
				<Name>printNewCards</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Prints all cards with status set to new for the kanban rule.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void printNewCards()
    {
        KanbanPrinter kanbanPrinter = KanbanPrinter::construct();

        // check input parameters
        if (!listCreatedCards)
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        kanbanPrinter.printKanbanCards(listCreatedCards, '', false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected static LeanCommandCreateCards construct()
    {
        return new LeanCommandCreateCards();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes an instance of the <c>LeanCommandCreateCards</c> class.
    /// </summary>
    /// <param name="_leanRuleData">
    ///    The rule for which the kanban cards are to be created.
    /// </param>
    /// <param name="_printCards">
    ///    Flag that indicates whether new cards should be printed.
    /// </param>
    /// <param name="_newCardsCount">
    ///    The number of new cards to be created.
    /// </param>
    /// <returns>
    ///    A <c>LeanCommandCreateCards</c> instance.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static LeanCommandCreateCards newStandard(
        LeanRuleData _leanRuleData,
        boolean _printCards,
        int _newCardsCount)
    {
        LeanCommandCreateCards leanCommandCreateCards = LeanCommandCreateCards::construct();

        leanCommandCreateCards.parmLeanRuleData(_leanRuleData);
        leanCommandCreateCards.parmPrintCards(_printCards);
        leanCommandCreateCards.parmNewCardsCount(_newCardsCount);

        return leanCommandCreateCards;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>