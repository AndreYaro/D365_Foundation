<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocPurchOrderDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider for purchase order.
/// </summary>
public class TaxableDocPurchOrderDataProvider extends TaxableDocumentDataProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to load detail data for taxable document. And setup the row definitions.
    /// </summary>
    /// <returns>
    /// The query to load detail data for taxable document.
    /// </returns>
    /// <remarks>
    /// The row definitions must be setup here. It defines the mapping between row and data source,
    /// the child/parent relations between rows, and the row data provider class.
    /// </remarks>
    public Query buildQuery()
    {
        Query                   query;
        QueryBuildDataSource    purchTableDataSource;
        QueryBuildDataSource    purchLineDataSource;
        QueryBuildDataSource    lineLevelMarkupTransDataSource;

        query = new Query();

        purchTableDataSource = query.addDataSource(tableNum(PurchTable));
        purchTableDataSource.addRange(fieldNum(PurchTable, RecId)).value(queryValue(rootTable.RecId));

        purchLineDataSource = purchTableDataSource.addDataSource(tableNum(PurchLine));
        purchLineDataSource.joinMode(JoinMode::OuterJoin);
        purchLineDataSource.relations(true);
        purchLineDataSource.addRange(fieldNum(PurchLine, IsDeleted)).value(queryValue(NoYes::No));

        lineLevelMarkupTransDataSource = purchLineDataSource.addDataSource(tableNum(MarkupTrans));
        lineLevelMarkupTransDataSource.joinMode(JoinMode::OuterJoin);
        lineLevelMarkupTransDataSource.relations(true);

        rowDefinitions.empty();
        this.registerRow(purchTableDataSource.id(), 0, TaxEngineIntegrationConstants::Header, classStr(TaxableDocPurchTableRowDataProvider));
        this.registerRow(purchLineDataSource.id(), purchTableDataSource.id(), TaxEngineModelFieldsConstants::Lines, classStr(TaxableDocPurchLineRowDataProvider));
        this.registerRow(lineLevelMarkupTransDataSource.id(), purchLineDataSource.id(), TaxEngineModelFieldsConstants::SubLines, classStr(TaxableDocMarkupTransRowDataProvider));

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>