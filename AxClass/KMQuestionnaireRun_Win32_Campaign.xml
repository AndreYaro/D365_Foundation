<?xml version="1.0" encoding="utf-8"?>

<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
  <Name>KmQuestionnaireRun_Win32_Campaign</Name>
  <SourceCode>
    <Declaration><![CDATA[
/// <summary>
/// The <c>KMQuestionnaireRun_Win32_Campaign</c> class manages the <c>Win32</c> client questionnaire answer session for campaigns.
/// </summary>
public class KmQuestionnaireRun_Win32_Campaign extends KMQuestionnaireRun_Win32
{
    smmCampaignSelection  smmCampaignSelection;
}
]]></Declaration>
    <Methods>
      <Method>
        <Name>end</Name>
        <Source><![CDATA[
    /// <summary>
    /// Saves a completed questionnaire when the user clicks the End button.
    /// </summary>
    /// <param name="_kmVirtualNetworkAnswerTableId">
    /// The table ID of the <c>kmVirtualNetworkAnswerTable</c> record.
    /// </param>
    public void end(KMVirtualNetworkAnswerTableId _kmVirtualNetworkAnswerTableId)
    {
        smmCampaignSelection            smmCampaignSelectionUpdate;
        smmCampaignResponsesQuest       responses;
        FormDataSource                  ds;

        super(_kmVirtualNetworkAnswerTableId);

        ttsbegin;

        //--- Select the record for update
        smmCampaignSelectionUpdate = smmCampaignSelection::findCampaign(smmCampaignSelection.CampaignId,
                                                                        smmCampaignSelection.Party,
                                                                        smmCampaignSelection.ContactPersonId, true);

        if (smmCampaignSelectionUpdate)
        {
            if (_kmVirtualNetworkAnswerTableId)
            {
                // If response exists.
                responses.ContactPersonId = smmCampaignSelectionUpdate.ContactPersonId;
                responses.Party     = smmCampaignSelectionUpdate.Party;
                responses.KmVirtualNetworkAnswerTableId = _kmVirtualNetworkAnswerTableId;
                responses.insert();

                smmCampaignSelectionUpdate.kmVirtualNetworkAnswerTableId = _kmVirtualNetworkAnswerTableId;
                smmCampaignSelectionUpdate.Responded        = NoYes::Yes;
                smmCampaignSelectionUpdate.CampaignResponse = smmCampaignResponse::Manual;

                if (smmCampaignSelectionUpdate.validateWrite())
                {
                    smmCampaignSelectionUpdate.update();
                }
            }

            //--- Update datasource on underlying form SmmCampaignTable
            ds = FormDataUtil::getFormDataSource(smmCampaignSelection);

            if (ds)
            {
                ds.executeQuery();
            }
        }
        ttscommit;
    }

]]></Source>
      </Method>
      <Method>
        <Name>new</Name>
        <Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>KMQuestionnaireRun_Win32_Campaign</c> class.
    /// </summary>
    /// <param name="_campaignSelection">
    /// A <c>smmCampaignSelection</c> value.
    /// </param>
    protected void new(smmCampaignSelection _campaignSelection)
    {
        smmCampaignSelection = _campaignSelection;
    }

]]></Source>
      </Method>
      <Method>
        <Name>main</Name>
        <Source><![CDATA[
    static void main(Args _args)
    {
        KMQuestionnaireRun_Win32_Campaign   kmQuestionnaireRun;
        HcmPersonRecId                      person;
        smmCampaignSelection                smmCampaignSelection = _args.record();
        KMCollectionId                      kmCollectionId = smmCampaignTable::find(smmCampaignSelection.CampaignId).kmCollectionId;

        if (isConfigurationkeyEnabled(configurationkeynum(KMQuestionnaireBasic)))
        {
            person = ContactPerson::find(smmCampaignSelection.ContactPersonId).Party;
        }

        kmQuestionnaireRun = new KMQuestionnaireRun_Win32_Campaign(smmCampaignSelection);

        if (kmQuestionnaireRun.set(KMQuestionnaireRunMode::All, kmCollectionId, person))
        {
            kmQuestionnaireRun.run();
        }
    }

]]></Source>
      </Method>
    </Methods>
  </SourceCode>
</AxClass>