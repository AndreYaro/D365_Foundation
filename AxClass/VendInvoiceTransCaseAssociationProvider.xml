<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceTransCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'VendorInvoiceline'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class VendInvoiceTransCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        VendInvoiceTrans vendInvoiceTrans;
        select crosscompany DataAreaId from vendInvoiceTrans where vendInvoiceTrans.RecId == _parentRecId;

        return vendInvoiceTrans.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        VendInvoiceTrans vendInvoiceTrans;
        select crosscompany InvoiceId from vendInvoiceTrans where vendInvoiceTrans.RecId == _parentRecId;

        return vendInvoiceTrans.InvoiceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        VendInvoiceTrans vendInvoiceTrans;
        select RecId from vendInvoiceTrans where vendInvoiceTrans.InvoiceId == _naturalKey;

        if (!vendInvoiceTrans.RecId)
        {
            vendInvoiceTrans = VendInvoiceTrans::findRecId(str2Int64(_naturalKey));
        }

        return vendInvoiceTrans.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        VendInvoiceTrans vendInvoiceTrans;
        select crosscompany Name from vendInvoiceTrans where vendInvoiceTrans.RecId == _parentRecId;

        return vendInvoiceTrans.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        VendInvoiceTrans vendInvoiceTrans;
        
        //We need this code in order not to lose the localization data in the opened form.
        VendInvoiceTrans vendInvoiceTransCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from vendInvoiceTransCrossCompany
            where vendInvoiceTransCrossCompany.RecId == _association.RefRecId;

        if (vendInvoiceTransCrossCompany.Recid)
        {
            changecompany (vendInvoiceTransCrossCompany.DataAreaId)
            {
                select firstonly vendInvoiceTrans where vendInvoiceTrans.RecId == _association.RefRecId;
            }
        }

        _args.record(vendInvoiceTrans);
        return menuitemdisplaystr(VendInvoiceJournal);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(VendInvoiceTrans), callerControl);

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(VendInvoiceTrans));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, RecId), true);
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, InvoiceId));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, PurchId));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, ItemId));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, LineAmount));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, PurchUnit));
        sysTableLookup.addLookupfield(fieldnum(VendInvoiceTrans, Qty));
        sysTableLookup.addLookupMethod(tableMethodStr(VendInvoiceTrans, displayDeliveryDate));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(VendInvoiceTrans))
        {
            _eventArgs.parmEntityType(CaseEntityType::VendorInvoiceline);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>