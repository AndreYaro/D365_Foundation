<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SourceDocumentStateInProcessBankStmt</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SourceDocumentStateInProcessBankStmt</c> class is an in-process source document state for
/// a Bank Statement Line.
/// </summary>
[SourceDocumentClassExtensionAttribute(tableStr(BankStmtISOAccountStatement))]
public class SourceDocumentStateInProcessBankStmt extends SourceDocumentStateInProcess
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSourceDocumentLineQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates query to obtain SourceDocumentLine and associated BankStmtISOReportEntry records
    /// </summary>
    /// <param name = "_sourceDocumentHeaderRecId">Source document header record</param>
    /// <returns>
    /// The initialized query
    /// </returns>
    protected Query getSourceDocumentLineQuery(SourceDocumentHeaderRecId _sourceDocumentHeaderRecId)
    {
        QueryBuildDataSource    retrieveSDLQbds;
        QueryBuildDataSource    bankStmtLineQbds;
        QueryBuildDataSource    bankStmtDocInfoQbds;
        Query                   query;

        query = super(_sourceDocumentHeaderRecId);

        retrieveSDLQbds = query.dataSourceNo(query.dataSourceCount());
        bankStmtLineQbds = retrieveSDLQbds.addDataSource(tableNum(BankStmtISOReportEntry));
        bankStmtLineQbds.joinMode(JoinMode::OuterJoin);
        bankStmtLineQbds.addLink(fieldNum(SourceDocumentLine, RecId),fieldNum(BankStmtISOReportEntry, SourceDocumentLine));

        bankStmtDocInfoQbds = bankStmtLineQbds.addDataSource(tableNum(BankStatementLineBankDocumentInformation));
        bankStmtDocInfoQbds.joinMode(JoinMode::OuterJoin);
        bankStmtDocInfoQbds.addLink(fieldNum(BankStmtISOReportEntry, RecId), fieldNum(BankStatementLineBankDocumentInformation, BankStmtISOReportEntry));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAdditionalInformationMap</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates map holding the BankStatementLineBankDocumentInformation record associated with the specific SourceDocumentLine
    /// </summary>
    /// <param name="_queryRun">
    /// The queryRun object holding the SourceDocumentLineImplementationRecords.
    /// </param>
    /// <returns>
    /// The initialized map.
    /// </returns>
    protected Map initAdditionalInformationMap(QueryRun _queryRun)
    {
        List tableList = new List(Types::Integer);
        tableList.addEnd(tableNum(BankStatementLineBankDocumentInformation));

        Map additionalInformation = this.fillMap(_queryRun, tableList);

        return additionalInformation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSDLImplementation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieve the BankStmtISOReportEntry record associated with the SourceDocumentLine instance.
    /// </summary>
    /// <param name="_queryRun">
    /// The queryRun object holding the BankStmtISOReportEntry records.
    /// </param>
    /// <returns>
    /// The BankStmtISOReportEntry record
    /// </returns>
    protected SourceDocumentLineImplementation initSDLImplementation(QueryRun _queryRun)
    {
        BankStmtISOReportEntry bankStmtISOReportEntryLine = _queryRun.get(tableNum(BankStmtISOReportEntry));

        return bankStmtISOReportEntryLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSourceDocumentLineQueryUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the getSourceDocumentLineQuery() implementation using the SysDa framework
    /// </summary>
    /// <param name = "_sourceDocumentHeaderRecId">Source document header record</param>
    /// <returns>The initialized query.</returns>
    protected SysDaQueryObject getSourceDocumentLineQueryUsingSysDa(SourceDocumentHeaderRecId _sourceDocumentHeaderRecId)
    {
        SysDaQueryObject    ret = super(_sourceDocumentHeaderRecId);

        SourceDocumentLine  sourceDocumentLine = ret.table();

        BankStmtISOReportEntry bankStmtISOReportEntryLine;
        SysDaQueryObject    sysDaQueryLineObject = new SysDaQueryObject(bankStmtISOReportEntryLine);

        ret.joinClause(SysDaJoinKind::OuterJoin, sysDaQueryLineObject);
        sysDaQueryLineObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(bankStmtISOReportEntryLine, fieldStr(AdvancedLedgerEntryLine, SourceDocumentLine)),
                new SysDaFieldExpression(sourceDocumentLine, fieldStr(SourceDocumentLine, RecId))));

        BankStatementLineBankDocumentInformation  bankStatementLineBankDocumentInformation;
        SysDaQueryObject    sysDaQueryTableObject = new SysDaQueryObject(bankStatementLineBankDocumentInformation);

        sysDaQueryLineObject.joinClause(SysDaJoinKind::OuterJoin, sysDaQueryTableObject);
        sysDaQueryTableObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(bankStatementLineBankDocumentInformation, fieldStr(BankStatementLineBankDocumentInformation, BankStmtISOReportEntry)),
                new SysDaFieldExpression(bankStmtISOReportEntryLine, fieldStr(BankStmtISOReportEntry, RecId))));

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initAdditionalInformationMapUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the implementation of initAdditionalInformationMap() using the SysDa framework
    /// </summary>
    /// <param name = "_sdso">SysDaSearchObject instead of QueryRun</param>
    /// <returns>
    /// The initialized map.
    /// </returns>
    protected Map initAdditionalInformationMapUsingSysDa(SysDaSearchObject _sdso)
    {
        List tableList = new List(Types::Integer);
        tableList.addEnd(tableNum(BankStatementLineBankDocumentInformation));

        Map additionalInformation = this.fillMapUsingSysDa(_sdso, tableList);

        return additionalInformation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSDLImplementationUsingSysDa</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is the implementation of initSDLImplementation(), using the SysDa framework.
    /// </summary>
    /// <param name = "_sdso">
    /// The SysDa Search object containing the query to search in.
    /// </param>
    /// <returns>
    /// The CustInvoiceLine record as <c>SourceDocumentLineImplementation</c>.
    /// </returns>
    protected SourceDocumentLineImplementation initSDLImplementationUsingSysDa(SysDaSearchObject _sdso)
    {
        BankStmtISOReportEntry bankStmtISOReportEntryLine = this.sysDaGetRecordByTableId(_sdso.query(), tableNum(BankStmtISOReportEntry));

        return bankStmtISOReportEntryLine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>