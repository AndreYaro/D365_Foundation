<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DateOfExchRateAutoDeterminationContextSalesParmTable_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;
/// <summary>
/// The <c>DateOfExchRateAutoDeterminationContextSalesParmTable_W</c> class is an implementation of the
/// <c>DateOfExchRateAutoDeterminationContextInterface_W</c> interface to support
/// getting exchange rate dates from sales order update table.
/// </summary>
[ExportMetadata(classStr(DateOfExchRateAutoDeterminationContextInterface_W), tableStr(SalesParmTable))]
[Export(identifierstr(Dynamics.AX.Application.DateOfExchRateAutoDeterminationContextInterface_W))]
internal class DateOfExchRateAutoDeterminationContextSalesParmTable_W implements DateOfExchRateAutoDeterminationContextInterface_W
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getInvoiceDate</Name>
				<Source><![CDATA[
    public TransDate getInvoiceDate(Common _common)
    {
        SalesParmTable salesParmTable = _common;
        return salesParmTable.Transdate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSalesDate</Name>
				<Source><![CDATA[
    public TransDate getSalesDate(Common _common)
    {
        SalesParmTable salesParmTable = _common;

        return salesParmTable.SalesDate_W != dateNull()
            ? salesParmTable.SalesDate_W
            : maxDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMinPaymentDate</Name>
				<Source><![CDATA[
    public TransDate getMinPaymentDate(Common _common)
    {
        SalesParmTable salesParmTable = _common;
        TransDate minPaymentDate = maxDate();

        SalesParmSubTable salesParmSubTable;
        SalesTable salesTable;

        while select tableId from salesParmSubTable
            where salesParmSubTable.ParmId == salesParmTable.ParmId
        join salesTable
            where salesTable.SalesId == salesParmSubTable.OrigSalesId
        {
            SpecTransManager specTransManager = SpecTransManager::newFromSpec(salesTable);
            Map map = specTransManager.getAllSpecTransMap();

            MapEnumerator me = map.getEnumerator();
            while (me.moveNext())
            {
                SpecTrans specTrans = me.currentValue();
                minPaymentDate = min(minPaymentDate, specTrans.custTrans().TransDate);
            }
        }

        return minPaymentDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>