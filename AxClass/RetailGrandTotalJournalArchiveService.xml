<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailGrandTotalJournalArchiveService</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// Retail archive report.
/// </summary>
public class RetailGrandTotalJournalArchiveService extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the archive report.
    /// </summary>
    /// <param name = "_dataContract">A data contract.</param>
    public void run(RetailGrandTotalJournalArchiveContract _dataContract)
    {
        ERIFormatMappingRun formatMappingRun = ERObjectsFactory::createFormatMappingRunByFormatMappingId(_dataContract.parmERFormatMappingId(), _dataContract.parmExportFileName());

        this.initFileDestinationFactory(formatMappingRun, _dataContract);
        this.initParameters(formatMappingRun, _dataContract);

        formatMappingRun.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFileDestinationFactory</Name>
				<Source><![CDATA[
    private void initFileDestinationFactory(ERIFormatMappingRun _formatMappingRun, RetailGrandTotalJournalArchiveContract _dataContract)
    {
        if (_dataContract.parmIsSigningRequired())
        {
            ERIFileDestinationFactoryHost factoryHost = ERCast::asObject(_formatMappingRun) as ERIFileDestinationFactoryHost;

            if (factoryHost)
            {
                ERIFileDestinationFactory fileDestinationFactory = factoryHost.getFileDestinationFactory();
                factoryHost.setFileDestinationFactory(RetailGrandTotalJournalArchiveDestinationFactory::construct(fileDestinationFactory, _dataContract));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    private void initParameters(ERIFormatMappingRun _formatMappingRun, RetailGrandTotalJournalArchiveContract _dataContract)
    {
        ERModelDefinitionDatabaseContext dataBaseParameters = new ERModelDefinitionDatabaseContext()
            .addValue(tableNum(RetailGrandTotalJournalTable), fieldNum(RetailGrandTotalJournalTable, RecId), _dataContract.parmGrandTotalJournalTableRecId());

        ERModelDefinitionInputParametersAction inputParameters = new ERModelDefinitionInputParametersAction()
            .addParameter('model/ExportFileName', _dataContract.parmExportFileName());

        ERIModelDefinitionParamsUIAction parameters = new ERModelDefinitionParamsUIActionComposite()
            .Add(dataBaseParameters)
            .Add(inputParameters);

        _formatMappingRun
            .withFileDestination(_dataContract.getFileDestination())
            .withParameter(parameters);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>