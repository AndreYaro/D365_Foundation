<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_SalesCROTmp</Name>
	<SourceCode>
		<Declaration><![CDATA[
class LogisticsLocationSelectForm_SalesCROTmp
    extends LogisticsLocationSelectForm
{
    SalesCreateReleaseOrderTableTmp salesCreateReleaseOrderTableTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling purchase order.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address in which to copy.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        salesCreateReleaseOrderTableTmp.DeliveryPostalAddress = _addressRecId;
        salesCreateReleaseOrderTableTmp.AddressRefRecId = _transferFromTable.RecId;
        salesCreateReleaseOrderTableTmp.AddressRefTableId = _transferFromTable.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (salesCreateReleaseOrderTableTmp.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                salesCreateReleaseOrderTableTmp.AddressRefTableId,
                salesCreateReleaseOrderTableTmp.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    salesCreateReleaseOrderTableTmp.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> record that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of the <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public recId getLogisticsPostalAddressRecId()
    {
        return salesCreateReleaseOrderTableTmp.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of the target form should have selected upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (salesCreateReleaseOrderTableTmp.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                salesCreateReleaseOrderTableTmp.AddressRefTableId,
                salesCreateReleaseOrderTableTmp.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            if (salesCreateReleaseOrderTableTmp.CustAccount)
            {
                addressOwner = CustTable::find(salesCreateReleaseOrderTableTmp.CustAccount);
            }
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(SalesCreateReleaseOrderTableTmp _salesCreateReleaseOrderTableTmp)
    {
        salesCreateReleaseOrderTableTmp = _salesCreateReleaseOrderTableTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>LogisticsLocationSelectForm_SalesCROTmp</c> class.
    /// </summary>
    /// <param name="_buffer">
    /// The record that is used to initialize the <c>LogisticsLocationSelectForm_SalesCROTmp</c> object.
    /// </param>
    /// <returns>
    /// A new instance of the <c>LogisticsLocationSelectForm_SalesCROTmp</c> class.
    /// </returns>
    public static LogisticsLocationSelectForm_SalesCROTmp construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_SalesCROTmp(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>