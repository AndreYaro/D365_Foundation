<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AccDistRuleProductTaxWithhold_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    This class derives LedgerPostingType and Side(Debit/Credit) parameter values for the withholding tax lines during PO distributions.
/// </summary>
[
SourceDocumentEnumExtensionAttribute(enumStr(BusinessEvent_ProductOrder), BusinessEvent_ProductOrder::ObligateBudgetFundsForExpensedProducts, MonetaryAmount::WithholdTax_IN),
SourceDocumentEnumExtensionAttribute(enumStr(BusinessEvent_ProductReceipt), BusinessEvent_ProductReceipt::ReceiptOfExpensedProducts, MonetaryAmount::WithholdTax_IN)
]
public class AccDistRuleProductTaxWithhold_IN extends AccountingDistributionRule
{
    TaxWithholdDirection_IN        taxWithholdDirection;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the accounting distribution rule.
    /// </summary>
    protected void initialize()
    {
        Object                                  object = sourceDocumentLineItem;
        SourceDocumentLineItemITaxWithhold_IN   iTaxWithhold;

        if (object is SourceDocumentLineItemITaxWithhold_IN)
        {
            iTaxWithhold            = object as SourceDocumentLineItemITaxWithhold_IN;
            taxWithholdDirection    = iTaxWithhold.parmTaxWithholdDirection();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLedgerPostingType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>LedgerPostingType</c> parameter during PO distributions for withholding tax.
    /// </summary>
    /// <returns>
    /// The value of the <c>LedgerPostingType</c> parameter.
    /// </returns>
    public LedgerPostingType parmLedgerPostingType()
    {
        TaxWithholdSourceDocSubLineItem_IN  taxWithholdSourceDocSubLineItem;
        if (sourceDocumentLineItem is TaxWithholdSourceDocSubLineItem_IN)
        {
            taxWithholdSourceDocSubLineItem = sourceDocumentLineItem;
            switch (taxWithholdSourceDocSubLineItem.getTaxWithholdMap().TaxWithholdType)
            {
                case TaxWithholdCodeType_IN::TDS:
                    return LedgerPostingType::TDS_IN;
                case TaxWithholdCodeType_IN::TCS:
                    return LedgerPostingType::TCS_IN;
                default:
                    break;
            }
        }
        return LedgerPostingType::TDS_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSide</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Side</c> parameter during PO distributions for withholding tax.
    /// </summary>
    /// <returns>
    /// The value of the <c>Side</c> parameter.
    /// </returns>
    public DebitCredit parmSide()
    {
        DebitCredit side = DebitCredit::Credit;

        switch (taxWithholdDirection)
        {
            case TaxWithholdDirection_IN::IncomingTax:
                if (this.parmAmount() >= 0.0)
                {
                    side = DebitCredit::Debit;
                }
                break;

            case TaxWithholdDirection_IN::OutgoingTax:
                if (this.parmAmount() < 0.0)
                {
                    side = DebitCredit::Debit;
                }
                break;
        }

        return side;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>