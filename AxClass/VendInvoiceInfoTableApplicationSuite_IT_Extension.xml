<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceInfoTableApplicationSuite_IT_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(VendInvoiceInfoTable))]
public final class VendInvoiceInfoTableApplicationSuite_IT_Extension
{

}

]]></Declaration>
		<Methods>
			<Method>
				<Name>modifiedField</Name>
				<Source><![CDATA[
    public void modifiedField(FieldId _fieldId)
    {
        next modifiedField(_fieldId);

        if (FeatureStateProvider::isFeatureEnabled(PlafondFeature::instance()))
        {
            switch (_fieldId)
            {
                case fieldNum(VendInvoiceInfoTable, TransDate):
                case fieldNum(VendInvoiceInfoTable, DocumentDate):
                    this.initPlafondDate_IT();
                    break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyFromHeader</Name>
				<Source><![CDATA[
    public void copyFromHeader(VendInvoiceInfoTable _header)
    {
        next copyFromHeader(_header);

        if (FeatureStateProvider::isFeatureEnabled(PlafondFeature::instance()))
        {
            this.PlafondDate_IT = _header.PlafondDate_IT;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initPlafondDate_IT</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes plafond date.
    /// </summary>
    public void initPlafondDate_IT()
    {
        switch(VendParameters::find().PlafondDateProposal_IT)
        {
            case PlafondDateProposal_IT::PostingDate:
                this.PlafondDate_IT = this.TransDate;
                break;

            case PlafondDateProposal_IT::DocumentDate:
                this.PlafondDate_IT = this.DocumentDate;
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>