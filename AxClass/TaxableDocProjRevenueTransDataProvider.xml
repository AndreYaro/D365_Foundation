<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocProjRevenueTransDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider for Project - Fee
/// </summary>
public class TaxableDocProjRevenueTransDataProvider extends TaxableDocumentDataProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to load detail data for taxable document. And setup the row definitions.
    /// </summary>
    /// <returns>
    /// The query to load detail data for taxable document.
    /// </returns>
    /// <remarks>
    /// The row definitions must be setup here. It defines the mapping between row and data source,
    /// the child/parent relations between rows, and the row data provider class.
    /// </remarks>
    public Query buildQuery()
    {
        Query                   query;
        QueryBuildDataSource    projRevenueTransHeadDataSource;
        QueryBuildDataSource    projProposalJourDataSource;
        QueryBuildDataSource    projProposalRevenueTransLineDataSource;
        QueryBuildDataSource    projRevenueTransLineDataSource;
        boolean                 hasTransLine = this.hasTransactionLine();

        query = new Query();
        projRevenueTransHeadDataSource = query.addDataSource(tableNum(TaxEngineProjRevenueTransHeader));
        projRevenueTransHeadDataSource.addRange(fieldNum(TaxEngineProjRevenueTransHeader, RecId)).value(queryValue(rootTable.RecId));

        projProposalJourDataSource = projRevenueTransHeadDataSource.addDataSource(tableNum(ProjProposalJour));
        projProposalJourDataSource.joinMode(JoinMode::OuterJoin);
        projProposalJourDataSource.relations(true);

        projProposalRevenueTransLineDataSource = projProposalJourDataSource.addDataSource(tableNum(ProjProposalRevenue));
        projProposalRevenueTransLineDataSource.joinMode(JoinMode::InnerJoin);
        projProposalRevenueTransLineDataSource.relations(true);

        projRevenueTransLineDataSource = projProposalRevenueTransLineDataSource.addDataSource(tableNum(ProjRevenueTrans));
        projRevenueTransLineDataSource.joinMode(JoinMode::InnerJoin);
        projRevenueTransLineDataSource.relations(true);
        projRevenueTransLineDataSource.addRange(fieldNum(ProjRevenueTrans, TransactionOrigin)).value(SysQuery::value(ProjOrigin::FeeJournal));

        if (FeatureStateProvider::isFeatureEnabled(TaxProjOpsIntegrationJournal_INFeature::instance())
            && ProjProjectOperationsIntegrationFeatureHelper::isFeatureEnabled())
        {
            projRevenueTransLineDataSource.addRange(fieldNum(ProjRevenueTrans, TransactionOrigin)).value(SysQuery::value(ProjOrigin::ProjAdvancedJournal));
        }

        rowDefinitions.empty();
        this.registerRow(projRevenueTransHeadDataSource.id(), 0, TaxEngineIntegrationConstants::Header, classStr(TaxableDocProjRevenueTransHeaderRowDP));
        this.registerRow(projProposalRevenueTransLineDataSource.id(), projRevenueTransHeadDataSource.id(), TaxEngineModelFieldsConstants::Lines, classStr(TaxableDocProjRevenueTransLineRowDP));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasTransactionLine</Name>
				<Source><![CDATA[
    private boolean hasTransactionLine()
    {
        TaxEngineProjRevenueTransHeader     header              = rootTable;
        ProjProposalJour                    projProposalJour    = ProjProposalJour::findRecid(header.ProjProposalJour);
        ProjRevenueTrans                    projRevenueTrans;
        ProjProposalRevenue                 projProposalRevenue;

        select firstOnly RecId from projProposalRevenue
            where projProposalRevenue.ProposalId == projProposalJour.ProposalId
                exists join RecId from projRevenueTrans
                    where projRevenueTrans.TransId == projProposalRevenue.TransId
                        && (projRevenueTrans.TransactionOrigin == ProjOrigin::FeeJournal
                            || (projRevenueTrans.TransactionOrigin == ProjOrigin::ProjAdvancedJournal
                                && FeatureStateProvider::isFeatureEnabled(TaxProjOpsIntegrationJournal_INFeature::instance())
                                && ProjProjectOperationsIntegrationFeatureHelper::isFeatureEnabled()));

        return projProposalRevenue.RecId != 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>