<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DlvReason_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class complements the logic of <c>DlvReason</c> table methods.
/// </summary>
public static class DlvReason_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>DlvReason_onModifiedFieldValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Allows to set payment terms.
    /// </summary>
    /// <param name = "_sender">The modified value.</param>
    /// <param name = "_e">The event arguments.</param>
    [DataEventHandler(tableStr(DlvReason), DataEventType::ModifiedFieldValue)]
    public static void DlvReason_onModifiedFieldValue(Common _sender, DataEventArgs _e)
    {
        if (FeatureStateProvider::isFeatureEnabled(SeparateInvoiceOnGoodsDeliveredForFreeFeature::instance()))
        {
            DlvReason dlvReason = _sender;
            ModifyFieldValueEventArgs event = _e as ModifyFieldValueEventArgs;

            if (event && event.parmFieldName())
            {
                switch (event.parmFieldName())
                {
                    case fieldStr(DlvReason, PaymTermId_IT):
                        dlvReason.setPaymentTerm_IT(dlvReason.PaymTermId_IT);
                        break;
                    case fieldStr(DlvReason, InvoiceAccount_IT):
                        if (dlvReason.InvoiceAccount_IT)
                        {
                            dlvReason.setPaymentTerm_IT(CustTable::find(dlvReason.InvoiceAccount_IT).PaymTermId);
                        }
                        else
                        {
                            dlvReason.setPaymentTerm_IT('');
                        }
                        break;
                    default:
                        break;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setPaymentTerm_IT</Name>
				<Source><![CDATA[
    /// <summary>
    /// Allows to set payment terms.
    /// </summary>
    /// <param name = "_dlvReason">Delivery Reason record</param>
    /// <param name = "_paymTermId">Terms of payment ID </param>
    public static void setPaymentTerm_IT(DlvReason _dlvReason, PaymTermId _paymTermId)
    {
        if (FeatureStateProvider::isFeatureEnabled(SeparateInvoiceOnGoodsDeliveredForFreeFeature::instance()))
        {
            PaymTerm paymTerm = PaymTerm::find(_paymTermId);
            _dlvReason.PaymTermId_IT = _paymTermId;

            if (!paymTerm.CashLedgerDimension)
            {
                Message::Add(MessageSeverity::Warning, "@ExtendedItallianLocalization:PaymentTermShouldHaveCashAccountSpecified");
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>