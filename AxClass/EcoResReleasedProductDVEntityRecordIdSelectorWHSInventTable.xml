<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResReleasedProductDVEntityRecordIdSelectorWHSInventTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

[ExportMetadata(identifierStr(TableName), tableStr(WHSInventTable))]
[ExportMetadata(identifierStr(EntityName), dataentityviewstr(EcoResReleasedProductDVEntity))]
[Export(identifierstr(Dynamics.AX.Application.IBusinessEventsTableToEntityRecordIdSelector))]
internal final class EcoResReleasedProductDVEntityRecordIdSelectorWHSInventTable implements IBusinessEventsTableToEntityRecordIdSelector
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEntityRecordIdsImpactedByTableChange</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public List getEntityRecordIdsImpactedByTableChange(BusinessEventsDefinition _eventDefinition, BusinessEventsActions _changeType, RecId _changedRecordId)
    {
        if (_eventDefinition.DataSourceName != dataEntityDataSourceStr(EcoResReleasedProductDVEntity, WHSInventTable)
            || !EcoResReleasedProductDVEntitiesUseRecordIdSelectorsFlight::instance().isEnabled())
        {
            return null;
        }

        List impactedRecIds = new List(Types::Int64);

        InventTable inventTable;
        WHSInventTable whsInventTable;
        
        while select RecId from inventTable
            exists join whsInventTable
            where inventTable.ItemId == whsInventTable.ItemId
                && whsInventTable.RecId == _changedRecordId
        {
            impactedRecIds.addEnd(inventTable.RecId);
        }

        return impactedRecIds;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>