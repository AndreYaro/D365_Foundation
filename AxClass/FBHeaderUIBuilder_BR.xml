<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBHeaderUIBuilder_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>FBHeaderUIBuilder_BR</c> class is UI builder class used for fiscal books reports.
/// </summary>
class FBHeaderUIBuilder_BR extends SrsReportDataContractUIBuilder
{
    DialogField         fiscalEstablishmentIdField;
    DialogField         fiscalEstablishmentDataAreaField;
    DialogField         monthPeriodField;
    DialogField         monthPeriodValueField;
    DialogField         yearPeriodField;
    FormRun             formRun;

    FBHeaderContract_BR contract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Builds the UI builder object.
    /// </summary>
    public void build()
    {
        contract = this.dataContractObject() as FBHeaderContract_BR;
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalEstablishmentLookup</Name>
				<Source><![CDATA[
    private void fiscalEstablishmentLookup(FormControl _formControl)
    {
        Args                args            = new Args(formStr(FBBookingPeriodLookup_BR));
        FormStringControl   stringControl   = _formControl;

        formRun = classFactory::formRunClassOnClient(args);
        formRun.init();

        stringControl.performFormLookup(formRun);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fiscalEstablishmentModified</Name>
				<Source><![CDATA[
    private boolean fiscalEstablishmentModified(FormControl _formControl = null)
    {
        Common              common;
        FBBookingPeriod_BR  bookingPeriod;

        if (formRun)
        {
            common = formRun.selectRecordModeSelectedRecord();

            if (common.TableId == tableNum(FBBookingPeriod_BR))
            {
                bookingPeriod = common;

                fiscalEstablishmentIdField.value        (bookingPeriod.FiscalEstablishmentId);
                fiscalEstablishmentDataAreaField.value  (bookingPeriod.FiscalEstablishmentDataArea);
                monthPeriodField.value                  (bookingPeriod.PeriodMonth);
                monthPeriodValueField.value             (bookingPeriod.PeriodMonth);
                yearPeriodField.value                   (bookingPeriod.PeriodYear);

                return true;
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the input dialog caption and changes the dimension focus dialog field to appear as lookup.
    /// </summary>
    public void postBuild()
    {
        fiscalEstablishmentIdField = this.bindInfo().getDialogField(this.dataContractObject(), methodstr(FBHeaderContract_BR, parmFiscalEstablishmentId));
        fiscalEstablishmentIdField.lookupButton(2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides this method in order to register the dialog field methods to capture events.
    /// </summary>
    public void postRun()
    {
        Dialog dialogLocal = this.dialog();

        super();

        // This method should be called in order to handle events on dialogs.
        dialogLocal.dialogForm().formRun().controlMethodOverload(false);

        fiscalEstablishmentIdField       = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBHeaderContract_BR, parmFiscalEstablishmentId));
        fiscalEstablishmentDataAreaField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBHeaderContract_BR, parmFiscalEstablishmentDataArea));
        monthPeriodField                 = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBHeaderContract_BR, parmMonthPeriod));
        monthPeriodValueField            = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBHeaderContract_BR, parmMonthPeriodValue));
        yearPeriodField                  = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(FBHeaderContract_BR, parmYearPeriod));

        fiscalEstablishmentIdField.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(FBHeaderUIBuilder_BR, fiscalEstablishmentLookup), this);
        fiscalEstablishmentIdField.registerOverrideMethod(methodStr(FormStringControl, modified), methodStr(FBHeaderUIBuilder_BR, fiscalEstablishmentModified), this);
        FiscalEstablishmentIdField.label("@GLS220103");

        fiscalEstablishmentDataAreaField.allowEdit(false);
        monthPeriodField.allowEdit(false);
        yearPeriodField.allowEdit(false);

        monthPeriodValueField.visible(false);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>