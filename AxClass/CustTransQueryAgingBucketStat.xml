<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTransQueryAgingBucketStat</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CustVendTransQueryBuildFactoryAttribute(CustVendTransQueryType::CustAgingBucketStat)]
class CustTransQueryAgingBucketStat extends CustVendTransQueryBuild
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Builds the base query that is used to determine the customer aging statistics.
    /// </summary>
    /// <returns>
    ///     The packed <c>Query</c> object.
    /// </returns>
    public container buildQuery()
    {
        query = new Query();
        query.addDataSource(tablenum(CustTrans));

        query.dataSourceNo(1).addRange(fieldnum(CustTrans, AccountNum)).value(accountNum);

        this.addRangeBasedOnTransactionDueDate();
        
        query.dataSourceNo(1).addRange(fieldnum(CustTrans, Closed)).value(SysQuery::valueEmptyString());

        return query.pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>addRangeBasedOnTransactionDueDate</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Add ranges in query based on transaction due date.
    /// </summary>
    [Wrappable(true)]
    protected final void addRangeBasedOnTransactionDueDate()
    {
        switch (typeOfDate)
        {
            case DateTransactionDuedate::TransactionDate :
                query.dataSourceNo(1).addRange(fieldnum(CustTrans, TransDate)).value(queryRange(fromDate, toDate));
                break;

            case DateTransactionDuedate::DocumentDate :
                this.addRangeBasedOnTransactionDocumentDate();
                break;

            case DateTransactionDuedate::DueDate :
                query.dataSourceNo(1).addRange(fieldnum(CustTrans, DueDate)).value(queryRange(fromDate, toDate));
                break;

            default :
                query.dataSourceNo(1).addRange(fieldnum(CustTrans, TransDate)).value(queryRange(fromDate, toDate));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addRangeBasedOnTransactionDocumentDate</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Add ranges in query based on transaction document date.
    /// </summary>
    private void addRangeBasedOnTransactionDocumentDate()
    {
        str dateNull = date2StrXpp(dateNull());
        QueryBuildRange qbr = query.dataSourceNo(1).addRange(fieldnum(CustTrans, RecId));

        date sqlFromDate = fromDate ? fromDate : dateNull();
        date sqlToDate = toDate ? toDate : maxdate();

        str documentDate = fieldStr(CustTrans, DocumentDate);
        str transDate = fieldStr(CustTrans, TransDate);

        Range range = '((' + documentDate + ' >= ' + this.dateToSqlStr(sqlFromDate) + ') &&';
        range += '(' + documentDate + ' <= ' + this.dateToSqlStr(sqlToDate) + ')) ||';
        range += '((' + documentDate + ' = ' + dateNull + ') &&';
        range += '(' + transDate + ' >= ' + this.dateToSqlStr(sqlFromDate) + ') && ';
        range += '(' + transDate + ' <= ' + this.dateToSqlStr(sqlToDate) + '))';

        qbr.value(range);
        qbr.status(RangeStatus::Hidden);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>