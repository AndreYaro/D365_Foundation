<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjOnAccountProj2ColDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjOnAccountProj2ColDP</c> class is the report data provider class for the
///    <c>ProjListProjOnAccountProj2Column</c> reports.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjOnAccountProjContract))
]

class ProjListProjOnAccountProj2ColDP extends SRSReportDataProviderPreProcessTempDB
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    TransDate               projFromDate;
    TransDate               projBreakDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerBreakDate;
    TransDate               ledgerToDate;
    projActualVsBudget      actualVsBudget;
    ProjForecastModelId     forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;

    ProjListProjTrans_OnAccount     listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjOnAccountTmp        projListProjOnAccountTmp;
    ProjListProjHelpClass           actualProjHelpClass;
    ProjListProjHelpClass           budgetProjHelpClass;

    AmountMST               actualOnAccount;
    AmountMST               budgetOnAccount;

    ProjId                  projId;
    ProjId                  projIdFound;
    ProjName                name;
    ProjTable               projTable;

    QueryRun                qrun;

    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    int                     amountDecimals, hourQtyDecimals, grossMarginDecimals, valueAddedDecimals;
    str                     headerLabel, devianceLabel,onAccountLabel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the on-account amount.
    /// </summary>
    void calculate()
    {
        actualProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        actualProjHelpClass.setParmField();
        budgetProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        budgetProjHelpClass.setParmField();

        actualOnAccount    += actualProjHelpClass.parmProjStatement().e100_Total_Revenue();
        budgetOnAccount    += budgetProjHelpClass.parmProjStatement().e100_Total_Revenue();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether line should print.
    /// </summary>
    /// <returns>
    ///    true if line should print; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(actualProjHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should print and calls the insert to temporary table.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record that contains information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (listProjTransLayout.parmStateProjType() == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= level) ||
                (projTableLocal.levelFormat(level)   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    actualProjHelpClass.setName();
                    this.insertProjListProjOnAccountTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjOnAccountTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getParameterValues</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the parameter values and assign them to instance variables.
    /// </summary>
    private void getParameterValues()
    {
        ProjListProjOnAccountProjContract contract = this.parmDataContract() as ProjListProjOnAccountProjContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        projFromDate = contract.parmProjFromDate();
        projBreakDate = contract.parmProjBreakDate();
        projToDate = contract.parmProjToDate();

        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerBreakDate = contract.parmLedgerBreakDate();
        ledgerToDate = contract.parmLedgerToDate();

        actualVsBudget = contract.parmActualVsBudget2Col();
        forecastModelId = contract.parmForecastModelId();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjOnAccountTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the data from the <c>ProjListProjOnAccountTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>ProjListProjOnAccountTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('ProjListProjOnAccountTmp')]
    public ProjListProjOnAccountTmp getProjListProjOnAccountTmp()
    {
        select * from projListProjOnAccountTmp;

        return projListProjOnAccountTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>ProjListProjTrans</c> class.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans = ProjListProjTrans::construct(ProjListStateType::OnAccount, ProjListStateId::Project);
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjBreakDate(projBreakDate);
        listProjTrans.parmProjToDate(projToDate);
        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerBreakDate(ledgerBreakDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmActualVsBudget(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjOnAccountTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjOnAccountTmp()
    {
        projListProjOnAccountTmp.clear();

        projListProjOnAccountTmp.Id            = actualProjHelpClass.id();
        projListProjOnAccountTmp.Name          = actualProjHelpClass.name();

        projListProjOnAccountTmp.OnAccount     = listProjTransLayout.showAmountReport(-actualOnAccount, showAmount);
        projListProjOnAccountTmp.OnAccount1    = listProjTransLayout.showAmountReport(-budgetOnAccount, showAmount);
        projListProjOnAccountTmp.OnAccount2    = ProjListProjHelpClass::calcActualVsBudget(projListProjOnAccountTmp.OnAccount,
                                                projListProjOnAccountTmp.OnAccount1, actualVsBudget, false);

        // set labels
        projListProjOnAccountTmp.HeaderLabel   = headerLabel;
        projListProjOnAccountTmp.IdLabel       = strfmt('%1',stateProjType);
        projListProjOnAccountTmp.DevianceLabel = devianceLabel;
        projListProjOnAccountTmp.PeriodLabel   = onAccountLabel;

        // set decimal values
        projListProjOnAccountTmp.DecimalAmount = amountDecimals;

        projListProjOnAccountTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the on-account amount is equal to zero.
    /// </summary>
    /// <returns>
    ///    true if on-account is not zero; otherwise, false.
    /// </returns>
    boolean lineValue()
    {
        return (actualOnAccount != 0) || (budgetOnAccount != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = qrun.query().dataSourceTable(tablenum(ProjTable));
        qbdsProjTable.addSortField(fieldnum(ProjTable, ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldnum(ProjTable, Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = qrun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (! (timeMaterial || fixedPrice))
            {
                QueryBuildRange rangeEmptyString = qbdsProjTable.addRange(fieldnum(ProjTable, Type));
                rangeEmptyString.value(queryValue(SysQuery::valueEmptyString()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the calculate method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = qrun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = qrun.get(tablenum(ProjFundingSource));
        }
        actualProjHelpClass.setId(projTableLocal, projId, projFundingSourceLocal );

        if (! listProjTransLayout.canPrintId(actualProjHelpClass.idNew()))
        {
            actualProjHelpClass.setOld();

            return;
        }

        if (actualProjHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        actualProjHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(level);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SSRS at
    ///    runtime. The method should compute data and populate the data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        firstRun = true;

        this.getParameterValues();
        this.initProjListTrans();

        onAccountLabel= listProjTrans.formatPeriodLabel("@SYS78931");
        headerLabel   = listProjTrans.formatSelection(showAmount, false);
        devianceLabel = ProjListProjHelpClass::descActualVsBudget(actualVsBudget);

        [hourQtyDecimals, amountDecimals,
        grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        actualProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newListProjOnAccount(listProjTrans),
                                    stateProjType);

        budgetProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newListProjOnAccount(listProjTrans,ProjActualBudget::Budget,forecastModelId),
                                    stateProjType);

        qrun = new QueryRun(this.parmQuery());

        this.modifyQuery();

        this.setSortField();

        while (qrun.next())
        {
           this.printProject();
        }

        if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjOnAccountTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the on-account amount to zero.
    /// </summary>
    void setFieldZero()
    {
        actualOnAccount   = 0;
        budgetOnAccount   = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        qrun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>