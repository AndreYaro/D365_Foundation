<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PCXmlSessionWriterComponentControl</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PCXmlSessionWriterComponentControl</c> class writes session XML for the component control
/// records.
/// </summary>
public class PCXmlSessionWriterComponentControl
{
    #PC
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeEnd</Name>
				<Source><![CDATA[
    /// <summary>
    /// Writes ending XML tags for the component control.
    /// </summary>
    /// <param name="_xmlWriter">
    ///    The XML writer to use to store the XML representation of this record.
    /// </param>
    protected void writeEnd(PCXmlWriter _xmlWriter)
    {
        _xmlWriter.writeEndElement();
    }

]]></Source>
			</Method>
			<Method>
				<Name>write</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Writes the XML representation of a component control.
    /// </summary>
    /// <param name="_componentControl">
    ///    The component control record for which to generate the XML.
    /// </param>
    /// <param name="_xmlWriter">
    ///    The XML writer to use to store the XML representation of this record.
    /// </param>
    public void write(EcoResComponentControl _componentControl, PCXmlWriter _xmlWriter)
    {
        PCExpressionConstraint constraint;

        PCXmlSessionWriterControlConstraint xmlSessionWriterControlConstraint;

        this.writeStart(_componentControl, _xmlWriter);

        constraint = _componentControl.getConstraint();

        if ((constraint) && (constraint.Expression))
        {
            xmlSessionWriterControlConstraint = PCXmlSessionWriterControlConstraint::construct();
            xmlSessionWriterControlConstraint.write(constraint, _xmlWriter);
        }

        this.writeEnd(_xmlWriter);
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeStart</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Writes the starting XML tags for the component control.
    /// </summary>
    /// <param name="_componentControl">
    ///    The component control record for which to generate the XML.
    /// </param>
    /// <param name="_xmlWriter">
    ///    The XML writer to use to store the XML representation of this record.
    /// </param>
    protected void writeStart(EcoResComponentControl    _componentControl,
                              PCXmlWriter               _xmlWriter)
    {
        switch (_componentControl.Modifier)
        {
            case EcoResModifierType::Hidden:
                _xmlWriter.writeStartElement(#HiddenModifierElement);
                break;
            case EcoResModifierType::Mandatory:
                _xmlWriter.writeStartElement(#MandatoryModifierElement);
                break;
            case EcoResModifierType::ReadOnly:
                _xmlWriter.writeStartElement(#ReadOnlyModifierElement);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PCXmlSessionWriterComponentControl construct()
    {
        return new PcXmlSessionWriterComponentControl();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>