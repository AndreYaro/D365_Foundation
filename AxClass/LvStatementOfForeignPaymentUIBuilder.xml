<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LvStatementOfForeignPaymentUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Builds the UI for the <c>LvStatmentOfForeignPayment</c> reports.
/// </summary>
/// <remarks>
/// This class is used by both the Foreign and Domestic report variations.
/// </remarks>
[
    SrsReportName(ssrsReportStr(LvStatementOfForeignPaymentForeignBank, Report)),
    SrsReportName(ssrsReportStr(LvStatementOfForeignPaymentDomesticBank, Report)),
    SysOperationContractProcessing(classstr(LvStatementOfForeignPaymentUIBuilder), SysOperationDataContractProcessingMode::CreateUIBuilderForRootContractOnly)
]
public class LvStatementOfForeignPaymentUIBuilder extends SrsReportDataContractUIBuilder
{
    LvStatementOfForeignPaymentRdlContract  rdlContract;

    DialogField     dialogResponsibleEmployeeRecId;
    DialogField     dialogReportingMonth;
    DialogField     dialogReportingYear;

    public const str ParameterCompanyRegistrationNumber = 'CompanyRegistrationNumber';
    public const str ParameterCompanyAddress = 'CompanyAddress';
    public const str ParameterResponsibleEmployeeName = 'ResponsibleEmployeeName';
    public const str ParameterResponsibleEmployeePhone = 'ResponsibleEmployeePhone';
    public const str ParameterReportingMonthOfYearDisplayString = 'ReportingMonthOfYearDisplayString';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add fields to the dialog which align with the visible report parameters.
    /// </summary>
    public void build()
    {
        Dialog      parameterDialog = this.dialog();

        rdlContract = this.getRdlContract();

        // Responsible field group
        parameterDialog.addGroup("@GLS113381");
        dialogResponsibleEmployeeRecId = parameterDialog.addFieldValue(extendedTypeStr(HcmWorkerRecId), rdlContract.getValue(LvStatementOfForeignPaymentRdlContract::ParameterResponsibleEmployeeRecId), "@GLS220735", "@GLS220738");

        // Reporting date field group
        parameterDialog.addGroup("@SYS23896");
        dialogReportingMonth = parameterDialog.addFieldValue(enumStr(MonthsOfYear), rdlContract.getValue(LvStatementOfForeignPaymentRdlContract::ParameterReportingMonth), "@SYS11451", "@GLS220739");
        dialogReportingYear = parameterDialog.addFieldValue(extendedTypeStr(Yr), rdlContract.getValue(LvStatementOfForeignPaymentRdlContract::ParameterReportingYear), "@SYS32606", "@GLS220740");
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfers data from the dialog into the data contract object.
    /// </summary>
    public void getFromDialog()
    {
        CompanyInfo     companyInfo;
        HcmWorker       hcmWorker;
        RecId           responsibleEmployeeRecId = dialogResponsibleEmployeeRecId.value();
        MonthsOfYear    reportingMonth = dialogReportingMonth.value();
        Yr              reportingYear = dialogReportingYear.value();

        companyInfo = companyInfo::find();
        rdlContract.setValue(ParameterCompanyRegistrationNumber, companyInfo.RegNum);
        rdlContract.setValue(ParameterCompanyAddress, companyInfo.postalAddress().Address);

        hcmWorker = HcmWorker::find(responsibleEmployeeRecId);
        rdlContract.setValue(LvStatementOfForeignPaymentRdlContract::ParameterResponsibleEmployeeRecId, int642str(hcmWorker.RecId));
        rdlContract.setValue(ParameterResponsibleEmployeeName, hcmWorker.name());
        rdlContract.setValue(ParameterResponsibleEmployeePhone, hcmWorker.phone());

        rdlContract.setValue(LvStatementOfForeignPaymentRdlContract::ParameterReportingMonth, enum2int(reportingMonth));
        rdlContract.setValue(LvStatementOfForeignPaymentRdlContract::ParameterReportingYear, reportingYear);
        rdlContract.setValue(ParameterReportingMonthOfYearDisplayString, strfmt("@GLS113138", reportingMonth, reportingYear));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRdlContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the <c>SrsReportRdlDataContract</c> class instance associated with the report.
    /// </summary>
    /// <returns>
    /// The <c>LvStatementOfForeignPaymentRdlContract</c> instance associated with the report.
    /// </returns>
    public LvStatementOfForeignPaymentRdlContract getRdlContract()
    {
        return this.getRdlContractInfo().dataContractObject() as LvStatementOfForeignPaymentRdlContract;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>