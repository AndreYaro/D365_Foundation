<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SplitPaymentMandatoryRuleEngine_Purchase</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SplitPaymentMandatoryRuleEngine_Purchase</c> class is extended class for updating paymMode
/// in Purchase order.
/// </summary>
public class SplitPaymentMandatoryRuleEngine_Purchase extends SplitPaymentMandatoryRuleEngine
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPurchTable</Name>
				<Source><![CDATA[
    private PurchTable getPurchTable()
    {
        return this.parmCommonTable() as PurchTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>findRuleForOrder</Name>
				<Source><![CDATA[
    protected SplitPaymentMandatoryRule_PL findRuleForOrder(Amount _totalAmount, PaymMode _paymMode = '', PaymSpec _paymSpec = '')
    {
        PurchTable purchTable = this.getPurchTable();
        PurchLine purchLine;
        SplitPaymentMandatoryRule_PL rule;
        SplitPaymentMandatoryRule_PL voluntaryRule;
        IntrastatCountryRegionParameters intrastatCountryRegionParameters = IntrastatCountryRegionParameters::find(purchTable.vendTable_InvoiceAccount().postalAddress().CountryRegionId);
        boolean isDomesticPartner = intrastatCountryRegionParameters && intrastatCountryRegionParameters.CountryRegionType == CountryRegionType::Domestic;

        while select purchLine
            group by ItemId
            where purchLine.PurchId == purchTable.PurchId
        {
            SplitPaymentMandatoryItemGroupId_PL itemGroupId = SplitPaymentMandatoryItemGroup_PL::findGroupByItem(
                this.getDocumentType(),
                purchLine.ItemId);

            rule = null;

            if (isDomesticPartner)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::Domestic)
                    .withCurrencyCode(purchTable.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }

            if (!rule)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::All)
                    .withCurrencyCode(purchTable.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }

            if (!voluntaryRule && rule && rule.VoluntarySplitPayment == NoYes::Yes)
            {
                voluntaryRule = rule;
            }

            if (rule && rule.VoluntarySplitPayment == NoYes::No)
            {
                break;
            }
        }

        if (!rule && voluntaryRule)
        {
            rule = voluntaryRule;
        }

        return rule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAmount</Name>
				<Source><![CDATA[
    protected Amount getAmount()
    {
        PurchTotals purchTotals = PurchTotals::newPurchTable(this.getPurchTable());
        purchTotals.calc();

        return purchTotals.purchTotalAmount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCommonTable</Name>
				<Source><![CDATA[
    protected void updateCommonTable(PaymMode _paymMode, PaymSpec _paymSpec)
    {
        PurchTable purchTableUpd;

        ttsbegin;

        purchTableUpd = PurchTable::findRecId(this.getPurchTable().RecId, true);

        if (purchTableUpd)
        {
            purchTableUpd.PaymMode = _paymMode;
            purchTableUpd.PaymSpec = _paymSpec;

            if (purchTableUpd.validateWrite())
            {
                using (SplitPaymentContext_PL splitPaymentContext = SplitPaymentContext_PL::instance())
                {
                    purchTableUpd.update();
                }
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymMode</Name>
				<Source><![CDATA[
    protected PaymMode getCurrentPaymMode()
    {
        return this.getPurchTable().PaymMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentType</Name>
				<Source><![CDATA[
    protected SplitPaymentDocumentType_PL getDocumentType()
    {
        return SplitPaymentDocumentType_PL::Vendor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isSplitPayment</Name>
				<Source><![CDATA[
    protected boolean isSplitPayment()
    {
        return this.getCurrentSplitPayment() == NoYes::Yes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SplitPaymentMandatoryRuleEngine_Purchase construct()
    {
        return new SplitPaymentMandatoryRuleEngine_Purchase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymModeSpec</Name>
				<Source><![CDATA[
    protected PaymSpec getCurrentPaymModeSpec()
    {
        return this.getPurchTable().PaymSpec;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>