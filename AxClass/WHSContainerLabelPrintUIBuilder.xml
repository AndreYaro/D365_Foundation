<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSContainerLabelPrintUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSContainerLabelPrintUIBuilder extends SysOperationAutomaticUIBuilder
{
    private DialogField userIdDlgField;
    private DialogField workerDlgField;
    private DialogField inventLocationIdDlgField;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        super();

        DialogField wMSLocationIdDlgField = this.bindInfo().getDialogField(
            this.dataContractObject(), methodStr(WHSContainerLabelPrintContract, wMSLocationId));
        wMSLocationIdDlgField.registerOverrideMethod(methodStr(
            FormStringControl, lookup), methodStr(WHSContainerLabelPrintUIBuilder, wMSLocationIdLookup), this);
        wMSLocationIdDlgField.registerOverrideMethod(methodstr(
            FormStringControl, modified), methodstr(WHSContainerLabelPrintUIBuilder, wMSLocationIdModified), this);

        inventLocationIdDlgField = this.bindInfo().getDialogField(
            this.dataContractObject(), methodStr(WHSContainerLabelPrintContract, inventLocationId));
        inventLocationIdDlgField.allowEdit(false);

        workerDlgField = this.bindInfo().getDialogField(
            this.dataContractObject(), methodStr(WHSContainerLabelPrintContract, worker));
        workerDlgField.registerOverrideMethod(methodStr(
            FormStringControl, modified), methodStr(WHSContainerLabelPrintUIBuilder, workerModified), this);
        workerDlgField.value('');

        userIdDlgField = this.bindInfo().getDialogField(
            this.dataContractObject(), methodStr(WHSContainerLabelPrintContract, userId));
        userIdDlgField.registerOverrideMethod(methodStr(
            FormStringControl, lookup), methodStr(WHSContainerLabelPrintUIBuilder, userIdLookup), this);
        userIdDlgField.value('');
        userIdDlgField.label('@WAX2764');
        
        DialogField containerIdDlgField = this.bindInfo().getDialogField(
            this.dataContractObject(), methodStr(WHSContainerLabelPrintContract, containerId));
        containerIdDlgField.visible(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>workerModified</Name>
				<Source><![CDATA[
    private boolean workerModified(FormReferenceGroupControl _worker)
    {
        userIdDlgField.value('');
        return _worker.modified();
    }

]]></Source>
			</Method>
			<Method>
				<Name>userIdLookup</Name>
				<Source><![CDATA[
    private void userIdLookup(FormStringControl _userId)
    {
        if (workerDlgField.value() != 0)
        {
            str workerId = workerDlgField.value();
            
            SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(WHSWorkUser), _userId);
            
            Query query = new Query();
            QueryBuildDataSource qbds;
            QueryBuildRange queryBuildRange;
        
            qbds = query.addDataSource(tableNum(WHSWorkUser)).addDataSource(tableNum(WHSWorker));
            qbds.joinMode(JoinMode::InnerJoin);
            qbds.addLink(fieldNum(WHSWorkUser, Worker), fieldNum(WHSWorker, RecId));

            qbds = qbds.addDataSource(tableNum(HcmWorker));
            qbds.addLink(fieldNum(WHSWorker, Worker), fieldNum(HcmWorker, RecId));
 
            queryBuildRange = qbds.addRange(fieldNum(HcmWorker, RecId));
            queryBuildRange.value(queryValue(workerId));
        
            qbds = qbds.addDataSource(tableNum(DirPerson));
            qbds.addLink(fieldNum(HcmWorker, Person), fieldNum(DirPerson, RecId));

            sysTableLookup.addLookupfield(fieldNum(WHSWorkUser, UserId));
            
            sysTableLookup.parmQuery(query);
            sysTableLookup.performFormLookup();
        }
        else
        {
            _userId.lookup();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>wMSLocationIdLookup</Name>
				<Source><![CDATA[
    private void wMSLocationIdLookup(FormStringControl _wMSLocationId)
    {
        str inventLocationIdDefaultValue = inventLocationIdDlgField.value();

        SysTableLookup wMSLocationTableLookup = 
            WMSLocation::buildDefaultLookup(_wMSLocationId, inventLocationIdDefaultValue);

        wMSLocationTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>wMSLocationIdModified</Name>
				<Source><![CDATA[
    private boolean wMSLocationIdModified(FormStringControl _wMSLocationId)
    {        
        str wMSLocationIdCurrentValue    = _wMSLocationId.valueStr();
        str inventLocationIdDefaultValue = inventLocationIdDlgField.value();

        if (WMSLocation::exist(wMSLocationIdCurrentValue, inventLocationIdDefaultValue)
                || wMSLocationIdCurrentValue == "")
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>