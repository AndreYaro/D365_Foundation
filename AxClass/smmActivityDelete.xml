<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmActivityDelete</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class <c>smmActivityDelete</c> supports Deletion of <c>smmActivities</c> records.
/// </summary>
public class smmActivityDelete
{
    smmActivities activityToDelete;
    boolean deleteSeries;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(smmActivities _activity)
    {
        // Set default values
        deleteSeries = false;
        activityToDelete = _activity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDeleteRecurringSeries</Name>
				<Source><![CDATA[
    public boolean parmDeleteRecurringSeries(boolean _deleteSeries = deleteSeries)
    {
        deleteSeries = _deleteSeries;
        return deleteSeries;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteRecurrenceActivities</Name>
				<Source><![CDATA[
    private void deleteRecurrenceActivities()
    {
        smmDeletedActivities    smmDeletedActivities;
        smmActivities           smmSeriesActivities;

        // Delete series
        update_recordset smmDeletedActivities
            setting IsEntireSeries = true
            where smmDeletedActivities.ActivityNumber                    == activityToDelete.ActivityNumber
                && smmDeletedActivities.OriginalAppointmentStartDateTime == activityToDelete.OriginalAppointmentStartDateTime
                && smmDeletedActivities.ResponsibleWorker                == activityToDelete.ResponsibleWorker;

        while select forupdate smmSeriesActivities
            where smmSeriesActivities.OutlookEntryID == activityToDelete.OutlookEntryID
                && smmSeriesActivities.RecId != activityToDelete.RecId
        {
            // Do no create insert in delete activity log for child activities
            smmSeriesActivities.OutlookEntryID = '';
            smmSeriesActivities.delete();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>delete</Name>
				<Source><![CDATA[
    public void delete()
    {
        if (!activityToDelete)
        {
            throw error("@SalesAndMarketing:ActivityRequired");
        }

        if (activityToDelete.validateDelete())
        {
            activityToDelete.delete();

            if (this.parmDeleteRecurringSeries())
            {
                this.deleteRecurrenceActivities();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>