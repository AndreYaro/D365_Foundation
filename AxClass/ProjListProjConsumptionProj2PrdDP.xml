<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjConsumptionProj2PrdDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjConsumptionProj2PrdDP</c> class provides data for the
///    <c>ProjListProjConsumptionProj2ColumnPeriod</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjConsumptionContract))
]
class ProjListProjConsumptionProj2PrdDP extends SRSReportDataProviderBase
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    TransDate               ledgerFromDate1;
    TransDate               ledgerToDate1;
    ProjListColumnData      actualVsBudget;
    ForecastModelId         forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;

    ProjListProjTrans_Consumption   listProjTrans;
    projListProjTrans2Period        listProjTrans2Period;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjConsumptionTmp      projListProjConsumptionTmp;

    ProjTable               projTable;

    QueryRun                queryRun;

    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    ProjListProjHelpClass   period1ProjHelpClass;
    ProjListProjHelpClass   period2ProjHelpClass;

    AmountMST               period1Salary;
    AmountMST               period2Salary;
    ProjQty                 period1QtyHour;
    ProjQty                 period2QtyHour;
    AmountMST               period1Total;
    AmountMST               period2Total;
    AmountMST               period1Item;
    AmountMST               period2Item;
    AmountMST               period1Expenses;
    AmountMST               period2Expenses;

    ProjId                  projId;
    ProjId                  projIdFound;
    ProjName                name;

    str                     headerLabel;
    str                     period1Label;
    str                     period2Label;
    int                     hourQtyDecimals;
    int                     valueAddedDecimals;
    int                     grossMarginDecimals;
    int                     amountDecimals;
    int                     showQtyHour;
    int                     showSalary;
    int                     showExpenses;
    int                     showItem;
    int                     showTotalCost;
    int                     reportType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculate the amounts that are needed for the report.
    /// </summary>
    void calculate()
    {
        period1ProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        period1ProjHelpClass.setParmField();

        period2ProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        period2ProjHelpClass.setParmField();

        if (amountQty == ProjAmountQty::Amount || amountQty ==ProjAmountQty::AmountQty)
        {
            period1Salary      += period1ProjHelpClass.parmProjStatement().c105_Consumption_Hour();
            period2Salary      += period2ProjHelpClass.parmProjStatement().c105_Consumption_Hour();
        }

        if (amountQty ==ProjAmountQty::Qty || amountQty ==ProjAmountQty::AmountQty)
        {
            period1QtyHour     += period1ProjHelpClass.parmProjStatement().c105_Consumption_Hour_qty();
            period2QtyHour     += period2ProjHelpClass.parmProjStatement().c105_Consumption_Hour_qty();
        }

        period1Total     += period1ProjHelpClass.parmProjStatement().c100_Total_Consumption();
        period1Item      += period1ProjHelpClass.parmProjStatement().c120_Consumption_Item();
        period1Expenses  += period1ProjHelpClass.parmProjStatement().c125_Consumption_Cost();

        period2Total     += period2ProjHelpClass.parmProjStatement().c100_Total_Consumption();
        period2Item      += period2ProjHelpClass.parmProjStatement().c120_Consumption_Item();
        period2Expenses  += period2ProjHelpClass.parmProjStatement().c125_Consumption_Cost();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks the parameters to determine whether a line should be printed.
    /// </summary>
    /// <returns>
    ///    true if the line should be printed; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(period1ProjHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should be printed and inserts it into the temporary table.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record buffer that contains the information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (listProjTransLayout.parmStateProjType() == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= listProjTransLayout.parmLevel()) ||
                (projTableLocal.levelFormat(listProjTransLayout.parmLevel())   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    period1ProjHelpClass.setName();
                    this.insertProjListprojListProjConsumptionTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            period1ProjHelpClass.setName();
            this.insertProjListprojListProjConsumptionTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjConsumptionTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the data from the <c>projListProjConsumptionTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>projListProjProfitLossTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('ProjListProjConsumptionTmp')]
    public ProjListProjConsumptionTmp getProjListProjConsumptionTmp()
    {
        select * from projListProjConsumptionTmp;

        return projListProjConsumptionTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the <c>ProjListProjTrans</c> class by using the parameters.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans2Period = projListProjTrans2Period::construct(ProjListStateType::Consumption, ProjListStateId::Project);
        listProjTrans = listProjTrans2Period.getProjListProjTrans1();
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans2Period.getProjListProjTrans1());

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);
        listProjTrans.parmtime(time);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);

        listProjTrans2Period.parmPeriod1FromDate(ledgerFromDate);
        listProjTrans2Period.parmPeriod1ToDate(ledgerToDate);
        listProjTrans2Period.parmPeriod2FromDate(ledgerFromDate1);
        listProjTrans2Period.parmPeriod2ToDate(ledgerToDate1);
        listProjTrans2Period.parmColumnData(actualVsBudget);
        listProjTrans2Period.parmForecastModelId(forecastModelId);

        listProjTrans.parmColumnData(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListprojListProjConsumptionTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListprojListProjConsumptionTmp()
    {
        projListProjConsumptionTmp.clear();

        projListProjConsumptionTmp.Id            = period1ProjHelpClass.id();
        projListProjConsumptionTmp.IdLabel       = enum2str(stateProjType);
        projListProjConsumptionTmp.Name          = period1ProjHelpClass.name();

        projListProjConsumptionTmp.QtyHour       = period1QtyHour;
        projListProjConsumptionTmp.QtyHour1      = period2QtyHour;

        projListProjConsumptionTmp.EmplCost      = listProjTransLayout.showAmountReport(period1Salary, showAmount);
        projListProjConsumptionTmp.EmplCost1     = listProjTransLayout.showAmountReport(period2Salary, showAmount);

        projListProjConsumptionTmp.CostCost      = listProjTransLayout.showAmountReport(period1Expenses, showAmount);
        projListProjConsumptionTmp.CostCost1     = listProjTransLayout.showAmountReport(period2Expenses, showAmount);

        projListProjConsumptionTmp.ItemCost      = listProjTransLayout.showAmountReport(period1Item, showAmount);
        projListProjConsumptionTmp.ItemCost1     = listProjTransLayout.showAmountReport(period2Item, showAmount);

        projListProjConsumptionTmp.TotalCost     = listProjTransLayout.showAmountReport(period1Total, showAmount);
        projListProjConsumptionTmp.TotalCost1    = listProjTransLayout.showAmountReport(period2Total, showAmount);

        // set labels
        projListProjConsumptionTmp.HeaderLabel     = headerLabel;
        projListProjConsumptionTmp.PeriodLabel     = period1Label;
        projListProjConsumptionTmp.PeriodLabel1    = period2Label;

        // set decimal values
        projListProjConsumptionTmp.DecimalHourQty     = hourQtyDecimals;
        projListProjConsumptionTmp.DecimalAmount      = amountDecimals;

        // set field visibility
        projListProjConsumptionTmp.ShowQtyHour        = showQtyHour;
        projListProjConsumptionTmp.ShowEmplCost       = showSalary;
        projListProjConsumptionTmp.ShowTotalCost      = showTotalCost;
        projListProjConsumptionTmp.ShowItemCost       = showItem;
        projListProjConsumptionTmp.ShowCostCost       = showExpenses;

        projListProjConsumptionTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks whether the amounts are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise false.
    /// </returns>
    boolean lineValue()
    {
        return ((period1Salary   != 0) ||
                (period1Total    != 0) ||
                (period1QtyHour  != 0) ||
                (period1Item     != 0) ||
                (period1Expenses != 0) ||
                (period2Salary   != 0) ||
                (period2Total    != 0) ||
                (period2QtyHour  != 0) ||
                (period2Item     != 0) ||
                (period2Expenses != 0));
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        Query query = queryRun.query();

        QueryBuildDataSource qbdsProjTable = query.dataSourceTable(tableNum(ProjTable));
        qbdsProjTable.addSortField(fieldNum(ProjTable, ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = query.dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
            PSAContractLineItemRangesHelper::addTransactionTypeRangesToDS(
                qbdsPSAContractLineItems,
                hour,
                expenses,
                false,
                item,
                false);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Internal));
            }

            if (time)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Time));
            }

            if (! (timeMaterial || fixedPrice   || investment   || internalCost || time))
            {
                QueryBuildRange rangeDummy = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
                rangeDummy.value(queryValue(SysQuery::valueEmptyString()));
            }
        }

        if (skipZero)
        {
            if (actualVsBudget == ProjListColumnData::Actual)
            {
                QueryBuildDataSource qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransPosting)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransPosting, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod1FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod1ToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod2FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod2ToDate()));
            }
            else
            {
                QueryBuildDataSource qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransBudget)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransBudget, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod1FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod1ToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans2Period.parmPeriod2FromDate()) + '..' + queryValue(listProjTrans2Period.parmPeriod2ToDate()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = queryRun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = queryRun.get(tablenum(ProjFundingSource));
        }
        period1ProjHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);

        if (! listProjTransLayout.canPrintId(period1ProjHelpClass.idNew()))
        {
            period1ProjHelpClass.setOld();

            return;
        }

        if (! firstRun && period1ProjHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        period1ProjHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(listProjTransLayout.parmLevel());
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SQL
    ///    Server Reporting Services (SSRS) at run time. This method should compute the data and populate the
    ///    data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjConsumptionContract contract = this.parmDataContract() as ProjListProjConsumptionContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();
        time = contract.parmTime();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();

        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();
        ledgerFromDate1 = contract.parmLedgerFromDate1();
        ledgerToDate1 = contract.parmLedgerToDate1();

        actualVsBudget = contract.parmActualVsBudget();
        forecastModelId = contract.parmForecastModelIdV2();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();

        reportType = contract.parmReportType();

        this.initProjListTrans();

        [showQtyHour, showSalary, showExpenses, showItem, showTotalCost] = listProjTrans.setDisplayFlags(amountQty, hour, expenses, item);
        [hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        period1Label = listProjTrans2Period.formatPeriodLabel("@SYS9308",1,DateFlags::None);
        period2Label = listProjTrans2Period.formatPeriodLabel("@SYS13183",2,DateFlags::None);
        headerLabel = listProjTrans2Period.formatSelection(showAmount);

        period1ProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                projStatement::newListProjConsumption(listProjTrans2Period.getProjListProjTrans1(),
                                    projListProjTrans2Period::columnData2ActualBudget(actualVsBudget) ,
                                    amountQty, forecastModelId),
                                stateProjType);

        period2ProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                projStatement::newListProjConsumption(listProjTrans2Period.getProjListProjTrans2(),
                                    projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                                    amountQty, forecastModelId),
                                stateProjType);

        queryRun = new SysQueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (queryRun.next())
        {
            if (stateProjType == ProjListStateProjType::FundingSource)
            {
                // set new funding source in statement classes
                period1ProjHelpClass.setParmField();
                period2ProjHelpClass.setParmField();
            }
            this.printProject();
        }

        if (this.checkPrintLine())
        {
            period1ProjHelpClass.setName();
            this.insertProjListprojListProjConsumptionTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the variables to zero.
    /// </summary>
    void setFieldZero()
    {
        period1Salary      = 0;
        period1QtyHour     = 0;
        period1Total       = 0;
        period1Item        = 0;
        period1Expenses    = 0;

        period2Salary      = 0;
        period2QtyHour     = 0;
        period2Total       = 0;
        period2Item        = 0;
        period2Expenses    = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        queryRun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                queryRun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                queryRun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                queryRun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>