<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerTurnoverAndBalanceDPInsertMainAccountQuery</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LedgerTurnoverAndBalanceDPInsertMainAccountQuery</c> class creates queries related to the <c>LedgerTurnoverAndBalanceDP</c> class.
/// </summary>
public final class LedgerTurnoverAndBalanceDPInsertMainAccountQuery
{
    private MainAccount mainAccount;
    private DimensionAttributeValueTotallingCriteria totallingCriteria;
    private RecId dimensionAttributeValueRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    private void initialize(MainAccount _mainAccount, RecId _dimensionAttributeValueRecId)
    {
        mainAccount = _mainAccount;
        dimensionAttributeValueRecId = _dimensionAttributeValueRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createSearchObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Selects records from the <c>MainAccount</c> table.
    /// </summary>
    /// <param name = "_mainAccount">
    /// The <c>MainAccount</c> buffer.
    /// </param>
    /// <param name = "_dimensionAttributeValueRecId">
    /// The <c>DimensionAttributeValue</c> record Id.
    /// </param>
    /// <returns>
    /// The selected records.
    /// </returns>
    internal static SysDaSearchObject createSearchObject(MainAccount _mainAccount, RecId _dimensionAttributeValueRecId)
    {
        LedgerTurnoverAndBalanceDPInsertMainAccountQuery ledgerTurnoverAndBalanceDPInsertMainAccountQuery = new LedgerTurnoverAndBalanceDPInsertMainAccountQuery();

        ledgerTurnoverAndBalanceDPInsertMainAccountQuery.initialize(_mainAccount, _dimensionAttributeValueRecId);

        SysDaQueryObject query = ledgerTurnoverAndBalanceDPInsertMainAccountQuery.createQueryObject();
        SysDaSearchObject searchObject = new SysDaSearchObject(query);

        return searchObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject createQueryObject()
    {
        SysDaQueryObject mainAccountQueryObject = this.addMainAccountFieldsToQueryObjectWhereClause();

        SysDaQueryObject dimensionAttributeValueQueryObject = new SysDaQueryObject(totallingCriteria);
        this.addDimensionAttributeValueTotallingCriteriaFieldsToQueryObjectWhereClause(dimensionAttributeValueQueryObject);

        mainAccountQueryObject.joinClause(SysDaJoinKind::ExistsJoin, dimensionAttributeValueQueryObject);

        return mainAccountQueryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addMainAccountFieldsToQueryObjectWhereClause</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the where clause expression to a query.
    /// </summary>
    /// <returns>
    /// The query to select records.
    /// </returns>
    [Wrappable(true)]
    protected final SysDaQueryObject addMainAccountFieldsToQueryObjectWhereClause()
    {
        SysDaQueryObject queryObject = new SysDaQueryObject(mainAccount);
        queryObject.groupByClause().add(fieldStr(MainAccount, RecId));

        queryObject.whereClause(new SysDaAndExpression(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, LedgerChartOfAccounts)), new SysDaValueExpression(LedgerChartOfAccounts::current())),
            new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::Equity))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::Liability)))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::Asset)))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::BalanceSheet)))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::Expense)))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::Revenue)))
            .or(new SysDaEqualsExpression(new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, Type)), new SysDaValueExpression(DimensionLedgerAccountType::ProfitAndLoss)))));

        return queryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addDimensionAttributeValueTotallingCriteriaFieldsToQueryObjectWhereClause</Name>
				<Source><![CDATA[
    private void addDimensionAttributeValueTotallingCriteriaFieldsToQueryObjectWhereClause(SysDaQueryObject _queryObject)
    {
        _queryObject.whereClause(new SysDaAndExpression(new SysDaEqualsExpression(
            new SysDaFieldExpression(totallingCriteria, fieldStr(DimensionAttributeValueTotallingCriteria, DimensionAttributeValue)), new SysDaValueExpression(dimensionAttributeValueRecId)),
            new SysDaLessThanOrEqualsExpression(new SysDaFieldExpression(totallingCriteria, fieldStr(DimensionAttributeValueTotallingCriteria, FromValue)), new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, MainAccountId))))
            .and(new SysDaGreaterThanOrEqualsExpression(new SysDaFieldExpression(totallingCriteria, fieldStr(DimensionAttributeValueTotallingCriteria, ToValue)), new SysDaFieldExpression(mainAccount, fieldStr(MainAccount, MainAccountId)))));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>