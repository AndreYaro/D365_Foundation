<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsCWFormCtrl_SalesTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Classes adds a new layer below the PdsCWFormCtrl to allow the independent call
/// to valiadtion of CW qtys
/// </summary>
/// <remarks>
/// This class extends the parent PdsCWFormCtrl class. The class will call the
/// standard catch weight validation
/// </remarks>
class PdsCWFormCtrl_SalesTable extends PdsCWFormCtrl_Std
{
    boolean itemIdModified;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dataSourceInitPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update form datasource after 'active' call for datasource
    /// </summary>
    /// <param name="_buffer">record buffer</param>
    /// <remarks>
    /// Method also calls the Form control button manager.
    /// </remarks>
    public void dataSourceInitPost(Common _buffer)
    {
        super(_buffer);

        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWInvoicedInTotal));
        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWDeliveredInTotal));
        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWPickedInTotal));
        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWReservedPhysical));
        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWReservedOnOrder));
        formDS.cacheAddMethod(tablemethodstr(SalesLine, pdsCWOnOrder));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Complete the standard field modified functionality
    /// </summary>
    /// <param name="_fieldId">
    /// FieldId of modified field
    /// </param>
    public void fieldModified(fieldId _fieldId)
    {
        itemIdModified = _fieldId == fieldnum(SalesLine,ItemId);
        super(_fieldId);
        itemIdModified = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new isntance of the PdsCWFormCtrl_SalesTable class.
    /// </summary>
    /// <remarks>
    /// <syntax>
    /// protected void new()
    /// </syntax>
    /// </remarks>
    protected void new()
    {
        super();

        this.parmFieldIds(PdsCWValidation::fieldMapFromContainers(
                [[fieldnum(SalesLine, SalesQty),
                  fieldnum(SalesLine, PdsCWQty),
                  false,
                  fieldnum(SalesLine, SalesUnit)],
                 [fieldnum(SalesLine, SalesDeliverNow),
                  fieldnum(SalesLine, PdsCWInventDeliverNow),
                  false,
                  fieldnum(SalesLine, SalesUnit)]
                ]
                ));
        this.parmItemFieldId(fieldnum(SalesLine, ItemId));
        this.parmUnitMethodStr(tablemethodstr(SalesLine, pdsCWUnitId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update properties for form fields
    /// </summary>
    /// <remarks>
    /// Update the Allow edit and skip properties of the catch weight quantity field
    /// </remarks>
    public void updateDesign()
    {
        SalesLine           salesLine = buffer;
        boolean             isCWItem;
        boolean             stdIsEditable;
        MapEnumerator       iter;
        PdsCWQtyFieldPair   fieldPair;

        isCWItem = PdsGlobal::pdsIsCWItem(cwValidation.itemId())
                    && salesLine.type().pdsIsCWItemAllowed();
        iter = cwValidation.parmFieldIds().getEnumerator();
        while (iter.moveNext())
        {
            fieldPair = iter.currentValue();
            stdIsEditable = formDS.object(fieldPair.parmQtyFieldId()).allowEdit();
            if (itemIdModified || stdIsEditable)
            {
                this.enableDSField(fieldPair.parmCWFieldId(), isCWItem);
                this.enableDSField(fieldPair.parmQtyFieldId(), !isCWItem
                    || fieldPair.parmAllowQtyEdit());
            }
            else
            {
                this.enableDSField(fieldPair.parmCWFieldId(), false);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a new instance of the PdsCWFormCtrl_SalesTable class.
    /// </summary>
    /// <returns>
    /// New instance of the PdsCWFormCtrl_SalesTable class.
    /// </returns>
    /// <remarks>
    /// <syntax>
    /// public static PdsCWFormCtrl_SalesTable construct()
    /// </syntax>
    /// </remarks>
    public static PdsCWFormCtrl_SalesTable construct()
    {
        return new PdsCWFormCtrl_SalesTable();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>