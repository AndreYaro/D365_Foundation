<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOMConsistOfUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BOMConsistOfUIBuilder</c> class is the UI Builder class for the associated SSRS Report.
/// </summary>
public class BOMConsistOfUIBuilder extends SrsReportDataContractUIBuilder
{
    BOMConsistOfContract    bomContract;
    DialogGroup             dialogSearchIntervalGroup;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Override this method in order to initialize the dialog fields after the fields are built.
    /// </summary>
    public void build()
    {
        FormBuildGroupControl       formBuildGroupControl;
        boolean                     versionsEnabled = isConfigurationkeyEnabled(configurationkeynum(BOMVersion));
        formBuildGroupControl = dialog.curFormGroup();
        formBuildGroupControl.columns(2);

        // Gets the underlying BOMContract, since this UI Builder is associated to the Contract by the attribute on contract.
        bomContract = this.dataContractObject() as BOMConsistOfContract;
        if (isConfigurationkeyEnabled(configurationKeyNum(COOCertification)))
        {
            this.addDialogField(methodStr(BOMConsistOfContract, parmDestinationCountryRegionId), bomContract);
        }
                       
        this.addDialogField(methodstr(BOMConsistOfContract, parmShow), bomContract);

        if (versionsEnabled)
        {
            this.addDialogField(methodstr(BOMConsistOfContract, parmDisplayPrinciple), bomContract);
        }

        this.addDialogField(methodstr(BOMConsistOfContract,parmMaxLevel), bomContract);

        dialogSearchIntervalGroup = dialog.addGroup("@SYS25853");
        dialogSearchIntervalGroup.frameOptionButton(FormFrameOptionButton::Check);
        dialogSearchIntervalGroup.optionValue(bomContract.parmSearchInterval());

        this.addDialogField(methodstr(BOMConsistOfContract, parmSearchIntervalWithLinesOnly), bomContract);
        this.addDialogField(methodstr(BOMConsistOfContract, parmFromDate), bomContract);
        this.addDialogField(methodstr(BOMConsistOfContract, parmToDate), bomContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    This method in used to get the dialog field values.
    /// </summary>
    public void getFromDialog()
    {
        // Call super. This will get the dialog fields and populate contract.
        super();

        // now get searchInterval flag. Since this is not added as a DialogField and put on group itself
        // framework will not know how to populate it automatically. So we will explicitly get it and set it on contract.
        bomContract.parmSearchInterval(dialogSearchIntervalGroup.optionValue());

        if (!bomContract.parmSearchInterval())
        {
            bomContract.parmSearchIntervalWithLinesOnly(false);
            bomContract.parmFromDate(datenull());
            bomContract.parmToDate(datenull());
            bomContract.parmCOOFeatureAndConfigKey(isConfigurationkeyEnabled(configurationKeyNum(COOCertification)));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>