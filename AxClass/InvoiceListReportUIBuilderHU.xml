<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InvoiceListReportUIBuilderHU</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The user interface builder of Invoice list report.
/// </summary>
class InvoiceListReportUIBuilderHU extends SysOperationAutomaticUIBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        DialogField dialogFromInvoiceId,
                    dialogToInvoiceId,
                    dialogFromDate,
                    dialogToDate,
                    dialogErBinding;

        super();

        dialogErBinding = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InvoiceListReportContractHU, parmERBinding));
        dialogErBinding.fieldControl().mandatory(true);
        this.overrideLookup(dialogErBinding);

        dialogFromInvoiceId = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InvoiceListReportContractHU, parmFromInvoiceId));
        this.overrideModified(dialogFromInvoiceId);

        dialogToInvoiceId = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InvoiceListReportContractHU, parmToInvoiceId));
        this.overrideModified(dialogToInvoiceId);

        dialogFromDate = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InvoiceListReportContractHU, parmFromDate));
        this.overrideModified(dialogFromDate);

        dialogToDate = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InvoiceListReportContractHU, parmToDate));
        this.overrideModified(dialogToDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>overrideLookup</Name>
				<Source><![CDATA[
    private void overrideLookup(DialogField _field)
    {
        _field.control().registerOverrideMethod(
            methodStr(FormReferenceControl, lookupReference),
            methodStr(InvoiceListReportUIBuilderHU, erBinding_lookup),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>erBinding_lookup</Name>
				<Source><![CDATA[
    private Common erBinding_lookup(FormReferenceControl _formControl)
    {
        return ERFormatMappingTable::lookup(_formControl, InvoiceListReportContractHU::parmModelName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>overrideModified</Name>
				<Source><![CDATA[
    private void overrideModified(DialogField _field)
    {
        _field.control().registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(InvoiceListReportUIBuilderHU, filterControlModified),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>filterControlModified</Name>
				<Source><![CDATA[
    private boolean filterControlModified(FormControl _callerControl)
    {
        this.getFromDialog();
        Query query = this.dataContractObject().getQuery();

        this.updateQueryBasedOnFilterControl(
            query, tableNum(CustInvoiceJour), fieldNum(CustInvoiceJour, InvoiceId), fieldNum(CustInvoiceJour, InvoiceDate));
        this.updateQueryBasedOnFilterControl(
            query, tableNum(ProjInvoiceJour), fieldNum(ProjInvoiceJour, ProjInvoiceId), fieldNum(ProjInvoiceJour, InvoiceDate));
        this.updateQueryBasedOnFilterControl(
            query, tableNum(CzCustAdvanceInvoiceTable), fieldNum(CzCustAdvanceInvoiceTable, InvoiceId), fieldNum(CzCustAdvanceInvoiceTable, InvoiceDate));
        this.updateQueryBasedOnFilterControl(
            query, tableNum(CzVendAdvanceInvoiceTable), fieldNum(CzVendAdvanceInvoiceTable, InvoiceId), fieldNum(CzVendAdvanceInvoiceTable, InvoiceDate));

        this.dataContractObject().setQuery(query);
        this.controller().putToDialog();

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQueryBasedOnFilterControl</Name>
				<Source><![CDATA[
    private void updateQueryBasedOnFilterControl(Query _query, int _tableNum, int _fieldNumInvoiceId, int _fieldNumInvoiceDate)
    {
        QueryBuildDataSource qbds = _query.dataSourceTable(_tableNum);
        findOrCreateRange_W(qbds, _fieldNumInvoiceId, SysQuery::range(this.dataContractObject().parmFromInvoiceId(), this.dataContractObject().parmToInvoiceId()));
        findOrCreateRange_W(qbds, _fieldNumInvoiceDate, SysQuery::range(this.dataContractObject().parmFromDate(), this.dataContractObject().parmToDate()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>