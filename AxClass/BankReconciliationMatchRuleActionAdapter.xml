<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankReconciliationMatchRuleActionAdapter</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class delegates the actions on bank reconciliation matching rule set form.
/// </summary>
public class BankReconciliationMatchRuleActionAdapter
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkArgsFailed</Name>
				<Source><![CDATA[
    /// <summary>
    /// Throws the exception for the failure of the parameter validation.
    /// </summary>
    /// <param name="_functionName">
    /// The function name.
    /// </param>
    /// <exception cref="Exception::Error">
    /// The error of the parameter validation.
    /// </exception>
    private static void checkArgsFailed(MethodName _functionName)
    {
        throw error(Error::wrongUseOfFunction(_functionName));
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        if (!args)
        {
            BankReconciliationMatchRuleActionAdapter::checkArgsFailed(funcName());
        }

        Common record = args.record();
        if (!record
            || record.RecId == 0
            || record.TableId != tableNum(BankReconciliationMatchRule))
        {
            BankReconciliationMatchRuleActionAdapter::checkArgsFailed(funcName());
        }

        BankReconciliationMatchRule matchRule = record;

        EnumId paramEnumType = args.parmEnumType();

        if (paramEnumType != enumNum(BankReconciliationMatchRuleActionType))
        {
            BankReconciliationMatchRuleActionAdapter::checkArgsFailed(funcName());
        }

        BankReconciliationMatchRuleActionType paramEnum = args.parmEnum();

        switch (paramEnum)
        {
            case BankReconciliationMatchRuleActionType::Activate:
                matchRule.activate();
                break;

            case BankReconciliationMatchRuleActionType::Deactivate:
                matchRule.deactivate();
                break;

            default:
                BankReconciliationMatchRuleActionAdapter::checkArgsFailed(funcName());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>