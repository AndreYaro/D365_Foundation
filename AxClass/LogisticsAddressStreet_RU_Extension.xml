<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsAddressStreet_RU_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of table <c>LogisticsAddressStreet_RU</c>
/// </summary>
[ExtensionOf(tableStr(LogisticsAddressStreet_RU))]
final class LogisticsAddressStreet_RU_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupStreetId_RU_Extension</Name>
				<Source><![CDATA[
    protected client static QueryBuildDataSource lookupStreetId_RU_Extension(QueryBuildDataSource _qbds, FieldId _recIdNum, FieldId _tableIdNum)
    {
        next lookupStreetId_RU_Extension(_qbds, _recIdNum, _tableIdNum);
        
        return LogisticsAddressNotFoundRecords_RU::filterQueryByLog(_qbds, _recIdNum, _tableIdNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>propertiesOKTMO_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays or sets OKTMO for address element
    /// </summary>
    /// <param name="_set">
    /// true when editing; false when viewing.
    /// </param>
    /// <param name="_oktmo">
    /// New value
    /// </param>
    /// <returns>
    /// Current (edited if _set is true) value
    /// </returns>
    [SysClientCacheDataMethodAttribute(true)]
    edit OKTMO_RU propertiesOKTMO_RU(
        boolean _set,
        OKATO_RU _oktmo)
    {
        if (!this.RecId && _set)
        {
            this.insert();
        }
        return LogisticsAddressFiasGuidCodes_RU::parmOKTMOByAddressTableId(this, _oktmo, _set);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupStreetIdWithGARLevel_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens lookup form for streets using PK reference
    /// </summary>
    /// <param name="_ctrlStreet">
    /// Street reference control
    /// </param>
    /// <param name="_countryRegionId">
    /// Related country/region ID
    /// </param>
    /// <param name="_stateId">
    /// Related state ID
    /// </param>
    /// <param name="_countyId">
    /// Related county ID
    /// </param>
    /// <param name="_settlementId">
    /// Related Urban/Rural settlement ID
    /// </param>
    /// <param name="_cityId">
    /// Related city ID
    /// </param>
    /// <param name="_localityId">
    /// Related locality ID
    /// </param>
    /// <param name="_districtId">
    /// Related district ID
    /// </param>
    /// <returns>
    /// Street record selected by user
    /// </returns>
    internal client static LogisticsAddressStreet_RU lookupStreetIdWithGARLevel_RU(
        FormReferenceControl _ctrlStreet,
        LogisticsAddressCountryRegionId _countryRegionId,
        LogisticsAddressStateId _stateId,
        LogisticsAddressCountyId _countyId,
        LogisticsAddressCityRecId _settlementId,
        LogisticsAddressCityRecId _cityId,
        LogisticsAddressCityRecId _localityId,
        LogisticsAddressDistrictRecId _districtId)
    {
        LogisticsAddressCityRecId cityTableRecId = _settlementId;
        if (_cityId)
        {
            cityTableRecId = _cityId;
        }
        if (_localityId)
        {
            cityTableRecId = _localityId;
        }

        SysReferenceTableLookup lookup = SysReferenceTableLookup::newParameters(
            tableNum(LogisticsAddressStreet_RU),
            _ctrlStreet,
            true);

        lookup.addLookupfield(fieldNum(LogisticsAddressStreet_RU, Name));
        lookup.addLookupfield(fieldNum(LogisticsAddressStreet_RU, Description));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressStreet_RU, fullName));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressStreet_RU, propertiesZipCode));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressStreet_RU, propertiesGniCode));

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(LogisticsAddressStreet_RU));
        qbds = LogisticsAddressStreet_RU::lookupStreetId_RU_Extension(qbds, fieldNum(LogisticsAddressStreet_RU, RecId), fieldNum(LogisticsAddressStreet_RU, TableId));

        qbds.addRange(fieldNum(LogisticsAddressStreet_RU, CountryRegion)).value(queryValue(_countryRegionId));
        qbds.addRange(fieldNum(LogisticsAddressStreet_RU, State)).value(queryValue(_stateId));
        qbds.addRange(fieldNum(LogisticsAddressStreet_RU, County)).value(queryValue(_countyId));
        qbds.addRange(fieldNum(LogisticsAddressStreet_RU, City)).value(queryValue(cityTableRecId));
        qbds.addRange(fieldNum(LogisticsAddressStreet_RU, District)).value(queryValue(_districtId));

        lookup.parmQuery(query);

        LogisticsAddressStreet_RU selectedRecord = lookup.performFormLookup();

        return selectedRecord;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>