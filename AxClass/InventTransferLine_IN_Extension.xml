<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransferLine_IN_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
#ISOCountryRegionCodes
/// <summary>
/// The <c>InventTransferLine_IN_Extension</c> class is an extension class for the InventTransferLine table.
/// </summary>
[ExtensionOf(tableStr(InventTransferLine))]
internal final class InventTransferLine_IN_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromInventTableModule</Name>
				<Source><![CDATA[
    public void initFromInventTableModule(InventTableModule _inventTableModule)
    {
        next initFromInventTableModule(_inventTableModule);

        if (this.inventTransferTable().StockTransferCostPriceHandlingImprovement_IN)
        {
            InventTransferLine_IN inventTransferLineIN = this.inventTransferLine_IN();

            inventTransferLineIN.UnitId = this.UnitId;

            this.packInventTransferLine_IN(inventTransferLineIN);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initDataDimensionsModified</Name>
				<Source><![CDATA[
    public boolean initDataDimensionsModified()
    {
        boolean result = next initDataDimensionsModified();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            InventTransferTable inventTransferTable = this.inventTransferTable();
            if (inventTransferTable.TransferType_IN == TransferType_IN::StockTransfer)
            {
                if (this.inventTransferTable().StockTransferCostPriceHandlingImprovement_IN)
                {
                    InventTransferLine_IN inventTransferLineIN = this.inventTransferLine_IN();
                    inventTransferLineIN.recalculateStockTransferValue(this, this.inventDim());
                    this.packInventTransferLine_IN(inventTransferLineIN);
                }
                else
                {
                    this.initItemIdModifiedTransferLine_IN();
                }
            }
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInventTransferTable</Name>
				<Source><![CDATA[
    public void initFromInventTransferTable(InventTransferTable _inventTransferTable, boolean _initShipReceiveDates)
    {
        next initFromInventTransferTable(_inventTransferTable, _initShipReceiveDates);

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            InventTransferLine_IN inventTransferLineIN = this.inventTransferLine_IN();
            inventTransferLineIN.initFromInventTransferTable(_inventTransferTable);
            this.packInventTransferLine_IN(inventTransferLineIN);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>