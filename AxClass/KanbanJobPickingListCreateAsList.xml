<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>KanbanJobPickingListCreateAsList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>KanbanJobPickingListCreateBase</c> class creates a picking list for a kanban job.
/// </summary>
/// <remarks>
///    Picking list records are stored in a <c>recordSortedList</c> list of <c>KanbanJobPickingList</c>
///    records.
/// </remarks>
public class KanbanJobPickingListCreateAsList extends KanbanJobPickingListCreateBase
{
    RecordSortedList    recordSortedList;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the class instance variables.
    /// </summary>
    /// <returns>
    /// true if the variables were initialized; otherwise, false.
    /// </returns>
    public boolean init()
    {
        boolean result = super();

        recordSortedList = new RecordSortedList(tableNum(KanbanJobPickingList));
        recordSortedList.sortOrder(fieldNum(KanbanJobPickingList,Job));
        recordSortedList.sortOrder(fieldNum(KanbanJobPickingList,bom));

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Inserts a <c>KanbanJobPickingList</c> record into the result list.
    /// </summary>
    /// <param name="_kanbanJobPickingList">
    ///    The <c>KanbanJobPickingList</c> record to insert.
    /// </param>
    protected void insert(KanbanJobPickingList _kanbanJobPickingList)
    {
        _kanbanJobPickingList.RecId = recordSortedList.len()+1;
        recordSortedList.ins(_kanbanJobPickingList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>kanbanJobPickingListAsList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the kanban job picking list in a <c>RecordSortedList</c> object.
    /// </summary>
    /// <returns>
    /// A <c>RecordSortedList</c> object that holds <c>KanbanJobPickingList</c> records.
    /// </returns>
    public RecordSortedList kanbanJobPickingListAsList()
    {
        if (!recordSortedList)
        {
            this.run();
        }

        return recordSortedList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        private static KanbanJobPickingListCreateAsList construct()
    {
        return new KanbanJobPickingListCreateAsList();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new instance of the <c>KanbanJobPickingListCreateAsList</c> class.
    /// </summary>
    /// <param name="_kanbanJob">
    ///    The <c>KanbanJob</c> table buffer that is used to initialize the class.
    /// </param>
    /// <param name="_kanban">
    ///    The <c>Kanban</c> table buffer that is used to initialize the class.
    /// </param>
    /// <param name="_serieSize">
    ///    A real value that holds the order size that applies for this calculation.
    /// </param>
    /// <param name="_isRecalculation">
    ///    A Boolean value that indicates if the picking list is recalculated.
    /// </param>
    /// <returns>
    ///    An instance of the <c>KanbanJobPickingListCreateAsList</c> class.
    /// </returns>
    /// <remarks>
    /// The forceCreatePickingList argument has been removed.
    /// </remarks>
    public static KanbanJobPickingListCreateAsList newStandard(
        KanbanJob _kanbanJob,
        Kanban    _kanban                 = _kanbanJob.kanban(),
        Qty       _serieSize              = _kanbanJob.QuantityOrdered,
        boolean   _isRecalculation        = false)
    {
        KanbanJobPickingListCreateAsList  kanbanJobPickingListCreateAsList = KanbanJobPickingListCreateAsList::construct();

        kanbanJobPickingListCreateAsList.parmKanbanJob(_kanbanJob);
        kanbanJobPickingListCreateAsList.parmKanban(_kanban);
        kanbanJobPickingListCreateAsList.parmSerieSize(_serieSize);
        kanbanJobPickingListCreateAsList.parmIsRecalculation(_isRecalculation);

        return kanbanJobPickingListCreateAsList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS131655";
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>