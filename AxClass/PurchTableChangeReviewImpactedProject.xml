<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableChangeReviewImpactedProject</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchTableChangeReviewImpactedProject</c> represents a project which has been impacted by the PO change.
/// </summary>
internal final class PurchTableChangeReviewImpactedProject extends PurchTableChangeReviewImpactedOrder
{
    private ProjName projectName;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>impactDescription</Name>
				<Source><![CDATA[
    public str impactDescription()
    {
        return strFmt('%1 - %2 - %3',
            this.referenceType,
            this.referenceId,
            this.projectName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef</Name>
				<Source><![CDATA[
    public void jumpRef(Object _caller)
    {
        ProjTable projTable = ProjTable::findRecId(this.parmReferenceRecId());

        Args args = new Args();
        args.record(projTable);
        args.lookupRecord(projTable);
        args.caller(_caller);

        MenuFunction menuFunction = new MenuFunction(menuItemDisplayStr(ProjTable), MenuItemType::Display);

        menuFunction.copyCallerQuery(CopyCallerQuery::No);

        FormRun formRun = menuFunction.create(args);
        formRun.run();
        formRun.wait();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromValues</Name>
				<Source><![CDATA[
    internal static PurchTableChangeReviewImpactedProject newFromValues(
        ProjId              _projectId,
        InventQty           _requiredQty,
        PurchLineDlvDate    _requiredOnDate)
    {
        PurchTableChangeReviewImpactedProject impactedProject = new PurchTableChangeReviewImpactedProject();

        ProjTable projTable = ProjTable::find(_projectId);

        impactedProject.referenceType = "@SYS4534"; // 'Project'.
        impactedProject.referenceId = _projectId;
        impactedProject.referenceRecId = projTable.RecId;
        impactedProject.impactedOrderType = PurchTableChangeReviewImpactedOrderType::Project;
        impactedProject.requiredQty = _requiredQty;
        impactedProject.requiredOnDate = _requiredOnDate;
        impactedProject.customerAccount = projTable.CustAccount;

        // Fields specific to projects.
        impactedProject.projectName = projTable.Name;

        return impactedProject;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>