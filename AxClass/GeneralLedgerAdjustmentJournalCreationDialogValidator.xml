<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GeneralLedgerAdjustmentJournalCreationDialogValidator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Static class contains validation helpers used on implementations of <c>GeneralLedgerAdjustmentIJournalCreationDialog</c>.
/// </summary>
internal static class GeneralLedgerAdjustmentJournalCreationDialogValidator
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateJournalNameIsValidForAdjustmentJournalCreation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates a given journal name and data area for adjustment journal creation.
    /// </summary>
    /// <param name = "_selectedJournalNameId">The journal name.</param>
    /// <param name = "_journalDataArea">The journal data area.</param>
    /// <returns>True if the journal name is valid for adjustment journal creation; else, false.</returns>
    internal static boolean validateJournalNameIsValidForAdjustmentJournalCreation(
        JournalNameId _selectedJournalNameId,
        DataAreaId _journalDataArea)
    {
        boolean isValidJournalName = true;

        // Adjustment journal creation dialogs can be opened in a global scope, so change to the specified
        // data area before validating the journal name
        changecompany (_journalDataArea)
        {
            LedgerJournalName selectedJournalName = LedgerJournalName::find(_selectedJournalNameId);

            if (selectedJournalName)
            {
                isValidJournalName =
                    GeneralLedgerAdjustmentJournalCreationDialogValidator::validateJournalNameVoucherAllocation(selectedJournalName);
            }
            else
            {
                isValidJournalName = checkFailed("@GeneralLedgerAdjustment:JournalCreationDialogValidationJournalNameDoesNotExist");
            }
        }

        return isValidJournalName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateJournalNameVoucherAllocation</Name>
				<Source><![CDATA[
    private static boolean validateJournalNameVoucherAllocation(LedgerJournalName _selectedJournalName)
    {
        boolean isJournalNameVoucherAllocationValid = true;

        if (!_selectedJournalName.VoucherAllocatedAtPosting)
        {
            isJournalNameVoucherAllocationValid = checkFailed("@GeneralLedgerAdjustment:JournalCreationDialogValidationJournalNameNoVoucherAtPosting");
        }

        return isJournalNameVoucherAllocationValid;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>