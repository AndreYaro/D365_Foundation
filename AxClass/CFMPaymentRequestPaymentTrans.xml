<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestPaymentTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
abstract class cfmPaymentRequestPaymentTrans extends RunBase
{
    CFMPaymentRequestTable requestTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CFMPaymentRequestPaymentTrans construct(CFMPaymentRequestTable _paymentRequest)
    {
        CFMAccountTypeAttribute extensionAttribute = new CFMAccountTypeAttribute(_paymentRequest.AccountType);
        CFMPaymentRequestPaymentTrans requestPaymentTrans = SysExtensionAppClassFactory::getClassFromSysAttribute(classStr(CFMPaymentRequestPaymentTrans), extensionAttribute);

        if (!requestPaymentTrans)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        requestPaymentTrans.parmPaymentRequestTable(_paymentRequest);

        return requestPaymentTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (!_args || !_args.record() || _args.record().TableId != tableNum(CFMPaymentRequestTable))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        CFMPaymentRequestPaymentTrans paymentTransCaller = CFMPaymentRequestPaymentTrans::construct(_args.record());

        paymentTransCaller.runOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>findPaymentSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Return value RecId of payment source trans record for the payment request.
    /// </summary>
    /// <param name = "_refTableId">Source trans table's reference.</param>
    /// <returns>Return Id of payment source trans's record.</returns>
    public RefRecId findPaymentSource(TableId _refTableId)
    {
        return CFMPaymentRequestPaymentLink::findPaymentSourceByPaymentRequest(requestTable.RecId, _refTableId).RefRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymentRequestTable</Name>
				<Source><![CDATA[
    public CFMPaymentRequestTable parmPaymentRequestTable(CFMPaymentRequestTable _requestTable = requestTable)
    {
        requestTable = _requestTable;

        return requestTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>paymentMenuItemName</Name>
				<Source><![CDATA[
    abstract protected str paymentMenuItemName()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        Args args = new Args();

        MenuFunction displayFunction = new MenuFunction(this.paymentMenuItemName(), MenuItemType::Display);

        args.caller(this);
        args.record(requestTable);

        displayFunction.run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>