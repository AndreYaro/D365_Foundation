<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjInvoiceJournal_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProjInvoiceJournal_ApplicationSuite_Extension</c> class is an extension class which extends the <c>ProjInvoiceJournal</c> form initialization functionality.
/// </summary>
[ExtensionOf(formStr(ProjInvoiceJournal))]
public final class ProjInvoiceJournal_ApplicationSuite_Extension
{
    #ISOCountryRegionCodes

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();

        if (!ProjFeatureStateHelper::isInvoiceFormsHeaderLineViewFeatureEnabled())
        {
            this.visibleSendButtons();

            this.setFieldVisibility();

            if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoEG, #isoPL]))
            {
                FormButtonControl invocieUUIDButton = this.design().controlName(formControlStr(ProjInvoiceJournal, EnterInvoiceUUIDButton)) as FormButtonControl;
                invocieUUIDButton.registerOverrideMethod(methodStr(FormButtonControl, clicked), methodStr(ProjInvoiceJournal_ApplicationSuite_Extension, EnterInvoiceUUIDButton_OnClicked), this);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>EnterInvoiceUUIDButton_OnClicked</Name>
				<Source><![CDATA[
    private void EnterInvoiceUUIDButton_OnClicked(FormButtonControl sender)
    {
        Dialog dialog = new Dialog("@BusinessDocumentSubmissionService:EnterSubmissionUUIDDialogCaption");
        DialogField field = dialog.addField(extendedTypeStr(UUID), "@BusinessDocumentSubmissionService:SubmissionUUID");
        field.value(projInvoiceJour.SubmissionUUID_EG);
        dialog.run();
        if (dialog.closedOk())
        {
            projInvoiceJour.SubmissionUUID_EG = field.value();
            ProjInvoiceJour_ds.write();
            ProjInvoiceJour_ds.refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>visibleSendButtons</Name>
				<Source><![CDATA[
    private void visibleSendButtons()
    {
        if (BusinessDocumentParameters::isAnyFeatureForCountryEnabled())
        {
            FormMenuButtonControl sendMenuButton = this.design().controlName(formControlStr(ProjInvoiceJournal, MenuSendXML)) as FormMenuButtonControl;

            if (sendMenuButton)
            {
                sendMenuButton.visible(false);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldVisibility</Name>
				<Source><![CDATA[
    private void setFieldVisibility()
    {
        FormDataObject taxIdFormDataObject = this.dataSource(tableStr(ProjInvoiceJour)).object(fieldNum(ProjInvoiceJour, TaxId));
        taxIdFormDataObject.visible(FeatureStateProvider::isFeatureEnabled(CompanyTaxRegistrationFeature::instance()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>