<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
  <Name>LedgerYearAccountDiskBEL</Name>
  <SourceCode>
    <Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// This class handles export of ledger accounting data in Belgium ACCON format.
/// </summary>
class LedgerYearAccountDiskBEL extends RunBaseBatch implements BatchRetryable
{
    ERFormatMappingId erBinding;
    DialogField dialogErBinding;

    const ModelName modelName = "Ledger accounting reports";

}
]]></Declaration>
    <Methods>
      <Method>
        <Name>main</Name>
        <Source><![CDATA[
    static void main(Args args)
    {
        LedgerYearAccountDiskBEL ledgerYearAccountDiskBEL;

        ledgerYearAccountDiskBEL = new LedgerYearAccountDiskBEL();
        ledgerYearAccountDiskBEL.erBinding = ledgerYearAccountDiskBEL.getERFormatMappingRecId();

        if (ledgerYearAccountDiskBEL.erBinding || ledgerYearAccountDiskBEL.prompt())
        {
            ledgerYearAccountDiskBEL.runOperation();
        }
    }

]]></Source>
      </Method>
      <Method>
        <Name>dialog</Name>
        <Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be built by using the <c>Dialog</c> class or by using a class that is created in the
    ///    Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        Dialog      dialog = super();

        dialog.caption("@SYS62941");

        dialog.addGroup("@AccountsReceivable:ElectronicReporting");
        dialogErBinding = dialog.addFieldValue(extendedTypeStr(ERFormatMappingId), erBinding);
        dialogErBinding.control().registerOverrideMethod(methodStr(FormReferenceControl, lookupReference),
            methodStr(LedgerYearAccountDiskBEL, erBinding_lookup),
            this);
        dialogErBinding.control().mandatory(true);

        return dialog;
    }

]]></Source>
      </Method>
      <Method>
        <Name>getFromDialog</Name>
        <Source><![CDATA[
    /// <summary>
    /// Get user entered values from dialog.
    /// </summary>
    /// <returns>true</returns>
    public boolean getFromDialog()
    {
        boolean ret;

        ret = super();

        if (ret)
        {
            erBinding = dialogErBinding.value();
        }

        return ret;
    }

]]></Source>
      </Method>
      <Method>
        <Name>erBinding_lookup</Name>
        <Source><![CDATA[
    private Common erBinding_lookup(FormReferenceControl _formControl)
    {
        return ERFormatMappingTable::lookup(_formControl, modelName);
    }

]]></Source>
      </Method>
      <Method>
        <Name>runElectonicReporting</Name>
        <Source><![CDATA[
    private void runElectonicReporting()
    {
        ERIModelDefinitionParamsAction parameters =
            new ERModelDefinitionParamsUIActionComposite()
            .add(new ERModelDefinitionDatabaseContext()
                .addValue(tableNum(CompanyInfo), fieldNum(CompanyInfo, DataArea), curext()));

        ERObjectsFactory::createFormatMappingRunByFormatMappingId(erBinding, '', true)
            .withParameter(parameters)
            .withFileDestination(ERObjectsFactory::createFileDestinationBrowser())
            .run();
    }

]]></Source>
      </Method>
      <Method>
        <Name>run</Name>
        <Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        this.runElectonicReporting();
    }

]]></Source>
      </Method>
      <Method>
        <Name>canRunInNewSession</Name>
        <Source><![CDATA[
    /// <summary>
    /// Returns whether the class instance can run in a new session.
    /// </summary>
    /// <returns>false</returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
      </Method>
      <Method>
        <Name>getERFormatMappingRecId</Name>
        <Source><![CDATA[
    /// <summary>
    /// Returns ER format ID specified in LedgerParameters table.
    /// </summary>
    /// <returns>false</returns>
    protected ERFormatMappingId getERFormatMappingRecId()
    {
        return LedgerParameters::find().LedgerTransactionsExportFormat;
    }

]]></Source>
      </Method>
      <Method>
        <Name>isRetryable</Name>
        <Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable() 
    {
        return false;
    }
]]></Source>
      </Method>
    </Methods>
  </SourceCode>
</AxClass>