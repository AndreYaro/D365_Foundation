<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendAgingCalculationPopulateOpenTransactions</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustVendAgingCalculationPopulateOpenTransactions</c> class copies values from the <c>CustVendTransAging</c>, <c>CustVendTrans</c> and <c>CustVendTransOpen</c> inserts
///     records into the <c>CustVendAgingProcessingTmp</c> table.
/// </summary>
public final class CustVendAgingCalculationPopulateOpenTransactions extends SysQueryInsertRecordSet
{
    private CustVendAgingCalculationParameters custVendAgingCalculationParameters;

    private int custVendTransAgingDataSourceId;
    private int custVendTransDataSourceId;
    private int custVendTransOpenDataSourceId;

    const str TransDateBelowOrAsOfBalanceDate = '(%1.%2 <= %3)';
    const str TransDateAboveOrAsOfBalanceDate = '(%1.%2 >= %3)';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>CustVendAgingCalculationPopulateOpenTransactions</c> class.
    /// </summary>
    /// <param name = "_custVendAgingCalculationParameters">
    /// The <c>CustVendAgingCalculationParameters</c> object which holds the parameter values.
    /// </param>
    /// <returns>
    /// The initialized <c>CustVendAgingCalculationPopulateOpenTransactions</c> object.
    /// </returns>
    public static CustVendAgingCalculationPopulateOpenTransactions newFromParameters(CustVendAgingCalculationParameters _custVendAgingCalculationParameters)
    {
        CustVendAgingCalculationPopulateOpenTransactions instance = CustVendAgingCalculationPopulateOpenTransactions::construct();
        instance.custVendAgingCalculationParameters = _custVendAgingCalculationParameters;

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransAgingDataSourceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the data source ID for <c>CustVendTransAging</c> table.
    /// </summary>
    /// <returns>
    /// The unique ID.
    /// </returns>
    protected int getCustVendTransAgingDataSourceId()
    {
        return custVendTransAgingDataSourceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransDataSourceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the data source ID for <c>CustVendTrans</c> table.
    /// </summary>
    /// <returns>
    /// The unique ID.
    /// </returns>
    protected int getCustVendTransDataSourceId()
    {
        return custVendTransDataSourceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransOpenDataSourceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the data source ID for <c>CustVendTransOpen</c> table.
    /// </summary>
    /// <returns>
    /// The unique ID.
    /// </returns>
    protected int getCustVendTransOpenDataSourceId()
    {
        return custVendTransOpenDataSourceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static CustVendAgingCalculationPopulateOpenTransactions construct()
    {
        return new CustVendAgingCalculationPopulateOpenTransactions();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initTargetCursor</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the target <c>CustVendAgingProcessingTmp</c> cursor.
    /// </summary>
    /// <returns>
    /// The <c>CustVendAgingProcessingTmp</c> table.
    /// </returns>
    protected Common initTargetCursor()
    {
        CustVendAgingProcessingTmp custVendAgingProcessingTmp = custVendAgingCalculationParameters.custVendAgingProcessingTmp;
        custVendAgingProcessingTmp.skipDataMethods(true);

        return custVendAgingProcessingTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFieldMapping</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the map fields.
    /// </summary>
    protected void initFieldMapping()
    {
        this.insertFieldMap(
            custVendTransAgingDataSourceId,
            custVendAgingCalculationParameters.customersVendors.TableId,
            fieldNum(CustVendTransAging, AccountNum),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, AccountNum));

        this.insertFieldMap(
            custVendTransDataSourceId,
            custVendAgingCalculationParameters.custVendTrans.TableId,
            fieldName2Id(custVendAgingCalculationParameters.custVendTrans.TableId, fieldStr(CustVendTrans, RecId)),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, TransRecId));

        this.insertFieldMap(
            custVendTransDataSourceId,
            custVendAgingCalculationParameters.custVendTrans.TableId,
            this.getFieldNum(fieldNum(CustVendTrans, CurrencyCode), custVendAgingCalculationParameters.custVendTrans.tableId),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, CurrencyCode));

        this.insertFieldMap(
            custVendTransOpenDataSourceId,
            custVendAgingCalculationParameters.custVendTransOpen.TableId,
            fieldName2Id(custVendAgingCalculationParameters.custVendTransOpen.TableId, fieldStr(CustVendTransOpen, RecId)),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, TransOpenRecId));

        this.insertFieldMap(
            custVendTransOpenDataSourceId,
            custVendAgingCalculationParameters.custVendTransOpen.TableId,
            this.getFieldNum(fieldNum(CustVendTransOpen, AmountMST), custVendAgingCalculationParameters.custVendTransOpen.tableId),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, Amount));

        this.insertFieldMap(
            custVendTransOpenDataSourceId,
            custVendAgingCalculationParameters.custVendTransOpen.TableId,
            this.getFieldNum(fieldNum(CustVendTransOpen, AmountCur), custVendAgingCalculationParameters.custVendTransOpen.tableId),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, AmountCur));

        this.insertFieldMap(
            custVendTransOpenDataSourceId,
            custVendAgingCalculationParameters.custVendTransOpen.TableId,
            this.getFieldNum(fieldNum(CustVendTransOpen, ReportingCurrencyAmount), custVendAgingCalculationParameters.custVendTransOpen.tableId),
            tableNum(CustVendAgingProcessingTmp),
            fieldNum(CustVendAgingProcessingTmp, AmountReporting));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the query to select <c>CustVendTransAging</c>, <c>CustVendTrans</c> and <c>CustVendTransOpen</c> records.
    /// </summary>
    /// <returns>
    /// Query instance to select <c>CustVendTransAging</c>, <c>CustVendTrans</c> and <c>CustVendTransOpen</c> records.
    /// </returns>
    protected Query initQuery()
    {
        Query query = new Query();
        QueryBuildDataSource qbdsCustVendTransAging = query.addDataSource(custVendAgingCalculationParameters.customersVendors.TableId);
        qbdsCustVendTransAging.cursor(custVendAgingCalculationParameters.customersVendors);

        QueryBuildDataSource qbdsCustVendTrans = this.addCustVendTransFieldsToQueryObject(qbdsCustVendTransAging);

        QueryBuildDataSource qbdsCustVendTransOpen = this.addCustVendTransOpenFieldsToQueryObject(qbdsCustVendTrans);

        query.clearAllFields();

        custVendTransAgingDataSourceId = qbdsCustVendTransAging.uniqueId();
        custVendTransDataSourceId = qbdsCustVendTrans.uniqueId();
        custVendTransOpenDataSourceId = qbdsCustVendTransOpen.uniqueId();

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addCustVendTransFieldsToQueryObject</Name>
				<Source><![CDATA[
    private QueryBuildDataSource addCustVendTransFieldsToQueryObject(QueryBuildDataSource _qbdsCustVendTransAging)
    {
        QueryBuildDataSource qbdsCustVendTrans = _qbdsCustVendTransAging.addDataSource(custVendAgingCalculationParameters.custVendTrans.TableId);

        // Joins the <c>CustVendTrans</c> data source with the <c>CustVendTransAging</c> datasource.
        qbdsCustVendTrans.addLink(fieldNum(CustVendTransAging, AccountNum),
            this.getFieldNum(fieldNum(CustVendTrans, AccountNum), custVendAgingCalculationParameters.custVendTrans.TableId),
            _qbdsCustVendTransAging.name());

        // Selects only the records where transaction date is below or same as balance as of date.
        qbdsCustVendTrans.addRange(this.getFieldNum(fieldNum(CustVendTrans, TransDate), custVendAgingCalculationParameters.custVendTrans.TableId)).value(
            strFmt(TransDateBelowOrAsOfBalanceDate,
            qbdsCustVendTrans.name(),
            fieldStr(CustVendTrans, TransDate),
            date2StrXpp(custVendAgingCalculationParameters.balanceAsOfDate)));

        // Selects only transactions which are not closed or transaction date is above or same as balance as of date.
        qbdsCustVendTrans.addRange(this.getFieldNum(fieldNum(CustVendTrans, Closed), custVendAgingCalculationParameters.custVendTrans.TableId)).value(SysQuery::valueEmptyString());
        qbdsCustVendTrans.addRange(this.getFieldNum(fieldNum(CustVendTrans, Closed), custVendAgingCalculationParameters.custVendTrans.TableId)).value(
            strFmt(TransDateAboveOrAsOfBalanceDate,
            qbdsCustVendTrans.name(),
            fieldStr(CustVendTrans, Closed),
            date2StrXpp(custVendAgingCalculationParameters.balanceAsOfDate)));
        qbdsCustVendTrans.cursor(custVendAgingCalculationParameters.custVendTrans);

        return qbdsCustVendTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addCustVendTransOpenFieldsToQueryObject</Name>
				<Source><![CDATA[
    private QueryBuildDataSource addCustVendTransOpenFieldsToQueryObject(QueryBuildDataSource _qbdsCustVendTrans)
    {
        QueryBuildDataSource qbdsCustVendTransOpen = _qbdsCustVendTrans.addDataSource(custVendAgingCalculationParameters.custVendTransOpen.TableId);

        // Joins the <c>CustVendTransOpen</c> data source with the <c>CustVendTrans</c> datasource.
        qbdsCustVendTransOpen.addLink(fieldName2Id(custVendAgingCalculationParameters.custVendTrans.TableId, fieldStr(CustVendTrans, RecId)),
            this.getFieldNum(fieldNum(CustVendTransOpen, RefRecId), custVendAgingCalculationParameters.custVendTransOpen.TableId),
            _qbdsCustVendTrans.name());

        // Selects only the transactions where transaction date is below or same as balance as of date.
        qbdsCustVendTransOpen.addRange(this.getFieldNum(fieldNum(CustVendTransOpen, TransDate), custVendAgingCalculationParameters.custVendTransOpen.TableId)).value(
            strFmt(TransDateBelowOrAsOfBalanceDate,
            qbdsCustVendTransOpen.name(),
            fieldStr(CustVendTransOpen, TransDate),
            date2StrXpp(custVendAgingCalculationParameters.balanceAsOfDate)));

        qbdsCustVendTransOpen.cursor(custVendAgingCalculationParameters.custVendTransOpen);

        return qbdsCustVendTransOpen;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldNum</Name>
				<Source><![CDATA[
    private fieldId getFieldNum(fieldId _fieldIdInMap, tableId _tableId)
    {
        tableId mapId;
        SysGlobalObjectCache globalObjectCache = new SysGlobalObjectCache();
        container result = globalObjectCache.find(funcname(), [_fieldIdInMap, _tableId]);

        if (result == connull())
        {
            switch (_tableId)
            {
                case tablenum(CustTrans), tablenum(VendTrans):
                    mapId = tablenum(CustVendTrans);
                    break;
                case tablenum(CustTransOpen), tableNum(VendTransOpen):
                    mapId = tablenum(CustVendTransOpen);
                    break;

                default:
                    throw error(strfmt("@SYS19306", funcname()));
            }

            Map idMapping = VendDocumentUtil::fieldMapping(mapId, _tableId);
            fieldId fieldIdInTable = idMapping.lookup(_fieldIdInMap);
            result = [fieldIdInTable];
            globalObjectCache.insert(funcname(), [_fieldIdInMap, _tableId], result);
        }

        return conpeek(result, 1);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>