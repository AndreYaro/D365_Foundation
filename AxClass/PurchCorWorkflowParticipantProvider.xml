<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchCorWorkflowParticipantProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PurchCorWorkflowParticipantProvider</c> class implements the
///    <c>WorkflowParticipantProvider</c> interface that resolves the <c>PurchLine</c> table
///    requisitioners to users.
/// </summary>
/// <remarks>
///    This implementation of the <c>WorkflowParticipantProvider</c> interface can be registered as the
///    <c>ParticipantProvider</c> object for any workflow task or approval that requires
///    <c>Requisitioner</c> field <c>PurchLine</c> table resolution.
/// </remarks>
public class PurchCorWorkflowParticipantProvider implements WorkflowParticipantProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getParticipantTokens</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves a list that has the <c>Requisitioner</c> field of the <c>PurchLine</c> table to display
    /// in the workflow configuration user interface.
    /// </summary>
    /// <returns>
    /// An instance of the <c>WorkflowParticipantTokenList</c> class that contains the field number of the
    /// <c>Requisitioner</c> field as the participant token ID and <c>PurchLine.Requisitioner</c> as the
    /// participant token name.
    /// </returns>
    public WorkflowParticipantTokenList getParticipantTokens()
    {
        WorkflowParticipantTokenList purchLineRequisitioner = WorkflowParticipantTokenList::construct();

        purchLineRequisitioner.add(fieldStr(PurchLine, Requester), "@SYS303442");

        return purchLineRequisitioner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolve</Name>
				<Source><![CDATA[
    /// <summary>
    /// Resolves the <c>Requester</c> field of the <c>PurchLine</c> table to a list of users.
    /// </summary>
    /// <param name="_context">
    /// An instance of the <c>WorkflowContext</c> class.
    /// </param>
    /// <param name="_participantTokenName">
    /// The <c>Requester</c> field of the <c>PurchLine</c> table token.
    /// </param>
    /// <returns>
    /// An instance of a <c>WorkflowUserList</c> class that contains all of the enabled users from the
    /// token.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    /// The participant token must be set.
    /// </exception>
    public WorkflowUserList resolve(WorkflowContext _context,
                                    WorkflowParticipantToken _participantTokenName)
    {
        WorkflowUserList    userList = WorkflowUserList::construct();

        if (!_participantTokenName)
            throw error("@SYS105453");

        if (PurchLine::findRecId(_context.parmRecId()).RecId)
        {
            switch (_participantTokenName)
            {
                case fieldStr(PurchLine, Requester):
                    userList.add(DirPersonUserEx::worker2UserId(PurchLine::findRecId(_context.parmRecId()).Requester));
                    break;
            }
        }

        return userList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PurchCorWorkflowParticipantProvider construct()
    {
        return new PurchCorWorkflowParticipantProvider();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>