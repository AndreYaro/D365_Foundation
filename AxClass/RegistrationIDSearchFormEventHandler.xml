<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RegistrationIDSearchFormEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
public static class RegistrationIDSearchFormEventHandler
{
    private static TmpTaxVATNumTable selectedRecord;
    private static boolean isCust;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>TmpTaxVATNumTable_OnActivated</Name>
				<Source><![CDATA[
    [FormDataSourceEventHandler(formDataSourceStr(RegistrationIDSearch, TmpTaxVATNumTable), FormDataSourceEventType::Activated)]
    public static void TmpTaxVATNumTable_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        selectedRecord = _sender.cursor();
        FormCommandButtonControl copyButton = _sender.formRun().design().controlName(formcontrolstr(RegistrationIDSearch, CopyButton));

        if (selectedRecord.TaxIDType == TaxIDSourceType::VATIDRegistration)
        {
            copyButton.visible(false);
            return;
        }

        switch (selectedRecord.SourceTableId)
        {
            case tableNum(CustTable):
                isCust = true;
                break;
            case tableNum(VendTable):
                isCust = false;
                break;
            default:
                copyButton.visible(false);
                return;
        }

        FormRun sendingForm = _sender.formRun();
        CustTable custTable;
        VendTable vendTable;
        DirPartyTable party;
        if (isCust)
        {
            select firstonly crosscompany * from custTable where custTable.RecId == selectedRecord.SourceRecId;
            party = DirPartyTable::findRec(custTable.Party);
        }
        else
        {
            select firstonly crosscompany * from vendTable where vendTable.RecId == selectedRecord.SourceRecId;
            party = DirPartyTable::findRec(vendTable.Party);
        }


        if (party)
        {
            changecompany(isCust ? custTable.dataAreaId : vendTable.dataAreaId)
            {
                boolean isCurAccountGlobal = sendingForm.parmIsCurAccountNumGlobal();
                boolean isCurAccountInCurCompany = DirPartyView::IsPartyRoleExistInCompany(
                    party.RecId,
                    isCust ? DirPartyRoleType::Customer : DirPartyRoleType::Vendor,
                    sendingForm.parmCurDataAreaId());
                boolean isDestinationAccountGlobal = isCust ?
                    CustVendCopyDataUtil::isCustAccountNumSequenceGlobal(custTable.dataAreaId) :
                    CustVendCopyDataUtil::isVendAccountNumSequenceGlobal(vendTable.dataAreaId);

                if (isCurAccountGlobal &&
                    !isCurAccountInCurCompany &&
                    isDestinationAccountGlobal)
                {
                    copyButton.visible(true);
                }
                else
                {
                    copyButton.visible(false);
                }
            }
        }
        else
        {
            copyButton.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>CopyButton_OnClicked</Name>
				<Source><![CDATA[
    [FormControlEventHandler(formControlStr(RegistrationIDSearch, CopyButton), FormControlEventType::Clicked)]
    public static void CopyButton_OnClicked(FormControl _sender, FormControlEventArgs _e)
    {
        if (selectedRecord)
        {
            FormRun custVendTableForm = _sender.formRun().args().caller();
            FormRun sendingForm = _sender.formRun();
            DataAreaId curCompany = sendingForm.parmCurDataAreaId();

            changecompany(curCompany)
            {
                if (isCust)
                {
                    CustTable custTable;
                    select firstonly crosscompany * from custTable where custTable.RecId == selectedRecord.SourceRecId;
                    CustVendCopyDataUtil::copyCustForm(custTable, custVendTableForm, false);
                }
                else
                {
                    VendTable vendTable;
                    select firstonly crosscompany * from vendTable where vendTable.RecId == selectedRecord.SourceRecId;
                    CustVendCopyDataUtil::copyVendForm(vendTable, custVendTableForm, true);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>