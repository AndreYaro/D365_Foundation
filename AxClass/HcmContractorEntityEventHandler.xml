<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmContractorEntityEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains event handlers for the HcmContractorEntity entity.
/// </summary>
class HcmContractorEntityEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmContractorEntity_onInsertingEntityDataSource</Name>
				<Source><![CDATA[
     /// <summary>
    /// Entity OnInsertingEntityDataSource event handler for HcmContractorEntity extension
    /// </summary>
    /// <param name="_sender">HcmContractor Entity raising the event</param>
    /// <param name="_eventArgs">The args of this event</param>
    [DataEventHandler(tableStr(HcmContractorEntity), DataEventType::InsertingEntityDataSource), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmContractorEntity_onInsertingEntityDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (dataSourceCtx.name() == dataentitydatasourcestr(HcmContractorEntity, HcmEmploymentContractorInternal))
        {
            HcmEmploymentContractorEntity_Internal employmentContractor = dataSourceCtx.getBuffer();
            employmentContractor.StartDate = HcmWorkerEntityHelper::defaultValidFromDateTime(employmentContractor.StartDate);
            employmentContractor.EndDate = HcmWorkerEntityHelper::defaultValidToDateTime(employmentContractor.EndDate);
        }

        if (dataSourceCtx.name() == dataEntityDataSourceStr(HcmContractorEntity, WorkCalendarEmployment))
        {
            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();

            if (workCalendarEmployment.Employment)
            {
                //
                // Conflict detection to resolve inserts into updates. This is required since <c>WorkCalendarEmployment</c> is 1-1 joined to <c>HcmEmployment</c>,
                // and the entity framework only resolves inserts into updates automatically for datasources in the 1 side on a 1-many relationship.
                //
                WorkCalendarEmployment existingRecord = WorkCalendarEmployment::findByEmploymentRecId(workCalendarEmployment.Employment, true);

                if (existingRecord)
                {
                    // Merge user modified fields into the existing record (excluding system fields).
                    buf2Buf(workCalendarEmployment, existingRecord);

                    // Set the datasource context buffer to the final merged record.
                    dataSourceCtx.setBuffer(existingRecord);
                    dataSourceCtx.setDatabaseOperation(DataEntityDatabaseOperation::Update);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmContractorEntity_onMappedEntityToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity onMappedEntityToDataSource event handler for HcmContractorEntity extension
    /// </summary>
    /// <param name="_sender">HcmContractor Entity raising the event</param>
    /// <param name="_eventArgs">The args of this event</param>
    [DataEventHandler(tableStr(HcmContractorEntity), DataEventType::MappedEntityToDataSource), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmContractorEntity_onMappedEntityToDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();
        HcmContractorEntity contractorEntity = _sender as HcmContractorEntity;

        switch (dataSourceCtx.name())
        {    
            case dataentitydatasourcestr(HcmContractorEntity, WorkCalendarEmployment):

                contractorEntity.skipDataSourceValidateField(fieldNum(HcmContractorEntity, CalendarDataAreaId), true);

                WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();
                workCalendarEmployment.CalendarDataAreaId = contractorEntity.EmploymentLegalEntityId;
                break;

            case dataentitydatasourcestr(HcmContractorEntity, HcmEmploymentContractorInternal):
                HcmEmploymentContractorEntity_Internal employmentContractor = dataSourceCtx.getBuffer();
                employmentContractor.VendorDataAreaID = contractorEntity.EmploymentLegalEntityId;            
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmContractorEntity_ValidatedWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity ValidatedWrite event handler for HcmContractorEntity extension
    /// </summary>
    /// <param name="_sender">HcmContractor Entity raising the event</param>
    /// <param name="_eventArgs">The args of this event</param>
    [DataEventHandler(tableStr(HcmContractorEntity), DataEventType::ValidatedWrite), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmContractorEntity_ValidatedWrite(Common _sender, DataEventArgs _eventArgs)
    {
        HcmContractorEntity contractorEntity = _sender as HcmContractorEntity;
        ValidateEventArgs validateEventArgs = _eventArgs as ValidateEventArgs;

        boolean ret = ValidateEventArgs.parmValidateResult();

        boolean isExist = true;
        
        if (contractorEntity.CalendarId !='')
        {
            WorkCalendarEmployment workCalendarEmployment;
            workCalendarEmployment.CalendarDataAreaId = contractorEntity.EmploymentLegalEntityId;
            workCalendarEmployment.CalendarId = contractorEntity.CalendarId;
            isExist = workCalendarEmployment.validateField(fieldNum(WorkCalendarEmployment, CalendarId));
        }

        ret = ret && isExist;

        validateEventArgs.parmValidateResult(ret);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>