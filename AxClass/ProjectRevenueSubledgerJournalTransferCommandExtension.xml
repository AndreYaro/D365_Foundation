<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjectRevenueSubledgerJournalTransferCommandExtension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Project Revenue extension for the <c>SubledgerJournalTransferCommand</c> class.
/// </summary>
[SourceDocumentExtensionAttribute()]
class ProjectRevenueSubledgerJournalTransferCommandExtension extends SubledgerJournalTransferCommandExtension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertedGeneralJournalAccountEntry</Name>
				<Source><![CDATA[
    public void insertedGeneralJournalAccountEntry(RefRecId _transferidVal, SourceDocument _sourceDocument, boolean _doSummarization)
    {
        GeneralJournalAccountEntry      localGeneralJournalAccountEntry;
        SubledgerJournalAccountEntry    subledgerJournalAccountEntry;
        SourceDocumentHeader            sourceDocumentHeader;
        TSTimesheetTable                tsTimesheetTable;
        SubledgerJournalEntry           subledgerJournalEntry;
        AccountingEvent                 accountingEvent;
        ProjectRevenueHeader            projectRevenueHeader;
        SubLedgerJournalAccountEntryView subLedgerJournalAccountEntryView;
        GeneralJournalEntry             generalJournalEntry;

        sourceDocumentHeader = _sourceDocument.parmSourceDocumentHeader();

        select firstonly tsTimesheetTable
        join projectRevenueHeader 
            where projectRevenueHeader.SourceDocumentHeader == sourceDocumentHeader.RecId &&
                projectRevenueHeader.ReferenceSourceDocumentHeader == tsTimesheetTable.SourceDocumentHeader;

        if (!_doSummarization && tsTimesheetTable)
        {
            tsTimesheetTable.clear();
            projectRevenueHeader.clear();

            while select tsTimesheetTable
            exists join projectRevenueHeader 
                where projectRevenueHeader.SourceDocumentHeader == sourceDocumentHeader.RecId &&
                    projectRevenueHeader.ReferenceSourceDocumentHeader == tsTimesheetTable.SourceDocumentHeader
            exists join subLedgerJournalAccountEntryView
                where subLedgerJournalAccountEntryView.SourceDocumentHeader == tsTimesheetTable.SourceDocumentHeader &&
                    subLedgerJournalAccountEntryView.TransferId == _transferidVal
            {
                ttsbegin;

                if (tsTimesheetTable)
                {
                    localGeneralJournalAccountEntry.skipDataMethods(true);
                    localGeneralJournalAccountEntry.skipDatabaseLog(true);
                    localGeneralJournalAccountEntry.skipEvents(true);

                    update_recordSet localGeneralJournalAccountEntry
                    setting Quantity = tsTimesheetTable.totalHours()
                    join generalJournalEntry
                        where generalJournalEntry.TransferId == _transferidVal &&
                            generalJournalEntry.RecId == localGeneralJournalAccountEntry.GeneralJournalEntry
                    join GeneralJournalAccountEntry, SubledgerJournalEntry from subledgerJournalAccountEntry
                        where subledgerJournalAccountEntry.GeneralJournalAccountEntry == localGeneralJournalAccountEntry.RecId
                    join AccountingEvent, RecId, TransferId from subledgerJournalEntry
                        where subledgerJournalEntry.RecId == subledgerJournalAccountEntry.SubledgerJournalEntry &&
                            subledgerJournalEntry.TransferId == _transferidVal
                    join SourceDocumentHeader, RecId from accountingEvent
                        where accountingEvent.RecId == subledgerJournalEntry.AccountingEvent
                    join SourceDocumentHeader, ReferenceSourceDocumentHeader from projectRevenueHeader
                        where projectRevenueHeader.SourceDocumentHeader == accountingEvent.SourceDocumentHeader &&
                            projectRevenueHeader.ReferenceSourceDocumentHeader == tsTimesheetTable.SourceDocumentHeader;
                }

                ttsCommit;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>subscribe</Name>
				<Source><![CDATA[
    protected void subscribe()
    {
        super();
        command.insertedGeneralJournalAccountEntry += eventhandler(this.insertedGeneralJournalAccountEntry);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>