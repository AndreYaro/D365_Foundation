<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmEmploymentEntityEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains event handlers for the HcmEmploymentEntity entity.
/// </summary>
class HcmEmploymentEntityEventHandler
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmEmploymentEntity_onMappedEntityToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity MappedEntityToDataSource event handler for <c>HcmEmploymentEntity</c> extension.
    /// </summary>
    /// <param name="_sender">The <c>HcmEmploymentEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The event args.</param>
    [DataEventHandler(tableStr(HcmEmploymentEntity), DataEventType::MappedEntityToDataSource)]
    public static void HcmEmploymentEntity_onMappedEntityToDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (dataSourceCtx.name() == dataEntityDataSourceStr(HcmEmploymentEntity, WorkCalendarEmployment))
        {
            HcmEmploymentEntity employmentEntity = _sender as HcmEmploymentEntity;

            employmentEntity.skipDataSourceValidateField(fieldNum(HcmEmploymentEntity, CalendarId), true);
            employmentEntity.skipDataSourceValidateField(fieldNum(HcmEmploymentEntity, CalendarLegalEntityId), true);

            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();
            workCalendarEmployment.CalendarDataAreaId = employmentEntity.LegalEntityId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmploymentEntity_onInsertingEntityDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity InsertingEntityDataSource event handler for <c>HcmEmploymentEntity</c> extension.
    /// </summary>
    /// <param name="_sender">The <c>HcmEmploymentEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The event args.</param>
    [DataEventHandler(tableStr(HcmEmploymentEntity), DataEventType::InsertingEntityDataSource)]
    public static void HcmEmploymentEntity_onInsertingEntityDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (dataSourceCtx.name() == dataEntityDataSourceStr(HcmEmploymentEntity, WorkCalendarEmployment))
        {
            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();

            if (workCalendarEmployment.Employment)
            {
                //
                // Conflict detection to resolve inserts into updates. This is required since <c>WorkCalendarEmployment</c> is 1-1 joined to <c>HcmEmployment</c>,
                // and the entity framework only resolves inserts into updates automatically for datasources in the 1 side on a 1-many relationship.
                //
                WorkCalendarEmployment existingRecord = WorkCalendarEmployment::findByEmploymentRecId(workCalendarEmployment.Employment, true);

                if (existingRecord)
                {
                    // Merge user modified fields into the existing record (excluding system fields).
                    buf2Buf(workCalendarEmployment, existingRecord);

                    // Set the datasource context buffer to the final merged record.
                    dataSourceCtx.setBuffer(existingRecord);
                    dataSourceCtx.setDatabaseOperation(DataEntityDatabaseOperation::Update);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>