<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerSettlementsAutomationScheduleExplodeProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>LedgerSettlementsAutomationScheduleExplodeProvider</c> class is designed to handle
/// creating the appropriate <c>LedgerSettlementsAutomationCriteria</c> records upon occurrence explosion.
/// </summary>
[ExportMetadata(extendedTypeStr(ProcessScheduleTypeName), 'LedgerSettlementsAutomation')]
[Export(identifierStr(Dynamics.AX.Application.ProcessScheduleIExplodeOccurrences))]
internal final class LedgerSettlementsAutomationScheduleExplodeProvider implements ProcessScheduleIExplodeOccurrences
{
    private LedgerSettlementsAutomationCriteria automationCriteria;
    private LedgerSettlementsAutomationCriteria automationCriteriaSelect;
    private ProcessScheduleSeriesOccurrenceTmp occurrencesExplodedTmp;
    private RefRecId processScheduleSeries; 

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>explodeOccurrences</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    public void explodeOccurrences(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        LedgerSettlementsAutomationCriteria automationCriteriaExplode;
        this.initialize(_occurrencesExplodedTmp);
        
        SysDaInsertObject scheduleExplodeInsertObject = this.buildInsertObject(occurrencesExplodedTmp);
        this.executeInsertStatement(scheduleExplodeInsertObject);

        select firstonly ProcessScheduleSeries from occurrencesExplodedTmp;
        processScheduleSeries = occurrencesExplodedTmp.ProcessScheduleSeries;

        select FinancialDimensions, Tags, RecId from automationCriteriaExplode
            where automationCriteriaExplode.ProcessScheduleSeries == processScheduleSeries;
        
        this.explodeMainAccountsSelection(automationCriteriaExplode.RecId);

        if (automationCriteriaExplode.FinancialDimensions)
        {
            this.explodeDimensionsSelection(automationCriteriaExplode.RecId);
        }

        if (automationCriteriaExplode.Tags)
        {
            this.explodeFinancialTagsSelection(automationCriteriaExplode.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>explodeMainAccountsSelection</Name>
				<Source><![CDATA[
    private void explodeMainAccountsSelection(RefRecId _automationCriteriaRecId)
    {
        LedgerSettlementsAutomationMatchSelection automationMatchSelection;
        LedgerSettlementsAutomationMatchSelection automationMatchSelectionInsert;

        LedgerSettlementsAutomationCriteriaSelection ledgerSettlementsAutomationCriteriaSelection;
        RefRecId mainAccountRecId;

        while select SelectionCriteria, SelectionRecId, RecId from automationMatchSelection
            where automationMatchSelection.LedgerSettlementsAutomationCriteria == _automationCriteriaRecId
                && automationMatchSelection.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::MainAccounts
        {
            ledgerSettlementsAutomationCriteriaSelection = automationMatchSelection.SelectionCriteria;
            mainAccountRecId = automationMatchSelection.SelectionRecId;

            insert_recordset automationMatchSelectionInsert(SelectionCriteria, SelectionRecId, LedgerSettlementsAutomationCriteria)
                select ledgerSettlementsAutomationCriteriaSelection, mainAccountRecId, RecId from automationCriteria
                join occurrencesExplodedTmp
                    where automationCriteria.ProcessScheduleOccurrence == occurrencesExplodedTmp.ProcessScheduleOccurrence;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>explodeDimensionsSelection</Name>
				<Source><![CDATA[
    private void explodeDimensionsSelection(RefRecId _automationCriteriaRecId)
    {
        LedgerSettlementsAutomationMatchSelection automationMatchSelection;
        LedgerSettlementsAutomationMatchSelection automationMatchSelectionInsert;

        LedgerSettlementsAutomationCriteriaSelection ledgerSettlementsAutomationCriteriaSelection;
        RefRecId ledgerDimensionAttribute;

        while select SelectionCriteria, SelectionRecId, RecId from automationMatchSelection
            where automationMatchSelection.LedgerSettlementsAutomationCriteria == _automationCriteriaRecId
            && automationMatchSelection.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::FinancialDimensions
        {
            ledgerSettlementsAutomationCriteriaSelection = automationMatchSelection.SelectionCriteria;
            ledgerDimensionAttribute = automationMatchSelection.SelectionRecId;

            insert_recordset automationMatchSelectionInsert(SelectionCriteria, SelectionRecId, LedgerSettlementsAutomationCriteria)
                select ledgerSettlementsAutomationCriteriaSelection, ledgerDimensionAttribute, RecId from automationCriteria
                join occurrencesExplodedTmp
                    where automationCriteria.ProcessScheduleOccurrence == occurrencesExplodedTmp.ProcessScheduleOccurrence;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>explodeFinancialTagsSelection</Name>
				<Source><![CDATA[
    private void explodeFinancialTagsSelection(RefRecId _automationCriteriaRecId)
    {
        LedgerSettlementsAutomationMatchSelection automationMatchSelection;
        LedgerSettlementsAutomationMatchSelection automationMatchSelectionInsert;

        LedgerSettlementsAutomationCriteriaSelection ledgerSettlementsAutomationCriteriaSelection;
        RefRecId financialTag;

        while select SelectionCriteria, SelectionRecId, RecId from automationMatchSelection
            where automationMatchSelection.LedgerSettlementsAutomationCriteria == _automationCriteriaRecId
            && automationMatchSelection.SelectionCriteria == LedgerSettlementsAutomationCriteriaSelection::Tags
        {
            ledgerSettlementsAutomationCriteriaSelection = automationMatchSelection.SelectionCriteria;
            financialTag = automationMatchSelection.SelectionRecId;

            insert_recordset automationMatchSelectionInsert(SelectionCriteria, SelectionRecId, LedgerSettlementsAutomationCriteria)
                select ledgerSettlementsAutomationCriteriaSelection, financialTag, RecId from automationCriteria
                join occurrencesExplodedTmp
                    where automationCriteria.ProcessScheduleOccurrence == occurrencesExplodedTmp.ProcessScheduleOccurrence;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    private void initialize(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        occurrencesExplodedTmp.linkPhysicalTableInstance(_occurrencesExplodedTmp);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildInsertObject</Name>
				<Source><![CDATA[
    internal SysDaInsertObject buildInsertObject(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        SysDaInsertObject scheduleExplodeInsertObject = new SysDaInsertObject(automationCriteria);
        
        SysDaQueryObject scheduleExplodeInsertQuery = this.buildInsertQueryObject();

        scheduleExplodeInsertObject.query(scheduleExplodeInsertQuery);

        this.addInsertFieldsToInsertObject(scheduleExplodeInsertObject);

        return scheduleExplodeInsertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildInsertQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildInsertQueryObject()
    {
        SysDaQueryObject scheduleExplodeInsertQuery = new SysDaQueryObject(automationCriteriaSelect);
        
        scheduleExplodeInsertQuery.joinClause(SysDaJoinKind::InnerJoin, this.getScheduleSeriesOccurrenceQueryObject());

        this.addProjectionFieldToQueryObject(scheduleExplodeInsertQuery);

        return scheduleExplodeInsertQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getScheduleSeriesOccurrenceQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject getScheduleSeriesOccurrenceQueryObject()
    {
        SysDaQueryObject daQuery = new SysDaQueryObject(occurrencesExplodedTmp);
        daQuery.whereClause(
            new SysDaEqualsExpression(
                    new SysDaFieldExpression(occurrencesExplodedTmp, fieldStr(ProcessScheduleSeriesOccurrenceTmp, ProcessScheduleSeries)),
                    new SysDaFieldExpression(automationCriteriaSelect, fieldStr(LedgerSettlementsAutomationCriteria, ProcessScheduleSeries)))
            .and(
                new SysDaEqualsExpression(
                    new SysDaFieldExpression(occurrencesExplodedTmp, fieldStr(ProcessScheduleSeriesOccurrenceTmp, TypeName)),
            new SysDaValueExpression(LedgerSettlementsAutomationConstants::RegisteredTypeName)))
                );
        daQuery.projection().add(fieldStr(ProcessScheduleSeriesOccurrenceTmp, ProcessScheduleOccurrence));

        return daQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addProjectionFieldToQueryObject</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal void addProjectionFieldToQueryObject(SysDaQueryObject _scheduleExplodeInsertQuery)
    {
        _scheduleExplodeInsertQuery.projection()
            .add(fieldStr(LedgerSettlementsAutomationCriteria, MainAccount))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, PostingLayer))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DebitandCreditAmounts))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, PostingType))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, FinancialDimensions))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, Tags))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DateTolerance))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DateToleranceDays));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addInsertFieldsToInsertObject</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal void addInsertFieldsToInsertObject(SysDaInsertObject _scheduleExplodeInsertObject)
    {
        _scheduleExplodeInsertObject.fields()
            .add(fieldStr(LedgerSettlementsAutomationCriteria, MainAccount))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, PostingLayer))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DebitandCreditAmounts))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, PostingType))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, FinancialDimensions))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, Tags))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DateTolerance))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, DateToleranceDays))
            .add(fieldStr(LedgerSettlementsAutomationCriteria, ProcessScheduleOccurrence));
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeInsertStatement</Name>
				<Source><![CDATA[
    internal void executeInsertStatement(SysDaInsertObject _scheduleExplodeInsertObject)
    {
        SysDaInsertStatement scheduleExplodeInsertStatement = new SysDaInsertStatement();

        scheduleExplodeInsertStatement.insert(_scheduleExplodeInsertObject);        
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>