<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTableDataSharingValidation_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTableDataSharingValidation_Extension</c> contains data sharing policy validation logic for the the <c>InventTable</c> and related tables.
/// </summary>
[ExtensionOf(classStr(SysDataSharingValidation))]
public static class InventTableDataSharingValidation_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateAdditionalPolicyConstraints</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that all necessary tables have been added to the sharing policy, when adding <c>InventTable</c> related tables.
    /// </summary>
    /// <param name = "_policyRecId">
    /// A sharing policy records ID.
    /// </param>
    /// <returns>
    /// true if the policy can be enabled; otherwise, false.
    /// </returns>
    public static boolean validateAdditionalPolicyConstraints(SysDataSharingPolicyRecId _policyRecId)
    {
        boolean canEnable = next validateAdditionalPolicyConstraints(_policyRecId);

        if (SysDataSharingRule::existsTable(_policyRecId, tablestr(InventTable)))
        {
            if (!InventTableDataSharingFeature::isEnabled())
            {
                return checkFailed(strFmt("@ApplicationSuite_SysSharing:DataSharingPolicyEnableFeatureWarning", strFmt("@SCM:InventTableDataSharingFeature_Label", tableStr(InventTable))));
            }

            if (canEnable)
            {
                canEnable = SysDataSharingPolicy::canEnableCollection(_policyRecId, InventTable::dataSharingTableList(), tablestr(InventTable));
            }

            if (canEnable)
            {
                if (!SysDataSharingPolicy::areCompaniesInSameContryRegions(_policyRecId))
                {
                    warning("@ApplicationSuite_SysSharing:DataSharingPolicyCountryRegionWarningInventTable");
                }

                canEnable = SysDataSharingPolicy::validateCompaniesAreInSupportedContryRegions(_policyRecId, tablestr(InventTable), InventTable::unsupportedDataSharingCountryRegions());
            }

            if (canEnable)
            {
                canEnable = !SysDataSharingPolicy::isIncludedImplicitly(_policyRecId, tablestr(InventTable));
            }
        }
        
        return canEnable;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>