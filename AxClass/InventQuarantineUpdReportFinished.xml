<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventQuarantineUpdReportFinished</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventQuarantineUpdReportFinished extends InventQuarantineUpd
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>beginLedger</Name>
				<Source><![CDATA[
        void beginLedger()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>commitLedger</Name>
				<Source><![CDATA[
        void commitLedger()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>createWMSJournal</Name>
				<Source><![CDATA[
        WMSJournalId createWMSJournal()
    {
        WMSJournalTable wmsJournalTable;

        if (!inventParmQuarantineOrder.wmsJournalNameId)
        {
            return '';
        }

        if (inventParmQuarantineOrder.wmsJournalId)
        {
            return inventParmQuarantineOrder.wmsJournalId;
        }

        wmsJournalTable.initFromWMSJournalName(WMSJournalName::find(inventParmQuarantineOrder.wmsJournalNameId));
        wmsJournalTable.InventTransType = InventTransType::QuarantineOrder;
        wmsJournalTable.InventDimId     = InventDim::inventDimIdBlank();
        wmsJournalTable.JournalType     = WMSJournalType::Reception;
        wmsJournalTable.insert();
        return wmsJournalTable.JournalId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preUpdateQuarantineOrder</Name>
				<Source><![CDATA[
        protected void preUpdateQuarantineOrder()
    {
        super();

        InventQualityManagementCreateHandler::createQuarantineReportFinishedBefore(inventParmQuarantineOrder);

        InventQualityManagementBlockHandler::blockQuarantineReportFinished(inventQuarantineOrder);
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
        void run()
    {
        setPrefix(InventQuarantineMultiReportFinished::description());

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateParmJournal</Name>
				<Source><![CDATA[
        void updateParmJournal(WMSJournalId wmsJournalId)
    {
        InventParmQuarantineOrder   parmBuffer = this.parmParmBuffer();

        ttsbegin;
        InventParmQuarantineOrder inventParmQuarantineOrderLocal = InventParmQuarantineOrder::find(parmBuffer.ParmId, parmBuffer.QuarantineId, true);
        if (inventParmQuarantineOrderLocal.RecId)
        {
            inventParmQuarantineOrderLocal.wmsJournalId = wmsJournalId;
            inventParmQuarantineOrderLocal.update();
        }

        ttscommit;
        inventParmQuarantineOrder.wmsJournalId = wmsJournalId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQuarantineOrder</Name>
				<Source><![CDATA[
        void updateQuarantineOrder()
    {
        WMSJournalTrans     wmsJournalTrans;
        WMSJournalId        wmsJournalId;

        if (inventParmQuarantineOrder.wmsJournalNameId)
        {
            wmsJournalId = this.createWMSJournal();
            WMSJournalTable wmsJournalTable = WMSJournalTable::find(wmsJournalId, true);
            wmsJournalTrans.initFromInventParmQuarantineOrder(inventParmQuarantineOrder, wmsJournalId);
            wmsJournalTrans.JournalId = wmsJournalId;
            wmsJournalTrans.insert();
            wmsJournalTable.NumOfLines++;
            wmsJournalTable.update();
        }

        inventQuarantineOrder.Status            = InventQuarantineStatus::ReportedFinished;
        inventQuarantineOrder.update();
        this.updateParmJournal(wmsJournalId);

        InventQualityManagementCreateHandler::createQuarantineReportFinishedAfter(inventParmQuarantineOrder);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
        boolean validate()
    {
        if (inventQuarantineOrder.Status != InventQuarantineStatus::Started)
            return checkFailed(strFmt("@SYS58736",InventQuarantineStatus::Started));

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        static InventQuarantineUpdReportFinished construct()
    {
        InventQuarantineUpdReportFinished inventQuarantineUpdReportFinished = new InventQuarantineUpdReportFinished();

        return inventQuarantineUpdReportFinished;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParmBuffer</Name>
				<Source><![CDATA[
        static InventQuarantineUpdReportFinished newParmBuffer(InventParmQuarantineOrder inventParmQuarantineOrder)
    {
        inventParmQuarantineOrder.skipTTSCheck(true);
        inventParmQuarantineOrder.ParmBuffer::initParmId();
        inventParmQuarantineOrder.write();

        InventQuarantineUpdReportFinished inventQuarantineUpdReportFinished = InventQuarantineUpdReportFinished::construct();
        inventQuarantineUpdReportFinished.parmParmBuffer(inventParmQuarantineOrder);

        return inventQuarantineUpdReportFinished;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
