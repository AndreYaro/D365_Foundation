<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSLoadTableFormArgs</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSLoadTableFormArgs
{
    internal str loadIdList;
    internal WHSLoadId forceSingleLoadId;
    internal WHSLoadTableForm whsLoadTableForm;
    
    protected Common  record;
    protected TableId tableId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    internal static WHSLoadTableFormArgs newFromArgs(Args _args)
    {
        WHSLoadTableFormArgs formArgs = new WHSLoadTableFormArgs();
        
        formArgs.record  = _args.record();
        formArgs.tableId = _args.dataset();
        formArgs.loadIdList = _args.parm();
        formArgs.whsLoadTableForm = new WHSLoadTableForm();

        formArgs.initExternalOutboundShipmentLoadId();
        
        return formArgs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createLoadIdList</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal str createLoadIdList()
    {
        switch (tableId)
        {
            case tableNum(SalesTable):
                SalesTable salesTableArgs = record;
                loadIdList = whsLoadTableForm.createLoadIdFilter(salesTableArgs.SalesId, InventTransType::Sales);
                break;

            case tableNum(SalesLine):
                SalesLine salesLineArgs = record;
                loadIdList = whsLoadTableForm.createLoadIdFilter(salesLineArgs.SalesId, InventTransType::Sales);
                break;

            case tableNum(PurchLine):
                PurchLine purchLineArgs = record;
                loadIdList = whsLoadTableForm.createLoadIdFilterFromInventTransId(purchLineArgs.InventTransId, InventTransType::Purch);
                break;

            case tableNum(InventTransferTable):
                InventTransferTable inventTransferTableArgs = record;
                loadIdList = whsLoadTableForm.createLoadIdFilter(inventTransferTableArgs.TransferId, InventTransType::TransferOrderShip);
                break;

            case tableNum(InventTransferLine):
                InventTransferLine inventTransferLineArgs = record;
                loadIdList = whsLoadTableForm.createLoadIdFilterFromInventTransId(inventTransferLineArgs.InventTransId, InventTransType::TransferOrderShip);
                break;
        }

        return loadIdList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canApplyLoadFilter</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal boolean canApplyLoadFilter()
    {
        if ( tableId == tableNum(SalesTable)
          || tableId == tableNum(SalesLine)
          || tableId == tableNum(PurchLine)
          || tableId == tableNum(InventTransferTable)
          || tableId == tableNum(InventTransferLine)
          || loadIdList)
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initExternalOutboundShipmentLoadId</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    protected void initExternalOutboundShipmentLoadId()
    {
        if (tableId == tableNum(SalesLine))
        {
            SalesLine salesLineArgs = record;
            forceSingleLoadId = WHSLoadLine::getLoadId(salesLineArgs.InventTransId);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>