<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesBaileeNonSSRSPrintMgmtAdapter</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class <c>SalesBaileeNonSSRSPrintMgmtAdapter</c> used for adoptation of print management for
/// "Return report from storage" excel report
/// </summary>
[NonSSRSPrintMgmtAdapterAttribute(PrintMgmtDocumentType::SalesOrderBailee)]
class SalesBaileeNonSSRSPrintMgmtAdapter extends NonSSRSPrintMgmtAdapter
{
    SalesInvoiceJournalPrint    salesInvoiceJournalPrint;
    RecordSortedList            journalList;
    CustInvoiceJour             custInvoiceJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getNodeType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>PrintMgmtNodeType</c> enumeration value of the node.
    /// </summary>
    /// <returns>
    /// The <c>PrintMgmtNodeType</c> enumeration value of the node.
    /// </returns>
    /// <remarks>
    /// New node types should be defined in the <c>PrintMgmtNodeType</c> enumeration and returned through
    /// this method.
    /// </remarks>
    protected PrintMgmtNodeType getNodeType()
    {
        return PrintMgmtNodeType::SalesTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method used to initialize class
    /// </summary>
    /// <remarks>
    /// Empty, in this case initialization depends on record processed
    /// </remarks>
    protected void initialize()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initPrintMgmtReportRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes <c>PrintMgmtReportRun</c> object depending on parameters passed into this class
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void initPrintMgmtReportRun()
    {
        printMgmtReportRun = PrintMgmtReportRun::construct(
            PrintMgmtHierarchyType::Sales,
            PrintMgmtNodeType::SalesTable,
            printMgmtDocumentType);

        if (salesInvoiceJournalPrint)
        {
            printMgmtReportRun.parmDefaultCopyPrintJobSettings(new SRSPrintDestinationSettings(salesInvoiceJournalPrint.parmPrinterSettingsFormLetterCopy()));
            printMgmtReportRun.parmDefaultOriginalPrintJobSettings(new SRSPrintDestinationSettings(salesInvoiceJournalPrint.parmPrinterSettingsFormLetter()));
            printMgmtReportRun.parmForcePrintJobSettings(!salesInvoiceJournalPrint.parmUsePrintManagement());
        }
        else if (printCopyOriginal == PrintCopyOriginal::OriginalPrint)
        {
            //forcePrintJobSettings is reversal to usePrintManagement
            printMgmtReportRun.parmForcePrintJobSettings(false);
        }

        printMgmtReportRun.parmSSRS(PrintMgmtSSRS::NonSSRS);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPrintMgmt</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes print management settings for the list of specified records
    /// </summary>
    /// <exception cref="Exception::Error">
    /// When object parameter not set
    /// </exception>
    /// <exception cref="Exception::Error">
    /// When record fetched from list, but it's not initialized
    /// </exception>
    protected void runPrintMgmt()
    {
        printCopyOriginal = this.parmArgs().parmEnum();

        if (this.parmArgs().record())
        {
            // Get journal list from the selected record/s
            journalList = FormLetter::createJournalListCopy(this.parmArgs().record());
        }
        else
        {
            journalList = this.parmArgs().object();
        }

        if (!journalList)
        {
            throw error("@SYS26348");
        }
        if (SysDictClass::isEqualOrSuperclass(classIdGet(args.caller()), classNum(FormletterJournalPrint)))
        {
            salesInvoiceJournalPrint = args.caller();
        }

        boolean nextRecord = journalList.first(custInvoiceJour);

        while (nextRecord)
        {
            if (!custInvoiceJour)
            {
                throw error("@SYS26348");
            }

            this.initPrintMgmtReportRun();

            this.loadPrintSettings(custInvoiceJour, custInvoiceJour.salesTable(), custInvoiceJour.LanguageId);
            this.outputReports();

            nextRecord = journalList.next(custInvoiceJour);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRecordId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets rec id of record being processed
    /// </summary>
    protected void setRecordId()
    {
        recordId = custInvoiceJour.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public static SalesBaileeNonSSRSPrintMgmtAdapter construct()
    {
        return new SalesBaileeNonSSRSPrintMgmtAdapter();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
