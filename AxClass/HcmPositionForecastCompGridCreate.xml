<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmPositionForecastCompGridCreate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Creates the compensation grid for the compensation group.
/// </summary>
class HcmPositionForecastCompGridCreate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createCompensationGridByCompRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the compensation grid for a compensation group.
    /// </summary>
    /// <param name="_compRateRecId">
    /// The compensation rate record Id value of the compensation rate for which to create.
    /// </param>
    /// <param name="_gridId">
    /// The <c>HRCHRCCompGridId</c> record Id value of the grid to create.
    /// </param>
    public static void createCompensationGridByCompRate(
                                                 HcmPositionForecastCompensationRateRecId _compRateRecId,
                                                 HRCCompGridId _gridId)
    {
        HcmPositionForecastCompGroupRefPoint    compGroupRefPoint;
        HcmPositionForecastCompGroupRefPtRecId  compGroupRefPointRecId;
        HRCCompGrid                             compGrid;
        HRCComp                                 comp;
        HcmPositionForecastCompensationGrid     forecastCompGrid;

        compGrid = HRCCompGrid::find(_gridId);

        if (compGrid.RecId != 0 && compGrid.Type == HcmCompensationType::Step)
        {
            ttsbegin;

            compGroupRefPoint = HcmPositionForecastCompGridCreate::createCompGroupRefPointByCompRate(_compRateRecId, compGrid.RefPointSetupId);

            compGroupRefPointRecId = compGroupRefPoint.RecId;

            insert_recordset forecastCompGrid (Amount, CompensationLevel, RefPointId, RefPointSetupId, CompensationGroupRefPointId)
                select Amount, CompensationLevel, RefPointId, RefPointSetupId, compGroupRefPointRecId from comp
                    where comp.GridId == _gridId;

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createCompGroupRefPointByCompRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the association between a compensation group and a compensation grid.
    /// </summary>
    /// <param name="_compensationRateRecId">
    /// The compensation rate record Id value of the compensation group for which to associate to the compensation grid.
    /// </param>
    /// <param name="_refPointSetupId">
    /// The <c>HRCCompRefPointSetupId</c> value of the grid for which to associate to the compensation group.
    /// </param>
    /// <returns>
    /// The <c>HcmPositionForecastCompGroupRefPoint</c> record created by the method.
    /// </returns>
    public static HcmPositionForecastCompGroupRefPoint createCompGroupRefPointByCompRate(HcmPositionForecastCompensationRateRecId _compensationRateRecId,
                                                 HRCCompRefPointSetupId _refPointSetupId)
    {
        HcmPositionForecastCompGroupRefPoint compensationGroupRefPoint;

        compensationGroupRefPoint.RefPointSetupId = _refPointSetupId;
        compensationGroupRefPoint.CompensationRate = _compensationRateRecId;

        compensationGroupRefPoint.insert();

        return compensationGroupRefPoint;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>