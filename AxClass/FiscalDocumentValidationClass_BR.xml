<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FiscalDocumentValidationClass_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class for Fiscal Document validations.
/// </summary>
class FiscalDocumentValidationClass_BR
{
    str     fiscalDocumentValidationClassName;
    Map     validationMethodsByType;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addMethodForElementType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a validation method to the list of validation methods associated with the given fiscal document element.
    /// </summary>
    /// <param name="_fiscalDocumentElement">
    /// The type of element to add the validation method to.
    /// </param>
    /// <param name="_dictMethod">
    /// The validation method to be added.
    /// </param>
    protected void addMethodForElementType(FiscalDocumentElement_BR _fiscalDocumentElement, SysDictMethod _dictMethod)
    {
        List validationMethods;

        if (!validationMethodsByType.exists(_fiscalDocumentElement))
        {
            validationMethods = new List(Types::Class);
        }
        else
        {
            validationMethods = validationMethodsByType.lookup(_fiscalDocumentElement);
        }

        validationMethods.addEnd(_dictMethod);
        validationMethodsByType.insert(_fiscalDocumentElement, validationMethods);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createListOfValidationMethods</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a list of validation methods for each type of fiscal document element.
    /// </summary>
    protected void createListOfValidationMethods()
    {
        SysDictClass                                dictClass = new SysDictClass(className2Id(fiscalDocumentValidationClassName));
        SysDictMethod                               dictMethod;
        SetEnumerator                               methodsEnumerator;
        FiscalDocumentValidationElementAttribute    validationForAttribute;

        methodsEnumerator = dictClass.methods(true, true, true).getEnumerator();
        while (methodsEnumerator.moveNext())
        {
            dictMethod = methodsEnumerator.current();

            validationForAttribute = dictMethod.getAttribute(classStr(FiscalDocumentValidationElementAttribute));
            if (validationForAttribute)
            {
                this.addMethodForElementType(validationForAttribute.getFiscalDocumentElement(), dictMethod);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMethodsApplicableToElement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the list of validation methods for a given fiscal document element type.
    /// </summary>
    /// <param name="_fiscalDocumentElement">
    /// The fiscal document element type to get the methods for.
    /// </param>
    /// <returns>
    /// The list of validation methods, if it exists; otherwise, an empty list.
    /// </returns>
    public List getMethodsApplicableToElement(FiscalDocumentElement_BR _fiscalDocumentElement)
    {
        List validationMethods = new List(Types::Class);

        if (validationMethodsByType.exists(_fiscalDocumentElement))
        {
            validationMethods = validationMethodsByType.lookup(_fiscalDocumentElement);
        }

        return validationMethods;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>FiscalDocumentValidationClass_BR</c> class.
    /// </summary>
    /// <param name="_fiscalDocumentValidationClassName">
    /// The name of the fiscal document validation class to be used by the <c>FiscalDocumentValidationClass_BR</c> class instance.
    /// </param>
    public void new(str _fiscalDocumentValidationClassName)
    {
        fiscalDocumentValidationClassName   = _fiscalDocumentValidationClassName;
        validationMethodsByType             = new Map(Types::Enum, Types::Class);

        this.createListOfValidationMethods();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>