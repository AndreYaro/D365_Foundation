<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RcSalesListCheck_UK</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RcSalesListCheck_UK extends RunBase
{
    QueryRun        qrcSalesList;
    RCSalesList_UK  rcSalesList;
    boolean         errorFound;
    Counter         infoLogCounter;
    Counter         progressCounter;
    str             prefix;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkRecord</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    boolean checkRecord()
    {
        boolean     ok = true;

        ok = rcSalesList.check(true, prefix);

        if (ok)
        {
            rcSalesList.Log = '';
        }
        else
        {
            errorFound = true;
        }

        rcSalesList.update();
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    /// A dialog box can be either built by using the <c>Dialog</c> class or by using a class that is
    /// created in the Application Object Tree (AOT).
    /// </remarks>
    Object dialog()
    {
        FormRun formRun = classfactory.createQueryForm(this);
        formRun.init();
        return formRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new()
    {
        super();

        qrcSalesList   = new QueryRun(querystr(RCsalesList_UK));
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,qrcSalesList.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>progressUpdate</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    void progressUpdate()
    {
        progressCounter++;
        progress.setCount(progressCounter);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the <c>QueryRun</c> object, which is used by the <c>QueryPrompt</c> object
    /// </summary>
    /// <returns>
    /// The <c>QueryRun</c> object.
    /// </returns>
    /// <remarks>
    /// This method is used when a query prompt is the dialog box, and when a Select menu item is added to
    /// a dialog box. Do not instantiate the <c>queryRun</c> object whenever the method is called. This
    /// should be done in the <c>unpack</c> method and the <c>initParmDefault</c> method, in the
    /// <c>init</c> method (if this is used), or in the method only when the object is not instantiated.
    /// </remarks>
    QueryRun queryRun()
    {
        return  qrcSalesList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that performs the actual job of the class.
    /// </summary>
    void run()
    {
        infoLogCounter = infolog.num();
        errorFound = false;

        this.progressInit("@SYS13894",(select maxof(SeqNum) from rcSalesList_UK).SeqNum, #AviSearch);

        ttsbegin;

        while (qrcSalesList.next())
        {
            rcSalesList = qrcSalesList.get(tablenum(RCSalesList_UK));
            prefix = #PreFixField(rCSalesList,AccountNum);
            this.progressUpdate();
            this.checkRecord();
        }
        ttscommit;

        if (!errorFound)
        {
            info(strfmt("@SYS11281"));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    boolean unpack(container packedClass)
    {
        Integer          version         = conpeek(packedClass,1);
        container       packedQuery;

        switch (version)
        {
            case #CurrentVersion:
                [version,packedQuery]      = packedClass;
                if (packedQuery)
                {
                    qrcSalesList   = new QueryRun(packedQuery);
                }
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args args)
    {
        RCSalesListCheck_UK listCheck;
        FormDataSource      rcSalesList_ds;

        listCheck       = new RCSalesListCheck_UK();
        rcSalesList_ds  = FormDataUtil::getFormDataSource(args.record());
        listCheck.getLast();

        if (listCheck.prompt())
        {
            listCheck.runOperation();
        }
        rcSalesList_ds.executeQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>