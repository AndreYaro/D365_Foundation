<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailPricingKitCalculator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Calculator for kit pricing.
/// </summary>
internal final class RetailPricingKitCalculator
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getCurrentKitPrice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get current price for the given kit.
    /// </summary>
    /// <param name = "_currentRetailKit">Kit.</param>
    /// <returns>Current price for the given kit.</returns>
    internal static AmountCur getCurrentKitPrice(RetailKit _currentRetailKit)
    {
        RetailKitComponent currentRetailKitComponent;
        RetailKitReleasedComponentPricing   currentRetailKitReleasedComponentPricing;

        select sum(KitPrice) from currentRetailKitReleasedComponentPricing
            join  RecId from currentRetailKitComponent
            where currentRetailKitComponent.Kit == _currentRetailKit.RecId &&
                currentRetailKitReleasedComponentPricing.KitComponent == currentRetailKitComponent.RecId;

        return currentRetailKitReleasedComponentPricing.KitPrice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateKitPrice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update price of the given kit.
    /// </summary>
    /// <param name = "_retailKit">Retail kit.</param>
    /// <param name = "_newKitPrice">New price for the kit.</param>
    internal static void updateKitPrice(RetailKit _retailKit, Amount _newKitPrice)
    {
        int64 noOfComponents, totalNoOfComponents;
        real totalBasePrice;
        real remainingprice;
        real computedprice;
        RetailKitComponent currentRetailKitComponent;
        RetailKitReleasedComponentPricing   currentRetailKitReleasedComponentPricing;

        if (!RetailParameters::find().JournalNameForKitPricing)
        {
            throw error("@REX4161201");
        }

        ttsBegin;

        // Determine the composition of prices
        while select BasePrice from currentRetailKitReleasedComponentPricing
            join  RecId from currentRetailKitComponent
            where currentRetailKitComponent.Kit == _retailKit.RecId &&
                currentRetailKitReleasedComponentPricing.KitComponent == currentRetailKitComponent.RecId
        {
            totalBasePrice += currentRetailKitReleasedComponentPricing.BasePrice;
            noOfComponents++;
        }

        // Set remaining price as total base price
        remainingprice = _newKitPrice;
        totalNoOfComponents = noOfComponents;

        changecompany(curext())
        {
            // Set Price for each Component
            while select RecId from currentRetailKitComponent
                where currentRetailKitComponent.Kit == _retailKit.RecId
                outer join forUpdate firstOnly BasePrice,KitPrice from currentRetailKitReleasedComponentPricing
                    order by currentRetailKitReleasedComponentPricing.BasePrice
                    where currentRetailKitReleasedComponentPricing.KitComponent == currentRetailKitComponent.RecId
            {
                if (noOfComponents != 1)
                {
                    if (totalBasePrice != 0.0)
                    {
                        // Distribute the price proportionally if total base price is not equal to 0
                        computedprice = CurrencyExchangeHelper::amount(
                            currentRetailKitReleasedComponentPricing.BasePrice * _newKitPrice / totalBasePrice);
                    }
                    else
                    {
                        computedprice = 0.00;
                    }
                    remainingprice -= computedprice;
                    noOfComponents--;
                }
                else
                {
                    computedprice = remainingprice;
                }

                currentRetailKitReleasedComponentPricing.KitPrice = computedprice;
                currentRetailKitReleasedComponentPricing.update();
            }
        }

        ttsCommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>