<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestUpdFromSourceUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CFMPaymentRequestUpdFromSourceUIBuilder</c> class is the dialog UI builder for the
/// <c>CFMPaymentRequestUpdFromSourceService</c> service.
/// </summary>
class CFMPaymentRequestUpdFromSourceUIBuilder extends SysOperationAutomaticUIBuilder
{
    DialogField dialogSources;
    DialogField dialogDataAreas;
    SysLookupMultiSelectCtrl ctrlSources;
    SysLookupMultiSelectCtrl ctrlDataAreas;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    public void postRun()
    {
        container selectFieldSources = CFMPRSourceTypesSelector::selectField();
        container selectFieldDataAreas = CFMCompanyInfoSelector::selectField();
        CFMPaymentRequestUpdFromSourceContract dataContract = this.dataContractObject() as CFMPaymentRequestUpdFromSourceContract;

        super();

        ctrlSources = SysLookupMultiSelectCtrl::constructWithQueryRun(
            this.dialog().formRun(),
            dialogSources.control(),
            CFMPRSourceTypesSelector::sourceTypesQueryRun(),
            true,
            selectFieldSources);

        ctrlSources.set(this.fetchSelectedLines(CFMPRSourceTypesSelector::sourceTypesQueryRun(),
                            selectFieldSources,
                            dataContract.parmSources()));

        ctrlDataAreas = SysLookupMultiSelectCtrl::constructWithQuery(
            this.dialog().formRun(),
            dialogDataAreas.control(),
            CFMCompanyInfoSelector::companyInfoQuery(),
            true,
            selectFieldDataAreas);

        ctrlDataAreas.set(this.fetchSelectedLines(new QueryRun(CFMCompanyInfoSelector::companyInfoQuery()),
                              selectFieldDataAreas,
                              dataContract.parmDataAreas()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public void getFromDialog()
    {
        CFMPaymentRequestUpdFromSourceContract dataContract = this.dataContractObject() as CFMPaymentRequestUpdFromSourceContract;

        super();

        dataContract.parmSources(ctrlSources.getSelectedFieldValues());
        dataContract.parmDataAreas(ctrlDataAreas.getSelectedFieldValues());
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetchSelectedLines</Name>
				<Source><![CDATA[
    private container fetchSelectedLines(QueryRun _queryRun, container _selectField, container _selectedValues)
    {
        TableId selectTableId = conPeek(_selectField, 1);
        FieldId selectFieldId = conPeek(_selectField, 2);

        container selectedRecIds;
        container selectedValues;

        while (_queryRun.next())
        {
            Common common = _queryRun.get(selectTableId);

            if (conFind(_selectedValues, common.(selectFieldId)))
            {
                selectedRecIds += common.RecId;
                selectedValues += common.(selectFieldId);
            }
        }

        return [selectedRecIds, selectedValues];
    }

]]></Source>
			</Method>
			<Method>
				<Name>addDialogField</Name>
				<Source><![CDATA[
    protected DialogField addDialogField(IdentifierName _methodName, Object _dataContract = this.dataContractObject())
    {
        DialogField dialogField;

        switch (_methodName)
        {
            case methodStr(CFMPaymentRequestUpdFromSourceContract, parmSources):
                dialogSources = this.dialog().addField(extendedTypeStr(Description), "@CashFlowManagement:RequestSources", "@CashFlowManagement:SelectPaymRequestSources");
                dialogField = dialogSources;
                break;

            case methodStr(CFMPaymentRequestUpdFromSourceContract, parmDataAreas):
                dialogDataAreas = this.dialog().addField(extendedTypeStr(Description), "@SYS129100", "@CashFlowManagement:DataSourcesForPaymRequestLocSelectCompanyAccounts");
                dialogField = dialogDataAreas;
                break;

            default:
                dialogField = super(_methodName, _dataContract);
                break;
        }

        return dialogField;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>