<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SubledgerJournalValidateSourceDocumentLineStatus</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Validates the subledger journal entries source document line entries are consistent
/// </summary>
[ExportMetadataAttribute(classstr(SubledgerJournalValidateSourceDocumentLineStatus), identifierStr(''))]
[ExportMetadataAttribute(identifierStr('SourceDocumentDataConsistencyValidators'), identifierStr(''))]
[ExportAttribute('Microsoft.Dynamics.AX.Application.SubledgerJournalizerDocumentValidationIProvider')]
internal final class SubledgerJournalValidateSourceDocumentLineStatus implements SubledgerJournalizerDocumentValidationIProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the subledger journal entries source document line entries are consistent
    /// </summary>
    /// <param name = "_accountingEvent">The accounting event for the journalization task using source document accounting processor</param>
    /// <param name = "_voucher">The voucher used for the posting to ledger; if no accounting is generated then this will be blank</param>
    /// <param name = "_sourceDocumentLineSubmittedForProcessing">All the source document lines that are submitted to source document accounting processor</param>
    /// <param name = "_accountingDistributionTmpForValidation">Accounting distribution detail for the document submitted for processing</param>
    /// <param name = "_subledgerJournalAccountEntryTmpForValidation">Detail accounting of the voucher that is getting posted</param>
    [Hookable(false)]
    void validateDocument(
        AccountingEvent _accountingEvent,
        Voucher _voucher,
        SourceDocumentLineRecIdTmp _sourceDocumentLineSubmittedForProcessing,
        AccountingDistributionTmpForValidation _accountingDistributionTmpForValidation,
        SubledgerJournalAccountEntryTmpForValidation _subledgerJournalAccountEntryTmpForValidation)
    {
        using (SysInstrumentationActivityContext activityContext = SourceDocumentAccountingFrameworkInstrumentationLogger::getActivityContext(
            SourceDocumentInstrumentationConstants::SourceDocumentValidationFrameworkNamespace,
            classStr(SubledgerJournalValidateSourceDocumentLineStatus),
            classStr(SubledgerJournalValidateSourceDocumentLineStatus)))
        {
            SourceDocumentLine sourceDocumentLine;
            AccountingDistribution accountingDistribution;
        
            //check if there is a source document line record that is being journalized in an invalid state
            select firstonly RecId from sourceDocumentLine
                where sourceDocumentLine.SourceDocumentHeader == _accountingEvent.SourceDocumentHeader &&
                    (sourceDocumentLine.AccountingStatus == SourceDocumentLineAccountingStatus::Draft ||
                    sourceDocumentLine.AccountingStatus == SourceDocumentLineAccountingStatus::FullyDistributed ||
                    sourceDocumentLine.AccountingStatus == SourceDocumentLineAccountingStatus::None ||
                    sourceDocumentLine.AccountingStatus == SourceDocumentLineAccountingStatus::PartlyDistributed)
            exists join _sourceDocumentLineSubmittedForProcessing
                where _sourceDocumentLineSubmittedForProcessing.SourceDocumentLineRecId == sourceDocumentLine.RecId;
        
            if (sourceDocumentLine.RecId != 0)
            {
                //log telemetry that the source document lines are in an invalid accounting status
                activityContext.addCustomProperty('ValidationResult', 'Fail');

                activityContext.addCustomProperty(
                    'Message', SourceDocumentInstrumentationConstants::ErrorWhileValidatingSourceDocumentLineDataForDocument);

                activityContext.addCustomProperty(
                    'SourceDocumentHeader', int642Str(_accountingEvent.SourceDocumentHeader));

                activityContext.addCustomProperty(
                    'AccountingEvent', int642Str(_accountingEvent.RecId));

                if (FeatureStateProvider::isFeatureEnabled(EnableSourceDocumentValidationFramework::instance()))
                {
                    throw error("@SourceDocumentAccountingFramework:ErrorMessageForValidatingSourceDocumentLineAccountingStatusDuringJournalization");
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>