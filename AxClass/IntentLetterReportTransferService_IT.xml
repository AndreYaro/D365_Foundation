<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntentLetterReportTransferService_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustIntentLetterCancelService_IT</c> class gets actions
/// for intent letter data transfering.
/// </summary>
public class IntentLetterReportTransferService_IT extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runTransfer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfers intent letter data to the selected model.
    /// </summary>
    /// <param name = "_dataContract">The data contract to be used for intent letter data transfering.</param>
    public void runTransfer(IntentLetterReportTransferDataContract_IT _dataContract)
    {
        IntentLetter_IT intentLetter;
        IntentLetterReportTrans_IT reportTrans;
        RecordInsertList recordList = new RecordInsertList(tableNum(IntentLetterReportTrans_IT));

        IntentLetterReportTable_IT intentLetterReportTable = _dataContract.parmIntentLetterReportTable();
        ttsbegin;

        if (intentLetterReportTable.ReportId)
        {
            delete_from reportTrans
                where reportTrans.ReportId == intentLetterReportTable.ReportId;

            QueryRun queryRun = new QueryRun(_dataContract.getQuery());

            while (queryRun.next())
            {
                intentLetter = queryRun.get(tableNum(IntentLetter_IT));

                reportTrans.clear();
                reportTrans.ReportId = intentLetterReportTable.ReportId;
                reportTrans.initFromIntentLetter(intentLetter);
                recordList.add(reportTrans);

                intentLetter.selectForUpdate(true);
                intentLetter.ReportId = intentLetterReportTable.ReportId;
                intentLetter.update();
            }

            recordList.insertDatabase();
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>