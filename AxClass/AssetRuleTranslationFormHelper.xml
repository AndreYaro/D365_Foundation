<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetRuleTranslationFormHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AssetRuleTranslationFormHelper</c> class is used to start the translation form for records
/// that pertain to an asset rule.
/// </summary>
class AssetRuleTranslationFormHelper
{
    recId translateFromRecId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>launchTranslationDetailForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Launches the system translation form.
    /// </summary>
    /// <param name="_helper">
    ///    An instance of the <c>SysTranslationHelper</c> class.
    /// </param>
    public void launchTranslationDetailForm(SysTranslationHelper _helper)
    {
        _helper.launchTranslationDetailForm(translateFromRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTranslateFromRecId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or sets the record ID of the record to be translated.
    /// </summary>
    /// <param name="_recId">
    ///    The record ID of the record to be set.
    /// </param>
    /// <returns>
    ///    The record ID of the record to be translated.
    /// </returns>
    public recId parmTranslateFromRecId(recId _recId = translateFromRecId)
    {
        translateFromRecId = _recId;

        return translateFromRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructSysTranslationHelper</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>SysTranslationHelper</c> class.
    /// </summary>
    /// <param name="_mainTableId">
    /// The table ID of the record from which to translate.
    /// </param>
    /// <returns>
    /// A new instance of the <c>SysTranslationHelper</c> class.
    /// </returns>
    public static SysTranslationHelper constructSysTranslationHelper(tableId _mainTableId)
    {
        tableId              translationTableId;
        SysTranslationHelper helper;

        switch (_mainTableId)
        {
            case tableNum(AssetRuleQualifier) :
                    translationTableId = tableNum(AssetRuleQualifierLanguage);            break;
            case tableNum(AssetRuleQualifierOption) :
                    translationTableId = tableNum(AssetRuleQualifierOptionLanguage);      break;
            case tableNum(AssetRuleQualifierLocal) :
                    translationTableId = tableNum(AssetRuleQualifierLanguageLocal);       break;
            case tableNum(AssetRuleQualifierOptionLocal) :
                    translationTableId = tableNum(AssetRuleQualifierOptionLanguageLocal); break;
        }

        helper = new SysTranslationHelper(_mainTableId, translationTableId);

        return helper;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args args)
    {
        AssetRuleTranslationFormHelper  assetRuleTranslationFormHelper = AssetRuleTranslationFormHelper::newFromArgs(args);

        assetRuleTranslationFormHelper.launchTranslationDetailForm(AssetRuleTranslationFormHelper::constructSysTranslationHelper(args.dataset()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>AssetRuleTranslationFormHelper</c> class.
    /// </summary>
    /// <param name="_args">
    /// The input arguments.
    /// </param>
    /// <returns>
    /// A new instance of the <c>AssetRuleTranslationFormHelper</c> class.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    /// The method has been called with the wrong table.
    /// </exception>
    public static AssetRuleTranslationFormHelper newFromArgs(Args _args)
    {
        AssetRuleTranslationFormHelper  assetRuleTranslationFormHelper;

        if (!(_args &&
               (_args.record() is AssetRuleQualifier       ||
                _args.record() is AssetRuleQualifierOption ||
                _args.record() is AssetRuleQualifierLocal  ||
                _args.record() is AssetRuleQualifierOptionLocal
               )
             )
           )
        {
            throw error(strFmt("@SYS23396",funcName()));
        }

        assetRuleTranslationFormHelper = new AssetRuleTranslationFormHelper();
        assetRuleTranslationFormHelper.parmTranslateFromRecId(_args.record().RecId);

        return assetRuleTranslationFormHelper;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>