<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetInventoryWorkSheetUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AssetInventoryWorkSheetUIBuilder</c> class is responsible for building an input dialog for the <c>AssetInventoryWorkSheet</c> report.
/// </summary>
class AssetInventoryWorkSheetUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField     dialogDimension;
    DialogField     dialogGroupBy;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Builds the UI of the report input dialog.
    /// </summary>
    /// <remarks>
    ///    This method has been overridden to build two nested groups that have a radio button style.
    /// </remarks>
    public void build()
    {
        DialogGroup dialogGroup;

        this.dialog().addGroup("@SYS190436");

        this.addDialogField(methodStr(AssetInventoryWorkSheetContract, parmPostingLayer));

        dialogGroup = this.dialog().addGroup("@SYS99522");
        dialogGroup.frameType(FormFrameType::None);
        dialogGroup.columns(2);

        dialogGroupBy = this.addDialogField(methodStr(AssetInventoryWorkSheetContract, parmGroupBy));
        dialogDimension = this.addDialogField(methodStr(AssetInventoryWorkSheetContract, parmDimension));
        dialogDimension.lookupButton(FormLookupButton::Always);

        this.dialog().addGroup("@SYS13865");

        this.addDialogField(methodStr(AssetInventoryWorkSheetContract, parmSubTotal));

        // The dimension field should reflect the state based on the group by selection read from SysLastValue
        this.selectionChange(null);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupDimensionAttribute</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Implements a custom lookup for dimension attribute.
    /// </summary>
    /// <param name="_control">
    ///    A <c>FormStringControl</c> that will be used for the lookup.
    /// </param>
    /// <remarks>
    ///    This method uses the <c>DimensionAttribute</c> table for lookup.
    /// </remarks>
    public void lookupDimensionAttribute(FormStringControl _control)
    {
        Query query;
        SysTableLookup sysTableLookup;
        QueryBuildDataSource qbds;
        QueryBuildDataSource qbdsSet;
        DimensionEnumeration dimensionSetId = DimensionCache::getDimensionAttributeSetForLedger();

        sysTableLookup = SysTableLookup::newParameters(tableNum(DimensionAttribute), _control);

        sysTableLookup.addLookupfield(fieldNum(DimensionAttribute, Name));

        query = new Query();
        qbds = query.addDataSource(tableNum(DimensionAttribute));
        qbds.addRange(fieldNum(DimensionAttribute, Type)).value(int2str(enum2int(DimensionAttributeType::ExistingList)));
        qbds.addRange(fieldNum(DimensionAttribute, Type)).value(int2str(enum2int(DimensionAttributeType::CustomList)));

        qbdsSet = qbds.addDataSource(tableNum(DimensionAttributeSetItem));
        qbdsSet.joinMode(JoinMode::ExistsJoin);
        qbdsSet.addLink(fieldNum(DimensionAttribute, RecId), fieldNum(DimensionAttributeSetItem, DimensionAttribute));
        qbdsSet.addRange(fieldNum(DimensionAttributeSetItem, DimensionAttributeSet)).value(SysQuery::value(dimensionSetId));

        sysTableLookup.parmQuery(query);

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides the defaults in order to handle events on dialogs.
    /// </summary>
    public void postRun()
    {
        super();

        dialogDimension.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(AssetInventoryWorkSheetUIBuilder, lookupDimensionAttribute),
            this);

        dialogGroupBy.registerOverrideMethod(
            methodStr(FormRadioControl, selectionChange),
            methodStr(AssetInventoryWorkSheetUIBuilder, selectionChange),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectionChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// The <c>Dimension</c> field is enabled or disabled based on selection change of <c>GroupBy</c> field.
    /// </summary>
    /// <param name="_control">
    /// The <c>FormRadioControl</c> takes the reference of a dialog dimension.
    /// </param>
    public void selectionChange(FormRadioControl _control)
    {
        if (dialogGroupBy.value() == AssetInventoryWorksheetGroupBy::Dimension)
        {
            dialogDimension.enabled(true);
        }
        else
        {
            dialogDimension.enabled(false);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>