<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntentLetterCreateUIBuilder_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>IntentLetterCreateUIBuilder</c> builder for intent letter creation.
/// </summary>
public class IntentLetterCreateUIBuilder_IT extends SysOperationAutomaticUIBuilder
{
    public DialogField dlgFromDate,
        dlgToDate,
        dlgLetterDate,
        dlgLetterType,
        dlgAmountMST,
        dlgLetterNotes;

    public IntentLetterCreateDataContract_IT contract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    public void build()
    {
        super();

        contract = this.dataContractObject();

        dlgLetterType  = this.bindInfo().getDialogField(contract, methodstr(IntentLetterCreateDataContract_IT, parmLetterType));
        dlgAmountMST = this.bindInfo().getDialogField(contract, methodstr(IntentLetterCreateDataContract_IT, parmAmountMST));

    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    public void postRun()
    {
        super();

        dlgLetterType.registerOverrideMethod(
            methodStr(FormComboBoxControl, modified),
            methodstr(IntentLetterCreateUIBuilder_IT, dlgLetterType_modified),
            this);

        dlgLetterType.registerOverrideMethod(
            methodStr(FormComboBoxControl, validate),
            methodstr(IntentLetterCreateUIBuilder_IT, dlgLetterType_validate),
            this);

        dlgLetterType.value(IntentLetterType_IT::Amount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableControls</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables / disables dialog controls.
    /// </summary>
    private void enableControls(IntentLetterType_IT _intentLetterType)
    {
        switch (_intentLetterType)
        {
            case IntentLetterType_IT::Period:
                dlgAmountMST.enabled(false);
                break;

            case IntentLetterType_IT::Amount:
            case IntentLetterType_IT::SpecificOperation:
                dlgAmountMST.enabled(true);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dlgLetterType_modified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables fields when letter type change.
    /// </summary>
    /// <param name = "_control"><c>FormComboBoxControl</c> object.</param>
    /// <returns>true if letter type was changed; false otherwise.</returns>
    private boolean dlgLetterType_modified(FormComboBoxControl _control)
    {
        boolean valueWasModified = _control.modified();

        if (valueWasModified)
        {
            this.enableControls(dlgLetterType.value());
        }

        return valueWasModified;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dlgLetterType_validate</Name>
				<Source><![CDATA[
    private boolean dlgLetterType_validate(FormComboBoxControl _control)
    {
        boolean ret = _control.validate();

        if (dlgLetterType.value() == IntentLetterType_IT::Period)
        {
            ret = checkFailed("@ExtendedItallianLocalization:PeriodLetterTypeOutdated");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showClearButton</Name>
				<Source><![CDATA[
    public boolean showClearButton()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showDefaultButton</Name>
				<Source><![CDATA[
    public boolean showDefaultButton()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>