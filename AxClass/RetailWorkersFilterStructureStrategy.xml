<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailWorkersFilterStructureStrategy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailWorkersFilterStructureStrategy</c> class applies a <c>SysIFilterStructureItem</c> class
/// to  <c>RetailWorkers</c> query. Org hierarchy filter is applied to the query in store management workspace.
/// </summary>
[
// Workers query used in Retail store management workspace .
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailWorkers))
]
public class RetailWorkersFilterStructureStrategy extends SysFilterStructureStrategyQuery implements SysIFilterStructureStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resolve</Name>
				<Source><![CDATA[
    protected boolean resolve(SysIFilterValueType _filterValueType)
    {
        boolean result = false;
        if (_filterValueType.parmType() == SysFilterValueTypeEnum::Integer)
        {
            if (_filterValueType.parmTypeIdentifier() == extendedTypeNum(RecId))
            {
                QueryBuildDataSource DirAddressBookPartyQdbs = this.getQueryBuildDataSource(tableNum(DirAddressBookParty));
                QueryBuildDataSource explodedOrgSecurityGraphQdbs = this.getQueryBuildDataSource(tableNum(OMExplodedOrganizationSecurityGraph));
                QueryBuildDataSource omHierarchyRelationshipQdbs = this.getQueryBuildDataSource(tableNum(OMHierarchyRelationship));

                // if a filter is specified and is not blank then apply the filter. In the workspace filter implementation logic, a SysFilterOperator::Equal operator is used  if the filter value is not blank.
                if (filterStructureItem.parmOperator() == SysFilterOperator::Equal)
                {
                    DirAddressBookPartyQdbs.enabled(true);
                    explodedOrgSecurityGraphQdbs.enabled(true);
                    omHierarchyRelationshipQdbs.enabled(true);
                    this.addLinkFromTableToFilter(omHierarchyRelationshipQdbs, fieldNum(OMHierarchyRelationship, RecId));
                }
                else // if filter is blank, then disable the datasource that is used to filter records by org hierachy filter value.
                    // in this case since we dont want to filter out records that do not have addressbook association we're disabling the DirAddressBookParty
                    // which will also disable the org related datasource nested under it
                {
                    DirAddressBookPartyQdbs.enabled(false);
                }
                result = true;
            }
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryBuildDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>QueryBuildDataSource</c> object from the query.
    /// </summary>
    /// <param name = "_tableId">The table id of the datasource.</param>
    /// <returns>The <c>QueryBuildDataSource</c> object if found; otherwise throw an error.</returns>
    public QueryBuildDataSource getQueryBuildDataSource(TableId _tableId)
    {
        Query _query = this.query;
        QueryBuildDataSource qdbs = _query.dataSourceTable(_tableId);
        if (!qdbs)
        {
            error(strFmt("@SYS89000", tableId2Name(_tableId), _query.name()));
        }
        return qdbs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addLinkFromTableToFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a link to the <c>SysFilterTable</c> table.
    /// </summary>
    /// <param name="_relatedTableDataSource">
    /// The <c>QueryBuildDataSource</c> class to which the link should be added.
    /// </param>
    /// <param name="_fieldId">
    /// The <c>FieldId</c> class for which the link should be added.
    /// </param>
    /// <param name="_filterOperator">
    /// The <c>SysFilterOperator</c> used in the range.
    /// </param>
    public void addLinkFromTableToFilter(
        QueryBuildDataSource _relatedTableDataSource,
        FieldId _fieldId,
        SysFilterOperator _filterOperator = SysFilterOperator::Equal)
    {
        QueryBuildRange qbr = SysQueryBase::findOrCreateRange(_relatedTableDataSource, _fieldId);
        qbr.status(RangeStatus::Hidden);
        // ((targetTable.TargetField <operator> sysFilterTable.ValueField) OR (sysFilterTable.ValueResolutionMethod != SysFilterValueResolutionMethod::None))
        qbr.value(strfmt('(((%1.%2 %3 %4.%5) || (%4.%6 != %7)))',
            _relatedTableDataSource.name(),
            fieldId2Name(_relatedTableDataSource.table(), _fieldId),
            SysFilterOperatorHelper::operator2SqlStr(_filterOperator),
            filterDatasource.name(),
            fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId()),
            fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValueResolutionMethod(), filterStructureItem.parmId()),
            int2Str(SysFilterValueResolutionMethod::None)));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>