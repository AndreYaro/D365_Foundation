<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventWarehouseOnHandAggregatedViewBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventWarehouseOnHandAggregatedViewBuilder</c> class is used to create the <c>InventWarehouseOnHandAggregatedView</c> view.
/// </summary>
public class InventWarehouseOnHandAggregatedViewBuilder extends InventOnHandAggregatedViewBuilder
{
    private TableName       aggregatedViewName;
    private SysDictView     inventWarehouseOnHandAggregatedViewDictionary;
    private SysDictView     inventWarehouseOnHandViewDictionary;
   
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildView</Name>
				<Source><![CDATA[
    [Hookable(false), Wrappable(false)]
    public static void buildView()
    {
        #OccRetryCount
        try
        {
            InventWarehouseOnHandAggregatedViewBuilder::construct().createSqlObjects();
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw Exception::Deadlock;
            }
            else
            {
                retry;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmViewName</Name>
				<Source><![CDATA[
    protected TableName parmViewName()
    {
        return aggregatedViewName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceViewName</Name>
				<Source><![CDATA[
    protected TableName parmSourceViewName()
    {
        return inventWarehouseOnHandViewDictionary.name(DbBackend::Sql);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildViewSqlSelectStatement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds a select SQl statement to query for inventory on-hand by warehouse data.
    /// </summary>
    /// <returns>SQL statement to retrieve inventory on-hand by warehouse data.</returns>
    /// <remarks>
    /// Creates the following SQL statement:
    /// SELECT INVENTWAREHOUSEONHANDVIEW.ITEMNUMBER AS ITEMNUMBER
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTNAME AS PRODUCTNAME
    ///     ,INVENTWAREHOUSEONHANDVIEW.INVENTORYSITEID AS INVENTORYSITEID
    ///     ,INVENTWAREHOUSEONHANDVIEW.INVENTORYWAREHOUSEID AS INVENTORYWAREHOUSEID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTCOLORID AS PRODUCTCOLORID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTCONFIGURATIONID AS PRODUCTCONFIGURATIONID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTSIZEID AS PRODUCTSIZEID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTSTYLEID AS PRODUCTSTYLEID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PRODUCTVERSIONID AS PRODUCTVERSIONID
    ///     ,INVENTWAREHOUSEONHANDVIEW.ONHANDQUANTITY AS ONHANDQUANTITY
    ///     ,INVENTWAREHOUSEONHANDVIEW.RESERVEDONHANDQUANTITY AS RESERVEDONHANDQUANTITYMAPPED
    ///     ,INVENTWAREHOUSEONHANDVIEW.AVAILABLEONHANDQUANTITY AS AVAILABLEONHANDQUANTITYMAPPED
    ///     ,INVENTWAREHOUSEONHANDVIEW.ORDEREDQUANTITY AS ORDEREDQUANTITYMAPPED
    ///     ,INVENTWAREHOUSEONHANDVIEW.RESERVEDORDEREDQUANTITY AS RESERVEDORDEREDQUANTITYMAPPED
    ///     ,INVENTWAREHOUSEONHANDVIEW.AVAILABLEORDEREDQUANTITY AS AVAILABLEORDEREDQUANTITYMAPPED
    ///     ,INVENTWAREHOUSEONHANDVIEW.ONORDERQUANTITY AS ONORDERQUANTITY
    ///     ,INVENTWAREHOUSEONHANDVIEW.TOTALAVAILABLEQUANTITY AS TOTALAVAILABLEQUANTITY
    ///     ,INVENTWAREHOUSEONHANDVIEW.AREWAREHOUSEMANAGEMENTPROCESSESUSED AS AREWAREHOUSEMANAGEMENTPROCESSESUSED
    ///     ,CALCULATIONS.RESERVEDONHANDQUANTITY AS RESERVEDONHANDQUANTITY
    ///     ,(INVENTWAREHOUSEONHANDVIEW.ONHANDQUANTITY - CALCULATIONS.RESERVEDONHANDQUANTITY) AS AVAILABLEONHANDQUANTITY
    ///     ,CALCULATIONS.ORDEREDQUANTITY AS ORDEREDQUANTITY
    ///     ,CALCULATIONS.RESERVEDORDEREDQUANTITY AS RESERVEDORDEREDQUANTITY
    ///     ,(CALCULATIONS.ORDEREDQUANTITY - CALCULATIONS.RESERVEDORDEREDQUANTITY) AS AVAILABLEORDEREDQUANTITY
    ///     ,INVENTWAREHOUSEONHANDVIEW.RECID AS RECID
    ///     ,INVENTWAREHOUSEONHANDVIEW.DATAAREAID AS DATAAREAID
    ///     ,INVENTWAREHOUSEONHANDVIEW.PARTITION AS PARTITION
    /// FROM INVENTWAREHOUSEONHANDVIEW
    /// CROSS APPLY (
    ///     SELECT COALESCE(SUM(WHSINVENTRESERVE.RESERVPHYSICAL), 0) AS RESERVPHYSICAL
    ///         ,COALESCE(SUM(WHSINVENTRESERVE.RESERVORDERED), 0) AS RESERVORDERED
    ///         ,COALESCE(SUM(WHSINVENTRESERVE.AVAILORDERED), 0) AS AVAILORDERED
    ///         ,COALESCE(SUM(WHSINVENTRESERVE.AVAILPHYSICAL), 0) AS AVAILPHYSICAL
    ///     FROM WHSINVENTRESERVE
    ///     WHERE WHSINVENTRESERVE.ITEMID = INVENTWAREHOUSEONHANDVIEW.ITEMNUMBER
    ///         AND WHSINVENTRESERVE.PARTITION = INVENTWAREHOUSEONHANDVIEW.PARTITION
    ///         AND WHSINVENTRESERVE.DATAAREAID = INVENTWAREHOUSEONHANDVIEW.DATAAREAID
    ///         AND EXISTS (
    ///             SELECT TOP 1 INVENTDIM.INVENTDIMID
    ///             FROM INVENTDIM
    ///             WHERE INVENTDIM.DATAAREAID = WHSINVENTRESERVE.DATAAREAID
    ///                 AND INVENTDIM.INVENTDIMID = WHSINVENTRESERVE.INVENTDIMID
    ///                 AND INVENTDIM.PARTITION = WHSINVENTRESERVE.PARTITION
    ///                 AND (
    ///                     INVENTDIM.INVENTSITEID = INVENTWAREHOUSEONHANDVIEW.INVENTORYSITEID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.INVENTORYSITEID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.INVENTLOCATIONID = INVENTWAREHOUSEONHANDVIEW.INVENTORYWAREHOUSEID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.INVENTORYWAREHOUSEID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.INVENTCOLORID = INVENTWAREHOUSEONHANDVIEW.PRODUCTCOLORID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.PRODUCTCOLORID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.CONFIGID = INVENTWAREHOUSEONHANDVIEW.PRODUCTCONFIGURATIONID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.PRODUCTCONFIGURATIONID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.INVENTSIZEID = INVENTWAREHOUSEONHANDVIEW.PRODUCTSIZEID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.PRODUCTSIZEID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.INVENTSTYLEID = INVENTWAREHOUSEONHANDVIEW.PRODUCTSTYLEID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.PRODUCTSTYLEID = ''
    ///                     )
    ///                 AND (
    ///                     INVENTDIM.INVENTVERSIONID = INVENTWAREHOUSEONHANDVIEW.PRODUCTVERSIONID
    ///                     OR INVENTWAREHOUSEONHANDVIEW.PRODUCTVERSIONID = ''
    ///                     )
    ///                 AND WHSINVENTRESERVE.HIERARCHYLEVEL = (
    ///                     SELECT MAX(WHSRESERVATIONHIERARCHYELEMENT.RESERVATIONHIERARCHYLEVEL)
    ///                     FROM WHSRESERVATIONHIERARCHYELEMENT
    ///                     JOIN WHSRESERVATIONHIERARCHYITEM ON WHSRESERVATIONHIERARCHYITEM.ITEMDATAAREAID = INVENTWAREHOUSEONHANDVIEW.DATAAREAID
    ///                         AND WHSRESERVATIONHIERARCHYELEMENT.PARTITION = INVENTWAREHOUSEONHANDVIEW.PARTITION
    ///                         AND WHSRESERVATIONHIERARCHYELEMENT.RESERVATIONHIERARCHY = WHSRESERVATIONHIERARCHYITEM.RESERVATIONHIERARCHY
    ///                         AND WHSRESERVATIONHIERARCHYITEM.ITEMID = INVENTWAREHOUSEONHANDVIEW.ITEMNUMBER
    ///                     WHERE (
    ///                             WHSRESERVATIONHIERARCHYELEMENT.DIMENSIONFIELDID = 10
    ///                             AND INVENTWAREHOUSEONHANDVIEW.INVENTORYSITEID != ''
    ///                             )
    ///                         OR (
    ///                             WHSRESERVATIONHIERARCHYELEMENT.DIMENSIONFIELDID = 6
    ///                             AND INVENTWAREHOUSEONHANDVIEW.INVENTORYWAREHOUSEID != ''
    ///                             )
    ///                     )
    ///             )
    ///     ) INVENTRESERVES
    /// CROSS APPLY (
    ///     SELECT (
    ///             CASE
    ///                 WHEN INVENTWAREHOUSEONHANDVIEW.AREWAREHOUSEMANAGEMENTPROCESSESUSED = 1
    ///                     THEN INVENTRESERVES.AVAILORDERED + INVENTRESERVES.RESERVORDERED - INVENTRESERVES.AVAILPHYSICAL
    ///                 ELSE INVENTWAREHOUSEONHANDVIEW.ORDEREDQUANTITY
    ///                 END
    ///             ) AS ORDEREDQUANTITY
    ///         ,(
    ///             CASE
    ///                 WHEN INVENTWAREHOUSEONHANDVIEW.AREWAREHOUSEMANAGEMENTPROCESSESUSED = 1
    ///                     THEN INVENTRESERVES.RESERVORDERED
    ///                 ELSE INVENTWAREHOUSEONHANDVIEW.RESERVEDORDEREDQUANTITY
    ///                 END
    ///             ) AS RESERVEDORDEREDQUANTITY
    ///         ,(
    ///             CASE
    ///                 WHEN INVENTWAREHOUSEONHANDVIEW.AREWAREHOUSEMANAGEMENTPROCESSESUSED = 1
    ///                     THEN INVENTRESERVES.RESERVPHYSICAL
    ///                 ELSE INVENTWAREHOUSEONHANDVIEW.RESERVEDONHANDQUANTITY
    ///                 END
    ///             ) AS RESERVEDONHANDQUANTITY
    ///     ) CALCULATIONS

    /// </remarks>
    protected SysSQLStatementText buildViewSqlSelectStatement()
    {
        FieldName sourceItemNumberFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ItemNumber));
        FieldName asItemNumberFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ItemNumber), true);

        FieldName sourceProductNameFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductName));
        FieldName asProductNameFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductName), true);

        FieldName sourceOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, OnHandQuantity));
        FieldName asOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, OnHandQuantity), true);

        FieldName sourceReservedOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ReservedOnHandQuantity));
        FieldName asReservedOnHandQuantityMappedFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ReservedOnHandQuantityMapped), true);

        FieldName sourceAvailableOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, AvailableOnHandQuantity));
        FieldName asAvailableOnHandQuantityMappedFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, AvailableOnHandQuantityMapped), true);

        FieldName sourceOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, OrderedQuantity));
        FieldName asOrderedQuantityMappedFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, OrderedQuantityMapped), true);

        FieldName sourceReservedOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ReservedOrderedQuantity));
        FieldName asReservedOrderedQuantityMappedFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ReservedOrderedQuantityMapped), true);

        FieldName sourceAvailableOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, AvailableOrderedQuantity));
        FieldName asAvailableOrderedQuantityMappedFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, AvailableOrderedQuantityMapped), true);

        FieldName sourceOnOrderQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, OnOrderQuantity));
        FieldName asOnOrderQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, OnOrderQuantity), true);

        FieldName sourceTotalAvailableQuantityFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, TotalAvailableQuantity));
        FieldName asTotalAvailableQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, TotalAvailableQuantity), true);

        FieldName sourceAreWarehouseManagementProcessesUsedFieldExpression;
        FieldName isWhsItemFieldAlias;

        if (this.isConfigurationKeyEnabledForEdt(new SysDictType(extendedTypeNum(WHSIsItemWHSEnabled))))
        {
            sourceAreWarehouseManagementProcessesUsedFieldExpression = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, AreWarehouseManagementProcessesUsed));
            isWhsItemFieldAlias = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, AreWarehouseManagementProcessesUsed), true);
        }
        else
        {
            sourceAreWarehouseManagementProcessesUsedFieldExpression = '0';
            isWhsItemFieldAlias = fieldStr(InventWarehouseOnHandAggregatedView, AreWarehouseManagementProcessesUsed);
        }

        str isWhsItemExpression = this.as(sourceAreWarehouseManagementProcessesUsedFieldExpression, isWhsItemFieldAlias);

        FieldName sourceRecIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, RecId));
        FieldName asRecIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, RecId), true);

        FieldName sourceDataAreaIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, DataAreaId));
        FieldName asDataAreaIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, DataAreaId), true);

        FieldName sourcePartitionFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, Partition));
        FieldName asPartitionFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, Partition), true);

        FieldName asReservedOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ReservedOnHandQuantity), true);
        FieldName asAvailableOnHandQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, AvailableOnHandQuantity), true);
        FieldName asOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, OrderedQuantity), true);
        FieldName asReservedOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ReservedOrderedQuantity), true);
        FieldName asAvailableOrderedQuantityFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, AvailableOrderedQuantity), true);
        
        FieldName sourcePartitionFullFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, Partition));
        FieldName sourceDataAreaIdFullFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, DataAreaId));
        
        return 'SELECT ' +
            this.buildExtensionFields() +
            this.as(sourceItemNumberFieldName, asItemNumberFieldName) + ', ' +
            this.as(sourceProductNameFieldName, asProductNameFieldName) + ', ' +
            this.inventoryDimensionsToSelectFromSource() +
            this.as(sourceOnHandQuantityFieldName, asOnHandQuantityFieldName) + ', ' +
            this.as(sourceReservedOnHandQuantityFieldName, asReservedOnHandQuantityMappedFieldName) + ', ' +
            this.as(sourceAvailableOnHandQuantityFieldName, asAvailableOnHandQuantityMappedFieldName) + ', ' +
            this.as(sourceOrderedQuantityFieldName, asOrderedQuantityMappedFieldName) + ', ' +
            this.as(sourceReservedOrderedQuantityFieldName, asReservedOrderedQuantityMappedFieldName) + ', ' +
            this.as(sourceAvailableOrderedQuantityFieldName, asAvailableOrderedQuantityMappedFieldName) + ', ' +
            this.as(sourceOnOrderQuantityFieldName, asOnOrderQuantityFieldName) + ', ' +
            this.as(sourceTotalAvailableQuantityFieldName, asTotalAvailableQuantityFieldName) + ', ' +
            isWhsItemExpression + ', ' +
            this.as(
                this.buildFullFieldName(CalculationsSubQueryAlias, asReservedOnHandQuantityFieldName),
                asReservedOnHandQuantityFieldName) + ', ' +
            this.as(
                this.availableOnHandQuantityExpression(sourceOnHandQuantityFieldName, asReservedOnHandQuantityFieldName),
                asAvailableOnHandQuantityFieldName) + ', ' +
            this.as(
                this.buildFullFieldName(CalculationsSubQueryAlias, asOrderedQuantityFieldName),
                asOrderedQuantityFieldName) + ', ' +
            this.as(
                this.buildFullFieldName(CalculationsSubQueryAlias, asReservedOrderedQuantityFieldName),
                asReservedOrderedQuantityFieldName) + ', ' +
            this.as(
                this.availableOrderedQuantityExpression(asOrderedQuantityFieldName, asReservedOrderedQuantityFieldName),
                asAvailableOrderedQuantityFieldName) + ', ' +
            this.as(sourceRecIdFieldName, asRecIdFieldName) + ', ' +
            this.as(sourceDataAreaIdFieldName, asDataAreaIdFieldName) + ', ' +
            this.as(sourcePartitionFieldName, asPartitionFieldName) +
            ' FROM ' + inventWarehouseOnHandViewDictionary.name(DbBackend::Sql) +
            ' CROSS APPLY ( ' + this.inventReserveSelectStatement(sourceDataAreaIdFullFieldName, sourcePartitionFullFieldName, sourceItemNumberFieldName) +
            ') ' + InventReserveSubQueryAlias +
            ' CROSS APPLY (' +
            '   SELECT ' +
            this.as(
                this.orderedQuantityExpression(sourceOrderedQuantityFieldName, sourceAreWarehouseManagementProcessesUsedFieldExpression),
                asOrderedQuantityFieldName)  + ', ' +
            this.as(
                this.reservedOrderedQuantityExpression(sourceReservedOrderedQuantityFieldName, sourceAreWarehouseManagementProcessesUsedFieldExpression),
                asReservedOrderedQuantityFieldName) + ', ' +
            this.as(
                this.reservedOnHandQuantityExpression(sourceReservedOnHandQuantityFieldName, sourceAreWarehouseManagementProcessesUsedFieldExpression),
                asReservedOnHandQuantityFieldName) + ') ' + CalculationsSubQueryAlias;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initInventDimensionsMetadataEntries</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes metadata regarding used inventory dimension fields.
    /// </summary>
    protected void initInventDimensionsMetadataEntries()
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(Config)))
        {
            FieldName sourceProductConfigurationIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductConfigurationId));
            FieldName asProductConfigurationIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductConfigurationId), true);
            this.addInventDimensionMetadata(asProductConfigurationIdFieldName, sourceProductConfigurationIdFieldName, fieldNum(InventDim, configId));
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(EcoResProductColor)))
        {
            FieldName sourceProductColorIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductColorId));
            FieldName asProductColorIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductColorId), true);
            this.addInventDimensionMetadata(asProductColorIdFieldName, sourceProductColorIdFieldName, fieldNum(InventDim, InventColorId));
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(EcoResProductSize)))
        {
            FieldName sourceProductSizeIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductSizeId));
            FieldName asProductSizeIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductSizeId), true);
            this.addInventDimensionMetadata(asProductSizeIdFieldName, sourceProductSizeIdFieldName, fieldNum(InventDim, InventSizeId));
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(EcoResProductStyle)))
        {
            FieldName sourceProductStyleIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductStyleId));
            FieldName asProductStyleIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductStyleId), true);
            this.addInventDimensionMetadata(asProductStyleIdFieldName, sourceProductStyleIdFieldName, fieldNum(InventDim, InventStyleId));
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(EcoResProductVersion)))
        {
            FieldName sourceProductVersionIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, ProductVersionId));
            FieldName asProductVersionIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, ProductVersionId), true);
            this.addInventDimensionMetadata(asProductVersionIdFieldName, sourceProductVersionIdFieldName, fieldNum(InventDim, InventVersionId));
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(LogisticsBasic)))
        {
            FieldName sourceInventorySiteIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, InventorySiteId));
            FieldName asInventorySiteIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, InventorySiteId), true);
            this.addInventDimensionMetadata(asInventorySiteIdFieldName, sourceInventorySiteIdFieldName, fieldNum(InventDim, InventSiteId), true);

            FieldName sourceInventoryLocationIdFieldName = this.buildFieldName(inventWarehouseOnHandViewDictionary, fieldNum(InventWarehouseOnHandView, InventoryWarehouseId));
            FieldName asInventoryLocationIdFieldName = this.buildFieldName(inventWarehouseOnHandAggregatedViewDictionary, fieldNum(InventWarehouseOnHandAggregatedView, InventoryWarehouseId), true);
            this.addInventDimensionMetadata(asInventoryLocationIdFieldName, sourceInventoryLocationIdFieldName, fieldNum(InventDim, InventLocationId), true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Recreates the <c>InventWarehouseOnHandAggregatedView</c> view.
    /// </summary>
    /// <remarks>
    /// Can be used for testing purposes.
    /// </remarks>
    /// <param name = "_args">Method arguments.</param>
    public static void main(Args _args)
    {
        InventWarehouseOnHandAggregatedViewBuilder::construct().createSqlObjects();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        super();

        inventWarehouseOnHandAggregatedViewDictionary = new SysDictView(tableNum(InventWarehouseOnHandAggregatedView));
        inventWarehouseOnHandViewDictionary = new SysDictView(tableNum(InventWarehouseOnHandView));
        aggregatedViewName = inventWarehouseOnHandAggregatedViewDictionary.name(DbBackend::Sql);
        
        this.initInventDimensionsMetadataEntries();

        this.initialize();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static InventWarehouseOnHandAggregatedViewBuilder construct()
    {
        return new InventWarehouseOnHandAggregatedViewBuilder();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>