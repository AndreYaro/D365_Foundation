<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDimCtrl_Frm_WBS</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventDimCtrl_Frm_WBS</c> class is a derived version of the <c>InventDimCtrl_Frm</c>
///    class that extends the inherited class with functionality to determine which inventory dimension
///    should be marked as mandatory on the WBS lines with transaction type = item.
/// </summary>
public class InventDimCtrl_Frm_WBS extends InventDimCtrl_Frm
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>mustEnableField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Enables the active inventory dimension fields.
    /// </summary>
    /// <param name="_dimFieldId">
    ///    The field ID of the inventory dimension.
    /// </param>
    /// <returns>
    ///    A <c>NoYes</c> enumeration value that indicates whether the field is enabled.
    /// </returns>
    /// <remarks>
    ///    The invocation of the <c>getFieldSetup</c> method makes sure that the object is initialized.
    /// </remarks>
    protected NoYes mustEnableField(FieldId _dimFieldId)
    {
        if (!_dimFieldId)
        {
            return NoYes::No;
        }

        InventDimGroupFieldSetup inventDimGroupFieldSetup = this.getFieldSetup(_dimFieldId);

        if (!inventDimGroupFieldSetup.isActive())
        {
            return NoYes::No;
        }

        return inventDimGroupFieldSetup.isMandatory() || !inventDimGroupFieldSetup.isProductDimension();
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustMarkFieldAsMandatory</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Marks dimension fields as mandatory.
    /// </summary>
    /// <param name="_dimFieldId">
    ///    The field ID of the inventory dimension to mark.
    /// </param>
    /// <returns>
    ///    A <c>NoYes</c> enumeration value that indicates whether the field should be marked as mandatory.
    /// </returns>
    /// <remarks>
    ///    Dimension fields are marked as mandatory based on the result of the <c>mustEnableFields</c> method
    ///    when used on the WBS lines with transaction type = item.
    /// </remarks>
    protected NoYes mustMarkFieldAsMandatory(FieldId _dimFieldId)
    {
        if (!_dimFieldId)
        {
            return NoYes::No;
        }

        InventDimGroupFieldSetup inventDimGroupFieldSetup = this.getFieldSetup(_dimFieldId);

        return inventDimGroupFieldSetup.isMandatory();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static InventDimCtrl_Frm_WBS construct()
    {
        return new InventDimCtrl_Frm_WBS();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates and initializes an instance of this class to use in forms.
    /// </summary>
    /// <param name="_formRun">
    ///    The form where controls are maintained.
    /// </param>
    /// <returns>
    ///    An instance of this class to use in a form.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static InventDimCtrl_Frm_WBS newFromForm(FormRun _formRun)
    {
        InventDimCtrl_Frm_WBS       inventDimCtrl = InventDimCtrl_Frm_WBS::construct();
        InventDimAxFormAdapter adapter = InventDimAxFormAdapter::newFromForm(_formRun);

        inventDimCtrl.parmCallingElement(adapter);
        inventDimCtrl.init();
        return inventDimCtrl;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
