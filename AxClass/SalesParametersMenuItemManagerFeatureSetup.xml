<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesParametersMenuItemManagerFeatureSetup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesParametersMenuItemManagerFeatureSetup</c> class sets the Dynamics 365 Sales enabled parameters based on sales menu item manger enabled features in feature management
/// </summary>
[SysSetupTable(tableStr(SalesParameters))]
[SysSetupConfig(true, 300)]
internal final class SalesParametersMenuItemManagerFeatureSetup extends SysSetupAsync implements SysSetup
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>loadData</Name>
				<Source><![CDATA[
    public void loadData()
    {
        SalesParametersMenuItemManagerFeatureSetup::setDynamics365SalesParameterEnabledFields();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDynamics365SalesParameterEnabledFields</Name>
				<Source><![CDATA[
    private static void setDynamics365SalesParameterEnabledFields()
    {
        boolean isSalesTableMenuItemManagerFeature = SalesTableMenuItemManagerFeature::instance().isEnabled();
        boolean isSalesQuotationMenuItemManagerFeature = SalesQuotationMenuItemManagerFeature::instance().isEnabled();

        ttsbegin;

        SalesParameters salesParameters;
        while select forupdate crosscompany salesParameters
        {
            changecompany (salesParameters.DataAreaId)
            {
                salesParameters.IsSalesTableMenuItemManagerEnabled =  isSalesTableMenuItemManagerFeature;
                salesParameters.IsSalesQuotationMenuItemManagerEnabled = isSalesQuotationMenuItemManagerFeature;

                salesParameters.update();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSalesTableMenuItemManagerLifecycleFeature</Name>
				<Source><![CDATA[
    internal static void setSalesTableMenuItemManagerLifecycleFeature(boolean _enabled)
    {
        SalesParametersMenuItemManagerFeatureSetup::updateFeatureState(
            fieldStr(SalesParameters, IsSalesTableMenuItemManagerEnabled),
            _enabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSalesQuotationMenuItemManagerLifecycleFeature</Name>
				<Source><![CDATA[
    internal static void setSalesQuotationMenuItemManagerLifecycleFeature(boolean _enabled)
    {
        SalesParametersMenuItemManagerFeatureSetup::updateFeatureState(
            fieldStr(SalesParameters, IsSalesQuotationMenuItemManagerEnabled),
            _enabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateFeatureState</Name>
				<Source><![CDATA[
    private static void updateFeatureState(fieldName _fieldName, boolean _enabled)
    {
        SalesParameters salesParameters;

        ttsbegin;

        while select forupdate crosscompany salesParameters
        {
            changecompany (salesParameters.DataAreaId)
            {
                salesParameters.setFieldValue(_fieldName, _enabled);
                salesParameters.update();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>