<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BomRouteApprove</Name>
	<SourceCode>
		<Declaration><![CDATA[
abstract class BomRouteApprove extends RunBase
{
    BOMRouteApprover approver;
    NoYes            remove;
    NoYes            blockRemoveOfApproval;
    NoYes            isAlreadyApproved;

    DialogField      fieldApprover;
    DialogField      fieldName;
    DialogField      fieldRemove;

    #DEFINE.CurrentVersion(3)
    #LOCALMACRO.CurrentList
        approver,
        remove,
        blockRemoveOfApproval,
        isAlreadyApproved
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>blockRemoveOfApproval</Name>
				<Source><![CDATA[
    public NoYes blockRemoveOfApproval()
    {
        return NoYes::No;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    Object dialog()
    {
        DialogRunbase   dialog = super();
        Object          fieldApproverControl;
        Object          fieldNameControl;

        dialog.formBuildDesign().dialogSize(0);

        fieldApprover = dialog.addFieldValue(extendedtypestr(BOMRouteApprover), approver, "@SYS3492");
        fieldApproverControl = fieldApprover.control();

        fieldApproverControl.registerOverrideMethod(identifierstr(LookupReference), methodstr(BOMRouteApprove, workerLookup), this);
        fieldApproverControl.registerOverrideMethod(methodStr(FormReferenceGroupControl, modified), methodstr(BOMRouteApprove, lookupModified), this);
        fieldApproverControl.replacementFieldGroup(identifierstr(PersonnelNumber));

        fieldName = dialog.addField(extendedtypestr(DirPartyRecId), "@SYS80991");
        fieldName.enabled(false);

        HcmWorker hcmWorker = HcmWorker::find(approver);
        if (hcmWorker)
        {
            fieldName.value(hcmWorker.Person);
        }

        if (! this.blockRemoveOfApproval())
        {
            if (this.isAlreadyApproved())
            {
                fieldRemove = dialog.addFieldValue(extendedtypestr(NoYesId),remove,"@SYS72947");
            }
        }

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogPostRun</Name>
				<Source><![CDATA[
    public void dialogPostRun(DialogRunbase dialog)
    {
        super(dialog);
        fieldApprover.alignControl(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    boolean getFromDialog()
    {
        if (fieldRemove)
        {
            remove = fieldRemove.value();
        }
        else
        {
            remove = NoYes::No;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public boolean init()
    {
        boolean ret = super();

        blockRemoveOfApproval = RouteParameters::find().BlockRemoveApproval;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAlreadyApproved</Name>
				<Source><![CDATA[
    abstract boolean isAlreadyApproved()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Packs the <c>RunBase</c> class.
    /// </summary>
    /// <returns>
    ///    The packed class, as a container.
    /// </returns>
    /// <remarks>
    ///    All variables that you must have to re-instantiate the class after you construct it must be packed.
    ///    Typically, a macro declared in the class declaration is used to list the variables to pack. Another
    ///    macro holds a version number. This number refers to the version of variable list. When a new
    ///    variable is introduced you should typically do the following:
    ///    <list type="number">
    ///       <item>
    ///          <description>Increment the version number in the version macro.</description>
    ///       </item>
    ///       <item>
    ///          <description>
    ///              Copy the current list to the <c>unpack</c> method.
    ///          </description>
    ///       </item>
    ///       <item>
    ///          <description>Add the new variable to the bottom of the list.</description>
    ///       </item>
    ///       <item>
    ///          <description>Recompile the class.</description>
    ///       </item>
    ///    </list>
    ///     When the
    ///    <c>
    ///       isSwappingPrompt
    ///    </c>
    ///     method returns true you may have to pack some prompt-related variables also.
    /// </remarks>
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmApprover</Name>
				<Source><![CDATA[
    BOMRouteApprover parmApprover(BOMRouteApprover _approver = approver)
    {
        approver = _approver;

        return approver;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRemove</Name>
				<Source><![CDATA[
    //SIG
    public NoYes parmRemove(NoYes _remove = remove)
    {
        if (!prmisdefault(_remove))
        {
            remove = _remove;
        }

        return remove;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Unpacks what the <c>pack</c> method has packed.
    /// </summary>
    /// <param name="packedClass">
    ///    The packed class in a container.
    /// </param>
    /// <returns>
    ///    true if the container has been unpacked successfully; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    When the <c>isSwappingPrompt</c> method returns true, you may have to unpack some prompt-related
    ///    variables also.
    /// </remarks>
    public boolean unpack(container packedClass)
    {
        Version version = RunBase::getVersion(packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    boolean validate(Object _calledFrom = null)
    {
        boolean ret;

        if (remove)
        {
            ret = true;
        }
        else
        {
            if (this.parmApprover())
            {
                //only active workers within the current legal entity may approve a BOM or route
                ret = HcmWorkerLookup::newOnlyActiveWorkersWithinCompany().validateWorker(this.parmApprover());
                if (!ret)
                {
                    //"ALA" is not active or is not an employee
                    error(strfmt("@SYS39817", HcmWorker::find(this.parmApprover()).PersonnelNumber));
                }
            }
            else
            {
                ret = checkFailed("@SYS8980");
            }
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>workerLookup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a lookup for selecting a worker in the current legal entity.
    /// </summary>
    /// <param name="_callerControl">
    ///    The control that called the method.
    /// </param>
    /// <returns>
    ///    The selected record.
    /// </returns>
    public Common workerLookup(Object _callerControl)
    {
        return HcmWorkerLookup::newOnlyActiveEmployeesWithinCompany().lookupWorker(_callerControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupModified</Name>
				<Source><![CDATA[
    /// <summary>
    ///     If the worker field is modified, then lookup the name and add it the the name control.
    /// </summary>
    /// <param name = "_callerControl">
    ///     The control that called the method.
    ///</param>
    /// <returns>
    ///    Always true (validation happens as part of the reference group).
    /// </returns>
    public boolean lookupModified(FormReferenceGroupControl _callerControl)
    {
        if (_callerControl)
        {
            _callerControl.modified();

            HcmWorker hcmWorker = HcmWorker::find(_callerControl.value());
            FormReferenceGroupControl controlName = fieldName.control();
            controlName.value(hcmWorker.Person);
            controlName.update();

            this.parmApprover(_callerControl.value());
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>