<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustFreeInvoiceCancellation</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustFreeInvoiceCancellation</c> class lets the user create adjusting invoices without
/// creating a corrected invoices.
/// </summary>
class CustFreeInvoiceCancellation extends CustFreeInvoiceCorrection
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createCancellingInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a canceling free text invoice.
    /// </summary>
    /// <param name="_reasonCodeRecId">
    /// The record ID of the reason code.
    /// </param>
    /// <param name="_adjustingInvoiceDate">
    /// The date of the adjusting invoice.
    /// </param>
    /// <param name="_invoiceDate">
    /// The date of the invoice.
    /// </param>
    /// <returns>
    /// The record ID of the canceling free text invoice that is created.
    /// </returns>
    public CustInvoiceTableRefRecId createCancellingInvoice(RefRecId _reasonCodeRecId, TransDate _adjustingInvoiceDate, TransDate _invoiceDate)
    {
        CustInvoiceTable adjustingCustInvoiceTable, custInvoiceTableParent;
        CustInvoiceJour_W custInvoiceJourOriginalW;

        if (this.validate(_reasonCodeRecId, _adjustingInvoiceDate, _invoiceDate))
        {
            invoiceType = InvoiceAssociationType::AdjustingInvoice;
            childReasonCodeRecId = _reasonCodeRecId;
            parentAdjustingInvoiceDate = _adjustingInvoiceDate;

            childCustInvoiceTableRecId = this.createInvoice();
            ttsBegin;
            adjustingCustInvoiceTable = CustInvoiceTable::findRecId(childCustInvoiceTableRecId, true);
            adjustingCustInvoiceTable.InvoiceDate = _adjustingInvoiceDate;
            adjustingCustInvoiceTable.AdjustingInvoiceDate = _adjustingInvoiceDate;
            adjustingCustInvoiceTable.update();

            custInvoiceTableParent = CustInvoiceTable::findRecId(custParentInvoiceRecId, true);
            custInvoiceTableParent.AdjustingInvoiceDate = _adjustingInvoiceDate;
            custInvoiceTableParent.update();

            ttsCommit;
        }

        return childCustInvoiceTableRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCancellingInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates an instance of the <c>CustFreeInvoiceCancellation</c> class.
    /// </summary>
    /// <param name="_custParentInvoiceRecId">
    ///    The record ID of the parent invoice.
    /// </param>
    /// <returns>
    ///    An instance of the <c>CustFreeInvoiceCancellation</c> class.
    /// </returns>
    public static CustFreeInvoiceCancellation newCancellingInvoice(CustInvoiceTableRefRecId _custParentInvoiceRecId)
    {
        CustFreeInvoiceCancellation custFreeInvoice;

        custFreeInvoice = new CustFreeInvoiceCancellation();
        custFreeInvoice.init(_custParentInvoiceRecId);
        return custFreeInvoice;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>