<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRRetailOENInfo_InstallmentCharge</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Generate event notification for installment charges.
/// </summary>
class MCRRetailOENInfo_InstallmentCharge extends RetailOENInfo
{
    #MCRRetailOENInfo

    MCRInstallmentPayment mcrInstallmentPayment;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>EventNotificationType</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is used to return the event notification type.
    /// </summary>
    /// <returns>
    /// Event notification type.
    /// </returns>
    public RetailEventNotificationType EventNotificationType()
    {
        return RetailEventNotificationType::MCRInstallmentCharge;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameterMap</Name>
				<Source><![CDATA[
    protected void initParameterMap()
    {
        CompanyInfo companyInfo = CompanyInfo::findDataArea(curext());

        super();
        parameterMap.insert(#TodaysDate, RetailENInfo::formatDatetimeData(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone()), languageId));
        parameterMap.insert(#AccountNum, mcrInstallmentPayment.CustAccount);
        parameterMap.insert(#CompanyName, companyInfo.name());
        parameterMap.insert(#CompanyPhone, companyInfo.phone());
        parameterMap.insert(#DueDate, RetailENInfo::formatDatetimeData(mcrInstallmentPayment.getDueDate(), languageId));
        parameterMap.insert(#DueAmount, num2Str(mcrInstallmentPayment.getDueAmount(), 0, 2, 1, 3));
        parameterMap.insert(#PaymentInfo, mcrInstallmentPayment.getPaymentInfo());
        parameterMap.insert(#OrderDate, RetailENInfo::formatDatetimeData(salesOrder.lastInvoiceDate(), languageId));
        parameterMap.insert(#URL, companyInfo.url());
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSetting</Name>
				<Source><![CDATA[
    public void initSetting(RefRecId _refRecId)
    {
        MCRChannelUser                  mcrChannelUser;
        RetailChannelTable              retailChannelTable;

        select firstOnly notificationSetting
            where notificationSetting.EventNotificationType == this.EventNotificationType()
            join notificationProfile
            where notificationProfile.RecId == notificationSetting.EventNotificationProfile
            exists join retailChannelTable
            where retailChannelTable.EventNotificationProfileId == notificationProfile.ProfileId
            exists join mcrChannelUser
            where mcrChannelUser.Channel == retailChannelTable.RecId
                && mcrChannelUser.User == curUserId();

        mcrInstallmentPayment = MCRInstallmentPayment::findRecId(_refRecId);

        this.resolveSalesOrder(_refRecId);

        this.initParameterMap();
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolveSalesOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds sales order and sets value to <c>RetailOENInfo</c>.
    /// </summary>
    /// <param name="_refRecId">
    /// Installment payment record id.
    /// </param>
    public void resolveSalesOrder(RefRecId _refRecId)
    {
        this.parmSalesOrder(SalesTable::find(mcrInstallmentPayment.SalesID));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>