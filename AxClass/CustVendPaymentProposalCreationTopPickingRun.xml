<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendPaymentProposalCreationTopPickingRun</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CustVendGeneralTopPickingPolicyAttribute(false, 'transferToPayment', "@CashManagement:PaymProposalCreationTPBatchCaption")]
internal final class CustVendPaymentProposalCreationTopPickingRun extends RunBaseBatch implements ICustVendGeneralTopPickingRun
{
    private container parentPayload;
    private ClassId originalClassId;
    private container originalPayload;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [parentPayload, originalClassId, originalPayload];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        [parentPayload, originalClassId, originalPayload] = _packedClass;
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DictClass dictClass = new DictClass(originalClassId);
        RunBase rb = dictClass.makeObject();
        rb.unpack(originalPayload);

        return rb.dialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal void new(container _parentPayload, ClassId _originalClassId, container _originalPayload)
    {
        parentPayload = _parentPayload;
        originalClassId = _originalClassId;
        originalPayload = _originalPayload;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryTopPickingList</Name>
				<Source><![CDATA[
    [CustVendGeneralTopPickingBatchControlAttribute(1, "@CashManagement:PaymProposalTPCalculationBatchCaption")]
    public List queryTopPickingList(container _basePayload)
    {
        return CustVendCreatePaymJournal::generateTopPickingList(_basePayload, CustVendGeneralTopPickingScheduleHelper::getBundleSizeWrapped(classIdGet(this)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>methodToExecute</Name>
				<Source><![CDATA[
    [CustVendGeneralTopPickingBatchControlAttribute(4, "@CashManagement:PaymProposalTPProcessBatchCaption", 3),
        CustVendGeneralTopPickingSchedulingCategoryAttribute(CustVendGeneralTopPickingSchedulingCategory::CustVendPaymProposalCreation)]
    public str methodToExecute()
    {
        return methodStr(CustVendPaymentProposalCreationTopPickingRun, execute);
    }

]]></Source>
			</Method>
			<Method>
				<Name>execute</Name>
				<Source><![CDATA[
    internal boolean execute(container _workingPayload)
    {
        return CustVendCreatePaymJournal::triggerTopPickingRun(_workingPayload);
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferToPayment</Name>
				<Source><![CDATA[
    internal static void transferToPayment(container _payLoad)
    {
        ClassId classId;
        container payload;
        [classId, payload] = _payload;
        DictClass dictClass = new DictClass(classId);
        CustVendCreatePaymJournal custVendCreatePaymJournal = dictClass.makeObject();
        custVendCreatePaymJournal.unpack(payload);
        custVendCreatePaymJournal.postOperationsInTopPicking();
        custVendCreatePaymJournal.triggerTransfer();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>