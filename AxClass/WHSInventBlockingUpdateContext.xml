<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSInventBlockingUpdateContext</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSInventBlockingUpdateContext</c> class provides a context to access blocking quantity delta during inventory blocking record update.
/// </summary>
internal class WHSInventBlockingUpdateContext implements System.IDisposable
{
    private static WHSInventBlockingUpdateContext instance;
    private InventQty           deltaBlockingQty;
    private PdsCWInventQty      cwDeltaBlockingQty;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(InventQty _deltaBlockingQty, PdsCWInventQty _cwDeltaBlockingQty)
    {
        if (instance)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        instance = this;
        instance.deltaBlockingQty = _deltaBlockingQty;
        instance.cwDeltaBlockingQty = _cwDeltaBlockingQty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>Dispose</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void Dispose()
    {
        instance = null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromDeltaBlockingQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the class with delta blocking quantity.
    /// </summary>
    /// <param name="_deltaBlockingQty">
    /// Delta blocking quantity.
    /// </param>
    /// <param name="_cwDeltaBlockingQty">
    /// Catch weight delta blocking quantity.
    /// </param>
    /// <returns>A new instance of the class.</returns>
    [Hookable(false)]
    public static WHSInventBlockingUpdateContext newFromDeltaBlockingQty(InventQty _deltaBlockingQty, PdsCWInventQty _cwDeltaBlockingQty)
    {
        return new WHSInventBlockingUpdateContext(_deltaBlockingQty, _cwDeltaBlockingQty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>current</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the instance of the class.
    /// </summary>
    /// <returns>The instance of the class. If no instance is created null is returned.</returns>
    [Hookable(false)]
    public static WHSInventBlockingUpdateContext current()
    {
        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>currentDeltaBlockingQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns current delta blocking quantity.
    /// </summary>
    /// <returns>The current delta blocking quantity. If no instance is created, exception is thrown.</returns>
    [Hookable(false)]
    public static InventQty currentDeltaBlockingQty()
    {
        if (!instance)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return instance.deltaBlockingQty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>currentCWDeltaBlockingQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns current catch weight delta blocking quantity.
    /// </summary>
    /// <returns>The current catch weight delta blocking quantity. If no instance is created, exception is thrown.</returns>
    [Hookable(false)]
    public static PdsCWInventQty currentCWDeltaBlockingQty()
    {
        if (!instance)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return instance.cwDeltaBlockingQty;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>