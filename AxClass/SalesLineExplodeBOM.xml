<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesLineExplodeBOM</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SalesLineExplodeBOM
{
    SalesLine salesLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFormRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the <c>FormRun</c> object.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    /// <param name="_salesLine">
    /// The <c>SalesLine</c> record.
    /// </param>
    /// <returns>
    /// The <c>FormRun</c> object.
    /// </returns>
    public FormRun createFormRun(Args _args, SalesLine _salesLine)
    {
        Args formArgs = new Args(formstr(BOMExpandSales));
        formArgs.caller(_args.caller());
        formArgs.record(_salesLine);
        return classfactory.formRunClass(formArgs);
    }

]]></Source>
			</Method>
			<Method>
				<Name>explode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the parameters and opens the form.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public void explode(Args _args)
    {
        if (!this.isValidArgs(_args))
        {
            throw error("@SYS25407");
        }

        salesLine = _args.record() as SalesLine;
        if (!this.isBOMExplosionAllowed(_args))
        {
            throw error("@SYS2069");
        }

        FormRun bomLExpand = this.createFormRun(_args, salesLine);
        bomLExpand.run();
        bomLExpand.wait();

        this.updateDataSource(FormDataUtil::getFormDataSource(_args.record()), bomLExpand.closedOk());
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if passed <c>Args</c> is valid.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    /// <returns>
    /// True if passed <c>Args</c> is valid; otherwise, false.
    /// </returns>
    protected boolean isValidArgs(Args _args)
    {
        return _args && _args.record() && _args.dataset() == tablenum(SalesLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isBOMExplosionAllowed</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if BOM explosion is allowed.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    /// <returns>
    /// True if BOM explosion is allowed; otherwise, false.
    /// </returns>
    //This is an extension point to control the BOM explosion check.
    protected boolean isBOMExplosionAllowed(Args _args)
    {
        return salesLine.inventTable().inventItemType().canHaveBOM();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the form data source if it is needed.
    /// </summary>
    /// <param name="formDataSource">
    /// The <c>FormDataSource</c> object to refresh.
    /// </param>
    /// <param name="refresh">
    /// The data source that should be refreshed.
    /// </param>
    public void updateDataSource(FormDataSource formDataSource, boolean refresh)
    {
        SalesLine localSalesLine;
        if (refresh && FormDataUtil::isFormDataSource(salesLine))
        {
            localSalesLine.data(salesLine);
            formDataSource.executeQuery();
            formDataSource.positionToRecord(localsalesLine);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SalesLineExplodeBOM construct()
    {
        return new SalesLineExplodeBOM();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args _args)
    {
        SalesLineExplodeBOM salesLineExplodeBOM = SalesLineExplodeBOM::construct();
        salesLineExplodeBOM.explode(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
