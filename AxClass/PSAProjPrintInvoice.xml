<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAProjPrintInvoice</Name>
	<SourceCode>
		<Declaration><![CDATA[
class PSAProjPrintInvoice extends RunBaseBatch implements BatchRetryable
{
    QueryRun    queryRun;

    boolean             includeInvoice;
    boolean             includeOnAcc;
    PrintCopyOriginal   printType;

    TransDate   startDate;
    TransDate   endDate;

    container   printerSettings;

    #define.CurrentVersion(3)

    #localmacro.CurrentList
        includeInvoice,
        includeOnAcc,
        printType,
        startDate,
        endDate,
        printerSettings
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase   dialog = Dialog::newFormnameRunbase(formStr(PSAProjPrintInvoiceDialog),this);

        dialog = this.dialogInternal(dialog);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    public void initParmDefault()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        super();

        queryRun = this.queryBuild();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList,queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEndDate</Name>
				<Source><![CDATA[
    public TransDate parmEndDate(TransDate _endDate = endDate)
    {
        endDate = _endDate;

        return endDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPrintType</Name>
				<Source><![CDATA[
    public PrintCopyOriginal parmPrintType(PrintCopyOriginal _printType = printType)
    {
        printType = _printType;

        return printType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmStartDate</Name>
				<Source><![CDATA[
    public TransDate parmStartDate(TransDate _startDate = startDate)
    {
        startDate = _startDate;

        return startDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>printerSettingsPrintInvoice</Name>
				<Source><![CDATA[
    container printerSettingsPrintInvoice()
    {
        return printerSettings;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
    protected QueryRun queryBuild()
    {
        queryRun = new QueryRun(queryStr(projPrintInvoice));

        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        #OCCRetryCount
        ProjInvoiceJour                               projInvoiceJour;
        Args                                          parameters = new Args();

        boolean             recordFound = false;

        if (!startDate)
        {
            startDate = dateNull();
        }

        if (!endDate)
        {
            endDate = dateMax();
        }

        try
        {
            parameters.caller(this);
            parameters.parmEnumType(enumNum(PrintCopyOriginal));

            queryRun.reset();
            while (queryRun.next())
            {
                projInvoiceJour = queryRun.get(tableNum(ProjInvoiceJour));

                if (projInvoiceJour.InvoiceDate >= startDate && projInvoiceJour.InvoiceDate <= endDate)
                {
                    parameters.record(projInvoiceJour);
                    // Store if a record is found, Common buffer will be empty at end of loop
                    if (!recordFound && parameters.record())
                    {
                        recordFound = true;
                    }
                    parameters.parmEnum(this.parmPrintType());
                    parameters.menuItemName(menuitemOutputStr(PSAProjPrintInvoice));
                    parameters.menuItemType(MenuItemType::Output);
                    PSAProjAndContractInvoiceController::main(parameters);
                }
            }
            if (!recordFound)
            {
                // No invoices fulfilled the selected (by the user) criteria.
                info("@SYS91621");
            }
        }
        catch (Exception::Deadlock)
        {
            retry;
        }

        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer         version         = RunBase::getVersion(packedClass);
        container       packedQuery;
        boolean         ret = true;

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList,packedQuery] = packedClass;

                if (SysQuery::isPackedOk(packedQuery))
                {
                    queryRun = new QueryRun(packedQuery);
                }
                else
                {
                    queryRun = this.queryBuild();
                }

                break;
            default:
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updatePrinterSettingsPrintInvoice</Name>
				<Source><![CDATA[
    void updatePrinterSettingsPrintInvoice(container   _printerSettings)
    {
        printerSettings = _printerSettings;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret;

        ret = super(_calledFrom);

        if (startDate > endDate && endDate)
        {
            ret = checkFailed("@SYS91020");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>callPrinterSettingsPrintInvoice</Name>
				<Source><![CDATA[
    static boolean callPrinterSettingsPrintInvoice(PSAProjPrintInvoice psaProjPrintInvoice)
    {
        SRSPrintDestinationSettings printSettings = new SRSPrintDestinationSettings(psaProjPrintInvoice.printerSettingsPrintInvoice());
        boolean ok = SrsReportRunUtil::showSettingsDialog(printSettings);

        psaProjPrintInvoice.updatePrinterSettingsPrintInvoice(printSettings.pack());
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static PSAProjPrintInvoice construct()
    {
        return new PSAProjPrintInvoice();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args args)
    {
        PSAProjPrintInvoice psaProjPrintInvoice = new PSAProjPrintInvoice();
        boolean validated = false;
        if (psaProjPrintInvoice.prompt())
        {
            psaProjPrintInvoice.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable() 
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>