<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RPayCreatePayment_BankSheetTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class RPayCreatePayment_BankSheetTable extends RPayCreatePayment_Bank
{
    RPaySheetTable      rPaySheetTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcTotalAmount</Name>
				<Source><![CDATA[
        protected Amount calcTotalAmount()
    {
        RPaySheetTable rPaySheetTableLocal, rPaySheetTableCheck;
        Amount         ret;

        for (rPaySheetTableLocal = table_ds.getFirst(true) ?
                                   table_ds.getFirst(true) :
                                   table_ds.cursor();
             rPaySheetTableLocal;
             rPaySheetTableLocal = table_ds.getNext())
        {
            rPaySheetTableCheck = RPaySheetTable::find(rPaySheetTableLocal.RecId);

            if (rPaySheetTableCheck.PaySheetSost != RPaySheetSost::Paid)
            {
                ret += rPaySheetTableLocal.payBankSum() - rPaySheetTableLocal.payBankSumInJournal();
                counter++;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postUpdate</Name>
				<Source><![CDATA[
        protected void postUpdate()
    {
        RPaySheetTrans  rPaySheetTransUpdate;
        RPaySheetTable  rPaySheetTableLocal, rPaySheetTableUpdate;
        RPayAmount      amount;

        ttsbegin;

        for (rPaySheetTableLocal = table_ds.getFirst(true) ? table_ds.getFirst(true) : table_ds.cursor();
             rPaySheetTableLocal;
             rPaySheetTableLocal = table_ds.getNext())
        {
            amount = 0;

            rPaySheetTableUpdate = RPaySheetTable::find(rPaySheetTableLocal.RecId, true);

            while select forupdate rPaySheetTransUpdate
                where rPaySheetTransUpdate.SheetTableRecId   == rPaySheetTableLocal.RecId &&
                      rPaySheetTransUpdate.SendToBank        == NoYes::Yes                &&
                      ! rPaySheetTransUpdate.JournalNum
            {
                rPaySheetTransUpdate.JournalNum     = ledgerJournalTable.JournalNum;
                rPaySheetTransUpdate.PayVoucherBank = payVoucherBank;
                rPaySheetTransUpdate.PayDateBank    = payDateBank;
                rPaySheetTransUpdate.update();

                amount += rPaySheetTransUpdate.Amount;
            }
            rPaySheetTableUpdate.updateDisbursedSum(amount);
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDataSource</Name>
				<Source><![CDATA[
        public void setDataSource(Common _table, FormDataSource _table_ds)
    {
        super(_table, _table_ds);
        rPaySheetTable    = _table;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public static ClassDescription description()
    {
        return "@RUP649";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>