<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendCreateInvoiceFromVendor</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendCreateInvoiceFromVendor</c> class creates a new vendor invoice for a given vendor.
/// </summary>
class VendCreateInvoiceFromVendor
{
    VendInvoiceInfoTable vendInvoiceInfoTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFromVendor</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches the <c>VendCreateInvoice</c> form in order to create a new vendor invoice.
    /// </summary>
    /// <param name="_accountNumber">
    /// The vendor account number for the new vendor invoice.
    /// </param>
    /// <returns>
    /// true when OK button in the <c>PurchCreateOrder</c> form is clicked.
    /// </returns>
    public boolean createFromVendor(VendAccount  _accountNumber)
    {
        Args args = new Args();
        args.name(formStr(VendCreateInvoice));
        args.parm(_accountNumber);
        args.caller(this);

        FormRun vendCreateInvoice = classfactory.formRunClass(args);
        vendCreateInvoice.init();
        vendCreateInvoice.run();
        vendCreateInvoice.wait();

        return vendCreateInvoice.closedOk();
    }

]]></Source>
			</Method>
			<Method>
				<Name>editInvoice</Name>
				<Source><![CDATA[
    private void editInvoice(Args _origArgs)
    {
        Args args = new Args();
        args.caller(_origArgs.caller());
        args.parm(_origArgs.parm());
        args.record(this.parmVendInvoiceInfoTable());
        args.openMode(OpenMode::Edit);

        MenuFunction menuFunction = new MenuFunction(menuitemDisplayStr(VendInvoice), MenuItemType::Display);
        menuFunction.run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendInvoiceInfoTable</Name>
				<Source><![CDATA[
    public VendInvoiceInfoTable parmVendInvoiceInfoTable(VendInvoiceInfoTable _vendInvoiceInfoTable = vendInvoiceInfoTable)
    {
        vendInvoiceInfoTable = _vendInvoiceInfoTable;
        return vendInvoiceInfoTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the <c>VendCreateInvoiceFromVendor</c> class.
    /// </summary>
    /// <returns>A new instance of the <c>VendCreateInvoiceFromVendor</c> class.</returns>
    public static VendCreateInvoiceFromVendor construct()
    {
        return new VendCreateInvoiceFromVendor();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// The default entry point of the <c>VendCreateInvoiceFromVendor</c> class.
    /// </summary>
    /// <param name = "_args">The arguments containing the vendor for the new invoice.</param>
    public static void main(Args _args)
    {
        if (!_args || !_args.parm())
        {
            throw error("@SYS132237");
        }

        VendCreateInvoiceFromVendor vendCreateInvoice = VendCreateInvoiceFromVendor::construct();

        boolean isInvoiceCreated = vendCreateInvoice.createFromVendor(_args.parm());

        if (!isInvoiceCreated)
        {
            return;
        }

        vendCreateInvoice.editInvoice(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>