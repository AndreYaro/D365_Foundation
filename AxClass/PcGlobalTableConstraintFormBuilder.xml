<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PcGlobalTableConstraintFormBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is responsible for building the form controls need to edit a user defined table constraint.
/// </summary>
class PcGlobalTableConstraintFormBuilder
{
    Form                    form;
    FormRun                 formRun;
    FormBuildGridControl    gridControl;

    PCGlobalTableConstraintDefinition tableConstraintDefinition;

    PCFormControlExtractor                      formControlExtractor;
    PCGlobalTableConstraintControlBuilder       controlBuilder;
    PCGlobalTableConstraintDataSourceBuilder    dataSourceBuilder;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the form.
    /// </summary>
    /// <param name="_tableConstraintDefinition">
    /// A table constraint definition.
    /// </param>
    /// <param name="_formRun">
    /// An instance of the <c>FormRun</c> class.
    /// </param>
    public void build(PCGlobalTableConstraintDefinition _tableConstraintDefinition, FormRun _formRun)
    {
        PCContractNotNull::assertObject(_formRun, funcName());
        PCContractNotNull::assertObject(_formRun.form(), funcName());
        PCContractNotNull::assertRecord(_tableConstraintDefinition, funcName());

        formRun                     = _formRun;
        form                        = _formRun.form();
        tableConstraintDefinition   = _tableConstraintDefinition;

        this.buildGrid();
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildColumn</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds a column of the table constraint.
    /// </summary>
    /// <param name="_tableConstraintColumnDefinition">
    /// A table constraint column definition.
    /// </param>
    /// <param name="_attributeDataType">
    /// An attribute data type.
    /// </param>
    protected void buildColumn(PCTableConstraintGlobalColumnDef _tableConstraintColumnDefinition, AttributeDataType _attributeDataType)
    {
        FormBuildDataSource formDataSource;

        formDataSource = dataSourceBuilder.addDataSource(_tableConstraintColumnDefinition.Name);

        controlBuilder.addControl(formDataSource.id(), _tableConstraintColumnDefinition.Name, _attributeDataType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGrid</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the grid control.
    /// </summary>
    protected void buildGrid()
    {
        PCTableConstraintGlobalColumnDef    columnDefinition;
        FormBuildActionPaneTabControl       actionPaneTab;
        str                                 firstDataSourceName;

        gridControl     = formControlExtractor.extractFromForm(formControlStr(PCGlobalTableConstraintEdit, Grid), form);
        actionPaneTab   = formControlExtractor.extractFromForm(formControlStr(PCGlobalTableConstraintEdit, ActionPaneTab), form);

        controlBuilder.initialize(gridControl, formRun);

        dataSourceBuilder.initialize(form);

        while select columnDefinition
            where columnDefinition.TableConstraintDefinition == tableConstraintDefinition.RecId
        {
            this.buildColumn(columnDefinition, columnDefinition.getDataType());
        }

        firstDataSourceName = dataSourceBuilder.getFirstDataSourceName();

        gridControl.dataSource(firstDataSourceName);
        actionPaneTab.dataSource(firstDataSourceName);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        formControlExtractor    = PCFormControlExtractor::construct();
        controlBuilder          = PCGlobalTableConstraintControlBuilder::construct();
        dataSourceBuilder       = PCGlobalTableConstraintDataSourceBuilder::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmControlBuilder</Name>
				<Source><![CDATA[
        public PCGlobalTableConstraintControlBuilder parmControlBuilder(PCGlobalTableConstraintControlBuilder _controlBuilder = controlBuilder)
    {
        controlBuilder = _controlBuilder;

        return controlBuilder;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDataSourceBuilder</Name>
				<Source><![CDATA[
        public PCGlobalTableConstraintDataSourceBuilder parmDataSourceBuilder(PCGlobalTableConstraintDataSourceBuilder _dataSourceBuilder = dataSourceBuilder)
    {
        dataSourceBuilder = _dataSourceBuilder;

        return dataSourceBuilder;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFormControlExtractor</Name>
				<Source><![CDATA[
        public PCFormControlExtractor parmFormControlExtractor(PCFormControlExtractor _formControlExtractor = formControlExtractor)
    {
        formControlExtractor = _formControlExtractor;

        return formControlExtractor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static PCGlobalTableConstraintFormBuilder construct()
    {
        return new PCGlobalTableConstraintFormBuilder();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>