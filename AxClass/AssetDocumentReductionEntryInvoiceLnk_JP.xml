<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentReductionEntryInvoiceLnk_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>AssetDocumentReductionEntryInvoiceLnk_JP</c> is an asset reduction entry invoice line link.
/// </summary>
class AssetDocumentReductionEntryInvoiceLnk_JP extends AssetDocumentInvoiceLink_JP
{
    AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
        public void create()
    {
        if (!linkReductionEntryTable.RecId)
        {
            ttsBegin;

            super();
            linkReductionEntryTable.AssetDocumentLinkTable_JP = this.parmLinkTable().RecId;
            linkReductionEntryTable.insert();

            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>delete</Name>
				<Source><![CDATA[
        public void delete()
    {
        AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTableLoc;

        if (this.parmRecId())
        {
            linkReductionEntryTableLoc = AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId(), true);

            ttsBegin;

            if (linkReductionEntryTableLoc)
            {
                linkReductionEntryTableLoc.delete();
            }
            super();

            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByReference</Name>
				<Source><![CDATA[
        public void findByReference(
        RefTableId            _refTableId,
        RefRecId              _refRecId,
        AssetDocumentRecId_JP _docRecId)
    {
        super(_refTableId, _refRecId, _docRecId);

        if (this.parmRecId())
        {
            this.parmExtReductionEntryTable(AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInvoiceLnk</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize asset document link reduction entry table from invoice.
    /// </summary>
    /// <param name="_invoiceLnk">
    /// The instance of <c>AssetDocumentReductionEntryInvoiceLnk_JP</c>
    /// </param>
    public void initFromInvoiceLnk(AssetDocumentReductionEntryInvoiceLnk_JP _invoiceLnk)
    {
        if (_invoiceLnk != null
            && _invoiceLnk.parmAssetDocumentEntryRecId()
            && _invoiceLnk.parmReductionEntryAmount())
        {
            this.parmAssetDocumentEntryRecId(_invoiceLnk.parmAssetDocumentEntryRecId());
            this.parmReductionEntryAmount(_invoiceLnk.parmReductionEntryAmount());
            this.parmReductionEntryCurrencyCode(_invoiceLnk.parmReductionEntryCurrencyCode());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromPurchLnk</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize asset document link reduction entry table from purchase order.
    /// </summary>
    /// <param name="_purchLnk">
    /// The instance of <c>AssetDocumentReductionEntryPurchLnk_JP</c>
    /// </param>
    public void initFromPurchLnk(AssetDocumentReductionEntryPurchLnk_JP _purchLnk)
    {
        if (_purchLnk != null
            && _purchLnk.parmAssetDocumentEntryRecId()
            && _purchLnk.parmReductionEntryAmount())
        {
            this.parmAssetDocumentEntryRecId(_purchLnk.parmAssetDocumentEntryRecId());
            this.parmReductionEntryAmount(_purchLnk.parmReductionEntryAmount());
            this.parmReductionEntryCurrencyCode(_purchLnk.parmReductionEntryCurrencyCode());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmExtReductionEntryTable</Name>
				<Source><![CDATA[
        public AssetDocumentLinkReductionEntryTable_JP parmExtReductionEntryTable(AssetDocumentLinkReductionEntryTable_JP _linkReductionEntryTable = linkReductionEntryTable)
    {
        linkReductionEntryTable = _linkReductionEntryTable;
        return linkReductionEntryTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryAmount</Name>
				<Source><![CDATA[
        public AssetReductionEntryAmount_JP parmReductionEntryAmount(AssetReductionEntryAmount_JP _reductionEntryAmount = linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount)
    {
        linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount = _reductionEntryAmount;
        return linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryCurrencyCode</Name>
				<Source><![CDATA[
        public AssetReductionEntryCurrencyCode_JP parmReductionEntryCurrencyCode(AssetReductionEntryCurrencyCode_JP  _reductionEntryCurrency = linkReductionEntryTable.ReductionEntryCurrencyCode)
    {
        linkReductionEntryTable.ReductionEntryCurrencyCode = _reductionEntryCurrency;
        return linkReductionEntryTable.ReductionEntryCurrencyCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
        public void update()
    {
        AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTableLoc;
        super();

        if (this.parmRecId())
        {
            linkReductionEntryTableLoc = AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId(),true);
            linkReductionEntryTableLoc.ReductionEntryTransactionCurrencyAmount = linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount;
            linkReductionEntryTableLoc.ReductionEntryCurrencyCode = linkReductionEntryTable.ReductionEntryCurrencyCode;
            ttsBegin;
            linkReductionEntryTableLoc.update();
            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyFromPurchExtToVendDocumentLineLnk</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copy reduction entry document link from original purch line to new invoice line.
    /// </summary>
    /// <param name="_purchLine">
    /// The original <c>PurchLine</c>
    /// </param>
    /// <param name="_purchParmLine_Asset">
    ///  The new <c>VendDocumentLineAssetMap</c> record.
    /// </param>
    /// <returns>
    ///  The <c>AssetDocumentReductionEntryInvoiceLnk_JP</c> record.
    /// </returns>
    public static AssetDocumentReductionEntryInvoiceLnk_JP copyFromPurchExtToVendDocumentLineLnk(
        PurchLine                 _purchLine,
        VendDocumentLineAssetMap  _purchParmLine_Asset)
    {
        AssetDocumentReductionEntryPurchLnk_JP    purchLnk =
            AssetDocumentLink_JP::construct(AssetDocumentLinkType_JP::Purch, AssetDocumentType_JP::AssetReductionEntry_DirectOff);
        AssetDocumentReductionEntryInvoiceLnk_JP  parmLnk  =
            AssetDocumentLink_JP::construct(AssetDocumentLinkType_JP::Invoice, AssetDocumentType_JP::AssetReductionEntry_DirectOff);
        AssetDocument_JP                          document;

        if (_purchLine.AssetId && _purchLine.AssetBookId)
        {
            document = AssetReductionEntryManager_JP::getAssetReductionEntryDocByAssetBook(
                _purchLine.AssetId,
                _purchLine.AssetBookId);
            if (document != null)
            {
                purchLnk.findByReference(_purchLine.TableId, _purchLine.RecId, document.parmAssetDocumentEntry().RecId);
                parmLnk.parmVendInvoiceInfoLineAsset(_purchParmLine_Asset);
                parmLnk.initFromPurchLnk(purchLnk);
                parmLnk.create();
            }
        }
        return parmLnk;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyFromVendDocLineToVendDocLineLnk</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copy reduction entry document link from original invoice line to new invoice line.
    /// </summary>
    /// <param name="_purchParmLine_Asset_orig">
    /// The original <c>VendDocumentLineAssetMap</c> record.
    /// </param>
    /// <param name="_purchParmLine_Asset">
    ///  The new <c>VendDocumentLineAssetMap</c> record.
    /// </param>
    /// <returns>
    ///  The <c>AssetDocumentReductionEntryInvoiceLnk_JP</c> record.
    /// </returns>
    public static AssetDocumentReductionEntryInvoiceLnk_JP copyFromVendDocLineToVendDocLineLnk(
        VendDocumentLineAssetMap  _purchParmLine_Asset_orig,
        VendDocumentLineAssetMap  _purchParmLine_Asset)
    {
        AssetDocumentReductionEntryInvoiceLnk_JP    origLnk =
            AssetDocumentLink_JP::construct(AssetDocumentLinkType_JP::Invoice, AssetDocumentType_JP::AssetReductionEntry_DirectOff);
        AssetDocumentReductionEntryInvoiceLnk_JP  parmLnk  =
            AssetDocumentLink_JP::construct(AssetDocumentLinkType_JP::Invoice, AssetDocumentType_JP::AssetReductionEntry_DirectOff);
        AssetDocument_JP                          document;

        if (_purchParmLine_Asset_orig.AssetId && _purchParmLine_Asset_orig.AssetBookId)
        {
            document = AssetReductionEntryManager_JP::getAssetReductionEntryDocByAssetBook(
                _purchParmLine_Asset_orig.AssetId,
                _purchParmLine_Asset_orig.AssetBookId);
            if (document != null)
            {
                origLnk.findByReference(_purchParmLine_Asset_orig.TableId, _purchParmLine_Asset_orig.RecId, document.parmAssetDocumentEntry().RecId);
                parmLnk.parmVendInvoiceInfoLineAsset(_purchParmLine_Asset);
                parmLnk.initFromInvoiceLnk(origLnk);
                parmLnk.create();
            }
        }
        return parmLnk;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>