<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxYearlyComCreate_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    This class is used for generation of the <c>TaxYearlyCom_IT</c> and <c>TaxYearlyComReport_IT</c>
///    records based on the content of the <c>TaxBook</c> table.
/// </summary>
public class TaxYearlyComCreate_IT extends RunBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Generates the <c>TaxYearlyCom_IT</c> and <c>TaxYearlyComReport_IT</c> records based on the content
    ///    of the <c>TaxBook</c> table.
    /// </summary>
    public void run()
    {
        TaxBook taxBook;
        TaxYearlyCom_IT taxYearlyCom;
        TaxYearlyCom_IT taxYearlyComLocal;
        TaxYearlyComReport_IT taxYearlyComReport;

        ttsbegin;

        Yr prevYear = year(prevyr(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone())));
        boolean recordsInserted = false;

        while select atecofinCode_IT from taxBook
            group by atecofinCode_IT
            where taxBook.atecofinCode_IT != ''
        {
            taxYearlyCom = TaxYearlyCom_IT::findByATECOFINCode(taxBook.atecofinCode_IT, prevYear);

            if (!taxYearlyCom)
            {
                select firstonly taxYearlyComLocal
                    where taxYearlyComLocal.Year == prevYear;

                taxYearlyCom.TaxYearlyComId = taxYearlyComLocal ?
                                              taxYearlyComLocal.TaxYearlyComId :
                                              NumberSeq::newGetNum(TaxParameters::numRefTaxYearlyComId_IT()).num();

                taxYearlyCom.Year = prevYear;
                taxYearlyCom.atecofinCode = taxBook.atecofinCode_IT;
                taxYearlyCom.insert();

                taxYearlyComReport.create(taxYearlyCom);

                recordsInserted = true;
            }
        }

        ttscommit;

        if (!recordsInserted)
        {
            info("@SYS329025");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (FeatureStateProvider::isFeatureEnabled(TaxYearlyCommunicationEnhancementFeature::instance()))
        {
            TaxYearlyComTaxPeriodCreate_IT::main(_args);
            return;
        }

        new TaxYearlyComCreate_IT().runOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>