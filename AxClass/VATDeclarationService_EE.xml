<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VATDeclarationService_EE</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The service of ER VAT declaration file generation.
/// </summary>
public class VATDeclarationService_EE extends VATDeclarationService_W
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateQueryForTaxTrans</Name>
				<Source><![CDATA[
    protected Query generateQueryForTaxTrans()
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(TaxTrans));
        QueryBuildDataSource qbdsTaxTable = qbds.addDataSource(tableNum(TaxTable));
        QueryBuildDataSource qbdsMap = qbdsTaxTable.addDataSource(tableNum(TaxReportExportTaxCodesMappings_EE));

        qbdsTaxTable.joinMode(JoinMode::InnerJoin);
        qbdsTaxTable.addLink(fieldNum(TaxTrans, TaxCode), fieldNum(TaxTable, TaxCode));
        qbdsTaxTable.addRange(fieldNum(TaxTable, TaxType_W)).value(queryValue(TaxType_W::VAT));
        qbdsTaxTable.addRange(fieldNum(TaxTable, TaxType_W)).value(queryValue(TaxType_W::VATReduced));

        qbdsMap.joinMode(JoinMode::ExistsJoin);
        qbdsMap.addLink(fieldNum(TaxTable, TaxCode), fieldNum(TaxReportExportTaxCodesMappings_EE, TaxCode));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateQueryForCustVendTrans</Name>
				<Source><![CDATA[
    protected Query generateQueryForCustVendTrans(TableId tableId, TransDate _fromDate)
    {
        QueryBuildDataSource qbds;

        Query query = new Query();
        if (tableId == tableNum(CustTrans))
        {
            qbds = query.addDataSource(tableNum(CustTrans));
            qbds.addRange(fieldNum(CustTrans, AmountMST))
                .value(strFmt('(%1 != %2)', fieldStr(CustTrans, AmountMST), fieldStr(CustTrans, SettleAmountMST)));
            qbds.addRange(fieldNum(CustTrans, TransDate))
                .value(strFmt('(%1 < %2)',fieldStr(CustTrans, TransDate), date2StrXpp(_fromDate)));
        }
        else if (tableId == tableNum(VendTrans))
        {
            qbds = query.addDataSource(tableNum(VendTrans));
            qbds.addRange(fieldNum(VendTrans, AmountMST))
                .value(strFmt('(%1 != %2)', fieldStr(VendTrans, AmountMST), fieldStr(VendTrans, SettleAmountMST)));
            qbds.addRange(fieldNum(VendTrans, TransDate))
                .value(strFmt('(%1 < %2)',fieldStr(VendTrans, TransDate), date2StrXpp(_fromDate)));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    protected ERModelDefinitionParamsUIActionComposite initParameters(VATDeclaraitonContract_W _contract)
    {
        ERModelDefinitionParamsUIActionComposite parameters = super(_contract);

        parameters.add(new ERModelDefinitionDatabaseContext().addQuery(tableNum(TaxTrans),this.generateQueryForTaxTrans()))
            .add(new ERModelDefinitionDatabaseContext().addQuery(tableNum(CustTrans),this.generateQueryForCustVendTrans(tableNum(CustTrans), _contract.parmFromDate())))
            .add(new ERModelDefinitionDatabaseContext().addQuery(tableNum(VendTrans),this.generateQueryForCustVendTrans(tableNum(VendTrans), _contract.parmFromDate())));

        return parameters;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>