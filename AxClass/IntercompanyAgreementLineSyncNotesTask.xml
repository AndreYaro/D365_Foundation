<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntercompanyAgreementLineSyncNotesTask</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>IntercompanyAgreementLineSyncNotesTask</c> class handles the transfer of documents when it
///    is called from the <c>AgreementLine</c> table.
/// </summary>
class IntercompanyAgreementLineSyncNotesTask extends IntercompanySyncNotesTask
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>synchronizeCommon</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Synchronizes notes for intercompany agreement lines.
    /// </summary>
    protected void synchronizeCommon()
    {
        using (SysInstrumentationActivityContext activityContext = this.instrumentationLogger().interCompanyActivities().synchronizeCommon(this.parmDocRefRecId(), this.parmRefCompanyId(), this.parmRefTableId(), this.parmRefRecId()))
        {
            ttsbegin;
            changecompany(refCompanyId)
            {
                setprefix(strfmt("@SYS93821",TradeInterCompany::curCompanyName()));

                AgreementLine fromAgreementLine  = AgreementLine::find(refRecId);

                if (fromAgreementLine.intercompanyLinkExist())
                {
                    // Delete all notes on associated agreementLine
                    AgreementLine toAgreementLine = fromAgreementLine.intercompanyAgreementLine();

                    if (toAgreementLine.RecId
                        && toAgreementLine.RecId != fromRecId)
                    {
                        // No need to delete all notes in order to improve performance if new notes are created
                        if (!this.isCreatedDocu())
                        {
                            this.deleteDocuRef(toAgreementLine,fromAgreementLine.RecId);
                        }
                    }

                    // Retrieve the base table reference for documents
                    TableId baseTableId = DocuRef::GetRootTableID(fromAgreementLine.TableId);

                    DocuRef         fromDocuRef;

                    // Create all notes on associated Agreement
                    while select fromDocuRef
                          where fromDocuRef.RefCompanyId == fromAgreementLine.DataAreaId
                             && fromDocuRef.RefTableId   == baseTableId
                             && fromDocuRef.RefRecId     == fromAgreementLine.RecId
                             // Only process new created notes if new notes are created
                             && (fromDocuRef.RecId       == this.parmDocRefRecId() || !this.isCreatedDocu())
                    {
                        if (this.validateDocuRefTypeGroup(fromDocuRef))
                        {
                            if (toAgreementLine.RecId
                                && toAgreementLine.RecId != fromRecId)
                            {
                                this.writeDocuRef(toAgreementLine,fromDocuRef,fromAgreementLine.RecId);
                            }
                        }
                    }
                }
            }
            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDocuRefTypeGroup</Name>
				<Source><![CDATA[
    protected boolean validateDocuRefTypeGroup(DocuRef _docuRef)
    {
        if (IntercompanySyncNotesTaskWriteDocuRefCheckFlight::instance().isEnabled())
        {
            return this.canWriteDocuRef(_docuRef);
        }
        else
        {
            return _docuRef.docuType().TypeGroup == DocuTypeGroup::Note;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static IntercompanyAgreementLineSyncNotesTask construct()
    {
        return new IntercompanyAgreementLineSyncNotesTask();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>