<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayStatAddAdjustment</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgPayStatAddAdjustment</c> class sets the Adjustment field on the specified <see
///    cref="T:JmgPayStatTrans" /> record.
/// </summary>
/// <remarks>
///    When run, this class sets the Adjustment field on the <see cref="T:JmgPayStatTrans" /> record.
/// </remarks>
class JmgPayStatAddAdjustment extends RunBase
{
    JmgPayStatTrans jmgPayStatTrans;
    FormRun caller;

    DialogField dfValue;

    JmgTimeString value;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods described by the <see cref="T:RunBaseDialogable" />
    ///    interface.
    /// </summary>
    /// <param name="dialog">
    ///    An already initialized dialog can be passed.
    /// </param>
    /// <param name="forceOnClient">
    ///    Specifies if the dialog must be instantiated on the client.
    /// </param>
    /// <returns>
    ///    A <c>runbaseDialogable</c> object.
    /// </returns>
    /// <remarks>
    ///    The <c>JmgPayStatAddAdjustment</c> class adds the <c>Adjustment</c> dialog field.
    /// </remarks>
    public Object dialog(DialogRunbase dialog = null, boolean forceOnClient = false)
    {
        Dialog ret;

        ret = this.dialogInternal(dialog, forceOnClient);

        ret.caption("@SYS39611");

        dfValue = ret.addField(extendedtypestr(JmgTimeString), "@SYS16104");
        dfValue.value(jmgPayStatTrans.adjustmentDisplay());

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS39611";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the Adjustment value entered by the user.
    /// </summary>
    /// <returns>
    ///    The Adjustment value entered by the user.
    /// </returns>
    /// <remarks>
    ///    This method is called after the dialog is closed. It must assign the dialog values to the internal
    ///    variables.
    ///    When inheriting from this method remember always to call the <c>super</c> method.
    /// </remarks>
    public boolean getFromDialog()
    {
        boolean ret;

        ret = super();

        value = dfValue.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>JmgPayStatAddAdjustment</c> class.
    /// </summary>
    /// <param name="payStatTrans">
    /// The <c>JmgPayStatTrans</c> record to set the adjustment on.
    /// </param>
    /// <param name="_caller">
    /// A reference to the calling form.
    /// </param>
    public void new(JmgPayStatTrans payStatTrans, FormRun _caller)
    {
        super();
        jmgPayStatTrans = payStatTrans;
        caller = _caller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    This method must contain the code that does the actual job of the class. Updates the Adjustment
    ///    field on the <see cref="T:JmgPayStatTrans" /> specified in the <see
    ///    cref="M:JmgPayStatAddAdjustment.new" /> method with the value entered by the user.
    /// </summary>
    public void run()
    {
        super();

        ttsbegin;
        jmgPayStatTrans.Adjustment = JmgProfileSeconds::unFormattedSeconds(value);
        jmgPayStatTrans.update();
        ttscommit;

        // Refresh caller as data might have changed
        caller.dataSource().research();
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        boolean ret;

        ret = super(packedClass);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        JmgPayStatAddAdjustment jmgPayStatAddAdjustment;
        if (args.record().TableId == tablenum(JmgPayStatTrans))
        {
            jmgPayStatAddAdjustment = new JmgPayStatAddAdjustment(args.record(), args.caller());

            if (jmgPayStatAddAdjustment.prompt())
            {
                jmgPayStatAddAdjustment.runOperation();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>