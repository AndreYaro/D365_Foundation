<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSupplyRiskCacheConsumer</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendSupplyRiskCacheConsumer</c> class implements a common consumer that must be used for all consumers of supply risk cache.
/// </summary>
public final class VendSupplyRiskCacheConsumer implements SysIDataCacheConsumer
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmConsumer</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public SysIDataConsumer parmConsumer()
    {
        return SysDataConsumerFactory::newFromType(SysDataConsumerTypeEnum::DataSet, classStr(VendSupplyRiskCacheDataSet));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQueryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public SysIQueryable parmQueryable()
    {
        Query query = new Query();
        query.addDataSource(tableNum(VendSupplyRiskPurchaseOrderLineAggregatedReceiptCache));

        return SysQueryableFactory::newFromQuery(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFilter</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public SysIFilter parmFilter()
    {
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public static VendSupplyRiskCacheConsumer construct()
    {
        return new VendSupplyRiskCacheConsumer();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prepareDataSet</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares the supply risk cache dataset.
    /// </summary>
    internal static void prepareDataSet()
    {
        SysDataSetService::prepareDataSet(VendSupplyRiskCacheConsumer::construct());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLastUpdate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the last update date for the supply risk dataset.
    /// </summary>
    /// <returns>The last update date for the supply risk dataset.</returns>
    [Hookable(false)]
    public static SysDataCacheLastUpdate getLastUpdate()
    {
        SysIDataCacheConsumer cacheConsumer = VendSupplyRiskCacheConsumer::construct();
        SysIDataConsumer dataConsumer = cacheConsumer.parmConsumer();
        SysIDataCacheConfigurationManager manager = SysDataCacheConfigurationManagerFactory::newFromType(dataConsumer.parmType());
        SysIDataCacheConfiguration cacheConfiguration = manager.getConfigurationForConsumer(dataConsumer.parmIdentifier());
        SysIDataContext dataContext = SysDataContextBuilder::newContextWithFilter(cacheConsumer.parmFilter(), cacheConfiguration.parmFilteringEnabled());
        SysIDataCacheContextManager cacheContextManager = SysDataCacheContextManagerFactory::newManager();
        SysIDataCacheContext cacheContext = cacheContextManager.findOrCreateCacheContext(dataConsumer.parmType(), dataConsumer.parmIdentifier(),dataContext.parmId());

        return cacheContextManager.getLastUpdate(cacheContext.parmId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>forcePrepareDataSet</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares the supply risk cache dataset.
    /// </summary>
    [Hookable(false)]
    public static void forcePrepareDataSet()
    {
        using (var activityContext = VendSupplyRiskInstrumentationLogger::createLogger(classStr(VendSupplyRiskCacheConsumer)).cache().forcePrepareDataSet())
        {
            SysIDataCacheConsumer cacheConsumer = VendSupplyRiskCacheConsumer::construct();
            SysIDataConsumer dataConsumer = cacheConsumer.parmConsumer();
            SysIDataCacheConfigurationManager manager = SysDataCacheConfigurationManagerFactory::newFromType(dataConsumer.parmType());
            SysIDataCacheConfiguration cacheConfiguration = manager.getConfigurationForConsumer(dataConsumer.parmIdentifier());
            SysIDataContext dataContext = SysDataContextBuilder::newContextWithFilter(cacheConsumer.parmFilter(), cacheConfiguration.parmFilteringEnabled());
            SysIDataCacheContextManager cacheContextManager = SysDataCacheContextManagerFactory::newManager();
            SysIDataCacheContext cacheContext = cacheContextManager.findOrCreateCacheContext(dataConsumer.parmType(), dataConsumer.parmIdentifier(),dataContext.parmId());
            SysIDataCacheManager cacheManager = SysDataCacheManagerFactory::newFromType(dataConsumer.parmType(), dataConsumer.parmIdentifier());

            cacheManager.forceRefreshCache(cacheContext);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>