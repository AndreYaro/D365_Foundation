<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLGActionTypeExeExtend</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLGActionTypeExeExtend</c> class is used to do the extend execution action for the letter
///    of guarantee.
/// </summary>
class BankLGActionTypeExeExtend extends BankLGActionType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doAction</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Does the execution action "Extend" based on the parameters.
    /// </summary>
    protected void doAction()
    {
        BankLGLedgerJournalPost bankLGLedgerJournalPost;

        ttsbegin;

        // Update LG
        bankLGGuarantee.ExpiryDateTime                      =  bankLGAction.ExpiryDateTime;
        bankLGGuarantee.BankAccountCurrencyExpenseAmount    += actionParms.parmExpenseCur();
        bankLGGuarantee.updateStatus(actionParms.getActionType());
        bankLGGuarantee.update();

        // Post
        if (actionParms.parmExpenseCur() != 0.0)
        {
            bankLGLedgerJournalPost = BankLGLedgerJournalPost::constructFromLG(bankLGGuarantee);
            // Extension commission
            bankLGLedgerJournalPost.addExpenseTrans(
                actionParms.parmExpenseCur(),
                "@SYS314679");
            bankLGLedgerJournalPost.run();
        }

        // Update LG action
        bankLGAction.ExecutionAction                    = BankLGExecutionActionType::Extend;
        bankLGAction.ExecutionDateTime                  = DateTimeUtil::getSystemDateTime();
        bankLGAction.BankAccountCurrencyExpenseAmount   = actionParms.parmExpenseCur();
        if (bankLGLedgerJournalPost)
        {
            bankLGAction.LedgerJournalTable = bankLGLedgerJournalPost.getLedgerJournalTable().JournalNum;
        }
        bankLGAction.update();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateActionParms</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Validates the action parameters.
    /// </summary>
    /// <returns>
    ///     True if all parameters are valid; otherwise, false.
    /// </returns>
    protected boolean validateActionParms()
    {
        boolean ret = super();

        if (ret)
        {
            if (actionParms.parmExpenseCur() < 0.0)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>