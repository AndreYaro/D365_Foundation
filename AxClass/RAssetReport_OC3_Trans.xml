<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>rAssetReport_OC3_Trans</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class rAssetReport_OC3_Trans extends RAssetReport_OC3_Base
{
    protected RAssetTrans rAssetTrans;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>check</Name>
				<Source><![CDATA[
    protected boolean check()
    {
        boolean ret = super();

        if (ret)
        {
            if (! (rAssetTrans.AssetTransType == RAssetTransType::Revaluation
                || rAssetTrans.AssetTransType == RAssetTransType::AcquisitionAdj))
            {
                return checkFailed("@GLS116456");
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    protected Dialog dialog()
    {
        Dialog dlg = super();

        this.documentDate(rAssetTrans.AttachDocumentDate ? rAssetTrans.AttachDocumentDate : this.transDate());
        this.documentNumber(rAssetTrans.AttachDocumentNumber);

        return dlg;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initDocumentValue</Name>
				<Source><![CDATA[
    public void initDocumentValue(RAssetDocument _document)
    {
        super(_document);

        _document.StandardId = rAssetTrans.AssetStandardId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Args _args)
    {
        if (_args.dataset() != tablenum(RAssetTrans))
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        rAssetTrans = _args.record();
        rAssetTable = RAssetTable::find(rAssetTrans.AccountNum);
        rAssetStandards = RAssetStandards::find(rAssetTrans.AccountNum, rAssetTrans.AssetStandardId);

        super(_args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>outSpendingRepair</Name>
				<Source><![CDATA[
    protected void outSpendingRepair()
    {
        if (!this.insertValues())
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts values by applicable asset trans type.
    /// </summary>
    /// <returns>True, if values were inserted; otherwise false.</returns>
    protected boolean insertValues()
    {
        boolean ret= true;

        switch (rAssetTrans.AssetTransType)
        {
            case RAssetTransType::Revaluation:
                this.insertValueIntoDoc(ConstRepairCostActual, rAssetTrans.AmountMST);
                this.insertValueIntoDoc(ConstRepairCostProject, rAssetTrans.AmountMST);

                this.insertValueIntoDoc(ConstTotalRepairCostActual, rAssetTrans.AmountMST);
                this.insertValueIntoDoc(ConstTotalRepairCostProject, rAssetTrans.AmountMST);
                break;

            case RAssetTransType::AcquisitionAdj:
                this.insertValueIntoDoc(ConstModernizationCostActual, rAssetTrans.AmountMST);
                this.insertValueIntoDoc(ConstModernizationCostProject, rAssetTrans.AmountMST);

                this.insertValueIntoDoc(ConstTotalModernizationCostActual, rAssetTrans.AmountMST);
                this.insertValueIntoDoc(ConstTotalModernizationCostProject, rAssetTrans.AmountMST);
                break;

            default:
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>relatedRecord</Name>
				<Source><![CDATA[
    public Common relatedRecord()
    {
        return rAssetTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transDate</Name>
				<Source><![CDATA[
    protected TransDate transDate()
    {
        return rAssetTrans.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDocuments</Name>
				<Source><![CDATA[
    protected void updateDocuments()
    {
        super();

        rAssetTrans = RAssetTrans::find(rAssetTrans.RecId, true);
        rAssetTrans.AttachDocumentDate = documentDate;
        rAssetTrans.AttachDocumentNumber = documentNumber;
        rAssetTrans.update();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>