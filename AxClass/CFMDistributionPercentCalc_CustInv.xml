<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMDistributionPercentCalc_CustInv</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class calculates an amount distribution percent for customer invoice journals.
/// </summary>
[CFMDistributionPercentSourceAttribute(NoYes::No, tableStr(CustInvoiceJour))]
public class CFMDistributionPercentCalc_CustInv extends CFMDistributionPercentCalc
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>custInvoiceJour</Name>
				<Source><![CDATA[
    private CustInvoiceJour custInvoiceJour()
    {
        return source;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillDistribution</Name>
				<Source><![CDATA[
    protected void fillDistribution()
    {
        CustInvoiceTrans invoiceTrans;
        CFMLedgerDimension cfmLedgerDimensionHeader = cfmDimensionConverter.convertDefaultDimension(this.custInvoiceJour().DefaultDimension);
        CFMLedgerDimension cfmLedgerDimensionLine;

        while select invoiceTrans
            where invoiceTrans.InvoiceId == this.custInvoiceJour().InvoiceId
               && invoiceTrans.InvoiceDate == this.custInvoiceJour().InvoiceDate
               && invoiceTrans.NumberSequenceGroup == this.custInvoiceJour().NumberSequenceGroup
        {
            cfmLedgerDimensionLine = paymentRequestsEnabled ? cfmDimensionConverter.convertDefaultDimension(invoiceTrans.DefaultDimension) : cfmLedgerDimensionHeader;

            this.addAmount(cfmLedgerDimensionLine,
                invoiceTrans.TaxGroup,
                invoiceTrans.TaxItemGroup,
                this.paymentSign() * (invoiceTrans.LineAmount + invoiceTrans.TaxAmount));

            this.addMarkupLines(invoiceTrans.TableId, invoiceTrans.RecId, cfmLedgerDimensionLine);
        }

        this.addMarkupLines(this.custInvoiceJour().TableId, this.custInvoiceJour().RecId, cfmLedgerDimensionHeader);
    }

]]></Source>
			</Method>
			<Method>
				<Name>paymentSign</Name>
				<Source><![CDATA[
    protected Sign paymentSign()
    {
        return -1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceCurrencyCode</Name>
				<Source><![CDATA[
    protected CurrencyCode sourceCurrencyCode()
    {
        return this.custInvoiceJour().CurrencyCode;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>