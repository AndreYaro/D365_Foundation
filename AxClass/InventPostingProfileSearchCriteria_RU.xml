<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventPostingProfileSearchCriteria_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventPostingProfileSearchCriteria_RU</c> class is responsible for populating the profile related fields
/// in the <c>InventPostingSearchState</c>.
/// </summary>
abstract class InventPostingProfileSearchCriteria_RU extends InventPostingSearchCriteria
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildSearchValuesList</Name>
				<Source><![CDATA[
    protected void buildSearchValuesList()
    {
        searchValues = new List(Types::Enum);
        if (this.inventDim().InventProfileId_RU)
        {
            searchValues.addEnd(InventProfileTypeAll_RU::Profile);
            searchValues.addEnd(InventProfileTypeAll_RU::Type);
        }
        searchValues.addEnd(InventProfileTypeAll_RU::All);
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateInventPostingSearchState</Name>
				<Source><![CDATA[
    public void populateInventPostingSearchState(anytype _searchValue)
    {
        InventProfileTypeAll_RU inventProfileTypeAllValue   = _searchValue;
        InventProfileId_RU      inventProfileId             = '';
        InventProfileType_RU    inventProfileType           = InventProfileType_RU::NotSpecified;
        
        switch (inventProfileTypeAllValue)
        {
            case InventProfileTypeAll_RU::Profile:
                inventProfileId   = this.inventDim().InventProfileId_RU;
                inventProfileType = InventProfile_RU::find(inventProfileId).InventProfileType;
                break;

            case InventProfileTypeAll_RU::Type:
                inventProfileId   = '';
                inventProfileType = InventProfile_RU::find(this.inventDim().InventProfileId_RU).InventProfileType;
                break;

            case InventProfileTypeAll_RU::All:
                inventProfileId   = '';
                inventProfileType = InventProfileType_RU::NotSpecified;
                break;
        }

        InventPosting inventPosting = inventPostingSearchState.parmInventPosting();
        inventPosting.InventAccountType = this.accountType();
        inventPosting.InventProfileTypeAll_RU = inventProfileTypeAllValue;
        inventPosting.InventProfileType_RU = inventProfileType;
        inventPosting.InventProfileId_RU = inventProfileId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustSearchCriteria</Name>
				<Source><![CDATA[
    public boolean mustSearchCriteria(anytype _searchValue)
    {
        return inventPostingParameters.inventProfileRelationEnabled_RU(_searchValue);
    }

]]></Source>
			</Method>
			<Method>
				<Name>accountType</Name>
				<Source><![CDATA[
    protected abstract InventAccountType accountType()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventDim</Name>
				<Source><![CDATA[
    protected abstract InventDim inventDim()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>