<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HRMParametersEventHandler_AppSuite</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles events for the <c>HRMParameters</c> table.
/// </summary>
public final class HRMParametersEventHandler_AppSuite
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HRMParameters_onValidatingWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>ValidatingWrite</c> event of the <c>HRMParameters</c> table.
    /// </summary>
    /// <param name="_sender">The HRMParameters record context.</param>
    /// <param name="_e">DataEventArgs from the event.</param>
    [DataEventHandler(tableStr(HRMParameters), DataEventType::ValidatingWrite)]
    public static void HRMParameters_onValidatingWrite(Common _sender, DataEventArgs _e)
    {
        HRMParameters hrmParameters = _sender as HRMParameters;
        ValidateEventArgs validateEventArgs = _e as ValidateEventArgs;

        boolean result = validateEventArgs.parmValidateResult();

        // Perform compensation validations
        if (hrmParameters.CompRatingModel == HRMCompRatingModel::SkillRating && !hrmParameters.RatingModel)
        {
            result = result && checkFailed(strFmt("@SYS84753", fieldId2pname(tableNum(HRMParameters), fieldNum(HRMParameters, RatingModel))));
        }
        else if (hrmParameters.CompRatingModel != HRMCompRatingModel::SkillRating && hrmParameters.RatingModel)
        {
            result = result && checkFailed(strFmt("@HCM:InvalidCompensationParameterCombination",
                                                fieldId2pname(tableNum(HRMParameters), fieldNum(HRMParameters, RatingModel)),
                                                fieldId2pname(tableNum(HRMParameters), fieldNum(HRMParameters, CompRatingModel)),
                                                enum2Str(HRMCompRatingModel::SkillRating)));
        }

        validateEventArgs.parmValidateResult(result);
    }

]]></Source>
			</Method>
			<Method>
				<Name>HRMParameters_onModifiedField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>onModifiedField</c> event and updates the fields appropriately.
    /// </summary>
    /// <param name="_sender">The HRMParameters record context.</param>
    /// <param name="_e">DataEventArgs from the event.</param>
    [DataEventHandler(tableStr(HRMParameters), DataEventType::ModifiedField)]
    public static void HRMParameters_onModifiedField(Common _sender, DataEventArgs _e)
    {
        ModifyFieldEventArgs modifyFieldEvent = _e as ModifyFieldEventArgs;

        switch (modifyFieldEvent.parmFieldId())
        {
            case fieldNum(HRMParameters, CompRatingModel):
                HRMParameters hrmParameters = _sender as HRMParameters;

                if (hrmParameters.CompRatingModel != HRMCompRatingModel::SkillRating && hrmParameters.RatingModel)
                {
                    // Remove the rating model reference when the competency rating model is not skill rating.
                    hrmParameters.RatingModel = 0;
                }
                break;
        }

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>