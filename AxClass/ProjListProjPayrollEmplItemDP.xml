<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjPayrollEmplItemDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjPayrollEmplItemDP</c> class provides data for the
///    <c>ProjListProjPayrollEmplItem</c> report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(ProjStatementEmplItem)),
    SRSReportParameterAttribute(classStr(ProjListProjPayrollContract))
]

class ProjListProjPayrollEmplItemDP extends SRSReportDataProviderBase
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    TransDate               projFromDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    ProjListColumnData      actualVsBudget;
    ProjForecastModelId     forecastModelId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;

    ProjListProjTrans_PayRoll       listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjHelpClass           projHelpClass;
    ProjListProjPayrollTmp          projListProjPayrollTmp;
    projStatement                   projStatement;

    ProjListStateProjType   stateProjType;
    QueryRun                qrun;

    ResourceRecId           resource;
    ProjEmplItemId          id;
    Name                    name;

    ProjQty                 qtyHour;
    AmountMST               payRollCost;

    int                     reportType;
    int                     hourQtyDecimals, valueAddedDecimals, grossMarginDecimals, amountDecimals;
    int                     showPayrollCost, showQtyHour;
    ProjLabel               valueFactorLabel,grossMarginPctLabel, headerLabel, periodLabel;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
/// <summary>
/// Calculates the payroll cost amount and hour quantity.
/// </summary>
void calculate()
{
    projStatement.parmResource(resource);

    switch (amountQty)
    {
        case ProjAmountQty::Amount:
            payRollCost     = projStatement.d100_PayrollAllocation();
            break;
        case ProjAmountQty::Qty:
            qtyHour         = projStatement.d100_PayrollAllocation_Qty();
            break;
        case ProjAmountQty::AmountQty:
            payRollCost     = projStatement.d100_PayrollAllocation();
            qtyHour         = projStatement.d100_PayrollAllocation_Qty();
            break;
    }
}
]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjPayrollTmp</Name>
				<Source><![CDATA[
/// <summary>
///    Retrieves the data from the <c>projListProjPayrollTmp</c> table.
/// </summary>
/// <returns>
///    The data from the <c>projListProjPayrollTmp</c> table.
/// </returns>

[SRSReportDataSetAttribute('ProjListProjPayrollTmp')]
public ProjListProjPayrollTmp getProjListProjPayrollTmp()
{
    select * from projListProjPayrollTmp;
    return projListProjPayrollTmp;
}
]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
/// <summary>
///    Initializes the <c>ProjListProjTrans</c> class by using the parameters.
/// </summary>
private void initProjListTrans()
{
    listProjTrans = ProjListProjTrans::construct(ProjListStateType::Payroll, ProjListStateId::Project);
    listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

    listProjTrans.parmTimeMaterial(timeMaterial);
    listProjTrans.parmFixedPrice(fixedPrice);
    listProjTrans.parmInvestment(investment);
    listProjTrans.parmInternalCost(internalCost);

    listProjTrans.parmProjFromDate(projFromDate);
    listProjTrans.parmProjToDate(projToDate);
    listProjTrans.parmLedgerFromDate(ledgerFromDate);
    listProjTrans.parmLedgerToDate(ledgerToDate);

    listProjTrans.parmColumnData(actualVsBudget);
    listProjTrans.parmForecastModelId(forecastModelId);

    listProjTransLayout.parmStateProjType(stateProjType);
    listProjTransLayout.parmSkipZero(skipZero);

    listProjTransLayout.parmShowAmount(showAmount);
    listProjTransLayout.parmAmountQty(amountQty);
}
]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjPayrollTmp</Name>
				<Source><![CDATA[
/// <summary>
///    Retrieves the required data and inserts it into the temporary table.
/// </summary>
private void insertProjListProjPayrollTmp()
{
    projListProjPayrollTmp.clear();

    projListProjPayrollTmp.Id   = id;
    projListProjPayrollTmp.Name = name;
    projListProjPayrollTmp.IdLabel       = "@SYS89158";

    projListProjPayrollTmp.QtyHour       = qtyHour;
    projListProjPayrollTmp.PayrollCost   = listProjTransLayout.showAmountReport(payRollCost, showAmount);

    // set labels
    projListProjPayrollTmp.HeaderLabel   = headerLabel;
    projListProjPayrollTmp.PeriodLabel   = periodLabel;

    // set decimal values
    projListProjPayrollTmp.DecimalPayrollCost = amountDecimals;
    projListProjPayrollTmp.DecimalHourQty = hourQtyDecimals;

    // set field visibility
    projListProjPayrollTmp.ShowQtyHour = showQtyHour;
    projListProjPayrollTmp.ShowPayrollCost = showPayrollCost;

    projListProjPayrollTmp.insert();
}
]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
/// <summary>
///    Checks whether the payroll cost amount and hour quantity are zero.
/// </summary>
/// <returns>
///    true if the values are not zero; otherwise false.
/// </returns>
boolean lineValue()
{
    return (payRollCost  != 0 || qtyHour != 0 );
}
]]></Source>
			</Method>
			<Method>
				<Name>printEmplItem</Name>
				<Source><![CDATA[
/// <summary>
///    Retrieves the employee record and calls the <c>calculate</c> method.
/// </summary>
void printEmplItem()
{
    ResourceView    resourceLocal;

    if (SysReportRun::changed(qrun,tableNum(ResourceView)))
    {
        resourceLocal  = qrun.get(tableNum(ResourceView));

        resource        = resourceLocal.RecId;
        id              = ResourceFacade::getID(resource);
        name            = ResourceFacade::getName(resource);

        this.calculate();

        if (! skipZero || (this.lineValue()))
        {
            this.insertProjListProjPayrollTmp();
        }
    }
}

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
/// <summary>
///    Processes the report business logic.
/// </summary>
/// <remarks>
///    This method provides the ability to write the report business logic. It will be called by SQL
///    Server Reporting Services (SSRS) at run time. This method should compute data and populate the data
///    tables that will be returned to SSRS.
/// </remarks>
public void processReport()
{
    ProjListProjPayrollContract contract = this.parmDataContract() as ProjListProjPayrollContract;

    timeMaterial = contract.parmTimeMaterial();
    fixedPrice = contract.parmFixedPrice();
    investment = contract.parmInvestment();
    internalCost = contract.parmInternalCost();

    projFromDate = contract.parmProjFromDate();
    projToDate = contract.parmProjToDate();
    ledgerFromDate = contract.parmLedgerFromDate();
    ledgerToDate = contract.parmLedgerToDate();

    actualVsBudget = contract.parmActualVsBudget();
    forecastModelId = contract.parmForecastModelId();

    skipZero = contract.parmSkipZero();

    showAmount = contract.parmShowAmount();
    amountQty = contract.parmAmountQty();

    reportType = contract.parmReportType();

    this.initProjListTrans();

    headerLabel   = listProjTrans.formatSelection(showAmount);
    periodLabel   = listProjTrans.formatPeriodLabel('', DateFlags::None);

    [showQtyHour, showPayrollCost] = listProjTrans.setDisplayFlags(amountQty);

    [hourQtyDecimals, amountDecimals,
    grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

    projStatement = projStatement::newProjListPayroll(
                        listProjTrans,
                        projListProjTrans2Period::columnData2ActualBudget(listProjTrans.parmColumnData()),
                        listProjTransLayout.parmAmountQty(),
                        listProjTrans.parmForecastModelId());

    qrun = new QueryRun(this.parmQuery());

    while (qrun.next())
    {
        this.printEmplItem();
    }
}
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
