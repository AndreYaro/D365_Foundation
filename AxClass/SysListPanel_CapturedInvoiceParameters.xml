<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysListPanel_CapturedInvoiceParameters</Name>
	<SourceCode>
		<Declaration><![CDATA[
[SysObsolete('This component has been replaced and is no longer needed.', false, 27\7\2023)]
class SysListPanel_CapturedInvoiceParameters extends SysListPanel
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newForm</Name>
				<Source><![CDATA[
    static SysListPanel newForm(FormRun formRun,
        int parentId,
        str notSelectedLabel,
        str selectedLabel)
    {
        SysListPanel_CapturedInvoiceParameters sysListPanel = new SysListPanel_CapturedInvoiceParameters();

        sysListPanel.parmFormRun(formRun);
        sysListPanel.parmParentId(parentId);
        sysListPanel.parmCaptionAvailable(notSelectedLabel);
        sysListPanel.parmCaptionSelected(selectedLabel);

        syslistPanel.parmHasUpDownButton(false);

        sysListPanel.build();
        return sysListPanel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addData</Name>
				<Source><![CDATA[
    void addData(container data)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getData</Name>
				<Source><![CDATA[
    container getData()
    {
        container availableData = this.fillNotSelected();
        container selectedData = this.fillSelected();

        return [availableData, selectedData];
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillNotSelected</Name>
				<Source><![CDATA[
    private container fillNotSelected()
    {
        OMLegalEntity omLegaLEntity;
        CapturedInvoiceLegalEntity selectedLegalEntities;
        container notSelectedLegalEntitiesContainer;

        while select RecId, LegalentityId from omLegaLEntity
            notexists join selectedLegalEntities
            where omLegaLEntity.RecId == selectedLegalEntities.selected
        {
            notSelectedLegalEntitiesContainer += [[omLegaLEntity.RecId, omLegaLEntity.legalentityId]];
        }

        return notSelectedLegalEntitiesContainer;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillSelected</Name>
				<Source><![CDATA[
    private container fillSelected()
    {
        OMLegalEntity omLegalEntity;
        CapturedInvoiceLegalEntity selectedLegalEntities;
        container selectedLegalEntitiesContainer;

        while select selected from selectedLegalEntities
            join LegalentityId, RecId from omLegaLEntity
            where omLegaLEntity.RecId == selectedLegalEntities.selected
        {
            selectedLegalEntitiesContainer += [[omLegaLEntity.RecId, omLegaLEntity.LegalentityId]];
        }

        return selectedLegalEntitiesContainer;
    }

]]></Source>
			</Method>
			<Method>
				<Name>finalize</Name>
				<Source><![CDATA[
    public void finalize()
    {
        super();

        this.writeFields();
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeFields</Name>
				<Source><![CDATA[
     private void writeFields()
    {
        List selectedLegalEntityList = new List(Types::Int64);
        int idx;
        CapturedInvoiceLegalEntity capturedInvoiceLegalEntity;
        ListEnumerator selectedEnumerator;
        RecId legalEntityId;

        idx = this.parmViewSelected().getNextItem(FormListNext::All);

        while (idx != -1)
        {
            selectedLegalEntityList.addEnd(this.parmViewSelected().getItem(idx).data());
            idx = this.parmViewSelected().getNextItem(FormListNext::All, idx);
        }

        // TODO: Move this close to Data: capturedInvoiceLegalEntity.
        ttsbegin;

        delete_from capturedInvoiceLegalEntity;

        if (selectedLegalEntityList)
        {
            selectedEnumerator = selectedLegalEntityList.getEnumerator();

            while (selectedEnumerator.moveNext())
            {
                legalEntityId = selectedEnumerator.current();

                capturedInvoiceLegalEntity.clear();
                capturedInvoiceLegalEntity.initValue();
                capturedInvoiceLegalEntity.selected = legalEntityId;
                capturedInvoiceLegalEntity.insert();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeData</Name>
				<Source><![CDATA[
    void removeData(container data)
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>