<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventJournalTransHandler_PL</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Provids pre/post event handlers for InventJournalTrans table.
/// </summary>
class InventJournalTransHandler_PL
{
    #isoCountryRegionCodes
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postCheckQtyWarehouseDocuments</Name>
				<Source><![CDATA[
    [PostHandlerFor(tableStr(InventJournalTrans), tableMethodStr(InventJournalTrans, checkQty))]
    /// <summary>
    /// Calls for check quantity <c>InventJournalTrans</c> lines.
    /// </summary>
    /// <param name="_args">
    /// Event handler argument.
    /// </param>
    public static void postCheckQtyWarehouseDocuments(XppPrePostArgs _args)
    {
        InventJournalTrans  inventJournalTrans = _args.getThis();
        anytype             returnValue = _args.getReturnValue();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoPL]))
        {
            InventJournalTable inventJournalTable = inventJournalTrans.inventJournalTable();
            InventMovement inventMovement = inventJournalTrans.inventMovement();

            if ((inventJournalTable.InventDocType_PL == PlInventDocType::PW &&
                    inventMovement.transSign() < 0) ||
                (inventJournalTable.InventDocType_PL == PlInventDocType::RW &&
                inventMovement.transSign() > 0))
            {
                returnValue =  checkFailed((inventJournalTable.InventDocType_PL == PlInventDocType::PW ? "@GLS110442" : "@GLS110443") +
                                    ' '                                                                                      +
                                    (inventJournalTrans.Qty > 0 ? "@GLS113021" : "@SYS50502"));
                inventJournalTrans.Qty = inventJournalTrans.orig().Qty;
            }
        }
        _args.setReturnValue(returnValue);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postValidateDeleteWarehouseDocuments</Name>
				<Source><![CDATA[

    [PostHandlerFor(tableStr(InventJournalTrans), tableMethodStr(InventJournalTrans, validateDelete))]
    /// <summary>
    /// Calls for validate delete <c>InventJournalTrans</c> lines.
    /// </summary>
    /// <param name="_args">
    /// Event handler argument.
    /// </param>
    public static void postValidateDeleteWarehouseDocuments(XppPrePostArgs _args)
    {
        InventJournalTrans  inventJournalTrans = _args.getThis();
        InventJournalTable  inventJournalTable;
        anytype             returnValue = _args.getReturnValue();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoPL]))
        {
            inventJournalTable = inventJournalTrans.inventJournalTable();
            if (inventJournalTable.Posted &&
                !InventJournalName::find(inventJournalTable.JournalNameId).DeletePostedLines)
            {
                returnValue = false;
            }
        }
        _args.setReturnValue(returnValue);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
