<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailTransactionSupplementaryInvoiceValidator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailTransactionSupplementaryInvoiceValidator</c> validates supplementary invoice.
/// </summary>
public class RetailTransactionSupplementaryInvoiceValidator
{
    CustInvoiceJour custInvoiceJour;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of <c>RetailTransactionSupplementaryInvoiceValidator</c>.
    /// </summary>
    /// <param name = "_sourceTable">Supplementary invoice source.</param>
    /// <returns>A new instance of <c>RetailTransactionSupplementaryInvoiceValidator</c>.</returns>
    static public RetailTransactionSupplementaryInvoiceValidator construct(Common _sourceTable)
    {
        return new RetailTransactionSupplementaryInvoiceValidator(_sourceTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates if supplementary invoice can be created.
    /// </summary>
    /// <returns>True if validation is successful; false otherwise.</returns>
    public boolean validate()
    {
        if (custInvoiceJour)
        {
            try
            {
                if (!this.validateRetailTransaction())
                {
                    return false;
                }
            }
            catch
            {
                error("@SYS138340");
                return false;
            }
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>RetailTransactionSupplementaryInvoiceCreator</c> class.
    /// </summary>
    /// <param name = "_source">Supplementary invoice source.</param>
    protected void new(Common _source)
    {
        this.init(_source);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateRetailTransaction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates retail transaction.
    /// </summary>
    /// <returns>True if validation is successful; false otherwise.</returns>
    protected boolean validateRetailTransaction()
    {
        if (this.checkIfInvoiceIsAggregated())
        {
            error("@RetailSupplementaryInvoice:AggregatedInvoiceStatusCannotBeChanged");
            return false;
        }

        RetailTransactionSupplementaryInvoice supplementaryInvoice = this.findSupplementaryInvoice();
        if (supplementaryInvoice)
        {
            error(strFmt("@RetailSupplementaryInvoice:RetailTransactionForInvoiceExists", supplementaryInvoice.Store, supplementaryInvoice.Terminal, supplementaryInvoice.TransactionId));
            return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    private void init(Common _source)
    {
        custInvoiceJour = _source as CustInvoiceJour;
        if (custInvoiceJour)
        {
            custInvoiceJour = CustInvoiceJour::findRecId(custInvoiceJour.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkIfInvoiceIsAggregated</Name>
				<Source><![CDATA[
    private boolean checkIfInvoiceIsAggregated()
    {
        RetailTransactionTable retailTransactionTable;

        select count (Recid) from retailTransactionTable
            where retailTransactionTable.SalesOrderId == custInvoiceJour.SalesId
            && retailTransactionTable.type == RetailTransactionType::Sales
            && retailTransactionTable.entryStatus == RetailEntryStatus::Posted;
        
        return retailTransactionTable.RecId > 1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findSupplementaryInvoice</Name>
				<Source><![CDATA[
    private RetailTransactionSupplementaryInvoice findSupplementaryInvoice()
    {
        return RetailTransactionSupplementaryInvoice::findBySalesId(custInvoiceJour.SalesId);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>