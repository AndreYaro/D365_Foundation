<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxTransactionInquiryFieldObject_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxTransactionInquiryFieldObject_IN</c> class packs information about
/// the fields of tax transaction inquiry. It is used for choosing the fields in
/// the list panel.
/// </summary>
public class TaxTransactionInquiryFieldObject_IN implements SysPackable
{
    TableId     tableId;
    FieldId     fieldId;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        TableId,
        fieldId
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedList)
    {
        Version     version = RunBase::getVersion(_packedList);
        boolean     ret;

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedList;
                ret = true;
                break;

            default :
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTableId</Name>
				<Source><![CDATA[
    public TableId parmTableId(TableId _tableId = tableId)
    {
        if (!prmisdefault(_tableId))
        {
            tableId = _tableId;
        }
        return _tableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFieldId</Name>
				<Source><![CDATA[
    public FieldId parmFieldId(FieldId _fieldId = fieldId)
    {
        if (!prmisdefault(_fieldId))
        {
            fieldId = _fieldId;
        }
        return _fieldId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>equal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Check if the current object is equal to the input field object.
    /// </summary>
    /// <param name = "_fieldObject">the object that you want to compare</param>
    /// <returns>
    /// True if two objects are equal.
    /// </returns>
    public boolean equal(Object _fieldObject)
    {
        boolean ret;

        TaxTransactionInquiryFieldObject_IN fieldObject = _fieldObject as TaxTransactionInquiryFieldObject_IN;

        if (fieldObject != null)
        {
            ret = (fieldObject.parmTableId() == this.parmTableId()
                && fieldObject.parmFieldId() == this.parmFieldId());
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>toString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the label string of the class TaxTransactionInquiryFieldObject.
    /// This string is shown in the list panels and we can see.
    /// </summary>
    /// <returns>
    /// The string of the label.
    /// </returns>
    public LabelString toString()
    {
        LabelString         ret;
        SysDictField        sysDictField;

        if (this.parmTableId() != 0 && this.parmFieldId() != 0)
        {
            sysDictField = new SysDictField(this.parmTableId(), this.parmFieldId());
            ret =  SysLabel::labelId2String(sysDictField.labelLabel());
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldName</Name>
				<Source><![CDATA[
    public RefFieldName getFieldName()
    {
        RefFieldName        ret;
        SysDictField        sysDictField;

        if (this.parmTableId() != 0 && this.parmFieldId() != 0)
        {
            sysDictField = new SysDictField(this.parmTableId(), this.parmFieldId());
            ret = sysDictField.name();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TaxTransactionInquiryFieldObject_IN construct(TableId _tableId, FieldId _fieldId)
    {
        TaxTransactionInquiryFieldObject_IN  fieldObject = new TaxTransactionInquiryFieldObject_IN();

        fieldObject.parmTableId(_tableId);
        fieldObject.parmFieldId(_fieldId);

        return fieldObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creat a new instance of class TaxTransactionInquiryFieldObject_IN by a packed field object.
    /// </summary>
    /// <param name = "_packedFieldObject">the packed field object</param>
    /// <returns>
    /// New instance of the class TaxTransactionInquiryFieldObject_IN.
    /// </returns>
    public static TaxTransactionInquiryFieldObject_IN create(container _packedFieldObject)
    {
        TaxTransactionInquiryFieldObject_IN  fieldObject;

        fieldObject = new TaxTransactionInquiryFieldObject_IN();
        fieldObject.unpack(_packedFieldObject);

        return fieldObject;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>