<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PmfFormCtrl_SalesCreateReleaseOrder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The form control object for the SalesCreateReleaseOrder form. The object maintains
/// the accesibility of the catchweight and weight fields of the line section of the form
/// </summary>
/// <remarks>
/// It is assumed, that the object is instantiated only, if the catch weight
/// configuration key is enabled. Access to the catch weight fields is not being
/// checked for this configuration key.
/// The catch weight field synchronization and editability cannot be maintained by
/// a PdsCWFormCtrl class as the buffer maintains the weight in sales units, but
/// the buffer does not contain the unit itself.
/// See <c>PmfFormCtrl</c>.
/// </remarks>
class PmfFormCtrl_SalesCreateReleaseOrder extends PmfFormCtrl
{
    fieldId fldItemId;
    fieldId fldTransId;
    fieldId fldSalesQty;
    fieldId fldPdsCWQty;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fieldModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method override to trigger quantity changes in the weight field when then catch weight value has been changed
    /// </summary>
    /// <param name="_fieldId">The id of the modified field</param>
    public void fieldModified(fieldId _fieldId)
    {
        switch (_fieldId)
        {
            case fldPdsCWQty:
                buffer.(fldSalesQty) = PdsCatchWeight::inventQty(
                                            buffer.(fldItemId),
                                            buffer.(fldPdsCWQty),
                                            SalesLine::findInventTransId(
                                                    buffer.(fldTransId))
                                                .SalesUnit);
                break;
        }

        super(_fieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// protected new method to initailize the class
    /// </summary>
    /// <remarks>
    /// The method initializes the field ids to be used in its instance methods. The
    /// field id variables are kept for convenience only.
    /// </remarks>
    protected void new()
    {
        super();

        fldItemId = fieldnum(SalesCreateReleaseOrderLineTmp,ItemId);
        fldTransId = fieldnum(SalesCreateReleaseOrderLineTmp,InventTransId);
        fldSalesQty = fieldnum(SalesCreateReleaseOrderLineTmp,SalesQty);
        fldPdsCWQty = fieldnum(SalesCreateReleaseOrderLineTmp,PdsCWQty);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method override to set the accessibility to the catchweight and weight fields based on the item type
    /// </summary>
    public void updateDesign()
    {
        boolean cwItem = PdsGlobal::pdsIsCWItem(buffer.(fldItemId));

        super();

        this.enableDSField(fldPdsCWQty,cwItem);
        this.enableDSField(fldSalesQty,!cwItem);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// public method to create an instance of this class
    /// </summary>
    /// <returns>The created class instance.</returns>
    /// <example>
    /// pmfFormCtrl = PmfFormCtrl_SalesCreateReleaseOrder::construct();
    /// </example>
    public static PmfFormCtrl_SalesCreateReleaseOrder construct()
    {
        return new PmfFormCtrl_SalesCreateReleaseOrder();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>