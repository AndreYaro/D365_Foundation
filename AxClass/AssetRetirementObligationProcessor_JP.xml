<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetRetirementObligationProcessor_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Processor for asset retirement obligation.
/// </summary>
[AssetDocumentTypeAttribute_JP(AssetDocumentType_JP::AssetRetirementObligation)]
class AssetRetirementObligationProcessor_JP extends AssetDocumentProcessor_JP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createDocumentTrans</Name>
				<Source><![CDATA[
        protected AssetDocumentTrans_JP createDocumentTrans()
    {
        AssetDocumentTrans_JP               assetDocumentTrans;
        AssetDocumentTrans_JP               assetDocumentTransCount;
        AssetRetirementObligationTrans_JP   assetRetirementObligationTrans;
        AssetTrans                          assetTrans;

        assetDocumentTrans.clear();
        assetDocumentTrans.initValue();
        assetDocumentTrans.AssetDocumentEntry_JP = assetDocumentEntryRecId;
        assetDocumentTrans.RefTableId = this.parmRefTableId();
        assetDocumentTrans.RefRecId = this.parmRefRecId();
        assetDocumentTrans.insert();

        if (this.parmRefTableId() == tableNum(AssetTrans))
        {
            assetRetirementObligationTrans.clear();
            assetTrans = AssetTrans::find(this.parmRefRecId());

            switch (assetTrans.TransType)
            {
                case AssetTransType::Acquisition:
                case AssetTransType::AcquisitionAdj:
                    select count(RecId) from  assetDocumentTransCount
                        where assetDocumentTransCount.AssetDocumentEntry_JP == assetDocumentEntryRecId;
                    if (assetDocumentTransCount.RecId == 1)
                    {
                        //Acquisition, construction or development
                        assetRetirementObligationTrans.Type = AssetRetirementObligationResultType_JP::Aquisition;
                    }
                    else
                    {
                        if (assetTrans.AmountMST > 0)
                        {
                            assetRetirementObligationTrans.Type = AssetRetirementObligationResultType_JP::Upward;
                        }
                        else
                        {
                            assetRetirementObligationTrans.Type = AssetRetirementObligationResultType_JP::Downward;
                        }
                    }
                    break;
                case AssetTransType::AssetRetirementObligationInterest_JP:
                    assetRetirementObligationTrans.Type = AssetRetirementObligationResultType_JP::Interest;
                    break;
                case AssetTransType::Depreciation:
                    assetRetirementObligationTrans.Type = AssetRetirementObligationResultType_JP::Depreciation;
                    break;
            }

            assetRetirementObligationTrans.AssetDocumentTrans_JP = assetDocumentTrans.RecId;
            assetRetirementObligationTrans.insert();
        }

        return assetDocumentTrans;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>