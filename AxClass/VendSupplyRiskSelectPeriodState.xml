<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSupplyRiskSelectPeriodState</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendSupplyRiskSelectPeriodState</c> class represents the user selected period or date range.
/// </summary>
public final class VendSupplyRiskSelectPeriodState
{
    private const DateCode  BlankDateCode = '';
    private date            fromDate;
    private date            toDate;
    private DateCode        periodCode;
    
    private const int       CurrentVersion = 1;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static VendSupplyRiskSelectPeriodState construct()
    {
        return new VendSupplyRiskSelectPeriodState();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDateCode</Name>
				<Source><![CDATA[
    internal DateCode parmDateCode(DateCode _periodCode = periodCode)
    {
        if (!prmIsDefault(_periodCode))
        {
            periodCode = _periodCode;

            this.updateDatesFromCode();
        }

        return periodCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFromDate</Name>
				<Source><![CDATA[
    internal date parmFromDate(date _fromDate = fromDate)
    {
        if (!prmIsDefault(_fromDate))
        {
            fromDate = _fromDate;
            this.clearPeriodCode();
        }

        return fromDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmToDate</Name>
				<Source><![CDATA[
    internal date parmToDate(date _toDate = toDate)
    {
        if (!prmIsDefault(_toDate))
        {
            toDate = _toDate;
            this.clearPeriodCode();
        }

        return toDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    internal container pack()
    {
        return [CurrentVersion, fromDate, toDate, periodCode];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    internal boolean unpack(container _con)
    {
        int version = conPeek(_con, 1);

        if (version == CurrentVersion)
        {
            this.parmFromDate(conPeek(_con, 2));
            this.parmToDate(conPeek(_con, 3));
            this.parmDateCode(conPeek(_con, 4));
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDatesFromCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the date ranges as a result of changing the period code.
    /// </summary>
    private void updateDatesFromCode()
    {
        if (this.periodCode)
        {
            var ledgerPeriodCode = LedgerPeriodCode::find(this.periodCode);
            this.fromDate = ledgerPeriodCode.fromDate();
            this.toDate = LedgerPeriodCode.toDate();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearPeriodCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears the period code as a result of changing the dates.
    /// </summary>
    private void clearPeriodCode()
    {
        this.periodCode = BlankDateCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromFiscalYearPeriod</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>VendSupplyRiskSelectPeriodState</c> with from and to dates set to current period start and end dates respectively.
    /// </summary>
    /// <returns>An instance of <c>VendSupplyRiskSelectPeriodState</c>.</returns>
    internal static VendSupplyRiskSelectPeriodState newFromFiscalYearPeriod()
    {
        var fiscalYearPeriod = LedgerFiscalCalendar::findPeriodByPeriodCodeDate(Ledger::fiscalCalendar(), DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()));

        var state = VendSupplyRiskSelectPeriodState::construct();
        state.parmFromDate(fiscalYearPeriod.StartDate);
        state.parmToDate(fiscalYearPeriod.EndDate);

        return state;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDaysInDateRange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates a number of days within a gived date range.
    /// </summary>
    /// <returns>A <c>NumberOfDays</c> value.</returns>
    internal NumberOfDays getDaysInDateRange()
    {
        var numberOfDays = date2Num(this.parmToDate()) - date2Num(this.parmFromDate()) + 1;
        return numberOfDays;
    }

]]></Source>
			</Method>
			<Method>
				<Name>asDateInterval</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a date interval representing the period.
    /// </summary>
    /// <returns>A <c>DateInterval</c> displaying the from and to date.</returns>
    internal DateInterval asDateInterval()
    {
        return strFmt('%1 - %2', date2StrUsr(fromDate), date2StrUsr(toDate));
    }

]]></Source>
			</Method>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>VendSupplyRiskSelectPeriodState</c> based on a packed version of the <c>VendSupplyRiskSelectPeriodState</c> class.
    /// </summary>
    /// <param name = "_packedNode">The packed version of the <c>VendSupplyRiskSelectPeriodState</c> class.</param>
    /// <returns>An instance of <c>VendSupplyRiskSelectPeriodState</c>.</returns>
    internal static VendSupplyRiskSelectPeriodState create(container _packedNode)
    {
        VendSupplyRiskSelectPeriodState periodState = VendSupplyRiskSelectPeriodState::construct();

        boolean ret = periodState.unpack(_packedNode);

        return ret ? periodState : null;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>