<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSCommercialInvoiceController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TMSCommercialInvoiceController</c> class is used to run print management for <c>TMSCommercialInvoice</c> report.
/// </summary>
class TMSCommercialInvoiceController extends SrsPrintMgmtController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        this.setRanges();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPrintMgmt</Name>
				<Source><![CDATA[
    protected void runPrintMgmt()
    {
        printMgmtReportRun = PrintMgmtReportRun::construct(PrintMgmtHierarchyType::TMS, PrintMgmtNodeType::TMS, PrintMgmtDocumentType::TMSCommercialInvoice);

        printMgmtReportRun.parmReportRunController(this);

        printMgmtReportRun.load(this.parmArgs().record(), this.parmArgs().record(), currentUserLanguage());

        if (printMgmtReportRun.more())
        {
            this.outputReports();
        }
        else
        {
            warning("@SYS78951");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    private void setRanges()
    {
        Query                   q;
        QueryBuildDataSource    queryBuildDataSource;
        QueryBuildRange         queryBuildRange;
        WHSShipmentTable        whsShipmentTable;

        q                       = this.getFirstQuery();
        queryBuildDataSource    = SysQuery::findOrCreateDataSource(q, tableNum(WHSShipmentTable));
        queryBuildRange         = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSShipmentTable, ShipmentId));

        if (this.parmArgs().parm())
        {
            queryBuildRange.value(queryValue(this.parmArgs().parm()));
        }
        else if (this.parmArgs().record() && this.parmArgs().dataset() == tableNum(WHSShipmentTable))
        {
            whsShipmentTable = this.parmArgs().record();
            queryBuildRange.value(whsShipmentTable.ShipmentId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initailzes the controller, sets the print management, assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        TMSCommercialInvoiceController tmsCommercialInvoiceController;

        if (_args.parm()
            || (_args.record() && _args.dataset() == tableNum(WHSShipmentTable)))
        {
            tmsCommercialInvoiceController = new TMSCommercialInvoiceController();
            tmsCommercialInvoiceController.parmArgs(_args);
            tmsCommercialInvoiceController.parmReportName(ssrsReportStr(TMSCommercialInvoice, Report));
            tmsCommercialInvoiceController.parmShowDialog(false);
            tmsCommercialInvoiceController.startOperation();
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>