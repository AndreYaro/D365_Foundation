<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SAFTInventoryAgingHelper_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.Collections;
/// <summary>
/// The <c>SAFTInventoryAgingHelper_W</c> calculates inventory balances on a particular dates.
/// </summary>
public class SAFTInventoryAgingHelper_W
{
    private InventPeriodAgingTmp_W inventPeriodAgingTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillInventoryPeriodBalances</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills <c>InventPeriodAgingTmp_W</c> of inventory balances in period or not zero as of end date.
    /// </summary>
    /// <param name = "_dateFrom">A start date of calculation period.</param>
    /// <param name = "_dateTo">An end date of calculation period.</param>
    /// <returns><c>InventPeriodAgingTmp_W</c> records.</returns>
    internal InventPeriodAgingTmp_W fillInventoryPeriodBalances(date _dateFrom, date _dateTo)
    {
        InventPeriodAgingTmp_W inventPeriodAgingTmpForPeriod;
        InventPeriodAgingTmp_W inventPeriodAgingTmpClosing;

        ttsbegin;
        inventPeriodAgingTmpClosing = this.getInventoryBalancesAsOfDate(_dateTo);

        inventPeriodAgingTmpForPeriod = this.getInventoryBalancesInPeriod(_dateFrom, _dateTo);

        inventPeriodAgingTmp = this.getInventoryBalancesInPeriodOrNotZero(inventPeriodAgingTmpForPeriod, inventPeriodAgingTmpClosing);
        ttscommit;

        return inventPeriodAgingTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventoryPeriodBalancesList</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills <c>InventPeriodAgingTmp_W</c> and returns it as an ArrayList
    /// of inventory balances in period or not zero as of end date.
    /// </summary>
    /// <param name = "_dateFrom">A start date of calculation period.</param>
    /// <param name = "_dateTo">An end date of calculation period.</param>
    /// <returns>An array list filled with <c>InventPeriodAgingTmp_W</c> records.</returns>
    [ERTableName(tableStr(InventPeriodAgingTmp_W))]
    public ArrayList getInventoryPeriodBalancesList(date _dateFrom, date _dateTo)
    {
        this.fillInventoryPeriodBalances(_dateFrom, _dateTo);

        ArrayList inventPeriodAgingTmps = new System.Collections.ArrayList();
        while select inventPeriodAgingTmp
        {
            inventPeriodAgingTmps.Add(inventPeriodAgingTmp.data());
        }

        return inventPeriodAgingTmps;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventoryBalancesAsOfDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills <c>InventPeriodAgingTmp_W</c> with inventory balances as of date.
    /// </summary>
    /// <param name = "_date">A calculation date.</param>
    /// <returns><c>InventPeriodAgingTmp_W</c> records.</returns>
    private InventPeriodAgingTmp_W getInventoryBalancesAsOfDate(date _date)
    {
        InventPeriodAgingTmp_W inventPeriodAgingTmpAsOfDate;
        InventValueReportView inventValueReportView;
        InventDim inventDim;
        InventTable inventTable;
        InventTableModule inventTableModule;
        WHSInventStatus inventStatus;

        insert_recordset inventPeriodAgingTmpAsOfDate (
            InventTableRefRecId,
            ItemId,
            ClosingQty,
            ClosingAmount,
            InventLocationId,
            InventStatusId,
            UOM)
            select RecId, ItemId from inventTable
                group by RecId, ItemId
            join sum(Qty), sum(Amount) from inventValueReportView
                where inventValueReportView.TransDate >= (_date + 1)
                      && inventValueReportView.TransDate <= dateMax()
                      && inventTable.ItemId == inventValueReportView.ItemId
            join InventLocationId, InventStatusId from inventDim
                group by InventLocationId, InventStatusId
                where inventDim.inventDimId == inventValueReportView.InventDimId
            join UnitId from inventTableModule
                group by UnitId
                where inventTableModule.ItemId == inventTable.ItemId
                      && inventTableModule.ModuleType == ModuleInventPurchSales::Invent;

        return inventPeriodAgingTmpAsOfDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventoryBalancesInPeriod</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills <c>InventPeriodAgingTmp_W</c> with inventory balances in period.
    /// </summary>
    /// <param name = "_dateFrom">A start date of calculation period.</param>
    /// <param name = "_dateTo">An end date of calculation period.</param>
    /// <returns><c>InventPeriodAgingTmp_W</c> records.</returns>
    private InventPeriodAgingTmp_W getInventoryBalancesInPeriod(date _dateFrom, date _dateTo)
    {
        InventPeriodAgingTmp_W inventPeriodAgingTmpInPeriod;
        InventValueReportView inventValueReportView;
        InventDim inventDim;
        InventTable inventTable;
        InventTableModule inventTableModule;
        WHSInventStatus inventStatus;

        insert_recordset inventPeriodAgingTmpInPeriod (
            InventTableRefRecId,
            ItemId,
            PeriodQty,
            PeriodAmount,
            InventLocationId,
            InventStatusId,
            UOM)
            select RecId, ItemId from inventTable
                group by RecId, ItemId
            join sum(Qty), sum(Amount) from inventValueReportView
                where inventValueReportView.TransDate >= _dateFrom
                      && inventValueReportView.TransDate <= _dateTo
                      && inventTable.ItemId == inventValueReportView.ItemId
            join InventLocationId, InventStatusId from inventDim
                group by InventLocationId, InventStatusId
                where inventDim.inventDimId == inventValueReportView.InventDimId
            join UnitId from inventTableModule
                group by UnitId
                where inventTableModule.ItemId == inventTable.ItemId
                      && inventTableModule.ModuleType == ModuleInventPurchSales::Invent;

        return inventPeriodAgingTmpInPeriod;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventoryBalancesInPeriodOrNotZero</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills <c>InventPeriodAgingTmp_W</c> with inventory balances in period or not zero as of date.
    /// </summary>
    /// <param name = "_inventPeriodAgingTmpInPeriod"><c>InventPeriodAgingTmp_W</c> records with inventory balances in period.</param>
    /// <param name = "_inventPeriodAgingTmpAsOfDate"><c>InventPeriodAgingTmp_W</c> records with inventory balances not zero as of date.</param>
    /// <returns><c>InventPeriodAgingTmp_W</c> records.</returns>
    private InventPeriodAgingTmp_W getInventoryBalancesInPeriodOrNotZero(InventPeriodAgingTmp_W _inventPeriodAgingTmpInPeriod, InventPeriodAgingTmp_W _inventPeriodAgingTmpAsOfDate)
    {
        InventPeriodAgingTmp_W inventPeriodAgingTmpLocal;
        InventValueReportView inventValueReportView;
        InventDim inventDim;
        InventTable inventTable;
        InventTableModule inventTableModule;
        WHSInventStatus inventStatus;

        insert_recordset inventPeriodAgingTmpLocal(
            InventTableRefRecId,
            ItemId,
            PeriodQty,
            PeriodAmount,
            InventLocationId,
            InventStatusId,
            UOM,
            ClosingQty,
            ClosingAmount)
            select InventTableRefRecId, ItemId, PeriodQty, PeriodAmount, InventLocationId, InventStatusId, UOM from _inventPeriodAgingTmpInPeriod
            join ClosingQty, ClosingAmount from _inventPeriodAgingTmpAsOfDate
                where _inventPeriodAgingTmpAsOfDate.InventTableRefRecId == _inventPeriodAgingTmpInPeriod.InventTableRefRecId
                      && _inventPeriodAgingTmpAsOfDate.InventLocationId == _inventPeriodAgingTmpInPeriod.InventLocationId
                      && _inventPeriodAgingTmpAsOfDate.InventStatusId == _inventPeriodAgingTmpInPeriod.InventStatusId
                      && _inventPeriodAgingTmpAsOfDate.UOM == _inventPeriodAgingTmpInPeriod.UOM;

        insert_recordset inventPeriodAgingTmpLocal(
            InventTableRefRecId,
            ItemId,
            ClosingQty,
            ClosingAmount,
            InventLocationId,
            InventStatusId,
            UOM)
            select InventTableRefRecId, ItemId, ClosingQty, ClosingAmount, InventLocationId, InventStatusId, UOM from _inventPeriodAgingTmpAsOfDate
                where _inventPeriodAgingTmpAsOfDate.ClosingQty != 0
                      || _inventPeriodAgingTmpAsOfDate.ClosingAmount != 0
            notexists join inventPeriodAgingTmpLocal
                where _inventPeriodAgingTmpAsOfDate.InventTableRefRecId == inventPeriodAgingTmpLocal.InventTableRefRecId
                      && _inventPeriodAgingTmpAsOfDate.InventLocationId == inventPeriodAgingTmpLocal.InventLocationId
                      && _inventPeriodAgingTmpAsOfDate.InventStatusId == inventPeriodAgingTmpLocal.InventStatusId
                      && _inventPeriodAgingTmpAsOfDate.UOM == inventPeriodAgingTmpLocal.UOM;

        insert_recordset inventPeriodAgingTmpLocal(
            InventTableRefRecId,
            ItemId,
            PeriodQty,
            PeriodAmount,
            InventLocationId,
            InventStatusId,
            UOM)
            select InventTableRefRecId, ItemId, PeriodQty, PeriodAmount, InventLocationId, InventStatusId, UOM from _inventPeriodAgingTmpInPeriod
            notexists join inventPeriodAgingTmpLocal
                where _inventPeriodAgingTmpInPeriod.InventTableRefRecId == inventPeriodAgingTmpLocal.InventTableRefRecId
                      && _inventPeriodAgingTmpInPeriod.InventLocationId == inventPeriodAgingTmpLocal.InventLocationId
                      && _inventPeriodAgingTmpInPeriod.InventStatusId == inventPeriodAgingTmpLocal.InventStatusId
                      && _inventPeriodAgingTmpInPeriod.UOM == inventPeriodAgingTmpLocal.UOM;

        return inventPeriodAgingTmpLocal;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>