<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxablePaymentsAnnualReport_AU</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// Microsoft internal use only.
/// </summary>
internal final class TaxablePaymentsAnnualReport_AU
{   
    private const str TPARFormatMappingGUID = '{783B000D-2D11-4112-9B08-05B4353C7433}';

    private TaxablePaymentsAnnualReportVersion_AU reportVersion;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmReportVersion</Name>
				<Source><![CDATA[
    internal TaxablePaymentsAnnualReportVersion_AU parmReportVersion(TaxablePaymentsAnnualReportVersion_AU _inputRecord = reportVersion)
    {
        reportVersion = _inputRecord;
        return reportVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    internal void run()
    {
        ERFormatMappingTable formatMappingTable = ERFormatMappingTable::findByGUID(str2Guid(TPARFormatMappingGUID));

        Query query = this.prepareQuery();
        ERIModelDefinitionParamsUIAction parameters = new ERModelDefinitionDatabaseContext().addQuery(query.dataSourceNo(1).table(), query);

        ERObjectsFactory::createFormatMappingRunByFormatMappingId(formatMappingTable.RecId, '', true)
            .withParameter(parameters)
            .withFileDestination(ERObjectsFactory::createFileDestinationBrowser())
            .withIntegrationPoint(new ERIntegrationPointFactory().WithTableRecordsIntegrationPoint(tableStr(TaxablePaymentsAnnualReportVersion_AU)).ToIntegrationPoint())
            .run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prepareQuery</Name>
				<Source><![CDATA[
    private Query prepareQuery()
    {
        Query ret = new Query();
        ret.addDataSource(tableNum(TaxablePaymentsAnnualReportVersion_AU))
            .addRange(fieldNum(TaxablePaymentsAnnualReportVersion_AU, RecId)).value(queryValue(reportVersion.RecId));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    internal static void main(Args _args)
    {
        TaxablePaymentsAnnualReport_AU report = new TaxablePaymentsAnnualReport_AU();
        report.parmReportVersion(_args.record());
        report.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>