<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxYearlyComExport_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

public class TaxYearlyComExport_IT extends RunBase implements ERIFormatPostProcessingTask
{
    private const int CurrentVersion = 1;
    private const int SysLastValuesCurrentVersion = 1;

    private RecId taxYearlyComId;
    private DialogField dialogFileName;
    private ERFileName filename;
    private Yr taxYearlyReportYear;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        const str ICL = '_ICL';

        Dialog dialog = super();

        if (!filename)
        {
            filename = strFmt('%1%2%3', CompanyInfo::find().CoRegNum, ICL, subStr(int2str(this.parmYear()), 3, 2));
        }

        dialog.addGroup("@SYS26552");

        dialogFileName = dialog.addFieldValue(extendedTypeStr(ERFileName), filename);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = true;

        filename = fileNameTrim(dialogFileName.value());

        if (!filename)
        {
            ret = checkFailed("@SYS18624");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFileName</Name>
				<Source><![CDATA[
    public ERFileName parmFileName(ERFileName _filename = filename)
    {
        filename = _filename;
        return filename;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTaxYearlyComId</Name>
				<Source><![CDATA[
    public RecId parmTaxYearlyComId(RecId _taxYearlyComId = taxYearlyComId)
    {
        taxYearlyComId = _taxYearlyComId;
        return taxYearlyComId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmYear</Name>
				<Source><![CDATA[
    public Yr parmYear(Yr _year = taxYearlyReportYear)
    {
        taxYearlyReportYear = _year;
        return taxYearlyReportYear;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        super();

        ERFormatMappingId formatMappingId = TaxParameters::find().TaxYearlyComERFormatMapping_IT;

        if (this.validateFormat(formatMappingId))
        {
            Query query = new Query();

            query.addDataSource(tableNum(TaxYearlyCom_IT))
                .addRange(fieldNum(TaxYearlyCom_IT, RecId))
                .value(queryValue(this.parmTaxYearlyComId()));

            ERObjectsFactory::createFormatMappingRunByFormatMappingId(formatMappingId, this.parmFileName())
                .withFileDestination(ERObjectsFactory::createFileDestinationBrowser())
                .withParameter(this.getERDataBaseContext())
                .withPostProcessingTask(this)
                .run();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateFormat</Name>
				<Source><![CDATA[
    private boolean validateFormat(ERFormatMappingId _formatMappingId)
    {
        boolean ret = true;

        if (!_formatMappingId)
        {
            // Error on function run. Format mapping is not specified in Sales tax parameters.
            str message = strfmt("@GLS110288" + "@SYS35672" + ' ' + "@SYS93945", "@AccountsReceivable:FormatMapping", "@SYS336146", "@SYS35672");
            error(message);
            ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>execute</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates report status.
    /// </summary>
    public void execute()
    {
        ttsbegin;

        TaxYearlyCom_IT taxYearlyComToUpdate = TaxYearlyCom_IT::find(this.parmTaxYearlyComId(), true);
        taxYearlyComToUpdate.ExportDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        taxYearlyComToUpdate.Exported = NoYes::Yes;
        taxYearlyComToUpdate.ExportedFile = filename;
        taxYearlyComToUpdate.update();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidForPacking</Name>
				<Source><![CDATA[
    /// <summary>
    /// Verifies if the object valid for packing.
    /// </summary>
    /// <returns>True if valid for packing; otherwise, return false.</returns>
    public boolean isValidForPacking()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidForPartiallyPacking</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checkes whether object is partially valid for packing.
    /// </summary>
    /// <returns>True if partially valid for packing; otherwise, return false.</returns>
    public boolean isValidForPartiallyPacking()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS28659";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (FeatureStateProvider::isFeatureEnabled(TaxYearlyCommunicationEnhancementFeature::instance()))
        {
            TaxYearlyComTaxPeriodExport_IT::main(_args);
            return;
        }

        if (!_args.record() || _args.dataset() != tablenum(TaxYearlyCom_IT))
        {
            throw error(strfmt("@SYS25516", TaxYearlyComExport_IT::description()));
        }

        TaxYearlyCom_IT taxYearlyCom = _args.record();

        TaxYearlyComExport_IT taxYearlyComExport = new TaxYearlyComExport_IT();
        taxYearlyComExport.parmTaxYearlyComId(taxYearlyCom.RecId);
        taxYearlyComExport.parmYear(taxYearlyCom.Year);

        if (taxYearlyComExport.prompt())
        {
            taxYearlyComExport.runOperation();
            taxYearlyComExport.refereshCallerDataSource(_args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>refereshCallerDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Refreshes the parent datasource of the caller form.
    /// </summary>
    /// <param name="_args">
    /// The arguments object that holds the reference to the caller form.
    /// </param>
    private void refereshCallerDataSource(Args _args)
    {
        common record = _args.record();
        FormDataSource ds = FormDataUtil::getFormDataSource(record);

        ds.reread();
        ds.refresh();
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveLast</Name>
				<Source><![CDATA[
    public void saveLast()
    {
        // Don't make the call to super as the #CurrentList and #SysLastValues lists are different,
        // requiring that we have specialized logic for the SysLastValues implementation.
        //super();

        // The following fields must be maintained to mimic the functionality of the overriden method.
        inGetSaveLast   = true;

        // Persist the pertinent values to the SysLastValue table.
        xSysLastValue::putValue(this.packSysLastValues(),
                                this.lastValueDataAreaId(),
                                this.lastValueUserId(),
                                this.lastValueType(),
                                this.lastValueElementName(),
                                this.lastValueDesignName());

        // The following fields must be maintained to mimic the functionality of the overriden method.
        inGetSaveLast = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLast</Name>
				<Source><![CDATA[
    public void getLast()
    {
        container packedValues;
        getLastCalled   = true;
        inGetSaveLast   = true;

        // Restore the pertinent values from the SysLastValue table.
        packedValues = xSysLastValue::getValue(this.lastValueDataAreaId(),
                                               this.lastValueUserId(),
                                               this.lastValueType(),
                                               this.lastValueElementName(),
                                               this.lastValueDesignName());

        this.unpackSysLastValues(packedValues);

        // The following fields must be maintained to mimic the functionality of the overriden method.
        inGetSaveLast = false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpackSysLastValues</Name>
				<Source><![CDATA[
    public void unpackSysLastValues(container _packedValues)
    {
        Version version = RunBase::getVersion(_packedValues);

        switch (version)
        {
            case SysLastValuesCurrentVersion:
                [version, fileName] = _packedValues;
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>packSysLastValues</Name>
				<Source><![CDATA[
    public container packSysLastValues()
    {
        return [SysLastValuesCurrentVersion, fileName];
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        container packed = [CurrentVersion, fileName];
        packed = this.appendVariableToPack(packed, varStr(taxYearlyComId), taxYearlyComId);
        return packed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case CurrentVersion:
                [version, fileName] = _packedClass;
                taxYearlyComId = this.extractPackedVariable(_packedClass, varStr(taxYearlyComId));
                break;

            default:
                return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>appendVariableToPack</Name>
				<Source><![CDATA[
    private container appendVariableToPack(container _pack, str _variableName, anytype _value)
    {
        XppPrePostArgs packArgs = this.createXppPrePostArgsWithPack(_pack);
        SysPackExtensions::pack(packArgs, this.getVariablePackKey(_variableName), [_value]);
        return packArgs.getReturnValue();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createXppPrePostArgsWithPack</Name>
				<Source><![CDATA[
    private XppPrePostArgs createXppPrePostArgsWithPack(container _pack)
    {
        XppPrePostArgs prePostArgs = new XppPrePostArgs(_pack, '',  XppEventHandlerCalledWhen::Post);
        prePostArgs.setReturnValue(_pack);
        return prePostArgs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVariablePackKey</Name>
				<Source><![CDATA[
    private ClassName getVariablePackKey(str _variableName)
    {
        return classStr(TaxYearlyComExport_IT) + '.' + _variableName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>extractPackedVariable</Name>
				<Source><![CDATA[
    private anytype extractPackedVariable(container _pack, str _variableName)
    {
        anytype value;
        [value] = SysPackExtensions::unpack(this.createXppPrePostArgsWithPack(_pack), this.getVariablePackKey(_variableName));
        return value;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERDataBaseContext</Name>
				<Source><![CDATA[
    private ERModelDefinitionDatabaseContext getERDataBaseContext()
    {
        ERModelDefinitionDatabaseContext databaseContext = new ERModelDefinitionDatabaseContext();

        Query query = new Query();
        query.addDataSource(tableNum(TaxYearlyCom_IT))
            .addRange(fieldNum(TaxYearlyCom_IT, RecId))
            .value(queryValue(this.parmTaxYearlyComId()));
        databaseContext.addQuery(tableNum(TaxYearlyCom_IT), query);

        databaseContext.addTemporaryTable(this.getTmpTaxReportLine());

        return databaseContext;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTmpTaxReportLine</Name>
				<Source><![CDATA[
    private TaxReportLine_IT getTmpTaxReportLine()
    {
        TaxReportLine_IT taxReportLine, tmpTaxReportLine;
        LineNum lineNum, fieldValueLength;
        const int WriterNonPosValueLength = 16;
        const str PlusSign = '+';
        int AdditionalValueLength = WriterNonPosValueLength - strLen(PlusSign);

        tmpTaxReportLine.setTempDB();

        Query query = new Query();
        QueryBuildDataSource qbdsTaxReportLine = query.addDataSource(tableNum(TaxReportLine_IT));
        qbdsTaxReportLine.addSortField(fieldNum(TaxReportLine_IT, LineNum), SortOrder::Ascending);

        QueryBuildDataSource qbdsTaxReportHeader = qbdsTaxReportLine.addDataSource(tableNum(TaxReportHeader_IT));
        qbdsTaxReportHeader.relations(true);
        qbdsTaxReportHeader.joinMode(JoinMode::ExistsJoin);

        QueryBuildDataSource qbdsTaxYearlyComReport = qbdsTaxReportHeader.addDataSource(tableNum(TaxYearlyComReport_IT));
        qbdsTaxYearlyComReport.relations(true);
        qbdsTaxYearlyComReport.joinMode(JoinMode::ExistsJoin);

        QueryBuildDataSource qbdsTaxYearlyCom = qbdsTaxYearlyComReport.addDataSource(tableNum(TaxYearlyCom_IT));
        qbdsTaxYearlyCom.relations(true);
        qbdsTaxYearlyCom.joinMode(JoinMode::ExistsJoin);
        qbdsTaxYearlyCom.addRange(fieldNum(TaxYearlyCom_IT, RecId)).value(queryValue(this.parmTaxYearlyComId()));

        QueryRun queryRun = new QueryRun(query);
        while (queryRun.next())
        {
            lineNum++;
            taxReportLine = queryRun.get(tableNum(TaxReportLine_IT));
            tmpTaxReportLine.data(taxReportLine);
            tmpTaxReportLine.TaxReportHeader = lineNum; // to skip the index constraints
            tmpTaxReportLine.LineNum = lineNum;

            fieldValueLength = strLen(taxReportLine.FieldValue);

            if (taxReportLine.FieldFormat == TaxReportFieldFormat_IT::AN
                && fieldValueLength > WriterNonPosValueLength)
            {
                tmpTaxReportLine.FieldValue = subStr(taxReportLine.FieldValue, 1, WriterNonPosValueLength);
                tmpTaxReportLine.insert();

                for (int position = WriterNonPosValueLength + 1; position <= fieldValueLength; position += AdditionalValueLength)
                {
                    lineNum++;
                    tmpTaxReportLine.TaxReportHeader = lineNum;
                    tmpTaxReportLine.LineNum = lineNum;
                    tmpTaxReportLine.FieldValue = PlusSign + subStr(taxReportLine.FieldValue, position, AdditionalValueLength);
                    tmpTaxReportLine.insert();
                }
            }
            else
            {
                tmpTaxReportLine.insert();
            }
        }

        return tmpTaxReportLine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>