<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TransactionTextContextCust</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TransactionTextContextCust</c> class enables additional parameter value functionality for
/// the documents of the <c>CustCreditNoteLedger</c>, <c>CustInvoiceCust</c>, and
/// <c>CustInvoiceLedger</c> type.
/// </summary>
[
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, CustCreditNoteCust)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, CustCreditNoteLedger)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, CustInvoiceCust)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, CustInvoiceLedger))
]
class TransactionTextContextCust extends TransactionTextContext
{
    CustInvoiceJour     custInvoiceJour;
    CustInvoiceTable    custInvoiceTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSupportedTables</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides a list of tables that are supported for use with a specified transaction type.
    /// </summary>
    /// <returns>
    /// A container that contains a collection of <c>TableId</c> values of tables that are supported by a
    /// specified transaction type.
    /// </returns>
    public container getSupportedTables()
    {
        container   tableList;

        tableList += tableNum(CustInvoiceJour);
        tableList += tableNum(CustInvoiceTable);

        return tableList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the parameter variable of an object to contain the records that are used with a specified
    /// transaction type.
    /// </summary>
    /// <remarks>
    /// This method must be called before the table fields and values are mapped for the transaction text
    /// setup parameters.
    /// </remarks>
    public void setParameters()
    {
        parameters = [custInvoiceJour, custInvoiceTable];
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTableBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set a particular table buffer for a specified transaction type.
    /// </summary>
    /// <param name="_common">
    /// The <c>Common</c> record that is set based on the <c>TableId</c> value of the buffer.
    /// </param>
    /// <returns>
    /// true if the table is supported and the buffer is saved; otherwise, false.
    /// </returns>
    /// <remarks>
    /// The <c>Common</c> record is cast to the appropriate table through switch logic. This method can be
    /// called repeatedly to assign multiple buffers.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    /// The table is not supported by this transaction type.
    /// </exception>
    public boolean setTableBuffer(Common _common)
    {
        boolean tableBufferSet;

        switch (_common.TableId)
        {
            case tableNum(CustInvoiceJour):
                custInvoiceJour = _common as CustInvoiceJour;
                tableBufferSet = true;
                break;
            case tableNum(CustInvoiceTable):
                custInvoiceTable = _common as CustInvoiceTable;
                tableBufferSet = true;
                break;
        }

        return tableBufferSet;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>