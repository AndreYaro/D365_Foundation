<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailTopProductsReportDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.Data;
using ACM = Microsoft.Dynamics.AX.Analytics.CalculationModel;

/// <summary>
/// The <c>RetailTopProductsReportDataProvider</c> class is the
/// data provider class for retail top products report.
/// </summary>
[SRSReportParameterAttribute(classstr(RetailReportParametersDataContract))]
class RetailTopProductsReportDataProvider extends RetailSrsReportDataProviderBase
{
    RetailTopProductsReportTable table;
    RetailReportParametersDataContract dc;
    ACM.NumericExpression grossProfitMarginPercentage;
    ACM.NumericExpression grossProfitMargin;
    ACM.DynamicMeasureDefinition amount;
    ACM.DynamicMeasureDefinition cogs;
    ACM.DynamicMeasureDefinition quantity;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize measure definitions.
    /// </summary>
    public void new()
    {
        super();

        if (RetailTopProductsReportUsesStagingTableFlightV2::instance().isEnabled())
        {

            amount = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailTopProductsReportStagingTable),
                    fieldStr( RetailTopProductsReportStagingTable, LineAmountMST),
                    ACM.Aggregation::Sum
                );

            cogs = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailTopProductsReportStagingTable),
                    fieldStr( RetailTopProductsReportStagingTable, COGS),
                    ACM.Aggregation::Sum
                );

            quantity = new ACM.DynamicMeasureDefinition(
                tableStr(RetailTopProductsReportStagingTable),
                fieldStr( RetailTopProductsReportStagingTable, Qty),
                ACM.Aggregation::Sum
            );
        }
        else
        {
            amount = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailTopProductsView),
                    fieldStr( RetailTopProductsView, LineAmountMST),
                    ACM.Aggregation::Sum
                );

            cogs = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailTopProductsView),
                    fieldStr( RetailTopProductsView, COGS),
                    ACM.Aggregation::Sum
                );

            quantity = new ACM.DynamicMeasureDefinition(
                tableStr(RetailTopProductsView),
                fieldStr( RetailTopProductsView, Qty),
                ACM.Aggregation::Sum
            );
        }

        grossProfitMargin = amount.Subtract(cogs);

        ACM.LogicalExpression isAmountZero = amount.CompareEquals(0);

        grossProfitMarginPercentage = new ACM.If(
                isAmountZero,
                ACM.Null::ConstValue,
                grossProfitMargin.Divide(amount));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    [SRSReportDataSetAttribute(tableStr(RetailTopProductsReportTable))]
    public RetailTopProductsReportTable getTable()
    {
        select table;
        return table;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process the report.
    /// </summary>
    public void processReport()
    {
        dc = this.parmDataContract();
        this.populateData();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Run the query and insert data into temp table.
    /// </summary>
    void populateData()
    {
        try
        {
            SysQueryRun::ExecuteInManagedStack(true);

            Query q = new Query();

            if (RetailTopProductsReportUsesStagingTableFlightV2::instance().isEnabled())
            {
                QueryBuildDataSource  qds = q.addDataSource(tableNum(RetailTopProductsReportStagingTable));

                // Add fields.
                qds.addSelectionFieldWithAlias('ProductId', fieldNum(RetailTopProductsReportStagingTable, ItemId));
                qds.addSelectionFieldWithAlias('ProductName', fieldNum(RetailTopProductsReportStagingTable, ProductName));
                qds.addCalculationField(amount, 'LineAmountMST' );
                qds.addCalculationField(cogs,'COGS');
                qds.addCalculationField(quantity, 'Quantity');
                qds.addCalculationField(grossProfitMargin, 'GrossProfitMargin');
                qds.addCalculationField(grossProfitMarginPercentage, 'GrossProfitMarginPercentage');
                qds.addGroupByField(fieldNum(RetailTopProductsReportStagingTable, ItemId));
                qds.addGroupByField(fieldNum(RetailTopProductsReportStagingTable, ProductName));
                this.AddSortOrder(qds);
                q.topRows(dc.parmTopCount());

                //Add ranges.
                QueryBuildRange dateTimeRange = qds.addRange(fieldnum(RetailTopProductsReportStagingTable, InvoiceDate));
                dateTimeRange.value(this.buildDateRangeForSpecifiedField(dc.parmFromDate(), dc.parmToDate(), fieldStr(RetailTopProductsReportStagingTable, InvoiceDate)));
            }
            else
            {
                this.InsertChannelsToTmpTable(dc.parmHierarchyRelationId());

                QueryBuildDataSource  qds = q.addDataSource(tableNum(RetailTopProductsView));

                // Add fields.
                qds.addSelectionFieldWithAlias('ProductId', fieldNum(RetailTopProductsView, ItemId));
                qds.addSelectionFieldWithAlias('ProductName', fieldNum(RetailTopProductsView, ProductName));
                qds.addCalculationField(amount, 'LineAmountMST' );
                qds.addCalculationField(cogs,'COGS');
                qds.addCalculationField(quantity, 'Quantity');
                qds.addCalculationField(grossProfitMargin, 'GrossProfitMargin');
                qds.addCalculationField(grossProfitMarginPercentage, 'GrossProfitMarginPercentage');
                qds.addGroupByField(fieldNum(RetailTopProductsView, ItemId));
                qds.addGroupByField(fieldNum(RetailTopProductsView, ProductName));
                this.AddSortOrder(qds);
                q.topRows(dc.parmTopCount());

                //Add ranges.
                QueryBuildRange dateTimeRange = qds.addRange(fieldnum(RetailTopProductsView, InvoiceDate));
                dateTimeRange.value(this.buildDateRangeForSpecifiedField(dc.parmFromDate(), dc.parmToDate(), fieldStr(RetailTopProductsView, InvoiceDate)));
            
                //Add join with RetailReportChannelTable.
                this.JoinChannelFilter(qds, fieldNum(RetailTopProductsView, RetailChannel));
            }

            //Build Mappings
            Map m = this.getMappings();

            // run the query and insert the query results into the form datasource
            QueryRun::runAndPopulate(q, table, m);

            this.ClearChannelFilterTable();
        }
        finally
        {
            SysQueryRun::ExecuteInManagedStack(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMappings</Name>
				<Source><![CDATA[
    private Map getMappings()
    {
        Map m = new Map(Types::String, Types::String);
        m.insert('ProductId', fieldStr(RetailTopProductsReportTable, ProductNumber ));
        m.insert('ProductName', fieldStr(RetailTopProductsReportTable, ProductName));
        m.insert('LineAmountMST', fieldStr(RetailTopProductsReportTable, Amount));
        m.insert('COGS', fieldStr(RetailTopProductsReportTable, COGS));
        m.insert('Quantity', fieldStr(RetailTopProductsReportTable, Quantity));
        m.insert('GrossProfitMargin', fieldStr(RetailTopProductsReportTable, Margin));
        m.insert('GrossProfitMarginPercentage', fieldStr(RetailTopProductsReportTable, Percentage));

        return m;
    }

]]></Source>
			</Method>
			<Method>
				<Name>AddSortOrder</Name>
				<Source><![CDATA[
    private void AddSortOrder(QueryBuildDataSource qbd)
    {
        switch (dc.parmOrderBy())
        {
            case RetailReportOrderBy::Amount:
                qbd.addOrderByCalculationField(amount, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Profit:
                qbd.addOrderByCalculationField(grossProfitMargin, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Quantity:
                qbd.addOrderByCalculationField(quantity, SortOrder::Descending);
                break;
            case RetailReportOrderBy::ProfitPercentage:
                qbd.addOrderByCalculationField(grossProfitMarginPercentage, SortOrder::Descending);
                qbd.addOrderByCalculationField(grossProfitMargin, SortOrder::Descending);
                break;
            default:
                throw Global::error("@Retail:RetailReportOrderByNotSupported");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>