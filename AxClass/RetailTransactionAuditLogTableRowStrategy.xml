<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailTransactionAuditLogTableRowStrategy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailTransactionAuditLogStrategy</c> class defines the persistency strategy for the Edit/Audit data backed by
/// a table that stores the following tuple: (RefTableId, RefRecId, FieldName, OldValue, NewValue)
/// </summary>
internal class RetailTransactionAuditLogTableRowStrategy extends RetailTransactionAuditLogStrategy
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a <c>RetailTransactionAuditLogTableRowStrategy</c>.
    /// </summary>
    /// <param name = "_targetTable">The record being audited.</param>
    /// <returns>An instance of <c>RetailTransactionAuditLogTableRowStrategy</c>.</returns>
    public static RetailTransactionAuditLogTableRowStrategy construct(Common _targetTable)
    {
        RetailTransactionAuditLogTableRowStrategy instance = new RetailTransactionAuditLogTableRowStrategy();
        
        instance.targetTable = _targetTable;
        
        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>persist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the audit information to the database.
    /// </summary>
    /// <param name = "_listOfChanges">The list of changes to be saved.</param>
    public void persist(List _listOfChanges)
    {
        RecordInsertList listOfRecords = new RecordInsertList(tableNum(RetailTransactionAuditLogTable));

        ListEnumerator enumerator = _listOfChanges.getEnumerator();

        while (enumerator.moveNext())
        {
            RetailTransactionAuditLogField auditLogField = enumerator.current();
                
            this.writeAuditLogField(listOfRecords, auditLogField);
        }

        listOfRecords.insertDatabase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeAuditLogField</Name>
				<Source><![CDATA[
    private void writeAuditLogField(RecordInsertList _listOfRecords, RetailTransactionAuditLogField _auditLogField)
    {
        RetailTransactionAuditLogTable log;

        log.RefTableId = _auditLogField.getTableId();
        log.RefRecId = _auditLogField.getRecId();
        log.FieldName = _auditLogField.getFieldName();
        log.OldValue = _auditLogField.getConvertedOldValue();
        log.NewValue = _auditLogField.getConvertedNewValue();

        _listOfRecords.add(log);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>