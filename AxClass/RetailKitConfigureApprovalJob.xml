<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailKitConfigureApprovalJob</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>RetailKitConfigureApprovalJob</c> is a dialog class used to schedule kit configure approval job.
/// </summary>
class RetailKitConfigureApprovalJob Extends RunBaseBatch
{
    RetailKitRecId retailKitRecId;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
        retailKitRecId
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Serializes the current instance of the <c>RetailKitConfigureApprovalJob</c>.
    /// </summary>
    /// <returns>A container that contains the current instance of <c>RetailKitConfigureApprovalJob</c>.</returns>
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deserializes the <paramref name="_packedClass"/> parameter to the current instance of the <c>RetailKitConfigureApprovalJob</c> class.
    /// </summary>
    /// <param name = "packedClass">The container from which to deserialize the instance.</param>
    /// <returns>true if deserialization was successful; otherwise, false.</returns>
    public boolean unpack(container packedClass)
    {
        Version version = RunBase::getVersion(packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRetailKitRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the assortment identifier to process. If not set, nothing is processed.
    /// </summary>
    /// <param name="_retailKitRecId">
    /// The record identifier of the assortment to process.
    /// </param>
    /// <returns>
    /// The record identifier of the assortment being processed. 
    /// </returns>
    public RetailKitRecId parmRetailKitRecId(RetailKitRecId _retailKitRecId = retailKitRecId)
    {
        retailKitRecId = _retailKitRecId;;
        return retailKitRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the batch job operation to approve the retail kit configuraiton.
    /// </summary>
    public void run()
    {
        RetailKit retailKit;

        ttsbegin;

        retailKit = RetailKit::find(retailKitRecId, true);

        if (retailKit.RecId)
        {
            RetailKit::approveKit(retailKit);
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the description of the class.
    /// </summary>
    /// <returns>The description of the class.</returns>
    public static ClassDescription description()
    {
        return "@Retail:KitConfigureApprovalJob";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// The main method which initializes the call for the other methods.
    /// </summary>
    /// <param name="_args">
    /// Retail kit scheduler job record.
    /// </param>
    /// <param name="_supressDialog">
    /// Boolean value, which shows suppress dialog state.
    /// </param>
    public static void main(Args _args, boolean _supressDialog = false)
    {
        RetailKitConfigureApprovalJob scheduler = new RetailKitConfigureApprovalJob();

        RetailKit retailKit = _args.record();

        if (retailKit)
        {
            scheduler.parmRetailKitRecId(retailKit.RecId);
        }

        if (!_supressDialog)
        {
            if (scheduler.prompt())
            {
                scheduler.runOperation();
            }
        }
        else
        {
            scheduler.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns a value indicating if the <c>RetailAssortmentLookupJob</c>
    /// class can be run in a new session.
    /// </summary>
    /// <returns>true if the the class <c>RetailAssortmentLookupJob</c> can
    /// be run in a new session; otherwise, false.</returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>