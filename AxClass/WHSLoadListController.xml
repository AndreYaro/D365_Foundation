<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSLoadListController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSLoadListController</c> class is used to run print management of the <c>WHSLoadList</c> report.
/// </summary>
class WHSLoadListController extends SrsPrintMgmtController implements BatchRetryable
{
    private static const str CurUserIdParameterName = 'CurrentUserId';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        this.setRanges();

        SrsReportRdlDataContract rdlContract = this.parmReportContract().parmRdlContract();
        if (rdlContract.parameterExists(WHSLoadListController::CurUserIdParameterName))
        {
            rdlContract.getParameter(WHSLoadListController::CurUserIdParameterName).setValueTyped(curUserId());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPrintMgmt</Name>
				<Source><![CDATA[
    protected void runPrintMgmt()
    {
        printMgmtReportRun = PrintMgmtReportRun::construct(PrintMgmtHierarchyType::WHS, PrintMgmtNodeType::WHS, PrintMgmtDocumentType::WHSLoadList);

        printMgmtReportRun.parmReportRunController(this);

        printMgmtReportRun.load(this.parmArgs().record(), this.parmArgs().record(), Global::currentUserLanguage());

        this.outputReports();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    private void setRanges()
    {
        Query                q                    = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = SysQuery::findOrCreateDataSource(q, tableNum(WHSLoadTable));
        QueryBuildRange      queryBuildRange      = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSLoadTable, LoadId));

        if (this.parmArgs().parm())
        {
            queryBuildRange.value(queryValue(this.parmArgs().parm()));
        }
        else if (this.parmArgs().record() && this.parmArgs().dataset() == tableNum(WHSLoadTable))
        {
            WHSLoadTable loadTable = this.parmArgs().record();
            queryBuildRange.value(loadTable.LoadId);
        }

        if (!SysQuery::queryHasRangesOrFilters(q))
        {
            SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSLoadTable, LoadStatus)).value(SysQuery::valueNot(WHSLoadStatus::Shipped));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>