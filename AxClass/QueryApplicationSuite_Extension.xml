<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>QueryApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>QueryApplicationSuite_Extension</c> is an extension class for the <c>Query</c> class.
/// </summary>
[ExtensionOf(classStr(Query))]
final class QueryApplicationSuite_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>showInForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Shows the query's ranges in a form.
    /// </summary>
    /// <param name = "_containingControl">The form group control that will contain the ranges.</param>
    /// <param name = "_maxControls">Maximum number of controls to add.</param>
    /// <returns>true if range controls are shown; otherwise, false.</returns>
    /// <remarks>Unused controls will be removed when this method is called multiple times, e.g. if a datasource or range is removed from the query.</remarks>
    public boolean showInForm(FormGroupControl _containingControl, int _maxControls = 10)
    {
        boolean result = false;

        this.removeControlsInGroup(_containingControl);

        if (! this.dataSourceCount())
            return result;

        for (int curDataSource=1; curDataSource <= this.dataSourceCount(); curDataSource++)
        {
            QueryBuildDataSource queryBuildDataSource = this.dataSourceNo(curDataSource);
            this.showQBDSInForm(_containingControl, queryBuildDataSource, _maxControls);
            result = true;
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeControlsInGroup</Name>
				<Source><![CDATA[
    private void removeControlsInGroup(FormGroupControl _groupControl)
    {
        FormRun formRun = _groupControl.formRun();

        while (_groupControl.controlCount())
        {
            //Remove the top-most child control
            FormControl control = _groupControl.controlNum(1);
            formRun.design().removeControl(control.id());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQBDSInForm</Name>
				<Source><![CDATA[
    private void showQBDSInForm(FormGroupControl _containingControl, QueryBuildDataSource _qbds, int _maxControls)
    {
        FormRun formRun = _containingControl.formRun();
        FormGroupControl formGroup = _containingControl.addControl(FormControlType::Group, strFmt('%1_group%2', classStr(QueryApplicationSuite_Extension), _qbds.name()));
        
        SysDictTable sysDictTable = new SysDictTable(_qbds.table());
        formGroup.caption(sysDictTable.label());
        formGroup.columns(1);
        formGroup.visible(true);

        int rangeCount = _qbds.rangeCount();
        int fieldCount = 1;
        int currentRange = 1;

        while (fieldCount <= _maxControls && currentRange <= rangeCount)
        {
            QueryBuildRange queryBuildRange = _qbds.range(currentRange);

            if (queryBuildRange.status() != RangeStatus::Hidden &&
                queryBuildRange.value())
            {
                SysDictField sysDictField = new SysDictField(queryBuildRange.table(), queryBuildRange.field());
                FormStringControl formField = formGroup.addControl(FormControlType::String, strFmt('%1_field%2_%3', classStr(QueryApplicationSuite_Extension), _qbds.name(), currentRange));

                formField.extendedDataType(sysDictField.typeId());
                formField.label(sysDictField.label());
                formField.text(queryBuildRange.value());
                formField.visible(true);
                formField.allowEdit(false);

                fieldCount++;
            }

            currentRange++;
        }

        _containingControl.columnsValue(_containingControl.controlCount());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>