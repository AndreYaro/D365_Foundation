<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventNonConformanceApprove</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventNonConformanceApprove</c> class marks a nonconformance as approved.
/// </summary>
class InventNonConformanceApprove extends InventNonConformanceUpdate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Marks the nonconformance as approved.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    ///    The nonconformance either does not exist or is already approved.
    /// </exception>
    public void run()
    {      
        super();
        ttsbegin;

        InventNonConformanceTable   inventNonConformanceTable;
        int rowCount;
        while select forupdate inventNonConformanceTable
            where inventNonConformanceTable.InventNonConformanceID        == inventNonConformanceID
                && inventNonConformanceTable.InventNonConformanceApproval != InventNonConformanceApproval::Approved
        {
            inventNonConformanceTable.InventNonConformanceApproval = InventNonConformanceApproval::Approved;
            inventNonConformanceTable.update();
            rowCount++;
        }

        if (rowCount != 1)
        {
            throw error(strFmt("@SYS114045", inventNonConformanceID));
        }
        InventNonConformanceHistory::insertApprove(inventNonConformanceID, worker);

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates that the nonconformance exists and is not already approved.
    /// </summary>
    /// <param name="calledFrom">
    ///    This parameter is not used in this class. See the base class, <c>RunBase</c>, for a description.
    /// </param>
    /// <returns>
    ///    true if the validation succeeded; otherwise, false.
    /// </returns>
    public boolean validate(Object calledFrom = null)
    {
        boolean ret = super(calledFrom);

        if (inventNonConformanceID)
        {
            InventNonConformanceTable inventNonConformanceTable = InventNonConformanceTable::find(inventNonConformanceID);
            if (!inventNonConformanceTable
              || inventNonConformanceTable.InventNonConformanceApproval == InventNonConformanceApproval::Approved)
            {
                return checkFailed(strFmt("@SYS114045", inventNonConformanceID));
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventNonConformanceApprove construct()
    {
        return new InventNonConformanceApprove();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static ClassDescription description()
    {
        return "@SYS114055";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        InventNonConformanceApprove inventNonConformanceApprove = InventNonConformanceApprove::construct();

        inventNonConformanceApprove.initFromArgs(args);

        if (Box::yesNo("@SYS114052", DialogButton::No) == DialogButton::Yes)
        {
            inventNonConformanceApprove.runOperation();

            if (args && args.record() && args.dataset() == tableNum(InventNonConformanceTable))
            {
                InventNonConformanceUpdate::refreshCaller(args.record());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
