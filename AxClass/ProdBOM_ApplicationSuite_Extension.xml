<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdBOM_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(ProdBOM))]
internal final class ProdBOM_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setInventDimIdFromInventDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates wms location for inventory dimensions record, if required
    /// </summary>
    /// <param name="_inventDim">
    /// Record buffer to be updated
    /// </param>
    public void setInventDimIdFromInventDim(InventDim _inventDim)
    {
        WMSLocationId wmsLocationid = _inventDim.wMSLocationId;

        if (!wmsLocationid && InventMovement::enabledSplittingByGTD_RU())
        {
            // define location from resource group
            RouteId     routeId = this.prodTable().RouteId;
            ItemId      itemId  = this.prodTable().ItemId;

            Route       route       = Route::findFirst(routeId); 
            RouteOpr    routeOpr    = RouteOpr::find(route.OprId, TableGroupAll::Table, itemId, '', RouteAll::Route, routeId, _inventDim.InventSiteId);

            WrkCtrActivityRequirementSet    activitySet;
            WrkCtrRouteOprActivity          routeOprActivity;
            WrkCtrResourceGroup             resourceGroup;

            while select activitySet
                exists join routeOprActivity
                    where routeOprActivity.Activity             == activitySet.Activity
                       && routeOprActivity.RouteOpr             == routeOpr.RecId
                       && routeOprActivity.RouteOprDataAreaId   == routeOpr.DataAreaId
            {
                resourceGroup = activitySet.resourceGroupRequirement();
                if (resourceGroup.InventUseInputLocationForFormulaBOM && resourceGroup.InputInventLocationId == _inventDim.InventLocationId)
                {
                    wmsLocationId = resourceGroup.InputWMSLocationId;
                    break;
                }
            }

            if (!wmsLocationId)
            {
                // define by default location from warehouse
                InventLocation  inventLocation = _inventDim.inventLocation();
                if (inventLocation.InventUseDefaultProductionLocationForFormulaBOM)
                {
                    wmsLocationId = inventLocation.DefaultProductionInputLocation;
                }
            }
        }

        _inventDim.wMSLocationId = wmsLocationid;
        
        next setInventDimIdFromInventDim(_inventDim);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>