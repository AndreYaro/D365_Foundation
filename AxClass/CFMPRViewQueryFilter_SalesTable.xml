<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPRViewQueryFilter_SalesTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class sets the payment request data source filters based upon the filter record SalesTable.
/// </summary>
[CFMPaymentRequestSourceAttribute(tableStr(SalesTable))]
public class CFMPRViewQueryFilter_SalesTable extends CFMPRViewQueryFilter
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>filter</Name>
				<Source><![CDATA[
    protected void filter()
    {
        QueryBuildDataSource paymentRequestDataSource = dataSourceQuery.dataSourceTable(tableNum(CFMPaymentRequestTable));

        if (paymentRequestDataSource)
        {
            paymentRequestDataSource.firstOnly(false);
            paymentRequestDataSource.clearDynalinks();
            paymentRequestDataSource.clearRanges();

            QueryBuildRange qbr = paymentRequestDataSource.addRange(fieldNum(CFMPaymentRequestTable, SourceType));
            qbr.value(queryValue(CFMPaymentRequestSourceType::CustInvoice));

            QueryBuildDataSource qbds = paymentRequestDataSource.addDataSource(tableNum(CustTrans));

            qbds.addLink(fieldNum(CFMPaymentRequestTable, HeaderRefRecId), fieldNum(CustTrans, RecId));

            qbds = qbds.addDataSource(tableNum(CustInvoiceJour));

            qbds.addLink(fieldNum(CustTrans, Invoice), fieldNum(CustInvoiceJour, InvoiceId));
            qbds.addLink(fieldNum(CustTrans, Voucher), fieldNum(CustInvoiceJour, LedgerVoucher));
            qbds.addLink(fieldNum(CustTrans, TransDate), fieldNum(CustInvoiceJour, InvoiceDate));
            qbds.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustInvoiceJour, InvoiceAccount));

            qbds = qbds.addDataSource(tableNum(CustInvoiceSalesLink));

            qbds.addLink(fieldNum(CustInvoiceJour, SalesId), fieldNum(CustInvoiceSalesLink, SalesId));
            qbds.addLink(fieldNum(CustInvoiceJour, InvoiceId), fieldNum(CustInvoiceSalesLink, InvoiceId));
            qbds.addLink(fieldNum(CustInvoiceJour, InvoiceDate), fieldNum(CustInvoiceSalesLink, InvoiceDate));
            qbds.joinMode(JoinMode::ExistsJoin);

            qbr = qbds.addRange(fieldNum(CustInvoiceSalesLink, OrigSalesId));

            SalesTable salesTable = filterRecord;
            qbr.value(queryValue(salesTable.SalesId));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>