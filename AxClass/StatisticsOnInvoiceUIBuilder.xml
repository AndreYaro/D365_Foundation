<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>StatisticsOnInvoiceUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>StatisticsOnInvoiceUIBuilder</c> class is a UI builder for statistics on invoices calculation dialog.
/// </summary>
public class StatisticsOnInvoiceUIBuilder extends SysOperationAutomaticUIBuilder
{
    private FormStringControl postingProfilesRecIdsControl;
    private FormStringControl vendGroupIdsRecIdsControl;
    private const str PostingProfilesRecIdsControlName = 'postingProfilesRecIdsControl';
    private const str VendGroupIdsRecIdsControlName = 'vendGroupIdsRecIdsControl';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        super();

        StatisticsOnInvoiceDataContract contract = this.dataContractObject();

        DialogField dialogFieldDateFrom = this.bindInfo().getDialogField(contract, methodStr(StatisticsOnInvoiceDataContract, parmDateFrom));
        dialogFieldDateFrom.fieldControl().mandatory(true);

        DialogField dialogFieldDateTo = this.bindInfo().getDialogField(contract, methodStr(StatisticsOnInvoiceDataContract, parmDateTo));
        dialogFieldDateTo.fieldControl().mandatory(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    public void postRun()
    {
        super();

        this.initPostingProfilesDialogField();
        this.initVendGroupIdsDialogField();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initPostingProfilesDialogField</Name>
				<Source><![CDATA[
    private void initPostingProfilesDialogField()
    {
        DialogField postingProfilesField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(StatisticsOnInvoiceDataContract, parmPostingProfiles));
        postingProfilesField.registerOverrideMethod(
            methodStr(FormDateControl, lookup),
            methodStr(StatisticsOnInvoiceUIBuilder, postingProfilesLookup),
            this);
        postingProfilesField.lookupButton(FormLookupButton::Always);

        SysOperationDialog reportDialogBox = this.dialog();
        postingProfilesRecIdsControl = reportDialogBox.formRun().design().addControl(FormControlType::String, PostingProfilesRecIdsControlName);
        postingProfilesRecIdsControl.visible(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postingProfilesLookup</Name>
				<Source><![CDATA[
    private void postingProfilesLookup(FormStringControl _postingProfilesControl)
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(VendLedger));
        QueryBuildFieldList qbfl = qbds.fields();
        qbfl.dynamic(false);
        qbfl.clearFieldList();
        qbfl.addField(fieldNum(VendLedger, PostingProfile));
        qbfl.addField(fieldNum(VendLedger, Name));

        container selectFields = [ tableNum(VendLedger), fieldNum(VendLedger, PostingProfile) ];

        SysLookupMultiSelectGrid::lookup(
            query,
            _postingProfilesControl,
            postingProfilesRecIdsControl,
            _postingProfilesControl,
            selectFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initVendGroupIdsDialogField</Name>
				<Source><![CDATA[
    private void initVendGroupIdsDialogField()
    {
        DialogField vendGroupIdsField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(StatisticsOnInvoiceDataContract, parmvendGroupIds));
        vendGroupIdsField.registerOverrideMethod(
            methodStr(FormDateControl, lookup),
            methodStr(StatisticsOnInvoiceUIBuilder, vendGroupIdsLookup),
            this);
        vendGroupIdsField.lookupButton(FormLookupButton::Always);

        SysOperationDialog reportDialogBox = this.dialog();
        vendGroupIdsRecIdsControl = reportDialogBox.formRun().design().addControl(FormControlType::String, vendGroupIdsRecIdsControlName);
        vendGroupIdsRecIdsControl.visible(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>vendGroupIdsLookup</Name>
				<Source><![CDATA[
    private void vendGroupIdsLookup(FormStringControl _vendGroupIdsControl)
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(VendGroup));
        QueryBuildFieldList qbfl = qbds.fields();
        qbfl.dynamic(false);
        qbfl.clearFieldList();
        qbfl.addField(fieldNum(VendGroup, VendGroup));
        qbfl.addField(fieldNum(VendGroup, Name));

        container selectFields = [ tableNum(VendGroup), fieldNum(VendGroup, VendGroup) ];

        SysLookupMultiSelectGrid::lookup(
            query,
            _vendGroupIdsControl,
            vendGroupIdsRecIdsControl,
            _vendGroupIdsControl,
            selectFields);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>