<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Project'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class ProjCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        ProjTable projTable;
        select crosscompany CustAccount from projTable where projTable.RecId == _caseAssociation.RefRecId;

        CustTable custTable;
        select firstonly RecId from custTable where custTable.AccountNum == projTable.CustAccount;
        if (custTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Customer, custTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        ProjTable projTable;
        select crosscompany DataAreaId from projTable where projTable.RecId == _parentRecId;

        return projTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        ProjTable projTable;
        select crosscompany ProjId from projTable where projTable.RecId == _parentRecId;

        return projTable.ProjId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        ProjTable projTable;
        select RecId from projTable where projTable.ProjId == _naturalKey;

        return projTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        ProjTable projTable;
        select crosscompany Name from projTable where projTable.RecId == _parentRecId;

        return projTable.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        ProjTable projTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        ProjTable projTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from projTableCrossCompany
            where projTableCrossCompany.RecId == _association.RefRecId;

        if (projTableCrossCompany.Recid)
        {
            changecompany (projTableCrossCompany.DataAreaId)
            {
                select firstonly projTable where projTable.RecId == _association.RefRecId;
            }
        }

        _args.record(projTable);
        _args.refField(fieldnum(ProjTable,ProjId));
        return menuitemdisplaystr(ProjTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        if (_caseAssociation.EntityType)
        {
            callerControl.performTypeLookup((new SysDictField(
                    tableNum(ProjTable),
                    fieldNum(ProjTable, ProjId)).typeId()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(ProjTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Project);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>