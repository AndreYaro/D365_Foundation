<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LeanScheduleGroupCache</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LeanScheduleGroupCache</c> class provides caching of the lean schedule groups information.
/// </summary>
public class LeanScheduleGroupCache
{
    SysGlobalObjectCache sysGlobalObjectCache;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>clearCache</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears all the lean schedule group entries from the cache that are related to a work cell.
    /// </summary>
    public void clearCache()
    {
        sysGlobalObjectCache.clear(classstr(LeanScheduleGroupCache));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLeanScheduleGroupItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a container from the cache that contains the <c>LeanScheduleGroup</c> and
    /// <c>LeanScheduleGroupItem</c> records.
    /// </summary>
    /// <param name="_wrkCtrResourceGroup">
    /// The record ID of a <c>WrkCtrResourceGroup</c> record.
    /// </param>
    /// <param name="_itemId">
    /// An item ID.
    /// </param>
    /// <param name="_inventDimId">
    /// An inventory dimensions ID.
    /// </param>
    /// <returns>
    /// A container from the cache that contains the <c>LeanScheduleGroup</c> and
    /// <c>LeanScheduleGroupItem</c> records.
    /// </returns>
    public container getLeanScheduleGroupItem(
        WrkCtrResourceGroupRefRecId _wrkCtrResourceGroup,
        ItemId                      _itemId,
        InventDimId                 _inventDimId)
    {
        return sysGlobalObjectCache.find(classstr(LeanScheduleGroupCache),
                                         [_wrkCtrResourceGroup, _itemId, _inventDimId]);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertLeanScheduleGroupItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts a new value into the cache.
    /// </summary>
    /// <param name="_wrkCtrResourceGroup">
    /// The record ID of a <c>WrkCtrResourceGroup</c> record.
    /// </param>
    /// <param name="_itemId">
    /// An item ID.
    /// </param>
    /// <param name="_inventDimId">
    /// An inventory dimensions ID.
    /// </param>
    /// <param name="_value">
    /// A container that contains the <c>LeanScheduleGroup</c> and <c>LeanScheduleGroupItem</c> records.
    /// </param>
    public void insertLeanScheduleGroupItem(
        WrkCtrResourceGroupRefRecId _wrkCtrResourceGroup,
        ItemId                      _itemId,
        InventDimId                 _inventDimId,
        container                   _value)
    {
        sysGlobalObjectCache.insert(classstr(LeanScheduleGroupCache),
                                    [_wrkCtrResourceGroup, _itemId, _inventDimId],
                                    _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>leanScheduleGroupItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves a container that contains the <c>LeanScheduleGroup</c> and <c>LeanScheduleGroupItem</c>
    /// records for the specified item, inventory dimensions, and work center.
    /// </summary>
    /// <param name="_wrkCtrResourceGroup">
    /// The record ID of a <c>WrkCtrResourceGroup</c> record.
    /// </param>
    /// <param name="_itemId">
    /// An item ID.
    /// </param>
    /// <param name="_inventDimId">
    /// An inventory dimensions ID.
    /// </param>
    /// <returns>
    /// A container that contains the <c>LeanScheduleGroup</c> and <c>LeanScheduleGroupItem</c> records.
    /// </returns>
    /// <remarks>
    /// The method also maintains a cache in the global object cache to reduce the database calls.
    /// </remarks>
    public container leanScheduleGroupItem(
        WrkCtrResourceGroupRefRecId _wrkCtrResourceGroup,
        ItemId                      _itemId,
        InventDimId                 _inventDimId)
    {
        LeanScheduleGroup       leanScheduleGroup;
        LeanScheduleGroupItem   leanScheduleGroupItem;
        container               result = connull();

        if (!_itemId ||
            !_inventDimId)
        {
            return [leanScheduleGroup, leanScheduleGroupItem];
        }

        // find in cache or look up in the database
        result = this.getLeanScheduleGroupItem(_wrkCtrResourceGroup,
                                               _itemId,
                                               _inventDimId);
        if (!result)
        {
            result = LeanScheduleGroup::findScheduleGroup(_wrkCtrResourceGroup,
                                                          _itemId,
                                                          _inventDimId);
            // Insert the new value into the cache
            this.insertLeanScheduleGroupItem(_wrkCtrResourceGroup,
                                             _itemId,
                                             _inventDimId,
                                             result);
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        sysGlobalObjectCache = new SysGlobalObjectCache();
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeLeanScheduleGroupItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes a lean schedule group item entry from the cache.
    /// </summary>
    /// <param name="_wrkCtrResourceGroup">
    /// The record ID of a <c>WrkCtrResourceGroup</c> record.
    /// </param>
    /// <param name="_itemId">
    /// An item ID.
    /// </param>
    /// <param name="_inventDimId">
    /// An inventory dimensions ID.
    /// </param>
    public void removeLeanScheduleGroupItem(
        WrkCtrResourceGroupRefRecId _wrkCtrResourceGroup,
        ItemId                      _itemId,
        InventDimId                 _inventDimId)
    {
        sysGlobalObjectCache.remove(classstr(LeanScheduleGroupCache),
                                    [_wrkCtrResourceGroup, _itemId, _inventDimId]);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLeanScheduleGroupItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates a lean schedule group item in the cache.
    /// </summary>
    /// <param name="_wrkCtrResourceGroup">
    /// The record ID of a <c>WrkCtrResourceGroup</c> record.
    /// </param>
    /// <param name="_oldItemId">
    /// The previous item ID.
    /// </param>
    /// <param name="_oldInventDimId">
    /// The previous inventory dimensions ID.
    /// </param>
    /// <param name="_newItemId">
    /// The new item ID.
    /// </param>
    /// <param name="_newInventDimId">
    /// The new inventory dimensions ID.
    /// </param>
    public void updateLeanScheduleGroupItem(
        WrkCtrResourceGroupRefRecId _wrkCtrResourceGroup,
        ItemId                      _oldItemId,
        InventDimId                 _oldInventDimId,
        ItemId                      _newItemId,
        InventDimId                 _newInventDimId)
    {
        container value = sysGlobalObjectCache.find(classstr(LeanScheduleGroupCache),
                                                    [_wrkCtrResourceGroup, _oldItemId, _oldInventDimId]);
        if (value)
        {
            sysGlobalObjectCache.remove(classstr(LeanScheduleGroupCache),
                                          [_wrkCtrResourceGroup, _oldItemId, _oldInventDimId]);

            sysGlobalObjectCache.insert(classstr(LeanScheduleGroupCache),
                                        [_wrkCtrResourceGroup, _newItemId, _newInventDimId],
                                        value);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the instance of the lean schedule group cache for the client session.
    /// </summary>
    /// <returns>
    /// The instance of the lean schedule group cache in the current client session.
    /// </returns>
    public static LeanScheduleGroupCache construct()
    {
        return new LeanScheduleGroupCache();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
