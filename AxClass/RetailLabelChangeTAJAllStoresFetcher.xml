<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailLabelChangeTAJAllStoresFetcher</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Fetches source data based on the price trade agreement journals that define a sales price for all customers.
/// </summary>
class RetailLabelChangeTAJAllStoresFetcher extends RetailLabelChangeDataFetcherBase
{
    FromDate activatedFromDate;
    FromDate modifiedFromDate;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createSourceSpecificQuery</Name>
				<Source><![CDATA[
    protected Query createSourceSpecificQuery()
    {
        Query query = new Query();
        QueryBuildDataSource qbds;
        QueryBuildFieldList qbfl;

        // PriceDiscTable
        qbds = query.addDataSource(tableNum(PriceDiscTable));
        qbfl = qbds.fields();
        qbfl.clearFieldList();
        qbfl.addField(fieldNum(PriceDiscTable, ItemRelation), SelectionField::Database);
        qbfl.addField(fieldNum(PriceDiscTable, InventDimId), SelectionField::Database); // Is used for VariantId calculation.
        qbds.addRange(fieldNum(PriceDiscTable, relation)).value(queryValue(PriceType::PriceSales));
        qbds.addRange(fieldNum(PriceDiscTable, ItemCode)).value(queryValue(PriceDiscProductCodeType::Table));
        qbds.addRange(fieldNum(PriceDiscTable, AccountCode)).value(queryValue(PriceDiscPartyCodeType::All));

        if (activatedFromDate && modifiedFromDate)
        {
            // Extended query syntax
            qbds.addRange(fieldNum(priceDiscTable, RecId)).value(
                strFmt(
                    '((((%1 == %3) || (%1 <= %4)) && ((%2 == %3) || (%2 >= %4)) && (%5 >= %6)) || ((%1 >= %7) && (%1 <= %4)) || ((%2 >= %8) && (%2 <= %9)))',
                    fieldStr(PriceDiscTable, FromDate),
                    fieldStr(PriceDiscTable, ToDate),
                    date2StrXpp(dateNull()),
                    date2StrXpp(priceDate),
                    fieldStr(PriceDiscTable, modifiedDateTime),
                    DateTimeUtil::toStr(DateTimeUtil::newDateTime(modifiedFromDate, 0)),
                    date2StrXpp(activatedFromDate),
                    date2StrXpp(activatedFromDate - 1),
                    date2StrXpp(priceDate - 1)));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getItemVariantId</Name>
				<Source><![CDATA[
    protected container getItemVariantId()
    {
        PriceDiscTable priceDiscTable;
        InventDimCombination inventDimCombination;

        priceDiscTable = qr.get(tableNum(PriceDiscTable));
        inventDimCombination = InventdimCombination::findByInventDim(priceDiscTable.ItemRelation, priceDiscTable.inventDim());

        return [priceDiscTable.ItemRelation, InventDimCombination.RetailVariantId];
    }

]]></Source>
			</Method>
			<Method>
				<Name>itemFieldId</Name>
				<Source><![CDATA[
    protected FieldId itemFieldId()
    {
        return fieldNum(PriceDiscTable, ItemRelation);
    }

]]></Source>
			</Method>
			<Method>
				<Name>itemTableId</Name>
				<Source><![CDATA[
    protected TableId itemTableId()
    {
        return tableNum(PriceDiscTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(
        FromDate _activatedFromDate,
        FromDate _modifiedFromDate,
        PriceDate _priceDate,
        RefRecId _hierarchyRecId,
        OMOperatingUnitRefRecId _operatingUnitId,
        EcoResCategoryId _categoryId,
        RetailItemQtyFilter _itemQtyFilter,
        NoYes _initWithQtyOnHand)
    {
        activatedFromDate = _activatedFromDate;
        modifiedFromDate = _modifiedFromDate;
        super(_priceDate, _hierarchyRecId, _operatingUnitId, _categoryId, _itemQtyFilter, _initWithQtyOnHand);
    }

]]></Source>
			</Method>
			<Method>
				<Name>productFieldId</Name>
				<Source><![CDATA[
    protected FieldId productFieldId()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>productTableId</Name>
				<Source><![CDATA[
    protected TableId productTableId()
    {
        return 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>