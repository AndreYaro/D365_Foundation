<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PersonSearchProcessor_Customer</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PersonSearchProcessor_Customer</c> executes personal information search criteria against customers.
/// </summary>
[PersonSearchProcessorFactoryAttribute(PersonSearchModule::Customer)]
public class PersonSearchProcessor_Customer extends PersonSearchProcessor
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doSearch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Search for customer matches.
    /// </summary>
    public void doSearch()
    {
        if (PersonSearchCriteriaKnownId::existAny(taskId))
        {
            this.findCustomerById();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findCustomerById</Name>
				<Source><![CDATA[
    /// <summary>
    /// Find customers by known id.
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void findCustomerById()
    {
        PersonSearchCriteriaKnownId personSearchCriteriaKnownId;
        CustTable custTable;
        DirPartyTable dirPartyTable;

        // Insert_recordset does not support cross company, so use while select.
        while select crossCompany custTable
            join dirPartyTable
                where custTable.Party == dirPartyTable.RecId
            exists join personSearchCriteriaKnownId
                where personSearchCriteriaKnownId.TaskId == taskId
                    && personSearchCriteriaKnownId.CustAccount != ''
                    && custTable.AccountNum == personSearchCriteriaKnownId.CustAccount
        {
            PersonSearchResult::findOrCreateResult(
                taskId,
                PersonSearchResultCategory::Customer,
                custTable.RecId,
                tableNum(CustTable),
                custTable.DataAreaId,
                dirPartyTable.PartyNumber);

            PersonSearchPartyNumberTmp::findOrCreate(taskId, dirPartyTable.PartyNumber);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>