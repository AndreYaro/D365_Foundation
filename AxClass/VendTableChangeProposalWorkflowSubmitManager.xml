<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendTableChangeProposalWorkflowSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The VendTableChangeProposalWorkflowSubmitManager menu item action event handler.
/// </summary>
public class VendTableChangeProposalWorkflowSubmitManager 
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (!_args.record() || !_args.caller())
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        // Opens the submit to workflow dialog.
        WorkflowSubmitDialog workflowSubmitDialog = WorkflowSubmitDialog::construct(_args.caller().getActiveWorkflowConfiguration());
        workflowSubmitDialog.run();

        if (workflowSubmitDialog.parmIsClosedOK())
        {
            // Get comments from the submit to workflow dialog.
            WorkflowComment initialNote = workflowSubmitDialog.parmWorkflowComment();

            try
            {
                RecId recId = _args.record().RecId;

                ttsbegin;
                VendTable::updateWorkflowState(recId, VendTableChangeProposalWorkflowState::Submitted);

                // Activate the workflow.
                Workflow::activateFromWorkflowType(workFlowTypeStr(VendTableChangeProposalWorkflow), recId, initialNote, NoYes::No);

                info("@AccountsPayable:VendTableChangeProposalWorkflow_Submitted");
                ttscommit;
            }
            catch(exception::Error)
            {
                info("@AccountsPayable:VendTableChangeProposalWorkflow_SubmittedError");
            }

            // make sure changes in status are reflected on the caller
            FormDataSource callerDS = _args.record().dataSource();
            callerDS.reread();
            callerDS.refresh();
            _args.caller().updateWorkflowControls();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>