<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetAcquisitionDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AssetAcquisitionDP</c> class is the data provider for the <c>AssetAcquisition</c> SSRS report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(AssetAcquisition)),
    SRSReportParameterAttribute(classstr(AssetAcquisitionContract))
]
public class AssetAcquisitionDP extends SRSReportDataProviderPreProcessTempDB
{
    AssetAcquisitionTmp assetAcquisitionTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssetAcquisitionTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the <c>AssetAcquisitionTmp</c> table.
    /// </summary>
    /// <returns>
    ///     The <c>AssetAcquisitionTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute(tableStr(AssetAcquisitionTmp))]
    public AssetAcquisitionTmp getAssetAcquisitionTmp()
    {
        select assetAcquisitionTmp;
        return assetAcquisitionTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the report business logic.
    /// </summary>
    public void processReport()
    {
        AssetTable assetTable;
        AssetBook assetBook;
        DirPartyTable dirPartyTable;
        VendTable vendTable;
        Query query;
        QueryRun queryRun;
        boolean calculateAcquisitionPrice;

        #IsoCountryRegionCodes

        calculateAcquisitionPrice = SysCountryRegionCode::isLegalEntityInCountryRegion([#isoLV]);

        if (calculateAcquisitionPrice)
        {
            GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceLV00019, funcName());
        }

        RecordInsertList recordInsertList = new RecordInsertList(tableNum(AssetAcquisitionTmp), true, true, true, false, true, assetAcquisitionTmp);

        query = this.parmQuery();

        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            assetTable = queryRun.get(tableNum(AssetTable)) as AssetTable;
            assetBook = queryRun.get(tableNum(AssetBook)) as AssetBook;
            vendTable = queryRun.get(tableNum(VendTable)) as VendTable;
            dirPartyTable = queryRun.get(tableNum(DirPartyTable)) as DirPartyTable;

            assetAcquisitionTmp.clear();
            assetAcquisitionTmp.initFromAssetTable(assetTable);
            assetAcquisitionTmp.initFromAssetBook(assetBook, calculateAcquisitionPrice);
            assetAcquisitionTmp.initFromVendTable(vendTable, dirPartyTable);

            recordInsertList.add(assetAcquisitionTmp);
        }

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>