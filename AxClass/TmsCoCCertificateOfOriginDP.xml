<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSCoCCertificateOfOriginDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This is the data provider class for the Certificate of Origin (Chamber of Commerce) report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(TMSCoCCertificateOfOrigin))
]
class TmsCoCCertificateOfOriginDP extends SRSReportDataProviderBase
{
    TMSCoCCertificateOfOriginTmp    tmsCoCCertificateOfOriginTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getTMSCoCCertificateOfOriginTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the temp table for the Certificate of Origin Report
    /// </summary>
    /// <returns>
    /// The temp table for the Certificate of Origin Report
    /// </returns>
    [SRSReportDataSetAttribute(tableStr(TMSCoCCertificateOfOriginTmp))]
    public TMSCoCCertificateOfOriginTmp getTMSCoCCertificateOfOriginTmp()
    {
        select tmsCoCCertificateOfOriginTmp;

        return tmsCoCCertificateOfOriginTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertTMSCoCCertificateOfOrigin</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts the fields into the temp table for the Certificate of Origin Report.
    /// </summary>
    /// <param name="_shipmentTable">
    /// The <c>WHSShipmentTable</c> record
    /// </param>
    /// <param name="_loadLine">
    /// The <c>WHSLoadLine</c> record
    /// </param>
    void insertTMSCoCCertificateOfOrigin(
        WHSShipmentTable _shipmentTable,
        WHSLoadLine      _loadLine)
    {
        tmsCoCCertificateOfOriginTmp.Exporter        = CompanyInfo::find().Name;
        tmsCoCCertificateOfOriginTmp.Distributor     = CompanyInfo::find().Name;
        tmsCoCCertificateOfOriginTmp.Carrier         = TMSCarrier::find(_shipmentTable.CarrierCode).Name;
        tmsCoCCertificateOfOriginTmp.ShipmentDate    = DateTimeUtil::date(DateTimeUtil::applyTimeZoneOffset(WHSLoadTable::find(_loadLine.LoadId).LoadShipConfirmUTCDateTime, DateTimeUtil::getUserPreferredTimeZone()));

        tmsCoCCertificateOfOriginTmp.DestinationName = _shipmentTable.DeliveryName;

        tmsCoCCertificateOfOriginTmp.uom         = _loadLine.uom;
        tmsCoCCertificateOfOriginTmp.Qty         = _loadLine.Qty;
        tmsCoCCertificateOfOriginTmp.GrossWeight = _loadLine.displayItemWeight();
        tmsCoCCertificateOfOriginTmp.NetWeight   = _loadLine.loadLineNetWeight();
        tmsCoCCertificateOfOriginTmp.ItemId      = _loadLine.ItemId;
        tmsCoCCertificateOfOriginTmp.ItemName    = _loadLine.displayItemName();

        tmsCoCCertificateOfOriginTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    public void processReport()
    {
        Query                   query;
        QueryRun                queryRun;
        WHSShipmentTable        shipmentTable;
        WHSLoadLine             loadLine;

        query    = this.parmQuery();
        queryRun = new QueryRun(query);

        ttsbegin;

        while (queryRun.next())
        {
            shipmentTable = queryRun.get(tableNum(WHSShipmentTable));
            loadLine      = queryRun.get(tableNum(WHSLoadLine));

            this.insertTMSCoCCertificateOfOrigin(shipmentTable, loadLine);
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
