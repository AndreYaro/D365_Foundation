<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSWaveTaskCreatorReplenishment</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSWaveTaskCreatorReplenish</c> class is responsible for creating the needed batch tasks
/// for running replenishment.
/// </summary>
[WHSWaveTaskCreatorAttribute(methodStr(WhsPostEngineBase, replenish))]
public class WHSWaveTaskCreatorReplenishment implements WHSWaveTaskICreator
{
    private WHSPostEngine postEngine;
    private BatchHeader   batchHeader;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFinalTask</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create a batch task that will be the final task run.
    /// </summary>
    /// <param name = "_batchGroupId">A batch group Id.
    /// </param>
    /// <returns>
    /// An instance of <c>WHSWaveFinalizeReplenishment</c>.
    /// </returns>
    /// <remarks>
    /// The batch task should be dependent on all other task created during classes execution.
    /// </remarks>
    private WHSWaveFinalizeReplenishment createFinalTask(BatchGroupId _batchGroupId)
    {
        var waveFinalizeReplenishment = WHSWaveFinalizeReplenishment::construct();
        waveFinalizeReplenishment.initFromPostEngine(postEngine);
        waveFinalizeReplenishment.batchInfo().parmGroupId(_batchGroupId);

        batchHeader.addTask(waveFinalizeReplenishment);

        return waveFinalizeReplenishment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createTasks</Name>
				<Source><![CDATA[
    /// <summary>
    /// Created a batch job with tasks to process wave replenishment.
    /// </summary>
    public void createTasks()
    {
        WHSWaveTable waveTable = postEngine.parmWaveTable();
        using (var perfTask = WHSInstrumentationWaveProcessingPerformanceTask::newTaskCreation(waveTable, postEngine.parmWaveExecutionId(), funcName()))
        {
            ttsbegin;

            var postMethodTaskConfig = WHSWavePostMethodTaskConfiguration::find(methodStr(WHSPostEngineBase, replenish), waveTable.InventLocationId);
            WHSMaximumBatchTasks numOfBatchTasks = postMethodTaskConfig.MaximumBatchTasks;
            perfTask.setMaximumBatchTasks(numOfBatchTasks)
                .setNumOfBatchTasks(numOfBatchTasks);

            if (numOfBatchTasks != WHSWaveTaskBasedProcessingConstants::ReplenishmentTaskBasedProcessingMaximumBatchTasks)
            {
                // If the number of batch tasks is not 1, then something went wrong because we have correctly setup task based processing,
                // yet the postMethodTaskConfig maximum batch tasks is not 1.
                // Throw an error to ensure that correct wave clean up is called.
                throw error("@WAX:WaveReplenishmentTasksNotCreatedError");
            }

            batchHeader = BatchHeader::construct();
            batchHeader.parmCaption(strFmt("@WAX:WaveReplenishmentBatchJobDescription", waveTable.WaveId));

            var batchGroupId = postMethodTaskConfig.WaveBatchGroup();
               
            WHSWaveFinalizeReplenishment waveFinalizeReplenishment = this.createFinalTask(batchGroupId);

            if (WHSParameters::find().WaveProgressLog)
            {
                WHSWaveMethodStatusHandler waveMethodStatusHandler = WHSWaveMethodStatusHandler::newWaveMethodStatusHandler(
                                                                            waveTable.WaveId,
                                                                            methodStr(WHSPostEngineBase, replenish));
                waveMethodStatusHandler.updateWaveStepProgressToStarted();
            }

            for (Counter i = 1; i <= numOfBatchTasks; i++)
            {
                var waveReplenishmentProcessor = WHSWaveReplenishmentProcessor::newFromPostEngine(postEngine);
                waveReplenishmentProcessor.batchInfo().parmCaption(strFmt("@WAX:WaveReplenishmentBatchTaskDescription", waveTable.WaveId, i));
                waveReplenishmentProcessor.batchInfo().parmGroupId(batchGroupId);

                batchHeader.addTask(waveReplenishmentProcessor);
                // The post replenishment batch task is dependent on all replenishment tasks
                batchHeader.addDependency(waveFinalizeReplenishment, waveReplenishmentProcessor, BatchDependencyStatus::FinishedOrError);
            }

            batchHeader.save();
            WHSWaveTableBatch::createSingleWaveBatchRelationship(waveTable.RecId, batchHeader.parmBatchHeaderId());
            perfTask.setBatchHeaderId(batchHeader.parmBatchHeaderId());
        
            ttscommit;
        }

        WHSPostEngineBase::immediatelyStartNextBatchTaskForWave();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPostEngine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the <c>WHSPostEngine</c> parameter.
    /// </summary>
    /// <param name = "_postEngine">The value to set.</param>
    /// <returns>The value of the <c>WHSPostEngine</c> parameter.</returns>
    /// <remarks>It is the responsibility of the consumer to correctly dispose the <c>WHSPostEngine</c> instance.</remarks>
    public WHSPostEngine parmPostEngine(WHSPostEngine _postEngine = postEngine)
    {
        postEngine = _postEngine;

        return postEngine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>