<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TMSLoadBuildParamValueMapBuilderAttrib</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TMSLoadBuildParamValueMapBuilderAttrib</c> class
/// builds <c>TMSLoadBuildParamValueMap</c> objects based on attribute data.
/// </summary>
class TMSLoadBuildParamValueMapBuilderAttrib implements TMSILoadBuildParamValueMapBuilder
{
    TMSLoadBuildStrategyAttribValueSetId    attributeValueSetId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildParamValueMap</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds an instance of <c>TMSLoadBuildParamValueMap</c> class.
    /// </summary>
    /// <returns>
    /// A new instance of <c>TMSLoadBuildParamValueMap</c> class.
    /// </returns>
    public TMSLoadBuildParamValueMap buildParamValueMap()
    {
        TMSLoadBuildParamValueMap   tmsLoadBuildParamValueMap = TMSLoadBuildParamValueMap::construct();
        EcoResAttributeValue        ecoResAttributeValue;
        EcoResValue                 ecoResValue;
        EcoResAttribute             ecoResAttribute;
        EcoResTextValue             ecoResTextValue;
        EcoResFloatValue            ecoResFloatValue;
        EcoResIntValue              ecoResIntValue;
        EcoResBooleanValue          ecoResBooleanValue;
        EcoResDateTimeValue         ecoResDateTimeValue;

        while select RecId from ecoResAttributeValue
            where ecoResAttributeValue.InstanceValue == attributeValueSetId
            join ecoResValue
                where ecoResValue.RecId == ecoResAttributeValue.Value
            join ecoResAttribute
                where ecoResAttribute.RecId == ecoResAttributeValue.Attribute
        {
            if (ecoResValue is EcoResTextValue)
            {
                ecoResTextValue = ecoResValue;
                tmsLoadBuildParamValueMap.addParameterValue(
                    TMSLoadBuildParamValueText::newParameters(ecoResAttribute.Name, ecoResTextValue.TextValue));
            }
            else if (ecoResValue is EcoResFloatValue)
            {
                ecoResFloatValue = ecoResValue;
                tmsLoadBuildParamValueMap.addParameterValue(
                    TMSLoadBuildParamValueDecimal::newParameters(ecoResAttribute.Name, ecoResFloatValue.FloatValue));
            }
            else if (ecoResValue is EcoResIntValue)
            {
                ecoResIntValue = ecoResValue;
                tmsLoadBuildParamValueMap.addParameterValue(
                    TMSLoadBuildParamValueInteger::newParameters(ecoResAttribute.Name, ecoResIntValue.IntValue));
            }
            else if (ecoResValue is EcoResBooleanValue)
            {
                ecoResBooleanValue = ecoResValue;
                tmsLoadBuildParamValueMap.addParameterValue(
                    TMSLoadBuildParamValueBool::newParameters(ecoResAttribute.Name, ecoResBooleanValue.BooleanValue == NoYes::Yes ? true : false));
            }
            else if (ecoResValue is EcoResDateTimeValue)
            {
                ecoResDateTimeValue = ecoResValue;
                tmsLoadBuildParamValueMap.addParameterValue(
                    TMSLoadBuildParamValueUTCDateTime::newParameters(ecoResAttribute.Name, ecoResDateTimeValue.DateTimeValue));
            }
        }

        return tmsLoadBuildParamValueMap;
    }
]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }
]]></Source>
			</Method>
			<Method>
				<Name>parmAttributeValueSetId</Name>
				<Source><![CDATA[
    public TMSLoadBuildStrategyAttribValueSetId parmAttributeValueSetId(TMSLoadBuildStrategyAttribValueSetId _attributeValueSetId = attributeValueSetId)
    {
        attributeValueSetId = _attributeValueSetId;
        return attributeValueSetId;
    }
]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    protected static TMSLoadBuildParamValueMapBuilderAttrib construct()
    {
        return new TMSLoadBuildParamValueMapBuilderAttrib();
    }
]]></Source>
			</Method>
			<Method>
				<Name>newAttributeValueSetId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of parameter value map builder, based on record ID of
    /// <c>TMSLoadBuildStrategyAttribValueSet</c> table.
    /// </summary>
    /// <param name="_attributeValueSetId">
    /// The record ID of <c>TMSLoadBuildStrategyAttribValueSet</c> table.
    /// </param>
    /// <returns>
    /// A new instance of <c>TMSLoadBuildParamValueMapBuilder</c> class.
    /// </returns>
    public static TMSLoadBuildParamValueMapBuilderAttrib newAttributeValueSetId(TMSLoadBuildStrategyAttribValueSetId _attributeValueSetId)
    {
        TMSLoadBuildParamValueMapBuilderAttrib tmsLoadBuildParamValueMapBuilder = TMSLoadBuildParamValueMapBuilderAttrib::construct();
        tmsLoadBuildParamValueMapBuilder.parmAttributeValueSetId(_attributeValueSetId);
        return tmsLoadBuildParamValueMapBuilder;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>