<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchSupplyPerformanceOnReceiptDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchSupplyPerformanceOnReceiptDP</c> class is the data provider class for the
/// <c>PurchSupplyPerformanceOnReceipt</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(PurchSupplyPerformanceOnReceipt))
]
class PurchSupplyPerformanceOnReceiptDP extends SRSReportDataProviderBase
{
    int                         totalLine;
    int                         totalLineLate;
    Days                        daysLate;
    VendPackingSlipJour         vendPackingSlipJour;
    VendPackingSlipTrans        vendPackingSlipTrans;
    VendTable                   vendTable;
    PurchSupplyPerformanceTmp   purchSupplyPerformanceTmp;
    VendAccount                 orderAccount;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPurchSupplyPerformanceTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the processed data set for the <c>PurchSupplyPerformanceOnReceipt</c> report.
    /// </summary>
    /// <returns>
    /// The processed data set for the <c>PurchSupplyPerformanceOnReceipt</c> report.
    /// </returns>
    [SRSReportDataSetAttribute('PurchSupplyPerformanceTmp')]
    public PurchSupplyPerformanceTmp getPurchSupplyPerformanceTmp()
    {
        select * from purchSupplyPerformanceTmp;
        return purchSupplyPerformanceTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertIntoTable</Name>
				<Source><![CDATA[
        private void insertIntoTable()
    {
        purchSupplyPerformanceTmp.clear();
        purchSupplyPerformanceTmp.AccountNum    =   orderAccount;
        purchSupplyPerformanceTmp.Count         =   totalLine;
        purchSupplyPerformanceTmp.TotalLineLate =   totalLineLate;
        purchSupplyPerformanceTmp.DaysLate      =   daysLate;
        purchSupplyPerformanceTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the report business logic for the <c>PurchSupplyPerformanceOnreceipt</c> report.
    /// </summary>
    public void processReport()
    {
        Days                days;
        Query               query;
        QueryRun            queryRun;
        boolean             isFirstFound = false;

        query   =   this.parmQuery();

        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            vendPackingSlipJour  =   queryRun.get(tablenum(VendPackingSlipJour));
            vendPackingSlipTrans =   queryRun.get(tablenum(VendPackingSlipTrans));

            if (isFirstFound && orderAccount !=  vendPackingSlipJour.OrderAccount)
            {
                this.insertIntoTable();
                this.resetLineValues();
            }

            orderAccount        = vendPackingSlipJour.OrderAccount;
            totalLine ++;
            isFirstFound   = true;
            if (vendPackingSlipTrans.DeliveryDate > vendPackingSlipTrans.PurchaseLineExpectedDeliveryDate)
            {
                days = vendPackingSlipTrans.DeliveryDate - vendPackingSlipTrans.PurchaseLineExpectedDeliveryDate;

                if (days)
                {
                    daysLate += days;
                    totalLineLate++;
                }
            }
        }
        if (orderAccount)
        {
            this.insertIntoTable();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>resetLineValues</Name>
				<Source><![CDATA[
        private void resetLineValues()
    {
        daysLate        = 0;
        totalLine       = 0;
        totalLineLate   = 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
