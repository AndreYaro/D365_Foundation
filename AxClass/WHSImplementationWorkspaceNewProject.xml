<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSImplementationWorkspaceNewProject</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSImplementationWorkspaceNewProject
{
    private WHSImplementationProject newProjectName;
    private WHSImplementationProject sourceProjectName;

    private boolean copyTasks, copyLegalEntities;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of a <c>WHSImplementationWorkspaceNewProject</c> class.
    /// </summary>
    /// <param name = "_projectName">The name of the project being created.</param>
    /// <returns>An instance of the <c>WHSImplementationWorkspaceNewProject</c> class.</returns>
    public static WHSImplementationWorkspaceNewProject construct(WHSImplementationProject _projectName)
    {
        WHSImplementationWorkspaceNewProject creator = new WHSImplementationWorkspaceNewProject();
        creator.parmProjectName(_projectName);

        return creator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runProjectCreation</Name>
				<Source><![CDATA[
    private WHSImplementationWorkspaceProject runProjectCreation()
    {
        WHSImplementationWorkspaceProject newProject;

        newProject.ImplementationProject = this.parmProjectName();
        newProject.insert();
        WHSImplementationProjectRecId newProjectRecId = newProject.RecId;

        if (this.parmCopyTasks())
        {
            WHSImplementationProjectTasks sourceTasks, destinationTasks;
            destinationTasks.skipEvents(true);
            destinationTasks.skipDataMethods(true);

            WHSImplementationWorkspaceProject sourceProject;

            insert_recordset destinationTasks
                (Project, Name, Description, Status, CompletedDateTime, CompletedByWorker, MenuItem, MenuItemType, SequenceNumber, ParentSequenceNumber)
            select newProjectRecId, Name, Description, Status, CompletedDateTime, CompletedByWorker, MenuItem, MenuItemType, SequenceNumber, ParentSequenceNumber
                from sourceTasks
            exists join sourceProject
                where sourceProject.RecId == sourceTasks.Project
                    && sourceProject.ImplementationProject == this.parmSourceProjectName();
        }

        return newProject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createBlankProject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new empty WHS implementation workspace project.
    /// </summary>
    /// <param name = "_projectName">The name of the project to create.</param>
    /// <returns>An inserted <c>WHSImplementationWorkspaceProject</c> record.</returns>
    public static WHSImplementationWorkspaceProject createBlankProject(WHSImplementationProject _projectName)
    {
        WHSImplementationWorkspaceNewProject createProject = WHSImplementationWorkspaceNewProject::construct(_projectName);
    
        return createProject.runProjectCreation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>createCopiedProject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new WHS implementation workspace project with data populated from an existing WHS implementation workspace project.
    /// </summary>
    /// <param name = "_newProjectName">The name of the project to create.</param>
    /// <param name = "_sourceProjectName">The name of the project to copy.</param>
    /// <param name = "_copyTasks">A flag that determines whether to copy the tasks from the source project.</param>
    /// <returns>An inserted <c>WHSImplementationWorkspaceProject</c> record.</returns>
    public static WHSImplementationWorkspaceProject createCopiedProject(
        WHSImplementationProject _newProjectName,
        WHSImplementationProject _sourceProjectName,
        boolean _copyTasks)
    {
        WHSImplementationWorkspaceNewProject createProject = WHSImplementationWorkspaceNewProject::construct(_newProjectName);
        createProject.parmSourceProjectName(_sourceProjectName);
        createProject.parmCopyTasks(_copyTasks);

        return createProject.runProjectCreation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProjectName</Name>
				<Source><![CDATA[
    public WHSImplementationProject parmProjectName(WHSImplementationProject _projectName = newProjectName)
    {
        newProjectName = _projectName;
        return newProjectName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceProjectName</Name>
				<Source><![CDATA[
    public WHSImplementationProject parmSourceProjectName(WHSImplementationProject _sourceProjectName = sourceProjectName)
    {
        sourceProjectName = _sourceProjectName;
        return sourceProjectName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyTasks</Name>
				<Source><![CDATA[
    public boolean parmCopyTasks(boolean _copyTasks = copyTasks)
    {
        copyTasks = _copyTasks;
        return copyTasks;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>