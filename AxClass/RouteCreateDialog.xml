<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RouteCreateDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>RouteCreateDialog</c> implements the functionality of the dialog to create new routes.
/// </summary>
public class RouteCreateDialog
{
    protected Dialog        dialog;

    protected DialogField   fieldNumber;
    protected DialogField   fieldName;
    protected DialogField   fieldCopy;
    protected DialogField   fieldSite;

    private RouteIdBase     routeId;
    private NoYes           mustCopy;
    private InventSiteId    inventSiteId;
    private Name            routeName;

    private boolean         isManual;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDialog</Name>
				<Source><![CDATA[
    protected Dialog parmDialog(Dialog _dialog)
    {
        dialog = _dialog;
        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the dialog.
    /// </summary>
    /// <returns>The dialog</returns>
    protected Dialog dialog()
    {
        dialog.addText("@SCM:RouteCreateStaticTextRoute");

        if (isManual)
        {
            fieldNumber = dialog.addField(extendedtypestr(RouteIdBase),"@SYS21709");
        }
        fieldName = dialog.addField(extendedtypestr(Name),"@SYS6303","@SYS50977");
        fieldCopy = dialog.addField(enumstr(NoYes),"@SYS12898","@SYS50978");
        fieldSite = dialog.addField(extendedtypestr(InventSiteId),"@SYS103211","@SYS103217");
        fieldSite.mandatory_RU(true);

        dialog.formBuildDesign().dialogSize(0);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves values from the dialog after it is completed.
    /// </summary>
    /// <returns>true if values are retrieved successfully; otherwise, false.</returns>
    protected boolean getFromDialog()
    {
        boolean ret = true;

        if (fieldNumber)
        {
            routeId = fieldNumber.value();
        }
        else
        {
            routeId = '';
        }

        if (fieldSite)
        {
            inventSiteId = fieldSite.value();
        }
        else
        {
            inventSiteId = '';
        }

        // validate that the site is valid
        if (!inventSiteId)
        {
            ret = checkFailed(strfmt("@SYS84378", "@SYS103211"));
        }
        else if (!InventSite::exist(inventSiteId))
        {
            ret = checkFailed(strfmt("@SYS111931", inventSiteId));
            inventSiteId = '';
        }

        mustCopy = fieldCopy.value();
        routeName = fieldName.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptCreateRouteDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays the dialog with the Route creation options and returns the user selection.
    /// </summary>
    /// <param name="_manual">
    /// A Boolean value indicating whether the Route ID is manually filled in.
    /// </param>
    /// <returns>
    /// A container with the new Route ID, name, a flag indicating whether the Route is copied or not and the site identifier,
    /// in the form [routeId, routeName, copyRoute, siteID].
    /// </returns>
    public container promptCreateRouteDialog(boolean _manual)
    {
        isManual = _manual;

        dialog = this.dialog();

        if (dialog.run())
        {
            if (this.getFromDialog())
            {
                return [routeId, routeName, mustCopy, inventSiteId];
            }
        }
                
        return connull();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static RouteCreateDialog construct()
    {
        return new RouteCreateDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>RouteCreateDialog</c> class.
    /// </summary>
    /// <param name="_dialog">
    /// A <c>Dialog</c> instance used for actually prompting the user; optional.
    /// </param>
    /// <returns>
    /// A new <c>RouteCreateDialog</c> instance.
    /// </returns>
    public static RouteCreateDialog newDialog(Dialog _dialog = new Dialog("@SYS25123"))
    {
        RouteCreateDialog routeCreateDialog = RouteCreateDialog::construct();

        routeCreateDialog.parmDialog(_dialog);

        return routeCreateDialog;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>