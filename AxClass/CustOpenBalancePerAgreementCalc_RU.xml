<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOpenBalancePerAgreementCalc_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>CustOpenBalancePerAgreementCalc_RU</c> is a class
/// for calculation open customer balance with regards to contract (agreement).
/// </summary>
final public class CustOpenBalancePerAgreementCalc_RU extends CustVendOpenBalancePerAgreementCalc_RU
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcForAgreement</Name>
				<Source><![CDATA[
    /// <summary>
    /// The function calculates open customer balance with regards to contract (agreement).
    /// </summary>
    /// <param name = "_agreementId"> Agreement identificator.</param>
    /// <returns> Returns open customer balance with regards to contract (agreement).</returns>
    protected AmountMST calcForAgreement(AgreementId_RU _agreementId)
    {
        AmountMST balance;

        Query query = new Query();

        QueryBuildDataSource mainQbds = this.addTransBalanceDataSource(query);

        SysQuery::addDimensionAttributeRange(
            query,
            mainQbds.name(),
            fieldStr(CustTrans, DefaultDimension),
            DimensionComponent::DimensionAttribute,
            _agreementId,
            this.agreementDimensionAttribute.Name);

        QueryRun qr = new QueryRun(query);

        CustTrans custTrans;

        if (qr.next())
        {
            custTrans = qr.get(tableNum(CustTrans));
            balance = custTrans.AmountMST;
        }

        query = new Query();
        mainQbds  = this.addSettlementBalanceDataSource(query);

        QueryBuildDataSource qbds = mainQbds.addDataSource(tableNum(CustTrans));
        qbds.addLink(fieldNum(CustSettlement, OffsetRecid), fieldNum(CustTrans, RecId));
        qbds.joinMode(JoinMode::ExistsJoin);

        SysQuery::addDimensionAttributeRange(
            query,
            qbds.name(),
            fieldStr(CustTrans, DefaultDimension),
            DimensionComponent::DimensionAttribute,
            _agreementId,
            this.agreementDimensionAttribute.Name);

        this.addJoinTransFilterByDimValue(
            query,
            mainQbds,
            SysQuery::valueNot(_agreementId));

        qr = new QueryRun(query);

        if (qr.next())
        {
            CustSettlement custSettlement = qr.get(tableNum(CustSettlement));
            balance += custSettlement.SettleAmountMST + custSettlement.ExchAdjustment;
        }

        return balance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcForEmptyAgreement</Name>
				<Source><![CDATA[
    /// <summary>
    /// The function calculates open customer balance without agreement.
    /// </summary>
    /// <returns> Returns open customer balance without agreement.</returns>
    protected AmountMST calcForEmptyAgreement()
    {
        AmountMST balance;

        Query query = new Query();
        this.addTransBalanceDataSource(query);

        QueryRun qr = new QueryRun(query);

        if (qr.next())
        {
            CustTrans custTrans = qr.get(tableNum(CustTrans));
            balance = custTrans.AmountMST;
        }

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            query = new Query();
            QueryBuildDataSource qbds = this.addSettlementBalanceDataSource(query);

            findOrCreateRange_W(qbds, fieldNum(CustSettlement, OffsetRecid)).value(SysQuery::valueNotEmptyString());

            this.addJoinTransFilterByDimValue(
                query,
                qbds,
                SysQuery::valueNotEmptyString());

            qr = new QueryRun(query);

            if (qr.next())
            {
                CustSettlement custSettlement = qr.get(tableNum(CustSettlement));

                balance += custSettlement.SettleAmountMST + custSettlement.ExchAdjustment;
            }
        }
 
        return balance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addTransBalanceDataSource</Name>
				<Source><![CDATA[
    private QueryBuildDataSource addTransBalanceDataSource(Query _query)
    {
        QueryBuildDataSource qbds = _query.addDataSource(tableNum(CustTrans));
        qbds.addRange(fieldNum(CustTrans, AccountNum)).value(this.accountNum);
        qbds.addSelectionField(fieldNum(CustTrans, AmountMST), SelectionField::Sum);

        return qbds;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addSettlementBalanceDataSource</Name>
				<Source><![CDATA[
    private QueryBuildDataSource addSettlementBalanceDataSource(Query _query)
    {
        QueryBuildDataSource qbds = _query.addDataSource(tableNum(CustSettlement));
        qbds.addRange(fieldNum(CustSettlement, AccountNum)).value(this.accountNum);
        qbds.addSelectionField(fieldNum(CustSettlement, SettleAmountMST), SelectionField::Sum);
        qbds.addSelectionField(fieldNum(CustSettlement, ExchAdjustment), SelectionField::Sum);

        return qbds;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addJoinTransFilterByDimValue</Name>
				<Source><![CDATA[
    private void addJoinTransFilterByDimValue(
        Query _query,
        QueryBuildDataSource _parentQbds,
        str _rangeValue)
    {
        QueryBuildDataSource qbds = _parentQbds.addDataSource(tableNum(CustTrans));
        qbds.addRange(fieldNum(CustTrans, AccountNum)).value(this.accountNum);
        qbds.addLink(fieldNum(CustSettlement, TransRecid), fieldNum(CustTrans, RecId));
        qbds.addLink(fieldNum(CustSettlement, AccountNum), fieldNum(CustTrans, AccountNum));
        qbds.addLink(fieldNum(CustSettlement, TransCompany), fieldNum(CustTrans, DataAreaId));
        qbds.joinMode(JoinMode::ExistsJoin);

        SysQuery::addDimensionAttributeRange(
                    _query,
                    qbds.name(),
                    fieldStr(CustTrans, DefaultDimension),
                    DimensionComponent::DimensionAttribute,
                    _rangeValue,
                    this.agreementDimensionAttribute.Name);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>