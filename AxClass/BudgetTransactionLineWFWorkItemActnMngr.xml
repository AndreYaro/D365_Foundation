<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetTransactionLineWFWorkItemActnMngr</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BudgetTransactionLineWFWorkItemActnMngr</c> class manages the work item actions for budget
///    transaction line workflows.
/// </summary>
public class BudgetTransactionLineWFWorkItemActnMngr extends WorkflowWorkItemActionManager
{
    BudgetTransactionLine budgetTransactionLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBudget</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether the budget funds are available for the budget transaction line.
    /// </summary>
    protected void checkBudget()
    {
        if (budgetTransactionLine &&
            budgetTransactionLine.BudgetType == BudgetType::Expense)
        {
            // Only perform budgetary control check if the budget type is Expense.
            BudgetControlBudgetTransactionProcessor::submitBudgetTransLineToCheckBudget(budgetTransactionLine.RecId, true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    public Args parmArgs(Args _args = args)
    {
        Args ret;

        ret = super(_args);

        if (_args)
        {
            this.parmCaller(_args.caller());

            if (_args.record() &&
                _args.record().TableId == tablenum(BudgetTransactionLine))
            {
                budgetTransactionLine = _args.record() as BudgetTransactionLine;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preActionDialogRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the budget funds are available when the action dialog run event occurs for workflow
    /// work item actions.
    /// </summary>
    protected void preActionDialogRun()
    {
        super();

        // Perform budget check before confirming the action dialog.
        // This gives the reviewer a chance to cancel the action if there are any budget check errors or warnings.
        this.checkBudget();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the workflow work item action or the line item workflow action.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    /// Arguments are null.
    /// </exception>
    public void run()
    {
        if (!this.parmArgs())
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is primary main method to be invoked via the selection of a menu item.
    /// </summary>
    /// <param name="_args">
    /// Menu item arguments.
    /// </param>
    public static void main(Args _args)
    {
        BudgetTransactionLineWFWorkItemActnMngr workflowWorkItemActionManager = new BudgetTransactionLineWFWorkItemActnMngr();

        workflowWorkItemActionManager.parmArgs(_args);
        workflowWorkItemActionManager.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>