<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmBusRelTable_PromoteBusinessPartner_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[
    ExtensionOf(formStr(smmBusRelTable))
]
final class smmBusRelTable_PromoteBusinessPartner_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    void init()
    {
        next init();

        if (isConfigurationkeyEnabled(configurationKeyNum(Retail)))
        {
            this.registerExtension();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>registerExtension</Name>
				<Source><![CDATA[
    private void registerExtension()
    {
        CustHierarchy_CustHierarchyId.registerOverrideMethod(
            methodStr(FormStringControl, jumpRef),
            methodStr(smmBusRelTable_PromoteBusinessPartner_Extension, custHierarchyId_jumpRef),
            this);

        CustHierarchy_ds.registerOverrideMethod(
            methodStr(FormDataSource, write),
            methodStr(smmBusRelTable_PromoteBusinessPartner_Extension, CustHierarchy_ds_write),
            this);

        CustHierarchy_ds.registerOverrideMethod(
            methodStr(FormDataSource, validateWrite),
            methodStr(smmBusRelTable_PromoteBusinessPartner_Extension, CustHierarchy_ds_validateWrite),
            this);

        RetailSmmBusRelTable_ds.registerOverrideMethod(
            methodStr(FormDataSource, active),
            methodStr(smmBusRelTable_PromoteBusinessPartner_Extension, RetailSmmBusRelTable_ds_active),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>custHierarchyId_jumpRef</Name>
				<Source><![CDATA[
    private void custHierarchyId_jumpRef(FormStringControl _custHierarchyIdControl)
    {
        MenuFunction menuFunction = new MenuFunction(menuItemDisplayStr(CustHierarchy), MenuItemType::Display);
        CustHierarchy custHierarchy;

        Args args = new Args(menuFunction.object());
        args.caller(this);

        select firstonly custHierarchy
            where custHierarchy.CustHierarchyId == _custHierarchyIdControl.text();
        
        args.record(custHierarchy);

        menuFunction.run(args);

        _custHierarchyIdControl.jumpRef();
    }

]]></Source>
			</Method>
			<Method>
				<Name>CustHierarchy_ds_write</Name>
				<Source><![CDATA[
    private void CustHierarchy_ds_write(FormDataSource _CustHierarchy_ds)
    {
        // Supress writing.
    }

]]></Source>
			</Method>
			<Method>
				<Name>CustHierarchy_ds_validateWrite</Name>
				<Source><![CDATA[
    private boolean CustHierarchy_ds_validateWrite(FormDataSource _CustHierarchy_ds)
    {
        // Suppress writing.
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>RetailSmmBusRelTable_ds_active</Name>
				<Source><![CDATA[
    private int RetailSmmBusRelTable_ds_active(FormDataSource _RetailSmmBusRelTable_ds)
    {
        RetailSmmBusRelTable currentRetailSmmBusRelTable = _RetailSmmBusRelTable_ds.cursor();
        boolean isPromoteProspect = currentRetailSmmBusRelTable.IsPromoteProspect;
        CustHierarchy_CustHierarchyId.visible(isPromoteProspect);
        this.setTabControlsVisibility(!isPromoteProspect);

        return _RetailSmmBusRelTable_ds.active();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTabControlsVisibility</Name>
				<Source><![CDATA[
    private void setTabControlsVisibility(boolean _visible)
    {
        AddressTab.visible(_visible);

        Communication.visible(_visible);

        FormControl DetailsTab = this.design(0).controlName(formControlStr(smmBusRelTable, Details));
        DetailsTab.visible(_visible);

        FormControl TabSalesDemographicsTab = this.design(0).controlName(formControlStr(smmBusRelTable, TabSalesDemographics));
        TabSalesDemographicsTab.visible(_visible);

        FormControl SetupTab = this.design(0).controlName(formControlStr(smmBusRelTable, Setup));
        SetupTab.visible(_visible);

        FormControl TabDeliveryDefaultsTab = this.design(0).controlName(formControlStr(smmBusRelTable, TabDeliveryDefaults));
        TabDeliveryDefaultsTab.visible(_visible);

        TabFinancialDimensions.visible(_visible);

        FormControl TabAdministrationTab = this.design(0).controlName(formControlStr(smmBusRelTable, TabAdministration));
        TabAdministrationTab.visible(_visible);

        List.visible(_visible);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>