<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchLineVersioning</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchLineVersioning</c> class provides versioning functionality for purchase order lines.
/// </summary>
final class PurchLineVersioning
{
    private Common purchLineVersion;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializePurchLineVersion</Name>
				<Source><![CDATA[
    private void initializePurchLineVersion(Common _purchLineVersion)
    {
        purchLineVersion = _purchLineVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPurchLineVersion</Name>
				<Source><![CDATA[
    [Hookable(false), Wrappable(false)]
    public Common parmPurchLineVersion()
    {
        return purchLineVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isChangeConfirmationRequired</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether a change to a record will require a purchase order confirmation.
    /// </summary>
    /// <param name = "_archivedPurchLineVersion">
    /// An archived <c>PurchLineVersioning</c> object to compare the current instance against.
    /// </param>
    /// <returns>
    /// true if the change requires confirmation; otherwise, false.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public boolean isChangeConfirmationRequired(PurchLineVersioning _archivedPurchLineVersion)
    {
        boolean ret = false;

        Enumerator enumerator = PurchLineDictVersioning::createInstance().versioningFieldSetsEnumerator();

        while (enumerator.moveNext() && !ret)
        {
            PurchLineIVersioningFieldSet versionFieldSet = enumerator.current() as PurchLineIVersioningFieldSet;

            ret = versionfieldset.isChangeConfirmationRequired(this, _archivedPurchLineVersion);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies versioned fields from a source version to the current instance.
    /// </summary>
    /// <param name = "_sourcePurchLineVersion">
    /// A <c>PurchLineVersioning</c> object containing the source version.
    /// </param>
    [Hookable(false), Wrappable(false)]
    public void copyVersion(PurchLineVersioning _sourcePurchLineVersion)
    {
        Enumerator enumerator = PurchLineDictVersioning::createInstance().versioningFieldSetsEnumerator();

        while (enumerator.moveNext())
        {
            PurchLineIVersioningFieldSet versionFieldSet = enumerator.current() as PurchLineIVersioningFieldSet;

            versionfieldset.copyVersion(this, _sourcePurchLineVersion);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>createInstance</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a <c>PurchLineVersioning</c> object based on the provided <c>PurchLineMap</c> record.
    /// </summary>
    /// <param name = "_purchLineVersion">
    /// A <c>PurchLineMap</c> record to create a <c>PurchLineVersioning</c> object for.
    /// </param>
    /// <returns>
    /// The instantiated <c>PurchLineVersioning</c> object.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public static PurchLineVersioning createInstance(PurchLineMap _purchLineVersion)
    {
        PurchLineVersioning instance = new PurchLineVersioning();

        instance.initializePurchLineVersion(_purchLineVersion);

        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>