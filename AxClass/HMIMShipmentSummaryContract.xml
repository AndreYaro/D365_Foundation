<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HMIMShipmentSummaryContract</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// <c>HMIMShipmentSummaryContract</c> data contract class for the <c>HMIMShipmentSummary</c> report
/// </summary>
class HMIMShipmentSummaryContract implements SysOperationValidatable
{
    WHSShipmentId shipmentId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmShipmentId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Accessor method for ShipmentID.
    /// </summary>
    [DataMemberAttribute('Shipment ID')]
    public WHSShipmentId parmShipmentId(WHSShipmentId _shipmentid = shipmentId)
    {
        shipmentId = _shipmentid;
        return shipmentId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the shipment data based on the contract information
    /// </summary>
    public boolean validate()
    {
        WHSLoadLine         loadLine;
        InventTable         inventTable;
        HMIMItemMaterial    itemMaterial;
        boolean             isValid = true;
        
        while select loadLine
            where loadLine.ShipmentId == shipmentId
        join inventTable
            where inventTable.ItemId == loadLine.ItemId
                && inventTable.HMIMIndicator == NoYes::Yes
        outer join itemMaterial
            where itemMaterial.ItemId == loadLine.ItemId
                && itemMaterial.RegCode == WHSShipmentTable::find(shipmentId).HMIMGetRegCode()
        {
            if (!loadLine.displayItemWeight() || !itemMaterial.TransportCategoryCode || !itemMaterial.Multiplier)
            {
                isValid = checkFailed(strFmt("@SCM:ShipSummaryValidation", loadLine.ItemId));
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>