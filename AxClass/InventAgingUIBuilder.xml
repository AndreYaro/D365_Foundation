<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventAgingUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventAgingUIBuilder</c> is a UIBuilder class for <c>InventAging</c> report.
/// </summary>
class InventAgingUIBuilder extends SrsReportDataContractExtendUIBuilder_CN
{
    DialogGroup     inventoryDimensionGroup;

    DialogField     dialogFieldAgingPeriodUnit;
    DialogField     dialogFieldAgingDaysOne;
    DialogField     dialogFieldAgingDaysTwo;
    DialogField     dialogFieldAgingDaysThree;
    DialogField     dialogFieldAgingDaysFour;
    DialogField     dialogFieldAgingDatesOne;
    DialogField     dialogFieldAgingDatesTwo;
    DialogField     dialogFieldAgingDatesThree;
    DialogField     dialogFieldAgingDatesFour;
    DialogField     dialogFieldAgingDatePeriodOne;
    DialogField     dialogFieldAgingDatePeriodTwo;
    DialogField     dialogFieldAgingDatePeriodThree;
    DialogField     dialogFieldAgingDatePeriodFour;

    #Define.SelectionChangeMethodName('selectionChange')
    #Define.ValidateMethodName('validate')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>enableAgingGroups</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables date field related to aging period.
    /// </summary>
    /// <param name="_agingPeriodUnitSelectionValue">
    /// A value index of enum <c>InventAgingPeriod</c> indicating the aging period selected.
    /// </param>
    protected void enableAgingGroups(int _agingPeriodUnitSelectionValue)
    {
        DictEnum            dictEnum = new DictEnum(enumNum(InventAgingPeriod));
        InventAgingPeriod   periodUnit = dictEnum.index2Value(_agingPeriodUnitSelectionValue);
        FormRun             formRun = this.dialog().dialogForm().formRun();

        dialogFieldAgingDaysOne.visible(periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysTwo.visible(periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysThree.visible(periodUnit == InventAgingPeriod::Days);
        dialogFieldAgingDaysFour.visible(periodUnit == InventAgingPeriod::Days);

        dialogFieldAgingDatesOne.visible(periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesTwo.visible(periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesThree.visible(periodUnit == InventAgingPeriod::Dates);
        dialogFieldAgingDatesFour.visible(periodUnit == InventAgingPeriod::Dates);

        dialogFieldAgingDatePeriodOne.visible(periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodTwo.visible(periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodThree.visible(periodUnit == InventAgingPeriod::DateIntervals);
        dialogFieldAgingDatePeriodFour.visible(periodUnit == InventAgingPeriod::DateIntervals);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyOverrideMethod</Name>
				<Source><![CDATA[
    protected void modifyOverrideMethod()
    {
        this.overrideDialogFieldMethod(dialogFieldAgingPeriodUnit, #SelectionChangeMethodName, methodStr(InventAgingUIBuilder, selectionChangeAging));
        this.overrideDialogFieldMethod(dialogFieldAgingDaysOne, #ValidateMethodName, methodStr(InventAgingUIBuilder, validateDays));
        this.overrideDialogFieldMethod(dialogFieldAgingDaysTwo, #ValidateMethodName, methodStr(InventAgingUIBuilder, validateDays));
        this.overrideDialogFieldMethod(dialogFieldAgingDaysThree, #ValidateMethodName, methodStr(InventAgingUIBuilder, validateDays));
        this.overrideDialogFieldMethod(dialogFieldAgingDaysFour, #ValidateMethodName, methodStr(InventAgingUIBuilder, validateDays));
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method initializes the dialog fields after the fields have been built.
    /// </summary>
    public void postBuild()
    {
        dialogFieldAgingPeriodUnit = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmAgingPeriodUnit));

        dialogFieldAgingDaysOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDaysOne));
        dialogFieldAgingDaysTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDaysTwo));
        dialogFieldAgingDaysThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDaysThree));
        dialogFieldAgingDaysFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDaysFour));
        dialogFieldAgingDatesOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDateOne));
        dialogFieldAgingDatesTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDateTwo));
        dialogFieldAgingDatesThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDateThree));
        dialogFieldAgingDatesFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDateFour));
        dialogFieldAgingDatePeriodOne = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDatePeriodOne));
        dialogFieldAgingDatePeriodTwo = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDatePeriodTwo));
        dialogFieldAgingDatePeriodThree = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDatePeriodThree));
        dialogFieldAgingDatePeriodFour = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventAgingContract, parmIntervalDatePeriodFour));
        this.setDefaultAgingFieldsVisibility();
        super();

        // If the dialog has not been rendered yet, then control() will return the build control.
        FormComboBoxControl agingPeriodUnitControl = dialogFieldAgingPeriodUnit.control() as FormComboBoxControl;
        if (agingPeriodUnitControl)
        {
            this.enableAgingGroups(agingPeriodUnitControl.selection());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultAgingFieldsVisibility</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the default visibility of the aging period fields for dates and date periods.
    /// </summary>
    private void setDefaultAgingFieldsVisibility()
    {
        const boolean isFieldVisibleByDefault = false;
        dialogFieldAgingDatesOne.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatesTwo.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatesThree.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatesFour.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatePeriodOne.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatePeriodTwo.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatePeriodThree.visible(isFieldVisibleByDefault);
        dialogFieldAgingDatePeriodFour.visible(isFieldVisibleByDefault);
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectionChangeAging</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the aging period selection change event.
    /// </summary>
    /// <param name="_formControl">
    /// The form control that raised the event.
    /// </param>
    /// <returns>
    /// The changed selection.
    /// </returns>
    final protected int selectionChangeAging(FormComboBoxControl _formControl)
    {
        int ret = _formControl.selectionChange();

        this.enableAgingGroups(_formControl.selection());

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDays</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the aging days validate event.
    /// </summary>
    /// <param name="_formControl">
    /// The form control that raised the event.
    /// </param>
    /// <returns>
    /// True if the days are greater or equals zero; otherwise false.
    /// </returns>
    protected boolean validateDays(FormIntControl   _formControl)
    {
        boolean ret = _formControl.validate();

        if (ret && _formControl.value() < 0)
        {
            throw error("@SYS102923");
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
