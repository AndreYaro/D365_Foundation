<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetFixedAssetEntityNumberSeqHandlerBase</Name>
	<SourceCode>
		<Declaration><![CDATA[
public abstract class AssetFixedAssetEntityNumberSeqHandlerBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static AssetFixedAssetEntityNumberSeqHandlerBase construct(TableId _entityId)
    {
        AssetFixedAssetEntityNumberSeqHandlerBase numberSeqHandlerBase = AssetFixedAssetEntityNumberSeqHandlerFactory::constructForEntity(_entityId);

        return numberSeqHandlerBase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleDefaultingRow</Name>
				<Source><![CDATA[
    internal void handleDefaultingRow(Common _record, DataEventArgs _eventArgs)
    {
        this.initializeNumberSequence(this.getGroupId(_record));
        this.initializeFixedAssetNumber(_record);
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleInserting</Name>
				<Source><![CDATA[
    internal void handleInserting(Common _record, DataEventArgs _eventArgs)
    {
        this.initializeNumberSequence(this.getGroupId(_record));
        this.initializeFixedAssetNumber(_record);
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleValidatingField</Name>
				<Source><![CDATA[
    internal void handleValidatingField(Common _record, DataEventArgs _eventArgs)
    {
        var validateEventArgs = _eventArgs as ValidateFieldEventArgs;

        if (validateEventArgs.parmFieldId() == this.getNumberFieldId())
        {
            boolean validationResult = this.validateManualFixedAssetNumber(_record);

            validateEventArgs.parmValidateResult(validationResult);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleInserted</Name>
				<Source><![CDATA[
    internal void handleInserted(Common _entity, DataEventArgs _eventArgs)
    {
        this.numberUsed(_entity);

        this.unsubscribeFromEvents(_entity);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeNumberSequence</Name>
				<Source><![CDATA[
    internal void initializeNumberSequence(AssetGroupId _assetGroupId)
    {
        if (!this.parmNumberSeq())
        {
            this.parmNumberSeq(AssetGroup::getNumberSeqForGroup(_assetGroupId));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateFixedAssetNumber</Name>
				<Source><![CDATA[
    internal void generateFixedAssetNumber()
    {
        if (!this.parmGeneratedNumber() && this.parmNumberSeq())
        {
            this.parmGeneratedNumber(this.parmNumberSeq().num());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeFixedAssetNumber</Name>
				<Source><![CDATA[
    internal void initializeFixedAssetNumber(Common _assetEntity)
    {
        if (!this.getAssetNumber(_assetEntity))
        {
            this.generateFixedAssetNumber();
            this.setFixedAssetNumber(_assetEntity);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateManualFixedAssetNumber</Name>
				<Source><![CDATA[
    internal boolean validateManualFixedAssetNumber(Common _assetEntity)
    {
        boolean ret = true;
        
        if (this.getAssetNumber(_assetEntity) && AssetGroup::find(this.getGroupId(_assetEntity)).AutoNumber)
        {
            if (!this.parmGeneratedNumber())
            {
                this.initializeNumberSequence(this.getGroupId(_assetEntity));
                this.generateFixedAssetNumber();
            }

            NumberSequenceTable numberSequenceTable = this.parmNumberSeq().parmNumTableNum(null);

            if (this.parmGeneratedNumber() != this.getAssetNumber(_assetEntity) &&
                !numberSequenceTable.numAllowEdit())
            {
                ret = ret && checkFailed(strFmt("@ApplicationFoundation:NumberSeqFieldDoesNotAllowEditing", fieldId2PName(this.getEntityId(), this.getNumberFieldId())));
            }

            if (this.parmGeneratedNumber())
            {
                ret = ret && NumberSeq::checkChangeNumber(numberSequenceTable, this.getEntityId(), this.getNumberFieldId(), this.parmGeneratedNumber(), this.getAssetNumber(_assetEntity));
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>numberUsed</Name>
				<Source><![CDATA[
    internal void numberUsed(Common _assetEntity)
    {
        if (this.parmNumberSeq())
        {
            this.parmNumberSeq().onNumberUsed(this.getAssetNumber(_assetEntity), this.parmGeneratedNumber());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>subscribeForEvents</Name>
				<Source><![CDATA[
    internal void subscribeForEvents(Common _record)
    {
        _record.onDefaultingRow += eventhandler(this.handleDefaultingRow);
        _record.onInserting += eventhandler(this.handleInserting);
        _record.onValidatingField += eventhandler(this.handleValidatingField);
        _record.onInserted += eventhandler(this.handleInserted);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unsubscribeFromEvents</Name>
				<Source><![CDATA[
    internal void unsubscribeFromEvents(Common _record)
    {
        _record.onDefaultingRow -= eventhandler(this.handleDefaultingRow);
        _record.onInserting -= eventhandler(this.handleInserting);
        _record.onValidatingField -= eventhandler(this.handleValidatingField);
        _record.onInserted -= eventhandler(this.handleInserted);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getGroupId</Name>
				<Source><![CDATA[
    abstract internal AssetGroupId getGroupId(Common _record)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssetNumber</Name>
				<Source><![CDATA[
    abstract internal AssetId getAssetNumber(Common _record)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEntityId</Name>
				<Source><![CDATA[
    abstract internal TableId getEntityId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNumberFieldId</Name>
				<Source><![CDATA[
    abstract internal TableId getNumberFieldId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFixedAssetNumber</Name>
				<Source><![CDATA[
    abstract internal void setFixedAssetNumber(Common _assetEntity)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNumberSeq</Name>
				<Source><![CDATA[
    abstract internal NumberSeq parmNumberSeq(NumberSeq _numberSeq = null)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmGeneratedNumber</Name>
				<Source><![CDATA[
    abstract internal Num parmGeneratedNumber(Num _num = '')
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>