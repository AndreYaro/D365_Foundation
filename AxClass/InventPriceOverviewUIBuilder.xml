<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventPriceOverviewUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventPriceOverviewUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogGroup groupActive;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Builds the dialog for the report.
    /// </summary>
    public void build()
    {
        InventPriceOverviewContract inventPriceOverviewContract = this.dataContractObject() as InventPriceOverviewContract;

        dialog.addGroup("@SYS117038").frameOptionButton(FormFrameOptionButton::None);
        DialogField fieldAsPerDate = dialog.addFieldValue(extendedTypeStr(FromDate), inventPriceOverviewContract.parmAsPerDate(), "@SYS117038");
        this.bindInfo().addDialogField(inventPriceOverviewContract, methodStr(InventPriceOverviewContract, parmAsPerDate), fieldAsPerDate);

        groupActive = dialog.addGroup("@SYS24676");
        groupActive.frameOptionButton(FormFrameOptionButton::Radio);
        groupActive.optionValue(inventPriceOverviewContract.parmActivePrices());
        DialogField fieldCostingType = dialog.addFieldValue(enumStr(InventItemCostingType), inventPriceOverviewContract.parmInventItemCostingType(), "@SYS111755");
        this.bindInfo().addDialogField(inventPriceOverviewContract, methodStr(InventPriceOverviewContract, parmInventItemCostingType), fieldCostingType);

        DialogGroup groupVersion = dialog.addGroup("@SYS41323");
        groupVersion.frameOptionButton(FormFrameOptionButton::Radio);
        groupVersion.optionValue(!inventPriceOverviewContract.parmActivePrices());
        DialogField fieldVersionId = dialog.addFieldValue(extendedTypeStr(CostingVersionId), inventPriceOverviewContract.parmCostingVersionId(), "@SYS41323");
        DialogField fieldFallback = dialog.addFieldValue(enumStr(boolean), inventPriceOverviewContract.parmFallback(), "@SYS106179", "@SYS106201");
        this.bindInfo().addDialogField(inventPriceOverviewContract, methodStr(InventPriceOverviewContract, parmCostingVersionId), fieldVersionId);
        this.bindInfo().addDialogField(inventPriceOverviewContract, methodStr(InventPriceOverviewContract, parmFallback), fieldFallback);

        dialog.addGroup("@SYS103211").frameOptionButton(FormFrameOptionButton::None);
        DialogField fieldSite = dialog.addFieldValue(extendedTypeStr(InventSiteId), inventPriceOverviewContract.parmInventSiteId(), "@SYS103211");
        this.bindInfo().addDialogField(inventPriceOverviewContract, methodStr(InventPriceOverviewContract, parmInventSiteId), fieldSite);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Transfers data from the dialog into the data contract object.
    /// </summary>
    public void getFromDialog()
    {
        InventPriceOverviewContract inventPriceOverviewContract = this.dataContractObject() as InventPriceOverviewContract;

        super();

        // This control cannot be bound to the contract parameter, so it is handled manually
        inventPriceOverviewContract.parmActivePrices(groupActive.optionValue());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
