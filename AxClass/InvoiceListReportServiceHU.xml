<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InvoiceListReportServiceHU</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The service class for Invoice list report.
/// </summary>
class InvoiceListReportServiceHU
{
    private InvoiceListReportContractHU dataContract;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run(InvoiceListReportContractHU _dataContract)
    {
        dataContract = _dataContract;

        this.runElectronicReporting();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runElectronicReporting</Name>
				<Source><![CDATA[
    private str runElectronicReporting()
    {
        ERIFormatMappingRun erFormatMappingRun = ERObjectsFactory::createFormatMappingRunByFormatMappingId(dataContract.parmERBinding(), dataContract.parmFilename());

        return erFormatMappingRun.withFileDestination(ERObjectsFactory::createFileDestinationBrowser())
               .withParameter(this.getERFormatParameters(erFormatMappingRun))
               .withCreatingObjectParameter(InvoiceListReportContractHU::parmModelName(), classStr(InvoiceListReportContractHU), dataContract).run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERFormatParameters</Name>
				<Source><![CDATA[
    private ERModelDefinitionParamsUIActionComposite getERFormatParameters(ERIFormatMappingRun _formatMappingRun)
    {
        return new ERModelDefinitionParamsUIActionComposite()
                .add(this.getERModelDefinitionDatabaseContext(tableNum(CustInvoiceJour)))
                .add(this.getERModelDefinitionDatabaseContext(tableNum(ProjInvoiceJour)))
                .add(this.getERModelDefinitionDatabaseContext(tableNum(CzCustAdvanceInvoiceTable)))
                .add(this.getERModelDefinitionDatabaseContext(tableNum(CzVendAdvanceInvoiceTable)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERModelDefinitionDatabaseContext</Name>
				<Source><![CDATA[
    private ERModelDefinitionDatabaseContext getERModelDefinitionDatabaseContext(int _tableNum)
    {
        return new ERModelDefinitionDatabaseContext()
            .addQuery(_tableNum, this.getQueryForSpecificTable(_tableNum))
            .addValue(tableNum(CompanyInfo), fieldNum(CompanyInfo, DataArea), curExt());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryForSpecificTable</Name>
				<Source><![CDATA[
    private Query getQueryForSpecificTable(int _tableNum)
    {
        Query newQuery = new Query();
        QueryBuildDataSource qbdsCopyFrom = datacontract.getQuery().dataSourceTable(_tableNum),
                             qbdsCopyTo = newQuery.addDataSource(_tableNum);
        int rangeCount = qbdsCopyFrom.rangeCount();

        for (int i = 1; i <= rangeCount; i++)
        {
            QueryBuildRange range = qbdsCopyFrom.range(i);
            findOrCreateRange_W(qbdsCopyTo, range.field(), range.value());
        }

        return newQuery;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>