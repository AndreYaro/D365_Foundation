<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymentByVendorDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BankPaymentByVendorDP</c> class is the Report Data Provider class for the
/// <c>BankPaymentByVendor</c> report.
/// </summary>
[
    SRSReportQuery(queryStr(BankPaymentByVendor)),
    SRSReportParameterAttribute(classstr(BankPaymentbyVendorContract))
]
public class BankPaymentByVendorDP extends SrsReportDataProviderPreProcessTempDB
{
    private BankPaymentByVendorTmp bankPaymentByVendorTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getBankPaymentByVendor</Name>
				<Source><![CDATA[
    [SRSReportDataSet(tableStr(BankPaymentByVendorTmp))]
    public BankPaymentByVendorTmp getBankPaymentByVendor()
    {
        select bankPaymentByVendorTmp;
        return bankPaymentByVendorTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Maps the fields from <c>BankPaymentByVendor</c> query fields to
    /// the fields from <c>BankPaymentByVendorTmp</c> table.
    /// </summary>
    public void processReport()
    {
        Query query = new Query(this.parmQuery());
        QueryRun queryRun = new QueryRun(query);
        RecordInsertList bankPaymentByVendorTmpRecords = new RecordInsertList(bankPaymentByVendorTmp.TableId, true, true, true, true, true, bankPaymentByVendorTmp);

        while (queryRun.next())
        {
            this.addRecordsFromQueryToList(queryRun, bankPaymentByVendorTmpRecords);
        }

        bankPaymentByVendorTmpRecords.insertDatabase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>addRecordsFromQueryToList</Name>
				<Source><![CDATA[
    private void addRecordsFromQueryToList(QueryRun _queryRun, RecordInsertList _recordInsertList)
    {
        VendTable vendTable = _queryRun.get(tableNum(VendTable));
        bankPaymentByVendorTmp.Currency = vendTable.Currency;
        bankPaymentByVendorTmp.AccountNum = vendTable.AccountNum;
        bankPaymentByVendorTmp.Name = vendTable.name();

        VendTrans vendTrans = _queryRun.get(tableNum(VendTrans));
        bankPaymentByVendorTmp.AmountCur = vendTrans.AmountCur;
        bankPaymentByVendorTmp.AmountMST = vendTrans.AmountMST;
        bankPaymentByVendorTmp.AmountMSTSecond = vendTrans.ReportingCurrencyAmount; 
        bankPaymentByVendorTmp.CurrencyCode = vendTrans.CurrencyCode;
        bankPaymentByVendorTmp.TransDate = vendTrans.TransDate;

        BankAccountTrans bankAccountTrans = _queryRun.get(tableNum(BankAccountTrans));
        bankPaymentByVendorTmp.PaymReference = bankAccountTrans.PaymReference;
        bankPaymentByVendorTmp.AccountId = bankAccountTrans.AccountId;
        bankPaymentByVendorTmp.BankTransAmountCur = bankAccountTrans.BankTransAmountCur;

        _recordInsertList.add(bankPaymentByVendorTmp);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>