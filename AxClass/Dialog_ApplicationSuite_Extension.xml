<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>Dialog_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(Dialog))]
internal final class Dialog_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>disableRecurrenceButton</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes the recurrence button from dialog.
    /// </summary>
    /// <param name = "_dialog">The current instance of the <c>DialogRunbase</c> class.</param>
    internal void disableRecurrenceButton()
    {
        #dialog

        DialogForm dialogForm = this.dialogForm();
        FormBuildDesign formBuildDesign = dialogForm.buildDesign();

        int i = 1;
        Object control = formBuildDesign.control(#PrefixMenuItemButton + '_' + int2Str(i));
        while (control)
        {
            if (control && classIdGet(control) == classNum(FormBuildFunctionButtonControl))
            {
                FormBuildFunctionButtonControl buttonControl = control;
                if (buttonControl.menuItemType() == MenuItemType::Display
                && buttonControl.menuItemName() == menuitemDisplayStr(SysRecurrenceRun))
                {
                    buttonControl.visible(false);
                    break;
                }
            }

            i++;
            control = formBuildDesign.control(#PrefixMenuItemButton + '_' + int2Str(i));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRecurrenceButtonVisibility</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the visibility status of the recurrence button.
    /// </summary>
    /// <param name = "_visible">The visibility of the recurrence button.</param>
    internal void setRecurrenceButtonVisibility(boolean _visible)
    {
        #dialog

        DialogForm dialogForm = this.dialogForm();
        FormBuildDesign formBuildDesign = dialogForm.buildDesign();

        int i = 1;
        Object control = formBuildDesign.control(#PrefixMenuItemButton + '_' + int2Str(i));
        while (control)
        {
            if (control && classIdGet(control) == classNum(FormBuildFunctionButtonControl))
            {
                FormBuildFunctionButtonControl buttonControl = control;
                if (buttonControl.menuItemType() == MenuItemType::Display
                    && buttonControl.menuItemName() == menuitemDisplayStr(SysRecurrenceRun))
                {
                    if (dialogForm.formRun())
                    {
                        FormRun formRun = dialogForm.formRun();
                        var design = formRun.design();
                        var ctrl = design.controlName(#PrefixMenuItemButton + '_' + int2Str(i));
                        ctrl.visible(_visible);
                    }
                    break;
                }
            }

            i++;
            control = formBuildDesign.control(#PrefixMenuItemButton + '_' + int2Str(i));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>