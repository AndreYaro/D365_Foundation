<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjBudgetLineToForecastCostMapper</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Xrm.Sdk;

internal class ProjBudgetLineToForecastCostMapper implements ProjBudgetLineEntityTableMapper
{
    private ProjForecastCost projForecastCost;
    private Entity entity;
    private const str DEFAULT_COST_CATEGORY = 'Default-Expense';
    private static ProjOpsInstrumentationLogger logger = ProjOpsInstrumentationLogger::createLogger(classStr(ProjBudgetLineToForecastCostMapper));


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Entity _entity)
    {
        
        if ( !_entity || !_entity.Attributes )
        {
            logger.logError(strFmt("@Proj:ProjBudgetLoggerEmptyEntityPassed", any2Str(_entity)));
            throw error(strFmt("@Proj:ProjBudgetEmptyEntityPassed"));
        }
        this.entity = _entity;
        this.mapEntityToTable();
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>mapEntityToTable</Name>
				<Source><![CDATA[
    private void mapEntityToTable()
    {
        AttributeCollection attributes = entity.Attributes;
        projForecastCost.ProjId = attributes.ContainsKey(ProjBudgetDataverseConstants::PROJECTNUMBER)
                                    ? attributes.get_Item(ProjBudgetDataverseConstants::PROJECTNUMBER) : '';

        AliasedValue description = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::DESCRIPTION);
        projForecastCost.Txt = description ? description.Value : '';
        
        AliasedValue startDateTime = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::STARTTIME);
        projForecastCost.StartDate = startDateTime ? startDateTime.Value : ProjBudgetDataverseConstants::DEFAULT_DATETIME;

        AliasedValue endDateTime = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::ENDTIME);
        projForecastCost.EndDate = endDateTime ? endDateTime.Value : ProjBudgetDataverseConstants::DEFAULT_DATETIME;

        this.updateQuantityAndCostPrice(projForecastCost, attributes);

        AliasedValue expandID = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::BUDGETID);
        projForecastCost.ExpandId = expandID ? expandID.Value : 0;

        EntityReference currencyId = attributes.ContainsKey(ProjBudgetDataverseConstants::CURRENCYID)
                                                        ? attributes.get_Item(ProjBudgetDataverseConstants::CURRENCYID): null;
        projForecastCost.CurrencyId = currencyId ? currencyId.Name : '';

        projForecastCost.ModelId = ProjBudgetLineEntityCollectionWriter::getBudgetForecastModel().ModelId;

        ProjCategory projCategory = ProjCategory::find(DEFAULT_COST_CATEGORY);
        AliasedValue category = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::TRANSACTION_NUMBER, ProjBudgetDataverseConstants::TRANSACTION_CATEGORY_ENTITY);
        projForecastCost.CategoryId = category ? category.Value : projCategory.CategoryId;

        AliasedValue activity = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::ACTIVITY_NUMBER, ProjBudgetDataverseConstants::TASK_ENTITY);
        projForecastCost.ActivityNumber = activity ? activity.Value : '';

        AliasedValue resource = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::RESOURCE_CATEGORY, ProjBudgetDataverseConstants::BOOKABLE_RESOURCE_CATEGORY_ENTITY);
        projForecastCost.ResourceCategory = resource ? resource.Value : 0;

        EntityReference contract = attributes.ContainsKey(ProjBudgetDataverseConstants::CONTRACTLINEID)
                                                        ? attributes.get_Item(ProjBudgetDataverseConstants::CONTRACTLINEID) : null;
        projForecastCost.ContractLineId = contract ? contract.Name : '';

        AliasedValue billingType = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::BILLINGTYPE);
        OptionSetValue billingTypeValue = billingType ? billingType.Value : null;
        projForecastCost.LinePropertyId = ProjBudgetLineToForecastMapperUtil::convertBillingTypeToLineProperty(billingTypeValue);

    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQuantityAndCostPrice</Name>
				<Source><![CDATA[
    /// <summary>
    /// updating the quantity and cost price to support the case where quantity is 0 and amount is not
    /// </summary>
    /// <param name="_projForecastCost">The ProjForecastCost object to be updated.</param>
    /// <param name="_attributes">Collection of attributes used to fetch the budget line item amount.</param>
    private void updateQuantityAndCostPrice(ProjForecastCost _projForecastCost, AttributeCollection _attributes)
    {
        AliasedValue qty = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::QUANTITY);
        real quantity = qty ? qty.Value : 0;
        real value;

        AliasedValue module = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::MODULE);
        OptionSetValue moduleOptionSet = module ? module.Value : null;
        int budgetType = moduleOptionSet ? moduleOptionSet.Value : 0;

        //Set qty
        if(quantity == 0)
        {
            AliasedValue amount = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::AMOUNT);
            Microsoft.Xrm.Sdk.Money money = amount ? amount.Value : null;
            value = money ? money.Value : 0;
            _projForecastCost.Qty = value ? 1 : 0;
        }
        else
        {
            AliasedValue price = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::PRICE);
            Microsoft.Xrm.Sdk.Money money = price ? price.Value : null;
            value = money ? money.Value : 0;
            _projForecastCost.Qty = quantity;
        }

        //Set Cost/Sales Price
        if (budgetType == ProjBudgetDataverseConstants::MODULE_COST)
        {
            _projForecastCost.CostPrice = value;
        }
        else if (budgetType == ProjBudgetDataverseConstants::MODULE_SALES)
        {
            _projForecastCost.SalesPrice = value;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeTableToDatabase</Name>
				<Source><![CDATA[
    /// <summary>
    /// Writes mapped forecastCost to Database
    /// </summary>
    public void writeTableToDatabase()
    {
        projForecastCost.selectForUpdate(true);
        projForecastCost.insert();
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionClassification</Name>
				<Source><![CDATA[
    public ProjTransType getTransactionClassification()
    {
        return ProjTransType::Cost;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    public Common getTable()
    {
        return projForecastCost;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>