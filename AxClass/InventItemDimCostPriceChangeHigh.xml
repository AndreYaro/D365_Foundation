<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventItemDimCostPriceChangeHigh</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventItemDimCostPriceChangeHigh</c> class creates new active item prices when the filed
///    'use dimensions combinations' on an item is either activated or deactivated.
/// </summary>
/// <remarks>
///    The new cost price is based on the highest cost price on the dimension combinations when
///    deactivating 'use dimensions combination'.
/// </remarks>
class InventItemDimCostPriceChangeHigh extends InventItemDimCostPriceFieldChange
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createPricesPerSite</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new cost price for each site based on the values in <c>inventDimCombinations</c>.
    /// </summary>
    /// <remarks>
    ///    The new cost price is the highest price of the active <c>inventDimCombinations</c>.
    /// </remarks>
    protected void createPricesPerSite()
    {
        InventSite              inventSite;
        InventDimCombination    inventDimCombination;
        InventItemPrice         maxInventItemPrice;

        while select SiteId
            from inventSite
        {
            maxInventItemPrice.clear();

            while select InventDimId
                from inventDimCombination
                where inventDimCombination.ItemId == inventTable.ItemId
            {
                InventItemPrice inventItemPrice = this.findSiteSpecificPrice(inventDimCombination.InventDimId, inventSite.SiteId);
                if (inventItemPrice.price() > maxInventItemPrice.price())
                {
                    maxInventItemPrice = inventItemPrice;
                }
            }

            if (maxInventItemPrice)
            {
                this.insertSiteSpecificInventItemPrice(InventDim::inventDimIdBlank(), inventSite.SiteId, maxInventItemPrice);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static InventItemDimCostPriceChangeHigh construct()
    {
        return new InventItemDimCostPriceChangeHigh();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newInventTable</Name>
				<Source><![CDATA[
        public static InventItemDimCostPriceChangeHigh newInventTable(InventTable _inventTable)
    {
        InventItemDimCostPriceChangeHigh inventItemDimCostPriceChangeHigh = InventItemDimCostPriceChangeHigh::construct();
        inventItemDimCostPriceChangeHigh.parmInventTable(_inventTable);
        return inventItemDimCostPriceChangeHigh;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
