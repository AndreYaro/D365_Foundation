<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanBudgetTransAuditUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetPlanBudgetTransAuditUIBuilder</c> class is used to build the user interface for the report parameter form.
/// </summary>
public class BudgetPlanBudgetTransAuditUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField budgetPlanScenarioField;

    BudgetPlanBudgetTransAuditContract  dataContract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupBudgetPlanScenario</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a localized budget plan scenario lookup.
    /// </summary>
    /// <param name="_formStringControl">
    /// The budget plan scenario control.
    /// </param>
    public void lookupBudgetPlanScenario(FormStringControl _formStringControl)
    {
        BudgetPlanReportHelper::lookupBudgetPlanScenario(_formStringControl, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedBudgetPlanScenario</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles actions on change of the budget plan scenario form control.
    /// </summary>
    /// <param name="_budgetPlanScenarioControl">
    /// The budget plan scenario form control.
    /// </param>
    ///    /// <returns>
    /// true when the value is modified; otherwise, false.
    /// </returns>
    public boolean modifiedBudgetPlanScenario(FormStringControl _budgetPlanScenarioControl)
    {
        boolean isModified;

        isModified = _budgetPlanScenarioControl.modified();

        dataContract.parmBudgetPlanScenario(
            BudgetPlanScenario::findByLocalizedName(_budgetPlanScenarioControl.text()).RecId);

        return isModified;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the dialog fields after the fields have been built.
    /// </summary>
    public void postBuild()
    {
        dataContract            = this.dataContractObject();
        budgetPlanScenarioField = this.bindInfo().getDialogField(dataContract, methodStr(BudgetPlanReportContract, parmBudgetPlanScenarioName));

        budgetPlanScenarioField.control().mandatory(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers the dialog field methods to capture events.
    /// </summary>
    public void postRun()
    {
        super();

        budgetPlanScenarioField = this.bindInfo().getDialogField(dataContract, methodStr(BudgetPlanReportContract, parmBudgetPlanScenarioName));

        // Override the lookup method for budget plan scenario
        budgetPlanScenarioField.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(BudgetPlanBudgetTransAuditUIBuilder, lookupBudgetPlanScenario), this);

        // Override the modified method for budget plan scenario
        budgetPlanScenarioField.registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(BudgetPlanBudgetTransAuditUIBuilder, modifiedBudgetPlanScenario), this);

        // Override the modified method for budget plan scenario
        budgetPlanScenarioField.registerOverrideMethod(
            methodStr(FormStringControl, validate),
            methodStr(BudgetPlanBudgetTransAuditUIBuilder, validateBudgetPlanScenario),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateBudgetPlanScenario</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the value of the budget plan scenario control.
    /// </summary>
    /// <param name="_budgetPlanScenarioControl">
    /// The budget plan scenario control.
    /// </param>
    /// <returns>
    /// true when the value is valid; otherwise, false.
    /// </returns>
    public boolean validateBudgetPlanScenario(FormStringControl _budgetPlanScenarioControl)
    {
        return BudgetPlanReportHelper::validateBudgetPlanScenario(_budgetPlanScenarioControl.text());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>