<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxReportCalculateFieldsAppendix2_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class TaxReportCalculateFieldsAppendix2_JP extends TaxReportCalculateFieldsAppendix_JP
{
    protected MainAccountNum collectedBadDebtAccountNum;
    protected Amount compensationAmount;
    protected RatioOfTaxableSales_JP ratioOfTaxableSales;

    protected const container Item9Fields = [#field_208, #field_214, #field_215, #field_8208, #field_8214, #field_8215];
    protected const container Item9CreditFields = [#field_9208, #field_9214, #field_9215, #field_7208, #field_7214, #field_7215];
    protected const container Item10Fields = [#field_8208, #field_8214, #field_8215];
    protected const container Item10CreditFields = [#field_7208, #field_7214, #field_7215];
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeCalculation</Name>
				<Source><![CDATA[
    protected void initializeCalculation()
    {
        super();

        collectedBadDebtAccountNum = LedgerDimensionFacade::getMainAccountIdFromLedgerDimension(LedgerBadDebtAccounts_JP::getLedgerCollectedBadDebtDimension());
        compensationAmount = 0;
        ratioOfTaxableSales = 0;

        this.calculateRatioOfTaxableSales();
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateRatioOfTaxableSales</Name>
				<Source><![CDATA[
    protected void calculateRatioOfTaxableSales()
    {
        Amount taxSalesAmount;
        Amount taxExemptSalesAmount;
        Amount nonTaxableExportAmount;
        Amount nonTaxableSalesAmount;
        Amount considerationAmount;

        MapEnumerator taxValueEnumerator = taxTypeValues.getEnumerator();
        while (taxValueEnumerator.moveNext())
        {
            TaxValue taxValue = taxValueEnumerator.currentKey();

            Map taxTypes = taxValueEnumerator.currentValue();
            MapEnumerator taxTypeEnumerator = taxTypes.getEnumerator();
            while (taxTypeEnumerator.moveNext())
            {
                TaxReportAmounts_JP amounts = TaxReportAmounts_JP::newFromAmountsMap(taxTypeEnumerator.currentValue());
                if (taxValue)
                {
                    taxSalesAmount += trunc(amounts.amount1() - amounts.amount9001());
                }
                else
                {
                    taxExemptSalesAmount += trunc(amounts.amount202() - amounts.amount9202());
                    nonTaxableExportAmount += trunc(amounts.amount203() - amounts.amount9203());
                    nonTaxableSalesAmount += trunc(amounts.amount206() - amounts.amount9206());
                }
            }
        }

        compensationAmount = taxSalesAmount + taxExemptSalesAmount + nonTaxableExportAmount;
        considerationAmount = compensationAmount + nonTaxableSalesAmount;
        ratioOfTaxableSales = considerationAmount ? round(compensationAmount / considerationAmount, 0.01) : 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateTaxValueType</Name>
				<Source><![CDATA[
    protected void calculateTaxValueType(TaxValue _taxValue, TaxType_JP _taxType, TaxReportAmounts_JP _amounts)
    {
        LedgerConsumptionTaxCalcTrans_JP taxCalcTrans = this.inititializeTaxCalcTrans(_taxValue, _taxType);

        if (_taxValue)
        {
            this.calculateTaxReportLine(taxCalcTrans, _amounts);
        }
        else
        {
            this.calculateTaxFreeReportLine(taxCalcTrans, _amounts);
        }

        taxCalcTrans.write();
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateTaxFreeReportLine</Name>
				<Source><![CDATA[
    protected void calculateTaxFreeReportLine(LedgerConsumptionTaxCalcTrans_JP _taxCalcTrans, TaxReportAmounts_JP _amounts)
    {
        _taxCalcTrans.RatioOfTaxableSales = ratioOfTaxableSales;

        _taxCalcTrans.CalcItem2 = trunc(_amounts.amount202() - _amounts.amount9202());
        _taxCalcTrans.CalcItem3 = trunc(_amounts.amount203() - _amounts.amount9203());
        _taxCalcTrans.CalcItem4 = _taxCalcTrans.CalcItem2 + _taxCalcTrans.CalcItem3;
        _taxCalcTrans.CalcItem5 = _taxCalcTrans.CalcItem4;
        _taxCalcTrans.CalcItem6 = trunc(_amounts.amount206() - _amounts.amount9206());
        _taxCalcTrans.CalcItem7 = _taxCalcTrans.CalcItem5 + _taxCalcTrans.CalcItem6;
        _taxCalcTrans.CalcItem8 = ratioOfTaxableSales;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateTaxReportLine</Name>
				<Source><![CDATA[
    private void calculateTaxReportLine(LedgerConsumptionTaxCalcTrans_JP _taxCalcTrans, TaxReportAmounts_JP _amounts)
    {
        _taxCalcTrans.RatioOfTaxableSales = ratioOfTaxableSales;

        _taxCalcTrans.CalcItem1 = trunc(_amounts.amount1() - _amounts.amount9001());
        _taxCalcTrans.CalcItem4 = _taxCalcTrans.CalcItem1;
        _taxCalcTrans.CalcItem5 = _taxCalcTrans.CalcItem4;
        _taxCalcTrans.CalcItem7 = _taxCalcTrans.CalcItem5;
        _taxCalcTrans.CalcItem8 = ratioOfTaxableSales;

        _taxCalcTrans.CalcItem9 = trunc(_amounts.sumFields(Item9Fields) - _amounts.sumFields(Item9CreditFields));
        _taxCalcTrans.CalcItem10 = trunc(_amounts.sumNationalFields(Item10Fields) - _amounts.sumNationalFields(Item10CreditFields));

        if (_taxCalcTrans.RatioOfTaxableSales < RatioTreshold)
        {
            if (_taxCalcTrans.TaxValue == 10)
            {
                _taxCalcTrans.CalcItem11 = trunc(_amounts.amount12() - _amounts.amount9012());
                _taxCalcTrans.CalcItem12 = trunc(_taxCalcTrans.CalcItem11 * 7.8 / 100);
            }
            else if (_taxCalcTrans.TaxValue == 8 && _taxCalcTrans.TaxType != TaxType_JP::Reduced)
            {
                _taxCalcTrans.CalcItem11 = trunc(_amounts.amount12() - _amounts.amount9012());
                _taxCalcTrans.CalcItem12 = trunc(_taxCalcTrans.CalcItem11 * 6.3 / 100);
            }
        }

        if (_taxCalcTrans.TaxValue == 10 || _taxCalcTrans.TaxValue == 8)
        {
            _taxCalcTrans.CalcItem13 = trunc(_amounts.nationalAmount8210() - _amounts.nationalAmount7210());
        }

        _taxCalcTrans.CalcItem14 = trunc(_amounts.nationalAmount8014() - _amounts.nationalAmount7014());
        _taxCalcTrans.CalcItem15 = _taxCalcTrans.CalcItem10 + _taxCalcTrans.CalcItem12 + _taxCalcTrans.CalcItem13 + _taxCalcTrans.CalcItem14;

        if (compensationAmount <= SalesTreshold || _taxCalcTrans.RatioOfTaxableSales >= RatioTreshold)
        {
            _taxCalcTrans.CalcItem16 = _taxCalcTrans.CalcItem15;
            _taxCalcTrans.CalcItem24 = _taxCalcTrans.CalcItem16;
        }
        else if (_taxCalcTrans.CalcMethod == CalcMethod_JP::IndividualMethod)
        {
            _taxCalcTrans.CalcItem17 = trunc(_amounts.nationalAmount8208() - _amounts.nationalAmount7208());
            _taxCalcTrans.CalcItem18 = trunc(_amounts.nationalAmount8215() - _amounts.nationalAmount7215());

            _taxCalcTrans.CalcItem19 = _taxCalcTrans.CalcItem17 + trunc(_taxCalcTrans.CalcItem18 * _taxCalcTrans.RatioOfTaxableSales);
            _taxCalcTrans.CalcItem24 = _taxCalcTrans.CalcItem19;
        }
        else
        {
            _taxCalcTrans.CalcItem20 = trunc(_taxCalcTrans.CalcItem15 * _taxCalcTrans.RatioOfTaxableSales);
            _taxCalcTrans.CalcItem24 = _taxCalcTrans.CalcItem20;
        }

        _taxCalcTrans.CalcItem21 = trunc(_amounts.nationalAmount8221() - _amounts.nationalAmount7221());
        _taxCalcTrans.CalcItem22 = trunc(_amounts.nationalAmount8222() - _amounts.nationalAmount7222());
        _taxCalcTrans.CalcItem23 = trunc(_amounts.nationalAmount8223());
        _taxCalcTrans.CalcItem24 += _taxCalcTrans.CalcItem21 + _taxCalcTrans.CalcItem22 + _taxCalcTrans.CalcItem23;

        if (_taxCalcTrans.CalcItem24 < 0)
        {
            _taxCalcTrans.CalcItem25 = -_taxCalcTrans.CalcItem24;
            _taxCalcTrans.CalcItem24 = 0;
        }

        Amount amount8310 = _amounts.nationalAmount8310();

        if (!amount8310)
        {
            amount8310 = TaxReportCalculateFields_JP::calculateNationalTaxAmount(
                _taxCalcTrans.TaxValue,
                _taxCalcTrans.TaxType, 
                -this.getTaxAmountForMainAccount(collectedBadDebtAccountNum, _taxCalcTrans.TaxValue, _taxCalcTrans.TaxType));
        }

        _taxCalcTrans.CalcItem26 = trunc(amount8310);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    internal static TaxReportCalculateFieldsAppendix2_JP newFromParameters(
        TaxPeriod _taxPeriod,
        FromDate _fromDate,
        ToDate _toDate,
        TypeOfDeclaration_JP _typeOfDeclaration,
        boolean _amendment,
        Map _taxTypeValues,
        CalcMethod_JP _calcMethod,
        TaxReportDataLayout_JP _dataLayout)
    {
        if (!_taxTypeValues)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        TaxReportCalculateFieldsAppendix2_JP instance = new TaxReportCalculateFieldsAppendix2_JP();

        instance.taxPeriod = _taxPeriod;
        instance.fromDate = _fromDate;
        instance.toDate = _toDate;
        instance.typeOfDeclaration = _typeOfDeclaration;
        instance.amendment = _amendment;
        instance.taxTypeValues = _taxTypeValues;
        instance.calcMethod = _calcMethod;
        instance.dataLayout = _dataLayout;

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    internal static void calculate(
        TaxPeriod _taxPeriod,
        FromDate _fromDate,
        ToDate _toDate,
        TypeOfDeclaration_JP _typeOfDeclaration,
        boolean _amendment,
        Map _taxTypeValues,
        CalcMethod_JP _calcMethod,
        TaxReportDataLayout_JP _dataLayout)
    {
        TaxReportCalculateFieldsAppendix2_JP::newFromParameters(
            _taxPeriod,
            _fromDate,
            _toDate,
            _typeOfDeclaration,
            _amendment,
            _taxTypeValues,
            _calcMethod,
            _dataLayout).calculateTaxReportSheet();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>