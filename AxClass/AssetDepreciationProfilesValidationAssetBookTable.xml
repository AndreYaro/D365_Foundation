<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepreciationProfilesValidationAssetBookTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class contains business logic for depreciation profiles and
/// related fields validation on <c>AssetBookTable</c> table.
/// </summary>
[AssetDepreciationProfilesValidationAttribute(tablestr(AssetBookTable))]
final public class AssetDepreciationProfilesValidationAssetBookTable extends AssetDepreciationProfilesValidationBooks
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateDepreciationConvention</Name>
				<Source><![CDATA[
    public boolean validateDepreciationConvention()
    {
        boolean ret = true;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validatePostingLayer</Name>
				<Source><![CDATA[
    public boolean validatePostingLayer()
    {
        boolean ret = super();

        if (this.assetBookTable().postToGeneralLedger() == NoYes::No)
        {
            ret = ret && this.checkDepreciationProfileIsNotPolish_PL();
            ret = ret && this.checkDepreciationProfileIsNotRBSL_FR();
            ret = ret && this.checkDepreciationProfileAccrualIsNotDaily();

            ret = ret && this.checkDepreciationAltProfileIsNotPolish_PL();
            ret = ret && this.checkDepreciationAltProfileIsNotRBSL_FR();
            ret = ret && this.checkDepreciationAltProfileAccrualIsNotDaily();

            ret = ret && this.checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBookGroupSetup();
            ret = ret && this.checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBook();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>assetBookTable</Name>
				<Source><![CDATA[
    protected AssetBookTable assetBookTable()
    {
        return assetDepreciationProfilesValidationMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBookGroupSetup</Name>
				<Source><![CDATA[
    private boolean checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBookGroupSetup()
    {
        boolean ret = true;

        if (this.assetBookTable().BookId &&
            this.assetBookTable().postToGeneralLedger() == NoYes::No)
        {
            AssetGroupBookSetup      assetGroupBookSetup;
            AssetDepreciationProfile assetDepreciationProfile;

            while select assetGroupBookSetup
                where assetGroupBookSetup.BookId == this.assetBookTable().BookId
            join assetDepreciationProfile
                where assetDepreciationProfile.Profile == assetGroupBookSetup.DepreciationProfile
                   || assetDepreciationProfile.Profile == assetGroupBookSetup.DepreciationAltProfile
            {
                ret = ret && this.checkDepreciationMethodIsNotPolish_PL(assetDepreciationProfile.Method);
                ret = ret && this.checkDepreciationMethodIsNotRBSL_FR(assetDepreciationProfile.Method);
                ret = ret && this.checkDepreciationAccrualIsNotDaily(assetDepreciationProfile.Accrual);
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBook</Name>
				<Source><![CDATA[
    private boolean checkDepreciationMethodAndAccrualCompatibleWithPostingLayerOnBook()
    {
        boolean ret = true;

        if (this.assetBookTable().BookId &&
            this.assetBookTable().postToGeneralLedger() == NoYes::No)
        {
            AssetBook assetBook;
            AssetDepreciationProfile assetDepreciationProfile;

            while select assetBook
                where assetBook.BookId == this.assetBookTable().BookId
            join assetDepreciationProfile
                where assetDepreciationProfile.Profile == assetBook.DepreciationProfile
                   || assetDepreciationProfile.Profile == assetBook.DepreciationAltProfile
            {
                ret = ret && this.checkDepreciationMethodIsNotPolish_PL(assetDepreciationProfile.Method);
                ret = ret && this.checkDepreciationMethodIsNotRBSL_FR(assetDepreciationProfile.Method);
                ret = ret && this.checkDepreciationAccrualIsNotDaily(assetDepreciationProfile.Accrual);
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>