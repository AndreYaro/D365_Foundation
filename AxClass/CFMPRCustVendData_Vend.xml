<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPRCustVendData_Vend</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class gathers generalized vendor information needed for payment request creation.
/// </summary>
[CFMPaymentRequestSourceAttribute(tableStr(VendTransOpen))]
public class CFMPRCustVendData_Vend extends CFMPRCustVendData
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    protected void initialize(CustVendTransOpen _sourceTransOpen)
    {
        #ISOCountryRegionCodes
        VendTransOpen vendTransOpen = _sourceTransOpen;

        accountType = CFMAccountType::Vend;
        sourceType = CFMPaymentRequestSourceType::VendInvoice;
        sourceTrans = VendTrans::find(_sourceTransOpen.RefRecId);
        sourcePaymModeTable = VendPaymModeTable::find(sourceTrans.PaymMode);
        sourceInvoiceJour = VendInvoiceJour::findFromVendTransVoucher(sourceTrans.Invoice,
            sourceTrans.Voucher,
            sourceTrans.TransDate,
            sourceTrans.AccountNum);
        thirdPartyBankAccountId = vendTransOpen.ThirdPartyBankAccountId;
        prepayment = sourceTrans.Prepayment;

        VendInvoiceJour vendInvoiceJour;
        if (sourceInvoiceJour)
        {
            vendInvoiceJour = sourceInvoiceJour;
            prepayment = prepayment || vendInvoiceJour.InvoiceType == PurchInvoiceType::VendorAdvance;
        }

        if (sourceInvoiceJour && sourceInvoiceJour.Payment)
        {
            requestType = this.paymentRequestTypeFromPaymTerm(PaymTerm::find(sourceInvoiceJour.Payment), prepayment);
        }

        if (!requestType)
        {
            requestType = prepayment ? CFMParameters::findInCentralizedPaymentsCompany().SupplierPrepayment : CFMParameters::findInCentralizedPaymentsCompany().IndebtednessToVendor;

            if (!requestType)
            {
                throw error(strFmt("@CashFlowManagement:OneOrMorePaymRequestTypeMissing","@SYS336089"));
            }
        }

        RefRecId agreementHeader;

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            agreementHeader = AgreementHeaderExt_RU::findByDefaultDimension(sourceTrans.DefaultDimension).AgreementHeader;
        }
        else
        {
            if (sourceInvoiceJour && isConfigurationkeyEnabled(configurationKeyNum(PublicSector)))
            {
                agreementHeader = vendInvoiceJour.PurchAgreementHeader_PSN;
            }
        }

        agreementId = PurchAgreementHeader::find(agreementHeader).PurchNumberSequence;

        agreementPaymentOrder = CFMAgreementPaymentOrder::findByAgreement(agreementHeader, SalesPurch::Purch);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>