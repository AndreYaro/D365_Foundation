<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAccountStatementExtDPTmpTableCopyMapBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustAccountStatementExtDPTmpTableCopyMapBuilder</c> class creates a field map to use with
/// <c>Query::insert_recordset</c> method that copies data from one tmp table instance to another
/// instance of the same tmp table.
/// </summary>
public class CustAccountStatementExtDPTmpTableCopyMapBuilder
{
    private Map fieldMap;
    private QueryBuildFieldList fieldList;
    private int sourceTableUniqueId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new ()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromQueryBuildDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the class from the query build data source.
    /// </summary>
    /// <param name = "_qbds">The query build data source usded for initialization.</param>
    protected void initFromQueryBuildDataSource(QueryBuildDataSource _qbds)
    {
        fieldMap = new Map(Types::String, Types::Container);
        fieldList = _qbds.fields();
        sourceTableUniqueId = _qbds.uniqueId();
    }

]]></Source>
			</Method>
			<Method>
				<Name>contruct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>CustAccountStatementExtDPTmpTableCopyMapBuilder</c>.
    /// </summary>
    /// <param name = "_qbds">The query data source used to construct the mapping.</param>
    /// <returns>An instance of <c>CustAccountStatementExtDPTmpTableCopyMapBuilder</c>.</returns>
    public static CustAccountStatementExtDPTmpTableCopyMapBuilder contruct(QueryBuildDataSource _qbds)
    {
        if (_qbds.table() != tableNum(CustAccountStatementExtTmp))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        CustAccountStatementExtDPTmpTableCopyMapBuilder mapBuilder = new CustAccountStatementExtDPTmpTableCopyMapBuilder();
        mapBuilder.initFromQueryBuildDataSource(_qbds);

        return mapBuilder;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addFieldToMap</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a field to the field map.
    /// </summary>
    /// <param name = "_fieldId">The field to add to the map.</param>
    public void addFieldToMap(FieldId _fieldId)
    {
        this.createFieldMapping(_fieldId, fieldId2Name(tableNum(CustAccountStatementExtTmp), _fieldId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldMapping</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the constructed field map.
    /// </summary>
    /// <returns>The field map.</returns>
    public Map getFieldMapping()
    {
        return fieldMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createFieldMapping</Name>
				<Source><![CDATA[
    private void createFieldMapping(FieldId _sourceFieldId, FieldName _fieldNameToCopy)
    {
        fieldList.addField(_sourceFieldId);
        fieldMap.insert(_fieldNameToCopy, [sourceTableUniqueId, _fieldNameToCopy]);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>