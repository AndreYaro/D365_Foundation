<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailLabelChangeJournalConfirmator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Performs label change journal confirmation.
/// </summary>
/// <remarks>
/// Sets confirmed flag in al the lines, changes the journal header status to Confirmed and sets confirm date to current system date.
/// </remarks>
class RetailLabelChangeJournalConfirmator
{
    RetailLabelChangeJournalId journalNum;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>confirmJournal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Confirms label change journal.
    /// </summary>
    /// <remarks>
    /// Updates all the lines setting confirmed field to Yes, changes the status of journal to Confirmed and puts current system date to confirmed field.
    /// </remarks>
    /// <exception cref="Exception::Error">
    /// Throws exception if label change journal header is not found.
    /// </exception>
    /// <exception cref="Exception::Error">
    /// Throws error if Worker field is not specified.
    /// </exception>
    private void confirmJournal()
    {
        RetailLabelChangeJournalTable journalTable;
        RetailLabelChangeJournalTrans journalTrans;

        ttsBegin;

        journalTable = RetailLabelChangeJournalTable::find(journalNum, true);
        if (!journalTable)
        {
            throw error(strFmt(RetailLabelChangeJournalTable::txtNotExist(), journalNum));
        }

        if (!journalTable.HcmWorker)
        {
            throw error("@SYP4880942");
        }

        update_recordSet journalTrans
            setting Confirmed = NoYes::Yes
            where journalTrans.JournalNum == journalTable.JournalNum;

        journalTable.Status = RetailLabelChangeJournalStatus::Confirmed;
        journalTable.ConfirmDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        journalTable.Active = NoYes::Yes;
        journalTable.update();

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of <c>RetailLabelChangeJournalConfirmator</c>.
    /// </summary>
    /// <param name="_journalNum">
    /// Label change journal number.
    /// </param>
    private void new(RetailLabelChangeJournalId _journalNum)
    {
        this.parmJournalNum(_journalNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmJournalNum</Name>
				<Source><![CDATA[
    public RetailLabelChangeJournalId parmJournalNum(RetailLabelChangeJournalId _journalNum = journalNum)
    {
        journalNum = _journalNum;
        return journalNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static RetailLabelChangeJournalConfirmator construct(RetailLabelChangeJournalId _journalNum)
    {
        return new RetailLabelChangeJournalConfirmator(_journalNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYP4880967";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        RetailLabelChangeJournalConfirmator confirmator;
        RetailLabelChangeJournalTable journalTable;

        if (!args || !args.record() || args.dataset() != tableNum(RetailLabelChangeJournalTable))
        {
            throw error(Error::missingRecord(RetailLabelChangeJournalConfirmator::description()));
        }

        journalTable = args.record();
        confirmator = RetailLabelChangeJournalConfirmator::newOnServer(journalTable.JournalNum);
        confirmator.confirmJournal();

        RetailLabelChangeJournalConfirmator::refreshFormDataSource(journalTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newOnServer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>RetailLabelChangeJournalConfirmator</c> on Server.
    /// </summary>
    /// <param name="_journalNum">
    /// Label change journal number.
    /// </param>
    /// <returns>
    /// An instance of <c>RetailLabelChangeJournalConfirmator</c>.
    /// </returns>
    /// <remarks>
    /// Creates an instance of a class on Server.
    /// </remarks>
    public static RetailLabelChangeJournalConfirmator newOnServer(RetailLabelChangeJournalId _journalNum)
    {
        return RetailLabelChangeJournalConfirmator::construct(_journalNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshFormDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Refreshes form data source by a given record.
    /// </summary>
    /// <param name="_journalTable">
    /// Label change journalTable.
    /// </param>
    /// <remarks>
    /// Checks whether the given record comes from a form data source , rereads the record on the data source and refreshes bound controls.
    /// Method is run on Client.
    /// </remarks>
    private static void refreshFormDataSource(RetailLabelChangeJournalTable _journalTable)
    {
        FormDataSource fds;
        if (_journalTable && FormDataUtil::isFormDataSource(_journalTable))
        {
            fds = FormDataUtil::getFormDataSource(_journalTable);
            if (fds)
            {
                fds.reread();
                fds.refresh();
                fds.active();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
