<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PrintMgmt_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PrintMgmt_ApplicationSuite_Extension</c> class extends the logic of the <c>PrintMgmt</c> class.
/// </summary>
[ExtensionOf(classStr(PrintMgmt))]
public final class PrintMgmt_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSettings</Name>
				<Source><![CDATA[
    public static PrintMgmtPrintSetting getSettings(PrintMgmtPrintContext _printContext)
    {
        PrintMgmtPrintSetting ret = next getSettings(_printContext);

        if (ret.numDetails() > 0)
        {
            List hierarchyContextList = new List(Types::Class);
            // Retrieve the settings from the database
            hierarchyContextList.addEnd(_printContext.getHierarchyContext());

            PrintMgmt printMgmt = new PrintMgmt();
            PrintMgmtNodeInstance rootInstance = printMgmt.getNodeInstances(hierarchyContextList, _printContext.parmLanguageId());

            // Get the leaf instance which has all of the necessary settings
            PrintMgmtNodeInstance leafInstance = rootInstance.getLeaf();

            int detailsPos = 1;
            // Evaluate settings to determine if they should be used
            MapEnumerator setupDocEnumerator = leafInstance.getSetupDocuments().getEnumerator();
            while (setupDocEnumerator.moveNext())
            {
                PrintMgmtSetupDoc setupDoc = setupDocEnumerator.currentValue();
                for (int i = 1; i <= setupDoc.numInstances(); i++)
                {
                    PrintMgmtSetupDocInstance instance = setupDoc.getInstanceByPos(i);

                    if (instance.parmSuppress() == NoYes::No)
                    {
                        boolean conditionalFound = false;
                        if (instance.numConditionalSettings() > 0)
                        {
                            var isTmpRecord = printMgmt.checkIsProforma(_printContext.parmQueryTableBuffer()) &&
                                      !(printMgmt.existsInDataBase(_printContext.parmQueryTableBuffer()));

                            Common queryTableBufferTmp;

                            if (isTmpRecord)
                            {
                                DictTable dictTable = new DictTable(_printContext.parmQueryTableBuffer().TableId);
                                queryTableBufferTmp = dictTable.makeRecord();
                                // BP Deviation Documented
                                queryTableBufferTmp.setTmp();
                                Global::buf2Buf(_printContext.parmQueryTableBuffer(), queryTableBufferTmp);
                                queryTableBufferTmp.doInsert();
                            }

                            // Check to see if any of the conditional settings apply.
                            // Start with the lowest priority and stop as soon as one is found.
                            for (int x = 1; x <= instance.numConditionalSettings(); x++)
                            {
                                PrintMgmtSetupSettingsConditional conditionalSetting = instance.getConditionalSettingByPos(x);
                                if (!conditionalSetting.parmSuppress())
                                {
                                    if (printMgmt.doesQueryHaveResults(isTmpRecord ? queryTableBufferTmp : _printContext.parmQueryTableBuffer(), conditionalSetting.parmCondition(), isTmpRecord))
                                    {
                                        PrintMgmtPrintSettingDetail details = ret.getDetailByPos(detailsPos);

                                        details.parmNamedDestination(conditionalSetting.parmNamedDestination());
                                        conditionalFound = true;
                                        detailsPos++;
                                        break;
                                    }
                                }
                            }
                        }
                     
                        // Add default if no conditional was valid and we are not suppressing the document
                        PrintMgmtSetupSettingsDefault defaultSetting = instance.parmDefaultSetting();
                        if (!conditionalFound && !defaultSetting.parmSuppress())
                        {
                            PrintMgmtPrintSettingDetail details = ret.getDetailByPos(detailsPos);
                            details.parmNamedDestination(defaultSetting.parmNamedDestination());
                            detailsPos++;
                        }
                    }
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>