<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInterestNoteDP_CH_QRBill_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// An extension class of Cust interest note report data provider to fill QR-Bill specific fields in the GIRO report.
/// </summary>
[ExtensionOf(classStr(CustInterestNoteDP))]
public final class CustInterestNoteDP_CH_QRBill_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>populateMoneyTransferSlip</Name>
				<Source><![CDATA[
    protected void populateMoneyTransferSlip(CustInterestJour _custInterestJour)
    {
        next populateMoneyTransferSlip(_custInterestJour);

        if (giroType == PaymentStub::QRBill)
        {
            moneyTransferSlip.initFromBankAccountTable(_custInterestJour.paymentBankAccountId());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateTempTableForPaymentSlip</Name>
				<Source><![CDATA[
    protected void populateTempTableForPaymentSlip(CustInterestJour _custInterestJour, CustInterestTrans _custInterestTrans)
    {
        next populateTempTableForPaymentSlip(_custInterestJour, _custInterestTrans);

        if (giroType == PaymentStub::QRBill)
        {
            custInterestNoteTmp.InterestAmountTotal = _custInterestJour.sumInterestAmountFeeInclTax();
            custInterestNoteTmp.IBAN_CH = moneyTransferSlip.getQRIBANOrIBAN(_custInterestJour.PaymId);
            custInterestNoteTmp.currencyCodeISO_CH = Currency::currencyCodeISO(_custInterestJour.CurrencyCode);

            LogisticsPostalAddress collectionAddress = LogisticsPostalAddress::findRecId(_custInterestJour.PostalAddress);
            custInterestNoteTmp.CustomerAddressLine1_CH = strRemoveCr(collectionAddress.Street);
            custInterestNoteTmp.CustomerAddressLine2_CH = strFmt('%1 %2', collectionAddress.ZipCode, collectionAddress.City);
            custInterestNoteTmp.CustomerCountryRegionId_CH = collectionAddress.CountryRegionId;
            custInterestNoteTmp.OcrField = _custInterestJour.PaymId;

            custInterestNoteTmp.BillInformation_CH = CustQRBillQRCodeCreator_CH::getBillInformation(_custInterestJour);
            custInterestNoteTmp.LanguageId_CH = _custInterestJour.LanguageId;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>