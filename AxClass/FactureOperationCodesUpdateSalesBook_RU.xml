<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FactureOperationCodesUpdateSalesBook_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class for the facture operation code update procedure run for sales book lines.
/// </summary>
class FactureOperationCodesUpdateSalesBook_RU extends FactureOperationCodesUpdate_RU
{
    SalesPurchBookRecId_RU  bookRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBuffer</Name>
				<Source><![CDATA[
    protected boolean checkBuffer(Common _common)
    {
        return _common.TableId == tablenum(SalesBookTrans_RU);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldIdFactureDate_External</Name>
				<Source><![CDATA[
    protected FieldId getFieldIdFactureDate_External(Common _common)
    {
        if (!this.checkBuffer(_common))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return fieldnum(SalesBookTrans_RU, FactureDate_External);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldIdFactureExternalId</Name>
				<Source><![CDATA[
    protected FieldId getFieldIdFactureExternalId(Common _common)
    {
        if (!this.checkBuffer(_common))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return fieldnum(SalesBookTrans_RU, FactureExternalId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFieldIdOperationTypeCodes</Name>
				<Source><![CDATA[
    protected FieldId getFieldIdOperationTypeCodes(Common _common = null)
    {
        if (_common && !this.checkBuffer(_common))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        return fieldnum(SalesBookTrans_RU, OperationTypeCodes);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromArgs</Name>
				<Source><![CDATA[
    protected void initFromArgs(Args _args)
    {
        SalesBookTable_RU salesBookTable;

        super(_args);

        if (_args.dataset() != tablenum(SalesBookTable_RU))
        {
            throw error(Error::missingParameter(this));
        }

        salesBookTable = _args.record();
        bookRecId = salesBookTable.recId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    public void initParmDefault()
    {
        Query query = new Query();
        QueryBuildDataSource qbdsBookTrans, qbdsFactureJour;

        super();

        qbdsBookTrans = query.addDataSource(tablenum(SalesBookTrans_RU));
        findOrCreateRange_W(qbdsBookTrans, fieldnum(SalesBookTrans_RU, SalesBookTable_RU), queryValue(bookRecId)).status(RangeStatus::Locked);
        qbdsBookTrans.addRange(fieldnum(SalesBookTrans_RU, OperationTypeCodes));
        qbdsBookTrans.addRange(fieldnum(SalesBookTrans_RU, FactureExternalId));
        qbdsBookTrans.addRange(fieldnum(SalesBookTrans_RU, TransType));

        qbdsFactureJour = qbdsBookTrans.addDataSource(tablenum(FactureJour_RU));
        qbdsFactureJour.relations(false);
        qbdsFactureJour.addLink(fieldnum(SalesBookTrans_RU, FactureId), fieldnum(FactureJour_RU, FactureId));
        qbdsFactureJour.joinMode(JoinMode::ExistsJoin);
        qbdsFactureJour.addRange(fieldnum(FactureJour_RU, InventProfileType)).value(queryValue(InventProfileType_RU::General));

        queryRun = new QueryRun(query);

        custVend = ModuleCustVend::Cust;
    }

]]></Source>
			</Method>
			<Method>
				<Name>linesForm</Name>
				<Source><![CDATA[
    protected str linesForm()
    {
        return formStr(SalesBookTrans_RU);
    }

]]></Source>
			</Method>
			<Method>
				<Name>linesFormBuffer</Name>
				<Source><![CDATA[
    protected Common linesFormBuffer()
    {
        return SalesBookTable_RU::find(bookRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>packIL</Name>
				<Source><![CDATA[
    protected container packIL()
    {
        return [currentVersionIL, super(), bookRecId];
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        boolean ret;

        ret = super(_packedClass);

        if (ret && queryRun)
        {
            findOrCreateRange_W(
            queryRun.query().dataSourceTable(tablenum(SalesBookTrans_RU)),
            fieldnum(SalesBookTrans_RU, SalesBookTable_RU), queryValue(bookRecId));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpackIL</Name>
				<Source><![CDATA[
    protected boolean unpackIL(container _packedILClass)
    {
        Integer     version = RunBase::getVersion(_packedILClass);
        boolean     ret = true;
        container   superILPacked;

        switch (version)
        {
            case currentVersionIL:
                [version, superILPacked, bookRecId] = _packedILClass;

                ret = super(superILPacked);

                break;
            default:
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>