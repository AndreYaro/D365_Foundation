<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResProductEntityIdentifiersCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The product entity identifiers creator class.
/// </summary>
public class EcoResProductEntityIdentifiersCreator
{
    private EcoResProduct           product;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>EcoResProductEntityIdentifiersCreator</c> class.
    /// </summary>
    /// <returns>The new instance of the <c>EcoResProductEntityIdentifiersCreator</c> class.</returns>
    protected static EcoResProductEntityIdentifiersCreator construct()
    {
        return new EcoResProductEntityIdentifiersCreator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromProduct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>EcoResProductEntityIdentifiersCreator</c> class.
    /// </summary>
    /// <param name = "_product">The product record.</param>
    /// <returns>The new instance of the <c>EcoResProductEntityIdentifiersCreator</c> class.</returns>
    public static EcoResProductEntityIdentifiersCreator newFromProduct(EcoResProduct _product)
    {
        if (!_product)
        {
            throw error(Error::missingRecord(tableStr(EcoResProduct)));
        }

        EcoResProductEntityIdentifiersCreator creator = EcoResProductEntityIdentifiersCreator::construct();
        creator.setProduct(_product);

        return creator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setProduct</Name>
				<Source><![CDATA[
    private void setProduct(EcoResProduct _product)
    {
        /*
            A product entity is either the product or the product variant, but not the product master. Hence we do not want to create records in the product entity
            identifier table for the product master.
        */
        if (_product.productSubtype() != EcoResProductSubtype::ProductMaster)
        {
            product = _product;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createAllIdentifiers</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates all the identifiers for the current product, based on the already created identifier code records.
    /// </summary>
    public void createAllIdentifiers()
    {
        if (product)
        {
            EcoResProductEntityIdentifierCode code;

            ttsbegin;

            while select code
            {
                EcoResProductEntityIdentifierCreator creator = EcoResProductEntityIdentifierCreatorFactory::newCreatorFromProductAndIdentifierCode(product, code);
                creator.create();
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>