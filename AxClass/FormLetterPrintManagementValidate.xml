<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FormLetterPrintManagementValidate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>FormLetterPrintManagementValidate</c> class acts as helper to validate the print management setup.
/// </summary>
public class FormLetterPrintManagementValidate
{
    public str printerSetupErrorText;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkClientOutputPrintManagement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether the selected printers in print management that are used for printing the original
    /// reports and the copies of reports are using client output options.
    /// </summary>
    /// <param name = "_printMgmtDocumentType">The document type.</param>
    /// <param name = "_printMgmtNodeType">The print management node type.</param>
    /// <returns>
    /// true if the selected printers are using client output options; otherwise, false.
    /// </returns>
    /// <remarks>
    /// The method is used for finding the default printer setup in print management for the specific
    /// document.
    /// </remarks>
    public boolean checkClientOutputPrintManagement(PrintMgmtDocumentType _printMgmtDocumentType, PrintMgmtNodeType _printMgmtNodeType)
    {
        PrintMgmtDocInstance    printMgmtDocInstance;
        PrintMgmtSettings       printMgmtSettings;
        boolean onClient = false;

        printerSetupErrorText = '';
        while select PrintJobSettings from printMgmtSettings
        where
            printMgmtSettings.Description == ''
        join PrintType from printMgmtDocInstance
        where printMgmtDocInstance.RecId             == printMgmtSettings.ParentId
            && printMgmtDocInstance.ReferencedRecId   == 0
            && printMgmtDocInstance.ReferencedTableId == 0
            && printMgmtDocInstance.DocumentType      == _printMgmtDocumentType
            && printMgmtDocInstance.NodeType          == _printMgmtNodeType
        {
            if (FormLetter::isClientOutput(printMgmtSettings.PrintJobSettings))
            {
                printerSetupErrorText += strFmt("@SYS118704", printMgmtDocInstance.PrintType) + '\n';
                onClient = true;
            }
        }

        return onClient;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrinterSettingsMultiThread</Name>
				<Source><![CDATA[
    public boolean checkPrinterSettingsMultiThread(boolean _usePrintManagement, boolean _printerSetupError, boolean _checkClientOutput, PrintMgmtDocumentType _printMgmtDocumentType, PrintMgmtNodeType _printMgmtNodeType)
    {
        if (_usePrintManagement)
        {
            if (_printerSetupError)
            {
                checkFailed("@SYS118708");
                return checkFailed("@SYS21533");
            }
            else
            {
                if (this.checkClientOutputPrintManagement(_printMgmtDocumentType, _printMgmtNodeType))
                {
                    if (Box::yesNo(strFmtLB(printerSetupErrorText), DialogButton::No) == DialogButton::No)
                    {
                        return checkFailed("@SYS21533");
                    }
                }
            }
        }
        else
        {
            if (_checkClientOutput)
            {
                checkFailed("@SYS118708");
                return checkFailed("@SYS21533");
            }
        }
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>