<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxGSTPaymentController_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>GSTPaymentController_IN</c> class is the helper class for the associated SRS Report.
/// </summary>
public class TaxGSTPaymentController_IN extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getReportName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets and set the report name.
    /// </summary>
    /// <param name="_reportName">
    /// Report name.
    /// </param>
    /// <returns>
    /// Returns the report name.
    /// </returns>
    public SRSCatalogItemName getReportName(SRSCatalogItemName _reportName = reportName)
    {
        reportName = _reportName;
        return reportName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>printReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public void printReport(Args _args)
    {
        LedgerJournalTrans                  ledgerJournalTrans;
        GSTInvoiceContract_IN               gstInvoiceContract;
        taxEngineLedgerJournalTransHeader   taxEngineLedgerJournalTransHeader;
        taxDocumentExtension_in             taxDocumentExtension;
        TaxDocument                         taxDocument;

        if(_args.record())
        {
            ledgerJournalTrans = _args.record();

            select firstOnly TableId, RecId from taxEngineLedgerJournalTransHeader
                where taxEngineLedgerJournalTransHeader.JournalNum == ledgerJournalTrans.JournalNum
                    && taxEngineLedgerJournalTransHeader.Voucher    == ledgerJournalTrans.Voucher;

            taxDocument = TaxDocumentUtils::getTaxDocumentBySource(taxEngineLedgerJournalTransHeader.TableId, taxEngineLedgerJournalTransHeader.RecId);

            if (taxDocument)
            {
                taxDocumentExtension = taxDocumentExtension_in::findByTaxDocument(taxDocument.TaxDocumentGuid);
            }
        }

        if (taxDocumentExtension.TaxInvReferenceType != GSTReference_IN::None)
        {
            this.getReportName(ssrsReportStr(GSTInvoiceReport_IN, Report));
            this.parmArgs(_args);
            gstInvoiceContract = this.parmReportContract().parmRdpContract();
            gstInvoiceContract.parmRecordId(ledgerJournalTrans.RecId);
            gstInvoiceContract.parmTableId(tableNum(LedgerJournalTrans));
            gstInvoiceContract.parmGSTInvoiceControllerName(classId2Name(classIdGet(this)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initailzes the controller, assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        TaxGSTPaymentController_IN formLetterController = new TaxGSTPaymentController_IN();

        formLetterController.printReport(_args);

        if (formLetterController.getReportName())
        {
            formLetterController.parmReportName(formLetterController.getReportName());
            formLetterController.parmShowDialog(false);
            formLetterController.startOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>