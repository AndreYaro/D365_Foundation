<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLineCustInvTransRetImpl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for <c>CustInvoiceTrans</c>.
/// </summary>
[
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::Credit),
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::CreditOnly),
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::ReplaceCredit),
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::ReplaceScrap),
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::ReturnToCust),
TaxModelDocumentLineReturnAttribute(tableStr(CustInvoiceTrans), DispositionAction::Scrap)
]
public class TaxModelDocLineCustInvTransRetImpl_IN extends TaxModelDocLineCustInvoiceTransImpl_IN
    implements TaxModelTaxableReturn
{
    //Add for interface conversion.
    TaxModelTaxableReturn referenceReturnModel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDispositionAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>DispositionAction</c> of the transaction.
    /// </summary>
    /// <returns>
    /// the <c>DispositionAction</c> of the transaction
    /// </returns>
    public DispositionAction getDispositionAction()
    {
        return referenceReturnModel.getDispositionAction();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExciseTaxAccountType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets tax account type based on the given <c>TaxOnItem</c>
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c>
    /// </param>
    /// <returns>
    /// The tax account type.
    /// </returns>
    public TaxAccountType_IN getExciseTaxAccountType(TaxOnItem _taxOnItem)
    {
        TaxAccountType_IN ret;

        switch (this.getDispositionAction())
        {
            case DispositionAction::ReturnToCust :
            case DispositionAction::Credit :
            case DispositionAction::ReplaceScrap :
            case DispositionAction::ReplaceCredit :
                ret = TaxAccountType_IN::ExciseRecoverableAccount;
                break;
            case DispositionAction::CreditOnly :
            case DispositionAction::Scrap :
                ret = TaxAccountType_IN::ExciseExpenseAccount;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOrignalInvoiceTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the original invoice transaction.
    /// </summary>
    /// <returns>
    /// The orignal invoice.
    /// </returns>
    public TaxModelTaxable getOrignalInvoiceTrans()
    {
        return referenceReturnModel.getOrignalInvoiceTrans();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSalesTaxAccountType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets tax account type based on the given <c>TaxOnItem</c>
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c>
    /// </param>
    /// <returns>
    /// The tax account type.
    /// </returns>
    public TaxAccountType_IN getSalesTaxAccountType(TaxOnItem _taxOnItem)
    {
        TaxAccountType_IN ret;

        switch (this.getDispositionAction())
        {
            case DispositionAction::ReturnToCust :
            case DispositionAction::Credit :
            case DispositionAction::ReplaceScrap :
            case DispositionAction::ReplaceCredit :
                ret = TaxAccountType_IN::SalesTaxPayableAccount;
                break;
            case DispositionAction::CreditOnly :
            case DispositionAction::Scrap :
                ret = TaxAccountType_IN::SalesTaxExpenseAccount;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax direction.
    /// </summary>
    /// <returns>
    /// The tax direction
    /// </returns>
    public TaxDirection getTaxDirection()
    {
        if (TaxSolutionScopeIntegrationUtil::isCompanyEnabled())
        {
            return TaxDirection::OutgoingTax;
        }

        return TaxDirection::IncomingTax;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxRateBasingDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax rate basing date for the given tax type.
    /// </summary>
    /// <param name="_taxType">
    /// The given tax type.
    /// </param>
    /// <returns>
    /// The tax rate date.
    /// </returns>
    public TransDate getTaxRateBasingDate(TaxType_IN _taxType)
    {
        if (this.isTaxPerOrignal())
        {
            TaxModelTaxable_IN taxModelTaxableIN = this.getOrignalInvoiceTrans() as TaxModelTaxable_IN;
            return taxModelTaxableIN.getTaxRateBasingDate(_taxType);
        }
        else
        {
            return super(_taxType);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets <c>TaxModelTransactionType_IN</c> of the transaction
    /// </summary>
    /// <returns>
    /// The <c>TaxModelTransactionType_IN</c> of the transaction
    /// </returns>
    public TaxModelTransactionType getTransactionType()
    {
        return TaxModelTransactionType::Return;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVATAccountType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets tax account type based on the given <c>TaxOnItem</c>
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c>
    /// </param>
    /// <returns>
    /// The tax account type.
    /// </returns>
    public TaxAccountType_IN getVATAccountType(TaxOnItem _taxOnItem)
    {
        TaxAccountType_IN ret;

        switch (this.getDispositionAction())
        {
            case DispositionAction::ReturnToCust :
            case DispositionAction::Credit :
            case DispositionAction::ReplaceScrap :
            case DispositionAction::ReplaceCredit :
                ret = TaxAccountType_IN::VATPayableAccount;
                break;
            case DispositionAction::CreditOnly :
            case DispositionAction::Scrap :
                ret = TaxAccountType_IN::VATExpenseAccount;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        super(_documentLineMap);

        referenceReturnModel = referenceModel as TaxModelTaxableReturn;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isOrignalTaxSettled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determine the orignal tax is settled or not.
    /// </summary>
    /// <returns>
    /// True, if the orignal tax is settled;Otherwise, false.
    /// </returns>
    public boolean isOrignalTaxSettled()
    {
        TaxModelTaxable  orignalInvoiceTransModel = this.getOrignalInvoiceTrans();

        if (orignalInvoiceTransModel)
        {
            if (TaxParameters::isAdvancedSalesTaxPaymentEnabled())
            {
                return TaxTrans_IN::isTaxSettledForTransaction(
                    orignalInvoiceTransModel.getDocumentLineMainTableId(),
                    orignalInvoiceTransModel.getDocumentLineMainTableRecId());
            }
            else
            {
                return ComponentTransactions_IN::isTaxSettledForTransaction(
                    orignalInvoiceTransModel.getDocumentLineMainTableId(),
                    orignalInvoiceTransModel.getDocumentLineMainTableRecId());
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isTaxPerOrignal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Is the tax rate using the same of the orignal document line.
    /// </summary>
    /// <returns>
    /// True, if the tax rate using the same of the orignal document line.
    /// </returns>
    public boolean isTaxPerOrignal()
    {
        return referenceReturnModel.isTaxPerOrignal();
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipCustomsTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Customs Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipCustomsTax(TaxOnItem _taxOnItem)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipExciseTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Excise Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipExciseTax(TaxOnItem _taxOnItem)
    {
        boolean ret;

        ret = super(_taxOnItem);

        if (!ret)
        {
            ret = this.getDispositionAction() == DispositionAction::ReturnToCust;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipSalesTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Sales Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipSalesTax(TaxOnItem _taxOnItem)
    {
        boolean ret;

        ret = super(_taxOnItem);

        if (!ret)
        {
            ret = this.getDispositionAction() == DispositionAction::ReturnToCust;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipServiceTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the Service Tax be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipServiceTax(TaxOnItem _taxOnItem)
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipVAT</Name>
				<Source><![CDATA[
    /// <summary>
    /// Should the VAT be skipped on this transaction line?
    /// </summary>
    /// <param name="_taxOnItem">
    /// The given <c>TaxOnItem</c> on the transaction line.
    /// </param>
    /// <returns>
    /// True, it should be skipped; Otherwise, false.
    /// </returns>
    public boolean shouldSkipVAT(TaxOnItem _taxOnItem)
    {
        boolean ret;

        ret = super(_taxOnItem);

        if (!ret)
        {
            ret = this.getDispositionAction() == DispositionAction::ReturnToCust;
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>