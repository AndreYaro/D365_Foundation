<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventBatchJournalAttributeOpWeightedAvg</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Class to perform average weight computations for a particular batch attribute.
/// </summary>
public class InventBatchJournalAttributeOpWeightedAvg extends InventBatchJournalAttributeOperator
{
    InventQty           qtyTotal;
    PdsCWInventQty      cwQtyTotal;
    PdsBatchAttribMin   attribValueTotal;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>compute</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Performs the weighted average computation using the specified batch attribute value and batch quantities.
    /// </summary>
    /// <param name="_value">
    ///     The batch attribute value.
    /// </param>
    /// <param name="_batchQty">
    ///     Batch inventory quantity.
    /// </param>
    /// <param name="_cwBatchQty">
    ///     Batch inventory catch weight quantity; optional.
    /// </param>
    /// <returns>
    ///     The result of the computation as batch attribute value.
    /// </returns>
    public PdsBatchAttribValue compute(PdsBatchAttribValue _value, InventQty _batchQty, PdsCWInventQty _cwBatchQty = 0)
    {
        qtyTotal            += _batchQty;
        cwQtyTotal          += _cwBatchQty;

        InventQty qty = _cwBatchQty ? _cwBatchQty : _batchQty;
        InventQty total = cwQtyTotal ? cwQtyTotal : qtyTotal;
        attribValueTotal    += str2num(_value) * qty;

        real result = !total ? total : attribValueTotal / total;

        return PdsBatchAttrib::value2StorageType(PdsBatchAttribType::Realnumber, result);
    }

]]></Source>
			</Method>
			<Method>
				<Name>supported</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Determines whether this object supports the batch attribute this object was created to compute for.
    /// </summary>
    /// <returns>
    ///     true if the batch attribute is supported; otherwise, false.
    /// </returns>
    /// <remarks>
    ///     This method always returns true.
    /// </remarks>
    public boolean supported()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventBatchJournalAttributeOpWeightedAvg construct()
    {
        return new InventBatchJournalAttributeOpWeightedAvg();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
