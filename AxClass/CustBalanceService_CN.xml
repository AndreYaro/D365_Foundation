<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBalanceService_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The service of Customer balance report generation
/// </summary>
class CustBalanceService_CN extends SysOperationServiceBase
{
    // Customer balance report
    public const str ReportName = "@AccountsReceivable:CustBalanceReport";

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateReportByGER</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates report by generic electronic reporting
    /// </summary>
    /// <param name = "_contract">
    /// The instance of <c>CustBalanceContract_CN</c>.
    /// </param>
    public void generateReportByGER(CustBalanceContract_CN _contract)
    {
        TransDate				    toPeriod;
        ERFormatMappingId		    erBinding;
        CustBalanceDP_CN		    dataProvider;
        CustVendLedgerBalanceTmp_CN custBalanceTmp_CN;

        dataProvider = new CustBalanceDP_CN();

        erBinding = _contract.parmERBinding();

        ERFinDimensionsRunDataContract finDimDataContract = new ERFinDimensionsRunDataContract(_contract.getDimSelectType());
        finDimDataContract.parmDimSetSelectType(_contract.getDimSetSelectType());
        finDimDataContract.parmIsMainAccountMandatory(_contract.getIsMainAccountMandatory());
        finDimDataContract.parmAskForMainAccount(_contract.getAskForMainAccount());
        finDimDataContract.parmLocalizedAttributeNames(_contract.parmLocalizedAttributeNames());

        custBalanceTmp_CN = dataProvider.prepareData(_contract, finDimDataContract);

        ERFinDimensionsDetailsUIAction eRFinDimensionsDetailsUIAction = ERFinDimensionsDetailsUIAction::constructFromFinDimensionsRunDataContract(finDimDataContract);

        if (custBalanceTmp_CN)
        {
            try
            {
                ERIModelDefinitionParamsAction parameters = new ERModelDefinitionParamsUIActionComposite()
                    .add(
                        new ERModelDefinitionDatabaseContext()
                            .addTemporaryTable(custBalanceTmp_CN))
                    .add (eRFinDimensionsDetailsUIAction)
                    .add(
                        new ERModelDefinitionDatabaseContext()
                            .addValue(tableNum(CompanyInfo), fieldNum(CompanyInfo, DataArea), curext()));

                // Call ER to generate the excel.
                ERObjectsFactory::createFormatMappingRunByFormatMappingId(erBinding, ReportName)
                    .withParameter(parameters)
                    .withFileDestination(_contract.getFileDestination())
                    .run();
            }
            catch
            {
                // An error occurred while exporting data.
                error("@SYP4861341");
            }
        }
        else
        {
            // There is no data available.
            checkFailed("@SYS300117");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>