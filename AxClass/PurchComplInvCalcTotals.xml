<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchComplInvCalcTotals</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Calculates the amounts for the purchase complementary invoice.
/// </summary>
class PurchComplInvCalcTotals
{
    PurchComplementaryInvoiceHeader complInvHeader;
    PurchComplementaryInvoiceLine   complInvLine;
    TaxComplementaryInvoice         tax;
    TaxAmount                       sumTax;
    AmountMST                       totalLinesAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calc</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the total amounts for a purchase complementary invoice.
    /// </summary>
    /// <remarks>
    /// This method must be called before the total amount and tax amount are retrieved.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void calc()
    {
        PurchComplInvoiceCalcTax calcTax;

        calcTax = new PurchComplInvoiceCalcTax(complInvHeader);
        sumTax = calcTax.calcTax(0);
        this.tax(calcTax.tax());

        select sum(Amount) from complInvLine
            where complInvLine.PurchComplementaryInvoiceHeader == complInvHeader.RecId;

        totalLinesAmount = complInvLine.Amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>cashDiscAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the cash discount amount for the complementary invoice.
    /// </summary>
    /// <returns>
    /// Amount of the discount.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public AmountCur cashDiscAmount()
    {
        return VendParameters::cashDiscAmount(complInvHeader.CashDisc,
                                              complInvHeader.Currency,
                                              this.totalAmount(),
                                              this.totalAmount() - sumTax);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new calc totals for a complementary invoice.
    /// </summary>
    /// <param name="_complInvHeader">
    /// <c>PurchComplementaryInvoiceHeader</c> to calculate the totals from.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void new (PurchComplementaryInvoiceHeader _complInvHeader)
    {
        complInvHeader = _complInvHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sumTax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the total tax amount not included in price for this invoice.
    /// </summary>
    /// <returns>
    /// Total tax amount not included in price for this invoice.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public TaxAmount sumTax()
    {
        return sumTax;
    }

]]></Source>
			</Method>
			<Method>
				<Name>tax</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves and set the <c>Tax</c> instance for this class.
    /// </summary>
    /// <param name="_tax">
    /// The <c>Tax</c> instance to be set, optional.
    /// </param>
    /// <returns>
    /// The current <c>Tax</c> instance.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public TaxComplementaryInvoice tax(TaxComplementaryInvoice _tax = tax)
    {
        tax = _tax;
        return tax;
    }

]]></Source>
			</Method>
			<Method>
				<Name>totalAmount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the total amount for this invoice, including tax.
    /// </summary>
    /// <returns>
    /// Total amount for this invoice, including tax.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public AmountMST totalAmount()
    {
        return totalLinesAmount + sumTax;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>