<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysListPageMenuItemFormView</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SysListPageMenuItemFormView</c> abstract class is the base class for altering the view
/// of a list page form depending on the menu item, that opened the list page.
/// </summary>
abstract public class SysListPageMenuItemFormView
{
    private SysListPageInteractionBase listPageInteraction;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmListPageInteraction</Name>
				<Source><![CDATA[
    final public SysListPageInteractionBase parmListPageInteraction()
    {
        return ListPageInteraction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmListPage</Name>
				<Source><![CDATA[
    final public ListPage parmListPage()
    {
        return this.parmListPageInteraction().listPage();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setButtonEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables action pane buttons according to the active record data.
    /// </summary>
    abstract void setButtonEnabled()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setButtonVisibility</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the visibility property on the buttons in the action pane.
    /// </summary>
    abstract void setButtonVisibility()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setCaption</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the caption on the list page.
    /// </summary>
    abstract void setCaption()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setGridFieldVisibility</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the visibility property on the grid fields.
    /// </summary>
    abstract void setGridFieldVisibility()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeListPageInteraction</Name>
				<Source><![CDATA[
    private void initializeListPageInteraction(SysListPageInteractionBase _listPageInteraction)
    {
        listPageInteraction = _listPageInteraction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromListPageInteraction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of a <c>SysListPageMenuItemFormView</c> derived class within the specified class hierarchy and 
    /// based on the <c>SysListPageInteractionBase</c> object
    /// </summary>
    /// <param name = "_listPageInteraction">
    /// A <c>SysListPageInteractionBase</c> object to create the instance for.
    /// </param>
    /// <param name = "_menuItemFormViewBaseClass">
    /// A class id identifying the base class of a class hierarchy, that the instance should be created within.
    /// </param>
    /// <returns>
    /// The <c>SysListPageMenuItemFormView</c> instance.
    /// </returns>
    /// <remarks>
    /// If a specific class cannot be identified within the class hierarchy, then an instance of the base class will be created.
    /// </remarks>
    public static SysListPageMenuItemFormView newFromListPageInteraction(SysListPageInteractionBase _listPageInteraction, ClassId _menuItemFormViewBaseClass)
    {
        var listPageArgs    = _listPageInteraction.listPage().listPageArgs();
        var formView        = SysListPageMenuItemFormView::newFromListPageMenuItem(MenuItemType::Display, listPageArgs.menuItemName(), _menuItemFormViewBaseClass);
        
        if (!formView && listPageArgs.enumTypeParameter())
        {
            formView = SysListPageMenuItemFormView::newFromListPageEnum(enumId2Name(listPageArgs.enumTypeParameter()),
                                                                        enum2Symbol(listPageArgs.enumTypeParameter(), listPageArgs.enumParameter()),
                                                                        _menuItemFormViewBaseClass);
        }

        if (!formView)
        {
            formView = new DictClass(_menuItemFormViewBaseClass).makeObject() as SysListPageMenuItemFormView;
        }

        formView.initializeListPageInteraction(_listPageInteraction);

        return formView;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromListPageMenuItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of a <c>SysListPageMenuItemFormView</c> derived class within the specified class hierarchy and
    /// based provided menu item details.
    /// </summary>
    /// <param name = "_menuItemType">
    /// A type of menu item.
    /// </param>
    /// <param name = "_menuItemName">
    /// A menu item name.
    /// </param>
    /// <param name = "_menuItemFormViewBaseClass">
    /// A class id identifying the base class of a class hierarchy, that the instance should be created within.
    /// </param>
    /// <returns>
    /// The <c>SysListPageMenuItemFormView</c> object decorated with the menu item name and type; otherwise, null.
    /// </returns>
    public static SysListPageMenuItemFormView newFromListPageMenuItem(MenuItemType _menuItemType, MenuItemName _menuItemName, ClassId _menuItemFormViewBaseClass)
    {
        var attribute   = new FormMenuItemFactoryAttribute(_menuItemType, _menuItemName);
        var formView    = SysExtensionAppClassFactory::getClassFromSysAttribute(classId2Name(_menuItemFormViewBaseClass), attribute) as SysListPageMenuItemFormView;
        
        return formView;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromListPageEnum</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of a <c>SysListPageMenuItemFormView</c> derived class within the specified class hierarchy and
    /// based provided enum details.
    /// </summary>
    /// <param name = "_enumName">
    /// An enum name.
    /// </param>
    /// <param name = "_enumSymbol">
    /// A string representation of an enum symbol.
    /// </param>
    /// <param name = "_menuItemFormViewBaseClass">
    /// A class id identifying the base class of a class hierarchy, that the instance should be created within.
    /// </param>
    /// <returns>
    /// The <c>SysListPageMenuItemFormView</c> object decorated with the enum name and symbol; otherwise, null.
    /// </returns>
    public static SysListPageMenuItemFormView newFromListPageEnum(EnumName _enumName, IdentifierName _enumSymbol, ClassId _menuItemFormViewBaseClass)
    {
        var attribute   = new FormEnumSymbolFactoryAttribute(_enumName, _enumSymbol);
        var formView    = SysExtensionAppClassFactory::getClassFromSysAttribute(classId2Name(_menuItemFormViewBaseClass), attribute) as SysListPageMenuItemFormView;
        
        return formView;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>