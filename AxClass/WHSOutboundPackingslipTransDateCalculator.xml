<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSOutboundPackingslipTransDateCalculator</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class WHSOutboundPackingslipTransDateCalculator
{
    private boolean hasInventoryClosingDateBeenSet;
    private TransDate inventoryClosingDate;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmInventoryClosingDate</Name>
				<Source><![CDATA[
    private TransDate parmInventoryClosingDate()
    {
        if (!hasInventoryClosingDateBeenSet)
        {
            
            inventoryClosingDate = InventClosing::closingDate();
            
            hasInventoryClosingDateBeenSet = true;
        }

        return inventoryClosingDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCalculator</Name>
				<Source><![CDATA[
    public static WHSOutboundPackingslipTransDateCalculator newCalculator()
    {
        return new WHSOutboundPackingslipTransDateCalculator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateOutboundSalesTransDateForLoad</Name>
				<Source><![CDATA[
    public TransDate calculateOutboundSalesTransDateForLoad(WHSLoadId _loadId, SalesParameters _salesParameters)
    {
        utcdatetime loadTransDate = WHSLoadTable::find(_loadId).LoadShipConfirmUTCDateTime;

        TransDate closingDate = this.parmInventoryClosingDate();

        // Packing slip can be printed during container close action, before load is ship confirmed
        if (!loadTransDate)
        {
            loadTransDate = DateTimeUtil::getSystemDateTime();
        }

        TransDate packingSlipTransDate;

        SalesDocumentTimezonePreference userTimezoneConvertion = _salesParameters.InvoiceTimezonePreference;
        Timezone tz = userTimezoneConvertion == SalesDocumentTimezonePreference::User ? DateTimeUtil::getUserPreferredTimeZone() : DateTimeUtil::getCompanyTimeZone();
        packingSlipTransDate = DateTimeUtil::date(DateTimeUtil::applyTimeZoneOffset(loadTransDate, tz));
        
        if (closingDate
            && packingSlipTransDate <= closingDate)
        {
            packingSlipTransDate = closingDate + 1;
        }
                
        return packingSlipTransDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>