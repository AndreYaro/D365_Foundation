<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BusinessDocumentApplicationResponseDropDialogPresenter_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class BusinessDocumentApplicationResponseDropDialogPresenter_W
{
    FormComboBoxControl applicationResponseType;
    FormComboBoxControl rejectReasonType;
    FormStringControl comments;
    FormRun caller;
    FormButtonControl okButton;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(
        FormRun _caller,
        FormComboBoxControl _applicationResponseType,
        FormComboBoxControl _rejectReasonType,
        FormStringControl _comments,
        FormButtonControl _okButton)
    {
        applicationResponseType = _applicationResponseType;
        rejectReasonType = _rejectReasonType;
        comments = _comments;
        caller = _caller;
        okButton = _okButton;

        okButton.OnClicked += eventhandler(this.saveButtonClickedEventHandler);
        applicationResponseType.OnSelectionChanged += eventhandler(this.applicationResponseSelectionChangedEventHandler);
        rejectReasonType.OnSelectionChanged += eventhandler(this.rejectReasonSelectionChangedEventHandler);
        caller.OnActivated += eventhandler(this.dropDownClickedEventHandler);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesign</Name>
				<Source><![CDATA[
    private void updateDesign(boolean _canModify)
    {
        if (applicationResponseType.selection() == BusinessDocumentVendorInvoiceAcceptedState_W::Accepted)
        {
            comments.visible(false);
            rejectReasonType.visible(false);
        }
        else if (applicationResponseType.selection() == BusinessDocumentVendorInvoiceAcceptedState_W::AcceptedWithRemarks)
        {
            comments.visible(true);
            rejectReasonType.visible(false);
        }
        else if (applicationResponseType.selection() == BusinessDocumentVendorInvoiceAcceptedState_W::Rejected)
        {
            comments.visible(true);
            rejectReasonType.visible(true);
        }

        applicationResponseType.enabled(_canModify);
        rejectReasonType.enabled(_canModify);
        comments.enabled(_canModify);
        okButton.enabled(_canModify && !this.isNoneSelected());
    }

]]></Source>
			</Method>
			<Method>
				<Name>applicationResponseSelectionChangedEventHandler</Name>
				<Source><![CDATA[
    private void applicationResponseSelectionChangedEventHandler(FormControl _sender, FormControlEventArgs _eventArgs)
    {
        this.updateDesign(this.canRecordBeModified(_sender.formRun()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>dropDownClickedEventHandler</Name>
				<Source><![CDATA[
    private void dropDownClickedEventHandler(xFormRun _sender, FormEventArgs _eventArgs)
    {
        this.updateDesign(this.canRecordBeModified(_sender));
    }

]]></Source>
			</Method>
			<Method>
				<Name>rejectReasonSelectionChangedEventHandler</Name>
				<Source><![CDATA[
    private void rejectReasonSelectionChangedEventHandler(FormControl _sender, FormControlEventArgs _eventArgs)
    {
        FormDataSource applicationResponse_ds = _sender.formRun().dataSource();
        SysDictEnum dictEnum = new SysDictEnum(enumNum(BusinessDocumentRejectionReason));
        BusinessDocumentVendorInvoiceApplicationResponse_W applicationResponse = applicationResponse_ds.cursor();
        applicationResponse.Comments = dictEnum.value2Label(rejectReasonType.selection());
        applicationResponse_ds.refresh();
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveButtonClickedEventHandler</Name>
				<Source><![CDATA[
    private void saveButtonClickedEventHandler(FormControl _sender, FormControlEventArgs _eventArgs)
    {
        info(strFmt("@BusinessDocumentSubmissionService:ApplicationResponseSaved"));
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRecordBeModified</Name>
				<Source><![CDATA[
    private boolean canRecordBeModified(xFormRun _sender)
    {
        FormDataSource applicationResponse_ds = _sender.dataSource();
        BusinessDocumentVendorInvoiceApplicationResponse_W applicationResponse = applicationResponse_ds.cursor();

        return !BusinessDocumentSubmission::exists(
            tableNum(BusinessDocumentVendorInvoiceApplicationResponse_W),
            applicationResponse.RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isNoneSelected</Name>
				<Source><![CDATA[
    private boolean isNoneSelected()
    {
        if (applicationResponseType.selection() == BusinessDocumentVendorInvoiceAcceptedState_W::None)
        {
            return true;
        }
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>