<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBLedgerJournalTransCancelVoucher_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FBLedgerJournalTransCancelVoucher_BR</c> cancels the ledger journal transaction.
/// </summary>
class FBLedgerJournalTransCancelVoucher_BR
{
    TmpFrmVirtual           tmpFrmVirtualLines;
    LedgerJournalId         ledgerJournalId;
    LedgerJournalTable      ledgerJournalTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getNewVoucher</Name>
				<Source><![CDATA[
    private Voucher getNewVoucher()
    {
        return new JournalVoucherNum(JournalTableData::newTable(ledgerJournalTable)).getNew(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmLedgerJournalId</Name>
				<Source><![CDATA[
    public LedgerJournalId parmLedgerJournalId(LedgerJournalId _ledgerJournalId = ledgerJournalId)
    {
        ledgerJournalId = _ledgerJournalId;

        return ledgerJournalId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTmpFrmVirtualLines</Name>
				<Source><![CDATA[
    public TmpFrmVirtual parmTmpFrmVirtualLines(TmpFrmVirtual _tmpFrmVirtualLines = tmpFrmVirtualLines)
    {
        tmpFrmVirtualLines = _tmpFrmVirtualLines;

        return tmpFrmVirtualLines;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateNow</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the ledger journal transaction.
    /// </summary>
    void updateNow()
    {
        LedgerJournalTrans ledgerJournalTrans, newLedgerJournalTrans;
        FBLedgerJournalTrans_FiscalBooks_BR ledgerJournalTransFiscalBooks, newLedgerJournalTransFiscalBooks;

        ledgerJournalTable = ledgerJournalTable::find(this.parmLedgerJournalId());

        ttsbegin;
        while select tmpFrmVirtualLines
        {
            select ledgerJournalTrans
                where ledgerJournalTrans.RecId   == tmpFrmVirtualLines.RecordNo
            join ledgerJournalTransFiscalBooks
                where ledgerJournalTransFiscalBooks.LedgerJournalTrans == ledgerJournalTrans.RecId;

            newLedgerJournalTrans.clear();

            newLedgerJournalTrans                   = ledgerJournalTrans.data();
            newLedgerJournalTrans.RecId             = 0;
            newLedgerJournalTrans.PaymentStatus     = CustVendPaymStatus::None;
            newLedgerJournalTrans.JournalNum        = ledgerJournalId;
            newLedgerJournalTrans.Voucher           = this.getNewVoucher();
            newLedgerJournalTrans.Company           = ledgerJournalTrans.Company;
            newLedgerJournalTrans.OffsetCompany     = ledgerJournalTrans.Company;
            newLedgerJournalTrans.AmountCurCredit   = ledgerJournalTrans.AmountCurDebit;
            newLedgerJournalTrans.AmountCurDebit    = ledgerJournalTrans.AmountCurCredit;
            newLedgerJournalTrans.RevRecId          = ledgerJournalTrans.RecId;
            newLedgerJournalTrans.insert();

            newLedgerJournalTransFiscalBooks = ledgerJournalTransFiscalBooks.data();
            newLedgerJournalTransFiscalBooks.LedgerJournalTrans = newLedgerJournalTrans.RecId;
            newLedgerJournalTransFiscalBooks.insert();
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newLedgerJournalTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates the <c>FBLedgerJournalTransCancelVoucher_BR</c> class object
    /// </summary>
    /// <param name="_ledgerJournalTable">
    /// A <c>LedgerJournalTable</c> table record
    /// </param>
    /// <returns>
    /// An initialized <c>FBLedgerJournalTransCancelVoucher_BR</c> class object
    /// </returns>
    static FBLedgerJournalTransCancelVoucher_BR newLedgerJournalTable(LedgerJournalTable _ledgerJournalTable)
    {
        FBLedgerJournalTransCancelVoucher_BR cancelVoucher = new FBLedgerJournalTransCancelVoucher_BR();

        cancelVoucher.parmLedgerJournalId(_ledgerJournalTable.JournalNum);

        return cancelVoucher;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>