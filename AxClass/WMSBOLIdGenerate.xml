<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>wmsbolIdGenerate</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class wmsbolIdGenerate
{
    WMSBillOfLadingId   wmsBillOfLadingId;

    #DEFINE.BOL_LEN(17)
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>makeBOL17</Name>
				<Source><![CDATA[
        public void makeBOL17()
    {
        if (!isInteger(wmsBillOfLadingId))
        {
            return;
        }

        if (strLen(wmsBillOfLadingId) < #BOL_LEN)
        {
            wmsBillOfLadingId += '0';
        }

        while (strLen(wmsBillOfLadingId) < #BOL_LEN)
        {
            wmsBillOfLadingId = '0' + wmsBillOfLadingId;
        }
        wmsBillOfLadingId = Barcode::insertModulo10CheckDigit(wmsBillOfLadingId, #BOL_LEN);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        // Is made protected to prevent calling new directly
    }

]]></Source>
			</Method>
			<Method>
				<Name>nextNumber</Name>
				<Source><![CDATA[
    private WMSBillOfLadingId nextNumber(Correct_RU _correct = NoYes::No)
    {
        // <GEERU>
        WMSBillOfLadingId = NumberSeq::newGetNum(_correct                                           ?
                                                 WMSParameters::numRefCorrectWMSBillOfLadingId_RU() :
                                                 WMSParameters::numRefWMSBillOfLadingId()).num();
                                                 // </GEERU>
        if (strLen(wmsBillOfLadingId) == #BOL_LEN - 1 && isInteger(wmsBillOfLadingId))
        {
            wmsBillOfLadingId += '0';
            wmsBillOfLadingId = Barcode::insertModulo10CheckDigit(wmsBillOfLadingId, #BOL_LEN);
        }
        return wmsBillOfLadingId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWMSBillOfLadingId</Name>
				<Source><![CDATA[
        public WMSBillOfLadingId parmWMSBillOfLadingId(WMSBillOfLadingId _wmsBillOfLadingId = wmsBillOfLadingId)
    {
        wmsBillOfLadingId = _wmsBillOfLadingId;
        return wmsBillOfLadingId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static WMSBOLIdGenerate construct()
    {
        return new WMSBOLIdGenerate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>nextBOLId</Name>
				<Source><![CDATA[
    public static WMSBillOfLadingId nextBOLId(Correct_RU _correct = NoYes::No)
    {
        boolean             ret = true;
        WMSBOLIdGenerate    wmsbolIdGenerate;
        WMSBillOfLading     wmsBillOfLading;
        WMSBillOfLadingId   wmsBillOfLadingId;

        wmsbolIdGenerate = WMSBOLIdGenerate::construct();
        while (ret)
        {
            // <GEERU>
            wmsBillOfLadingId = wmsbolIdGenerate.nextNumber(_correct);
            // </GEERU>
            select wmsBillOfLading
                where wmsBillOfLading.BillOfLadingId == wmsBillOfLadingId;
            if (!wmsBillOfLading)
            {
                ret = false;
            }
        }
        return wmsBillOfLadingId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>