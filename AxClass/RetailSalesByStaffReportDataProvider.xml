<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSalesByStaffReportDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.Data;
using ACM = Microsoft.Dynamics.AX.Analytics.CalculationModel;

/// <summary>
/// The <c>RetailSalesByStaffReportDataProvider</c> class is the report data provider
/// class for retail sales by staff report.
/// </summary>
[SRSReportParameterAttribute(classstr(RetailReportParametersDataContract))]
class RetailSalesByStaffReportDataProvider extends RetailSrsReportDataProviderBase
{
    RetailSalesByStaffReportTable table;
    RetailReportParametersDataContract dc;
    ACM.NumericExpression grossProfitMarginPercentage;
    ACM.NumericExpression grossProfitMargin;
    ACM.DynamicMeasureDefinition amount;
    ACM.DynamicMeasureDefinition cogs;
    ACM.DynamicMeasureDefinition quantity;
    ACM.DynamicMeasureDefinition tax;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize measure definitions.
    /// </summary>
    public void new()
    {
        super();

        amount = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailSalesByStaffReportView),
                    fieldStr( RetailSalesByStaffReportView, LineAmountMST),
                    ACM.Aggregation::Sum
                );

        cogs = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailSalesByStaffReportView),
                    fieldStr( RetailSalesByStaffReportView, COGS),
                    ACM.Aggregation::Sum
                );

        quantity = new ACM.DynamicMeasureDefinition(
                tableStr(RetailSalesByStaffReportView),
                fieldStr( RetailSalesByStaffReportView, InventQty),
                ACM.Aggregation::Sum
            );

        tax = new ACM.DynamicMeasureDefinition(
                tableStr(RetailSalesByStaffReportView),
                fieldStr( RetailSalesByStaffReportView, LineAmountTaxMST),
                ACM.Aggregation::Sum
            );

        grossProfitMargin = amount.Subtract(cogs);

        ACM.LogicalExpression isAmountZero = amount.CompareEquals(0);

        grossProfitMarginPercentage = new ACM.If(
                isAmountZero,
                ACM.Null::ConstValue,
                grossProfitMargin.Divide(amount));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    [SRSReportDataSetAttribute(tableStr(RetailSalesByStaffReportTable))]
    public RetailSalesByStaffReportTable getTable()
    {
        select table;
        return table;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    Provides the ability to write the report business logic. This method will be called by SSRS at
    ///    runtime. The method should compute data and populate the data tables that will be returned to
    ///    SSRS.Note that this is a framework class. Customizing this class may cause problems with future
    ///    upgrades to the software.
    /// </remarks>
    public void processReport()
    {
        dc = this.parmDataContract();
        this.populateData();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Run the query and insert data into temp table.
    /// </summary>
    void populateData()
    {
        try
        {
            SysQueryRun::ExecuteInManagedStack(true);

            this.InsertChannelsToTmpTable(dc.parmHierarchyRelationId());
            Query q = new Query();
            QueryBuildDataSource  qds = q.addDataSource(tableNum(RetailSalesByStaffReportView));

            // Add fields.
            qds.addSelectionFieldWithAlias('PersonnelNumber', fieldNum(RetailSalesByStaffReportView, PersonnelNumber));
            qds.addSelectionFieldWithAlias('StaffName', fieldNum(RetailSalesByStaffReportView, StaffName));
            qds.addCalculationField(amount, 'LineAmountMST' );
            qds.addCalculationField(tax, 'LineAmountTaxMST' );
            qds.addCalculationField(cogs,'COGS');
            qds.addCalculationField(quantity, 'Quantity');
            qds.addCalculationField(grossProfitMargin, 'GrossProfitMargin');
            qds.addCalculationField(grossProfitMarginPercentage, 'GrossProfitMarginPercentage');
            qds.addGroupByField(fieldNum(RetailSalesByStaffReportView, PersonnelNumber));
            qds.addGroupByField(fieldNum(RetailSalesByStaffReportView, StaffName));
            this.AddSortOrder(qds);
            q.topRows(dc.parmTopCount());

            //Add ranges.
            QueryBuildRange dateTimeRange = qds.addRange(fieldnum(RetailSalesByStaffReportView, InvoiceDate));
            dateTimeRange.value(this.buildDateRangeForSpecifiedField(dc.parmFromDate(), dc.parmToDate(), fieldStr(RetailSalesByStaffReportView, InvoiceDate)));
            
            //Add join with RetailReportChannelTable.
            this.JoinChannelFilter(qds, fieldNum(RetailSalesByStaffReportView, RetailChannel));

            //Build Mappings
            Map m = this.getMappings();

            // run the query and insert the query results into the form datasource
            QueryRun::runAndPopulate(q, table, m);

            this.ClearChannelFilterTable();
        }
        finally
        {
            SysQueryRun::ExecuteInManagedStack(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMappings</Name>
				<Source><![CDATA[
    private Map getMappings()
    {
        Map m = new Map(Types::String, Types::String);
        m.insert('PersonnelNumber', fieldStr(RetailSalesByStaffReportTable, StaffId ));
        m.insert('StaffName', fieldStr(RetailSalesByStaffReportTable, StaffName));
        m.insert('LineAmountMST', fieldStr(RetailSalesByStaffReportTable, Amount));
        m.insert('COGS', fieldStr(RetailSalesByStaffReportTable, COGS));
        m.insert('Quantity', fieldStr(RetailSalesByStaffReportTable, Quantity));
        m.insert('GrossProfitMargin', fieldStr(RetailSalesByStaffReportTable, Margin));
        m.insert('GrossProfitMarginPercentage', fieldStr(RetailSalesByStaffReportTable, Percentage));

        return m;
    }

]]></Source>
			</Method>
			<Method>
				<Name>AddSortOrder</Name>
				<Source><![CDATA[
    private void AddSortOrder(QueryBuildDataSource qbd)
    {
        switch (dc.parmOrderBy())
        {
            case RetailReportOrderBy::Amount:
                qbd.addOrderByCalculationField(amount, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Profit:
                qbd.addOrderByCalculationField(grossProfitMargin, SortOrder::Descending);
                break;
            case RetailReportOrderBy::Quantity:
                qbd.addOrderByCalculationField(quantity, SortOrder::Descending);
                break;
            case RetailReportOrderBy::ProfitPercentage:
                qbd.addOrderByCalculationField(grossProfitMarginPercentage, SortOrder::Descending);
                break;
            default:
                throw Global::error("@Retail:RetailReportOrderByNotSupported");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>