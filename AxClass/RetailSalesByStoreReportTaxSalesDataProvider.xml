<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSalesByStoreReportTaxSalesDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.Data;
using ACM = Microsoft.Dynamics.AX.Analytics.CalculationModel;

/// <summary>
/// The <c>RetailSalesByStoreReportTaxSalesDataProvider</c> class is the  data provider
/// class for tax data section in retail store summary report.
/// </summary>
[SRSReportParameterAttribute(classstr(RetailReportParametersDataContract))]
class RetailSalesByStoreReportTaxSalesDataProvider extends RetailSrsReportDataProviderChannelBase
{
    RetailSalesByStoreReportTaxSalesTable table;
    RetailReportParametersDataContract dc;

    ACM.DynamicMeasureDefinition taxesCount;
    ACM.DynamicMeasureDefinition amount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize measure definitions.
    /// </summary>
    public void new()
    {
        super();
        taxesCount = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailSalesByStoreReportTaxSalesView),
                    fieldStr( RetailSalesByStoreReportTaxSalesView, TaxTransRecId),
                    ACM.Aggregation::Count
                );

        amount = new ACM.DynamicMeasureDefinition(
                    tableStr(RetailSalesByStoreReportTaxSalesView),
                    fieldStr( RetailSalesByStoreReportTaxSalesView, Amount),
                    ACM.Aggregation::Sum
                );
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    [SRSReportDataSetAttribute(tableStr(RetailSalesByStoreReportTaxSalesTable))]
    public RetailSalesByStoreReportTaxSalesTable getTable()
    {
        select table;
        return table;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is called by the SSRS report framework to execute the report queries.
    /// </summary>
    public void processReport()
    {
        dc = this.parmDataContract();
        this.populateData();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Run the query and insert data into temp table.
    /// </summary>
    void populateData()
    {
        try
        {
            SysQueryRun::ExecuteInManagedStack(true);

            this.InsertChannelsToTmpTable(dc.parmHierarchyRelationId());
            Query q = new Query();
            QueryBuildDataSource  qds = q.addDataSource(tableNum(RetailSalesByStoreReportTaxSalesView));

            // Add fields.
            qds.addSelectionFieldWithAlias('TaxName', fieldNum(RetailSalesByStoreReportTaxSalesView, TaxName));
            qds.addCalculationField(taxesCount, 'TaxTransRecId' );
            qds.addCalculationField(amount,'Amount');
            qds.addGroupByField(fieldNum(RetailSalesByStoreReportTaxSalesView, TaxName));

            //Add ranges.
            QueryBuildRange dateTimeRange = qds.addRange(fieldnum(RetailSalesByStoreReportTaxSalesView, transDate));
            dateTimeRange.value(this.buildDateRangeForSpecifiedField(dc.parmFromDate(), dc.parmToDate(), fieldStr(RetailSalesByStoreReportTaxSalesView, transDate)));

            //Add join with RetailReportChannelTable.
            this.JoinChannelFilter(qds, fieldNum(RetailSalesByStoreReportTaxSalesView, Channel));

            //Build Mappings
            Map m = this.getMappings();

            // run the query and insert the query results into the form datasource
            QueryRun::runAndPopulate(q, table, m);

            this.ClearChannelFilterTable();
        }
        finally
        {
            SysQueryRun::ExecuteInManagedStack(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMappings</Name>
				<Source><![CDATA[
    private Map getMappings()
    {
        Map m = new Map(Types::String, Types::String);
        m.insert('TaxName', fieldStr(RetailSalesByStoreReportTaxSalesTable, TaxCode));
        m.insert('Amount', fieldStr(RetailSalesByStoreReportTaxSalesTable, Amount));
        m.insert('TaxTransRecId', fieldStr(RetailSalesByStoreReportTaxSalesTable, NumberOfTransactions));

        return m;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>