<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetReservationWorkflowWorkItemActionManager_PSN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetReservationWorkflowWorkItemActionManager_PSN</c> class manages the work item actions for budget
/// reservation workflows.
/// </summary>
public class BudgetReservationWorkflowWorkItemActionManager_PSN extends WorkflowWorkItemActionManager
{
    BudgetReservationHeader_PSN budgetReservationHeader;
    BudgetReservation_PSN       budgetReservation;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBudget</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the budget check of all the budget reservation line records for a given budget reservation table record.
    /// </summary>
    protected void checkBudget()
    {
        budgetReservation = BudgetReservation_PSN::newFromBudgetReservationHeader_PSN(budgetReservationHeader);
    
        budgetReservation.validateBudgetCheckOnBudgetResLines(budgetReservationHeader);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    public Args parmArgs(Args _args = args)
    {
        Args ret;
    
        ret = super(_args);
    
        if (_args)
        {
            this.parmCaller(_args.caller());
    
            if (_args.record() &&
                _args.record().TableId == tablenum(BudgetReservationHeader_PSN))
            {
                budgetReservationHeader = _args.record() as BudgetReservationHeader_PSN;
            }
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptWorkflowWorkItemAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether budget funds are available and then prompts for the action dialog box for workflow
    /// work item actions.
    /// </summary>
    protected void promptWorkflowWorkItemAction()
    {
        // Perform budget check before prompting the action dialog.
        // This gives the approver or reviewer a chance to cancel the action if there are any budget check errors.
        this.checkBudget();
    
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the workflow work item action or the line item workflow action.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    /// Args is null.
    /// </exception>
    public void run()
    {
        if (!this.parmArgs())
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is primary main method to be invoked via the selection of a menu item.
    /// </summary>
    /// <param name="_args">
    /// Menu item arguments.
    /// </param>
    public static void main(Args _args)
    {
        BudgetReservationWorkflowWorkItemActionManager_PSN workflowWorkItemActionManager = new BudgetReservationWorkflowWorkItemActionManager_PSN();
    
        workflowWorkItemActionManager.parmArgs(_args);
        workflowWorkItemActionManager.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>