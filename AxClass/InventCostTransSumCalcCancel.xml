<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventCostTransSumCalcCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventCostTransSumCalcCancel</c> class deletes the <c>inventCostTransSum</c> records when a
///    closing is canceled.
/// </summary>

class InventCostTransSumCalcCancel
{
    InventClosing     inventClosing;

    ItemId            itemId;
    /// <summary>
    ///    Deletes <c>InventCostTransSum</c> records.
    /// </summary>

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteRecords</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void deleteRecords()
    {
        InventCostTransSum  inventCostTransSum;

        ttsbegin;

        if (this.shouldSkipInventCostTransSumDataMethods())
        {
            inventCostTransSum.skipDataMethods(true);
        }

        if (itemId != '')
        {
            delete_from inventCostTransSum
                where inventCostTransSum.InventClosingRefRecId == inventClosing.RecId
                   && inventCostTransSum.ItemId                == itemId;
        }
        else
        {
            delete_from inventCostTransSum
                where inventCostTransSum.InventClosingRefRecId == inventClosing.RecId;
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldSkipInventCostTransSumDataMethods</Name>
				<Source><![CDATA[
    protected boolean shouldSkipInventCostTransSumDataMethods()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInventClosing</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the variables from a given <c>InventClosing</c> record.
    /// </summary>
    /// <param name="_inventClosing">
    ///    The <c>InventClosing</c> record that is currently canceled.
    /// </param>

    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void initFromInventClosing(InventClosing _inventClosing)
    {
        // Set the caller record
        inventClosing = _inventClosing;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new instance of the <c>InventCostTransSumCalcCancel</c> class.
    /// </summary>
    /// <remarks>
    ///    This method is protected to ensure that the <c>construct</c> method is used.
    /// </remarks>

    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmItemId</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public ItemId parmItemId(ItemId _itemId = itemId)
    {
        itemId = _itemId;

        return itemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>cancelFromInventClosing</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Cancels a closing.
    /// </summary>
    /// <param name="_inventClosing">
    ///    The closing that is canceled.
    /// </param>
    /// <param name="_itemId">
    ///    The item ID for which the records should be deleted; optional.
    /// </param>

    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void cancelFromInventClosing(InventClosing    _inventClosing,
                                               ItemId           _itemId = '')
    {
        InventCostTransSumCalcCancel     inventCostTransSumCalcCancel;

        // Initialize instance
        inventCostTransSumCalcCancel = InventCostTransSumCalcCancel::construct();

        // Initialize parameters
        inventCostTransSumCalcCancel.initFromInventClosing(_inventClosing);

        inventCostTransSumCalcCancel.parmItemId(_itemId);

        // Create closing records
        inventCostTransSumCalcCancel.deleteRecords();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public static InventCostTransSumCalcCancel construct()
    {
        return new InventCostTransSumCalcCancel();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>