<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerAdvancedSettlement_ApplicationSuite_DetailsView_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formDataSourceStr(LedgerAdvancedSettlement, LedgerTransSettlementWithDetailsFromYecView))]
internal final class LedgerAdvancedSettlement_ApplicationSuite_DetailsView_Extension
{
    private readonly boolean isAdvancedAwarenessOptsParmEnabled = LedgerTransSettlementAdvancedParameters::isAwarenessParameterEnabled();

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>active</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables <c>ViewOriginalVoucher</c> control when AdvancedAwarenessOptions parameter is enabled when LedgerTransSettlementWithDetailsFromYecView
    /// datasource in <c>LedgerAdvancedSettlement<c> form gets activated.
    /// If accounting date is valid then <c>ViewOriginalVoucher</c> is enabled otherwise it is disabled.
    /// </summary>
    /// <returns>An integer return code.</returns>
    public int active()
    {
        if (isAdvancedAwarenessOptsParmEnabled)
        {
            var viewOrigVoucher = this.formRun().design().controlName(formControlStr(LedgerAdvancedSettlement, ViewOriginalVoucher)) as FormButtonControl;

            if (!element.LedgerTransSettlementWithDetailsFromYecView.AccountingDate)
            {
                viewOrigVoucher.enabled(false);
            }
            else
            {
                viewOrigVoucher.enabled(true);
            }
        }
        
        return next active();
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds link to <c>LedgerTransSettlementWithDetailsFromYecView</c> datasource on initialization.
    /// </summary>
    public void init()
    {
        next init();

        if (isAdvancedAwarenessOptsParmEnabled)
        {
            QueryBuildDataSource qbdsDetailsView = element.LedgerTransSettlementView_ds.query().dataSourceTable(tableNum(LedgerTransSettlementWithDetailsFromYecView));
            qbdsDetailsView.addLink(fieldNum(LedgerTransSettlementView, GeneralJournalAccountEntryRecId), fieldNum(LedgerTransSettlementWithDetailsFromYecView, GeneralJournalAccountEntryRecId));
        }
        else
        {
            QueryBuildDataSource qbdsDetailsView = element.LedgerTransSettlementView_ds.query().dataSourceTable(tableNum(LedgerTransSettlementWithDetailsFromYecView));
            qbdsDetailsView.enabled(false);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>