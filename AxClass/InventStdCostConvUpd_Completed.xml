<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventStdCostConvUpd_Completed</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventStdCostConvUpd_Completed extends InventStdCostConvUpd
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Updates the <c>Status</c> field of the <c>InventStdCostConv</c> record buffer to a value of
    ///    <c>InventStdCostConvStatus::Completed</c>.
    /// </summary>
    /// <exception cref="M:Exception::UpdateConflict">
    ///    An update conflict was caught in a transaction scope.
    /// </exception>
    /// <exception cref="M:Exception::UpdateConflictNotRecovered">
    ///    An update conflict could not be recovered.
    /// </exception>
    /// <exception cref="M:Exception::Error">
    ///    The validation failed.
    /// </exception>
    public void run()
    {
        #OCCRetryCount

        try
        {
            ttsbegin;

            inventStdCostConv = InventStdCostConv::find(inventStdCostConv.RecId, true);

            if (!this.validate())
                throw Exception::Error;

            inventStdCostConv.Status = InventStdCostConvStatus::Completed;
            inventStdCostConv.update();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                    throw Exception::UpdateConflictNotRecovered;
                else
                    retry;
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates whether the status can be updated.
    /// </summary>
    /// <returns>
    /// true if no errors exist; otherwise, false.
    /// </returns>
    public boolean validate()
    {
        boolean ret = true;

        if (!inventStdCostConv.allowCompletedConversion())
        {
            return checkFailed(strFmt("@SYS120306",InventStdCostConvStatus::Completed));
        }

        if (!this.validateItemsConverted())
        {
            ret = false;
        }

        if (!ret)
        {
            ret = checkFailed(strFmt("@SYS120307",InventStdCostConvStatus::Completed));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateItemsConverted</Name>
				<Source><![CDATA[
        protected boolean validateItemsConverted()
    {
        InventStdCostConvItem   inventStdCostConvItem;
        boolean                 ret = true;

        select count(RecId)
            from inventStdCostConvItem
            where inventStdCostConvItem.StdCostConvRecId    == inventStdCostConv.RecId &&
                  inventStdCostConvItem.Status              != InventStdCostConvItemStatus::Converted;

        if (inventStdCostConvItem.RecId)
        {
            ret = checkFailed(strFmt("@SYS120311",inventStdCostConvItem.RecId));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Instantiates an <c>InventStdCostConvUpd_Completed</c> object.
    /// </summary>
    /// <param name="_inventStdCostConv">
    ///    An <c>InventStdCostConv</c> record buffer that must have the status updated.
    /// </param>
    /// <returns>
    ///    An <c>InventStdCostConvUpd_Completed</c> object.
    /// </returns>
    /// <remarks>
    ///    This method should not be called directly. An <c>InventStdCostConvUpd</c> object should be
    ///    instantiated using the constructor in the <c>InventStdCostConvUpd</c> class.
    /// </remarks>
    public static InventStdCostConvUpd_Completed construct(InventStdCostConv _inventStdCostConv)
    {
        return new InventStdCostConvUpd_Completed(_inventStdCostConv);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>