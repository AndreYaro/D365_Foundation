<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmLeadCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Lead'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class smmLeadCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        smmLeadTable leadTable;
        select crosscompany Party from leadTable where leadTable.RecId == _caseAssociation.RefRecId;

        CustTable custTable;
        select firstonly RecId from custTable where custTable.Party == leadTable.Party;
        if (custTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Customer, custTable.RecId);
        }
        else
        {
            smmBusRelTable smmBusRelTable;
            select firstonly RecId from smmBusRelTable where smmBusRelTable.Party == leadTable.Party;

            _caseAssociation.insertCaseAssociation(CaseEntityType::BusinessRelation, smmBusRelTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        smmLeadTable leadTable;
        select crosscompany DataAreaId from leadTable where leadTable.RecId == _parentRecId;

        return leadTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        smmLeadTable leadTable;
        select crosscompany LeadId from leadTable where leadTable.RecId == _parentRecId;

        return leadTable.LeadId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        smmLeadTable leadTable;
        select RecId from leadTable where leadTable.LeadId == _naturalKey;

        return leadTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        smmLeadTable leadTable;
        select crosscompany Subject from leadTable where leadTable.RecId == _parentRecId;

        return leadTable.Subject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        smmLeadTable leadTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        smmLeadTable leadTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from leadTableCrossCompany
            where leadTableCrossCompany.RecId == _association.RefRecId;

        if (leadTableCrossCompany.Recid)
        {
            changecompany (leadTableCrossCompany.DataAreaId)
            {
                select firstonly leadTable where leadTable.RecId == _association.RefRecId;
            }
        }

        _args.record(leadTable);
        return menuitemdisplaystr(smmLeadTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(smmLeadTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(smmLeadTable));

        oSysTableLookup.addLookupfield(fieldnum(smmLeadTable, LeadId));
        oSysTableLookup.addLookupfield(fieldnum(smmLeadTable, Subject));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(smmLeadTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Lead);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>