<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventNonConformanceClose</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventNonConformanceClose</c> class marks a nonconformance as closed.
/// </summary>
class InventNonConformanceClose extends InventNonConformanceUpdate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Overrides the standard <c>run</c> method and marks the nonconformance as closed.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    ///    The nonconformance either does not exists or is already closed.
    /// </exception>
    public void run()
    {       
        if (!this.validate(null))
            return;

        super();
        ttsbegin;

        InventNonConformanceTable   inventNonConformanceTable;
        int rowCount;

        while select forupdate inventNonConformanceTable
            where inventNonConformanceTable.InventNonConformanceID        == inventNonConformanceID
                && !inventNonConformanceTable.Closed
        {
            inventNonConformanceTable.Closed = NoYes::Yes;
            inventNonConformanceTable.update();
            rowCount++;
        }

        if (rowCount != 1)
        {
            throw error(strFmt("@SYS114045", inventNonConformanceID));
        }
        InventNonConformanceHistory::insertClose(inventNonConformanceID, worker);

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates that the nonconformance exists and is not already closed.
    /// </summary>
    /// <param name="calledFrom">
    ///    This parameter is not used in this class. See the base class, <c>RunBase</c>, for a description.
    /// </param>
    /// <returns>
    ///    true if the validation succeeded; otherwise, false.
    /// </returns>
    public boolean validate(Object calledFrom = null)
    {
        boolean ret = super(calledFrom);

        if (inventNonConformanceID)
        {
            InventNonConformanceTable inventNonConformanceTable = InventNonConformanceTable::find(inventNonConformanceID);
            if (!inventNonConformanceTable || inventNonConformanceTable.Closed || inventNonConformanceTable.openCorrectionsExist())
            {
                return checkFailed(strFmt("@SYS114045", inventNonConformanceID));
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventNonConformanceClose construct()
    {
        return new InventNonConformanceClose();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static ClassDescription description()
    {
        return "@SYS114065";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        InventNonConformanceClose   inventNonConformanceClose = InventNonConformanceClose::construct();

        inventNonConformanceClose.initFromArgs(args);

        if (Box::yesNo("@SYS114063", DialogButton::No) == DialogButton::Yes)
        {
            inventNonConformanceClose.runOperation();

            if (args && args.record() && args.dataset() == tableNum(InventNonConformanceTable))
            {
                InventNonConformanceUpdate::refreshCaller(args.record());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
