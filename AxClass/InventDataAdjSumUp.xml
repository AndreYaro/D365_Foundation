<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDataAdjSumUp</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventDataAdjSumUp extends InventDataAdj
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcInventOnHandPercent</Name>
				<Source><![CDATA[
    Percent calcInventOnHandPercent(Percent percent)
    {
        CostAmount      positiveValue;
        CostAmount      negativeValue;
        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        while select tmp
        {
            if (tmp.PostedValue > 0 && tmp.Qty > 0)
            {
                positiveValue+= tmp.PostedValue;
            }
            else
            {
                negativeValue+= tmp.PostedValue;
            }
        }

        if (positiveValue+ negativeValue <= 0 || positiveValue <= 0)
        {
            percent = 0;
        }
        else
        {
            percent = ((positiveValue+ negativeValue) / positiveValue) * percent;
        }

        return percent;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcValueAmount</Name>
				<Source><![CDATA[
    CostAmount calcValueAmount(CostAmount value)
    {
        CostAmount      positiveValue;
        CostAmount      negativeValue;

        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        while select tmp
        {
            if (tmp.PostedValue > 0 && tmp.Qty > 0)
            {
                positiveValue+= tmp.PostedValue;
            }
            else
            {
                negativeValue+= tmp.PostedValue;
            }
        }

        if (positiveValue+ negativeValue <= 0 || positiveValue <= 0)
        {
            throw error("@SYS24508");
        }
        Percent percent = ((positiveValue+ negativeValue) / positiveValue) * (value - (positiveValue+ negativeValue)) / (positiveValue+ negativeValue) * 100;

        return CurrencyExchangeHelper::amount(positiveValue * percent / 100);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAdjustment</Name>
				<Source><![CDATA[
    void setAdjustment(TmpFormLookUp tmp, CostAmount adjustNow)
    {
        if (adjustNow != 0)
        {
            adjustNow = CurrencyExchangeHelper::amount(adjustNow);

            if (tmp.Qty   <= 0 )
            {
                throw error("@SYS18720");
            }

            if (tmp.PostedValue + adjustNow < 0)
            {
                throw error("@SYS2591");
            }
        }
        tmp.AdjustNow = adjustNow;
        tmp.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAdjustmentPercent</Name>
				<Source><![CDATA[
    void setAdjustmentPercent(Percent percent)
    {
        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        ttsbegin;
        while select forupdate tmp
        {
            if (tmp.Qty > 0 && tmp.PostedValue > 0)
            {
                this.setAdjustment(tmp,CurrencyExchangeHelper::amount(tmp.PostedValue * percent / 100));
            }
            else
            {
                this.setAdjustment(tmp,0);
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAdjustmentQty</Name>
				<Source><![CDATA[
    void setAdjustmentQty(CostAmount amount)
    {
        InventQty       qtyTotal;

        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        while select tmp
            where tmp.Qty > 0
        {
             qtyTotal  += tmp.Qty;
        }

        CostAmount amountRemain = amount;
        InventQty remain = qtyTotal;

        if (qtyTotal  <= 0)
        {
            throw error("@SYS24504");
        }

        while select forupdate tmp
        {
            if (tmp.Qty > 0)
            {
                if (tmp.Qty >= remain)
                {
                    this.setAdjustment(tmp,CurrencyExchangeHelper::amount(amountRemain));
                }
                else
                {
                    this.setAdjustment(tmp,CurrencyExchangeHelper::amount(amount * tmp.Qty / qtyTotal));
                }

                amountRemain    -= tmp.AdjustNow;
                remain          -= tmp.Qty;
            }
            else
            {
                this.setAdjustment(tmp,0);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAdjustmentValue</Name>
				<Source><![CDATA[
    void setAdjustmentValue(CostAmount amount)
    {
        CostAmount      amountInTotal;

        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        while select tmp
            where tmp.Qty     > 0 &&
                  tmp.PostedValue   > 0
        {
            amountInTotal+= tmp.PostedValue;
        }

        CostAmount amountRemain = amount;
        CostAmount remain = amountInTotal;

        if (amountInTotal <= 0)
        {
            throw error("@SYS24503");
        }

        while select forupdate tmp
        {
            if (tmp.PostedValue > 0 && tmp.Qty > 0)
            {
                if (tmp.PostedValue >= remain)
                {
                    this.setAdjustment(tmp,CurrencyExchangeHelper::amount(amountRemain));
                }
                else
                {
                    this.setAdjustment(tmp ,CurrencyExchangeHelper::amount(amount * tmp.PostedValue / amountInTotal));
                }

                amountRemain    -= tmp.AdjustNow;
                remain          -= tmp.PostedValue;
            }
            else
            {
                this.setAdjustment(tmp,0);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustAmountServer</Name>
				<Source><![CDATA[
    static void adjustAmountServer(TmpFormLookUp tmpFormLookUp, ValueQty valueQty, CostAmount amount)
    {
        InventDataAdjSumUp  inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.setTmpFormLookUp(tmpFormLookUp);

        if (valueQty == ValueQty::Value)
        {
            inventDataAdjSumUp.setAdjustmentValue(amount);
        }

        if (valueQty == ValueQty::Qty)
        {
            inventDataAdjSumUp.setAdjustmentQty(amount);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustCostPriceServer</Name>
				<Source><![CDATA[
    static void adjustCostPriceServer(TmpFormLookUp          tmp,
                                             InventTransCurrency_RU _inventTransCurrency = InventTransCurrency_RU::PrimaryCur)
    {
        InventDataAdjSumUp  inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.setTmpFormLookUp(tmp);

        ttsbegin;
        while select forupdate tmp
        {
            if (tmp.Qty > 0)
            {
                // <GEERU>
                if (_inventTransCurrency == InventTransCurrency_RU::PrimaryCur)
                {
                // </GEERU>
                    inventDataAdjSumUp.setAdjustment(tmp,CurrencyExchangeHelper::amount(tmp.Qty * InventTable::findCostPcsPrice(tmp.ItemId, tmp.InventDimId) - tmp.PostedValue));
                // <GEERU>
                }
                else
                {
                    inventDataAdjSumUp.setAdjustment(tmp,CurrencyExchangeHelper::amount(tmp.Qty * InventTable::findCostPcsPriceSecCur_RU(tmp.ItemId, tmp.InventDimId) - tmp.PostedValue));
                }
                // </GEERU>
            }
            else
            {
                inventDataAdjSumUp.setAdjustment(tmp,0);
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustPercentServer</Name>
				<Source><![CDATA[
    static void adjustPercentServer(TmpFormLookUp tmpFormLookUp, InventAdjustmentBy inventAdjustmentBy, Percent percent)
    {
        InventDataAdjSumUp  inventDataAdjSumUp = InventDataAdjSumUp::construct();
        Percent             percent1 = percent;

        inventDataAdjSumUp.setTmpFormLookUp(tmpFormLookUp);
        if (inventAdjustmentBy == InventAdjustmentBy::InventOnHandSum)
        {
            percent1 = inventDataAdjSumUp.calcInventOnHandPercent(percent1);
        }

        inventDataAdjSumUp.setAdjustmentPercent(percent1);
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustPriceServer</Name>
				<Source><![CDATA[
    static void adjustPriceServer(TmpFormLookUp tmp, Price price)
    {
        InventDataAdjSumUp  inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.setTmpFormLookUp(tmp);

        ttsbegin;
        while select forupdate tmp
        {
            if (tmp.Qty > 0)
            {
                inventDataAdjSumUp.setAdjustment(tmp,CurrencyExchangeHelper::amount(tmp.Qty * price - tmp.PostedValue));
            }
            else
            {
                inventDataAdjSumUp.setAdjustment(tmp,0);
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>adjustValueServer</Name>
				<Source><![CDATA[
    static void adjustValueServer(TmpFormLookUp tmpFormLookUp, ValueQty valueQty, CostAmount amountDialog)
    {
        InventDataAdjSumUp  inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.setTmpFormLookUp(tmpFormLookUp);

        CostAmount amount = inventDataAdjSumUp.calcValueAmount(amountDialog);

        if (valueQty == ValueQty::Value)
        {
            inventDataAdjSumUp.setAdjustmentValue(amount);
        }

        if (valueQty == ValueQty::Qty)
        {
            inventDataAdjSumUp.setAdjustmentQty(amount);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static InventDataAdjSumUp construct()
    {
        return new InventDataAdjSumUp();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromForm</Name>
				<Source><![CDATA[
    static InventDataAdjSumUp newFromForm(TmpFormLookUp tmpFormLookUp)
    {
        InventDataAdjSumUp inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.setTmpFormLookUp(tmpFormLookUp);
        return inventDataAdjSumUp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newPacked</Name>
				<Source><![CDATA[
    static InventDataAdjSumUp newPacked(container data)
    {
        InventDataAdjSumUp inventDataAdjSumUp = InventDataAdjSumUp::construct();

        inventDataAdjSumUp.unpack(data);
        return inventDataAdjSumUp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptAmount</Name>
				<Source><![CDATA[
    static boolean  promptAmount(TmpFormLookUp          tmpFormLookUp,
                                 InventTransCurrency_RU _inventTransCurrency = InventTransCurrency_RU::PrimaryCur)
    {
        // <GEERU>
        Dialog          dialog  = new Dialog(_inventTransCurrency == InventTransCurrency_RU::PrimaryCur ? "@SYS9523" : "@GLS102381");
        // </GEERU>
        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@SYS2674");
        }

        DialogField dialogAmount = dialog.addField(extendedTypeStr(CostAmount), "@SYS12227");

        DialogField dialogAllocate = dialog.addField(enumStr(ValueQty), "@SYS24502");

        if (! dialog.run())
        {
            return false;
        }

        InventDataAdjSumUp::adjustAmountServer(tmp, dialogAllocate.value(), dialogAmount.value());

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptCostPrice</Name>
				<Source><![CDATA[
    static boolean  promptCostPrice(TmpFormLookUp          tmpFormLookUp,
                                    InventTransCurrency_RU _inventTransCurrency = InventTransCurrency_RU::PrimaryCur)
    {
        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@SYS2674");
        }

        // <GEERU>
        if (Box::yesNo(_inventTransCurrency == InventTransCurrency_RU::PrimaryCur ? "@SYS79189" : "@GLS102382" ,DialogButton::No) != DialogButton::Yes)
        // </GEERU>
        {
            return false;
        }

        // <GEERU>
        InventDataAdjSumUp::adjustCostPriceServer(tmp,_inventTransCurrency);
        // </GEERU>
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptMarkupCode_RU</Name>
				<Source><![CDATA[
    static boolean promptMarkupCode_RU(
        FormDataSource  _formDataSource,
        boolean         _showDialog = true,
        MarkupCode      _markupCode = '',
        NoYes           _set4All    = NoYes::No)
    {
        Dialog          dialog  = new Dialog("@GLS113926");
        DialogField     dialogSet4All;

        MarkupCode      markupCode;
        NoYes           set4All;
        TmpFormLookUp   tmp, curRec;
        RecId           curRecId = _formDataSource.cursor().RecId;

        tmp.setTmpData(_formDataSource.cursor());
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@GLS113794");
        }

        DialogField dialogMarkupCode = dialog.addField(extendedTypeStr(MarkupCodeInvent_RU), "@SYS23727");

        if (!_formDataSource.getFirst(1))
        {
            dialogSet4All = dialog.addField(enumStr(NoYes), "@GLS113925");
        }

        if (_showDialog)
        {
            if (!dialog.run())
            {
                return false;
            }

            markupCode   = dialogMarkupCode.value();
            set4All      = dialogSet4All ? any2enum(dialogSet4All.value()) : NoYes::No;
            if (dialogSet4All)
            {
                set4All      = dialogSet4All.value();
            }
            else
            {
                set4All      = NoYes::No;
            }
        }
        else
        {
            markupCode   = _markupCode;
            set4All      = _set4All;
        }

        _formDataSource.formRun().lockWindowUpdate(true);

        if (dialogSet4All)
        {
            update_recordset tmp setting MarkupCode_RU = markupCode
                where set4All || tmp.RecId == _formDataSource.cursor().RecId;
        }
        else
        {
            curRec = _formDataSource.getFirst(1);
            while (curRec)
            {
                select firstonly forupdate tmp where tmp.RecId == curRec.RecId;
                tmp.MarkupCode_RU = markupCode;
                tmp.update();
                curRec = _formDataSource.getNext();
            }
        }

        _formDataSource.research();
        if (curRecId)
        {
            select firstonly tmp where tmp.RecId == curRecId;
            _formDataSource.findRecord(tmp);
        }

        _formDataSource.formRun().lockWindowUpdate(false);

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptPercent</Name>
				<Source><![CDATA[
    static boolean  promptPercent(TmpFormLookUp tmpFormLookUp)
    {
        Dialog          dialog  = new Dialog("@SYS24507");

        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@SYS2674");
        }

        DialogField dialogPercent = dialog.addField(extendedTypeStr(Percent), "@SYS15774");

        DialogField dialogMethod = dialog.addField(enumStr(InventAdjustmentBy), "@SYS10736");

        if (! dialog.run())
        {
            return false;
        }

        Percent percent = dialogPercent.value();

        if (percent < -100)
        {
            throw error("@SYS1852");
        }

        InventDataAdjSumUp::adjustPercentServer(tmp, dialogMethod.value(), percent);

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptPrice</Name>
				<Source><![CDATA[
    static boolean  promptPrice(TmpFormLookUp tmpFormLookUp)
    {
        Dialog          dialog  = new Dialog("@SYS4915");

        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@SYS2674");
        }

        DialogField dialogPrice = dialog.addField(extendedTypeStr(Price), "@SYS11865");

        if (! dialog.run())
        {
            return false;
        }

        Price price = dialogPrice.value();

        if (price < 0)
        {
            throw error("@SYS1852");
        }

        InventDataAdjSumUp::adjustPriceServer(tmp, price);

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptReset</Name>
				<Source><![CDATA[
    static boolean  promptReset(TmpFormLookUp tmpFormLookUp)
    {
        if (Box::yesNo("@SYS9461",DialogButton::No) == DialogButton::Yes)
        {
            delete_from tmpFormLookUp;

            return true;
        }
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>promptValue</Name>
				<Source><![CDATA[
    static boolean  promptValue(TmpFormLookUp tmpFormLookUp)
    {
        Dialog          dialog  = new Dialog("@SYS8668");
        TmpFormLookUp   tmp;

        tmp.setTmpData(tmpFormLookUp);
        select firstonly tmp;
        if (!tmp)
        {
            return checkFailed("@SYS2674");
        }

        DialogField dialogValue = dialog.addField(extendedTypeStr(CostAmount), "@SYS11762");
        DialogField dialogAllocate = dialog.addField(enumStr(ValueQty), "@SYS24502");

        if (! dialog.run())
        {
            return false;
        }

        InventDataAdjSumUp::adjustValueServer(tmp, dialogAllocate.value(), dialogValue.value());

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
