<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSWorkExecuteDisplayTOLineReceiving</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the logic behind the RF flow used to manage received transfer order lines.
/// </summary>
[WHSWorkExecuteMode(WHSWorkExecuteMode::TransferOrderLineReceiving)]
[WHSWorkExecuteMode(WHSWorkExecuteMode::TransferOrderLineReceivingAndLocate)]
class WhsWorkExecuteDisplayTOLineReceiving extends WhsWorkExecuteDisplayTOReceiving
{
    EcoResReleasedProductUnitConverter  unitConverter;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildTOReceiving</Name>
				<Source><![CDATA[
    protected container buildTOReceiving(container _con, str _extratext = '')
    {
        container           ret = _con;
        boolean             showExceptionButton;
        InventTable         inventTable;
        InventTransferLine  transferLine;
        InventDim           inventDim;
        WHSInventTable      whsInventTable;
        WHSProdQty          defaultQty;

        ret += [this.buildControl(#RFLabel, 'TOLineReceiving', pass.exists(#MenuItem) ? WHSRFMenuItemTable::find(pass.lookup(#MenuItem)).rfTitle : '', 1, '', #WHSRFUndefinedDataType, '', 0)];

        if (pass.exists(#TONum) && pass.lookup(#TONum) != '')
        {
            ret += [this.buildControl(#RFText, #TONum, "@WAX3858", 1, pass.lookup(#TONum), extendedTypeNum(InventTransferId), '', 0, false)];

            if (pass.exists(#TOLineNum) && pass.lookup(#TOLineNum) != '')
            {
                ret += [this.buildControl(#RFText, #TOLineNum, "@WAX1236", 1, pass.lookup(#TOLineNum), extendedTypeNum(LineNum), '', 0, false)];
                showExceptionButton = true;
            }
            else
            {
                ret += [this.buildControl(#RFText, #TOLineNum, "@WAX1236", 1, '', extendedTypeNum(LineNum), '', 0)];
            }
        }
        else
        {
            ret += [this.buildControl(#RFText, #TONum, "@WAX3858", 1, '', extendedTypeNum(InventTransferId), '', 0)];
            ret += [this.buildControl(#RFText, #TOLineNum, "@WAX1236", 1, '', extendedTypeNum(LineNum), '', 0)];
        }

        ret += [this.buildControl(#RFText, #ItemId, "@SYS14428", 1, pass.lookupStr(#ItemId), extendedTypeNum(InventInventItemSearchItem), '', 0, false)];

        if (pass.exists(#ItemId))
        {
            inventTable     = InventTable::find(pass.lookup(#ItemId));
            transferLine    = InventTransferLine::find(pass.lookup(#TONum), pass.lookupNum(#TOLineNum));
            inventDim.InventDimId = transferLine.InventDimId;
            inventDim = InventDim::find(transferLine.InventDimId);
            whsInventTable = WHSInventTable::find(inventTable.ItemId);
            defaultQty = whsInventTable.ProdQty;

            if (whsInventTable.rfDescription1)
            {
                ret += [this.buildControl(#RFLabel, #RFDescription1, strFmt("\t        %1", whsInventTable.rfDescription1), 1, '', #WHSRFUndefinedDataType, '', 0, false, "", WHSRFColorText::Default, extendedTypeNum(WHSRFDescription1))];
            }
            else
            {
                ret += [this.buildControl(#RFLabel, #RFDescription1, strFmt("\t        %1", inventTable.productNameWhsWorkExecute()), 1, '', #WHSRFUndefinedDataType, '', 0, false, "", WHSRFColorText::Default, extendedTypeNum(EcoResProductName))];
            }

            if (whsInventTable.rfDescription2)
            {
                ret += [this.buildControl(#RFLabel, #RFDescription2, strFmt("\t        %1", whsInventTable.rfDescription2), 1, '', #WHSRFUndefinedDataType, '', 0, false, "", WHSRFColorText::Default, extendedTypeNum(WHSRFDescription2))];
            }

            ret = this.buildProductDimensionsFromInventDim(ret, inventTable.ItemId, inventDim, false);
        }

        if ((!pass.exists(#Qty) || pass.lookupNum(#Qty) == 0) && defaultQty)
        {
            pass.insert(#Qty, defaultQty);
        }
        if (!pass.exists(#SelectedValueUOM) || pass.lookup(#SelectedValueUOM) == '')
        {
            pass.insert(#SelectedValueUOM, WHSUOMSeqGroupTable::getDefaultPurchUOM(inventTable.ItemId));
        }

        ret += [this.buildControl(#RFText, #Qty, "@SYS105177", 1, pass.lookupStr(#Qty), extendedTypeNum(Qty), '', 0)];
        ret = this.buildUOM(ret, '', false, false, false, pass.lookupStr(#SelectedValueUOM));

        if (inventTable.ItemId
            && WHSInventTable::shouldCaptureAggregateWeight(inventTable.ItemId)
            && WHSInventTable::isCatchWeightTagTracked(inventTable.ItemId))
        {
            ret += [this.buildControl(#RFText,
                                      WHSWorkExecuteDisplayCatchWeightControls::CatchWeight,
                                      "@WAX:Weight",
                                      1,
                                      pass.lookupStr(WHSWorkExecuteDisplayCatchWeightControls::CatchWeight),
                                      extendedTypeNum(Weight),
                                      '',
                                      0)];
        }

        InventHandlingUnitId inventHandlingUnitId = inventTable.ItemId ? WHSCatchWeightHelper::inventHandlingUnitId(inventTable.ItemId) : '';
        
        if (pass.exists(#SelectedValueUOM)
            && pass.lookup(#SelectedValueUOM) != ''
            && pass.lookup(#SelectedValueUOM) != inventHandlingUnitId)
        {
            if (!pass.exists(#CurrentUOM) || pass.lookup(#CurrentUOM) != pass.lookup(#SelectedValueUOM))
            {
                pass.insert(#CurrentUOM, pass.lookup(#SelectedValueUOM));
            }
            else if (!pass.exists(#Initiation))
            {
                pass.remove(#CurrentUOM);
            }

            // Only add conversion information if it is meaningful
            if (!inventTable.isProductVariantUnitConversionEnabled()
                || unitConverter.canResolveProductVariant(inventTable.ItemId, inventDim))
            {
                ret += [this.buildControl(#RFLabel, 'Invent Conversion', strFmt("1 %1 = %2 %3",
                                                                            pass.lookup(#SelectedValueUOM),
                                                                            EcoResReleasedProductUnitConverter::construct().convertGivenUnitSymbols(
                                                                                1,
                                                                                pass.lookup(#SelectedValueUOM),
                                                                                inventHandlingUnitId,
                                                                                NoYes::No,
                                                                                inventTable.ItemId,
                                                                                inventDim.InventDimId,
                                                                                NoYes::Yes),
                                                                            inventHandlingUnitId), 1, '', #WHSRFUndefinedDataType, '', 0)];
            }
        }
        else
        {
            if (pass.exists(#CurrentUOM))
            {
                pass.remove(#CurrentUOM);
            }
        }

        if (WHSRFMenuItemTable::find(pass.lookup(#MenuItem)).ShowDisposition)
        {
            ret = this.buildDispositon(ret, pass.lookupStr(#Disposition), true, true);
        }

        ret += [this.buildControl(#RFButton, #RFOK, "@SYS5473", 1, '', #WHSRFUndefinedDataType, '', 1)];

        if (showExceptionButton)
        {
            ret = this.addCancelButton(ret, 1, true);
            ret = this.addExceptionButton(ret);
            pass.insert(#NoCancelButton, '1');
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTOReceivingAndPutawayExecuteMode</Name>
				<Source><![CDATA[
    protected WHSWorkExecuteMode getTOReceivingAndPutawayExecuteMode()
    {
        return WHSWorkExecuteMode::TransferOrderLineReceivingAndLocate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        unitConverter = EcoResReleasedProductUnitConverter::construct();
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>