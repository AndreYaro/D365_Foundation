<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmTotalCompStatementUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>HcmTotalCompStatementUIBuilder</c> class is used to build the user interface for the report parameter form.
/// </summary>
public class HcmTotalCompStatementUIBuilder extends SrsReportDataContractUIBuilder
{
    FormBuildGroupControl           sectionGroupControl;
    HcmTotalCompSectionsListPanel   hcmTotalCompSectionsListPanel;
    HcmTotalCompStatementContract   dataContract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addHcmTotalCompSectionsListPanel</Name>
				<Source><![CDATA[
    private void addHcmTotalCompSectionsListPanel()
    {
        hcmTotalCompSectionsListPanel = HcmTotalCompSectionsListPanel::newForm(sectionGroupControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    public void build()
    {
        Dialog dlg = this.dialog() as Dialog;
        dlg.dialogForm().buildDesign().dialogSize(DialogSize::Large);

        dataContract = this.dataContractObject();
        if (!dataContract.parmTotalRowText())
        {
            dataContract.parmTotalRowText("@SYS179740");
        }

        if (!dataContract.parmStatementTitle())
        {
            dataContract.parmStatementTitle("@HCM:TotalCompensationStatement");
        }

        super();

        this.addHcmTotalCompSectionsListPanel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGroup</Name>
				<Source><![CDATA[
    protected void buildGroup(SysOperationGroupInfo _groupInfo, FormBuildGroupControl _groupControl)
    {
        if (_groupControl.name() == 'SectionGroup')
        {
            sectionGroupControl = _groupControl;
        }

        super(_groupInfo, _groupControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public void getFromDialog()
    {
        super();

        dataContract.parmStatementSectionsList(this.getListPanelSelectedFields(hcmTotalCompSectionsListPanel as SysListPanel));

        if (!dataContract.parmTotalRowText())
        {
            dataContract.parmTotalRowText("@SYS179740");
        }

        if (!dataContract.parmStatementTitle())
        {
            dataContract.parmStatementTitle("@HCM:TotalCompensationStatement");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getListPanelSelectedFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a list of selected rows in a sys list panel.
    /// </summary>
    /// <param name="listPanel">
    /// The sys list panel to grab the rows from.
    /// </param>
    /// <returns>
    /// A list of selected rows.
    /// </returns>
    private str getListPanelSelectedFields(SysListPanel listPanel)
    {
        int i;
        str selectedSectionList;

        for (i = 0; i < listPanel.parmViewSelected().getCount(); i++)
        {
            if (selectedSectionList)
            {
                selectedSectionList += ",";
            }

            selectedSectionList += listPanel.parmViewSelected().getItem(i).data();
        }

        return selectedSectionList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initHcmTotalCompSectionsListPanel</Name>
				<Source><![CDATA[
    private void initHcmTotalCompSectionsListPanel()
    {
        hcmTotalCompSectionsListPanel.parmFormRun(this.dialog().formRun());
        hcmTotalCompSectionsListPanel.init();
        hcmTotalCompSectionsListPanel.parmStatementSectionsList(dataContract.parmStatementSectionsList());
        hcmTotalCompSectionsListPanel.fill();
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers the dialog field methods to capture events.
    /// </summary>
    public void postRun()
    {
        super();

        this.initHcmTotalCompSectionsListPanel();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>