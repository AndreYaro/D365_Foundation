<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsPickingRouteCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class WmsPickingRouteCancel extends RunBase
{
    WMSPickingRouteID   wmsPickingRouteID;

    #define.CurrentVersion(2)
    #localmacro.CurrentList
        wmsPickingRouteId
    #endmacro

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog(DialogRunbase dialog = null, boolean forceOnClient = false)
    {
        DialogRunbase ret;

        ret = this.dialogInternal(dialog, forceOnClient);
        ret.addText(strFmt("@SYS114078", wmsPickingRouteID));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWmsPickingRouteId</Name>
				<Source><![CDATA[
    public WMSPickingRouteID parmWmsPickingRouteId(WMSPickingRouteID _wmsPickingRouteID = wmsPickingRouteID)
    {
        wmsPickingRouteID = _wmsPickingRouteID;
        return wmsPickingRouteID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    This method will cancel the picking route that instantiated this object and all picking lines that
    ///    belong to this picking route.
    /// </summary>
    public void run()
    {
        #OCCRetryCount
        WMSPickingRoute wmsPickingRouteUpdate;

        try
        {
            ttsbegin;

            wmsPickingRouteUpdate = WMSPickingRoute::find(wmsPickingRouteID,true);
            if (!wmsPickingRouteUpdate.RecId)
            {
                throw (error(strFmt("@SYS76877",tableId2pname(tableNum(WMSPickingRoute)))));
            }

            if (!wmsPickingRouteUpdate.canBeCancelled())
            {
                throw(error("@SYS50574"));
            }

            wmsPickingRouteUpdate.cancel();

            ttscommit;
        }
        catch(Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Version     version;

        if (packedClass == conNull())
        {
            return false;
        }

        version = RunBase::getVersion(packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object calledFrom = null)
    {
        WMSPickingRoute     wmsPickingRoute = WMSPickingRoute::find(wmsPickingRouteID);

        if (!wmsPickingRoute.RecId)
        {
            throw(error(strFmt("@SYS51116",wmsPickingRoute.PickingRouteID)));
        }

        if (!wmsPickingRoute.canBeCancelled(NoYes::Yes))
        {
            throw(error("@SYS122557"));
        }

        return super(calledFrom);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WMSPickingRouteCancel construct()
    {
        return new WMSPickingRouteCancel();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS114076";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        WMSPickingRouteCancel   wmsPickingRouteCancel;
        WMSPickingRoute         wmsPickingRoute;
        Object                  callerForm;

        if (!args || !args.record())
        {
            throw error(strFmt("@SYS19306",funcName()));
        }

        switch (args.record().TableId)
        {
            case (tableNum(WMSPickingRoute)) :
                wmsPickingRoute = args.record();

                wmsPickingRouteCancel = WMSPickingRouteCancel::newWMSPickingRoute(wmsPickingRoute);

                if (wmsPickingRouteCancel.prompt())
                {
                    wmsPickingRouteCancel.runOperation();

                    wmsPickingRoute = args.record();
                    if (FormDataUtil::isFormDataSource(wmsPickingRoute))
                    {
                        callerForm = args.caller();
                        if (formHasMethod(callerForm,identifierStr(doResearch)))
                        {
                            callerForm.doResearch();
                        }
                        else
                        {
                            FormDataUtil::getFormDataSource(wmsPickingRoute).reread();
                            FormDataUtil::getFormDataSource(wmsPickingRoute).refresh();
                        }
                    }
                }
                break;
            default:
                throw error(strFmt("@SYS19306",funcName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newWMSPickingRoute</Name>
				<Source><![CDATA[
    static WMSPickingRouteCancel newWMSPickingRoute(WMSPickingRoute _wmsPickingRoute)
    {
        WMSPickingRouteCancel   wmsPickingRouteCancel = WMSPickingRouteCancel::construct();

        wmsPickingRouteCancel.getLast();
        wmsPickingRouteCancel.parmWmsPickingRouteId(_wmsPickingRoute.PickingRouteID);

        return wmsPickingRouteCancel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>