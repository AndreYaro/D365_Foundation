<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAVendorRetentionUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class PSAVendorRetentionUIBuilder extends SrsReportDataContractUIBuilder
{
    PSAVendorRetentionContract contract;
    DialogField dialogFieldAssessment;
    DialogField dialogFieldVendor;
    DialogField dialogFieldProject;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>assessmentField_Modified</Name>
				<Source><![CDATA[
        public boolean assessmentField_Modified(FormComboBoxControl _control)
    {
        boolean ret = true;

        if (_control.modified())
        {
            if (dialogFieldAssessment.value() == VendorRetention::Vendor)
            {
               dialogFieldVendor.enabled(true);
               dialogFieldProject.enabled(false);
            }

            if (dialogFieldAssessment.value() == VendorRetention::Project)
            {
               dialogFieldProject.enabled(true);
               dialogFieldVendor.enabled(false);
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        boolean projMultContractLinesForProjFeatureEnabled = ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled();

        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmTimeMaterial), !projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmFixedPrice), !projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmInvestment), !projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmInternal), !projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmCostProject), !projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmTimeMaterialBillingMethod), projMultContractLinesForProjFeatureEnabled);
        this.setVisibilityForControls(methodstr(PSAVendorRetentionContract, parmFixedPriceBillingMethod), projMultContractLinesForProjFeatureEnabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
        public void postRun()
    {
        Dialog      dialogLocal = this.dialog();

        super();

        dialogLocal.dialogForm().formRun().controlMethodOverload(false);

        dialogFieldAssessment = this.bindInfo().getDialogField(
            this.dataContractObject(),
            methodstr(PSAVendorRetentionContract, parmVendorRetention));

        dialogFieldVendor = this.bindInfo().getDialogField(
            this.dataContractObject(),
            methodstr(PSAVendorRetentionContract, parmVendAccount));

        dialogFieldProject = this.bindInfo().getDialogField(
            this.dataContractObject(),
            methodstr(PSAVendorRetentionContract, parmProjId));

        dialogFieldAssessment.registerOverrideMethod(methodstr(FormComboBoxControl, modified), methodstr(PSAVendorRetentionUIBuilder, assessmentField_Modified), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setVisibilityForControls</Name>
				<Source><![CDATA[
    private void setVisibilityForControls(identifierName methodName, boolean _visible = false)
    {
        DialogField dialogField = this.bindInfo().getDialogField(this.dataContractObject(), methodName);

        if (dialogField)
        {
            dialogField.visible(_visible);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>