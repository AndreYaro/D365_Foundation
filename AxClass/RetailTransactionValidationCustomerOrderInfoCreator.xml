<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailTransactionValidationCustomerOrderInfoCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics.Commerce.Headquarters.Instrumentation;
using Microsoft.Dynamics.Commerce.Runtime.Services.CustomerOrder;
using Microsoft.Dynamics.Commerce.Runtime.DataModel;

/// <summary>
/// The <c>RetailTransactionValidationCustomerOrderInfoCreator</c> class is responsible for creating the <c>CustomerOrderInfo</c> object 
/// so that this object can be used by the Transaction validation stack to validate cash and carry transactions.
/// </summary>
internal final class RetailTransactionValidationCustomerOrderInfoCreator
{
    private static CommerceHQEventSource eventSource = CommerceHQEventSource::Log;

    private RetailTransactionTable transactionTable;
    private System.Diagnostics.Stopwatch transformerStopwatch;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {

    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of <c>RetailTransactionValidationCustomerOrderInfoCreator</c>.
    /// </summary>
    /// <param name = "_transactionTable">The transaction record.</param>
    /// <returns>A <c>RetailTransactionValidationCustomerOrderInfoCreator</c> object.</returns>
    public static RetailTransactionValidationCustomerOrderInfoCreator construct(RetailTransactionTable _transactionTable)
    {
        if (!_transactionTable)
        {
            throw error(error::wrongUseOfFunction(funcName()));
        }

        RetailTransactionValidationCustomerOrderInfoCreator instance = new RetailTransactionValidationCustomerOrderInfoCreator();
        instance.transactionTable = _transactionTable;
        instance.transformerStopwatch = new System.Diagnostics.Stopwatch();

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the <c>CustomerOrderInfo</c> object.
    /// </summary>
    /// <returns>A <c>CustomerOrderInfo</c> instance representing RetailTransactionTable and its children table.</returns>
    public CustomerOrderInfo create()
    {
        CustomerOrderInfo customerOrderInfo;
        
        transformerStopwatch.Start();

        eventSource.EventWriteTransactionValidationStartCreateCustOrderInfoForTransaction(transactionTable.RecId);
        
        RetailTransactionTableColumnInitializer::initTaxIncludedInPrice(transactionTable);

        using (RetailTransactionTransformerContext context = RetailTransactionTransformerContext::construct())
        {
            context.parmIgnoreReturnLink(true);
            context.parmUseTaxIncludedInPriceFlag(true);
            context.parmSetGiftCard(true);
            context.parmReadPaymentTrans(true);
            context.parmSkipTaxRounding(true);

            customerOrderInfo = RetailTransactionTransformer::ConvertTransactionToCustomerOrderInfo(transactionTable);
        }

        transformerStopwatch.Stop();
        eventSource.EventWriteTransactionValidationFinishCreateCustOrderInfoForTransaction(transactionTable.RecId, transformerStopwatch.ElapsedMilliseconds);
        
        return customerOrderInfo;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>