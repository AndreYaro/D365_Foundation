<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFDIWithholdingMailSender_MX</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CFDIWithholdingMailSender_MX</c> class is responsible to send the CFDI witholding documents to a vendor.
/// </summary>
public class CFDIWithholdingMailSender_MX extends EInvoiceCFDIMailSender_MX
{
    private const str FilePrefix = 'XMLRetenciones_';

    private VendTable vendTable;
    private CFDIWithholdingJour_MX withholdingJour;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAttachmentFilePrefix</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets file prefix for attachment.
    /// </summary>
    /// <returns>
    /// File prefix.
    /// </returns>
    protected str getAttachmentFilePrefix()
    {
        return FilePrefix;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEInvoiceMail</Name>
				<Source><![CDATA[
    protected EInvoiceMail_MX getEInvoiceMail(
        Filename _tempFile,
        BinData _attachmentContent,
        SysEmailRecipients _email,
        SysEmailDescriptionType _eMailDescription)
    {
        return CFDIWithholdingMail_MX::construct(_tempFile, _attachmentContent, _email, _eMailDescription);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new(EInvoiceJour_MX _eInvoiceJour)
    {
        withholdingJour = CFDIWithholdingJour_MX::find(_eInvoiceJour.RefRecId);
        vendTable = VendTable::find(withholdingJour.AccountNum);

        super(_eInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>sendMail</Name>
				<Source><![CDATA[
    public void sendMail(boolean _sendByMail = eInvoiceCFDIParameters.WithholdingSendByMail)
    {
        if (_sendByMail)
        {
            this.sendXMLFile();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>sendXMLFile</Name>
				<Source><![CDATA[
    protected boolean sendXMLFile()
    {
        Email email = vendTable.email();

        if (email)
        {
            BinData xmlBinData = new BinData();
            xmlBinData.setAsciiData(einvoiceJour.XMLDoc);

            this.sendEinvoiceMail(email, this.getTempFileName(), xmlBinData);

            return true;
        }
        else
        {
            warning(strfmt("@MexicoCFDIWithholding:VendorDoesNotHaveEmail_error", vendTable.AccountNum));
            return false;
        }
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CFDIWithholdingMailSender_MX construct(EInvoiceJour_MX _eInvoiceJour)
    {
        return new CFDIWithholdingMailSender_MX(_eInvoiceJour);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>