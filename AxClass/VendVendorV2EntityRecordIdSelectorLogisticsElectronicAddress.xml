<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendVendorV2EntityRecordIdSelectorLogisticsElectronicAddress</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

[ExportMetadataAttribute(identifierStr(TableName), tableStr(LogisticsElectronicAddress))]
[ExportMetadataAttribute(identifierStr(EntityName), dataentityviewstr(VendVendorV2Entity))]
[ExportAttribute(identifierstr(Dynamics.AX.Application.IBusinessEventsTableToEntityRecordIdSelector))]
public final class VendVendorV2EntityRecordIdSelectorLogisticsElectronicAddress implements IBusinessEventsTableToEntityRecordIdSelector
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEntityRecordIdsImpactedByTableChange</Name>
				<Source><![CDATA[
    public List getEntityRecordIdsImpactedByTableChange(BusinessEventsDefinition _eventDefinition, BusinessEventsActions _changeType, RecId _changedRecordId)
    {
        // Only process primary contact changes for types that are mapped and retun null for mappings not known.
        List impactedRecIds = null;

        FieldId primaryContactFieldId;

        // This mapping is defined in VendVendorV2Entity::getEntityDataSourceToFieldMapping.
        // That definition and this mappy need to stay in sync.
        switch (_eventDefinition.DataSourceName)
        {
            case identifierstr(DirPartyBaseEntity):
                primaryContactFieldId = fieldNum(DirPartyTable, PrimaryContactURL);
                break;
            case identifierstr(DirPartyBaseEntity1):
                primaryContactFieldId = fieldNum(DirPartyTable, PrimaryContactPhone);
                break;
            case identifierstr(DirPartyBaseEntity2):
                primaryContactFieldId = fieldNum(DirPartyTable, PrimaryContactEmail);
                break;
            case identifierstr(DirPartyBaseEntity3):
                primaryContactFieldId = fieldNum(DirPartyTable, PrimaryContactFax);
                break;
            default:
                primaryContactFieldId = 0;
                break;
        }

        if (primaryContactFieldId)
        {
            impactedRecIds = new List(Types::Int64);

            VendTable vendTable;
            DirPartyTable dirPartyTable;

            while select RecId from vendTable
                exists join dirPartyTable
                    where dirPartyTable.RecId == vendTable.Party
                        && dirPartyTable.(primaryContactFieldId) == _changedRecordId
            {
                impactedRecIds.addEnd(vendTable.RecId);
            }
        }

        return impactedRecIds;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>