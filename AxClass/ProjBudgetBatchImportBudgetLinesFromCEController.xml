<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjBudgetBatchImportBudgetLinesFromCEController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Service controller class for class <c>ProjBudgetBatchImportBudgetLinesFromCEService</c> of method <c>importBudgetLinesFromDataverse</c>.
/// </summary>
public final class ProjBudgetBatchImportBudgetLinesFromCEController extends SysOperationServiceController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        super();

        this.parmDialogCaption("@Proj:ProjBudgetBatchImportBudgetLinesTaskName");
        this.parmShowDialog(true);
        this.parmShowProgressForm(false);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main (Args _args)
    {
        ProjTable projTable;
        
        ProjBudgetBatchImportBudgetLinesFromCEController controller = new ProjBudgetBatchImportBudgetLinesFromCEController();
        controller.parmClassName(classstr(ProjBudgetBatchImportBudgetLinesFromCEService));
        controller.parmMethodName(methodstr(ProjBudgetBatchImportBudgetLinesFromCEService, importBudgetLinesFromDataverse));
        controller.parmExecutionMode(SysOperationExecutionMode::Synchronous);

        ProjBudgetBatchImportBudgetLinesFromCEContract request = controller.getBaseDataContract() as ProjBudgetBatchImportBudgetLinesFromCEContract;
        request.parmProjTable(projTable);
                
        controller.startOperation();
        SysGlobalCache cache = ClassFactory.globalCache();
        if (controller.isDialogCancelled())
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::Cancel);
        }
        else if (controller.parmExecutionMode() == SysOperationExecutionMode::ScheduledBatch)
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::ScheduledBatch);
        }
        else if (controller.parmExecutionMode() == SysOperationExecutionMode::Synchronous)
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::Synchronous);
        }
        else if (controller.parmExecutionMode() == SysOperationExecutionMode::ReliableAsynchronous)
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::ReliableAsynchronous);
        }
        else if (controller.parmExecutionMode() == SysOperationExecutionMode::Asynchronous)
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::Asynchronous);
        }
        else
        {
            cache.set(classStr(ProjBudgetBatchImportBudgetLinesFromCEController), _args.parm(), ProjBudgetBatchImportBudgetLineOption::NotSpecified);
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>doBatch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Perform batch process.
    /// </summary>
    /// <returns><c>Batch</c> table buffer.</returns>
    public Batch doBatch()
    {
        if (executionMode == SysOperationExecutionMode::ScheduledBatch)
        {
            BatchHeader batchheader = this.batchInfo().parmBatchHeader();
            batchheader.clearAllAlerts();
            batchheader.addUserAlerts(curUserId(), NoYes::Yes, NoYes::Yes, NoYes::Yes, NoYes::Yes, NoYes::No);
        }
    
        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isDialogCancelled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Verifies whether user has cancelled further execution of batch tasks by cancellation of the batch parameters dialog.
    /// </summary>
    /// <returns>
    /// true if the user has cancelled further processing; otherwise false.
    /// </returns>
    public boolean isDialogCancelled()
    {
        return dialogCanceled;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>