<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchRFQReplySubmitController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchRFQReplySubmitController</c> class acts as a controller for the <c>PurchRFQReplySubmitService</c> service.
/// </summary>
internal class PurchRFQReplySubmitController extends SysOperationServiceController implements BatchRetryable
{
    private boolean allowRunningInBatch;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of <c>PurchRFQReplySubmitController</c> class.
    /// </summary>
    /// <param name = "_args">A controller arguments.</param>
    /// <returns>A instance of <c>SysOperationController</c> class.</returns>
    public static SysOperationController construct(Args _args)
    {
        if (!_args
            || !_args.record()
            || (_args.dataset() != tableNum(PurchRFQTable)
                && _args.dataset() != tableNum(PurchRFQReplyTable)))
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        PurchRFQReplySubmitController controller = new PurchRFQReplySubmitController(classstr(PurchRFQReplySubmitService),
                                                            methodstr(PurchRFQReplySubmitService, sendRFQ),
                                                            SysOperationExecutionMode::Synchronous);
        controller.parmArgs(_args);
        controller.initializeContract(controller);

        controller.parmShowDialog(controller.allowRunningInBatch);

        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the contract parameters.
    /// </summary>
    /// <param name = "_controller">An instance of <c>PurchRFQReplySubmitController</c> whose contract to initialize.</param>
    private void initializeContract(PurchRFQReplySubmitController _controller)
    {
        PurchRFQId rfqId;
        PurchRFQReplyTable purchRFQReplyTable;

        switch (_controller.parmArgs().dataset())
        {
            case tablenum(PurchRFQReplyTable):
                purchRFQReplyTable = _controller.parmArgs().record();
                rfqId = purchRFQReplyTable.RFQId;

                if (PurchRFQReplyEditAllowSubmitDeclineInBatch::instance().isEnabled())
                {
                    allowRunningInBatch = purchRFQReplyTable.isProcurementAgentEditing();
                }
                break;

            case tablenum(PurchRFQTable):
                PurchRFQTable purchRFQTable = _controller.parmArgs().record();
                rfqId = purchRFQTable.RFQId;

                if (PurchRFQReplyEditAllowSubmitDeclineInBatch::instance().isEnabled())
                {
                    purchRFQReplyTable = PurchRFQReplyTable::find(rfqId);

                    if (purchRFQReplyTable)
                    {
                        allowRunningInBatch = purchRFQReplyTable.isProcurementAgentEditing();
                    }
                }
                break;
        }

        if (rfqId)
        {
            PurchRFQReplyEditContract contract = _controller.getDataContractObject() as PurchRFQReplyEditContract;

            if (contract)
            {
                contract.parmPurchRFQId(rfqId);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return allowRunningInBatch;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the operation.
    /// </summary>
    /// <param name = "_args">The arguments for which to run the operation.</param>
    public static void main(Args _args)
    {
        PurchRFQReplySubmitController::construct(_args).startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>caption</Name>
				<Source><![CDATA[
    public ClassDescription caption()
    {
        return "@SCM:Caption_PurchRFQReplySubmitController";
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>