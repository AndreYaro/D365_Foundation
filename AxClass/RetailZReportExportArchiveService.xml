<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailZReportExportArchiveService</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The <c>RetailZReportExportArchiveService</c> class is a service that is used to export the Retail Z-report archive.
/// </summary>
internal final class RetailZReportExportArchiveService extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs Z-report export.
    /// </summary>
    /// <param name = "_dataContract">A data contract.</param>
    public void run(RetailZReportExportArchiveContract _dataContract)
    {
        ERIFormatMappingRun formatMappingRun = ERObjectsFactory::createFormatMappingRunByFormatMappingId(_dataContract.parmERFormatMappingId(), _dataContract.parmExportFileName());

        this.initFileDestinationFactory(formatMappingRun, _dataContract);
        this.initParameters(formatMappingRun, _dataContract);

        formatMappingRun.run();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFileDestinationFactory</Name>
				<Source><![CDATA[
    private void initFileDestinationFactory(ERIFormatMappingRun _formatMappingRun, RetailZReportExportArchiveContract _dataContract)
    {
        if (_dataContract.parmIsSigningRequired())
        {
            ERIFileDestinationFactoryHost factoryHost = ERCast::asObject(_formatMappingRun) as ERIFileDestinationFactoryHost;

            if (factoryHost)
            {
                ERIFileDestinationFactory fileDestinationFactory = factoryHost.getFileDestinationFactory();
                factoryHost.setFileDestinationFactory(RetailZReportExportArchiveDestinationFactory::construct(fileDestinationFactory, _dataContract));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    private void initParameters(ERIFormatMappingRun _formatMappingRun, RetailZReportExportArchiveContract _dataContract)
    {
        ERModelDefinitionInputParametersAction inputParameters = new ERModelDefinitionInputParametersAction()
            .addParameter('model/ExportFileName', _dataContract.parmExportFileName())
            .addParameter('model/RetailPosBatchTableRecId', _dataContract.parmPosBatchTableRecId());

        ERIModelDefinitionParamsUIAction parameters = new ERModelDefinitionParamsUIActionComposite()
            .Add(inputParameters);

        _formatMappingRun
            .withFileDestination(_dataContract.getFileDestination())
            .withParameter(parameters);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>