<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentFAJourREGenerator_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Reduction entry acquisition generator.
/// </summary>
[AssetDocumentGenerateTypeAttribute_JP(AssetDocumentGenerateType_JP::ReductionEntryFAJournal)]
class AssetDocumentFAJourREGenerator_JP extends AssetDocumentFAJournalGenerator_JP
{
    AssetTransTypeJournal           assetTransTypeJournal;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createTrans</Name>
				<Source><![CDATA[
    protected void createTrans()
	{
	    if (assetTransTypeJournal == AssetTransTypeJournal::Acquisition)
	    {
	        super();
	    }
	    else if (assetTransTypeJournal == AssetTransTypeJournal::DisposalSale
	            || assetTransTypeJournal == AssetTransTypeJournal::DisposalScrap)
	    {
	        ttsBegin;
	        AssetReductionEntryManager_JP::generateRepaymentTrans(
	            this.parmAssetBook(),
	            this.parmAssetTransDate(),
	            this.parmAssetJournal()
	            );
	        ttsCommit;
	    }
	}

]]></Source>
			</Method>
			<Method>
				<Name>getAssetTransTypeJournal</Name>
				<Source><![CDATA[
    public AssetTransTypeJournal getAssetTransTypeJournal()
    {
        if (!assetTransTypeJournal)
        {
            assetTransTypeJournal = AssetTransTypeJournal::Acquisition;
        }

        return assetTransTypeJournal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init(Args _args)
    {
        AssetDocumentEntry_JP entryTableLoc;
        AssetDocumentTable_JP documentTableLoc;

        LedgerJournalTrans          ledgerJournalTransLoc = _args.record();
        LedgerJournalTrans_Asset    ledgerJournalTransAssetLoc;

        if (ledgerJournalTransLoc.RecId)
        {
            ledgerJournalTransAssetLoc = LedgerJournalTrans_Asset::find(ledgerJournalTransLoc.RecId);
        }
        assetTransTypeJournal = ledgerJournalTransAssetLoc.TransType;

        super(_args);

        if (assetBook)
        {
            select firstonly *
                from entryTableLoc
                where entryTableLoc.AssetId  == assetBook.AssetId
                    && entryTableLoc.BookId  == assetBook.BookId
                join documentTableLoc
                where documentTableLoc.MainType == AssetDocumentMainType_JP::ReductionEntry
                    && documentTableLoc.RecId   == entryTableLoc.AssetDocumentTable_JP;

            this.parmAssetDocumentEntry(entryTableLoc);
            this.parmAssetDocumentTable(documentTableLoc);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    protected boolean validate()
    {
        boolean ret = true;

        ret = super();
        if (ret)
        {
            if ((this.parmAssetDocumentTable().MainType != AssetDocumentMainType_JP::ReductionEntry
                || (this.parmAssetDocumentTable().Type != AssetDocumentType_JP::AssetReductionEntry_DirectOff
                    && this.parmAssetDocumentTable().Type != AssetDocumentType_JP::AssetReductionEntry_Reserve)
                || this.parmLedgerJournalTransAsset().DocumentType_JP != AssetDocumentType_JP::None
                || this.parmLedgerJournalTransAsset().AssetDocumentEntry_JP)
                ||
                // Generate reduction entry trans when doing acquisition
                (
                    this.parmLedgerJournalTransAsset().TransType != AssetTransTypeJournal::Acquisition
                    &&
                    // Generate reduction entry trans when doing disposal
                    (
                        (this.parmLedgerJournalTransAsset().TransType != AssetTransTypeJournal::DisposalSale
                        && this.parmLedgerJournalTransAsset().TransType != AssetTransTypeJournal::DisposalScrap)
                        || !AssetReductionEntryManager_JP::canGenerateRepaymentTrans(this.parmAssetBook().AssetId, this.parmAssetBook().BookId))
                    )
                )
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>