<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerTransStatementController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerTransStatementController</c> class is the controller class for the
///    <c>LedgerTransStatement</c> SRS Report.
/// </summary>
public class LedgerTransStatementController extends SrsReportRunController implements BatchRetryable
{
    #ISOCountryRegionCodes

    private date userPreferredCurrentDate = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getQuery</Name>
				<Source><![CDATA[
    public Query getQuery()
    {
        return this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey());
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastValueDesignName</Name>
				<Source><![CDATA[
    protected IdentifierName lastValueDesignName()
    {
        if (this.parmArgs() != null && this.parmArgs().dataset() == tableNum(MainAccount) && this.parmArgs().record() != null)
        {
            return super() + tableStr(MainAccount);
        }
        else
        {
            return super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets default parameter values before the user is prompted for input.
    /// </summary>
    protected void prePromptModifyContract()
    {
        LedgerTransStatementContract ledgerTransStatementContract = this.parmReportContract().parmRdpContract() as LedgerTransStatementContract;

        QueryBuildDataSource qbds;
        Query query = this.getQuery();

        if (this.parmArgs() 
            && this.parmArgs().dataSet() == tableNum(MainAccount))
        {
            MainAccount mainAccount = this.parmArgs().record() as mainAccount;

            if (mainAccount)
            {                
                // A Main Account was passed to the report.  Restrict the report results to the specified Main Account.                
                qbds = query.dataSourceTable(tableNum(MainAccount));
                QueryBuildRange qbr = SysQuery::findOrCreateRange(qbds, fieldNum(MainAccount, MainAccountId));
                qbr.value(SysQuery::value(mainAccount.MainAccountId));
                qbr.status(RangeStatus::Locked);
            }
        }
        else 
        {
            qbds = query.dataSourceTable(tableNum(MainAccount));
            qbds.clearRange(fieldNum(MainAccount, MainAccountId));
        }

        if (!ledgerTransStatementContract.parmFromDate())
        {
            // Set the default value for the From date to the first day of the current fiscal year.Apply the default value.
            ledgerTransStatementContract.parmFromDate(this.getPrePromptFromDateValue());
        }

        if (!ledgerTransStatementContract.parmToDate())
        {
            // Set the default value for the To date to the last day of the current fiscal year.Apply the default value.
            ledgerTransStatementContract.parmToDate(this.getPrePromptToDateValue());
        }

        if (!ledgerTransStatementContract.parmPostingLayers())
        {
            List postingLayerList = new List(Types::Enum);
            postingLayerList.addStart(CurrentOperationsTax::Current);
            ledgerTransStatementContract.parmPostingLayers(postingLayerList);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrePromptFromDateValue</Name>
				<Source><![CDATA[
    private TransDate getPrePromptFromDateValue()
    {
        // Get the default value for the From date to the first day of the current fiscal period.
        return FiscalCalendars::findFirstDayOfMonth(Ledger::fiscalCalendar(), userPreferredCurrentDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrePromptToDateValue</Name>
				<Source><![CDATA[
    private TransDate getPrePromptToDateValue()
    {
        TransDate toDate;

        // Get the default value for the To date to the last day of the current fiscal period.
        return FiscalCalendars::findLastDayOfMonth(Ledger::fiscalCalendar(), userPreferredCurrentDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Changes the report contract before the report is executed.
    /// </summary>
    protected void preRunModifyContract()
    {
        LedgerTransStatementContract contract;
        str reportTitle;

        contract = this.parmReportContract().parmRdpContract() as LedgerTransStatementContract;

        // <GEEPL>
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoPL]))
        {
            contract.parmShowFooter(true);
            contract.parmShowTotals(true);

            contract.parmSysVersion(SysReportRun::sysVersion());
            contract.parmUserName(SysReportRun::userName());
        }
        else
        {
            contract.parmShowFooter(false);
            contract.parmShowTotals(false);
        }
        // </GEEPL>

        // if we are printing the tax information and InterCompany is enabled, we need to display the Company for ledger accounts on the report
        contract.parmDisplayCompanyForLedgerAccount(contract.parmSalesTaxSpec() && isConfigurationkeyEnabled(configurationKeyNum(LedgerAdvIntercompanyAccounting)));

        // Prepare the title.
        reportTitle = strfmt("@SYS83214", enum2str(contract.parmTransaction()), DimensionHierarchy::findByTypeAndName(DimensionHierarchyType::Focus, contract.parmPrimaryDimensionFocus()).Description);

        this.parmReportContract().parmReportCaption(reportTitle);

        contract.parmReportTitle(reportTitle);
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunValidate</Name>
				<Source><![CDATA[
    protected container preRunValidate()
    {
        container validateResult = super();
        LedgerTransStatementContract contract = this.parmReportContract().parmRdpContract() as LedgerTransStatementContract;
        const boolean ShowErrors = true;
        boolean runUpdateBalancesInNewSession = true;

        if (Dynamics.AX.Application.FeatureStateProvider::IsFeatureEnabled(GeneralLedgerBalanceFeature::instance()))
        {
            GeneralLedgerBalanceProvider::getBalanceStatus(Ledger::current());
        }
        else
        {
            // Ensure that the balances used by this run are up to date by
            // running this prior to starting the transaction scope for the report.
            // This must be done outside of the report transaction scope 
            // so that the updates become available before the report transaction scope completes.
            DimensionFocusUpdateBalance::checkForRebuildBalancesInProgress(true);
            DimensionFocusUpdateBalance::updateBalance(
                DimensionHierarchy::findByTypeAndName(
                    DimensionHierarchyType::Focus,
                    contract.parmPrimaryDimensionFocus()),
                    ShowErrors,
                    runUpdateBalancesInNewSession,
                    Ledger::current());
        }

        return validateResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        LedgerTransStatementController controller = new LedgerTransStatementController();

        controller.parmReportName(ssrsReportStr(LedgerTransStatement, Report));
        controller.parmArgs(_args);
        controller.parmDialogCaption("@SYS83215");
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>