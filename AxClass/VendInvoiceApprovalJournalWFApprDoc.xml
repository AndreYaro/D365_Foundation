<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceApprovalJournalWFApprDoc</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>VendInvoiceApprovalJournalWFApprDoc</c> class is the workflow document class for
///    <c>VendInvoiceApprovalJournal</c> journals.
/// </summary>
/// <remarks>
///    This class is required in order to provide a unique workflow document for each journal.
/// </remarks>
public class VendInvoiceApprovalJournalWFApprDoc extends LedgerJournalWFApprovalDocument
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculateDocumentSpecificAmounts</Name>
				<Source><![CDATA[
    protected void calculateDocumentSpecificAmounts(CompanyId _companyId, RecId _recId)
    {
        LedgerJournalTable ledgerJournalTable;
        LedgerJournalTrans ledgerJournalTrans;

        // Set the overall process focus to the id of the passed in journal's company.
        changecompany(_companyId)
        {
            while select Voucher, TransDate, CurrencyCode, Triangulation, ExchRate,
                    ExchRateSecond, ReportingCurrencyExchRate, ReportingCurrencyExchRateSecondary, OffsetLedgerDimension, AmountCurCredit, AmountCurDebit,
                    Company, OffsetCompany
                from ledgerJournalTrans
                order by Voucher
                where ledgerJournalTrans.PurchLedgerPosting == ArrivalPostingType::None
                    || ledgerJournalTrans.PurchLedgerPosting == ArrivalPostingType::Vendor
                exists join ledgerJournalTable
                    where ledgerJournalTable.RecId == _recId
                        && ledgerJournalTable.JournalNum == ledgerJournalTrans.JournalNum
            {
                // Update the debit and credit voucher amounts cached for
                // max/min voucher and journal totals.
                this.updateDebitCreditForVoucher(ledgerJournalTrans);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAreAllRequestersTheSame</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the requester is the same for the purchase order that is associated with each
    /// ledger journal trans record for the corresponding ledger journal.
    /// </summary>
    /// <param name="_companyId">
    /// The company ID that corresponds to the workflow instance.
    /// </param>
    /// <param name="_tableId">
    /// The table ID that corresponds to the workflow instance.
    /// </param>
    /// <param name="_recId">
    /// The record ID that corresponds to the workflow instance.
    /// </param>
    /// <returns>
    /// The <c>Yes</c> enumeration value if the requester is the same for the purchase order that is
    /// associated with each ledger journal transaction; otherwise, the <c>No</c> enumeration value.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    /// The table ID must be of the <c>LedgerJournalTable</c> table.
    /// </exception>
    /// <exception cref="M:Exception::Error">
    /// The table ID must be of the <c>LedgerJournalTable</c> table. The record ID cannot be zero.
    /// </exception>
    public LedgerJournalAreAllRequestersSame parmAreAllRequestersTheSame(CompanyId _companyId, tableId _tableId, RecId _recId)
    {
        return this.areAllRequestersTheSame(_companyId, _tableId, _recId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmBudgetCheckResult</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>BudgetSourceCheckResult</c> value for all the vouchers that are associated with the
    /// <c>LedgerJournalTable</c> record.
    /// </summary>
    /// <param name="_companyId">
    /// The company ID that corresponds to the workflow instance.
    /// </param>
    /// <param name="_tableId">
    /// The table ID that corresponds to the workflow instance.
    /// </param>
    /// <param name="_recId">
    /// The record ID that corresponds to the workflow instance.
    /// </param>
    /// <returns>
    /// The <c>BudgetSourceCheckResult</c> value for all the vouchers that are associated with the
    /// <c>LedgerJournalTable</c> record.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    /// The table ID must be of the <c>LedgerJournalTable</c> table.
    /// </exception>
    public BudgetSourceCheckResult parmBudgetCheckResult(CompanyId _companyId, tableId _tableId, RecId _recId)
    {
        LedgerJournalTable ledgerJournalTable;
        BudgetSourceCheckResult budgetSourceCheckResult;

        if (_tableId != tablenum(LedgerJournalTable))
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }

        Debug::assert(_companyId != '');

        changecompany(_companyId)
        {
            ledgerJournalTable = LedgerJournalTable::findByRecId(_recId);

            if (ledgerJournalTable)
            {
                budgetSourceCheckResult = BudgetSourceLedgerJournalTable::findBudgetCheckResult(ledgerJournalTable.RecId);
            }
        }

        return budgetSourceCheckResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static VendInvoiceApprovalJournalWFApprDoc construct()
    {
        return new VendInvoiceApprovalJournalWFApprDoc();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>