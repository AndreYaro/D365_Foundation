<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RhrmEmployeeDismiss</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RhrmEmployeeDismiss extends RHRMEmploymentBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>cancel</Name>
				<Source><![CDATA[
    public void cancel(RPayHistory _rPayHistory)
    {
        emplTable.PayResignedDate = dateNull();

        if (emplTable.validateWrite())
        {
            emplTable.update();
        }

        this.updateTimesheetDismiss(_rPayHistory);
    }

]]></Source>
			</Method>
			<Method>
				<Name>formMenuItemName</Name>
				<Source><![CDATA[
    protected IdentifierName formMenuItemName()
    {
        return menuitemDisplayStr(RHRMVirtualNetworkDismiss);
    }

]]></Source>
			</Method>
			<Method>
				<Name>operationType</Name>
				<Source><![CDATA[
    public RHRMJournalType operationType()
    {
        return RHRMJournalType::Dismiss;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run(RPayHistory _rPayHistory)
    {
        emplTable.PayResignedDate = _rPayHistory.StartDate;

        if (emplTable.validateWrite())
        {
            emplTable.update();
        }

        this.updateTimesheetDismiss(_rPayHistory);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTimesheetDismiss</Name>
				<Source><![CDATA[
    private void updateTimesheetDismiss(RPayHistory _rPayHistory)
    {
        ReportPeriod_RU lastTimesheetDate;

        if (updateTbl)
        {
            lastTimesheetDate = RPayTblTrans::lastTimesheetPeriod(_rPayHistory.emplId(), _rPayHistory.StartDate);
            this.updateTimesheet(_rPayHistory.StartDate + 1,
                                 max(dateEndMth(_rPayHistory.StartDate), lastTimesheetDate));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(RPayHistory _rPayHistory)
    {
        if (! emplTable.PayEmploymentDate ||
            emplTable.PayEmploymentDate && emplTable.PayResignedDate)
            return checkFailed(strFmt("@RUP1655", emplTable.EmployeeId, emplTable.name()));

        return RHRMEmploymentBase::checkDismissOperation(_rPayHistory);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        FormDataSource      formDataSource;
        RHRMEmployeeDismiss rhrmEmployeeDismiss;

        if (! _args)
            throw error("@RUP1336");

        if (! _args.record())
            throw error("@RUP1336");

        formDataSource       = FormDataUtil::getFormDataSource(_args.record());

        rhrmEmployeeDismiss  = new RHRMEmployeeDismiss(_args.record());

        if (rhrmEmployeeDismiss.prompt())
        {
            if (formDataSource)
            {
                formDataSource.reread();
                formDataSource.refresh();
                formDataSource.active();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>