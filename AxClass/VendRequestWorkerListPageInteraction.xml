<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendRequestWorkerListPageInteraction</Name>
	<SourceCode>
		<Declaration><![CDATA[
class VendRequestWorkerListPageInteraction extends ListPageInteraction
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializeQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets filters on a query for a list page.
    /// </summary>
    /// <param name="_query">
    /// The query on which to set filters.
    /// </param>
    public void initializeQuery(Query _query)
    {
        QueryBuildRange                 range;
        VendRequestCompany              vendRequestCompany;
        VendRequestStatusChange         vendRequestStatusChange;
        VendRequestCategoryExtension    vendRequestCategoryExtension;
        str                             newVendorCompanyRequestIds = '',newTableIds='';
        VendRequestChild                vendRequestChild;
        QueryBuildDataSource            qbds;

        delete_from vendRequestChild;
        // Use id list because it is not possible to add a derived datasource range with a modal driven list page

        // Company - Not possible to add an range on a derived datasource so, add the recId range to the parent datasource
        insert_recordset vendRequestChild(RefRecId)
            select RecId from vendRequestCompany
                where vendRequestCompany.VendRequestCompanyType == VendRequestCompanyType::NewVendorCompanyRequest;

        // Category Extension - Not possible to add an range on a derived datasource so, add the recId VendRequestCategoryExtensino range to the parent datasource
        insert_recordset vendRequestChild(RefRecId)
            select RecId from vendRequestCategoryExtension
                where vendRequestCategoryExtension.OriginatedByType == VendRequestOriginatedByType::Procurement;

        // Status Change - Not possible to add an range on a derived datasource so, add the recId from VendRequstStatusChange range to the parent datasource
        insert_recordset vendRequestChild(RefRecId)
            select RecId from vendRequestStatusChange;

        qbds = _query.dataSourceTable(tableNum(VendRequest)).addDataSource(tableNum(VendRequestChild));
        qbds.joinMode(joinMode::ExistsJoin);
        qbds.addLink(fieldNum(VendRequest, RecId), fieldNum(VendRequestChild, RefRecId));

        range = _query.dataSourceTable(tableNum(VendRequest)).addRange(fieldNum(VendRequest, InstanceRelationType));
        range.status(RangeStatus::Hidden);
        newTableIds = queryValue(DictTable::getRelationTypeFromTableName(tableStr(VendRequestCompany)));
        newTableIds = queryRangeConcat(newTableIds,DictTable::getRelationTypeFromTableName(tableStr(VendRequestCategoryExtension)));
        newTableIds = queryRangeConcat(newTableIds,DictTable::getRelationTypeFromTableName(tableStr(VendRequestStatusChange)));
        range.value(newTableIds);

        range = _query.dataSourceTable(tableNum(VendRequest)).addRange(fieldNum(VendRequest, DataAreaId));
        range.status(RangeStatus::Hidden);
        range.value(strFmt('((%1 == "%2") || (%3 == "%4"))',
                    fieldStr(VendRequest,CreatedBy),
                    curUserId(),
                    fieldStr(VendRequest, SponsoredBy),
                    DirPersonUserEx::currentWorkerPersonnelNumber()));

        _query.dataSourceTable(tableNum(VendRequest)).addOrderByField(fieldNum(VendRequest, VendRequestNumber),SortOrder::Descending);

        super(_query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables the delete button based on the selected vendor request.
    /// </summary>
    /// <remarks>
    /// This method calls the <c>validateDelete</c> method to check if the record can be deleted.
    /// </remarks>
    public void selectionChanged()
    {
        VendRequest vendRequest;

        super();

        vendRequest = this.listPage().activeRecord(tableStr(VendRequest));

        this.listPage().actionPaneControlEnabled(
            formControlStr(VendRequestWorkerRequestListPage, VendRequestDelete),
            vendRequest.validateDelete());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>