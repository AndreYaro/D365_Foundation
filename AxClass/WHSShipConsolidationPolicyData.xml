<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSShipConsolidationPolicyData</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSShipConsolidationPolicyData</c> class stores a Shipment consolidation policy data for the release to warehouse procedure.
/// </summary>
public class WHSShipConsolidationPolicyData
{
    private WHSShipConsolidationPolicy  shipConsolidationPolicy;
    private List                        shipConsolidationPolicyFields;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of <c>WHSShipConsolidationPolicyData</c>.
    /// </summary>
    /// <param name="_shipConsolidationPolicy">A shipment consolidation policy.</param>
    /// <returns>
    /// An instance of <c>WHSShipConsolidationPolicyData</c>.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public static WHSShipConsolidationPolicyData construct(WHSShipConsolidationPolicy _shipConsolidationPolicy)
    {
        WHSShipConsolidationPolicyData    shipConsolidationPolicyData = new WHSShipConsolidationPolicyData();

        shipConsolidationPolicyData.setShipConsolidationPolicy(_shipConsolidationPolicy);

        return shipConsolidationPolicyData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructDefaultToProcess</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new default instance of <c>WHSShipConsolidationPolicyData</c>.
    /// </summary>
    /// <param name="_policyType">A shipment consolidation policy type.</param>
    /// <returns>
    /// A default instance of <c>WHSShipConsolidationPolicyData</c>.
    /// </returns>
    internal static WHSShipConsolidationPolicyData constructDefaultToProcess(WHSShipConsolidationPolicyType _policyType)
    {
        WHSShipConsolidationPolicyData      shipConsolidationPolicyData = new WHSShipConsolidationPolicyData();
        WHSShipConsolidationPolicy          shipConsolidationPolicy;

        shipConsolidationPolicy.initValue();
        shipConsolidationPolicy.ShipConsolidationPolicyType = _policyType;
        shipConsolidationPolicy.Valid = WHSShipConsolidationPolicyValidationStatus::Valid;
        shipConsolidationPolicy.ConsolidateWithExistingShipments = NoYes::No;
        shipConsolidationPolicy.ShipConsolidationPolicyQuery = new QueryRun(WHSShipConsolidationPolicyProvider::buildCriteriaQueryFromPolicyType(_policyType)).pack();
        shipConsolidationPolicy.ShipConsolidationPolicyName = '';

        shipConsolidationPolicyData.setShipConsolidationPolicy(shipConsolidationPolicy);
        shipConsolidationPolicyData.setShipConsolidationPolicyFields(new List(Types::Class));

        return shipConsolidationPolicyData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmShipConsolidationPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns an assigned shipment consolidation policy.
    /// </summary>
    /// <returns>
    /// An assigned shipment consolidation policy.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public WHSShipConsolidationPolicy parmShipConsolidationPolicy()
    {
        return shipConsolidationPolicy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setShipConsolidationPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets shipment consolidation policy.
    /// </summary>
    /// <param name="_shipConsolidationPolicy">A shipment consolidation policy.</param>
    private void setShipConsolidationPolicy(WHSShipConsolidationPolicy _shipConsolidationPolicy)
    {
        shipConsolidationPolicy = _shipConsolidationPolicy.data();
    }

]]></Source>
			</Method>
			<Method>
				<Name>shipConsolidationPolicyFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns policy fields of assigned shipment consolidation policy.
    /// </summary>
    /// <returns>
    /// Policy fields of assigned shipment consolidation policy.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public List shipConsolidationPolicyFields()
    {
        if (!shipConsolidationPolicyFields)
        {
            shipConsolidationPolicyFields = new List(Types::Class);

            WHSShipConsolidationPolicyField     shipConsolidationPolicyField;

            while select shipConsolidationPolicyField
                order by SourceTableName asc, SourceFieldName asc
                where shipConsolidationPolicyField.ShipConsolidationPolicyName == shipConsolidationPolicy.ShipConsolidationPolicyName
                &&    shipConsolidationPolicyField.ShipConsolidationPolicyType == shipConsolidationPolicy.ShipConsolidationPolicyType
            {
                WHSShipConsolidationPolicyFieldData shipConsolidationPolicyFieldData = WHSShipConsolidationPolicyFieldData::construct(shipConsolidationPolicyField);

                shipConsolidationPolicyFields.addEnd(shipConsolidationPolicyFieldData);
            }
        }

        return shipConsolidationPolicyFields;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setShipConsolidationPolicyFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets policy fields of assigned shipment consolidation policy.
    /// </summary>
    /// <param name="_shipConsolidationPolicyFields">Policy fields of assigned shipment consolidation policy.</param>
    [Hookable(false)]
    internal void setShipConsolidationPolicyFields(List _shipConsolidationPolicyFields)
    {
        shipConsolidationPolicyFields = _shipConsolidationPolicyFields;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcShipConsolidationFieldValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates shipment field values based on the assigned consolidation policy.
    /// </summary>
    /// <param name="_shipmentTable">The shipment record.</param>
    /// <param name="_queryRunReleaseLine">The QueryRun object with other table records.</param>
    [Hookable(false), Wrappable(false)]
    public void calcShipConsolidationFieldValues(WHSShipmentTable _shipmentTable, QueryRun _queryRunReleaseLine)
    {
        if (!shipConsolidationPolicyFields)
        {
            this.shipConsolidationPolicyFields(); // creates field list
        }

        if (!shipConsolidationPolicyFields.empty())
        {
            ListEnumerator      policyFieldEnumerator = shipConsolidationPolicyFields.getEnumerator();

            while (policyFieldEnumerator.moveNext())
            {
                WHSShipConsolidationPolicyFieldData shipConsolidationPolicyFieldData = policyFieldEnumerator.current();

                shipConsolidationPolicyFieldData.calcFieldValue(_shipmentTable, _queryRunReleaseLine);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>