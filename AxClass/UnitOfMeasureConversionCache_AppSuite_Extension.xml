<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>UnitOfMeasureConversionCache_AppSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(TableStr(UnitOfMeasureConversionCache))]
public final static class UnitOfMeasureConversionCache_AppSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>flushOnConversionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Flushes the unit of measure conversion cache when a conversion is inserted, updated or deleted.
    /// </summary>
    /// <param name="_fromUnitOfMeasure">
    ///    A from unit of measure conversion.
    /// </param>
    /// <param name="_toUnitOfMeasure">
    ///    A to unit of measure conversion.
    /// </param>
    /// <param name="_product">
    ///    A product conversion.
    /// </param>
    /// <remarks>
    ///    Deletes all cached conversions that have the <paramref name="_fromUnitOfMeasure" /> or <paramref
    ///    name="_toUnitOfMeasure" /> units of measure as from or to unit of measure conversion parameter.
    /// </remarks>
    public static void flushOnConversionChanged(
        UnitOfMeasureFromRecId  _fromUnitOfMeasure,
        UnitOfMeasureToRecId    _toUnitOfMeasure,
        RefRecId                _product)
    {
        next flushOnConversionChanged(_fromUnitOfMeasure, _toUnitOfMeasure, _product);
        
        EcoResProductMaster productMaster = EcoResProductMaster::find(_product);

        if (productMaster.RecId != 0 && productMaster.IsProductVariantUnitConversionEnabled)
        {
            ttsbegin;
            
            EcoResDistinctProductVariant distinctProductVariant;

            while select RecId from distinctProductVariant
                where distinctProductVariant.ProductMaster == productMaster.RecId
            {
                UnitOfMeasureConversionCache::flushOnConversionChanged(_fromUnitOfMeasure, _toUnitOfMeasure, distinctProductVariant.RecId);
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>