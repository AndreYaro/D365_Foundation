<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableForm_INTax_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchTableForm_INTax_Extension</c> class is an extension class which extends the <c>PurchTable</c> form functionality.
/// </summary>
[ExtensionOf(formStr(PurchTable))]
internal final class PurchTableForm_INTax_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>setTaxWithholdEnabled_IN</Name>
				<Source><![CDATA[
    public void setTaxWithholdEnabled_IN()
    {
        next setTaxWithholdEnabled_IN();

        purchTable_W_ds.object(fieldNum(PurchTable_W, TCSGroup_IN)).enabled(!purchTable_W.TDSGroup_IN);
        purchTable_W_ds.object(fieldNum(PurchTable_W, TDSGroup_IN)).enabled(!purchTable_W.TCSGroup_IN);
        purchLine_IN_ds.object(fieldNum(PurchLine_IN, TCSGroup)).enabled(!purchLine_IN.TDSGroup);
        purchLine_IN_ds.object(fieldNum(PurchLine_IN, TDSGroup)).enabled(!purchLine_IN.TCSGroup);
        buttonWithholdTax_IN.enabled(purchLine_IN.TDSGroup || purchLine_IN.TCSGroup);

        if (TaxWithholdParameters_IN::isTaxWithholdingCreditNoteTaxGroupDefaultBlank())
        {
            if (PurchTable.containsCreditNoteLine())
            {
                // As long as the SO contains credit note line, the TCS / TDS group in header should be disabled.
                purchTable_W_ds.object(fieldNum(PurchTable_W, TCSGroup_IN)).enabled(false);
                purchTable_W_ds.object(fieldNum(PurchTable_W, TDSGroup_IN)).enabled(false);
            }

            if (purchLine.isCreditNoteLineWithRefId() && !purchTable.isInterCompanyOrder())
            {
                // If TDS/TCS group is set in original transaction, the TDS/TCS group in the credit note line will be editable; otherwise 
                // TDS/TCS group will be uneditable.
                PurchLine_IN originPurchLine_IN = PurchLine::findInventTransId(purchLine.InventRefTransId).purchLine_IN();
                purchLine_IN_ds.object(fieldNum(PurchLine_IN, TCSGroup)).enabled(originPurchLine_IN.TCSGroup != '');
                purchLine_IN_ds.object(fieldNum(PurchLine_IN, TDSGroup)).enabled(originPurchLine_IN.TDSGroup != '');
            }
        }
        else
        {
            if (purchLine.InventRefId
                && purchLine.InventRefTransId
                && !purchTable.isInterCompanyOrder()
                && purchLine.PurchQty < 0)
            {
                purchTable_W_ds.object(fieldNum(PurchTable_W, TCSGroup_IN)).enabled(false);
                purchTable_W_ds.object(fieldNum(PurchTable_W, TDSGroup_IN)).enabled(false);
                purchLine_IN_ds.object(fieldNum(PurchLine_IN, TCSGroup)).enabled(false);
                purchLine_IN_ds.object(fieldNum(PurchLine_IN, TDSGroup)).enabled(false);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTaxWithholdVisible_IN</Name>
				<Source><![CDATA[
    public void setTaxWithholdVisible_IN()
    {
        next setTaxWithholdVisible_IN();

        purchTable_W_ds.object(fieldNum(PurchTable_W, TDSGroup_IN)).visible(taxWithholdParameters.TDSActivate);
        purchLine_IN_ds.object(fieldNum(PurchLine_IN, TDSGroup)).visible(taxWithholdParameters.TDSActivate);
        purchTable_W_ds.object(fieldNum(PurchTable_W, TCSGroup_IN)).visible(taxWithholdParameters.TCSActivate);
        purchLine_IN_ds.object(fieldNum(PurchLine_IN, TCSGroup)).visible(taxWithholdParameters.TCSActivate);
        withholdingTax_IN.visible(taxWithholdParameters.TDSActivate || taxWithholdParameters.TCSActivate);
        buttonWithholdTax_IN.visible(taxWithholdParameters.TDSActivate || taxWithholdParameters.TCSActivate);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>