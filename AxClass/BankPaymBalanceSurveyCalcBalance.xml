<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymBalanceSurveyCalcBalance</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Calculates the survey amount from open balance.
/// </summary>
[BankPaymBalanceSurveyCalcAttribute(BankPaymBalanceSurveyCalcType::Balance)]
public class BankPaymBalanceSurveyCalcBalance extends BankPaymBalanceSurveyCalc
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcLineAmount</Name>
				<Source><![CDATA[
    protected boolean calcLineAmount(CustVendTrans _custVendTrans, TradeBLWILines _tradeLine)
    {
        boolean ret = super(_custVendTrans, _tradeLine);

        if (ret)
        {
            _custVendTrans.transactionPerDate(toDate);

            _tradeLine.AmountCur = _custVendTrans.remainAmountCur();
            _tradeLine.AmountMst = _custVendTrans.remainAmountMst();
            _tradeLine.AmountMSTOrig = _tradeLine.AmountMst;

            if (!_tradeLine.AmountCur && !_tradeLine.AmountMst)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createQuery</Name>
				<Source><![CDATA[
    protected Query createQuery(TradeBLWICustVend _custVend)
    {
        Query query = super(_custVend);
        QueryBuildDataSource qbds;

        switch (_custVend)
        {
            case TradeBLWICustVend::Customer:
                qbds = query.dataSourceTable(tableNum(CustTrans));
                qbds.findRange(fieldNum(CustTrans, TransDate)).value(queryRange(dateNull(), toDate));
                qbds.addRange(fieldNum(CustTrans, Closed)).value(queryValue(dateNull()) + ',' + queryRange(toDate + 1, maxDate()));
                break;

            case TradeBLWICustVend::Vendor:
                qbds = query.dataSourceTable(tableNum(VendTrans));
                qbds.findRange(fieldNum(VendTrans, TransDate)).value(queryRange(dateNull(), toDate));
                qbds.addRange(fieldNum(VendTrans, Closed)).value(queryValue(dateNull()) + ',' + queryRange(toDate + 1, maxDate()));
                break;
        }

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>