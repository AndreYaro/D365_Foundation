<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMPromoMgmtActionManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMPromoMgmtActionManager</c> class handles updating the
/// TAMTradePromotion table when the workflow status changes.
/// </summary>
class TAMPromoMgmtActionManager  extends TAMWorkflowActionManager
{
    TAMTradePromotion               tamTradePromotion;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>process</Name>
				<Source><![CDATA[
    protected void process(Args _args)
    {
        object                      tamTradePromotion_ds;

        workflowWorkItemTable = _args.caller().getActiveWorkflowWorkItem();
        menuitemActionName = _args.menuItemName();
        workflowWorkItemActionType = WorkflowWorkItemActionManager::findActionTypeForMenuItem(workflowWorkItemTable, menuitemActionName);

        this.showDialog();

        if (workflowWorkItemActionDialog.parmIsClosedOK())
        {
            tamTradePromotion = _args.record();

            switch (workflowWorkItemActionType)
            {
                case WorkflowWorkItemActionType::RecallLineItem:
                case WorkflowWorkItemActionType::CancelLineItem:
                    tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::NotSubmitted;
                    break;

                case WorkflowWorkItemActionType::Complete:
                    tamTradePromotion.WFApprovalStatus  = TAMPromoMgmtApprovalStatus::Approved;
                    tamTradePromotion.PromotionStatus   = TAMStatus::ManagementApproved;
                    break;

                case WorkflowWorkItemActionType::Reassign:
                case WorkflowWorkItemActionType::Delegate:
                    tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::Submitted;
                    break;

                case WorkflowWorkItemActionType::Return:
                case WorkflowWorkItemActionType::Deny:
                case WorkflowWorkItemActionType::RequestChange:
                    tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::Rejected;
                    break;

                case WorkflowWorkItemActionType::Resubmit:
                    tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::Submitted;
                    break;

                default:
                    tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::NotSubmitted;
                    break;
            }

            this.updateWorkflowWorkItem();

            tamTradePromotion_ds = FormDataUtil::getFormDataSource(tamTradePromotion);

            if (tamTradePromotion_ds)
            {
                tamTradePromotion_ds.write();
                tamTradePromotion_ds.refresh();
            }

            _args.caller().updateWorkFlowControls();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TAMPromoMgmtActionManager construct()
    {
        return new TAMPromoMgmtActionManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        TAMPromoMgmtActionManager  tamPromoMgmtActionManager;

        if (_args.record())
        {
            tamPromoMgmtActionManager = TAMPromoMgmtActionManager::construct();
            tamPromoMgmtActionManager.process(_args);
        }
        else
        {
            throw error("@MCR27548");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>