<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSWorkCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSWorkCancel</c> class handles work cancelation action.
/// </summary>
public class WHSWorkCancel
{
    private const int NumOfWorkHeadersToCancelThatRequireConfirmation = 5;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WHSWorkTable workArgs = _args.record() as WHSWorkTable;
        if (workArgs && FormDataUtil::isFormDataSource(workArgs))
        {
            FormDataSource dataSource = FormDataUtil::getFormDataSource(workArgs) as FormDataSource;

            if (WHSWorkCancel::ensureShouldCancelWork(dataSource))
            {
                using (SysErrorMessageHelpScope scope = SysErrorMessageHelpScope::newErrorContext(_args))
                {
                    if (WHSWorkCancelServiceFlight::instance().isEnabled())
                    {
                        MenuFunction menuFunction = new MenuFunction(menuItemActionStr(WHSWorkCancelNoBatch), MenuItemType::Action);

                        menuFunction.run(_args);
                    }
                    else
                    {
                        for (WHSWorkTable work = dataSource.getFirst(true) ? dataSource.getFirst(true) : dataSource.cursor();
                        work;
                        work = dataSource.getNext())
                        {
                            WHSWorkTable::cancelWorkValidateWorkNotAssignedToActiveCluster(work.WorkId);
                        }
                    }

                    dataSource.research();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ensureShouldCancelWork</Name>
				<Source><![CDATA[
    private static boolean ensureShouldCancelWork(FormDataSource _workTableFormDataSource)
    {
        boolean doCancel = true;

        int numOfMarkedWorkHeaders = _workTableFormDataSource.recordsMarked().lastIndex();
        if (numOfMarkedWorkHeaders >= NumOfWorkHeadersToCancelThatRequireConfirmation)
        {
            doCancel = Box::confirm(strFmt("@WAX:ConfirmCancelManyWorkHeaders", numOfMarkedWorkHeaders));
        }

        return doCancel;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>