<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntentLetterOpenController_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>IntentLetterOpenController_IT</c> controller for start intent letter opening.
/// </summary>
public class IntentLetterOpenController_IT extends SysOperationServiceController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs opening operations.
    /// </summary>
    /// <param name = "_args">The <c>IntentLetter_IT</c> record.</param>
    public static void main(Args _args)
    {
        if (_args && _args.caller() is FormRun)
        {
            Common record = _args.record();

            if (record.tableId == tablenum(IntentLetter_IT))
            {
                IntentLetter_IT intentLetter = record;

                IntentLetterOpenController_IT controller = IntentLetterOpenController_IT::construct();
                controller.parmDialogCaption("@ExtendedItallianLocalization:SetStatusOpened");

                IntentLetterOpenDataContract_IT contract = controller.getDataContractObject();

                List intentLetterList = new List(Types::Record);
                MultiSelectionHelper multiselectionHelper = MultiSelectionHelper::createFromCaller(_args.caller());

                intentLetter = multiselectionHelper.getFirst();

                while (intentLetter)
                {
                    intentLetterList.addEnd(intentLetter);
                    intentLetter = multiselectionHelper.getNext();
                }

                contract.parmIntentLetterList(intentLetterList);
                controller.startOperation();

                _args.record().dataSource().reSearch(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static IntentLetterOpenController_IT construct()
    {
        IdentifierName className = classStr(IntentLetterOpenService_IT);
        IdentifierName methodName = methodStr(IntentLetterOpenService_IT, runProcessing);
        SysOperationExecutionMode mode = SysOperationExecutionMode::Synchronous;

        IntentLetterOpenController_IT controller = new IntentLetterOpenController_IT(className, methodName, mode);
        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>