<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendInvoiceTransApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(VendInvoiceTrans))]
internal final class VendInvoiceTransApplicationSuite_Extension
{
}

]]></Declaration>
		<Methods>
			<Method>
				<Name>updateFromLedgerJournalTrans_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates tax-fields of <c>VendInvoiceTrans</c> from the <c>LedgerJournalTrans</c> record.
    /// </summary>
    /// <param name = "_ledgerJournalTrans">
    /// The <c>LedgerJournalTrans</c> record.
    /// </param>
    /// <param name = "_amountInclTax">
    /// Shows if tax is included or not.
    /// </param>
    /// <param name = "_debCred">
    /// Type of amount. Positive for debet and negative for credit.
    /// </param>
    internal void updateFromLedgerJournalTrans_RU(LedgerJournalTrans _ledgerJournalTrans, UnknownNoYes _amountInclTax, DebitCredit _debCred)
    {
        Sign sign = _debCred == DebitCredit::Credit ? -1 : 1;
        TaxAmount lineAmountTax;
        TaxAmount taxAmountAdd;
        TaxTrans taxTrans;

        this.TaxGroup = _ledgerJournalTrans.TaxGroup;
        this.TaxItemGroup = _ledgerJournalTrans.TaxItemGroup;

        if (this.TaxGroup && this.TaxItemGroup)
        {
            taxTrans = TaxTrans::findByLedgerJournalTrans(_ledgerJournalTrans);

            if (taxTrans.RecId)
            {
                lineAmountTax = Tax::taxTotalVoucherDateRecId_RU(_ledgerJournalTrans.Voucher, _ledgerJournalTrans.TransDate, _ledgerJournalTrans.RecId);

                this.initFromTaxTrans_RU(taxTrans);

                if (_amountInclTax == UnknownNoYes::No)
                {
                    taxAmountAdd = lineAmountTax;
                    lineAmountTax = 0;
                }

                if (sign(lineAmountTax) != sign(this.vatAmount_RU))
                {
                    this.vatAmount_RU = -this.vatAmount_RU;
                    this.vatAmountMST_RU = -this.vatAmountMST_RU;
                    this.ExciseAmount_RU = -this.ExciseAmount_RU;
                    this.ExciseAmountMST_RU = -this.ExciseAmountMST_RU;
                }

                this.TaxAmount = lineAmountTax + taxAmountAdd;
                this.LineAmountTax = lineAmountTax;

                this.LineAmount = this.LineAmount - lineAmountTax;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>