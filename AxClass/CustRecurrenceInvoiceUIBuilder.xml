<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustRecurrenceInvoiceUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
class CustRecurrenceInvoiceUIBuilder extends SysOperationAutomaticUIBuilder
{
    SysLookupMultiSelectCtrl templateMultiSelectControl;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the dialog fields after the fields are built.
    /// </summary>
    public void postBuild()
    {
        CustRecurrenceInvoiceDataContract custRecurrenceInvoiceDataContract = this.dataContractObject();
        DialogField dialogRecurrenceInvoiceGenerationDate;
        DialogField dialogRecurrenceInvoiceDate;

        super();

        // From binding info, get the dialog field for recurrence invoice generation date attribute and mark it as a mandatory field.
        dialogRecurrenceInvoiceGenerationDate = this.bindInfo().getDialogField(custRecurrenceInvoiceDataContract, methodStr(CustRecurrenceInvoiceDataContract, parmRecurrenceGenerationDate));
        dialogRecurrenceInvoiceGenerationDate.fieldControl().mandatory(true);

        // From binding info, get the dialog field for recurrence invoice date attribute and mark it as a mandatory field.
        dialogRecurrenceInvoiceDate = this.bindInfo().getDialogField(custRecurrenceInvoiceDataContract, methodStr(CustRecurrenceInvoiceDataContract, parmRecurrenceInvoiceDate));
        dialogRecurrenceInvoiceDate.fieldControl().mandatory(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers the dialog field methods to capture events,
    /// and create multi-select lookup control for Template Name dialog field.
    /// </summary>
    public void postRun()
    {
        Dialog dialogLocal = this.dialog();

        super();

         // This method should be called in order to handle events on dialogs.
        dialogLocal.dialogForm().formRun().controlMethodOverload(false);

        DialogField dialogFieldTemplateName = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(CustRecurrenceInvoiceDataContract, parmCustInvoiceTemplateName));
        dialogFieldTemplateName.value(''); // clear last saved value

        // create multi-select lookup control for Template dialog field
        FormRun dialogFormRun = dialogLocal.dialogForm().formRun();

        Query templateQuery = new Query();
        QueryBuildDataSource templateDS = templateQuery.addDataSource(tableNum(CustInvoiceTemplate));
        templateDS.addSelectionField(fieldNum(CustInvoiceTemplate, TemplateName));
        templateDS.addSelectionField(fieldNum(CustInvoiceTemplate, TemplateDescription));

        if (CustParameters::isBillingClassificationEnabled())
        {
            templateDS.addSelectionField(fieldNum(CustInvoiceTemplate, BillingClassification));
        }

        container selectedFields = [tableNum(CustInvoiceTemplate), fieldNum(CustInvoiceTemplate, TemplateName)];

        templateMultiSelectControl = SysLookupMultiSelectCtrl::constructWithQuery(
            dialogFormRun,
            dialogFieldTemplateName.control(),
            templateQuery,
            false,
            selectedFields);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>