<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetImpairmentAccountingRptUIBuilder_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The UIBuilder for the <c>AssetImpairmentAccountingReport</c> SSRS report.
/// </summary>
public class AssetImpairmentAccountingRptUIBuilder_JP extends SrsReportFileFormatDataContractUIBuilder
{
    DialogField     dialogFieldTestResultID;
    DialogField     dialogFieldCurrencyUnit;
    AssetImpairmentAccountingRptContract_JP contract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initDialogField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the dialog fields from binding information.
    /// </summary>
    private void initDialogField()
    {
        contract = this.dataContractObject();
        dialogFieldTestResultID = this.bindInfo().getDialogField(contract, methodStr(AssetImpairmentAccountingRptContract_JP, parmTestResultNumber));
        dialogFieldCurrencyUnit = this.bindInfo().getDialogField(contract, methodStr(AssetImpairmentAccountingRptContract_JP, parmCurrencyUnit));
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyOverrideMethod</Name>
				<Source><![CDATA[
    /// <summary>
    /// Register the overrided lookup method
    /// </summary>
    protected void modifyOverrideMethod()
    {
        dialogFieldTestResultID.registerOverrideMethod(
            methodStr(FormStringControl, lookup),
            methodStr(AssetImpairmentAccountingRptUIBuilder_JP, testResultIdLookup),
            this);

        dialogFieldTestResultID.registerOverrideMethod(
            methodStr(FormStringControl, validate),
            methodStr(AssetImpairmentAccountingRptUIBuilder_JP, testResultIdValidate),
            this);

        dialogFieldCurrencyUnit.registerOverrideMethod(
            methodStr(FormStringControl, modified),
            methodStr(AssetImpairmentAccountingRptUIBuilder_JP, getCurrencyUnitLabel),
            this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post build method
    /// </summary>
    public void postBuild()
    {
        this.initDialogField();

        super();

        this.modifyOverrideMethod();
    }

]]></Source>
			</Method>
			<Method>
				<Name>testResultIdLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides the lookup method
    /// </summary>
    /// <param name="_testResultIdDialogControl">
    /// The form string control
    /// </param>
    public void testResultIdLookup(FormStringControl _testResultIdDialogControl)
    {
        QueryBuildDataSource            qbdsTestResult;
        Query                           query = new Query();
        SysTableLookup                  lookup;
        Args                            args = controller.parmArgs();

        qbdsTestResult  = query.addDataSource(tableNum(AssetImpairmentTestResult_JP));
        qbdsTestResult.addRange(fieldNum(AssetImpairmentTestResult_JP, SharedAssetImpairMethod)).value(queryValue(args.parmEnum()));

        lookup = SysTableLookup::newParameters(tableNum(AssetImpairmentTestResult_JP), _testResultIdDialogControl);
        lookup.parmQuery(query);

        // Add the fields to be shown in the lookup form.
        lookup.addLookupfield(fieldNum(AssetImpairmentTestResult_JP, TestNum), true);
        lookup.addLookupfield(fieldNum(AssetImpairmentTestResult_JP, Description));
        lookup.addLookupfield(fieldNum(AssetImpairmentTestResult_JP, TestDate));

        // Perform the lookup.
        lookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>testResultIdValidate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides the validate method
    /// </summary>
    /// <param name="_testResultIdDialogControl">
    /// The form string control
    /// </param>
    /// <returns>
    /// The result of validation.
    /// </returns>
    public boolean testResultIdValidate(FormStringControl _testResultIdDialogControl)
    {
        AssetImpairmentTestNum_JP       testNum;
        AssetImpairmentTestResult_JP    result;

        boolean     ret = true;

        if (dialogFieldTestResultID.value())
        {
            testNum = _testResultIdDialogControl.valueStr();

            if (testNum)
            {
                result  = AssetImpairmentTestResult_JP::findByTestNum(testNum);

                if (result.SharedAssetImpairMethod != controller.parmArgs().parmEnum())
                {
                    // @SYP4860084: The impairment test ID %1 is not valid.
                    ret = checkFailed(strFmt("@SYP4860084", testNum));
                }
            }
        }
        else
        {
            // @SYP4860085: Select an impairment test ID before generating the report.
            ret = checkFailed("@SYP4860085");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrencyUnitLabel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the currency unit label
    /// </summary>
    public boolean getCurrencyUnitLabel(FormComboBoxControl _getCurrencyUnitLabel)
    {
        boolean ret = true;

        contract.parmCurrencyUnitLabel(_getCurrencyUnitLabel.valueStr());

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>