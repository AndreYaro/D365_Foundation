<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDim_Retail_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventDim_Retail_Extension</c> class that handles <c>InventDim</c> customizations for retail specific logics.
/// </summary>
[ExtensionOf(tableStr(InventDim))]
final internal class InventDim_Retail_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>InventDim</c> record that has the same values as specified in the <paramref
    ///    name="_inventDim" /> buffer and populates the legacy SHA1HASH value that is used by AX2012.
    /// </summary>
    /// <param name="_inventDim">
    ///    The <paramref name="_inventDim" /> buffer contains all the inventory dimensions for the new record.
    /// </param>
    /// <returns>
    ///    A buffer from the <c>InventDim</c> table.
    /// </returns>
    /// <remarks>
    ///    This method will fail if a record with the same dimensions already exists.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    ///    Number sequence for <c>InventDimId</c> was incorrectly configured.
    /// </exception>
    static public InventDim create(InventDim  _inventDim)
    {
        InventDim inventDim = next create(_inventDim);

        if (RetailUtilities::isRetailCDXBackwardCompatibilityEnabled() 
            && RetailCDXFeatureControl::retailInventDimSHA1HashTableLegacyEnabled())
        {
            RetailInventDimSHA1HashTableLegacy::findOrCreateFromInventDim(inventDim);
        }

        return inventDim;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findOrCreate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds or creates an inventory dimension record and populates the legacy SHA1HASH value that is used by Ax2012.
    /// </summary>
    /// <param name = "_inventDim">A record with the dimension fields set.</param>
    /// <returns>The inventory dimension.</returns>
    static public InventDim findOrCreate(InventDim _inventDim)
    {
        InventDim inventDim = next findOrCreate(_inventDim);

        if (RetailUtilities::isRetailCDXBackwardCompatibilityEnabled() 
            && RetailCDXFeatureControl::retailInventDimSHA1HashTableLegacyEnabled())
        {
            RetailInventDimSHA1HashTableLegacy::findOrCreateFromInventDim(inventDim);
        }

        return inventDim;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>