<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendVendorContactPersonHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendVendorContactPersonHelper</c> class is the helper class for creating contact persons for selected vendors
/// </summary>
public static class VendVendorContactPersonHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addContactForSelectedVendors</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create contact persons for the selected vendors in the datasource
    /// </summary>
    /// <param name="_vendors">
    /// Vendors datasource
    /// </param>
    /// <param name="_dirPersonRecId">
    /// RecId of the person which the created contact persons will be attached to
    /// </param>
    public static void addContactForSelectedVendors(FormDataSource _vendors, DirPersonRecId _dirPersonRecId)
    {
        MultiSelectionHelper selectionHelper = MultiSelectionHelper::construct();

        selectionHelper.parmDataSource(_vendors);

        Vendtable selectedVendor = selectionHelper.getFirst();
        while (selectedVendor)
        {
            VendVendorContactPersonHelper::addContactPersonToVendor(selectedVendor, _dirPersonRecId);
            selectedVendor = selectionHelper.getNext();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addContactPersonToVendor</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create contact persons for the passed vendor
    /// </summary>
    /// <param name="_vendor">
    /// <c>VendTable</c> record
    /// </param>
    /// <param name="_dirPersonRecId">
    /// RecId of the person which the created contact persons will be attached to
    /// </param>
    public static void addContactPersonToVendor(Vendtable _vendtable, DirPersonRecId _dirPersonRecId)
    {
        changecompany(_vendtable.DataAreaId)
        {
            ContactPerson contactPersonToInsert;

            contactPersonToInsert.initFromVendTable(_vendtable);
            contactPersonToInsert.Party = _dirPersonRecId;
            contactPersonToInsert.insert();
            VendVendorContactPersonHelper::setContactPersonUserRelation(contactPersonToInsert);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setContactPersonUserRelation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set contact persons user relation.
    /// </summary>
    /// <param name="_contactPerson"> to setup user relation for.
    /// The <c>ContactPerson</c> record
    /// </param>
    public static void setContactPersonUserRelation(ContactPerson _contactPerson)
    {
        changecompany(_contactPerson.DataAreaId)
        {
            ContactPersonUserRelation contactPersonUserRelation;

            contactPersonUserRelation.clear();
            contactPersonUserRelation.ContactPerson = _contactPerson.RecId;
            contactPersonUserRelation.UserAlias = xUserInfo::find(false, curUserId()).networkAlias;
            contactPersonUserRelation.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>