<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMVendRebateApproval</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMVendRebateApproval</c> class serves to approve calculated rebates.
/// </summary>
class TAMVendRebateApproval extends RunBaseBatch implements BatchRetryable
{
    TAMVendRebateApproval           tamVendRebateApproval;

    VendAccount                     vendAccount;
    TAMVendRebateGroupId            tamVendRebateGroupId;

    DialogField                     diagVendAccount;
    DialogField                     diagtamVendRebateGroupId;

    DialogRunbase                   dialogRunbase;

    #DEFINE.CurrentVersion(3)
    #LOCALMACRO.CurrentList
    vendAccount,
    tamVendRebateGroupId
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the vendor related fields to the dialog box.
    /// </summary>
    /// <returns>
    /// The dialog box object.
    /// </returns>
    public Object dialog()
    {
        dialogRunbase = super();

        diagVendAccount = dialogRunbase.addFieldValue(
                                extendedtypestr(VendAccount),
                                vendAccount,
                                "@SYS80105",
                                "@SYS14048");
        diagTAMVendRebateGroupId = dialogRunbase.addFieldValue(
                                        extendedtypestr(TAMVendRebateGroupId),
                                        tamVendRebateGroupId,
                                        "@MCR40079",
                                        "@MCR40079");

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the dialog vendor field values.
    /// </summary>
    /// <returns>
    /// true if the field values were retrieved successfully; otherwise, false.
    /// </returns>
    public boolean getFromDialog()
    {
        vendAccount = diagVendAccount.value();
        tamVendRebateGroupId = diagTAMVendRebateGroupId.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the application module value.
    /// </summary>
    /// <returns>
    /// The vendor application module value.
    /// </returns>
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Substantiates the <c>TAMVendRebateApproval</c> object.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Stores the dialog object parameters.
    /// </summary>
    /// <returns>
    /// The list of stored parameters.
    /// </returns>
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method processes the list of records selected and approves the calculated rebates.
    /// </summary>
    public void run()
    {
        TAMVendRebateTable::processRebates(vendAccount, tamVendRebateGroupId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Restores the last parameters saved.
    /// </summary>
    /// <param name="_packedClass">
    /// The list of stored parameters.
    /// </param>
    /// <returns>
    /// true if the restore succeded; otherwise, false.
    /// </returns>
    public boolean unpack(container _packedClass)
    {
        Integer     version     = conpeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case #CurrentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            default :
                ret = false;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method returns an instance of the <c>TAMVendRebateApproval</c> class.
    /// </summary>
    /// <returns>
    /// An instance of the <c>TAMVendRebateApproval</c> class.
    /// </returns>
    public static TAMVendRebateApproval construct()
    {
        return new TAMVendRebateApproval();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the class description.
    /// </summary>
    /// <returns>
    /// The class description.
    /// </returns>
    public static ClassDescription description()
    {
        return "@PRO1359";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method constructs an instance of the <c>TAMVendRebateApproval</c> class and runs the class.
    /// </summary>
    /// <param name="_args">
    /// Any arguments passed to the class.
    /// </param>
    public static void main(Args _args)
    {
        Common                  record;
        FormDataSource          tamVendRebateTable_ds;

        TAMVendRebateApproval   tamVendRebateApproval =
            TAMVendRebateApproval::construct();

        try
        {
            if (TAMVendRebateApproval.prompt())
            {
                TAMVendRebateApproval.runOperation();

                record = _args.record();
                if (record && FormDataUtil::getFormDataSource(record))
                {
                    tamVendRebateTable_ds = FormDataUtil::getFormDataSource(record);
                    tamVendRebateTable_ds.research();
                }
            }
        }
        catch (Exception::Error)
        {
            error("@SYS4205");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>