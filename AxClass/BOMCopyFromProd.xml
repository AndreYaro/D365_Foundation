<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOMCopyFromProd</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class BomCopyFromProd extends BOMCopyFrom
{
    ProdBOMSearch   search;

    ProdTable       prodTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>bomId</Name>
				<Source><![CDATA[
    BOMId bomId()
    {
        return prodTable.bomId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    void copy(boolean copyItemRef = false)
    {
        ProdBOM searchProdBOM;
        boolean searchCopyRef;

        setPrefix(strFmt("@SYS21832",search.prodId()));

        copyTo.initLineOffset();

        ttsbegin;

        while (search.next())
        {
            searchProdBOM = search.prodBOM();
            searchCopyRef = copyItemRef && (searchProdBOM.ProdLineType == BOMType::Item);
            setPrefix(strFmt("@SYS24300", searchProdBOM.ItemId));
            copyTo.copyTo(this, search, copyTo.defaultBOM(), searchCopyRef);
        }

        setPrefix(strFmt("@SYS21832",search.prodId()));

        copyTo.postCopy();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initToBom</Name>
				<Source><![CDATA[
    void initToBom(BOM _toBom, InventDim _inventDim)
    {
        // Copy only product dimensions
        _inventDim.clear();
        EcoResProductDimGroupSetup::copyProductDimensionsForItem(_toBom.ItemId, _toBom.inventDim(), _inventDim);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initToProdBom</Name>
				<Source><![CDATA[
    void initToProdBom(ProdBOM toProdBom, boolean copyRef = false)
    {
        BOM         bom;
        ProdBOM     searchProdBOM;

        searchProdBOM.data(search.prodBOM());
        bom.BOMMap::initFromBOMMap(searchProdBOM);
        toProdBom.BOMMap::initFromBOMMap(bom);

        if (copyRef)
        {
            toProdBom.InventRefType     = searchProdBOM.InventRefType;
            toProdBom.InventRefId       = searchProdBOM.InventRefId;
            toProdBom.InventRefTransId  = searchProdBOM.InventRefTransId;
            toProdBom.bomRefRecId       = searchProdBOM.bomRefRecId;
        }

        toProdBom.Reservation = searchProdBOM.Reservation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new(
        BOMCopyTo       _copyTo,
        ProdBOMSearch   _search
        )
    {
        search      = _search;

        super(_copyTo);

        prodTable  = ProdTable::find(search.prodId());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>