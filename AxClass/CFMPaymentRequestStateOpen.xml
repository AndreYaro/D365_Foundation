<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPaymentRequestStateOpen</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMPaymentRequestStateAttribute(CFMPaymentRequestStatus::Opened)]
class CFMPaymentRequestStateOpen extends CFMPaymentRequestState
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findNextStatus</Name>
				<Source><![CDATA[
    protected CFMPaymentRequestStatus findNextStatus(CFMPaymentRequestStatusOperation _operation)
    {
        CFMPaymentRequestStatus nextStatus;

        switch (_operation)
        {
            case CFMPaymentRequestStatusOperation::Correction:
                nextStatus = CFMPaymentRequestStatus::Opened;
                break;

            case CFMPaymentRequestStatusOperation::Confirmation:
                nextStatus = CFMPaymentRequestStatus::Confirmed;
                break;

            case CFMPaymentRequestStatusOperation::Cancellation:
                nextStatus = CFMPaymentRequestStatus::Canceled;
                break;

            case CFMPaymentRequestStatusOperation::Delete:
                nextStatus = CFMPaymentRequestStatus::Canceled;
                break;
        }

        return nextStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isOperationApplicable</Name>
				<Source><![CDATA[
    protected boolean isOperationApplicable(CFMPaymentRequestStatusOperation _operation)
    {
        boolean ret;

        switch (_operation)
        {
            case CFMPaymentRequestStatusOperation::Confirmation:
                ret = true;

                if (!paymentRequestTable.linesExist())
                {
                    ret = false;
                    validationResulTxt = "@Cashflowmanagement:PaymentRequestNotContainLine";
                }

                if (ret && !paymentRequestTable.isAccountActive())
                {
                    ret = false;
                    validationResulTxt = "@Cashflowmanagement:PaymentRequestPartyIsOnHold";
                }

                ret = ret && (paymentRequestTable.WorkflowStatus == CFMPaymentRequestWorkflowStatus::None
                   || paymentRequestTable.WorkflowStatus == CFMPaymentRequestWorkflowStatus::NotSubmitted
                   || paymentRequestTable.WorkflowStatus == CFMPaymentRequestWorkflowStatus::Rejected);
                break;

            case CFMPaymentRequestStatusOperation::Correction:
                ret = paymentRequestTable.WorkflowStatus != CFMPaymentRequestWorkflowStatus::Submitted
                   && !paymentRequestTable.unpostedJournalExist();
                break;

            case CFMPaymentRequestStatusOperation::Cancellation:
                ret = true;
                break;

            case CFMPaymentRequestStatusOperation::Delete:
                ret = paymentRequestTable.WorkflowStatus == CFMPaymentRequestWorkflowStatus::None
                  || paymentRequestTable.WorkflowStatus == CFMPaymentRequestWorkflowStatus::NotSubmitted;
                break;

            default:
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>paymentRequestStatus</Name>
				<Source><![CDATA[
    public CFMPaymentRequestStatus paymentRequestStatus()
    {
        return CFMPaymentRequestStatus::Opened;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>