<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustRecurrenceInvoiceDataContract</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustRecurrenceInvoiceDataContract</c> class is the data contract class for the
/// <c>CustRecurrenceInvoiceGenerationService</c> service.
/// </summary>
[DataContractAttribute,
    SysOperationContractProcessingAttribute(classStr(CustRecurrenceInvoiceUIBuilder)),
    SysOperationAlwaysInitializeAttribute
]
class CustRecurrenceInvoiceDataContract implements SysOperationValidatable, SysOperationInitializable
{
    TransDate recurrenceInvoiceDate;
    TransDate recurrenceGenerationDate;
    str custInvoiceTemplateName;
    container queryCustRecurrenceInvoice;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    public void initialize()
    {
        this.parmRecurrenceGenerationDate(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        queryCustRecurrenceInvoice = new QueryRun(querystr(CustRecurrenceInvoice)).pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustInvoiceTemplateName</Name>
				<Source><![CDATA[
    [DataMemberAttribute,
        SysOperationLabelAttribute(literalstr("@SYS318869")),
        SysOperationHelpTextAttribute(literalstr("@SYS318852")),
        SysOperationDisplayOrderAttribute('1')
    ]
    public str parmCustInvoiceTemplateName(str _custInvoiceTemplateName = custInvoiceTemplateName)
    {
        custInvoiceTemplateName = _custInvoiceTemplateName;

        return custInvoiceTemplateName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQueryCustRecurrenceInvoice</Name>
				<Source><![CDATA[
    [DataMemberAttribute,
        AifQueryTypeAttribute('custRecurrenceInvoice',
        querystr(CustRecurrenceInvoice))
    ]
    public str parmQueryCustRecurrenceInvoice(str custRecurrenceInvoice=SysOperationHelper::base64Encode(queryCustRecurrenceInvoice))
    {
        queryCustRecurrenceInvoice = SysOperationHelper::base64Decode(custRecurrenceInvoice);

        return custRecurrenceInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRecurrenceGenerationDate</Name>
				<Source><![CDATA[
    [DataMemberAttribute,
        SysOperationAlwaysInitializeAttribute,
        SysOperationLabelAttribute(literalstr("@SYS318853")),
        SysOperationHelpTextAttribute(literalstr("@SYS318854")),
        SysOperationDisplayOrderAttribute('1')
    ]
    public TransDate parmRecurrenceGenerationDate(TransDate _recurrenceGenerationDate = recurrenceGenerationDate)
    {
        recurrenceGenerationDate = _recurrenceGenerationDate;

        return recurrenceGenerationDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRecurrenceInvoiceDate</Name>
				<Source><![CDATA[
    [DataMemberAttribute,
        SysOperationLabelAttribute(literalstr("@SYS57610")),
        SysOperationHelpTextAttribute(literalstr("@SYS318868")),
        SysOperationDisplayOrderAttribute('0')
    ]
    public TransDate parmRecurrenceInvoiceDate(TransDate _recurrenceInvoiceDate = recurrenceInvoiceDate)
    {
        recurrenceInvoiceDate = _recurrenceInvoiceDate;

        return recurrenceInvoiceDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the parameters are valid.
    /// </summary>
    /// <returns>
    ///    true when the parameters are valid; otherwise, false.
    /// </returns>
    public boolean validate()
    {
        boolean ok = true;
        RecId fiscalCalendarRecId = CompanyInfoHelper::fiscalCalendarRecId();

        if (!(this.parmRecurrenceInvoiceDate() && LedgerFiscalCalendar::existDate(fiscalCalendarRecId, recurrenceInvoiceDate)))
        {
            ok = checkFailed("@SYS318910");
        }
        if (this.parmCustInvoiceTemplateName() != '' && !this.validateSelectedTemplates())
        {
            ok = checkFailed("@SYS318901");
        }
        if (!(this.parmRecurrenceGenerationDate() && LedgerFiscalCalendar::existDate(fiscalCalendarRecId, recurrenceGenerationDate)))
        {
            ok = checkFailed("@SYS318902");
        }
        if (this.parmRecurrenceInvoiceDate() > this.parmRecurrenceGenerationDate())
        {
            ok = checkFailed("@AccountsReceivable:ErrorRecurrenceInvoiceDateValidation");
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static CustRecurrenceInvoiceDataContract construct()
    {
        return new CustRecurrenceInvoiceDataContract();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateSelectedTemplates</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determine every name from Template dialog field is a valid template name in <c>CustInvoiceTemplate</c> table.
    /// </summary>
    /// <returns>
    /// Returns true if every name in the string is a valid template name, otherwise, false.
    /// </returns>
    protected boolean validateSelectedTemplates()
    {
        #Characters

        str selectedTemplates = this.parmCustInvoiceTemplateName();

        List templates = strSplit(selectedTemplates, #SEMICOLON);
        ListEnumerator templateList = templates.getEnumerator();

        while (templateList.moveNext())
        {
            if (CustInvoiceTemplate::findByTemplateName(templateList.current()).RecId == 0)
            {
                return false;
            }
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>