<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRCouponCopy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>MCRCouponCopy</c> copies coupons.
/// </summary>
class MCRCouponCopy
{
    MCRCouponId origCouponId;
    MCRCouponNumber mcrCouponNumber;
    MCRCouponDesc   mcrCouponDesc;
    MCRCouponId newCouponId;
    boolean copyItems;
    boolean copyCatalogs;
    boolean copyCustomer;
    boolean copyAttributes;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyCatalogs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates new <c>MCRCouponCatalog</c> records that relate
    /// to the record used to instantiate this class for the new Coupon.
    /// </summary>
    public void copyCatalogs()
    {
        MCRCouponCatalog    origMCRCouponCatalog;
        MCRCouponCatalog    newMCRCouponCatalog;

        insert_recordset newMCRCouponCatalog(CouponId, CatalogCode, Catalog)
            select newCouponId, CatalogCode, Catalog from origMCRCouponCatalog
                where origMCRCouponCatalog.CouponId == origCouponId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyCoupon</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new <c>MCRCoupon</c> record that is a
    /// copy of the record used to instantiate this class.
    /// </summary>
    /// <exception cref="Exception::Error">
    /// Throw error when validateWrite fails.
    /// </exception>
    public void copyCoupon()
    {
        MCRCoupon       newCoupons;
        // Create the new coupon
        ttsBegin;
        newCoupons.MCRCouponId = newCouponId;
        newCoupons.MCRCouponNumber = mcrCouponNumber;
        newCoupons.MCRCouponDesc = mcrCouponDesc;
        newCoupons.MCRCouponVoided = NoYes::No;
        newCoupons.MCREnabled = NoYes::No;
        if (newCoupons.validateWrite())
        {
            newCoupons.insert();
        }
        else
        {
            throw error("@SYS104318");
        }

        ttsCommit;

        // Copy the coupon attributes
        if (this.parmCopyAttributes())
        {
            this.copyCouponValueAttr();
        }

        // copy the item rules
        if (this.parmCopyItems())
        {
            this.copyItems();
        }

        // copy the catalog rules
        if (this.parmCopyCatalogs())
        {
            this.copyCatalogs();
        }

        // copy the customers
        if (this.parmCopyCustomer())
        {
            this.copyCustomers();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyCouponValueAttr</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new <c>MCRCoupon</c> record that is a copy
    /// of the record that has the value in the origCouponId calss
    /// variable.
    /// </summary>
    /// <exception cref="Exception::Error">
    /// Throw error when validateWrite fails.
    /// </exception>
    public void copyCouponValueAttr()
    {
        MCRCoupon       origCoupons, newCoupons;
        MCRCouponDiscount origMCRCouponDiscount, newMCRCouponDiscount;
        ttsBegin;

        origCoupons = MCRCoupon::find(origCouponId);

        newCoupons = MCRCoupon::find(newCouponId, true);

        newCoupons.MCRCustomerSpecific = origCoupons.MCRCustomerSpecific;
        newCoupons.MCROneTimeUse = origCoupons.MCROneTimeUse;
        newCoupons.MCRCouponExpDate = origCoupons.MCRCouponExpDate;
        newCoupons.MCRReissue = origCoupons.MCRReissue;
        newCoupons.MCRExclusive = origCoupons.MCRExclusive;
        newCoupons.MCRFromDate = origCoupons.MCRFromDate;
        newCoupons.MCRToDate = origCoupons.MCRToDate;
        newCoupons.MCRCouponParentId = origCoupons.MCRCouponParentId;
        newCoupons.MCRManufacturerCoupon = origCoupons.MCRManufacturerCoupon;
        newCoupons.MCRIncludeExcludeCatalog = origCoupons.MCRIncludeExcludeCatalog;
        newCoupons.MCRIncludeExcludeItem = origCoupons.MCRIncludeExcludeItem;
        newCoupons.MCRCouponRedemptionRate = origCoupons.MCRCouponRedemptionRate;
        newCoupons.MCRCouponOrign = origCoupons.MCRCouponOrign;
        if (newCoupons.validateWrite())
        {
            newCoupons.update();
        }
        else
        {
            throw error("@SYS104318");
        }

        insert_recordset newMCRCouponDiscount(MCRCouponId,MCRCouponAmount,MCRCouponDiscAttr,MCRCouponValue)
            select newCouponId, MCRCouponAmount, MCRCouponDiscattr, MCRCouponValue from origMCRCouponDiscount
                where origMCRCouponDiscount.MCRCouponId == origCouponId;

        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyCustomers</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates new <c>MCRCouponCustomer</c> records that relate
    /// to the record used to instantiate this class for the new Coupon.
    /// </summary>
    public void copyCustomers()
    {
        MCRCouponCustomer   origMCRCouponCustomer, newMCRCouponCustomer;
        NoYes voided = NoYes::No;

        insert_recordset newMCRCouponCustomer(CustomerCode,CustVendRel,MCRCouponId, MCRCouponVoided)
            select CustomerCode, CustVendRel, newCouponId, voided from origMCRCouponCustomer
                where origMCRCouponCustomer.MCRCouponId == origCouponId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyItems</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates new <c>MCRCouponItem</c> records that relate
    /// to the record used to instantiate this class for the new Coupon.
    /// </summary>
    public void copyItems()
    {
        MCRCouponItem   origMCRCouponItem, newMCRCouponItem;

        insert_recordset newMCRCouponItem(ItemCode, ItemRelation, MCRCouponId)
            select ItemCode, ItemRelation, newCouponId from origMCRCouponItem
                where origMCRCouponItem.MCRCouponId == origCouponId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new object of <c>MCRCouponCopy</c> class, setting
    /// class variables to the passed values.
    /// </summary>
    /// <param name="_origCouponId">
    /// Coupon ID of the existing <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_mcrCouponNumber">
    /// Coupon number of the existing <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_mcrCouponDesc">
    /// Coupon description of the existing <c>MCRCoupon</c> record.
    /// </param>
    /// <param name="_copyItems">
    /// A <c>boolean</c> value used to determine whether or not to copy the items
    /// for the current coupon to the new one.
    /// </param>
    /// <param name="_copyoffers">
    /// A <c>boolean</c> value used to determine whether or not to copy the offers
    /// for the current coupon to the new one.
    /// </param>
    /// <param name="_copyCustomer">
    /// A <c>boolean</c> value used to determine whether or not to copy the customers
    /// for the current coupon to the new one.
    /// </param>
    /// <param name="_copyAttributes">
    /// A <c>boolean</c> value used to determine whether or not to copy the attributes
    /// for the current coupon to the new one.
    /// </param>
    public void new(MCRCouponId _origCouponId,
                    MCRCouponNumber _mcrCouponNumber,
                    MCRCouponDesc   _mcrCouponDesc,
                    boolean _copyItems,
                    boolean _copyoffers,
                    boolean _copyCustomer,
                    boolean _copyAttributes)
    {
        this.parmCopyAttributes(_copyAttributes);
        this.parmCopyCustomer(_copyCustomer);
        this.parmCopyItems(_copyItems);
        this.parmCopyCatalogs(_copyoffers);
        this.parmOrigCouponId(_origCouponId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyAttributes</Name>
				<Source><![CDATA[
        public boolean parmCopyAttributes (boolean _copyAttributes = copyAttributes)
    {
        copyAttributes = _copyAttributes;
        return  copyAttributes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyCatalogs</Name>
				<Source><![CDATA[
        public boolean parmCopyCatalogs(boolean _copyCatalogs = copyCatalogs)
    {
        copyCatalogs = _copyCatalogs;
        return copyCatalogs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyCustomer</Name>
				<Source><![CDATA[
        public boolean parmCopyCustomer (boolean _copyCustomer = copyCustomer)
    {
        copyCustomer = _copyCustomer;
        return copyCustomer;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCopyItems</Name>
				<Source><![CDATA[
        public boolean parmCopyItems(boolean _copyItems = copyItems)
    {
        copyItems = _copyItems;
        return copyItems;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMCRCouponDesc</Name>
				<Source><![CDATA[
        public MCRCouponDesc parmMCRCouponDesc (MCRCouponDesc _mcrCouponDesc = mcrCouponDesc)
    {
        mcrCouponDesc = _mcrCouponDesc;
        return mcrCouponDesc;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmMCRCouponNumber</Name>
				<Source><![CDATA[
        public MCRCouponNumber parmMCRCouponNumber (MCRCouponNumber _mcrCouponNumber = mcrCouponNumber)
    {
        mcrCouponNumber = _mcrCouponNumber;
        return mcrCouponNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNewCouponId</Name>
				<Source><![CDATA[
        public MCRCouponId parmNewCouponId(MCRCouponId _newCouponId = newCouponId)
    {
        newCouponId = _newCouponId;
        return newCouponId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmOrigCouponId</Name>
				<Source><![CDATA[
        public MCRCouponId parmOrigCouponId (MCRCouponId _origCouponId = origCouponId)
    {
        origCouponId = _origCouponId;
        return origCouponId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>