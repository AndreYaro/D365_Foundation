<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EssPersonProjectRoleHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Shared common code between HcmPersonProjectRole and EssPersonProjectRole
/// </summary>
class EssPersonProjectRoleHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>promoteFromEssPersonProjectRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transition a record to a final state and move it into the HcmPersonProjectRole table
    /// </summary>
    /// <param name = "_personProjectRoleWorkflowRedID">The recId of the record to write the HcmPersonProjectRole Table</param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public void promoteFromEssPersonProjectRole(RecId _personProjectRoleWorkflowRedID)
    {
        EssPersonProjectRole personProjectRole;

        if (_personProjectRoleWorkflowRedID)
        {
            select firstonly forupdate personProjectRole where
                personProjectRole.RecId == _personProjectRoleWorkflowRedID;

            if (EssPersonProjectRoleHelper::persistToHcmPersonProjectRole(personProjectRole))
            {
                ttsbegin;
                personProjectRole.DocumentLifecycle().TransitionTo(HcmApprovalStatus::Completed);
                personProjectRole.update();
                ttscommit;
            }
            else
            {
                ttsbegin;
                personProjectRole.DocumentLifecycle().TransitionTo(HcmApprovalStatus::Failed);
                personProjectRole.update();
                ttscommit;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyFromHcmPersonProjectRoleToEssPerson</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copy the table buffer from HcmPersonProjectRole to EssPersonProjectRole
    /// </summary>
    /// <param name = "_hcmPersonProjectRole">The HcmPersonProjectRole table buffer to copy</param>
    /// <param name = "_operation">What HcmWorkflowOperation to set on the EssPersonProjectRole record</param>
    /// <returns>Populated EssPersonProjectRole table buffer</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public EssPersonProjectRole copyFromHcmPersonProjectRoleToEssPerson(HcmPersonProjectRole _hcmPersonProjectRole, HcmWorkflowOperation _operation)
    {
        EssPersonProjectRole personProjectRole;

        EssPersonProjectRoleHelper::initEssPersonProjectRoleFromHcmPersonProjectRole(_hcmPersonProjectRole, personProjectRole);

        personProjectRole.WorkflowOperation = _operation;

        return personProjectRole;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initEssPersonProjectRoleFromHcmPersonProjectRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the table <c>EssPersonProjectRole</c> from table <c>HcmPersonProjectRole</c>.
    /// </summary>
    /// <param name = "_hcmPersonProjectRole">
    /// The <c>HcmPersonProjectRole</c> table buffer.
    /// </param>
    /// <param name = "_essPersonProjectRole">
    /// The <c>EssPersonProjectRole</c> table buffer.
    /// </param>
    static protected void initEssPersonProjectRoleFromHcmPersonProjectRole(HcmPersonProjectRole _hcmPersonProjectRole, EssPersonProjectRole _essPersonProjectRole)
    {
        _essPersonProjectRole.Person = _hcmPersonProjectRole.Person;
        _essPersonProjectRole.ProjId = _hcmPersonProjectRole.ProjId;
        _essPersonProjectRole.ProjectName = _hcmPersonProjectRole.ProjectName;
        _essPersonProjectRole.Role = _hcmPersonProjectRole.Role;
        _essPersonProjectRole.RoleStartDate = _hcmPersonProjectRole.RoleStartDate;
        _essPersonProjectRole.RoleEndDate = _hcmPersonProjectRole.RoleEndDate;
        _essPersonProjectRole.LegalEntity = _hcmPersonProjectRole.LegalEntity;
        _essPersonProjectRole.PersonProjectRoleRecId = _hcmPersonProjectRole.RecId;
        _essPersonProjectRole.PersonProjectRoleRecVersion = _hcmPersonProjectRole.RecVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>persistToHcmPersonProjectRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Move or update a record from EssPersonProjectRole to HcmPersonProjectRole
    /// </summary>
    /// <param name = "_essPersonProjectRole">The table buffer in EssPersonProjectRole to move</param>
    /// <returns>True if the move was successful</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public boolean persistToHcmPersonProjectRole(EssPersonProjectRole _essPersonProjectRole)
    {
        HcmPersonProjectRole personProjectRole;
        Exception exceptionType;
        HcmApprovalStatus approvalStatus;
        boolean success = false;

        personProjectRole = HcmPersonProjectRole::find(_essPersonProjectRole.PersonProjectRoleRecId, true);
        if (_essPersonProjectRole.PersonProjectRoleRecId != 0
                && _essPersonProjectRole.PersonProjectRoleRecVersion != personProjectRole.RecVersion)
        {
            success = false;
        }
        else
        {
            EssPersonProjectRoleHelper::initHcmPersonProjectRoleFromEssPersonProjectRole(_essPersonProjectRole, personProjectRole);

            if (personProjectRole.validateWrite())
            {
                ttsbegin;
                personProjectRole.write();
                ttscommit;
                success = true;
            }
        }

        return success;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initHcmPersonProjectRoleFromEssPersonProjectRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the table <c>HcmPersonProjectRole</c> from table <c>EssPersonProjectRole</c>.
    /// </summary>
    /// <param name = "_esspersonProjectRole">
    /// The table <c>EssPersonProjectRole</c> buffer.
    /// </param>
    /// <param name = "_hcmPersonProjectRole">
    /// The table <c>HcmPersonProjectRole</c> buffer.
    /// </param>
    static protected void initHcmPersonProjectRoleFromEssPersonProjectRole(EssPersonProjectRole _esspersonProjectRole, HcmPersonProjectRole _hcmPersonProjectRole)
    {
        _hcmPersonProjectRole.ProjId = _essPersonProjectRole.ProjId;
        _hcmPersonProjectRole.ProjectName = _essPersonProjectRole.ProjectName;
        _hcmPersonProjectRole.Role = _essPersonProjectRole.Role;
        _hcmPersonProjectRole.Person = _essPersonProjectRole.Person;
        _hcmPersonProjectRole.RoleStartDate = _essPersonProjectRole.RoleStartDate;
        _hcmPersonProjectRole.RoleEndDate = _essPersonProjectRole.RoleEndDate;
        _hcmPersonProjectRole.LegalEntity = _essPersonProjectRole.LegalEntity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// The shared validateWrite method for both the HcmPersonProjectRole and EssPersonProjectRole tables.
    /// </summary>
    /// <param name = "_roleStartDate">The role start date</param>
    /// <param name = "_roleEndDate">The role end date</param>
    /// <returns></returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public boolean validateWrite(StartDate _roleStartDate, EndDate _roleEndDate)
    {
        boolean retValue = true;

        if (_roleEndDate && _roleEndDate < _roleStartDate)
        {
            retValue = checkFailed("@SYS21592");
        }

        return retValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjectName</Name>
				<Source><![CDATA[
    /// <summary>
    /// The shared method for both the HcmPersonProjectRole and EssPersonProjectRole tables
    /// to get project name for specified project id.
    /// </summary>
    /// <param name = "_projId">The project Id</param>
    /// <returns>The correspond project name</returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public HrmDescription getProjectName(ProjId _projId)
    {
        return ProjTable::find(_projId).Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLegalEntity</Name>
				<Source><![CDATA[
    /// <summary>
    /// The shared method for both the HcmPersonProjectRole and EssPersonProjectRole tables
    /// to get legal entity based on whether projId is set or not.
    /// </summary>
    /// <param name = "_projId">The project Id</param>
    /// <returns>The legal entity </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    static public LegalEntity getLegalEntity(ProjId _projId)
    {
        LegalEntity legalEntity;
        if (_projId)
        {
            legalEntity = CompanyInfo::current();
        }

        return  legalEntity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isEditable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Check if the record is editable while workflow is enabled
    /// </summary>
    /// <param name="_personProjectRoleRecId">
    ///    A value of the <c>PersonProjectRoleRecId</c> field.
    /// </param>
    /// <returns>
    ///    Returns true if the record is editable, otherwise false
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static boolean isEditable(HcmPersonProjectRoleRecId _personProjectRoleRecId)
    {
        EssPersonProjectRole essPersonProjectRole;
        boolean isEditable = true;

        if (EssPersonProjectRole::IsWorkflowConfigured())
        {
            isEditable = (select firstonly RecID from essPersonProjectRole
                where essPersonProjectRole.PersonProjectRoleRecId == _personProjectRoleRecId
                    && essPersonProjectRole.ApprovalStatus != HcmApprovalStatus::Completed
                    && essPersonProjectRole.ApprovalStatus != HcmApprovalStatus::Cancelled).RecId == 0;
        }

        return isEditable;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>