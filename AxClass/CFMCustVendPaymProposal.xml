<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMCustVendPaymProposal</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CFMCustVendPaymProposal</c> class represents a payment proposal by relating payment requests
/// selected for payment into grouped payment suggestions.
/// </summary>
class CFMCustVendPaymProposal extends CustVendPaymProposal
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newLedgerJournal</Name>
				<Source><![CDATA[
    public static CFMCustVendPaymProposal newLedgerJournal(LedgerJournalId _ledgerJournalId)
    {
        CFMCustVendPaymProposal custVendPaymProposal = new CFMCustVendPaymProposal();

        custVendPaymProposal.setLedgerJournalId(_ledgerJournalId);
        custVendPaymProposal.initOrgStrategy();

        return custVendPaymProposal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPaymentRequest</Name>
				<Source><![CDATA[
    public void addPaymentRequest(CFMPaymentRequestTable _paymentRequest)
    {
        CustVendPaymProposalLine paymProposalLine = this.buildPaymProposalLinePR(_paymentRequest,
            _paymentRequest.PaymentDate,
            _paymentRequest.PaymentCurrency);

        if (paymProposalLine.AccountNum != '')
        {
            // verify the proposal record is created in the same company as the associated Payment Journal.
            Debug::assert(paymProposalLine.company() == this.parmLedgerJournalTableCompany());

            paymProposalLine = this.findOrCreate(paymProposalLine);

            SpecTransManager specTransManager = SpecTransManager::constructServer(paymProposalLine, false);

            specTransManager.insert(
                _paymentRequest.company(),
                _paymentRequest.TableId,
                _paymentRequest.RecId,
                -_paymentRequest.amountToPayInCurrency(),
                _paymentRequest.PaymentCurrency,
                NoYes::No,
                0,
                _paymentRequest.PaymentDate);

            this.createPaymentRequestLinkToPaymProposal(paymProposalLine, _paymentRequest);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildPaymProposalLinePR</Name>
				<Source><![CDATA[
    public CustVendPaymProposalLine buildPaymProposalLinePR(CFMPaymentRequestTable _paymentRequestTable, TransDate _paymDate, CurrencyCode _paymCurrency)
    {
        CFMCustVendProposalOrgPropose paymProposalOrgTmp = paymProposalOrg as CFMCustVendProposalOrgPropose;

        return paymProposalOrgTmp.buildPaymProposalLinePR(_paymentRequestTable, _paymDate, _paymCurrency);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setLedgerJournalId</Name>
				<Source><![CDATA[
    protected void setLedgerJournalId(LedgerJournalId _ledgerJournalId)
    {
        ledgerJournalTable = LedgerJournalTable::find(_ledgerJournalId);
        custVendTransStatic = CustVendTransStatic::newModuleCustVend(LedgerJournalName::journalType2ModuleCustVend(ledgerJournalTable.JournalType));
        paymProposalCalcPaym = CustVendPaymProposalCalcPaym::construct(ledgerJournalTable.JournalType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initOrgStrategy</Name>
				<Source><![CDATA[
    public void initOrgStrategy()
    {
        paymProposalOrg = new CFMCustVendProposalOrgPropose();
        paymProposalOrg.parmPaymProposal(this);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>