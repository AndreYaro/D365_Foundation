<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAccountStatementExtController_CH_QRBill_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// An extension class of Account statement report data provider to fill QR-Bill specific fields in the GIRO report.
/// </summary>
[ExtensionOf(classStr(CustAccountStatementExtController))]
public final class CustAccountStatementExtController_CH_QRBill_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>populateMoneyTransferSlip</Name>
				<Source><![CDATA[
    protected void populateMoneyTransferSlip()
    {
        next populateMoneyTransferSlip();

        if (printGiro == PaymentStub::QRBill)
        {
            CompanyBankAccountId paymBankAccountId;
            CustPaymModeTable custPaymModeTable = CustPaymModeTable::find(currentStatementCust.PaymMode);
            if (custPaymModeTable.AccountType == LedgerJournalACType::Bank)
            {
                paymBankAccountId = custPaymModeTable.parmPaymAccount();
            }

            moneyTransferSlip.initFromBankAccountTable(paymBankAccountId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateGiro</Name>
				<Source><![CDATA[
    protected void populateGiro()
    {
        next populateGiro();

        if (printGiro == PaymentStub::QRBill)
        {
            custAccountStatementExtTmp.InvoiceAmount = 0;
            custAccountStatementExtTmp.IBAN_CH = moneyTransferSlip.getQRIBANOrIBAN('');
            custAccountStatementExtTmp.currencyCodeISO_CH = Currency::currencyCodeISO(currentCust.Currency);

            LogisticsPostalAddress invoiceAddress = currentCust.postalAddress();
            custAccountStatementExtTmp.InvoiceAddressLine1_CH = strRemoveCr(invoiceAddress.Street);
            custAccountStatementExtTmp.InvoiceAddressLine2_CH = strFmt('%1 %2', invoiceAddress.ZipCode, invoiceAddress.City);
            custAccountStatementExtTmp.InvoiceCountryRegionId_CH = invoiceAddress.CountryRegionId;
            custAccountStatementExtTmp.OcrField = '';
            custAccountStatementExtTmp.LanguageId_CH = currentCust.languageId();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>