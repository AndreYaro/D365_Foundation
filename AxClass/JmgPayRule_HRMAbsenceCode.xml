<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayRule_HRMAbsenceCode</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgPayRule_HRMAbsenceCode</c> class represents the absence code pay rule.
/// </summary>
/// <remarks>
///    This class lets users delimit pay agreement lines by absence codes.
/// </remarks>
class JmgPayRule_HRMAbsenceCode extends JmgPayRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the description of the absence codes pay rule.
    /// </summary>
    /// <returns>
    ///    The description of the absence codes pay rule.
    /// </returns>
    public JmgDescription description()
    {
        return "@SYS32249";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDesignTimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the design time data source for the absence codes pay rule.
    /// </summary>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the design time data source.
    /// </returns>
    /// <remarks>
    ///    The absence codes pay rule adds a <c>HrmAbsenceCodeGroupId</c> field and a <c>HRMAbsenceCodeId</c>
    ///    field to the custom design data source.
    /// </remarks>
    protected ExpressionDataSource getDesignTimeCustomDataSource()
    {
        ExpressionDataSource ret = super();

        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(HRMAbsenceCodeGroupId));
        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(HRMAbsenceCodeId));

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJoinConditions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the expression for the Absence codes pay rule.
    /// </summary>
    /// <returns>
    ///    The expression for the Absence codes pay rule.
    /// </returns>
    /// <remarks>
    ///    The Absence codes pay rule adds two conditions: The <c>HrmAbsenceCodeGroupId</c> field equals the
    ///    <c>Query.HrmAbsenceCodeGroupId</c> value and the <c>HrmAbsenceCodeId</c> field equals
    ///    <c>Query.HrmAbsenceCodeId</c> value.
    /// </remarks>
    public Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement getJoinConditions()
    {
        Query q = this.getQuery();

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionProjection designTimeProjection =
            SysExpression::getDesignTimeProjectionModel(classstr(JmgPayRule_HRMAbsenceCode), this.getDesignTimeCustomDataSources());

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement expressionDefinition =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement::Create();

        Microsoft.Dynamics.AX.Framework.Expression.Model.Field hrmAbsenceCodeField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionAbsenceCodeField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field hrmAbsenceCodeGroupField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionAbsenceCodeGroupField;

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement andElement =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement::Create();

        hrmAbsenceCodeGroupField = designTimeProjection.GetField(q.dataSourceTable(tablenum(HRMAbsenceCode)).name(), fieldstr(HRMAbsenceCode, hrmAbsenceCodeGroupId));
        transactionAbsenceCodeGroupField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(HRMAbsenceCodeGroupId));

        hrmAbsenceCodeField = designTimeProjection.GetField(q.dataSourceTable(tablenum(HRMAbsenceCode)).name(), fieldstr(HRMAbsenceCode, hrmAbsenceCodeId));
        transactionAbsenceCodeField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(HRMAbsenceCodeId));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldStringElement::Create(
                hrmAbsenceCodeGroupField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.StringOperator::Equals,
                transactionAbsenceCodeGroupField));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldStringElement::Create(
                hrmAbsenceCodeField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.StringOperator::Equals,
                transactionAbsenceCodeField));

        expressionDefinition.AddAndElement(andElement);

        return expressionDefinition;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the name of the <c>JmgPayRuleHRMAbsenceCode</c> query.
    /// </summary>
    /// <returns>
    ///    The name of the <c>JmgPayRuleHRMAbsenceCode</c> query.
    /// </returns>
    queryName getQueryName()
    {
        return querystr(JmgPayRuleHRMAbsenceCode);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRuntimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the runtime custom time data source for the absence codes pay rule.
    /// </summary>
    /// <param name="_jmgStampJournalTrans">
    ///    The transaction from where the values of the runtime fields are initialized.
    /// </param>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the run time data source for the absence codes
    ///    pay rule.
    /// </returns>
    /// <remarks>
    ///    Adds two run time fields: the <c>HRMAbsenceCodeGroupId</c> field that is initialized from the
    ///    <c>JobRef</c> field of the transaction and the <c>HRMAbsenceCodeId</c> field that is initialized
    ///    from the <c>ActOprID</c> field of the transaction.
    /// </remarks>
    protected ExpressionDataSource getRuntimeCustomDataSource(JmgStampJournalTrans _jmgStampJournalTrans)
    {
        ExpressionDataSource expressionDataSource;

        expressionDataSource = super(_jmgStampJournalTrans);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(HRMAbsenceCodeGroupId), _jmgStampJournalTrans.JobRef);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(HRMAbsenceCodeId), _jmgStampJournalTrans.ActOprID);
        return expressionDataSource;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>