<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayRule_ProjActivity</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgPayRule_ProjActivity</c> class represents the project activities pay rule.
/// </summary>
/// <remarks>
///    This class lets users delimit the pay agreement lines by the project activities.
/// </remarks>
class JmgPayRule_ProjActivity extends JmgPayRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the description of the project activities pay rule.
    /// </summary>
    /// <returns>
    ///    The description of the project activities pay rule.
    /// </returns>
    public JmgDescription description()
    {
        return "@SYS55088";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDesignTimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the design time data source for the project activities pay rule.
    /// </summary>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the design time data source.
    /// </returns>
    /// <remarks>
    ///    The project activities pay rule adds a <c>ProjActivityNumber</c> field to the custom design data
    ///    source.
    /// </remarks>
    protected ExpressionDataSource getDesignTimeCustomDataSource()
    {
        ExpressionDataSource ret;

        ret = super();
        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(ProjActivityNumber));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJoinConditions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves an expression with conditions for the Project activities pay rule.
    /// </summary>
    /// <returns>
    ///    An expression with conditions for the Project activities pay rule.
    /// </returns>
    /// <remarks>
    ///    The Project activities pay rule adds one condition: the <c>ProjActivityNumber</c> field equals the
    ///    <c>Query.ActivityNumber</c> value.
    /// </remarks>
    public Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement getJoinConditions()
    {
        Query q = this.getQuery();

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionProjection designTimeProjection =
            SysExpression::getDesignTimeProjectionModel(classstr(JmgPayRule_ProjActivity), this.getDesignTimeCustomDataSources());

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement expressionDefinition =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement::Create();

        Microsoft.Dynamics.AX.Framework.Expression.Model.Field projActivityField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionProjActivityField;

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement andElement =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement::Create();

        projActivityField = designTimeProjection.GetField(q.dataSourceTable(tablenum(ProjActivity)).name(), fieldstr(ProjActivity, ActivityNumber));
        transactionProjActivityField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(ProjActivityNumber));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldStringElement::Create(
                projActivityField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.StringOperator::Equals,
                transactionProjActivityField));

        expressionDefinition.AddAndElement(andElement);

        return expressionDefinition;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the name of the <c>JmgPayRuleProjActivity</c> query.
    /// </summary>
    /// <returns>
    ///    The name of the <c>JmgPayRuleProjActivity</c> query.
    /// </returns>
    queryName getQueryName()
    {
        return querystr(JmgPayRuleProjActivity);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRuntimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the runtime custom time data source for the project activities pay rule.
    /// </summary>
    /// <param name="_jmgStampJournalTrans">
    ///    The transaction from where the values of the runtime fields are initialized.
    /// </param>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the run time data source for the project
    ///    activities pay rule.
    /// </returns>
    /// <remarks>
    ///    Adds one run time fields: the <c>ProjActivityNumber</c> field, which is initialized from the
    ///    <c>ActOprID</c> field of the transaction.
    /// </remarks>
    protected ExpressionDataSource getRuntimeCustomDataSource(JmgStampJournalTrans _jmgStampJournalTrans)
    {
        ExpressionDataSource expressionDataSource;

        expressionDataSource = super(_jmgStampJournalTrans);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(ProjActivityNumber), _jmgStampJournalTrans.ActOprID);
        return expressionDataSource;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>