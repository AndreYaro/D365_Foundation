<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTestCorrectionController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTestCorrectionController</c> class is controller class for the
/// <c>InventTestCorrection</c> report.
/// </summary>
class InventTestCorrectionController extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Modifies the report query and parameters by using the caller settings.
    /// </summary>
    protected void prePromptModifyContract()
    {
        if (this.parmArgs() && this.parmArgs().dataset() == tableNum(InventNonConformanceTable))
        {
            InventNonConformanceTable inventNonConformanceTable = this.parmArgs().record();
            Query query = this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey());
            QueryBuildDataSource queryBuildDataSource = SysQuery::findOrCreateDataSource(query, tableNum(InventNonConformanceTable));
            QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(InventNonConformanceTable, InventNonConformanceID));
            queryBuildRange.value(inventNonConformanceTable.InventNonConformanceID);

            this.addRestrictionRangeValue(query);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addRestrictionRangeValue</Name>
				<Source><![CDATA[
    private void addRestrictionRangeValue(Query _query)
    {
        InventTestReportSetup inventTestReportSetup;
        DocuRestriction restriction;
        int correctionReportCount = 0;

        while select DocuRestriction from inventTestReportSetup
                where inventTestReportSetup.InventTestReport == InventTestReport::CorrectionReport
        {
            correctionReportCount++;
            restriction = inventTestReportSetup.DocuRestriction;
        }

        if (correctionReportCount == 1)
        {
            QueryBuildDataSource queryBuildDataSourceDocuRef = SysQuery::findOrCreateDataSourceByName(_query, 'DocuRef', tableNum(DocuRef));
            QueryBuildRange queryBuildRangeRestriction = SysQuery::findOrCreateRange(queryBuildDataSourceDocuRef, fieldNum(DocuRef, Restriction));
            queryBuildRangeRestriction.value(queryValue(restriction));

            QueryBuildDataSource queryBuildDataSourceDocuRefTest = SysQuery::findOrCreateDataSourceByName(_query, 'DocuRefTest', tableNum(DocuRef));
            QueryBuildRange queryBuildRangeRestrictionTest = SysQuery::findOrCreateRange(queryBuildDataSourceDocuRefTest, fieldNum(DocuRef, Restriction));
            queryBuildRangeRestrictionTest.value(queryValue(restriction));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static InventTestCorrectionController construct()
    {
        return new InventTestCorrectionController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args _args)
    {
        InventTestCorrectionController controller  = InventTestCorrectionController::construct();
        controller.parmReportName(ssrsReportStr(InventTestCorrection, Report));
        controller.parmArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>