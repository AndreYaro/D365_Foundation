<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EGAISRequestRepealWBCreate_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>EGAISRequestRepealWBCreate_RU</c> class is used to create EGAIS bill of lading repeal request.
/// </summary>
public class EGAISRequestRepealWBCreate_RU extends RunBase
{
    EGAISRequestRepealWBNum_RU      requestNum;
    EGAISRequestRepealWBDateTime_RU requestDateTime;

    DialogField                     fieldRequestNum;
    DialogField                     fieldRequestDateTime;

    EGAISWaybillJour_RU             waybillJour;
    RefRecId                        waybillJourRecId;
    EGAISRequestRepealWB_RU         requestRepeal;

    protected const int CurrentVersion = 1;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canSaveLast</Name>
				<Source><![CDATA[
    /// <summary>
    /// Defines if the last user choices can be saved and restored.
    /// </summary>
    /// <returns>
    /// true, if the last user choices can be saved and restored; false, otherwise.
    /// </returns>
    protected boolean canSaveLast()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createRequestRepealWB</Name>
				<Source><![CDATA[
    private void createRequestRepealWB()
    {
        requestRepeal.clear();
        requestRepeal.Client            = waybillJour.Consignee;
        requestRepeal.RequestNum        = requestNum;
        requestRepeal.RequestDateTime   = requestDateTime;
        requestRepeal.WaybillJour       = waybillJour.RecId;
        requestRepeal.Version           = EGAISDocumentVersion_RU::Version1;
        requestRepeal.StatusIssue       = EGAISRequestRepealWBStatusIssue_RU::Created;
        requestRepeal.Direction         = EGAISDocumentDirection_RU::Outgoing;
        requestRepeal.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        Dialog  dialog = super();

        fieldRequestNum         = dialog.addField(extendedtypestr(EGAISRequestRepealWBNum_RU));
        fieldRequestDateTime    = dialog.addFieldValue(extendedtypestr(EGAISRequestRepealWBDateTime_RU), DateTimeUtil::applyTimeZoneOffset(DateTimeUtil::utcNow(), DateTimeUtil::getCompanyTimeZone()));

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = super();

        requestNum      = fieldRequestNum.value();
        requestDateTime = DateTimeUtil::removeTimeZoneOffset(fieldRequestDateTime.value(), DateTimeUtil::getCompanyTimeZone());

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLast</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the last choice that is stored in the last value table.
    /// </summary>
    /// <remarks>
    ///    If no record can be found or the <c>unpack</c> method returns false, <c>initParmDefault</c> method
    ///    will be called. Normally, this method should not be overridden.
    /// </remarks>
    public void getLast()
    {
        if (this.canSaveLast())
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    private void initParameters()
    {
        waybillJour = EGAISWaybillJour_RU::findRecId(waybillJourRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [CurrentVersion,  waybillJourRecId, requestNum, requestDateTime];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmWaybillJourRecId</Name>
				<Source><![CDATA[
    public RecId parmWaybillJourRecId(RecId _waybillJourRecId = waybillJourRecId)
    {
        waybillJourRecId = _waybillJourRecId;

        return waybillJourRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        #OCCRetryCount
        SysInfoAction_MenuFunction  sysInfoAction;

        setPrefix(EGAISRequestRepealWBCreate_RU::description());

        if (!this.validate())
        {
            throw error("@SYS18447");
        }

        try
        {
            ttsBegin;

            this.initParameters();
            this.createRequestRepealWB();

            ttsCommit;

            sysInfoAction = SysInfoAction_MenuFunction::newMenuItem(menuitemDisplayStr(EGAISRequestRepealWBOutgoing_RU), MenuItemType::Display);
            sysInfoAction.parmCallerBuffer(requestRepeal);

            info(strFmt("@EGAIS:EgaisBolRepealRequestNumberDateHasBeenCreated", requestNum, DateTimeUtil::applyTimeZoneOffset(requestDateTime, DateTimeUtil::getCompanyTimeZone())), '', sysInfoAction);

        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
        catch (Exception::Error)
        {
            throw error("@SYS18447");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveLast</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Saves the last choice by using the <c>pack</c> method.
    /// </summary>
    /// <remarks>
    ///    This method should not normally be overridden.
    /// </remarks>
    public void saveLast()
    {
        if (this.canSaveLast())
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer version = conPeek(_packedClass, 1);

        switch (version)
        {
            case CurrentVersion:
                [version, waybillJourRecId, requestNum, requestDateTime] = _packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean                 ret;
        EGAISWaybillJour_RU     waybillJourLoc;

        ret = super(_calledFrom);

        if (!requestNum)
        {
            ret = checkFailed(strFmt("@SYS84753", "@SYS301337"));
        }

        if (!requestDateTime)
        {
            ret = checkFailed(strFmt("@SYS84753", "@SYS317952"));
        }

        waybillJourLoc = EGAISWaybillJour_RU::findRecId(waybillJourRecId);

        if (!waybillJourLoc.canRequestRepealWBBeCreated())
        {
            ret = checkFailed(strFmt("@EGAIS:EgaisBolRepealRequestCannotBeCreatedForTheEgaisBol", waybillJourLoc.WaybillNum, waybillJourLoc.WaybillDate));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    protected static EGAISRequestRepealWBCreate_RU construct()
    {
        return new EGAISRequestRepealWBCreate_RU();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the description of the class.
    /// </summary>
    /// <returns>
    ///    The description of the class.
    /// </returns>
    static public ClassDescription description()
    {
        return "@EGAIS:CreateEgaisBolRepealRequest";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        EGAISRequestRepealWBCreate_RU   runClass;
        FormDataSource                  formDataSource;

        if (!_args || _args.dataset() != tableNum(EGAISWaybillJour_RU))
        {
            throw error(strFmt("@SYS25516", EGAISRequestRepealWBCreate_RU::description()));
        }

        runClass = EGAISRequestRepealWBCreate_RU::newFromWaybillJour(_args.record());

        if (runClass.prompt())
        {
            runClass.runOperation();

            if (_args && _args.record() && _args.record().isFormDataSource())
            {
                formDataSource = _args.record().dataSource();

                if (formDataSource)
                {
                    formDataSource.research(true);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromWaybillJour</Name>
				<Source><![CDATA[
    public static EGAISRequestRepealWBCreate_RU newFromWaybillJour(EGAISWaybillJour_RU _waybillJour)
    {
        EGAISRequestRepealWBCreate_RU runClass;

        runClass = EGAISRequestRepealWBCreate_RU::construct();
        runClass.parmWaybillJourRecId(_waybillJour.RecId);

        return runClass;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>