<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSSequenceNumberMoverFromQuery</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class WHSSequenceNumberMoverFromQuery extends WHSSequenceNumberMoverBase
{
    private FormDataSource formDataSource;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromTableNameAndQuery</Name>
				<Source><![CDATA[
    public static WHSSequenceNumberMoverFromQuery newFromTableNameAndQuery(FormDataSource _dataSource, FieldName _sequenceNumberFieldName)
    {
        TableName tableName = tableId2Name(_dataSource.table());

        WHSSequenceNumberMoverFromQuery mover = new WHSSequenceNumberMoverFromQuery();

        mover.sysDictTable = SysDictTable::newName(tableName);
        mover.sequenceNumberFieldName = _sequenceNumberFieldName;
        mover.formDataSource = _dataSource;

        return mover;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findAdjacentSequenceNumberRow</Name>
				<Source><![CDATA[
    protected Common findAdjacentSequenceNumberRow(Common _row, boolean _up)
    {
        Query dataSourceQuery = new Query(formDataSource.query());

        this.setSequenceNumberCompareRange(_row, dataSourceQuery, _up);
        this.addSortFieldOnQuery(dataSourceQuery, _up);

        QueryRun queryRun = new QueryRun(dataSourceQuery);

        if (queryRun.next())
        {
            Common common = queryRun.get(sysDictTable.id());
            return common;
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSequenceNumberCompareRange</Name>
				<Source><![CDATA[
    private void setSequenceNumberCompareRange(Common _row, Query _query, boolean _up)
    {
        QueryBuildRange dsRange = SysQuery::findOrCreateRange(_query.dataSourceTable(sysDictTable.id()), sysDictTable.fieldName2Id(sequenceNumberFieldName));

        anytype currentSequenceNumber = _row.getFieldValue(sequenceNumberFieldName);

        str compareExpression = _up ? '(%1.%2 < %3)' : '(%1.%2 > %3)';

        dsRange.value(strFmt(
            compareExpression,
            _query.dataSourceTable(sysDictTable.id()).name(),
            sequenceNumberFieldName,
            queryValue(currentSequenceNumber)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addSortFieldOnQuery</Name>
				<Source><![CDATA[
    private void addSortFieldOnQuery(Query _query, boolean _up)
    {
        SortOrder sortOrder = _up ? SortOrder::Descending : SortOrder::Ascending;
        _query.dataSourceTable(sysDictTable.id()).addSortField(sysDictTable.fieldName2Id(sequenceNumberFieldName), sortOrder);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>