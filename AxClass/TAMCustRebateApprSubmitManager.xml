<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMCustRebateApprSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMCustRebateApprSubmitManager</c> class manages the actions related to the submit workflow step
/// of the customer rebate workflow.
/// </summary>
public class TAMCustRebateApprSubmitManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WorkflowCorrelationId   workflowCorrelationId;
        WorkflowTypeName        workflowTypeName = workflowtypestr(MCRCustRebateAppr);
        WorkflowComment         note = "";
        WorkflowSubmitDialog    workflowSubmitDialog;
        PdsRebateAgreement      pdsRebateAgreement;
        Object                  pdsRebateAgreement_ds;

        if (_args != null && _args.record())
        {
            pdsRebateAgreement = _args.record();

            if (TAMCustRebateApprSubmitManager::validateRebateAgreement(pdsRebateAgreement))
            {
                // Opens the submit to workflow dialog.
                workflowSubmitDialog = WorkflowSubmitDialog::construct(_args.caller().getActiveWorkflowConfiguration());
                workflowSubmitDialog.run();
                if (workflowSubmitDialog.parmIsClosedOK())
                {
                    // Get comments from the submit to workflow dialog.
                    note = workflowSubmitDialog.parmWorkflowComment();
                    try
                    {
                        ttsbegin;
                        workflowCorrelationId = Workflow::activateFromWorkflowType(workflowTypeName, pdsRebateAgreement.RecId, note, NoYes::No);

                        pdsRebateAgreement_ds = FormDataUtil::getFormDataSource(pdsRebateAgreement);
                        pdsRebateAgreement.TAMCustRebateApprovalStatus = TAMCustRebateApprovalStatus::Submitted;

                        if (pdsRebateAgreement_ds)
                        {
                            pdsRebateAgreement_ds.write();
                            pdsRebateAgreement_ds.refresh();
                        }
                        // Send an Infolog message.
                        info("@MCR40275");
                        ttscommit;

                        _args.caller().updateWorkFlowControls();
                    }
                    catch(exception::Error)
                    {
                            throw error("@SYS303438");
                    }
                }
            }
        }
        else
        {
            throw error("@MCR27548");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateRebateAgreement</Name>
				<Source><![CDATA[
private static boolean validateRebateAgreement(PdsRebateAgreement _pdsRebateAgreement)
    {
        boolean ret = true;

        ret = _pdsRebateAgreement.checkIfLineExists();
        ret = ret && _pdsRebateAgreement.checkLineBreaks();
        ret = ret && _pdsRebateAgreement.checkUOMConvert();

        return ret;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>