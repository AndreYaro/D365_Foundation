<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustCustomerV3EntityRecordIdSelectorLogisticsLocation</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

[ExportMetadataAttribute(identifierStr(TableName), tableStr(LogisticsLocation))]
[ExportMetadataAttribute(identifierStr(EntityName), dataentityviewstr(CustCustomerV3Entity))]
[ExportAttribute(identifierstr(Dynamics.AX.Application.IBusinessEventsTableToEntityRecordIdSelector))]
internal final class CustCustomerV3EntityRecordIdSelectorLogisticsLocation implements IBusinessEventsTableToEntityRecordIdSelector
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEntityRecordIdsImpactedByTableChange</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public List getEntityRecordIdsImpactedByTableChange(BusinessEventsDefinition _eventDefinition, BusinessEventsActions _changeType, RecId _changedRecordId)
    {
        // Only process default address changes for types that are mapped and retun null for mappings not known.
        List impactedRecIds = null;

        if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(CustCustomerV3Entity, LogisticsPostalAddressBaseEntity))
        {
            impactedRecIds = new List(Types::Int64);

            CustTable custTable;
            DirPartyTable dirPartyTable;

            while select RecId from custTable
                exists join dirPartyTable
                    where dirPartyTable.PrimaryAddressLocation == _changedRecordId
                        && dirPartyTable.RecId == custTable.Party
            {
                impactedRecIds.addEnd(custTable.RecId);
            }
        }
        else
        {
            LogisticsLocationRoleType roleType = LogisticsLocationRoleType::None;

            if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(CustCustomerV3Entity, DeliveryLogisticsPostalAddressBaseEntity))
            {
                roleType = LogisticsLocationRoleType::Delivery;
            }
            else if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(CustCustomerV3Entity, InvoiceLogisticsPostalAddressBaseEntity))
            {
                roleType = LogisticsLocationRoleType::Invoice;
            }

            if (roleType != LogisticsLocationRoleType::None)
            {
                impactedRecIds = new List(Types::Int64);

                CustTable custTable;
                DirPartyLocation partyLocation;
                DirPartyLocationRole partyLocationRole;
                LogisticsLocationRole locationRole;

                while select RecId from custTable
                    exists join partyLocation
                        where partyLocation.Location == _changedRecordId
                            && partyLocation.Party == custTable.Party
                    exists join partyLocationRole
                        where partyLocationRole.PartyLocation == partyLocation.RecId
                    exists join locationRole
                        where locationRole.RecId == partyLocationRole.LocationRole
                            && locationRole.Type == roleType
                {
                    impactedRecIds.addEnd(custTable.RecId);
                }
            }
        }

        return impactedRecIds;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>