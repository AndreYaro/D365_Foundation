<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustFreeInvoiceCancel_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustFreeInvoiceCancel_BR</c> class cancels the free text invoice.
/// </summary>
class CustFreeInvoiceCancel_BR extends CustFreeInvoiceCorrection
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createCancelInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a cancelling free text invoice.
    /// </summary>
    /// <param name="_reasonCodeRecId">
    ///    The record ID of the reason code.
    /// </param>
    /// <param name="_adjustingInvoiceDate">
    ///    The date of the adjusting invoice.
    /// </param>
    /// <param name="_invoiceDate">
    ///    The date of the invoice.
    /// </param>
    /// <returns>
    ///    The record ID of the cancelling free text invoice that is created.
    /// </returns>
    public CustInvoiceTableRefRecId createCancelInvoice(RefRecId _reasonCodeRecId, TransDate _adjustingInvoiceDate, TransDate _invoiceDate)
    {
        if (this.validate(_reasonCodeRecId, _adjustingInvoiceDate, _invoiceDate))
        {
            invoiceType = InvoiceAssociationType::AdjustingInvoice;
            childReasonCodeRecId = _reasonCodeRecId;
            parentAdjustingInvoiceDate = _adjustingInvoiceDate;

            childCustInvoiceTableRecId = this.createInvoice();
            this.updateChildCustInvoiceTable();
        }

        return childCustInvoiceTableRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateChildCustInvoiceTable</Name>
				<Source><![CDATA[
        private void updateChildCustInvoiceTable()
    {
        CustInvoiceTable custInvoiceTable;

        ttsBegin;
        select forUpdate custInvoiceTable
            where custInvoiceTable.RecId == childCustInvoiceTableRecID;

        custInvoiceTable.FiscalDocumentType_BR = 0;
        custInvoiceTable.update();
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
        protected boolean validate(RefRecId _reasonCodeRecId, TransDate _adjustingInvoiceDate, TransDate _invoiceDate)
    {
        CustInvoiceLine         custInvoiceLineParent;
        CustInvoiceTable        custInvoiceTableParent;
        boolean ret;
        FiscalDocument_BR       fiscalDocument;

        ret = super(_reasonCodeRecId, _adjustingInvoiceDate, _invoiceDate);

        select firstOnly custInvoiceTableParent
            where custInvoiceTableParent.RecId == custParentInvoiceRecId;

        if (ret)
        {
            fiscalDocument = FiscalDocument_BR::findByRef(tableNum(CustInvoiceJour), custInvoiceTableParent.custInvoiceJour().RecId);
            ret = EFDocUserInterfaceUtil_BR::constructFromFiscalDocument(fiscalDocument).checkReversal();
        }

        select firstOnly recId from custInvoiceLineParent
            where custInvoiceLineParent.ParentRecId == custParentInvoiceRecId
                && custInvoiceLineParent.AssetBookId != ''
                && custInvoiceLineParent.AssetId != '';

        if (custInvoiceLineParent)
        {
            if (Box::yesNo("@GLS63958",DialogButton::No, "@GLS63948") == DialogButton::No)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCancelInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates an instance of the <c>CustFreeInvoiceCancel_BR</c> class.
    /// </summary>
    /// <param name="_custParentInvoiceRecId">
    ///    The record ID of the parent invoice.
    /// </param>
    /// <returns>
    ///    An instance of the <c>CustFreeInvoiceCancel_BR</c> class.
    /// </returns>
    public static CustFreeInvoiceCancel_BR newCancelInvoice (CustInvoiceTableRefRecId _custParentInvoiceRecId)
    {
        CustFreeInvoiceCancel_BR custFreeInvoice;

        custFreeInvoice = new CustFreeInvoiceCancel_BR();
        custFreeInvoice.init(_custParentInvoiceRecId);
        return custFreeInvoice;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>