<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PlanActivityNextLevelCache</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PlanActivityNextLevelCache</c> class provides caching of next level plan activities.
/// </summary>
public class PlanActivityNextLevelCache extends LeanCache
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds values in the cache that is stored under the specified key.
    /// </summary>
    /// <param name="_key">
    /// The cache key.
    /// </param>
    /// <returns>
    /// The <c>PlanActivity</c> record that represents the next level activity; otherwise, an empty buffer.
    /// </returns>
    public anytype find(anytype _key)
    {
        PlanActivity    nextLevelPlanActivity;
        container       value;

        // retrieve value as container
        value = super(_key);

        if (   value
            && !this.findIsNullValue())
        {
            // extract buffer
            nextLevelPlanActivity.con2buf(value);
        }

        return nextLevelPlanActivity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>key</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds a cache key by using a <c>PlanActivity</c> record ID and a <c>PlanActivityType</c>
    /// enumeration value.
    /// </summary>
    /// <param name="_planActivityRefRecId">
    /// A record ID for the <c>PlanActivity</c> table.
    /// </param>
    /// <param name="_planActivityType">
    /// A <c>PlanActivityType</c> enumeration value.
    /// </param>
    /// <returns>
    /// A <c>container</c> object that can be used as a key to the cache.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public container key(
        PlanActivityRefRecId _planActivityRefRecId,
        PlanActivityType     _planActivityType)
    {
        return [_planActivityRefRecId,_planActivityType];
    }

]]></Source>
			</Method>
			<Method>
				<Name>nextLevelPlanActivity</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds the next level <c>PlanActivity</c> record of the specified type for the current record.
    /// </summary>
    /// <param name="_planActivityRefRecId">
    ///    The record ID of a record in the <c>PlanActivity</c> table.
    /// </param>
    /// <param name="_planActivityType">
    ///    A <c>PlanActivityType</c> enumeration value.
    /// </param>
    /// <param name="_planActivity">
    ///    The <c>PlanActivity</c> record that belongs to the specified record ID; optional.
    /// </param>
    /// <returns>
    ///    A <c>PlanActivity</c> record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public PlanActivity nextLevelPlanActivity(
        PlanActivityRefRecId _planActivityRefRecId,
        PlanActivityType     _planActivityType,
        PlanActivity         _planActivity = null)
    {
        PlanActivity    planActivity;
        PlanActivity    nextLevelPlanActivity;
        container       key = this.key(_planActivityRefRecId,_planActivityType);

        if (_planActivityRefRecId)
        {
            nextLevelPlanActivity = this.find(key);

            if (!this.findIsCacheHit())
            {
                planActivity          = _planActivity ? _planActivity : PlanActivity::find(_planActivityRefRecId);
                nextLevelPlanActivity = planActivity.nextLevelPlanActivity(_planActivityType,false);

                if (nextLevelPlanActivity)
                {
                    this.insert(key,nextLevelPlanActivity.buf2con());
                }
                else
                {
                    this.insert(key,this.nullValue());
                }
            }
        }

        return nextLevelPlanActivity;
    }

]]></Source>
			</Method>
			<Method>
				<Name>remove</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes an entry from the cache that is stored under the specified key.
    /// </summary>
    /// <param name="_key">
    /// The key to remove.
    /// </param>
    public void remove(anytype _key)
    {
        PlanActivityRefRecId    planActivityRefRecId;
        PlanActivityType        planActivityType;
        Common                  common;
        SysDictEnum             dictEnum;
        boolean                 removeAllActivityTypes = true;
        int                     i;

        switch (typeof(_key))
        {
            case Types::Container:
                if (conlen(_key) == 2)
                {
                    // completely specified key, just call super()
                    super(_key);
                    removeAllActivityTypes = false;
                }
                break;
            case Types::Record:
                // get recId from record
                common = _key;
                if (common is PlanActivity)
                {
                    planActivityRefRecId = common.RecId;
                }
                break;
            case Types::Int64:
                // get recId from key
                planActivityRefRecId = _key;
                break;
        }

        if (   removeAllActivityTypes
            && planActivityRefRecId)
        {
            // loop all enum values and remove each
            dictEnum         = new SysDictEnum(enumnum(PlanActivityType));
            planActivityType = dictEnum.firstValue();

            for (i = 1; i <= dictEnum.values(); i++)
            {
                super(this.key(planActivityRefRecId,planActivityType));
                planActivityType = dictEnum.nextValue(planActivityType);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public static PlanActivityNextLevelCache construct()
    {
        return new PlanActivityNextLevelCache();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>