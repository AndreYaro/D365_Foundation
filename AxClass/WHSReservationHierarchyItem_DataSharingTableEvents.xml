<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSReservationHierarchyItem_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>WHSReservationHierarchyItem_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class WHSReservationHierarchyItem_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static WHSReservationHierarchyItem_DataSharingTableEvents construct()
    {
        return new WHSReservationHierarchyItem_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = WHSReservationHierarchyItem_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Inserted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                WHSReservationHierarchyItem origReservationHierarchyItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;
                WHSReservationHierarchyItem reservationHierarchyItem;
                buf2Buf(origReservationHierarchyItem, reservationHierarchyItem);
                reservationHierarchyItem.ItemDataAreaId = _dataSharingCompany;
                if (!reservationHierarchyItem.validateWrite())
                {
                    throw error(strFmt("@SCM:SysDataSharingCannotInsertRecordInTable", tablePName(WHSReservationHierarchyItem), _dataSharingCompany));
                }
                reservationHierarchyItem.insert();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Deleted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                WHSReservationHierarchyItem origReservationHierarchyItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                WHSReservationHierarchyItem reservationHierarchyItem = WHSReservationHierarchyItem::findByItem(_dataSharingCompany, origReservationHierarchyItem.ItemId, true);
                if (reservationHierarchyItem.RecId)
                {
                    if (!reservationHierarchyItem.validateDelete())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotDeleteRecordInTable", tablePName(WHSReservationHierarchyItem), _dataSharingCompany));
                    }
                    reservationHierarchyItem.delete();                       
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                WHSReservationHierarchyItem origReservationHierarchyItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                WHSReservationHierarchyItem reservationHierarchyItem = WHSReservationHierarchyItem::findByItem(_dataSharingCompany, origReservationHierarchyItem.ItemId, true);
                if (reservationHierarchyItem.RecId)
                {
                    buf2Buf(origReservationHierarchyItem, reservationHierarchyItem);
                    reservationHierarchyItem.ItemDataAreaId = _dataSharingCompany;
                    if (!reservationHierarchyItem.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotUpdateRecordInTable", tablePName(WHSReservationHierarchyItem), _dataSharingCompany));
                    }
                    reservationHierarchyItem.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>WHSReservationHierarchyItem_onInserted</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of WHSReservationHierarchyItem table 
    [DataEventHandler(tableStr(WHSReservationHierarchyItem), DataEventType::Inserted)]
    public static void WHSReservationHierarchyItem_onInserted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Inserted);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorReservationHierarchyItem = WHSReservationHierarchyItem_DataSharingTableEvents::construct();
                tableEventsProcessorReservationHierarchyItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorReservationHierarchyItem);                
                using (var contextInsertCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(WHSReservationHierarchyItem), DataEventType::Inserted))
                {
                    processor.insertCrossCompany(false);
                }
            }        
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>WHSReservationHierarchyItem_onDeleted</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(WHSReservationHierarchyItem), DataEventType::Deleted)]
    public static void WHSReservationHierarchyItem_onDeleted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Deleted);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorReservationHierarchyItem = new WHSReservationHierarchyItem_DataSharingTableEvents();
                tableEventsProcessorReservationHierarchyItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorReservationHierarchyItem);
                using (var contextDeleteCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(WHSReservationHierarchyItem), DataEventType::Deleted))
                {
                    processor.deleteCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>WHSReservationHierarchyItem_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(WHSReservationHierarchyItem), DataEventType::Updated)]
    public static void WHSReservationHierarchyItem_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(WHSReservationHierarchyItem), DataEventType::Updated);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorReservationHierarchyItem = new WHSReservationHierarchyItem_DataSharingTableEvents();
                tableEventsProcessorReservationHierarchyItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorReservationHierarchyItem);
                using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(WHSReservationHierarchyItem), DataEventType::Updated))
                {
                    processor.updateCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>