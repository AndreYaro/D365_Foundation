<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalMultiPostDemoDataPost_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extends the <c>LedgerJournalMultiPost</c> class functionality for the demo data automatic posting feature.
/// </summary>
[ExtensionOf(classStr(LedgerJournalMultiPost))]
public final class LedgerJournalMultiPostDemoDataPost_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runSilent</Name>
				<Source><![CDATA[
    /// <summary>
    /// Extend ledger functionality to suppress UI interaction for multiple postings
    /// when the caller is part of the demo data post functionality.
    /// </summary>
    /// <param name = "_ledgerJournalMultiPost">
    /// An instance of the <c>LedgerJournalMultiPost</c> class.
    /// </param>
    /// <param name="_caller">
    /// The object that calls the method.
    /// </param>
    /// <param name="_ledgerJournalTableSortedList">
    /// A <c>RecordSortedList</c> of <c>LedgerJournalTable</c> records.
    /// </param>
    /// <param name="_transferErrors">
    /// A <c>NoYes</c> enumeration value that controls the transfer of posting errors.
    /// </param>
    /// <returns>
    /// True if the posting should be done without prompting the user; otherwise, false.
    /// </returns>
    protected static boolean runSilent(LedgerJournalMultiPost _ledgerJournalMultiPost, Object _caller, RecordSortedList _ledgerJournalTableSortedList, NoYes _transferErrors)
    {
        boolean shouldRunSilent = next runSilent(_ledgerJournalMultiPost, _caller, _ledgerJournalTableSortedList, _transferErrors);

        if (_caller is DemoDataPostBase)
        {
            shouldRunSilent = true;
        }

        return shouldRunSilent;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>