<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PaymCalendarSourceVendInvoiceJournal</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PaymCalendarSourceVendInvoiceJournal</c> class provides an interface that is used to find
/// payment calendars when the source is the <c>VendInvoiceJour</c> table.
/// </summary>
public class PaymCalendarSourceVendInvoiceJournal implements PaymCalendarIDerivationSource, PaymCalendarIBankAddress
{
    private VendInvoiceJour vendInvoiceJour;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getBankPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the postal address associated to the bank account.
    /// </summary>
    /// <returns>
    /// The <c>LogisticsPostalAddress</c> record that represents the postal address.
    /// </returns>
    public LogisticsPostalAddress getBankPostalAddress()
    {
        VendTable vendTable;
        VendBankAccount vendBankAccount;

        vendTable = VendTable::findByCompany(vendInvoiceJour.DataAreaId, vendInvoiceJour.InvoiceAccount);
        vendBankAccount = VendBankAccount::findByCompany(vendTable.DataAreaId, vendTable.AccountNum, vendTable.BankAccount);

        return LogisticsLocationEntity::location2PostalAddress(vendBankAccount.Location);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrimaryPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the postal address associated to the third-party entity.
    /// </summary>
    /// <returns>
    /// The <c>LogisticsPostalAddress</c> record that represents the postal address.
    /// </returns>
    public LogisticsPostalAddress getPrimaryPostalAddress()
    {
        VendTable vendTable;

        vendTable = VendTable::findByCompany(vendInvoiceJour.DataAreaId, vendInvoiceJour.InvoiceAccount);

        return vendTable.postalAddress();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the payment calendar derivation source instance for the specified record.
    /// </summary>
    /// <param name="_vendInvoiceJour">
    /// A populated <c>VendInvoiceJour</c> table buffer that specifies the initialization data.
    /// </param>
    public void initialize(VendInvoiceJour _vendInvoiceJour)
    {
        vendInvoiceJour = _vendInvoiceJour;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmModuleType</Name>
				<Source><![CDATA[
    public PaymCalendarModuleType parmModuleType()
    {
        return PaymCalendarModuleType::Vendor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymMode</Name>
				<Source><![CDATA[
    public PaymMode parmPaymMode()
    {
        return vendInvoiceJour.purchTable().PaymMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymTermDataAreaId</Name>
				<Source><![CDATA[
    public DataAreaId parmPaymTermDataAreaId()
    {
        return vendInvoiceJour.DataAreaId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPaymTermId</Name>
				<Source><![CDATA[
    public PaymTermId parmPaymTermId()
    {
        return vendInvoiceJour.purchTable().Payment;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves an instance of the <c>PaymCalendarSourceVendInvoiceJournal</c> class.
    /// </summary>
    /// <returns>
    /// A new instance of the <c>PaymCalendarSourceVendInvoiceJournal</c> class.
    /// </returns>
    private static PaymCalendarSourceVendInvoiceJournal construct()
    {
        return new PaymCalendarSourceVendInvoiceJournal();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAdjustedDueDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the adjusted due date so that it falls on a business day per calendar rules.
    /// </summary>
    /// <param name="_dueDate">
    /// The original due date.
    /// </param>
    /// <param name="_updatePolicy">
    /// A value that specifies the search direction when seeking the next valid business day.
    /// </param>
    /// <param name="_vendInvoiceJour">
    /// The vendor invoice journal table buffer that specifies the calendar source.
    /// </param>
    /// <returns>
    /// An instance of <c>PaymCalendarAdjustedDate</c> which contains the adjusted due date together with
    /// the calendar and rule that was applied when adjusting the date.
    /// </returns>
    /// <remarks>
    /// If no adjustment is needed, the original date is returned. An empty record ID is returned for the
    /// payment calendar and calendar rule whenever a calendar is not found.
    /// </remarks>
    public static PaymCalendarAdjustedDate getAdjustedDueDate(DueDate _dueDate, PaymentDueDateUpdatePolicy _updatePolicy, VendInvoiceJour _vendInvoiceJour)
    {
        return PaymCalendarSourceVendInvoiceJournal::getAdjustedDueDatePrivate(_dueDate, _updatePolicy, _vendInvoiceJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAdjustedDueDatePrivate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the adjusted due date so that it falls on a business day per calendar rules.
    /// </summary>
    /// <param name="_dueDate">
    /// The original due date.
    /// </param>
    /// <param name="_updatePolicy">
    /// A value that specifies the search direction when seeking the next valid business day.
    /// </param>
    /// <param name="_vendInvoiceJour">
    /// The vendor invoice journal table buffer that specifies the calendar source.
    /// </param>
    /// <returns>
    /// An instance of <c>PaymCalendarAdjustedDate</c> which contains the adjusted due date together with
    /// the calendar and rule that was applied when adjusting the date.
    /// </returns>
    public static PaymCalendarAdjustedDate getAdjustedDueDatePrivate(DueDate _dueDate, PaymentDueDateUpdatePolicy _updatePolicy, VendInvoiceJour _vendInvoiceJour)
    {
        PaymCalendarSourceVendInvoiceJournal calendarSource;
        PaymCalendarProcessor paymCalendarProcessor;
        PaymCalendarAdjustedDate paymCalendarAdjustedDate;

        paymCalendarProcessor = PaymCalendarProcessor::construct();
        calendarSource = PaymCalendarSourceVendInvoiceJournal::newCalendarSource(_vendInvoiceJour);
        paymCalendarAdjustedDate = paymCalendarProcessor.getAdjustedDate(_dueDate, _updatePolicy, calendarSource);

        return paymCalendarAdjustedDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCalendarSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves an instance of the <c>PaymCalendarSourceVendInvoiceJournal</c> class.
    /// </summary>
    /// <param name="_vendInvoiceJour">
    /// The <c>VendInvoiceJour</c> record to load.
    /// </param>
    /// <returns>
    /// A new instance of the <c>PaymCalendarSourceVendInvoiceJournal</c> class.
    /// </returns>
    public static PaymCalendarSourceVendInvoiceJournal newCalendarSource(VendInvoiceJour _vendInvoiceJour)
    {
        PaymCalendarSourceVendInvoiceJournal paymCalendarSourceVendInvoiceJournal;

        paymCalendarSourceVendInvoiceJournal = PaymCalendarSourceVendInvoiceJournal::construct();
        paymCalendarSourceVendInvoiceJournal.initialize(_vendInvoiceJour);

        return paymCalendarSourceVendInvoiceJournal;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>