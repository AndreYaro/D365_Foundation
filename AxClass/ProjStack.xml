<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjStack</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProjStack</c> class is a stack collection.
/// </summary>
/// <remarks>
/// The <c>ProjStack</c> class supports the storage of any type of object, whereas
/// the <c>Stack</c> class only supports elements that can be added to a <c>container</c>.
/// </remarks>
class ProjStack
{
    Types type;
    List list;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isEmpty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the stack is empty.
    /// </summary>
    /// <returns>
    /// true if the stack is empty; otherwise, false.
    /// </returns>
    public boolean isEmpty()
    {
        return list.empty();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets up a new stack.
    /// </summary>
    /// <param name="_type">
    /// The type of object this stack will hold.
    /// </param>
    public void new(Types _type)
    {
        type = _type;
        list = new list(_type);
    }

]]></Source>
			</Method>
			<Method>
				<Name>peek</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the next item in the stack.
    /// </summary>
    /// <returns>
    /// The next item in the stack.
    /// </returns>
    public anytype peek()
    {
        ListEnumerator enumerator = list.getEnumerator();
        if (enumerator.moveNext())
        {
            return enumerator.current();
        }
        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pop</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retreives the next item and removes it from the stack.
    /// </summary>
    /// <returns>
    /// The top item of the stack.
    /// </returns>
    public anytype pop()
    {
        ListIterator listIterator = new ListIterator(list);
        anytype element = listIterator.value();
        listIterator.delete();
        return element;
    }

]]></Source>
			</Method>
			<Method>
				<Name>push</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds an item onto the stack.
    /// </summary>
    /// <param name="element">
    /// The element to put on the stack.
    /// </param>
    public void push(anytype element)
    {
        list.addStart(element);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>