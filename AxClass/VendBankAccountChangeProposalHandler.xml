<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendBankAccountChangeProposalHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class VendBankAccountChangeProposalHandler
{
    private boolean showedWarningNoChangeControlOnCreation;
    private FormDataSource VendBankAccount_ds;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static VendBankAccountChangeProposalHandler construct(FormRun _formRun)
    {
        if (_formRun.name() != formStr(VendBankAccounts) 
            && _formRun.name() != formStr(VendBankInformationSliderForm))
        {
            throw error("@SYS22828", funcname());
        }

        VendBankAccountChangeProposalHandler instance = new VendBankAccountChangeProposalHandler();
        instance.VendBankAccount_ds = _formRun.dataSource(tableStr(VendBankAccount));

        return instance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedControlledField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles a change in a change-controlled field.
    /// </summary>
    /// <param name = "_vendBankAccountField">The number of the changed field in <c>VendBankAccount</c>.</param>
    /// <param name = "_changeProposalField">The number of the changed field in <c>VendBankAccountChangeProposal</c>.</param>
    /// <param name = "_vendBankAccount">The pointer to a buffer to manipulate as a result of the change.</param>
    /// <param name = "_controlledField">The enumeration representing the changed field. Used to check if change control is enabled on the field.</param>
    /// <returns>True if changes have been reverted due to change control, otherwise false.</returns>
    [Hookable(false)]
    public boolean modifiedControlledField(FieldId _vendBankAccountField, FieldId _changeProposalField, VendBankAccount _vendBankAccount, VendBankAccountChangeProposalFields _controlledField)
    {
        boolean ret;

        if (!VendBankAccountChangeProposalFeature::instance().isEnabled())
        {
            return ret;
        }

        VendBankAccount originalTable = _vendBankAccount.orig();

        if (originalTable.(_vendBankAccountField) != _vendBankAccount.(_vendBankAccountField)
                && VendBankAccountChangeProposalFieldEnablement::isEnabledFor(_controlledField))
        {
            if(!_vendBankAccount)
            {
                this.showWarningNoChangeControlOnCreation();
            }
            else
            {
                if (VendBankAccountChangeProposalSaveWithoutValidationFlight::instance().isEnabled())
                {
                    if (this.checkShowChangeProposal(_vendBankAccount))
                    {
                        _vendBankAccount.modifyChangeProposalRecord(_changeProposalField, _vendBankAccountField, _controlledField);
                        
                        if (VendBankAccount_ds.validateWrite())
                        {
                            VendBankAccount_ds.write();
                            this.showChangeProposalDialog(_vendBankAccount);
                        }
                    }
                }
                else
                {
                    if (VendBankAccount_ds.validateWrite() &&
                        this.checkShowChangeProposal(_vendBankAccount))
                    {
                        _vendBankAccount.modifyChangeProposalRecord(_changeProposalField, _vendBankAccountField, _controlledField);
                        VendBankAccount_ds.write();
                    
                        this.showChangeProposalDialog(_vendBankAccount);
                    }
                }

                ret = true;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showWarningNoChangeControlOnCreation</Name>
				<Source><![CDATA[
    private void showWarningNoChangeControlOnCreation()
    {
        if (!showedWarningNoChangeControlOnCreation)
        {
            showedWarningNoChangeControlOnCreation = true;
            info("@AccountsPayable:VendorBankAccountApproveNotActive");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkShowChangeProposal</Name>
				<Source><![CDATA[
    private boolean checkShowChangeProposal(VendBankAccount _bankAccount)
    {
        return _bankAccount.WorkflowState == VendBankAccountChangeProposalWorkflowState::Submitted
            || ! _bankAccount.isDraft();
    }

]]></Source>
			</Method>
			<Method>
				<Name>showChangeProposalDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Shows the dialog listing changes to change-controlled fields.
    /// </summary>
    private void showChangeProposalDialog(VendBankAccount _bankAccount)
    {
        Args args = new Args();
        args.menuItemName(menuItemDisplayStr(VendBankAccountChangeProposal));
        args.menuItemType(MenuItemType::Display);
        args.record(_bankAccount);
        args.parm("@AccountsPayable:NotificationChangesRequireApproval");

        new MenuFunction(menuItemDisplayStr(VendBankAccountChangeProposal), MenuItemType::Display).run(args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>