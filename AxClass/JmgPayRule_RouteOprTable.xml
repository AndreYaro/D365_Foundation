<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayRule_RouteOprTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>JmgPayRule_RouteOprTable</c> class represents the operations pay rule.
/// </summary>
/// <remarks>
///    This class lets users delimit the pay agreement lines by the operations pay rule.
/// </remarks>
class JmgPayRule_RouteOprTable extends JmgPayRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the description of the operations pay rule.
    /// </summary>
    /// <returns>
    ///    The description of the operations pay rule.
    /// </returns>
    public JmgDescription description()
    {
        return "@SYS8986";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDesignTimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the design time data source for the operations pay rule.
    /// </summary>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the design time data source.
    /// </returns>
    /// <remarks>
    ///    The operations pay rule adds a <c>RouteOprId</c> field to the custom design data source.
    /// </remarks>
    protected ExpressionDataSource getDesignTimeCustomDataSource()
    {
        ExpressionDataSource ret;

        ret = super();
        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(RouteOprId));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJoinConditions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves an expression with conditions for the Operations pay rule.
    /// </summary>
    /// <returns>
    ///    An expression with conditions for the Operations pay rule.
    /// </returns>
    /// <remarks>
    ///    The Operations pay rule adds one condition: the <c>RouteOprId</c> field equals the
    ///    <c>Query.OprId</c> value.
    /// </remarks>
    public Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement getJoinConditions()
    {
        Query q = this.getQuery();

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionProjection designTimeProjection =
            SysExpression::getDesignTimeProjectionModel(classstr(JmgPayRule_RouteOprTable), this.getDesignTimeCustomDataSources());

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement expressionDefinition =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement::Create();

        Microsoft.Dynamics.AX.Framework.Expression.Model.Field oprField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionOprField;

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement andElement =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement::Create();

        oprField = designTimeProjection.GetField(q.dataSourceTable(tablenum(RouteOprTable)).name(), fieldstr(RouteOprTable, OprId));
        transactionOprField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(RouteOprId));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldStringElement::Create(
                oprField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.StringOperator::Equals,
                transactionOprField));

        expressionDefinition.AddAndElement(andElement);

        return expressionDefinition;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the name of the <c>JmgPayRuleRouteOprTable</c> query.
    /// </summary>
    /// <returns>
    ///    The name of the <c>JmgPayRuleRouteOprTable</c> query.
    /// </returns>
    queryName getQueryName()
    {
        return querystr(JmgPayRuleRouteOprTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRuntimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the runtime custom time data source for the operations pay rule.
    /// </summary>
    /// <param name="_jmgStampJournalTrans">
    ///    The transaction to use to initialize the values of the runtime fields.
    /// </param>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the run time data source for the operations pay
    ///    rule.
    /// </returns>
    /// <remarks>
    ///    Adds one run time field: the <c>RouteOprId</c> field that is initialized from the <c>ActOprID</c>
    ///    field of the transaction.
    /// </remarks>
    protected ExpressionDataSource getRuntimeCustomDataSource(JmgStampJournalTrans _jmgStampJournalTrans)
    {
        ExpressionDataSource expressionDataSource;

        expressionDataSource = super(_jmgStampJournalTrans);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(RouteOprId), _jmgStampJournalTrans.ActOprID);
        return expressionDataSource;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>