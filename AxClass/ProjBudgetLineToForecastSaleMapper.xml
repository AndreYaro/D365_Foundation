<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjBudgetLineToForecastSaleMapper</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Xrm.Sdk;

internal class ProjBudgetLineToForecastSaleMapper implements ProjBudgetLineEntityTableMapper
{
    private ForecastSales forecastSale;
    private Entity entity;
    private const str DEFAULT_SALE_CATEGORY = 'Default-Item';
    private static ProjOpsInstrumentationLogger logger = ProjOpsInstrumentationLogger::createLogger(classStr(ProjBudgetLineToForecastSaleMapper));

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Entity _entity)
    {
        if ( !_entity || !_entity.Attributes )
        {
            logger.logError(strFmt("@Proj:ProjBudgetLoggerEmptyEntityPassed", any2Str(_entity)));
            throw error(strFmt("@Proj:ProjBudgetEmptyEntityPassed"));
        }
        this.entity = _entity;
        this.mapEntityToTable();
    }

]]></Source>
			</Method>
			<Method>
				<Name>mapEntityToTable</Name>
				<Source><![CDATA[
    private void mapEntityToTable()
    {
        AttributeCollection attributes = entity.Attributes;
        forecastSale.ProjId = attributes.ContainsKey(ProjBudgetDataverseConstants::PROJECTNUMBER)
                                    ? attributes.get_Item(ProjBudgetDataverseConstants::PROJECTNUMBER) : '';

        this.updateQuantityAndCostPrice(forecastSale, attributes);
        
        AliasedValue startDateTime = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::STARTTIME);
        forecastSale.StartDate = startDateTime ? startDateTime.Value : ProjBudgetDataverseConstants::DEFAULT_DATETIME;
        forecastSale.ProjForecastCostPaymDate = forecastSale.StartDate;
        forecastSale.ProjForecastInvoiceDate = forecastSale.StartDate;

        AliasedValue endDateTime = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::ENDTIME);
        forecastSale.EndDate = endDateTime ? endDateTime.Value : ProjBudgetDataverseConstants::DEFAULT_DATETIME;

        AliasedValue description = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::DESCRIPTION);
        forecastSale.Comment = description ? description.Value : '';

        AliasedValue productDescription = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(attributes, ProjBudgetDataverseConstants::PRODUCT_DESCRIPTION);
        forecastSale.ItemDescription = productDescription ? productDescription.Value : '';

        EntityReference currencyId = attributes.ContainsKey(ProjBudgetDataverseConstants::CURRENCYID)
                                                        ? attributes.get_Item(ProjBudgetDataverseConstants::CURRENCYID): null;
        forecastSale.Currency = currencyId ? currencyId.Name : '';

        forecastSale.ModelId = ProjBudgetLineEntityCollectionWriter::getBudgetForecastModel().ModelId;

        AliasedValue activity = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::ACTIVITY_NUMBER, ProjBudgetDataverseConstants::TASK_ENTITY);
        forecastSale.ActivityNumber = activity ? activity.Value : '';
        
        ProjCategory defaultProjectCategory = ProjCategory::find(DEFAULT_SALE_CATEGORY);
        AliasedValue category = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::TRANSACTION_NUMBER, ProjBudgetDataverseConstants::TRANSACTION_CATEGORY_ENTITY);
        forecastSale.ProjCategoryId = category ? category.Value : defaultProjectCategory.CategoryId;

        AliasedValue item = ProjBudgetLineToForecastMapperUtil::getItem(attributes, ProjBudgetDataverseConstants::PRODUCT_NUMBER, ProjBudgetDataverseConstants::PRODUCT_ENTITY);
        forecastSale.ItemId = item ? item.Value : '';

        EntityReference contract = attributes.ContainsKey(ProjBudgetDataverseConstants::CONTRACTLINEID)
                                                        ? attributes.get_Item(ProjBudgetDataverseConstants::CONTRACTLINEID) : null;
        forecastSale.ContractLineId = contract ? contract.Name : '';
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQuantityAndCostPrice</Name>
				<Source><![CDATA[
    /// <summary>
    /// updating the quantity and cost price to support the case where quantity is 0 and amount is not
    /// </summary>
    /// <param name="_forecastSale">The ProjForecastSales object to be updated.</param>
    /// <param name="_attributes">Collection of attributes used to fetch the budget line item amount.</param>
    public void updateQuantityAndCostPrice(ForecastSales _forecastSale, AttributeCollection _attributes)
    {
        AliasedValue qty = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::QUANTITY);
        _forecastSale.SalesQty = qty ? qty.Value : 0;

        AliasedValue module = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::MODULE);
        OptionSetValue moduleOptionSet = module ? module.Value : null;
        int budgetType = moduleOptionSet ? moduleOptionSet.Value : 0;

        if(_forecastSale.SalesQty == 0)
        {
            AliasedValue amount = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::AMOUNT);
            Microsoft.Xrm.Sdk.Money money = amount ? amount.Value : null;
            _forecastSale.CostPrice = money ? money.Value : 0;
            if (budgetType == ProjBudgetDataverseConstants::MODULE_COST)
            {
                _forecastSale.CostPrice = money ? money.Value : 0;
            }
            else if (budgetType == ProjBudgetDataverseConstants::MODULE_SALES)
            {
                _forecastSale.SalesPrice = money ? money.Value : 0;
            }
            _forecastSale.SalesQty = ( _forecastSale.CostPrice || _forecastSale.SalesPrice ) ? 1 : 0;

        }
        else
        {
            AliasedValue amount = ProjBudgetLineToForecastMapperUtil::getBudgetLineItem(_attributes, ProjBudgetDataverseConstants::PRICE);
            Microsoft.Xrm.Sdk.Money money = amount ? amount.Value : null;
            if (budgetType == ProjBudgetDataverseConstants::MODULE_COST)
            {
                _forecastSale.CostPrice = money ? money.Value : 0;
            }
            else if (budgetType == ProjBudgetDataverseConstants::MODULE_SALES)
            {
                _forecastSale.SalesPrice = money ? money.Value : 0;
            }
        }
        _forecastSale.PriceUnit = _forecastSale.CostPrice;
        _forecastSale.InventQty = _forecastSale.SalesQty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>writeTableToDatabase</Name>
				<Source><![CDATA[
    /// <summary>
    /// Writes mapped forecastSales to Database
    /// </summary>
    public void writeTableToDatabase()
    {
        forecastSale.selectForUpdate(true);
        forecastSale.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionClassification</Name>
				<Source><![CDATA[
    public ProjTransType getTransactionClassification()
    {
        return ProjTransType::Item;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTable</Name>
				<Source><![CDATA[
    public Common getTable()
    {
        return forecastSale;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>