<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntrastatAmountCalcInvoiceAmount</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Calculates Intrastat amounts using <c>IntrastatAmountCalcMethod::InvoiceAmount</c> calculation method.
/// </summary>
[IntrastatAmountCalcAttribute(IntrastatAmountCalcMethod::InvoiceAmount)]
class IntrastatAmountCalcInvoiceAmount extends IntrastatAmountCalc
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calcInvoiceAccountingAmount</Name>
				<Source><![CDATA[
    protected AmountMST calcInvoiceAccountingAmount(CustVendInvoiceJour _headerSource, CustVendInvoiceTrans _lineSource)
    {
        AmountMST amount;

        if (calcByExchType)
        {
            amount = this.calcByExchType(_lineSource.LineAmount, _lineSource);
        }
        else
        {
            if (_headerSource.TableId == tableNum(ProjInvoiceJour))
            {
                amount = _headerSource.amountMST(_lineSource.LineAmount);
            }
            else
            {
                amount = _lineSource.LineAmountMST;
            }
        }

        amount = this.applyTotalDiscount(amount, _headerSource);

        return amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcInvoiceTransactionAmount</Name>
				<Source><![CDATA[
    protected AmountCur calcInvoiceTransactionAmount(CustVendInvoiceJour _headerSource, CustVendInvoiceTrans _lineSource)
    {
        AmountCur amount = _lineSource.LineAmount;

        amount = this.applyTotalDiscount(amount, _headerSource);

        return amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcPackingSlipAccountingAmount</Name>
				<Source><![CDATA[
    protected AmountMST calcPackingSlipAccountingAmount(CustVendInvoiceJour _headerSource, CustVendInvoiceTrans _lineSource)
    {
        AmountMST amount;

        if (calcByExchType)
        {
            amount = this.calcByExchType(_lineSource.LineAmount, _lineSource);
        }
        else
        {
            amount = _lineSource.LineAmountMST;
        }

        return amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcPackingSlipTransactionAmount</Name>
				<Source><![CDATA[
    protected AmountCur calcPackingSlipTransactionAmount(CustVendInvoiceJour _headerSource, CustVendInvoiceTrans _lineSource)
    {
        AmountCur amount;

        if (isLegalEntityInEEU)
        {
            amount = _lineSource.LineAmount;
        }
        else if (transactionCurrency)
        {
            amount = CurrencyExchangeHelper::curAmount(_lineSource.LineAmountMST, transactionCurrency, _headerSource.InvoiceDate);
        }

        return amount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyTotalDiscount</Name>
				<Source><![CDATA[
    private Amount applyTotalDiscount(Amount _amount, CustVendInvoiceJour _headerSource)
    {
        real        totalDiscountFactor;
        container   cacheKey, cachedDiscountFactorValue;
        Amount      amount = _amount;

        if (amount)
        {
            cacheKey = [_headerSource.TableId, _headerSource.RecId, funcName()];
            cachedDiscountFactorValue = this.getCachedValue(cacheKey);

            if (cachedDiscountFactorValue == conNull())
            {
                totalDiscountFactor = _headerSource.SalesOrderbalance ? 1 - _headerSource.EndDisc / _headerSource.SalesOrderbalance : 1;

                this.setCachedValue(cacheKey, [totalDiscountFactor]);
            }
            else
            {
                [totalDiscountFactor] = cachedDiscountFactorValue;
            }

            amount = amount * totalDiscountFactor;
        }

        return amount;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>