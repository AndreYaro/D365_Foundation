<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesQuotationController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesQuotationController</c> class is the helper class for the associated SSRS Report.
/// </summary>
public class SalesQuotationController extends TradeDocumentReportController implements BatchRetryable
{
    CustQuotationJour                   custQuotationJour;
    SalesQuotationEditLinesForm         salesQuotationEditLines;
    str                                 inqueryType;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>documentTitle</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Sales Quotation report title.
    /// </summary>
    /// <returns>
    /// The report title.
    /// </returns>
    protected str documentTitle()
    {
        str quotationTxt;

        if ((inqueryType != "@SYS12529" ) || (custQuotationJour.showQuotationStatus() != SalesQuotationStatus::Confirmed))
        {
            if (printCopyOriginal == PrintCopyOriginal::Copy)
            {
                if (custQuotationJour.isProforma())
                {
                    quotationTxt = literalStr("@SYS70791");
                }
                else
                {
                    quotationTxt = literalStr("@SYS24431");
                }
            }
            else
            {
                if (custQuotationJour.isProforma())
                {
                    quotationTxt = literalStr("@SYS269");
                }
                else
                {
                    quotationTxt = literalStr("@SYS5864");
                }
            }
        }
        else
        {
            if (printCopyOriginal == PrintCopyOriginal::Copy)
            {
                if (custQuotationJour.isProforma())
                {
                    quotationTxt = literalStr("@SYS70793");
                }
                else
                {
                    quotationTxt = literalStr("@SYS24432");
                }
            }
            else
            {
                if (custQuotationJour.isProforma())
                {
                    quotationTxt = literalStr("@SYS7845");
                }
                else
                {
                    quotationTxt = literalStr("@SYS12529");
                }
            }
        }

        return quotationTxt;
    }

]]></Source>
			</Method>
			<Method>
				<Name>documentIdentifier</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected str documentIdentifier()
    {
        return custQuotationJour.QuotationDocNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleReportExceptions</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected boolean handleReportExceptions()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reportExceptionContextTableId</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected TableId reportExceptionContextTableId()
    {
        return custQuotationJour.TableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>reportExceptionContextRecordId</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    protected RecId reportExceptionContextRecordId()
    {
        return custQuotationJour.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstJournal</Name>
				<Source><![CDATA[
    protected boolean getFirstJournal()
    {
        return journalList.first(custQuotationJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNextJournal</Name>
				<Source><![CDATA[
    protected boolean getNextJournal()
    {
        return journalList.next(custQuotationJour);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecordId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the ID of the journal record that should be passed to the contract.
    /// </summary>
    /// <returns>
    /// The ID of the journal record.
    /// </returns>
    protected recId getRecordId()
    {
        return custQuotationJour.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFormLetterReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the report and sets the print management.
    /// </summary>
    protected void initFormLetterReport()
    {
        printCopyOriginal = this.parmArgs().parmEnum();

        if (classIdGet(this.parmArgs().caller()) == classNum(SalesQuotationEditLinesForm_Sales_Send) || classIdGet(this.parmArgs().caller()) == classNum(SalesQuotationEditLinesForm_Proj_Send))
        {
            salesQuotationEditLines = this.parmArgs().caller();
        }
        if (this.parmArgs().record())
        {
            journalList = SalesQuotationEditLinesForm::createJournalListCopy(this.parmArgs().record() as CustQuotationJour);
        }
        else
        {
            journalList = this.parmArgs().object();
        }
        if (this.parmArgs().parm())
        {
            inqueryType = this.parmArgs().parm();
        }
        else
        {
            inqueryType = '';
        }

        formLetterReport = FormLetterReport::construct(PrintMgmtDocumentType::Quotation);
        formLetterReport.parmPrintType(printCopyOriginal);

        if (salesQuotationEditLines)
        {
            formLetterReport.parmDefaultCopyPrintJobSettings(new SRSPrintDestinationSettings(salesQuotationEditLines.printerSettingsFormletter()));
            formLetterReport.parmDefaultOriginalPrintJobSettings(new SRSPrintDestinationSettings(salesQuotationEditLines.printerSettingsFormletter()));
            formLetterReport.parmUsePrintMgmtDestinations(salesQuotationEditLines.usePrintManagement());
        }
        else if (printCopyOriginal == PrintCopyOriginal::OriginalPrint)
        {
            // Always use the print mgmt destinations when reprinting for the OriginalPrint case.
            formLetterReport.parmUsePrintMgmtDestinations(true);
        }

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>SRSReportParameters</c> values to the report contract.
    /// </summary>
    protected void modifyContract()
    {
        SRSTmpDataStore         srsTmpDataStore;
        SalesQuotationContract  salesQuotationContract = contract as SalesQuotationContract;

        if (salesQuotationEditLines)
        {
            srsTmpDataStore.Value = salesQuotationEditLines.pack();
            srsTmpDataStore.insert();
            salesQuotationContract.parmSalesQuotationEditLinesRecId(srsTmpDataStore.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>output</Name>
				<Source><![CDATA[
    protected void output()
    {
        using (SysInstrumentationActivityContext activityContext = this.instrumentationLogger().documentReport().output(this.reportExceptionContextTableId(), this.reportExceptionContextRecordId()))
        {
            formLetterReport.loadPrintSettings(custQuotationJour, custQuotationJour.salesQuotationTable(), custQuotationJour.LanguageId);
            this.parmReportContract().parmRdlContract().parmLanguageId(custQuotationJour.LanguageId);
            this.parmReportContract().parmRdlContract().parmLabelLanguageId(custQuotationJour.LanguageId);

            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDataContractRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>CustQuotationJour</c> record.
    /// </summary>
    /// <param name="_common">
    /// A <c>CustQuotationJour</c> record.
    /// </param>
    protected void setDataContractRecord(Common _common)
    {
        custQuotationJour = _common;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SalesQuotationController construct()
    {
        return new SalesQuotationController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initailzes the controller, sets the print management, assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        SalesQuotationController formLetterController = SalesQuotationController::construct();

        formLetterController.initArgs(_args, PrintMgmtDocType::construct(PrintMgmtDocumentType::Quotation).getDefaultReportFormat());

        formLetterController.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newDataContract</Name>
				<Source><![CDATA[
    public static SrsReportDataContract newDataContract(Args _args)
    {
        SalesQuotationController formLetterController = SalesQuotationController::construct();

        if (_args.dataset() != tableNum(CustQuotationJour))
        {
            throw error(strFmt("@SYS19306",funcName()));
        }

        formLetterController.initDataContract(_args, PrintMgmtDocType::construct(PrintMgmtDocumentType::Quotation).getDefaultReportFormat());

        return formLetterController.parmReportContract();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>