<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FormletterJournalPrintCBD_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>FormletterJournalPrintCBD_Extension</c> is an extension class of the <c>FormletterJournalPrint</c>
/// that supports Print management extension settings.
/// </summary>
[ExtensionOf(classStr(FormletterJournalPrint))]
internal final class FormletterJournalPrintCBD_Extension
{
    private container printManagementExtensionsSettings;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromFormletterContract</Name>
				<Source><![CDATA[
    static public FormletterJournalPrint newFromFormletterContract(FormLetterContract _formLetterContract)
    {
        FormletterJournalPrint formletterJournalPrint = next newFromFormletterContract(_formLetterContract);

        if (formletterJournalPrint)
        {
            formletterJournalPrint.parmPrintManagementExtensionsSettings(SysOperationHelper::base64Decode(_formLetterContract.parmPrintManagementExtensionsSettings()));
        }

        return formletterJournalPrint;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPrintManagementExtensionsSettings</Name>
				<Source><![CDATA[
    public container parmPrintManagementExtensionsSettings(container _printManagementExtensionsSettings = printManagementExtensionsSettings)
    {
        printManagementExtensionsSettings = _printManagementExtensionsSettings;
        return printManagementExtensionsSettings;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        container packed = next pack();
        packed = SysPackExtensions::appendVariableToPack(
            packed,
            classStr(FormletterJournalPrintCBD_Extension),
            varStr(printManagementExtensionsSettings),
            printManagementExtensionsSettings);
        return packed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        boolean ret = next unpack(_packedClass);
        
        if (ret)
        {
            printManagementExtensionsSettings = SysPackExtensions::extractPackedVariable(
                _packedClass,
                classStr(FormletterJournalPrintCBD_Extension),
                varStr(printManagementExtensionsSettings));
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>