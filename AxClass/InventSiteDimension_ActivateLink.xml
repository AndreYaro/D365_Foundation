<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventSiteDimension_ActivateLink</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class InventSiteDimension_ActivateLink
{
    InventParameters    inventParameters;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        inventParameters    = InventParameters::find();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount

        if (!this.validate())
            return;

        try
        {
            ttsbegin;

            inventParameters = InventParameters::find(true);
            inventParameters.MultiSiteDimensionLink = InventMultiSiteDimensionLink::Active;
            inventParameters.update();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                    throw Exception::UpdateConflictNotRecovered;
                else
                    retry;
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate()
    {
        boolean ret = true;

        if (ret && !inventParameters.allowActivateDimensionLink())
        {
            ret = checkFailed("@SYS109417");
        }

        if (ret && !this.validateInventSite())
        {
            ret = checkFailed("@SYS109396");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateInventSite</Name>
				<Source><![CDATA[
    protected boolean validateInventSite()
    {
        InventSite          inventSite;
        boolean             ok = true;

        while select inventSite
        {
            if (inventSite.dimensionAttributeValue() == 0)
            {
                ok = checkFailed(strFmt("@SYS109397", inventSite.SiteId, inventParameters.siteDimensionAttribute().localizedName()));
            }
        }
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventSiteDimension_ActivateLink construct()
    {
        return new InventSiteDimension_ActivateLink();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        InventSiteDimension_ActivateLink    inventSiteDimension_ActivateLink = InventSiteDimension_ActivateLink::construct();

        inventSiteDimension_ActivateLink.run();

        if (args)
        {
            Common common = args.record();
            if (FormDataUtil::isFormDataSource(common))
            {
                FormDataSource formDataSource = FormDataUtil::getFormDataSource(common);
                formDataSource.executeQuery();
            }

            if (FormDataUtil::isFormDataSource(common) && InventParameters::find().MultiSiteDimensionLink == InventMultiSiteDimensionLink::Active)
            {
                MenuFunction inventSiteDimensionUpdateMenu = new MenuFunction(menuitemActionStr(InventSiteDimensionUpdate), MenuItemType::Action);
                if (inventSiteDimensionUpdateMenu.checkAccessRights())
                {
                    inventSiteDimensionUpdateMenu.run();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
