<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankBillOfExchangeDelete</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class BankBillOfExchangeDelete extends BankNegInstDelete
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteDocuments</Name>
				<Source><![CDATA[
    protected void deleteDocuments()
    {
        BankBillOfExchangeTable bankBillOfExchangeTable;

        delete_from bankBillOfExchangeTable
            where bankBillOfExchangeTable.BankAccountID == accountID
                && bankBillOfExchangeTable.BillOfExchangeNum >= fromNegInstNum
                && bankBillOfExchangeTable.BillOfExchangeNum <= toNegInstNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        DialogRunbase dialog = super();

        dialog.addGroup("@SYS79258");

        dialogFromNegInstNum = dialog.addField(extendedTypeStr(BankBillOfExchangeNum), "@SYS4083");
        dialogToNegInstNum = dialog.addField(extendedTypeStr(BankBillOfExchangeNum), "@SYS8828");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doAnyBillsExistInRange</Name>
				<Source><![CDATA[
    private boolean doAnyBillsExistInRange()
    {
        BankBillOfExchangeTable bankBillOfExchangeTable;

        select firstonly bankBillOfExchangeTable
            where bankBillOfExchangeTable.BankAccountID == accountID
                && bankBillOfExchangeTable.BillOfExchangeNum >= fromNegInstNum
                && bankBillOfExchangeTable.BillOfExchangeNum <= toNegInstNum;

        return bankBillOfExchangeTable.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mayBeDeleted</Name>
				<Source><![CDATA[
    protected boolean mayBeDeleted()
    {
        BankBillOfExchangeTable bankBillOfExchangeTable;

        select firstonly bankBillOfExchangeTable
            where bankBillOfExchangeTable.BankAccountID == accountID
                && bankBillOfExchangeTable.BillOfExchangeNum >= fromNegInstNum
                && bankBillOfExchangeTable.BillOfExchangeNum <= toNegInstNum
                && bankBillOfExchangeTable.DocumentStatus != BankBillOfExchangeStatus::Created;

          return bankBillOfExchangeTable.RecId == 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtDocumentsDeletedOk</Name>
				<Source><![CDATA[
    protected str txtDocumentsDeletedOk()
    {
        return strfmt("@CashManagement:BillOfExchangeDeleteComplete", fromNegInstNum, toNegInstNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret = super();

        if (ret && !this.doAnyBillsExistInRange())
        {
            return checkFailed("@CashManagement:BillOfExchangeDeleteNoBillsToDelete");
        }

        if (ret && !this.mayBeDeleted())
        {
            ret = checkFailed(strfmt("@SYS79260", BankBillOfExchangeStatus::Created));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS79261";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        BankBillOfExchangeDelete noteDelete;

        switch (args.dataset())
        {
            case tablenum(BankAccountTable):
                noteDelete = BankNegInstDelete::newBankAccountTable(BankNegInst::BillOfExchange, args.record());
                noteDelete.setAccountIdInactive();
                break;

            case tablenum(BankBillOfExchangeTable):
                noteDelete = BankNegInstDelete::newBankNegInstTableMap(BankNegInst::BillOfExchange, args.record());
                break;

            default:
                throw error(Error::wrongUseOfFunction(BankBillOfExchangeDelete::description()));
        }

        if (noteDelete.prompt())
        {
            noteDelete.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>