<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailPricingPropertySalesOrderLineAttributeProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;
using System.Collections;
using CrtPricingPropertyDefinition = Microsoft.Dynamics.Commerce.Runtime.DataModel.PricingPropertyDefinition;
using CrtSalesTransaction = Microsoft.Dynamics.Commerce.Runtime.DataModel.SalesTransaction;
using CrtPricingPropertyValueContext = Microsoft.Dynamics.Commerce.Runtime.DataModel.PricingPropertyValueContext;
using CrtPricingPropertySource = Microsoft.Dynamics.Commerce.Runtime.DataModel.PricingPropertySource;
using CrtPricingPropertyLevel = Microsoft.Dynamics.Commerce.Runtime.DataModel.PricingPropertyLevel;
using CrtPricingPropertyType = Microsoft.Dynamics.Commerce.Runtime.DataModel.PricingPropertyType;

/// <summary>
/// Property provider for sales order line attributes.
/// </summary>
[ExportAttribute(identifierStr(Dynamics.AX.Application.RetailPricingPropertyValueProviderBase))]
internal final class RetailPricingPropertySalesOrderLineAttributeProvider extends RetailPricingPropertyValueProviderBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>IsPropertySupported</Name>
				<Source><![CDATA[
    public boolean IsPropertySupported(CrtPricingPropertyDefinition definition)
    {
        return definition.get_PropertyLevel() == CrtPricingPropertyLevel::Line
            && definition.get_PropertyType() == CrtPricingPropertyType::Attribute
            && definition.get_PropertySource() == CrtPricingPropertySource::SalesOrderLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>Resolve</Name>
				<Source><![CDATA[
    public void Resolve(CrtPricingPropertyValueContext valueContext, CrtSalesTransaction salesTransaction)
    {        
        Map attributeRecIdMap = new Map(Types::Int64, Types::AnyType);
        
        IEnumerator enumerator = valueContext.get_PricingProperties().GetEnumerator();
        while (enumerator.moveNext())
        {
            CrtPricingPropertyDefinition crtDefinition = enumerator.get_Current();
            if (this.IsPropertySupported(crtDefinition))
            {
                attributeRecIdMap.insert(crtDefinition.get_AttributeRecordId(), crtDefinition);
            }
        }

        EcoResAttributeValue ecoResAttributeValue;
        EcoResTextValue ecoResTextValue; // Sales order attribute only supports text value.
        int64 mTransactionRecId;
        if (System.Int64::TryParse(this.mTransactionId, byref mTransactionRecId)) // Check whether it's a price simulator transaction.
        {
            RetailPricingSimulatorSalesTransaction psSalesTransaction = RetailPricingSimulatorSalesTransaction::findByRef(mTransactionRecId);
            RetailPricingSimulatorInstanceValue retailPricingSimulatorInstanceValue;

            while select LineNum from mTmpOrderItem
                join RecId from retailPricingSimulatorInstanceValue
                    where retailPricingSimulatorInstanceValue.SimulationRecId == psSalesTransaction.RecId
                        && retailPricingSimulatorInstanceValue.SimulationHeaderOrLineNum == mTmpOrderItem.LineNum
                join RecId, Attribute from ecoResAttributeValue
                    where ecoResAttributeValue.InstanceValue == retailPricingSimulatorInstanceValue.RecId
                join TextValue from ecoResTextValue
                    where ecoResTextValue.RecId == ecoResAttributeValue.Value
            {
                this.addToValueContext(valueContext, attributeRecIdMap, mTmpOrderItem.LineNum, ecoResAttributeValue, ecoResTextValue);
            }
        }
        else
        {
            RetailSalesTableInstanceValue retailSalesTableInstanceValue;

            while select LineNum from mTmpOrderItem
                join RecId from retailSalesTableInstanceValue
                    where retailSalesTableInstanceValue.SalesId == this.mTransactionId
                        && retailSalesTableInstanceValue.SalesDataAreaId == curExt()
                        && retailSalesTableInstanceValue.HeaderOrLineNum == mTmpOrderItem.LineNum
                join RecId, Attribute from ecoResAttributeValue
                    where ecoResAttributeValue.InstanceValue == retailSalesTableInstanceValue.RecId
                join TextValue from ecoResTextValue
                    where ecoResTextValue.RecId == ecoResAttributeValue.Value
            {
                this.addToValueContext(valueContext, attributeRecIdMap, mTmpOrderItem.LineNum, ecoResAttributeValue, ecoResTextValue);
            }
        }
        
        enumerator = valueContext.get_PricingProperties().GetEnumerator();
        while (enumerator.moveNext())
        {
            CrtPricingPropertyDefinition crtDefinition = enumerator.get_Current();
            if (this.IsPropertySupported(crtDefinition))
            {
                valueContext.Resolve(crtDefinition);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addToValueContext</Name>
				<Source><![CDATA[
    private void addToValueContext(CrtPricingPropertyValueContext valueContext, Map attributeRecIdMap, LineNum lineNum, EcoResAttributeValue ecoResAttributeValue, EcoResTextValue ecoResTextValue)
    {
        if (!attributeRecIdMap.exists(ecoResAttributeValue.Attribute))
        {
            return;
        }

        CrtPricingPropertyDefinition crtDefinition = attributeRecIdMap.lookup(ecoResAttributeValue.Attribute);

        valueContext.AddLineValue(crtDefinition, lineNum, ecoResTextValue.TextValue);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>