<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxTransTotalTaxAmountFetcher_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///  Country customization extension of the base class.
/// </summary>
public class TaxTransTotalTaxAmountFetcher_IT extends TaxTransTotalTaxAmountFetcher
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TaxTransTotalTaxAmountFetcher_IT construct()
    {
        return new TaxTransTotalTaxAmountFetcher_IT();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTotalActualTaxAmount</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Gets the actual total tax amount in the transaction currency.
    /// </summary>
    /// <param name="_query">
    ///  A query that indicates the range restrictions.
    /// </param>
    /// <param name="_changeDisplaySign">
    ///  A Boolean value that indicates if the sign should be determined based on the tax code tax direction.
    /// </param>
    /// <param name="_generalJournalEntryId">
    ///  A RefRecId value that indicates an additional range restriction.
    /// </param>
    /// <returns>
    ///  The total tax amount in the transaction currency.
    /// </returns>
    public TaxAmountCur getTotalActualTaxAmount(
        Query _query, 
        boolean _changeDisplaySign = false, 
        RefRecId _generalJournalEntryId = 0)
    {
        this.addFilterByGeneralJournalEntryId(_generalJournalEntryId, _query);

        AmountCur taxAmountCur = this.calculateTotalTaxAmount( _query, _changeDisplaySign);

        return taxAmountCur;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateTotalTaxAmount</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Calculates the total tax amount for all tax transactions.
    /// </summary>
    /// <param name = "_taxQuery">The query that will find the tax transactions to get the total tax amount from.</param>
    /// <param name = "_changeDisplaySign">Is this a change display sign scenario?</param>
    /// <returns>The total tax amount for all tax transactions found by the query.</returns>
    [Hookable(false)]
    protected TaxAmountCur calculateTotalTaxAmount(Query _taxQuery, boolean _changeDisplaySign)
    {
        TaxAmountCur sourceTaxAmountCur;
        Query query = new Query(_taxQuery);

        QueryBuildDataSource qbds = query.dataSourceTable(tableNum(TaxTrans));
        qbds.orderMode(OrderMode::GroupBy);
        qbds.addSortField(fieldNum(TaxTrans, TaxDirection));
        qbds.addSortField(fieldNum(TaxTrans, ReverseCharge_W));
        qbds.addSelectionField(fieldNum(TaxTrans, SourceRegulateAmountCur), SelectionField::Sum);
        
        QueryRun taxQueryRun = new QueryRun(query);

        while (taxQueryRun.next())
        {
            TaxTrans taxTrans = taxQueryRun.get(tableNum(TaxTrans));

            int sign = this.determineSign(_changeDisplaySign, taxTrans);

            sourceTaxAmountCur += taxTrans.SourceRegulateAmountCur * sign;
        }

        return sourceTaxAmountCur;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>