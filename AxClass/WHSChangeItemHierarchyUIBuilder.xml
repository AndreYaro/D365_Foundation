<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSChangeItemHierarchyUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSChangeItemHierarchyUIBuilder</c> class provides the UIBuilder for the
/// <c>WHSChangeItemHierarchyDataContract</c> class.
/// </summary>
class WHSChangeItemHierarchyUIBuilder extends SysOperationAutomaticUIBuilder
{
    DialogField                         dfHierarchyName;
    WHSChangeItemHierarchyDataContract  changeItemHierarchyDataContract;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupHierarchy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Override the lookup for hierarchy name so there is a dropdown.
    /// </summary>
    /// <param name="_ctrl">
    /// The <c>FormStringControl</c> that will use the lookup.
    /// </param>
    public void lookupHierarchy(FormStringControl _ctrl)
    {
        WHSReservationHierarchy::lookupHierarchy(_ctrl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Used to override the need method of form controls.
    /// </summary>
    public void postBuild()
    {
        super();

        changeItemHierarchyDataContract = this.dataContractObject();

        dfHierarchyName = this.bindInfo().getDialogField(changeItemHierarchyDataContract, methodStr(WHSChangeItemHierarchyDataContract, parmNewHierarchyName));

        dfHierarchyName.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(WHSChangeItemHierarchyUIBuilder, lookupHierarchy), this);
        dfHierarchyName.registerOverrideMethod(methodStr(FormStringControl, validate), methodStr(WHSChangeItemHierarchyUIBuilder, validateHierarchy), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateHierarchy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overrides the validation method for the hierarchy name so that invalid entries aren't permitted.
    /// </summary>
    /// <param name="_ctrl">
    /// The <c>FormStringControl</c> that will use the validation logic.
    /// </param>
    /// <returns>
    /// True if the hierarchy name is valid; otherwise, false.
    /// </returns>
    public boolean validateHierarchy(FormStringControl _ctrl)
    {
        boolean ret = true;

        if (WHSReservationHierarchy::findByName(_ctrl.valueStr()).RecId == 0)
        {
            ret = checkFailed("@WAX4814");
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>