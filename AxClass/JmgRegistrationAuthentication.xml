<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgRegistrationAuthentication</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///  The <c>JmgRegistrationAuthentication</c> class supports the login system on the <c>JmgRegistration</c> and <c>JmgRegistrationTouch</c> forms.
/// </summary>
class JmgRegistrationAuthentication
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>authenticate</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Returns a <c>JmgEmployee</c> record for the pecified card number or personnel number.
    /// </summary>
    /// <param name="_workerText">
    ///  The card or personel number of the worker to retrieve from the <c>JmgEmployee</c> table.
    /// </param>
    /// <param name="_password">
    ///  The password of the worker to retrieve from the <c>JmgEmployee</c> table.
    /// </param>
    /// <param name="_useBadgeID">
    ///  A Boolean value that indicates to use badge ID.
    /// </param>
    /// <param name="_usePin">
    ///  A Boolean value that indicates to use Pin code.
    /// </param>
    /// <returns>
    ///  A <c>JmgEmployee</c> record for the specified card number/ personnel number.
    /// </returns>
    public static JmgEmployee authenticate(str _workerText, str _password, boolean _useBadgeID, boolean _usePin)
    {
        JmgEmployee             emplTbl;

        if (_workerText && _useBadgeID)
        {
            emplTbl = JmgEmployee::findCardNo(_workerText);
            if (!emplTbl)
            {
                Box::info("@SYS183536","@SYS183537");
                return null;
            }
        }
        else
        {
            emplTbl = JmgEmployee::findPersonnelNumber(_workerText);
        }

        if (!emplTbl)
        {
            Box::info("@SYS183536","@SYS183537");
            return null;
        }

        if (_usePin && !emplTbl.validatePINCode(_password))
        {
            Box::info("@SYS183538","@SYS183539");
            return null;
        }

        JmgInstrumentationClockInOut::registerDailyActiveUsage(emplTbl.Worker);

        return emplTbl;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getIdentificationLabel</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Retrieves Identification Label.
    /// </summary>
    /// <param name="_useBadgeID">
    ///  A Boolean value that indicates to use Badge ID.
    /// </param>
    /// <returns>
    ///  Return label for Badg Id if _useBadgeID is set, otherwise return label for Personnel number .
    /// </returns>
    public static str getIdentificationLabel(boolean _useBadgeID)
    {
        if (_useBadgeID)
        {
            return "@SYS39204"; // Badge ID
        }
        else
        {
            return "@SYS303656"; // Personnel number
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEnterIdText</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Retrieves login instruction text.
    /// </summary>
    /// <param name="_password">
    ///  A Boolean value that indicates whether to use password.
    /// </param>
    /// <param name="_useBadgeID">
    ///  A Boolean value that indicates to use BadgeID.
    /// </param>
    /// <returns>
    ///  Return label with instruction to enter Badg Id if _password is not set,
    ///  otherwise return label with instruction to enter Personnel number and password.
    /// </returns>
    public static str getEnterIdText(boolean _password, boolean _useBadgeID)
    {
        if (!_password)
        {
            return strFmt("@SYS308390", JmgRegistrationAuthentication::getIdentificationLabel(_useBadgeID));
        }
        else
        {
            return strFmt("@SYS183542", JmgRegistrationAuthentication::getIdentificationLabel(_useBadgeID), "@SYS12019");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>