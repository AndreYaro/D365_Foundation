<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_PurchTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_PurchTable extends LogisticsLocationSelectForm
{
    PurchTable purchTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates the entities from which addresses can be selected.
    /// </summary>
    /// <returns>
    ///    A set of <c>LogisticsLocationSelectSourceTypes</c> enumeration types from which addresses can be
    ///    selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret;

        ret = super();

        ret.add(LogisticsLocationSelectSourceType::PurchaseOrder);
        ret.add(LogisticsLocationSelectSourceType::PurchaseOrderLine);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuote);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuoteLine);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling purchase order.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        // <GBR>
        FormDataSource  purchTable_ds;
        // </GBR>

        purchTable.DeliveryPostalAddress = _addressRecId;
        purchTable.setDeliveryName();
        purchTable.AddressRefRecId = _transferFromTable.RecId;
        purchTable.AddressRefTableId = _transferFromTable.TableId;
        purchTable.setListCode();

        this.setTaxGroup(_addressRecId);

        // <GBR>
        if (BrazilParameters::isEnabled())
        {
            if (FormDataUtil::isFormDataSource(purchTable))
            {
                purchTable_ds = FormDataUtil::getFormDataSource(purchTable);
                purchTable_ds.cacheCalculateMethod(tableMethodStr(PurchTable, deliveryCNPJCPFNum_BR));
                purchTable_ds.cacheCalculateMethod(tableMethodStr(PurchTable, deliveryIENum_BR));
            }
        }
        // </GBR>
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (purchTable.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchTable.AddressRefTableId,
                purchTable.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    purchTable.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> record that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of the <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public RecId getLogisticsPostalAddressRecId()
    {
        return purchTable.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the grid of the target form should have select upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (purchTable.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchTable.AddressRefTableId,
                purchTable.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            addressOwner = CompanyInfo::find();
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new(PurchTable _purchTable)
    {
        purchTable = _purchTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTaxGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Copies the tax group from the address to the purchase order.
    /// </summary>
    /// <param name="_addressRecId">
    ///    The record ID of an address.
    /// </param>
    internal void setTaxGroup(LogisticsPostalAddressRecId _addressRecId)
    {
        TaxGroup taxGroup;
        DlvTerm dlvTerm;

        if (purchTable.DlvTerm)
        {
            taxGroup = LogisticsLocationExt::find(LogisticsPostalAddress::findRecId(_addressRecId).Location).TaxGroup;

            if (taxGroup)
            {
                dlvTerm  = DlvTerm::find(purchTable.DlvTerm);

                if (dlvTerm.TaxLocationRole == LogisticsLocationRoleType::Delivery)
                {
                    purchTable.TaxGroup = taxGroup;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_PurchTable</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A <c>LogisticsLocationSelectForm_PurchTable</c> record to use to initialize the object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_PurchTable</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_PurchTable construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_PurchTable(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>