<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RPayPFReportXML_SZV64</Name>
	<SourceCode>
		<Declaration><![CDATA[
class RPayPFReportXML_SZV64 extends RPayPFReportXML implements IRPayReportRunClass
{
    #define.ReportTitle("@RUP3515")

    RPayPFReportCtrl                reportData;
    IRPayPFReportCommand            totalFundCommand, seniorityCommand, monthFundCommand;

    IRPayPFReportDataIterator       fundDataIterator, seniorityIterator, monthFundIterator;
    Set                             keyDocChanged, keyFileChanged;
    Counter                         docPersonQty, docPersonQtyMax;
    int                             keyAgreementType;
    ListEnumerator                  totalPerFileEnumerator;

    #RPayPFReportSZV64XML
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>caption</Name>
				<Source><![CDATA[
    public ClassDescription caption()
    {
        return #ReportTitle;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEmpl</Name>
				<Source><![CDATA[
    protected boolean checkEmpl(RHRMEmplTable   _emplTable,
                                boolean         _displayMessage = true,
                                str 20          _personCategory = '',
                                str 20          _formType       = '')
    {
        boolean ret = true;

        ret = this.checkEmplIsInsured(_emplTable, _displayMessage) && ret;

        if (ret)
        {
            ret = this.checkEmplIsInsured(_emplTable, false, _personCategory);
        }

        if (ret)
        {
            ret = this.checkMainInsuredPersonCat(_emplTable, _displayMessage) && ret;
            ret = this.checkEmplPFNumber(_emplTable, _displayMessage) && ret;
            ret = this.checkEmplPersonalInfo(_emplTable, _displayMessage, false, true) && ret;

            if (_formType == #FormTypeSZV61)
            {
                ret = this.checkEmplHasSpecSen(_emplTable, false) && ret;
            }
            else if (_formType == #FormTypeSZV62)
            {
                ret = this.checkEmplDoesNotHaveSpecSen(_emplTable, false) && ret;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>descriptionTag</Name>
				<Source><![CDATA[
    protected str descriptionTag()
    {
        return 'ВХОДЯЩАЯ_ОПИСЬ_ПО_СУММАМ_ВЫПЛАТ_И_ПО_СТРАХОВЫМ_ВЗНОСАМ';
    }

]]></Source>
			</Method>
			<Method>
				<Name>docQty</Name>
				<Source><![CDATA[
    protected int docQty(
        str 100             _docType,
        str 20              _personCategory = '',
        RPayPFSeniorityType _seniorityType = RPayPFSeniorityType::None)
    {
        container ret;
        //return emplNo;
        if (totalPerFileEnumerator.moveNext())
        {
            ret = totalPerFileEnumerator.current();
        }
        else
        {
            ret = connull();
        }

        return conpeek(ret, 1);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillDocContPerFile</Name>
				<Source><![CDATA[
    protected void fillDocContPerFile()
    {
        container   con;
        container   monthFundCon;
        int         counter, monthIdx;
        List        totalPerFile;
        RPayAmount  totalByMonths,
                    totalLessLimitByMonths,
                    totalMoreLimitByMonths,
                    totalFundAmountIns,
                    totalFundAmountSto;

        totalPerFile   = new List(Types::Container);
        totalFundCommand.parmValue(connull());

        fundDataIterator = totalFundCommand.iterator();

        if (fundDataIterator.more())
        {
            while (fundDataIterator.more())
            {
                con = fundDataIterator.value();
                [#parmKeyReport] = conpeek(con, 1);
                [#parmValue]     = conpeek(con, 2);

                if (this.isNewFile())
                {
                    if (counter)
                    {
                        totalPerFile.addEnd([counter, totalByMonths, totalLessLimitByMonths, totalMoreLimitByMonths, totalFundAmountIns, totalFundAmountSto]);
                    }

                    counter = 0;

                    totalByMonths           = 0;
                    totalLessLimitByMonths  = 0;
                    totalMoreLimitByMonths  = 0;

                    totalFundAmountIns = 0;
                    totalFundAmountSto = 0;
                }

                if (this.isNewDocument())
                {
                    for (monthIdx = 1; monthIdx <= 12; monthIdx++)
                    {
                        monthFundCommand.parmValue([#parmKeyReport, monthIdx]);
                        monthFundIterator = monthFundCommand.iterator();

                        if (monthFundIterator.more())
                        {
                            monthFundCon = monthFundIterator.value();

                            totalByMonths           += conpeek(monthFundCon, 1);
                            totalLessLimitByMonths  += conpeek(monthFundCon, 2);
                            totalMoreLimitByMonths  += conpeek(monthFundCon, 3);
                        }
                    }

                    totalFundAmountIns += fundAmountIns;
                    totalFundAmountSto += fundAmountSto;

                    docPersonQty++;
                    counter ++;
                }

                fundDataIterator.moveNext();
            }
        }

        if (counter)
        {
            totalPerFile.addEnd([counter, totalByMonths, totalLessLimitByMonths, totalMoreLimitByMonths, totalFundAmountIns, totalFundAmountSto]);
        }

        totalPerFileEnumerator = totalPerFile.getEnumerator();
        keyDocChanged  = new Set(Types::Container);
        keyFileChanged = new Set(Types::Container);
        docPersonQty = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillHeader</Name>
				<Source><![CDATA[
    protected void fillHeader(str 100               _docType,
                              str 20                _personCategory,
                              RPayPFSeniorityType   _seniorityType  = RPayPFSeniorityType::None)
    {
        SysDictEnum     dictEnum;
        RHRMSortOfWork  sortOfWork;
        container       monthFundCon;

        super(_docType, _personCategory, RPayPFSeniorityType::None);

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипСведений',       this.dataType());

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'КодКатегории',      _personCategory);

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ОтчетныйПериод');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Квартал',       this.getReportPeriodQtrStr(reportEndPeriod));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Год',           int2str(reportYear));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Название',      this.getReportPeriodName(reportStartPeriod, reportEndPeriod));

        if (reportType != RpayPensionFundExportKindOfCorrection::None &&
            this.needCorrectedPeriod())
        {
            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'КорректируемыйОтчетныйПериод');

                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Квартал',       this.getReportPeriodQtrStr(corrReportEndPeriod));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Год',           int2str(corrReportYear));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Название',      this.getReportPeriodName(corrReportStartPeriod, corrReportEndPeriod));
        }

        dictEnum = new SysDictEnum(enumnum(RHRMSortOfWork));
        if (dictEnum.lastValue() >= (keyAgreementType - 1) && dictEnum.firstValue() <= (keyAgreementType - 1))
        {
            sortOfWork = keyAgreementType - 1;

            switch (sortOfWork)
            {
                case RHRMSortOfWork::CivilContract:
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипДоговора',      #ContractTypeCivil);
                    break;

                default:
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипДоговора',      #ContractTypeLabor);
            }
        }

        if (reportType == RpayPensionFundExportKindOfCorrection::Cancellative)
        {
            return;
        }

        monthFundCon = totalPerFileEnumerator.current();

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВыплатИвознагражденийВпользуЗЛ');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ТипСтроки', 'ИТОГ');
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатВсего', num2str(conpeek(monthFundCon, 2), 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыНеПревышающие', num2str(conpeek(monthFundCon, 3), 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыПревышающие', num2str(conpeek(monthFundCon, 4), 1, 2, 1, 0));

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВзносовНаСтраховую');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Начислено',     num2str(conpeek(monthFundCon, 5), 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Уплачено',      num2str(conpeek(monthFundCon, 5), 1, 2, 1, 0));

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВзносовНаНакопительную');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Начислено',     num2str(conpeek(monthFundCon, 6), 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Уплачено',      num2str(conpeek(monthFundCon, 6), 1, 2, 1, 0));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillLine</Name>
				<Source><![CDATA[
    protected void fillLine(RHRMEmplTable       _emplTable,
                            str 20              _personCategory)
    {
        DirPersonName                   dirPersonName = _emplTable.personTable().dirPersonName();
        container                       seniorityCon, monthFundCon;
        RPayInsuranceSeniorityTypeId    insuranceTypeId;
        TransDate                       seniorityDateFrom, seniorityDateTo;
        Months                          senMonths;
        Days                            senDays;
        int                             senIdx;
        RHRMSpecCond                    specCondition;
        RHRMPositionList                positionList;
        RHRMSortOfWork                  sortOfWork;
        SysDictEnum                     dictEnum;
        int                             monthIdx;
        RPayAmount                      totalByMonths,
                                        totalLessLimitByMonths,
                                        totalMoreLimitByMonths,
                                        totalExtra1ByMonths,
                                        totalExtra2ByMonths;
        boolean                         existsMonthData = false;

        numInPack++;

        xmlLevel_3 = this.appendXMLElement(xmlLevel_2, #DocTypeSZV64);

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'НомерВпачке',           int2str(numInPack));
        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипСведений',           this.dataType());
        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'РегистрационныйНомер',  companyInfo.PFRegNum_RU);
        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'НаименованиеКраткое',   strreplace(strreplace(strupr(companyInfo.Name), '«', '\''), '»', '\''));

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'НалоговыйНомер');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ИНН',       CompanyInfoHelper::partyINN_RU(companyInfo));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'КПП',       CompanyInfoHelper::partyKPP_RU(companyInfo));

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'КодКатегории',          _personCategory);

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ОтчетныйПериод');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Квартал',       this.getReportPeriodQtrStr(reportEndPeriod));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Год',           int2str(reportYear));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Название',      this.getReportPeriodName(reportStartPeriod, reportEndPeriod));

        if (reportType != RpayPensionFundExportKindOfCorrection::None &&
            this.needCorrectedPeriod())
        {
            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'КорректируемыйОтчетныйПериод');

                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Квартал',       this.getReportPeriodQtrStr(corrReportEndPeriod));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Год',           int2str(corrReportYear));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Название',      this.getReportPeriodName(corrReportStartPeriod, corrReportEndPeriod));
        }

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СтраховойНомер',    _emplTable.payPFRINN());

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ФИО');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Фамилия',       strupr(dirPersonName.LastName));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Имя',           strupr(dirPersonName.FirstName));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Отчество',      strupr(dirPersonName.MiddleName));

        dictEnum = new SysDictEnum(enumnum(RHRMSortOfWork));
        if (dictEnum.lastValue() >= (keyAgreementType - 1) && dictEnum.firstValue() <= (keyAgreementType - 1))
        {
            sortOfWork = keyAgreementType - 1;

            switch (sortOfWork)
            {
                case RHRMSortOfWork::CivilContract:
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипДоговора',      #ContractTypeCivil);
                    break;

                default:
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ТипДоговора',      #ContractTypeLabor);
            }
        }

        if (reportType == RpayPensionFundExportKindOfCorrection::Cancellative)
        {
            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ДатаЗаполнения',     date2str(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), 123, 2, 2, 2, 2, 4, DateFlags::None));
            return;
        }

        existsMonthData = false;
        for (monthIdx = 1; monthIdx <= 12; monthIdx++)
        {
            monthFundCommand.parmValue([#parmKeyReport, monthIdx]);
            monthFundIterator = monthFundCommand.iterator();

            if (monthFundIterator.more())
            {
                monthFundCon = monthFundIterator.value();

                if (conpeek(monthFundCon, 1) ||
                    conpeek(monthFundCon, 2) ||
                    conpeek(monthFundCon, 3))
                {
                    existsMonthData = true;
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВыплатИвознагражденийВпользуЗЛ');

                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ТипСтроки', 'МЕСЦ');
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Месяц', int2str(monthIdx));
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатВсего', num2str(conpeek(monthFundCon, 1), 1, 2, 1, 0));
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыНеПревышающие', num2str(conpeek(monthFundCon, 2), 1, 2, 1, 0));
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыПревышающие', num2str(conpeek(monthFundCon, 3), 1, 2, 1, 0));

                    totalByMonths           += conpeek(monthFundCon, 1);
                    totalLessLimitByMonths  += conpeek(monthFundCon, 2);
                    totalMoreLimitByMonths  += conpeek(monthFundCon, 3);
                }
            }
        }

        if (existsMonthData)
        {
            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВыплатИвознагражденийВпользуЗЛ');

                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ТипСтроки', 'ИТОГ');
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатВсего', num2str(totalByMonths, 1, 2, 1, 0));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыНеПревышающие', num2str(totalLessLimitByMonths, 1, 2, 1, 0));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатНачисленыСтраховыеВзносыПревышающие', num2str(totalMoreLimitByMonths, 1, 2, 1, 0));
        }

        existsMonthData = false;
        for (monthIdx = 1; monthIdx <= 12; monthIdx++)
        {
            monthFundCommand.parmValue([#parmKeyReport, monthIdx]);
            monthFundIterator = monthFundCommand.iterator();

            if (monthFundIterator.more())
            {
                monthFundCon = monthFundIterator.value();

                if (conpeek(monthFundCon, 4) != 0 ||
                    conpeek(monthFundCon, 5) != 0)
                {
                    existsMonthData = true;
                    xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВыплатИвознагражденийПоДопТарифу');

                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ТипСтроки', 'МЕСЦ');
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Месяц', int2str(monthIdx));
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатПоДопТарифу27-1', num2str(conpeek(monthFundCon, 4), 1, 2, 1, 0));
                        xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатПоДопТарифу27-2-18', num2str(conpeek(monthFundCon, 5), 1, 2, 1, 0));

                    totalExtra1ByMonths  += conpeek(monthFundCon, 4);
                    totalExtra2ByMonths  += conpeek(monthFundCon, 5);
                }
            }
        }

        if (existsMonthData)
        {
            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВыплатИвознагражденийПоДопТарифу');

                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ТипСтроки', 'ИТОГ');
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатПоДопТарифу27-1', num2str(totalExtra1ByMonths, 1, 2, 1, 0));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'СуммаВыплатПоДопТарифу27-2-18', num2str(totalExtra2ByMonths, 1, 2, 1, 0));
        }

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВзносовНаСтраховую');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Начислено',     num2str(fundAmountIns, 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Уплачено',      num2str(fundAmountIns, 1, 2, 1, 0));

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СуммаВзносовНаНакопительную');

            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Начислено',     num2str(fundAmountSto, 1, 2, 1, 0));
            xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'Уплачено',      num2str(fundAmountSto, 1, 2, 1, 0));

        // Вывод стажа
        seniorityCommand.parmValue([#SeniorityKey]);
        seniorityIterator = seniorityCommand.iterator();

        while (seniorityIterator.more())
        {
            seniorityCon = seniorityIterator.value();
            [#seniorityValue] = seniorityCon;
            ++senIdx;

            xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'СтажевыйПериод');

                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'НомерСтроки',           int2str(senIdx));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ДатаНачалаПериода',     date2str(seniorityDateFrom, 123, 2, 2, 2, 2, 4, DateFlags::None));
                xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ДатаКонцаПериода',      date2str(seniorityDateTo, 123, 2, 2, 2, 2, 4, DateFlags::None));

                if (insuranceTypeId || (specCondition && (totalExtra1ByMonths + totalExtra2ByMonths != 0)))
                {
                    xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'КоличествоЛьготныхСоставляющих',    int2str(1));
                    xmlLevel_5 = this.appendXMLElement(xmlLevel_4, 'ЛьготныйСтаж');

                        xmlLevel_6 = this.appendXMLElement(xmlLevel_5, 'НомерСтроки',           int2str(1));

                        xmlLevel_6 = this.appendXMLElement(xmlLevel_5, 'ОсобенностиУчета');

                            if (totalExtra1ByMonths + totalExtra2ByMonths != 0)
                            {
                                xmlLevel_7 = this.appendXMLElement(xmlLevel_6, 'ОсобыеУсловияТруда');

                                    xmlLevel_8 = this.appendXMLElement(xmlLevel_7, 'ОснованиеОУТ', specCondition);
                                    xmlLevel_8 = this.appendXMLElement(xmlLevel_7, 'ПозицияСписка', positionList);
                            }

                            xmlLevel_7 = this.appendXMLElement(xmlLevel_6, 'ДекретДети', insuranceTypeId);
                }

            seniorityIterator.moveNext();
        }

        xmlLevel_4 = this.appendXMLElement(xmlLevel_3, 'ДатаЗаполнения',     date2str(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), 123, 2, 2, 2, 2, 4, DateFlags::None));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fullTotals</Name>
				<Source><![CDATA[
    protected boolean fullTotals()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReportPeriodName</Name>
				<Source><![CDATA[
    private str 20 getReportPeriodName(ReportPeriod_RU _reportStartPeriod,
                                       ReportPeriod_RU _reportEndPeriod)
    {
        return 'С '   + date2str(dateStartMth(_reportStartPeriod), 123, 2, 2, 2, 2, 4, DateFlags::None) +
               ' ПО ' + date2str(dateEndMth(_reportEndPeriod), 123, 2, 2, 2, 2, 4, DateFlags::None);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReportPeriodQtrStr</Name>
				<Source><![CDATA[
    private str 20 getReportPeriodQtrStr(ReportPeriod_RU _reportEndPeriod)
    {
        int     quarterSign;

        if (year(_reportEndPeriod) == #ReportYear2010)
        {
            quarterSign = (_reportEndPeriod == dateEndYr(_reportEndPeriod) ? 2 : 1);
        }
        else
        {
            quarterSign = mthOfYr(_reportEndPeriod) / 3;
        }

        return int2str(quarterSign);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initEmployeesData</Name>
				<Source><![CDATA[
    protected void initEmployeesData()
    {
        parmContract = new RPayPFReportParmContract();
        parmContract.reportStartPeriod(reportStartPeriod);
        parmContract.reportEndPeriod(reportEndPeriod);
        parmContract.corrReportStartPeriod(corrReportStartPeriod);
        parmContract.corrReportEndPeriod(corrReportEndPeriod);
        parmContract.seniorityEndPeriod(seniorityEndPeriod);
        parmContract.seniorityStartPeriod(seniorityStartPeriod);
        parmContract.queryRun(queryRun.pack());
        parmContract.correctionType(reportType);

        reportData = RPayPFReportCtrl::construct(this);
        reportData.prepareData();
        totalFundCommand = new RPayReportCommandSZV6(reportData.reportData(), [#FundAmountDS]);
        seniorityCommand = new RPayReportCommandSZV6(reportData.reportData(), [#SeniorityDS]);
        monthFundCommand = new RPayReportCommandSZV64(reportData.reportData(), [#MonthFundAmountDS]);

        if (payTaxParameters.SZVPersonCounter)
        {
            docPersonQtyMax = real2int(RPayRateTable::find(payTaxParameters.SZVPersonCounter).getValue(RPayRateCallContext::construct(reportEndPeriod)));

            if (maxDocPerFile)
            {
                docPersonQtyMax = min(maxDocPerFile, docPersonQtyMax);
            }
        }
        else
        {
            docPersonQtyMax = maxDocPerFile;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isNewDocument</Name>
				<Source><![CDATA[
    protected boolean isNewDocument()
    {
        boolean ret;

        if (keyDocChanged.in([#parmKeyReport]) || !keyEmplId)
        {
            ret = false;
        }
        else
        {
            ret = true;
            keyDocChanged.add([#parmKeyReport]);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isNewFile</Name>
				<Source><![CDATA[
    protected boolean isNewFile()
    {
        boolean ret;

        if (keyFileChanged.in([#parmKeyFile]) || !keyCategory || !keyAgreementType)
        {
            ret = false;
        }
        else
        {
            ret = true;
            keyFileChanged.add([#parmKeyFile]);
            docPersonQty = 0;
        }

        if (docPersonQtyMax && docPersonQtyMax <= docPersonQty &&
            keyCategory && keyAgreementType)
        {
            ret = true;
            docPersonQty = 0;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>minYearEnabled</Name>
				<Source><![CDATA[
    protected Yr minYearEnabled()
    {
        return #ReportYear2010;
    }

]]></Source>
			</Method>
			<Method>
				<Name>needCorrectedPeriod</Name>
				<Source><![CDATA[
    protected boolean needCorrectedPeriod()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList, queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmContract</Name>
				<Source><![CDATA[
    public container parmContract()
    {
        return parmContract.pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>periodType</Name>
				<Source><![CDATA[
    protected enumId periodType()
    {
        return enumnum(RPayPFReportPeriod);
    }

]]></Source>
			</Method>
			<Method>
				<Name>reportData</Name>
				<Source><![CDATA[
    public RPayPFReportCtrl reportData()
    {
        return new RPayPFReportCtrlSZV64();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        RHRMEmplTable   emplTableLocal;
        container con;

        if (! this.checkCompanyInfo() || ! this.checkTaxParameters())
        {
            return;
        }

        keyDocChanged  = new Set(Types::Container);
        keyFileChanged = new Set(Types::Container);

        this.initEmployeesData();

        this.fillDocContPerFile();

        totalFundCommand.parmValue(connull());

        fundDataIterator = totalFundCommand.iterator();

        if (fundDataIterator.more())
        {
            this.progressInit(this.caption(), SysQuery::countTotal(queryRun), #AviFormLetter);
            progress.updateInterval(0);

            while (fundDataIterator.more())
            {
                con = fundDataIterator.value();
                [#parmKeyReport] = conpeek(con, 1);
                [#parmValue]     = conpeek(con, 2);

                if (this.isNewFile())
                {
                    if (xmlDoc)
                    {
                        new FileIOPermission(conpeek(this.fileName(), 1), #io_write).assert();
                        xmlDoc.save(conpeek(this.fileName(), 1));
                        xmlDoc = NULL;
                        CodeAccessPermission::revertAssert();
                        ++packageNum;
                    }

                    if (this.makeDocument())
                    {
                        this.fillHeader(#DocTypeSZV64, keyCategory);
                        keyDocChanged = new Set(Types::Container);
                    }
                }

                if (this.isNewDocument())
                {
                    emplTableLocal = RHRMEmplTable::find(keyEmplId);
                    progress.setText(strFmt("@RUP3038", emplTableLocal.EmployeeId, emplTableLocal.Name()));
                    progress.incCount();

                    this.fillLine(emplTableLocal, keyCategory);
                    docPersonQty++;
                }

                fundDataIterator.moveNext();
            }

            if (xmlDoc)
            {
                new FileIOPermission(conpeek(this.fileName(), 1), #io_write).assert();
                xmlDoc.save(conpeek(this.fileName(), 1));
                xmlDoc = NULL;
                CodeAccessPermission::revertAssert();
            }
        }
        else
        {
            info("@GLS101555");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer         version        = conpeek(_packedClass, 1);
        container       packedQuery;

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedQuery] = _packedClass;

                if (SysQuery::isPackedOk(packedQuery))
                {
                    queryRun = new QueryRun(packedQuery);
                }
                else
                {
                    this.initQuery();
                }

                this.setQueryEmplRange();

                break;

            default:
                return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return #ReportTitle;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>