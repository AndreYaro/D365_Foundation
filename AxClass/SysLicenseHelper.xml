<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysLicenseHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class SysLicenseHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>codesModifiedSecurityReload</Name>
				<Source><![CDATA[
    [SubscribesTo(classstr(SysLicenseCodeReadFile), delegatestr(SysLicenseCodeReadFile, codesModifiedSecurityReloadDelegate))]
    public static void codesModifiedSecurityReload(boolean _allowSynchronize)
    {
        SysSecurity::reload(true, _allowSynchronize, true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postCodesModified</Name>
				<Source><![CDATA[
    [SubscribesTo(classstr(SysLicenseCodeReadFile), delegatestr(SysLicenseCodeReadFile, postCodesModifiedDelegate))]
    public static void postCodesModified()
    {
        Dictionary          dictionary = new Dictionary();
        boolean             sysSqlSyncValueSet=false;
        boolean             disableSyncWarnings=false;
        #define.SqlSyncOwner('SysSqlSync')
        #define.ShowSysSqlSync('showSysSqlSync')

        //Appl(System) tables have been synced at startup
        //New licensecodes requires them to be resynced
        if (! isRunningMode())
        {
            //For clean uninstall we do not want to show any sync warnings related to dropping fields
            //Sync errors will still be thrown
            disableSyncWarnings=appl.isConfigMode() && (!SysCheckList_Upgrade::isUpgradeMode());
            if (disableSyncWarnings)
            {
                if (appl.globalCache().elements(#SqlSyncOwner) > 0)
                {
                    //disable sync warnings only if there are no flags already set for SysSqlSync form
                    sysSqlSyncValueSet=true;
                }
                else
                {
                    // disable sync warnings from showing.
                    appl.globalCache().set(#SqlSyncOwner, #ShowSysSqlSync, false);
                }
            }

            if (disableSyncWarnings && !sysSqlSyncValueSet)
            {
                appl.globalCache().clear(#SqlSyncOwner);
            }
        }

        SysCheckList::finished(classNum(SysCheckListItem_LicenseCode));
        SysCheckList::finished(classNum(SysCheckListItem_LicenseCodeUpgrade));
        SysCheckList::finished(classNum(SysCheckListItem_LicenseCodeUpdate));
        SysCheckList::finished(classNum(SysCheckListItem_SysUpdateCodeLicense));
    }

]]></Source>
			</Method>
			<Method>
				<Name>showConfirmDialog</Name>
				<Source><![CDATA[
    [SubscribesTo(classstr(SysLicenseCodeReadFile), delegatestr(SysLicenseCodeReadFile, showConfirmDialogDelegate))]
    public static void showConfirmDialog(Set _licenseCodeSet, EventHandlerResult _ret)
    {
        int confCount = 0;
        FormRun confirmForm;
        Args            args    = new Args(formStr(SysLicenseCompareForm));

        confCount = SysLicenseCodeReadFile::findConfigKeysFromLicenseCodeSet(_licenseCodeSet);

        // if we need to remove config key, let's show the dialog
        // and let customer decide whether to proceed
        if (confCount > 0 )
        {
            args.menuItemName(menuItemDisplayStr(SysLicenseCompareForm));
            confirmForm = classfactory.formRunClass(args);

            confirmForm.init();
            confirmForm.run();
            confirmForm.wait();

            if ( confirmForm.closedCancel() )
                _ret.result(false);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>