<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTableTemplateSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The InventTableTemplateSubmitManager menu item action event handler.
/// </summary>
public class InventTableTemplateSubmitManager 
{
    Args args;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    protected Args parmArgs(Args _args = args)
    {
        args = _args;
        return args;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
	{
        InventTableTemplateSubmitManager journalWFSubmit = new InventTableTemplateSubmitManager();

        journalWFSubmit.parmArgs(_args);
        journalWFSubmit.run();
	}

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handle the submit operation of workflow.
    /// </summary>
    protected void run()
    {
        RecId                           recId               = args.record().RecId;
        TableId                         tableId             = args.record().TableId;
        InventJournalTable              inventJournalTable  = args.record();
        InventJournalName               inventJournalName   = inventJournalTable.inventJournalName();
        WorkflowCorrelationId           workflowCorrelationId;
        WorkflowComment                 comment;
        WorkflowSubmitDialog            workflowSubmitDialog;

        // The method has not been called correctly.
        if (tableId != tableNum(InventJournalTable) ||
            recId == 0)
        {
            throw error(strFmt("@SYS19306", funcName()));
        }

        if (inventJournalName.RecId != 0 &&
            inventJournalName.checkWorkflowApprovalEnabled())
        {
            // The journal does support workflow approvals.
            // Capture any comments/notes attached to the submission.
            workflowSubmitDialog = WorkflowSubmitDialog::construct(args.caller().getActiveWorkflowConfiguration());
            workflowSubmitDialog.run();

            InventJournalTrans inventJournalTrans;
            
            //Throwing an error message if the reference was changed after the submit dialog box opens and before click on submit button
            if (recId != inventJournalTable.RecId && workflowSubmitDialog.parmIsClosedOK())
            {
                throw error(strFmt("@SYS111653", inventJournalTable.JournalNameId, inventJournalTable.JournalId));
            }

            if (workflowSubmitDialog.parmIsClosedOK() &&
                inventJournalTable.WorkflowApprovalStatus == InventJournalWorkflowApprovalStatus::NotSubmitted)
            {
                comment = workflowSubmitDialog.parmWorkflowComment();

                try
                {
                    ttsbegin;
                    // Start the workflow.
                    // This process does not record the WorkflowCorrelationId.
                    // Create a new workflow from the workflow template used on the workflow configuration associated to the journal's journal name.
                    workflowCorrelationId = Workflow::activateFromWorkflowSequenceNumber(inventJournalName.Configuration, recId, comment, NoYes::No);

                    // Set the workflow status to Submitted.
                    InventJournalWorkflowApprovalStatusManager::submitFromForm(inventJournalTable);
                    ttscommit;
                }
                catch(Exception::Error)
                {
                    throw error(strFmt("@SYS111653", inventJournalTable.JournalNameId, inventJournalTable.JournalId));
                }
            }
        }
        else
        {
            try
            {
                // The journal does not support workflow approvals.
                // Reset the workflow status to "None" because the journal is no longer workflow enabled.
                InventJournalWorkflowApprovalStatusManager::noneFromForm(inventJournalTable);

                // Notify the user that the journal's workflow status has been reset to "None".
                info("@SYS111654");
            }
            catch(Exception::Error)
            {
                throw error(strFmt("@SYS111655", inventJournalTable.JournalNameId, inventJournalTable.JournalId));
            }
        }

        // Make the form refresh its common workflow UI controls.
        args.caller().updateWorkflowControls();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>