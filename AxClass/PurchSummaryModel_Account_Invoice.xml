<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchSummaryModel_Account_Invoice</Name>
	<SourceCode>
		<Declaration><![CDATA[
class PurchSummaryModel_Account_Invoice extends SalesPurchSummaryModel_Account
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addDataSources</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds data sources to the query during the query build process.
    /// </summary>
    /// <param name="_queryRun">
    /// The query that is being built.
    /// </param>
    /// <remarks>
    /// This method overrides the method in the base class.
    /// </remarks>
    public void addDataSources(QueryRun _queryRun)
    {
        _queryRun.query().dataSourceTable(orderSummary.tableNumParmTable()).addDataSource(orderSummary.tableNumSalesPurchParmTable(), tableid2name(orderSummary.tableNumSalesPurchParmTable()));
        _queryRun.query().dataSourceTable(orderSummary.tableNumSalesPurchParmTable()).relations(true);
        _queryRun.query().dataSourceTable(orderSummary.tableNumSalesPurchParmTable()).fetchMode(0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addOrderTableOrderByField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs when an order by field is being added to the order table data source.
    /// </summary>
    /// <param name="_queryRun">
    /// The query that is being built, which includes the order table data source.
    /// </param>
    /// <param name="_fieldId">
    /// The field from the order table that is being added to the sort order.
    /// </param>
    /// <remarks>
    /// This method overrides the method in the base class.
    /// </remarks>
    public void addOrderTableOrderByField(QueryRun _queryRun, FieldId _fieldId)
    {
        FieldId invoiceFieldId = VendInvoiceInfoTable::convertPurchTableFieldToVendInvoice(fieldExt2Id(_fieldId));

        if (invoiceFieldId != 0)
        {
            // To include non-PO invoices in the query in the right order, add the same order fields to parmTable, if they exist
            _queryRun.query().dataSourceTable(orderSummary.tableNumSalesPurchParmTable()).addOrderByField(new SysDictField(tablenum(VendInvoiceInfoTable), invoiceFieldId).id());
        }
        else
        {
            super(_queryRun, _fieldId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query that must be used in the summary update process.
    /// </summary>
    /// <param name="_isInBatch">
    /// true if the query is being built in a batch; otherwise, false.
    /// </param>
    /// <returns>
    /// The query that must be used in the summary update process.
    /// </returns>
    /// <remarks>
    /// This method overrides the method in the base class.
    /// </remarks>
    QueryRun queryBuild(boolean _isInBatch = false)
    {
        QueryRun queryRun = super(_isInBatch);

        queryRun.query().dataSourceTable(orderSummary.tableNumOrderTable()).joinMode(JoinMode::OuterJoin);

        return queryRun;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>