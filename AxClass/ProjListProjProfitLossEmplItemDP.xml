<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjProfitLossEmplItemDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjProfitLossEmplItemDP</c> class provides data for the
///    <c>ProjListProjProfitLossEmplItem</c> report.
/// </summary>
[
    SRSReportQueryAttribute(queryStr(ProjStatementEmplItem)),
    SRSReportParameterAttribute(classStr(ProjListProjProfitLossProjContract))
]

class ProjListProjProfitLossEmplItemDP extends SrsReportDataProviderPreProcessTempDB
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    ProjOptionRevenue       revenue;
    ProjOptionOnAcc         onAcc;
    TransDate               projFromDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    ProjListColumnData      actualVsBudget;
    ForecastModelId         forecastModelId;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;
    ProjListProfitFactor    profitFactor;

    ProjListProjTrans_ProfitLoss    listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjHelpClass           projHelpClass;
    ProjListProjProfitLossTmp       projListProjProfitLossTmp;
    projStatement                   projStatement;
    ProjParameters                  parameters;

    QueryRun                qrun;

    ProjEmplItemId          id;
    Name                    name;
    ResourceRecId           resource;

    ProjListIDType          projListIDType;

    ProjQty                 tmpQtyHour;
    AmountMST               tmpSalary, tmpExpenses, tmpInventory, tmpTotalCost, tmpRevenue, tmpValueAdded, tmpMargin;

    int                     hourQtyDecimals, valueAddedDecimals, grossMarginDecimals, amountDecimals;
    int                     showContributionMargin, showCostExpense, showCostHour, showCostInventory, showGrossMarginPct,
                            showQtyHour, showRevenue, showTotalCost, showValueAdded, showValueFactor;
    str                     valueFactorLabel,grossMarginPctLabel, headerLabel, periodLabel, idLabel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines the amounts that are needed for the report.
    /// </summary>
    void calculate()
    {
        if (projListIDType == ProjListIDType::Employee)
        {
            projStatement.parmResource(resource);
            projStatement.parmEmplItemId('');
        }
        else
        {
            projStatement.parmResource(0);
            projStatement.parmEmplItemId(id);
        }

        switch (amountQty)
        {
            case ProjAmountQty::Amount:
                tmpSalary      = projStatement.a310_Cost_Hour(false,ProjListStateType::ProfitLoss);
                break;
            case ProjAmountQty::Qty:
                tmpQtyHour     = projStatement.a310_Cost_Hour_Qty(false,ProjListStateType::ProfitLoss);
                break;
            case ProjAmountQty::AmountQty:
                tmpSalary      = projStatement.a310_Cost_Hour(false,ProjListStateType::ProfitLoss);
                tmpQtyHour     = projStatement.a310_Cost_Hour_Qty(false,ProjListStateType::ProfitLoss);
                break;
        }

        tmpExpenses    = projStatement.a330_Cost_Expenses()+
                  projStatement.a335_Accrued_Cost();
        tmpInventory   = projStatement.a315_Item_Total();
        tmpTotalCost   = projStatement.a305_Cost();
        tmpRevenue     = projStatement.a100_Total_Revenue();
        tmpValueAdded  = projStatement.a400_Value_Added();
        tmpMargin      = projStatement.a500_Gross_Margin();
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateNoId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines the amounts for blank IDs.
    /// </summary>
    void calculateNoId()
    {
        projStatement.parmResource(0);
        projStatement.parmEmplItemId(SysQuery::valueEmptyString());

        tmpExpenses    = projStatement.a330_Cost_Expenses()+
                  projStatement.a335_Accrued_Cost();
        tmpTotalCost   = projStatement.a305_Cost();
        tmpRevenue     = projStatement.a100_Total_Revenue();
        tmpValueAdded  = projStatement.a400_Value_Added();
        tmpMargin      = projStatement.a500_Gross_Margin();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the data from the <c>projListProjProfitLossTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>projListProjProfitLossTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('ProjListProjProfitLossTmp')]
    public ProjListProjProfitLossTmp getProjListProjProfitLossTmp()
    {
        select * from projListProjProfitLossTmp;
        return projListProjProfitLossTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>ProjListProjTrans</c> class by using the parameters.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans = ProjListProjTrans::construct(ProjListStateType::ProfitLoss, ProjListStateId::EmplItem);
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);
        listProjTrans.parmTime(time);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);
        listProjTrans.parmRevenue(revenue);
        listProjTrans.parmOnAcc(onAcc);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjToDate(projToDate);
        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmColumnData(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);
        listProjTransLayout.parmProfitFactor(profitFactor);

        listProjTransLayout.parmLevel(2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjProfitLossTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjProfitLossTmp()
    {
        projListProjProfitLossTmp.clear();
        projListProjProfitLossTmp.Id            = id;
        projListProjProfitLossTmp.IdLabel       = idLabel;
        projListProjProfitLossTmp.Name          = name;
        projListProjProfitLossTmp.ProjListIdType = projListIDType;

        projListProjProfitLossTmp.QtyHour       = tmpQtyHour;

        projListProjProfitLossTmp.CostHour      = listProjTransLayout.showAmountReport(tmpSalary, showAmount);
        projListProjProfitLossTmp.CostExpense   = listProjTransLayout.showAmountReport(tmpExpenses, showAmount);
        projListProjProfitLossTmp.CostInventory = listProjTransLayout.showAmountReport(tmpInventory, showAmount);
        projListProjProfitLossTmp.TotalCost     = listProjTransLayout.showAmountReport(tmpTotalCost, showAmount);
        projListProjProfitLossTmp.Revenue       = listProjTransLayout.showAmountReport(tmpRevenue, showAmount);
        projListProjProfitLossTmp.ValueAdded    = listProjTransLayout.showAmountReport(tmpValueAdded, showAmount);
        projListProjProfitLossTmp.ContributionMargin = listProjTransLayout.showAmountReport(tmpMargin, showAmount);

        projListProjProfitLossTmp.ValueFactor   = this.valueFactor();
        projListProjProfitLossTmp.GrossMarginPct= ProjParameters::grossMarginPct(tmpTotalCost,tmpRevenue,tmpMargin);

        // set labels
        projListProjProfitLossTmp.HeaderLabel   = headerLabel;
        projListProjProfitLossTmp.PeriodLabel   = periodLabel;
        projListProjProfitLossTmp.GrossMarginPctLabel = grossMarginPctLabel;
        projListProjProfitLossTmp.ValueFactorLabel = valueFactorLabel;

        // set decimal values
        projListProjProfitLossTmp.DecimalHourQty     = hourQtyDecimals;
        projListProjProfitLossTmp.DecimalValueAdded  = valueAddedDecimals;
        projListProjProfitLossTmp.DecimalGrossMargin = grossMarginDecimals;
        projListProjProfitLossTmp.DecimalAmount      = amountDecimals;

        // set field visibility
        projListProjProfitLossTmp.ShowContributionMargin = showContributionMargin;
        projListProjProfitLossTmp.ShowCostExpense        = showCostExpense;
        projListProjProfitLossTmp.ShowCostHour           = showCostHour;
        projListProjProfitLossTmp.ShowCostInventory      = showCostInventory;
        projListProjProfitLossTmp.ShowGrossMarginPct     = showGrossMarginPct;
        projListProjProfitLossTmp.ShowQtyHour            = showQtyHour;
        projListProjProfitLossTmp.ShowRevenue            = showRevenue;
        projListProjProfitLossTmp.ShowTotalCost          = showTotalCost;
        projListProjProfitLossTmp.ShowValueAdded         = showValueAdded;
        projListProjProfitLossTmp.ShowValueFactor        = showValueFactor;

        projListProjProfitLossTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks whether the amounts are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise false.
    /// </returns>
    boolean lineValue()
    {
        return (tmpExpenses  != 0 || tmpSalary != 0 || tmpInventory != 0 || tmpRevenue != 0 || tmpQtyHour != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>printEmplItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the resource and item records and calls the calculate method.
    /// </summary>
    void printEmplItem()
    {
        ResourceView            resourceLocal;
        InventTable             itemTableLocal;
        SMASubscriptionTable    smaTableLocal;

        if (SysReportRun::changed(qrun,tableNum(ResourceView)))
        {
            resourceLocal  = qrun.get(tableNum(ResourceView));

            resource          = resourceLocal.RecId;
            id              = ResourceFacade::getID(resource);
            name            = ResourceFacade::getName(resource);
            projListIDType  = ProjListIDType::Employee;
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(LogisticsBasic)) && SysReportRun::changed(qrun,tableNum(InventTable)))
        {
            itemTableLocal  = qrun.get(tableNum(InventTable));

            id              = itemTableLocal.ItemId;
            name            = itemTableLocal.defaultProductName();
            projListIDType  = ProjListIDType::Item;
        }

        if (isConfigurationkeyEnabled(configurationKeyNum(SMASubscription)) && SysReportRun::changed(qrun,tableNum(SMASubscriptionTable)))
        {
            smaTableLocal   = qrun.get(tableNum(SMASubscriptionTable));

            id              = smaTableLocal.SubscriptionId;
            name            = smaTableLocal.Name;
            projListIDType  = ProjListIDType::Subscription;
        }

        this.calculate();

        if (! skipZero || (this.lineValue()))
        {
            this.insertProjListProjProfitLossTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SQL
    ///    Server Reporting Services (SSRS) at runtime. This method should compute data and populate the data
    ///    tables that will be returned to SSRS.
    /// </remarks>

    public void processReport()
    {
        ProjListProjProfitLossProjContract contract = this.parmDataContract() as ProjListProjProfitLossProjContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();
        time = contract.parmTime();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();
        revenue = contract.parmRevenue();
        onAcc = contract.parmOnAcc();

        projFromDate = contract.parmProjFromDate();
        projToDate = contract.parmProjToDate();
        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();

        actualVsBudget = contract.parmActualVsBudget();
        forecastModelId = contract.parmForecastModelIdV2();

        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();
        profitFactor = contract.parmProfitFactor();
        parameters = ProjParameters::find();

        this.initProjListTrans();
        this.setIdLabel();

        headerLabel   = listProjTrans.formatSelection(showAmount);
        periodLabel   = listProjTrans.formatPeriodLabel('',DateFlags::None);

        [valueFactorLabel, grossMarginPctLabel] = listProjTrans.setDisplayLabel(profitFactor);

        [showQtyHour, showCostHour, showCostExpense, showCostInventory,
     showTotalCost, showRevenue, showContributionMargin,
     showGrossMarginPct, showValueAdded, showValueFactor] = listProjTrans.setDisplayFlags(hour, expenses,
                                                                                          item, amountQty,
                                                                                          profitFactor);

        [hourQtyDecimals, amountDecimals,
    grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        projStatement = projStatement::newProjListProfitLoss(
                    listProjTrans,
                    ProjListStateProjType::Project,
                    projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                    amountQty,
                    forecastModelId);

        qrun = new QueryRun(this.parmQuery());

        //  print lines with no Id eg. cost and revenue with no Employee
        if (! skipBlankId)
        {
            this.calculateNoId();
            if (this.lineValue())
            {
                this.insertProjListProjProfitLossTmp();
            }
        }
        while (qrun.next())
        {
            this.printEmplItem();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setIdLabel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the label for the Id.
    /// </summary>
    void setIdLabel()
    {
        if ((hour || revenue || expenses) && !(item || onAcc))
        {
            idLabel = "@Res:Resource";
        }
        else if ((item || onAcc) && !(expenses || hour || revenue))
        {
            idLabel = "@SYS14428";
        }
        else
        {
            idLabel = "@Res:ResourceItem";
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>valueFactor</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the value-added factor based on the project parameter setup.
    /// </summary>
    /// <returns>
    ///    The value-added factor.
    /// </returns>
    private ProjFactorValueAdded valueFactor()
    {
        switch (parameters.ValueAddedDisplay)
        {
            case ProjValueAddedDisplay::ValueAddedMarkup:
            if (tmpSalary)
                {
                    return (tmpValueAdded / tmpSalary);
                }
                break;

            case ProjValueAddedDisplay::ValueAddedRation:
            if (tmpRevenue)
                {
                    return (tmpValueAdded * 100 / tmpRevenue);
                }
                break;

            case ProjValueAddedDisplay::ValueAddedMarupPct:
            if (tmpSalary)
                {
                    return (tmpValueAdded * 100 / tmpSalary);
                }
                break;
        }

        return 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>