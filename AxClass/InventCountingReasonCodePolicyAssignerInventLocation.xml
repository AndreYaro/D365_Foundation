<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventCountingReasonCodePolicyAssignerInventLocation</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventCountingReasonCodePolicyAssignerInventLocation</c> class is used for assigning counting reason code for an warehouse.
/// </summary>
[SysTableNameFactory(tableStr(InventLocation))]
public class InventCountingReasonCodePolicyAssignerInventLocation extends InventCountingReasonCodePolicyAssigner
{
    private InventLocation inventLocation;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes invent location table buffer.
    /// </summary>
    /// <param name = "_common">
    /// <c>Common</c> buffer used for this instance of the table.
    /// </param>
    public void initFromBuffer(Common _common)
    {
        inventLocation = _common as InventLocation;

        if (!inventLocation.InventLocationId)
        {
            this.throwRequiredFieldIdHasNoValue(fieldId2PName(tableNum(InventLocation), fieldNum(InventLocation, InventLocationId)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>assignPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Assigns counting reason code policy rec id to a warehouse .
    /// </summary>
    /// <param name = "_inventCountingReasonCodePolicyRecId">
    /// The counting reason code policy rec id.
    /// </param>
    /// <returns>
    /// true if the <c>InventCountingReasonCodePolicyRecId</c> is updated on the warehouse; otherwise false
    /// </returns>
    public boolean assignPolicy(InventCountingReasonCodePolicyRecId _inventCountingReasonCodePolicyRecId)
    {
        ttsbegin;

        InventLocationInventCountingReasonCodePolicy inventLocationInventCountingReasonCodePolicy = InventLocationInventCountingReasonCodePolicy::findByInventLocation(inventLocation.InventLocationId, true);

        boolean ret;

        if (_inventCountingReasonCodePolicyRecId != inventLocationInventCountingReasonCodePolicy.CountingReasonCodePolicy)
        {
            ret = true;

            if (_inventCountingReasonCodePolicyRecId != 0)
            {
                if (!inventLocationInventCountingReasonCodePolicy)
                {
                    inventLocationInventCountingReasonCodePolicy.InventLocationId = inventLocation.InventLocationId;
                }

                inventLocationInventCountingReasonCodePolicy.CountingReasonCodePolicy = _inventCountingReasonCodePolicyRecId;
                inventLocationInventCountingReasonCodePolicy.write();
            }
            else
            {
                inventLocationInventCountingReasonCodePolicy.delete();
            }

        }

        ttscommit;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventCountingReasonCodePolicyRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the counting reason code policy for the selected warehouse.
    /// </summary>
    /// <returns>
    /// The counting reason code policy rec id assigned to the warehouse.
    /// </returns>
    public InventCountingReasonCodePolicyRecId inventCountingReasonCodePolicyRecId()
    {
        return InventLocationInventCountingReasonCodePolicy::findByInventLocation(inventLocation.InventLocationId).CountingReasonCodePolicy;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>