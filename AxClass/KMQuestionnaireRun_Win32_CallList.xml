<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>KmQuestionnaireRun_Win32_CallList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>KMQuestionnaireRun_Win32_CallList</c> class manages the questionnaire answer session for the
///    call lists.
/// </summary>
public class KmQuestionnaireRun_Win32_CallList extends KMQuestionnaireRun_Win32
{
    smmTMCallListTable  smmTMCallListTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>end</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves a completed questionnaire when the End button is clicked.
    /// </summary>
    /// <param name="_kmVirtualNetworkAnswerTableId">
    /// The table ID of the <c>kmVirtualNetworkAnswerTable</c> table record.
    /// </param>
    public void end(KMVirtualNetworkAnswerTableId _kmVirtualNetworkAnswerTableId)
    {
        smmTMCallListTable          smmTMCallListTableUpdate;
        smmTMContactResponses       smmTMContactResponses;
        FormDataSource              ds;

        super(_kmVirtualNetworkAnswerTableId);

        ttsbegin;
        //--- Select the record for update
        smmTMCallListTableUpdate = smmTMCallListTable::findCallListContact(smmTMCallListTable.CallListId, smmTMCallListTable.ContactPersonId, true);

        if (smmTMCallListTableUpdate)
        {
            if (_kmVirtualNetworkAnswerTableId)  // If response exists
            {
                smmTMContactResponses.ContactPersonId = smmTMCallListTableUpdate.ContactPersonId;
                smmTMContactResponses.Party   = smmTMCallListTableUpdate.Party;
                smmTMContactResponses.KmVirtualNetworkAnswerTableId = _kmVirtualNetworkAnswerTableId;
                smmTMContactResponses.insert();

                smmTMCallListTableUpdate.kmVirtualNetworkAnswerTableId = _kmVirtualNetworkAnswerTableId;

                if (smmTMCallListTableUpdate.validateWrite())
                {
                    smmTMCallListTableUpdate.update();
                }

                //--- Update datasource on underlying form SmmTMCallListTable
                ds = FormDataUtil::getFormDataSource(smmTMCallListTable);
                if (ds)
                {
                    ds.executeQuery();
                }
            }
        }
        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>KMQuestionnaireRun_Win32_CallList</c> class.
    /// </summary>
    /// <param name="_callListTable">
    /// A <c>smmTMCallListTable</c> value.
    /// </param>
    protected void new(smmTMCallListTable _callListTable)
    {
        smmTMCallListTable = _callListTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args _args)
    {
        KMQuestionnaireRun_Win32_CallList   kmQuestionnaireRun;
        HcmPersonRecId                      person;
        smmTMCallListTable                  smmTMCallListTable = _args.record();
        KMCollectionId                      kmCollectionId = smmTMCallListGroup::find(smmTMCallListTable.CallListId).kmCollectionId;

        if (isConfigurationkeyEnabled(configurationkeynum(KMQuestionnaireBasic)))
        {
            person = ContactPerson::find(smmTMCallListTable.ContactPersonId).Party;
        }

        kmQuestionnaireRun = new KMQuestionnaireRun_Win32_CallList(smmTMCallListTable);

        if (kmQuestionnaireRun.set(KMQuestionnaireRunMode::All, kmCollectionId, person))
        {
            kmQuestionnaireRun.run();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>