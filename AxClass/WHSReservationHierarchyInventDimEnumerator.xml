<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSReservationHierarchyInventDimEnumerator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Allows to traverse inventory dimensions reservation hierarchy bottom-up for a given item.
/// </summary>
internal final class WHSReservationHierarchyInventDimEnumerator
{
    #WHSReservationHierarchy

    private InventDim                       inventDimWorkingCopy;
    private WHSReservationHierarchyLevel    upperHierarchyLevel;
    private List                            hierarchyList;
    private ListEnumerator                  hierarchyListEnumerator;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>current</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retuns inventory dimension corresponding to the current position of the enumerator.
    /// </summary>
    /// <returns>Current inventory dimension.</returns>
    [Hookable(false)]
    public InventDim current()
    {
        return inventDimWorkingCopy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>moveNext</Name>
				<Source><![CDATA[
    /// <summary>
    /// Moves enumerator to the next level of the reservation hierarchy.
    /// </summary>
    /// <returns>true if was able to move to the next level; false otherwise.</returns>
    [Hookable(false)]
    public boolean moveNext()
    {
        if (!hierarchyListEnumerator)
        {
            hierarchyListEnumerator = hierarchyList.getEnumerator();
        }

        while (hierarchyListEnumerator.moveNext())
        {
            WHSReservationHierarchyElementData hierarchyElement = hierarchyListEnumerator.current();
            FieldId inventDimFieldId = hierarchyElement.parmDimensionFieldId();

            // We are iterating bottom-up and upper level is represented by a lower number.
            if (hierarchyElement.parmLevel() <= upperHierarchyLevel)
            {
                break;
            }

            if (inventDimWorkingCopy.(inventDimFieldId) != InventDim::emptyDimField(inventDimFieldId))
            {
                inventDimWorkingCopy.(inventDimFieldId) = InventDim::emptyDimField(inventDimFieldId);

                InventDim foundInventDim = InventDim::findDim(inventDimWorkingCopy);
                if (foundInventDim)
                {
                    inventDimWorkingCopy = foundInventDim;
                    return true;
                }
            }
        }

        inventDimWorkingCopy.clear();
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the <c>WHSReservationHierarchyInventDimEnumerator</c> class.
    /// </summary>
    /// <param name = "_inventTable">An item for which to find a reservation hierarchy.</param>
    /// <param name = "_inventDimCriteria">A starting inventory dimension from which to traverse the reservation hierarchy.</param>
    /// <param name = "_upperReservationHierarchyLevel">An upper level of the reservation hierarchy to look.</param>
    public void new(InventTable _inventTable, InventDim _inventDimCriteria, WHSReservationHierarchyLevel _upperReservationHierarchyLevel)
    {
        this.init(_inventTable, _inventDimCriteria, _upperReservationHierarchyLevel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    private void init(InventTable _inventTable, InventDim _inventDimCriteria, WHSReservationHierarchyLevel _upperReservationHierarchyLevel)
    {
        inventDimWorkingCopy.data(_inventDimCriteria);
        upperHierarchyLevel = _upperReservationHierarchyLevel;

        hierarchyList = WHSReservationHierarchyProvider::construct().getDimListAllFromInventTable(_inventTable, WHSReservationHierarchySortOrder::BottomUp);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>