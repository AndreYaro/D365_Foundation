<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResStorageDimensionGroupItem_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>EcoResStorageDimensionGroupItem_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class EcoResStorageDimensionGroupItem_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static EcoResStorageDimensionGroupItem_DataSharingTableEvents construct()
    {
        return new EcoResStorageDimensionGroupItem_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = EcoResStorageDimensionGroupItem_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Inserted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);
                
                EcoResStorageDimensionGroupItem origStorageDimensionGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;
                EcoResStorageDimensionGroupItem storageDimensionGroupItem;
                buf2Buf(origStorageDimensionGroupItem, storageDimensionGroupItem);
                storageDimensionGroupItem.ItemDataAreaId = _dataSharingCompany;
                if (!storageDimensionGroupItem.validateWrite())
                {
                    throw error(strFmt("@SCM:SysDataSharingCannotInsertRecordInTable", tablePName(EcoResStorageDimensionGroupItem), _dataSharingCompany));
                }
                storageDimensionGroupItem.insert();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Deleted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                EcoResStorageDimensionGroupItem origStorageDimensionGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                EcoResStorageDimensionGroupItem storageDimensionGroupItem = EcoResStorageDimensionGroupItem::findByItem(_dataSharingCompany, origStorageDimensionGroupItem.ItemId, true);
                if (storageDimensionGroupItem.RecId)
                {
                    // validateDelete has a check if warehouse item has reservation hierarchy exist, if deleting an item then casade delete will not call validateDelete, 
                    // so introducing validateDelete here would cause a regression. Workaround is to remove the reservation hierarchy from the item before deleting the storage group.
                    storageDimensionGroupItem.delete();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);
                EcoResStorageDimensionGroupItem origStorageDimensionGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                EcoResStorageDimensionGroupItem storageDimensionGroupItem = EcoResStorageDimensionGroupItem::findByItem(_dataSharingCompany, origStorageDimensionGroupItem.ItemId, true);
                if (storageDimensionGroupItem.RecId)
                {
                    buf2Buf(origStorageDimensionGroupItem, storageDimensionGroupItem);
                    storageDimensionGroupItem.ItemDataAreaId = _dataSharingCompany;
                    storageDimensionGroupItem.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>EcoResStorageDimensionGroupItem_onInserted</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of EcoResStorageDimensionGroupItem table
    [DataEventHandler(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Inserted)]
    public static void EcoResStorageDimensionGroupItem_onInserted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Inserted);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorStorageDimensionGroupItem = EcoResStorageDimensionGroupItem_DataSharingTableEvents::construct();
                tableEventsProcessorStorageDimensionGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorStorageDimensionGroupItem);
                using (var contextInsertCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Inserted))
                {
                    processor.insertCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>EcoResStorageDimensionGroupItem_onDeleted</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Deleted)]
    public static void EcoResStorageDimensionGroupItem_onDeleted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Deleted);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorStorageDimensionGroupItem = new EcoResStorageDimensionGroupItem_DataSharingTableEvents();
                tableEventsProcessorStorageDimensionGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorStorageDimensionGroupItem);
                using (var contextDeleteCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Deleted))
                {
                    processor.deleteCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>EcoResStorageDimensionGroupItem_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Updated)]
    public static void EcoResStorageDimensionGroupItem_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Updated);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorStorageDimensionGroupItem = new EcoResStorageDimensionGroupItem_DataSharingTableEvents();
                tableEventsProcessorStorageDimensionGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorStorageDimensionGroupItem);
                using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(EcoResStorageDimensionGroupItem), DataEventType::Updated))
                {
                    processor.updateCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>