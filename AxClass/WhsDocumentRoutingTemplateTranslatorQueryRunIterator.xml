<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WhsDocumentRoutingTemplateTranslatorQueryRunIterator</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WhsDocumentRoutingTemplateTranslatorQueryRunIterator extends WhsDocumentRoutingTemplateTranslatorIterator
{
    private QueryRun currentQueryRun;
    private int rowTableDSNo;
    private RecId currentRecIdOfRowTable;
    private RecId translatorLastUpdatedByRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromTemplateTranslatorAndQueryRun</Name>
				<Source><![CDATA[
    internal static WhsDocumentRoutingTemplateTranslatorQueryRunIterator newFromTemplateTranslatorAndQueryRun(WhsDocumentRoutingTemplateTranslator _templateTranslator, QueryRun _queryRun, str _rowTableDSName)
    {
        WhsDocumentRoutingTemplateTranslatorQueryRunIterator iterator = new WhsDocumentRoutingTemplateTranslatorQueryRunIterator(_templateTranslator);
        iterator.currentQueryRun = _queryRun;        
        iterator.resolveRowTableDataSource(_rowTableDSName);

        return iterator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>moveNext</Name>
				<Source><![CDATA[
    internal boolean moveNext()
    {       
        Common currentRowRecord;

        do
        {
            boolean hasMoreData = currentQueryRun.next();

            if (!hasMoreData)
            {
                return false;
            }            

            currentRowRecord = currentQueryRun.getNo(rowTableDSNo);
        }
        while (currentRecIdOfRowTable == currentRowRecord.RecId);

        currentRecIdOfRowTable = currentRowRecord.RecId;

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTranslatorFromIterator</Name>
				<Source><![CDATA[
    internal void updateTranslatorFromIterator(WhsDocumentRoutingTranslator _translator)
    {
        if (translatorLastUpdatedByRecId && currentRecIdOfRowTable == translatorLastUpdatedByRecId)
        {
            return;
        }

        int dataSourceCount = currentQueryRun.query().dataSourceCount();

        for (int dsNo = 1; dsNo <= dataSourceCount; dsNo++)
        {
            QueryBuildDataSource currentQbds = currentQueryRun.query().dataSourceNo(dsNo);
            if (currentQbds && currentQbds.enabled())
            {
                Common record = currentQueryRun.getNo(dsNo);

                _translator.withRecord(record, currentQbds.name());

                if (dsNo == rowTableDSNo)
                {
                    translatorLastUpdatedByRecId = record.RecId;
                }
            }            
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolveRowTableDataSource</Name>
				<Source><![CDATA[
    private void resolveRowTableDataSource(str _rowTableDSName)
    {
        QueryBuildDataSource rowTableDS;

        if (!_rowTableDSName)
        {
            rowTableDS = currentQueryRun.query().dataSourceNo(1);
        }
        else
        {
            rowTableDS = currentQueryRun.query().dataSourceName(_rowTableDSName);
        }
        
        if (rowTableDS)
        {
            rowTableDSNo = rowTableDS.id();
        }
        else
        {
            throw Error(strFmt("@WAX:WhsDocumentRoutingTemplateTranslator_RowTableInvalid", _rowTableDSName));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>