<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmEmployeeV2EntityEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains event handlers for the HcmEmployeeV2Entity entity.
/// </summary>
class HcmEmployeeV2EntityEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmEmployeeV2Entity_onMappedEntityToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity onMappedEntityToDataSource event handler for HcmEmployeeV2Entity extension
    /// </summary>
    /// <param name="_sender">HcmEmployeeV2 Entity raising the event</param>
    /// <param name="_eventArgs">The args of this event</param>
    [DataEventHandler(tableStr(HcmEmployeeV2Entity), DataEventType::MappedEntityToDataSource)]
    public static void HcmEmployeeV2Entity_onMappedEntityToDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();
        HcmEmployeeV2Entity employeeEntity = _sender as HcmEmployeeV2Entity;

        if (dataSourceCtx.name() == dataentitydatasourcestr(HcmEmployeeV2Entity, WorkCalendarEmployment))
        {
            employeeEntity.skipDataSourceValidateField(fieldNum(HcmEmployeeV2Entity, CalendarId), true);
            employeeEntity.skipDataSourceValidateField(fieldNum(HcmEmployeeV2Entity, CalendarDataAreaId), true);

            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();
            workCalendarEmployment.CalendarDataAreaId = employeeEntity.EmploymentLegalEntityId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmployeeV2Entity_ValidatingField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event Handler for validate Field to return if company context should be changed while validating CalendarId field.
    /// </summary>
    /// <param name="_contractorEntity"><c>HcmEmployeeV2Entity</c> raising the event</param>
    /// <param name="_fieldIdToCheck">FieldId to validate</param>
    /// <param name="_changeCompanyContext">The EventHandlerResult the handler will use to return if company context should be changed before performing validation</param>
    [SubscribesTo(tableStr(HcmEmployeeV2Entity), delegateStr(HcmEmployeeV2Entity, ValidatingField))]
    public static void HcmEmployeeV2Entity_ValidatingField(Common _employeeEntity, FieldId _fieldIdToCheck, EventHandlerResult _changeCompanyContext)
    {
        HcmEmployeeV2Entity employeeEntity = _employeeEntity as HcmEmployeeV2Entity;

        if (_fieldIdToCheck == fieldNum(HcmEmployeeV2Entity, CalendarId)
             && employeeEntity.CalendarId != '')
        {
            _changeCompanyContext.booleanResult(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmployeeV2Entity_onInsertingEntityDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity InsertingEntityDataSource event handler for <c>HcmEmployeeV2Entity</c> extension.
    /// </summary>
    /// <param name="_sender">The <c>HcmEmployeeV2Entity</c> raising the event.</param>
    /// <param name="_eventArgs">The event args.</param>
    [DataEventHandler(tableStr(HcmEmployeeV2Entity), DataEventType::InsertingEntityDataSource)]
    public static void HcmEmployeeV2Entity_onInsertingEntityDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (dataSourceCtx.name() == dataEntityDataSourceStr(HcmEmployeeV2Entity, WorkCalendarEmployment))
        {
            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();

            if (workCalendarEmployment.Employment)
            {
                //
                // Conflict detection to resolve inserts into updates. This is required since <c>WorkCalendarEmployment</c> is 1-1 joined to <c>HcmEmployment</c>,
                // and the entity framework only resolves inserts into updates automatically for datasources in the 1 side on a 1-many relationship.
                //
                WorkCalendarEmployment existingRecord = WorkCalendarEmployment::findByEmploymentRecId(workCalendarEmployment.Employment, true);

                if (existingRecord)
                {
                    // Merge user modified fields into the existing record (excluding system fields).
                    buf2Buf(workCalendarEmployment, existingRecord);

                    // Set the datasource context buffer to the final merged record.
                    dataSourceCtx.setBuffer(existingRecord);
                    dataSourceCtx.setDatabaseOperation(DataEntityDatabaseOperation::Update);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>