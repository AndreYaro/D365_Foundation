<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppServiceDecoratorRuleInstruction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSMobileAppServiceDecoratorRuleInstruction</c> class populates the InstructionControl attribute.
/// This attribute is used to express that a field provides an instruction for a given input field.
/// </summary>
public class WHSMobileAppServiceDecoratorRuleInstruction implements WHSIMobileAppServiceXMLDecoratorRule
{
    #WHSRF
    #WHSWorkExecuteControlElements
    #WHSWorkExecuteDisplayCases
    #XmlDocumentation

    private WHSMobileAppControlName quantityControlName = '';
    private WHSMobileAppControlName itemControlName = '';
    private WHSMobileAppControlName locationControlName = '';
    private WHSMobileAppControlName qtyAndUoMControlName = '';

    private WHSFieldQuantity       quantityMobileAppField;
    private WHSFieldItem           itemMobileAppField;
    private WHSFieldLocation       locationMobileAppField;
    private WHSFieldQunatityAndUOM quantityAndUOMMobileAppField;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WHSMobileAppServiceDecoratorRuleInstruction construct()
    {
        return new WHSMobileAppServiceDecoratorRuleInstruction();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Method to run the transformation.
    /// </summary>
    /// <param name = "_pageInfo">
    /// The <c>WHSMobileAppPageInfo</c> containing controls and information to carry out the transformation.
    /// </param>
    public void run(WHSMobileAppPageInfo _pageInfo)
    {
        quantityMobileAppField       = new WHSFieldQuantity();
        itemMobileAppField           = new WHSFieldItem();
        locationMobileAppField       = new WHSFieldLocation();
        quantityAndUOMMobileAppField = new WHSFieldQunatityAndUOM();

        ListEnumerator le = _pageInfo.parmControlsEnumerator();
        while (le.moveNext())
        {
            Map controlMap = le.current();

            WHSMobileAppControlType controlType = controlMap.lookup(#XMLControlCtrlType);
            WHSMobileAppControlName controlName = controlMap.lookup(#XMLControlName);
            WHSMobileAppField controlMobileAppField = controlMap.lookup(#MobileAppField);

            if ((controlType == #RFText) || (controlType == #RFLabel))
            {
                WHSMobileAppControlName instructionControlName = this.getInstructionControlName(controlName);
                if (instructionControlName != '')
                {
                    controlMap.insert(#XMLControlInstructionControl, instructionControlName);
                }
                else
                {
                    this.setControlNamesForMobileAppFields(controlMobileAppField.name(), controlName);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInstructionControlName</Name>
				<Source><![CDATA[
    [Hookable]
    protected WHSMobileAppControlName getInstructionControlName(WHSMobileAppControlName _controlName)
    {
        switch (_controlName)
        {
            case #ProductConfirmation:
                return itemControlName;
            case #QtyVerification:
                if (quantityControlName != '')
                {
                    return quantityControlName;
                }
                return qtyAndUoMControlName;
            case #LocVerification:
            case #CheckDigit:
                return locationControlName;
            default:
                return '';
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setControlNamesForMobileAppFields</Name>
				<Source><![CDATA[
    [Hookable]
    protected void setControlNamesForMobileAppFields(WHSFieldName _controlFieldName, WHSMobileAppControlName _controlName)
    {
        switch (_controlFieldName)
        {
            case quantityMobileAppField.getName():
                quantityControlName = _controlName;
                break;
            case itemMobileAppField.getName():
                itemControlName = _controlName;
                break;
            case locationMobileAppField.getName():
                locationControlName = _controlName;
                break;
            case quantityAndUOMMobileAppField.getName():
                qtyAndUoMControlName = _controlName;
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>