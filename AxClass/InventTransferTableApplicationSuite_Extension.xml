<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransferTableApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTransferTableApplicationSuite_Extension</c> class is an extension class which extends the <c>InventTransferTable</c> table.
/// </summary>
[ExtensionOf(tableStr(InventTransferTable))]
internal final class InventTransferTableApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>modifiedField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles logic of field modification.
    /// </summary>
    /// <param name = "_fieldId">Field identifier.</param>
    public void modifiedField(FieldId _fieldId)
    {
        switch (_fieldId)
        {
            case fieldNum(InventTransferTable, CFDIEnabled_MX):
                this.CFDICartaPorteEnabled_MX = this.CFDIEnabled_MX;
                break;

            default:
                break;
        }

        next modifiedField(_fieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>editReasonCode</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets and sets the reason code.
    /// </summary>
    /// <param name="_set">Set to true if the reason code is set; otherwise, set to false.</param>
    /// <param name="_reasonCode">The new reason code when <paramref name="set" /> is true.</param>
    /// <returns>
    ///    The reason code of the record in the <c>ReasonTableRef</c> table.
    /// </returns>
    public edit ReasonCode editReasonCode(boolean _set, ReasonCode _reasonCode)
    {
        ReasonCode reasonCode = strLRTrim(_reasonCode);

        if (_set)
        {
            if (this.ReasonTableRef)
            {
                if (reasonCode)
                {
                    RefRecId originalReasonRefRecID = this.ReasonTableRef;
                    this.ReasonTableRef = ReasonTableRef::updateCode(reasonCode, this.ReasonTableRef);

                    if (this.ReasonTableRef == originalReasonRefRecID)
                    {
                        FormDataSource formInventTransferTable = FormDataUtil::getFormDataSource(this);
                        if (formInventTransferTable)
                        {
                            formInventTransferTable.forceWrite(true);
                        }
                    }
                }
                else
                {
                    this.ReasonTableRef = ReasonTableRef::updateCode(reasonCode, this.ReasonTableRef);
                }
            }
            else
            {
                this.ReasonTableRef = ReasonTableRef::createFromCode(reasonCode);
            }

            this.editReasonComment(true, ReasonTable::find(_reasonCode).Description);
        }
        else
        {
            reasonCode = ReasonTableRef::find(this.ReasonTableRef).Reason;
        }

        return reasonCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>editReasonComment</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets and sets the reason comment.
    /// </summary>
    /// <param name="_set">Set to true if the reason comment is set; otherwise, set to false.</param>
    /// <param name="_reasonComment">The new reason comment when <paramref name="_set" /> is true.</param>
    /// <returns>
    ///    The reason comment of the record in the <c>ReasonTableRef</c> table.
    /// </returns>
    public edit ReasonComment editReasonComment(boolean _set, ReasonComment _reasonComment)
    {
        ReasonComment reasonComment = _reasonComment;

        if (_set)
        {
            if (this.ReasonTableRef)
            {
                this.ReasonTableRef = ReasonTableRef::updateComment(reasonComment, this.ReasonTableRef);
            }
            else
            {
                this.ReasonTableRef = ReasonTableRef::createFromComment(reasonComment);
            }
        }
        else
        {
            reasonComment = ReasonTableRef::find(this.ReasonTableRef).ReasonComment;
        }

        return reasonComment;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>