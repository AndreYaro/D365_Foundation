<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxVATnumValidateDE</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class TaxVATnumValidateDE extends TaxVATNumValidate
{    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        #ISOCountryRegionCodes
        super();
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceDE00014, funcName());
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDigit</Name>
				<Source><![CDATA[
    private boolean validateDigit(VATNum _vatNum)
    {
        #define.Modulo10(10)
        #define.Modulo11(11)

        boolean ret = true;

        int     digit;
        int     result = #Modulo10;
        Counter pos;

        digit = str2int(substr(_vatNum, 1, 1));

        if (digit <= 0)
        {
            ret = checkFailed("@SYS84772");
        }

        if (ret)
        {
            for (pos = 1; pos < strlen(_vatNum); pos++)
            {
                result = (digit + result) mod #Modulo10;
                result = 2 * (result ? result : #Modulo10) mod 11;
                digit = str2int(substr(_vatNum, pos + 1, 1));
            }

            result = #Modulo11 - result;

            if (digit != (result == #Modulo10 ? 0 : result))
            {
                ret = checkFailed("@SYS84772");
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateVATNum</Name>
				<Source><![CDATA[
    boolean validateVATNum(VATNum _vatNum, LogisticsAddressCountryRegionId _countryRegionId = '')
    {
        boolean ret = true;
        ;

        if (_vatNum)
        {
            ret = this.isValidChars(_vatNum);

            if (ret)
            {
                ret = this.isNumeric(_vatNum,9);
            }

            if (ret)
            {
                ret = this.validateDigit(_vatNum);
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>