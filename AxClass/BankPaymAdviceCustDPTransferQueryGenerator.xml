<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankPaymAdviceCustDPTransferQueryGenerator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BankPaymAdviceCustDPTransferQueryGenerator</c> class generates the query used to transfer
/// report data for the batchable customer bank payment advice report. This query will copy data
/// from the <c>BankPaymAdviceCustTmp</c> table used in report generation to the <c>BankPaymAdviceCustV2Tmp</c> table
/// used for report processing.
/// </summary>
public final class BankPaymAdviceCustDPTransferQueryGenerator
{
    BankPaymAdviceCustTmp bankPaymAdviceCustTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createQuery</Name>
				<Source><![CDATA[
    internal SysDaInsertObject createQuery(BankPaymAdviceCustV2Tmp _bankPaymAdviceCustV2Tmp, guid _reportExecutionContext)
    {
        SysDaQueryObject queryObject = this.createQueryObject(_reportExecutionContext);
        this.addSelectionCriteria(queryObject, _reportExecutionContext);

        SysDaInsertObject insertObject = this.createInsertObject(_bankPaymAdviceCustV2Tmp);
        insertObject.query(queryObject);

        return insertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createInsertObject</Name>
				<Source><![CDATA[
    private SysDaInsertObject createInsertObject(BankPaymAdviceCustV2Tmp _bankPaymAdviceCustV2Tmp)
    {
        SysDaInsertObject bankPaymAdviceCustV2TmpInsertObject = new SysDaInsertObject(_bankPaymAdviceCustV2Tmp);
        this.addInsertionFields(bankPaymAdviceCustV2TmpInsertObject);

        return bankPaymAdviceCustV2TmpInsertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addInsertionFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the <c>BankPaymAdviceCustV2Tmp</c> table fields to an insert object.
    /// </summary>
    /// <param name = "_insertObject">
    /// The insert object to add the fields.
    /// </param>
    protected void addInsertionFields(SysDaInsertObject _insertObject)
    {
        _insertObject.fields()
            .add(fieldStr(BankPaymAdviceCustV2Tmp, AmountCur))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, Balance01))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CashDisc))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CompanyCoRegNum))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CompanyGiro))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CompanyPhone))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CompanyTelefax))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CurrencyCode))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, Grouping))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, Invoice))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, InvoiceDate))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, PaymAdviceDate))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, PaymentReference))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, PaymRefLabel))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, SettledCurrencyCode))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, TransDate))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CustAddress))
            .add(fieldStr(BankPaymAdviceCustV2Tmp, CustName));
    }

]]></Source>
			</Method>
			<Method>
				<Name>createQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject createQueryObject(guid _reportExecutionContext)
    {
        SysDaQueryObject bankPaymAdviceCustTmpQueryObject = new SysDaQueryObject(bankPaymAdviceCustTmp);

        this.addProjectionFields(bankPaymAdviceCustTmpQueryObject);

        return bankPaymAdviceCustTmpQueryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addProjectionFields</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>BankPaymAdviceCustTmp</c> fields to the projection of a query object.
    /// </summary>
    /// <param name = "_queryObject">
    /// The query object to add the fields.
    /// </param>
    protected void addProjectionFields(SysDaQueryObject _queryObject)
    {
        _queryObject.projection()
            .add(fieldStr(BankPaymAdviceCustTmp, AmountCur))
            .add(fieldStr(BankPaymAdviceCustTmp, Balance01))
            .add(fieldStr(BankPaymAdviceCustTmp, CashDisc))
            .add(fieldStr(BankPaymAdviceCustTmp, CompanyCoRegNum))
            .add(fieldStr(BankPaymAdviceCustTmp, CompanyGiro))
            .add(fieldStr(BankPaymAdviceCustTmp, CompanyPhone))
            .add(fieldStr(BankPaymAdviceCustTmp, CompanyTelefax))
            .add(fieldStr(BankPaymAdviceCustTmp, CurrencyCode))
            .add(fieldStr(BankPaymAdviceCustTmp, Grouping))
            .add(fieldStr(BankPaymAdviceCustTmp, Invoice))
            .add(fieldStr(BankPaymAdviceCustTmp, InvoiceDate))
            .add(fieldStr(BankPaymAdviceCustTmp, PaymAdviceDate))
            .add(fieldStr(BankPaymAdviceCustTmp, PaymentReference))
            .add(fieldStr(BankPaymAdviceCustTmp, PaymRefLabel))
            .add(fieldStr(BankPaymAdviceCustTmp, SettledCurrencyCode))
            .add(fieldStr(BankPaymAdviceCustTmp, TransDate))
            .add(fieldStr(BankPaymAdviceCustTmp, CustAddress))
            .add(fieldStr(BankPaymAdviceCustTmp, CustName));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addSelectionCriteria</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>BankPaymAdviceCustTmp</c> fields to the query object where clause.
    /// </summary>
    /// <param name = "_queryObject">
    /// The query object to add the fields
    /// </param>
    /// <param name = "_reportExecutionContext">
    /// A unique execution guid used to distinguish between report executions.
    /// </param>
    protected void addSelectionCriteria(SysDaQueryObject _queryObject, guid _reportExecutionContext)
    {
        _queryObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(bankPaymAdviceCustTmp, fieldStr(BankPaymAdviceCustTmp, ExecutionContext)),
                new SysDaValueExpression(_reportExecutionContext)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a SysDA query for the transfer of data between the <c>BankPaymAdviceCustTmp</c> physical table used by payment advice
    /// report generation and the <c>BankPaymAdviceCustV2Tmp</c> temporary table used by the batchable report processing.
    /// </summary>
    /// <param name = "_bankPaymAdviceCustV2Tmp">
    /// The <c>BankPaymAdviceCustV2Tmp</c> temporary table for insert.
    /// </param>
    /// <param name = "_reportExecutionContext">
    /// A unique execution guid used to distinguish between report executions.
    /// </param>
    /// <returns>
    /// A <c>SysDaInsertObject</c> object for transfer of data.
    /// </returns>
    internal static SysDaInsertObject newQuery(BankPaymAdviceCustV2Tmp _bankPaymAdviceCustV2Tmp, guid _reportExecutionContext)
    {
        BankPaymAdviceCustDPTransferQueryGenerator copyData = new BankPaymAdviceCustDPTransferQueryGenerator();

        return copyData.createQuery(_bankPaymAdviceCustV2Tmp, _reportExecutionContext);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>