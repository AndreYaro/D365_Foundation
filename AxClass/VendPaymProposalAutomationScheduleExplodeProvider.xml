<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendPaymProposalAutomationScheduleExplodeProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>VendPaymProposalAutomationScheduleExplodeProvider</c> class is designed to handle 
/// creating the appropriate <c>VendPaymProposalAutomationCriteria</c> records upon occurrence explosion.
/// </summary>
[ExportMetadata(extendedTypeStr(ProcessScheduleTypeName), 'VendPaymProposalAutomation')]
[Export(identifierStr(Dynamics.AX.Application.ProcessScheduleIExplodeOccurrences))]
internal final class VendPaymProposalAutomationScheduleExplodeProvider implements ProcessScheduleIExplodeOccurrences
{
    private VendPaymProposalAutomationCriteria automationCriteria;
    private VendPaymProposalAutomationCriteria automationCriteriaSelect;
    private ProcessScheduleSeriesOccurrenceTmp occurrencesExplodedTmp;
        
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>explodeOccurrences</Name>
				<Source><![CDATA[
    [Wrappable(false)]
    public void explodeOccurrences(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        this.initialize(_occurrencesExplodedTmp);
        
        SysDaInsertObject scheduleExplodeInsertObject = this.buildInsertObject(occurrencesExplodedTmp);
        this.executeInsertStatement(scheduleExplodeInsertObject);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    private void initialize(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        occurrencesExplodedTmp.linkPhysicalTableInstance(_occurrencesExplodedTmp);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildInsertObject</Name>
				<Source><![CDATA[
    internal SysDaInsertObject buildInsertObject(ProcessScheduleSeriesOccurrenceTmp _occurrencesExplodedTmp)
    {
        SysDaInsertObject scheduleExplodeInsertObject = new SysDaInsertObject(automationCriteria);
        
        SysDaQueryObject scheduleExplodeInsertQuery = this.buildInsertQueryObject();

        scheduleExplodeInsertObject.query(scheduleExplodeInsertQuery);

        this.addInsertFieldsToInsertObject(scheduleExplodeInsertObject);

        return scheduleExplodeInsertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildInsertQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildInsertQueryObject()
    {
        SysDaQueryObject scheduleExplodeInsertQuery = new SysDaQueryObject(automationCriteriaSelect);
        
        scheduleExplodeInsertQuery.joinClause(SysDaJoinKind::InnerJoin, this.getScheduleSeriesOccurrenceQueryObject());

        this.addProjectionFieldToQueryObject(scheduleExplodeInsertQuery);

        return scheduleExplodeInsertQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getScheduleSeriesOccurrenceQueryObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject getScheduleSeriesOccurrenceQueryObject()
    {
        SysDaQueryObject daQuery = new SysDaQueryObject(occurrencesExplodedTmp);
        daQuery.whereClause(
            new SysDaEqualsExpression(
                    new SysDaFieldExpression(occurrencesExplodedTmp, fieldStr(ProcessScheduleSeriesOccurrenceTmp, ProcessScheduleSeries)),
                    new SysDaFieldExpression(automationCriteriaSelect, fieldStr(VendPaymProposalAutomationCriteria, ProcessScheduleSeries)))
            .and(
                new SysDaEqualsExpression(
                    new SysDaFieldExpression(occurrencesExplodedTmp, fieldStr(ProcessScheduleSeriesOccurrenceTmp, TypeName)),
            new SysDaValueExpression(VendPaymProposalAutomationConstants::RegisteredTypeName)))
                );
        daQuery.projection().add(fieldStr(ProcessScheduleSeriesOccurrenceTmp, ProcessScheduleOccurrence));

        return daQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addProjectionFieldToQueryObject</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal void addProjectionFieldToQueryObject(SysDaQueryObject _scheduleExplodeInsertQuery)
    {
        _scheduleExplodeInsertQuery.projection()
            .add(fieldStr(VendPaymProposalAutomationCriteria, JournalName))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymProposalType))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseToDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, ToDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, AmountLimit))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymentCurrencyCode))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseMinimumPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, MinimumPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymentWeekDayFixed))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DefaultOffsetAccountType))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DefaultOffsetAccount))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseSummarizedPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, SummarizedPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, BalanceCheck))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DeleteCredit))
            .add(fieldStr(VendPaymProposalAutomationCriteria, IncludeInvoicesFromOtherCompanies))
            .add(fieldStr(VendPaymProposalAutomationCriteria, ProposeSeparatePaymentPerCompany));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addInsertFieldsToInsertObject</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal void addInsertFieldsToInsertObject(SysDaInsertObject _scheduleExplodeInsertObject)
    {
        _scheduleExplodeInsertObject.fields()
            .add(fieldStr(VendPaymProposalAutomationCriteria, JournalName))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymProposalType))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseToDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, ToDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, AmountLimit))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymentCurrencyCode))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseMinimumPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, MinimumPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, PaymentWeekDayFixed))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DefaultOffsetAccountType))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DefaultOffsetAccount))
            .add(fieldStr(VendPaymProposalAutomationCriteria, UseSummarizedPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, SummarizedPaymentDateOffsetDays))
            .add(fieldStr(VendPaymProposalAutomationCriteria, BalanceCheck))
            .add(fieldStr(VendPaymProposalAutomationCriteria, DeleteCredit))
            .add(fieldStr(VendPaymProposalAutomationCriteria, IncludeInvoicesFromOtherCompanies))
            .add(fieldStr(VendPaymProposalAutomationCriteria, ProposeSeparatePaymentPerCompany))
            .add(fieldStr(VendPaymProposalAutomationCriteria, ProcessScheduleOccurrence));
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeInsertStatement</Name>
				<Source><![CDATA[
    internal void executeInsertStatement(SysDaInsertObject _scheduleExplodeInsertObject)
    {
        SysDaInsertStatement scheduleExplodeInsertStatement = new SysDaInsertStatement();

        scheduleExplodeInsertStatement.executeQuery(_scheduleExplodeInsertObject);        
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>