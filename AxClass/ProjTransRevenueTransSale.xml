<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjTransRevenueTransSale</Name>
	<SourceCode>
		<Declaration><![CDATA[
class ProjTransRevenueTransSale extends ProjTransRevenueTrans
{
    ProjRevenueTransSale   projRevenueTransSale;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canBeInvoiced</Name>
				<Source><![CDATA[
    public boolean canBeInvoiced(boolean _chargeable = false)
    {
        ProjFundingSource   projFundingSource;
        boolean             ret = true;

        ProjTable projTableLocal = this.projTable();

        if (ProjMultipleContractLinesForProjectFeatureHelper::isTimeMaterialBillingMethod(projTableLocal, ProjTransType::Revenue) ||
            ProjMultipleContractLinesForProjectFeatureHelper::isFixedPriceBillingMethod(projTableLocal, ProjTransType::Revenue))
        {
            if (this.originRevenue() == ProjOrigin::Subscription)
            {
                ret = this.canTransStatusBeInvoiced(_chargeable);
            }
            else
            {
                ret = super(_chargeable);
            }
        }

        if (ret)
        {
            projFundingSource = ProjFundingSource::find(this.fundingSource());
            if ((projFundingSource.FundingType == ProjFundingType::Organization) ||
                (projFundingSource.FundingType == ProjFundingType::OnHoldFundingSource))
            {
                ret = false;
            }
        }

        if (ret)
        {
            ret = ProjInvoiceTable::isBillingRuleInvoiceable(this.projId(), this.categoryId(), this.psaContractLineNum(), this.transType());
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>controlId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the cost template ID for this transaction.
    /// </summary>
    /// <returns>
    /// The cost template ID for this transaction.
    /// </returns>
    public ProjControlId controlId()
    {
        return projRevenueTransSale.ControlId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>controlPeriodTo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the end date of the project period for this transaction.
    /// </summary>
    /// <returns>
    /// The end date of the project period for this transaction.
    /// </returns>
    public ProjPeriodFrom controlPeriodTo()
    {
        return projRevenueTransSale.wipPeriod;
    }

]]></Source>
			</Method>
			<Method>
				<Name>controlVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the version number of the project period for this transaction.
    /// </summary>
    /// <returns>
    /// A <c>ProjControlVersion</c> value that indicates the version number.
    /// </returns>
    public ProjControlVersion controlVersion()
    {
        return projRevenueTransSale.ControlVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultDimension</Name>
				<Source><![CDATA[
    public DimensionDefault defaultDimension()
    {
        return  projRevenueTransSale.DefaultDimension;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fundingSource</Name>
				<Source><![CDATA[
    public ProjFundingSourceRefId fundingSource()
    {
        return projRevenueTransSale.FundingSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>invoiced</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether this transaction has been invoiced.
    /// </summary>
    /// <returns>
    /// true if this transactions has been invoiced; otherwise, false.
    /// </returns>
    public  boolean invoiced()
    {
        boolean ret;

        if (projRevenueTrans.TransactionOrigin == ProjOrigin::BeginningBalance && projRevenueTransSale.TransStatus == ProjTransStatus::Invoiced)
        {
            ret = true;
        }
        else
        {
            ret = ProjInvoiceRevenueDetail::find(this.recId()).RecId ? true : false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>invoiceId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the invoice ID of the invoice for this transaction.
    /// </summary>
    /// <returns>
    /// The invoice ID of the invoice for this transaction.
    /// </returns>
    public ProjInvoiceId invoiceId()
    {
        if (this.transStatus() == ProjTransStatus::CreditnoteProposal)
        {
            return '';
        }
        else
        {
            return  ProjInvoiceRevenueDetail::find(this.recId()).projInvoiceRevenue().ProjInvoiceId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ledgerSalesAmountMST</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the sales amount to be posted to the general ledger.
    /// </summary>
    /// <returns>
    /// The sales amount to be posted to the general ledger.
    /// </returns>
    public AmountMST ledgerSalesAmountMST()
    {
        return projRevenueTransSale.LedgerSalesAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>ledgerSalesPosted</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether sales have been posted to the general ledger.
    /// </summary>
    /// <returns>
    /// A <c>NoYes</c> enumeration value.
    /// </returns>
    public NoYes ledgerSalesPosted()
    {
        return projRevenueTransSale.LedgerSalesPosted;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustHandleBudget</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether budgetary control is enabled.
    /// </summary>
    /// <returns>
    ///    Always returns false.
    /// </returns>
    public boolean mustHandleBudget()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(ProjRevenueTrans _projRevenueTrans, ProjRevenueTransSale _projRevenueTransSale)
    {
        projRevenueTransSale = _projRevenueTransSale;

        super(_projRevenueTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>proposal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether this transaction is from a project proposal.
    /// </summary>
    /// <returns>
    /// true if this transaction is from a project proposal; otherwise, false.
    /// </returns>
    /// <remarks>
    /// This method should be overridden by classes that extend this class to provide the necessary
    /// functionality for the specific transaction. This base class implementation returns false.
    /// </remarks>
    public  boolean proposal()
    {
        return ProjProposalRevenueDetail::findActiveProposalId(this.recId()) ? true : false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>proposalId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the ID of the project proposal that is associated with this transaction.
    /// </summary>
    /// <returns>
    /// An empty string.
    /// </returns>
    public  ProjProposalId proposalId()
    {
        return ProjProposalRevenueDetail::findActiveProposalId(this.recId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>recId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the record ID for this transaction.
    /// </summary>
    /// <returns>
    /// The record ID for this transaction.
    /// </returns>
    public RecId recId()
    {
        return projRevenueTransSale.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>salesPrice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the sales price for this transaction.
    /// </summary>
    /// <returns>
    /// The sales price for this transaction.
    /// </returns>
    public SalesPrice salesPrice()
    {
        return projRevenueTransSale.SalesPrice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>tableId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the table ID for this transaction.
    /// </summary>
    /// <returns>
    /// The table ID for this transaction.
    /// </returns>
    public TableId tableId()
    {
        return tableName2id(tableStr(ProjRevenueTransSale));
    }

]]></Source>
			</Method>
			<Method>
				<Name>transStatus</Name>
				<Source><![CDATA[
    public ProjTransStatus transStatus()
    {
        return projRevenueTransSale.TransStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transTurnover</Name>
				<Source><![CDATA[
    public AmountCur transTurnover()
    {
        return projRevenueTransSale.LineAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>voucherInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the voucher number that is associated with this transaction.
    /// </summary>
    /// <returns>
    /// The voucher number that is associated with this transaction.
    /// </returns>
    public Voucher voucherInvoice()
    {
        ProjInvoiceRevenueDetail   projInvoiceRevenueDetail;
        ProjInvoiceRevenue         projInvoiceRevenue;

        if (ProjInvoiceRevenueDetail::exist(this.recId()))
        {
            projInvoiceRevenueDetail = ProjInvoiceRevenueDetail::find(this.recId());
            projInvoiceRevenue = projInvoiceRevenueDetail.projInvoiceRevenue();
            return ProjInvoiceJour::find(projInvoiceRevenue.ProjInvoiceId, projInvoiceRevenue.InvoiceDate).LedgerVoucher;
        }

        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCreditNoteOrigTransInvoiceDate</Name>
				<Source><![CDATA[
    internal TransDate getCreditNoteOrigTransInvoiceDate()
    {
        ProjRevenueTransSale projRevenueTransSaleLoc;
        ProjInvoiceRevenue projInvoiceRevenueLoc;
        ProjInvoiceRevenueDetail projInvoiceRevenueDetailLoc;

        select firstonly InvoiceDate from projInvoiceRevenueLoc
            order by projInvoiceRevenueLoc.InvoiceDate desc
            where projInvoiceRevenueLoc.TransId == this.transId()
                && projInvoiceRevenueLoc.Qty == this.qty()
            exists join ProjInvoiceRevenueDetailLoc
                where projInvoiceRevenueDetailLoc.InvoiceRefRecId == projInvoiceRevenueLoc.RecId
            exists join projRevenueTransSaleLoc
                where projRevenueTransSaleLoc.RecId == projInvoiceRevenueDetailLoc.SaleRefRecId
                    && projRevenueTransSaleLoc.TransStatus == ProjTransStatus::MarkedCreditNote
                    && projRevenueTransSaleLoc.FundingSource == this.fundingSource();

        InvoiceDate origInvoiceDate = projInvoiceRevenueLoc.InvoiceDate;
        
        return origInvoiceDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>