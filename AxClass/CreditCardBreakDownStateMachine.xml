<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CreditCardBreakDownStateMachine</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class CreditCardBreakDownStateMachine implements ICreditCardBreakDownStateMachine
{
    internal static List internalStatusList;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>typenew</Name>
				<Source><![CDATA[
    private static void typenew()
    {
        internalStatusList = CreditCardBreakDownStateMachine::generateInternalStatusList();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static ICreditCardBreakDownStateMachine construct()
    {
        return new CreditCardBreakDownStateMachine();
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasNext</Name>
				<Source><![CDATA[
    public boolean hasNext(CreditCardPaymentStatus _creditCardPaymentStatus)
    {
        return _creditCardPaymentStatus != CreditCardPaymentStatus::Finished;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNextStatus</Name>
				<Source><![CDATA[
    public CreditCardPaymentStatus getNextStatus(CreditCardPaymentStatus _creditCardPaymentStatus)
    {
        if (!this.hasNext(_creditCardPaymentStatus))
        {
            return CreditCardPaymentStatus::Invalid;
        }

        ListEnumerator enumerator = CreditCardBreakDownStateMachine::internalStatusList.getEnumerator();
        CreditCardPaymentStatus nextStatus = CreditCardPaymentStatus::Invalid;
        while (enumerator.moveNext())
        {
            if (enumerator.current() == _creditCardPaymentStatus)
            {
                enumerator.moveNext();
                nextStatus = enumerator.current();
                break;
            }
        }
        return nextStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateInternalStatusList</Name>
				<Source><![CDATA[
    private static List generateInternalStatusList()
    {
        List fixedStatusList = new List(Types::Enum);
        fixedStatusList.addEnd(CreditCardPaymentStatus::Initial);
        fixedStatusList.addEnd(CreditCardPaymentStatus::Calculated);
        fixedStatusList.addEnd(CreditCardPaymentStatus::Captured);
        fixedStatusList.addEnd(CreditCardPaymentStatus::PaymentPosted);
        fixedStatusList.addEnd(CreditCardPaymentStatus::Finalized);
        fixedStatusList.addEnd(CreditCardPaymentStatus::Finished);
        return fixedStatusList;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>