<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FormDataSourceMarks</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>FormDataSourceMarks</c> class makes it easy to remember all marked rows in a given data
///    source.
/// </summary>
/// <remarks>
///    The current record should remain the same after the use of the class.This class will not change the
///    current settings of the data source. This class enables performing actions using the data source in
///    between calling the <c>getMarks</c> and <c>setMarks</c> method calls.
/// </remarks>
// This is a framework class. Customizing this class may cause problems with future upgrades to the software.
class FormDataSourceMarks implements SysFormDataSourceMarks
{
    container           recIds;
    FormDataSource      formDataSource;
    recId               recID;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getMarksFromDataSource</Name>
				<Source><![CDATA[
    public void getMarksFromDataSource()
    {
        Common localCommon;
        int initialPosition = 0;

        for (localCommon=formDataSource.getFirst(1);
            localCommon;
            localCommon=formDataSource.getNext())
        {
            recIds = conins(recIds,1,localCommon.RecId);

            if (localCommon.RecId == recID)
            {
                // cache the position, so it can be restored
                initialPosition = formDataSource.getPosition();
            }

            if (!localCommon.RecId)
                break;
        }

        if (initialPosition)
        {
            formDataSource.setPosition(initialPosition);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFormDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets or sets the <c>FormDataSource</c> parameter.
    /// </summary>
    /// <param name="_formDataSource">
    ///    The value to set; optional.
    /// </param>
    /// <returns>
    ///    The value of the <c>FormDataSource</c> parameter.
    /// </returns>
    public FormDataSource parmFormDataSource(FormDataSource _formDataSource = formDataSource)
    {
        formDataSource = _formDataSource;
        recId = formDataSource.cursor().RecId;
        return formDataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setMarksOnDataSource</Name>
				<Source><![CDATA[
    public void setMarksOnDataSource()
    {
        Common localCommon;
        int current;
        boolean blnAllowCreate;
        int initialPosition = 0;

        blnAllowCreate = formDataSource.allowCreate();
        if (blnAllowCreate)
            formDataSource.allowCreate(false);
        current = formDataSource.first();
        localCommon = formDataSource.cursor();

        while (current)
        {
            localCommon = formDataSource.cursor();
            if (confind(recIds, localCommon.RecId))
            {
                formDataSource.mark(1);
            }
            if (localCommon.RecId == recID)
            {
                // cache the position, so it can be restored
                initialPosition = formDataSource.getPosition();
            }
            current = formDataSource.next();
            localCommon = formDataSource.cursor();
        }

        formDataSource.setPosition(initialPosition);

        if (blnAllowCreate)
        {
            formDataSource.allowCreate(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static FormDataSourceMarks construct()
    {
        return new FormDataSourceMarks();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>