<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjPostItemPackingSlip_AdjCost</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class is responsible for handling additional postings for cost achanges done while vendor invoice posting.
/// This specifically handles the case of PO-IR combination with a non-stocked item
/// </summary>
internal class ProjPostItemPackingSlip_AdjCost extends ProjPostItemPackingSlip
{
    ProjItemTransCost itemTransCost;
    AmountCur costDiffAmount;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Common _buffer, ProjItemTrans _itemTrans, ProjItemTransCost _itemTransCost, AmountCur _diffAmount)
    {
        if (_buffer.TableId == tableNum(CustPackingSlipTrans))
        {
            super(_buffer);
            custPackingSlipTrans = _buffer;
            projItemTrans = _itemTrans;
            itemTransCost = _itemTransCost;
            costDiffAmount = _diffAmount;
            projTrans = ProjTrans::newCustPackingSlipTrans_AdjCost(custPackingSlipTrans, _diffAmount);
            this.parmLedgerStatusCost(itemTransCost.LedgerStatusCost);
            this.parmCreateTrans(true);
            this.parmEnterCost(true);
            this.parmUpdateTrans(false);
        }
        else
        {
            throw error("@SYS22828",funcname());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmItemTrans</Name>
				<Source><![CDATA[
    public ProjItemTrans parmItemTrans(ProjItemTrans _itemTrans = projItemTrans)
    {
        projItemTrans = _itemTrans;
        return projItemTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postCostAdjustment</Name>
				<Source><![CDATA[
    public void postCostAdjustment()
    {
        this.postTrans();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkEnterCostValue</Name>
				<Source><![CDATA[
    protected boolean checkEnterCostValue()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>costValueCUR</Name>
				<Source><![CDATA[
    protected AmountCur costValueCUR()
    {
        return costDiffAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>projTransCreate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Overridden method implements only the creation of <c>ProjItemTransCost</c> record since this is just a cost adjustment and not a creation of new transaction.
    /// </summary>
    public void projTransCreate()
    {
        SalesLine salesLineLocal = custPackingSlipTrans.salesLine();
        if (salesLineLocal && salesLineLocal.SalesType == SalesType::ItemReq && !salesLineLocal.isStocked())
        {
            // Need only cost distribution hence 0 sales amount
            this.createCostSalesDistributions(this.costValueCUR(), 0);
            if (allocationError == NoYes::No)
            {
                MapEnumerator costMapEnumerator = new MapEnumerator(costDistributions);
                while (costMapEnumerator.moveNext())
                {
                    //Create ProjItemTransCost record
                    ProjItemTransCost projItemTransCost = this.initializeProjItemTransCost(costMapEnumerator.currentKey(), costMapEnumerator.currentValue());
                    projItemTransCost.insert();
                }
            }
            if (this.parmCostUseOrig())
            {
                this.parmTransIdCost(this.parmTransIdOrig());
            }
            else
            {
                this.parmTransIdCost(projItemTrans.ProjTransId);
            }
        }
        else
        {
            throw error("@SYS22828",funcname());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkUpdateBudget</Name>
				<Source><![CDATA[
    public boolean checkUpdateBudget()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkUpdateForecast</Name>
				<Source><![CDATA[
    public boolean checkUpdateForecast()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>