<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransferParmShipFormEventHandler_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
#ISOCountryRegionCodes
/// <summary>
/// This class handles the events for the <c>InventTransferParmShip</c> form.
/// </summary>
public static class InventTransferParmShipFormEventHandler_IN
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>InventParameters_OnActivated</Name>
				<Source><![CDATA[
    /// <summary>
    /// This is event handler for the <c>active</c> method of the <c>InventTransferParmUpdate</c> datasource.
    /// </summary>
    /// <param name = "_sender">The form datasource that triggered the event.</param>
    /// <param name="_e">The <c>FormDataSourceEventArgs</c> object associated to the event.</param>
    [FormDataSourceEventHandler(formDataSourceStr(InventTransferParmShip, InventTransferParmUpdate), FormDataSourceEventType::Activated)]
    public static void InventParameters_OnActivated(FormDataSource _sender, FormDataSourceEventArgs _e)
    {
        if (_sender && 
            _sender.table() == tableNum(InventTransferParmUpdate) &&
            SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            InventTransferParmUpdate inventTransferParmUpdate = _sender.cursor();

            InventTransferParmTable inventTransferParmTable;
            InventTransferTable inventTransferTable;

            select firstonly RecId from inventTransferParmTable
                where inventTransferParmTable.ParmId == inventTransferParmUpdate.ParmId
                exists join inventTransferTable
                    where inventTransferTable.TransferId == inventTransferParmTable.TransferId 
                        && inventTransferTable.StockTransferCostPriceHandlingImprovement_IN == NoYes::Yes;
                
            boolean showStockTransferCostPriceHandlingImprovementFields = inventTransferParmTable.RecId != 0;

            _sender.object(fieldNum(InventTransferParmUpdate, StockTransferBlockZeroPriceShipment_IN)).visible(showStockTransferCostPriceHandlingImprovementFields);

            if (showStockTransferCostPriceHandlingImprovementFields)
            {
                InventParameters inventParameters = InventParameters::find();

                _sender.object(fieldNum(InventTransferParmUpdate, StockTransferBlockZeroPriceShipment_IN)).enabled(inventParameters.StockTransferBlockZeroPriceShipmentSetup_IN == StockTransferBlockZeroPriceShipmentSetup_IN::Prompt);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>