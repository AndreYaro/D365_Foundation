<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSShipConsolidationWorkbenchBaseForm</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSShipConsolidationWorkbenchBaseForm</c> class contains base form logic for <c>WHSShipConsolidationTemplate</c> and <c>WHSShipConsolidationWorkbench</c> forms.
/// </summary>
internal abstract class WHSShipConsolidationWorkbenchBaseForm
{
    private str queryRangePartMenuName;
    private WHSIQueryPart headerRangePart;

    protected FormRun                       formRun;
    protected Object                        formRunObj;
    protected FormDataSource                whsShipConsolidationTemplate_ds;
    protected WHSShipConsolidationTemplate  whsShipConsolidationTemplate;
    protected FormButtonControl             ctrlQueryButton;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFormRun</Name>
				<Source><![CDATA[
    internal FormRun parmFormRun(FormRun _formRun = formRun)
    {
        formRun    = _formRun;
        formRunObj = _formRun;

        return formRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmQueryRangePartMenuName</Name>
				<Source><![CDATA[
    internal str parmQueryRangePartMenuName(str _queryRangePartMenuName = queryRangePartMenuName)
    {
        queryRangePartMenuName = _queryRangePartMenuName;

        return queryRangePartMenuName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmShipConsolidateTemplateDS</Name>
				<Source><![CDATA[
    internal FormDataSource parmShipConsolidateTemplateDS(FormDataSource _whsShipConsolidationTemplate_ds = whsShipConsolidationTemplate_ds)
    {
        whsShipConsolidationTemplate_ds = _whsShipConsolidationTemplate_ds;
        whsShipConsolidationTemplate    = whsShipConsolidationTemplate_ds.cursor();

        return whsShipConsolidationTemplate_ds;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCtrlQueryButton</Name>
				<Source><![CDATA[
    internal FormButtonControl parmCtrlQueryButton(FormButtonControl _ctrlQueryButton = ctrlQueryButton)
    {
        ctrlQueryButton = _ctrlQueryButton;

        return ctrlQueryButton;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryUpdated</Name>
				<Source><![CDATA[
    internal void queryUpdated(FormRun _sender, container _packedQuery)
    {
        this.updateCriteriaQueryAndRefreshUI(_packedQuery);
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearInvalidInventLocationIdFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears the warehouse field if the site is changed.
    /// </summary>
    /// <param name="_inventLocationId">
    /// The current location id.
    /// </param>
    /// <param name="_inventSiteId">
    /// The new site id.
    /// </param>
    /// <returns>
    /// The updated warehouse value.
    /// </returns>
    private InventLocationId clearInvalidInventLocationIdFilter(InventLocationId _inventLocationId, InventSiteId _inventSiteId)
    {
        if (_inventSiteId &&
            _inventLocationId &&
            InventLocation::find(_inventLocationId).InventSiteId != _inventSiteId)
        {
            _inventLocationId = '';
        }

        return _inventLocationId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableDestinationAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables the destination address fields.
    /// </summary>
    /// <param name="_enabled">
    /// If true, enable the fields, disable otherwise.
    /// </param>
    private void enableDestinationAddress(boolean _enabled)
    {
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationFromPostalCode)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationToPostalCode)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationCountryId)).enabled(_enabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableDestinationWarehouseAndSite</Name>
				<Source><![CDATA[
    internal void enableDestinationWarehouseAndSite(boolean _enabled)
    {
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationLocationId)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationSiteId)).enabled(_enabled);

        if (_enabled)
        {
            if (whsShipConsolidationTemplate.DestinationLocationId)
            {
                this.enableDestinationAddress(false);
            }
            else
            {
                this.enableDestinationAddress(true);
            }
        }
        else
        {
            whsShipConsolidationTemplate.DestinationLocationId = '';
            whsShipConsolidationTemplate.DestinationSiteId     = '';

            this.enableDestinationAddress(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableOriginAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Enables or disables the origin address fields.
    /// </summary>
    /// <param name="_enabled">
    /// If true, enable the fields, disable otherwise.
    /// </param>
    private void enableOriginAddress(boolean _enabled)
    {
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginFromPostalCode)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginToPostalCode)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginCountryId)).enabled(_enabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>enableOriginWarehouseAndSite</Name>
				<Source><![CDATA[
    internal void enableOriginWarehouseAndSite(boolean _enabled)
    {
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginLocationId)).enabled(_enabled);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginSiteId)).enabled(_enabled);

        if (_enabled)
        {
            if (whsShipConsolidationTemplate.OriginLocationId)
            {
                this.enableOriginAddress(false);
            }
            else
            {
                this.enableOriginAddress(true);
            }
        }
        else
        {
            whsShipConsolidationTemplate.OriginLocationId = '';
            whsShipConsolidationTemplate.OriginSiteId = '';

            this.enableOriginAddress(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setupForm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets up the form depending on the consolidation type used.
    /// </summary>
    internal void setupForm()
    {
        boolean isFormSetup = this.setupFormByPolicyType(whsShipConsolidationTemplate.ShipConsolidationPolicyType);
        if (!isFormSetup)
        {
            throw error("@SYS6144");
        }

        if (formHasMethod(formRun, methodStr(WHSShipConsolidationWorkbenchBaseForm, setupForm)))
        {
            formRunObj.setupForm();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setupFormByPolicyType</Name>
				<Source><![CDATA[
    [Wrappable]
    internal boolean setupFormByPolicyType(WHSShipConsolidationPolicyType _shipConsolidationPolicyType)
    {
        switch (_shipConsolidationPolicyType)
        {
            case WHSShipConsolidationPolicyType::Sales:
                whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, CustAccount)).enabled(true);
                this.enableOriginWarehouseAndSite(true);
                this.enableDestinationWarehouseAndSite(false);
                return true;

            case WHSShipConsolidationPolicyType::TransferIssue:
                whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, CustAccount)).enabled(false);
                whsShipConsolidationTemplate.CustAccount = '';
                this.enableOriginWarehouseAndSite(true);
                this.enableDestinationWarehouseAndSite(true);
                return true;

            default:
                return false;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshQueryParts</Name>
				<Source><![CDATA[
    internal void refreshQueryParts()
    {
        if (headerRangePart)
        {
            headerRangePart.refresh();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearQueryParts</Name>
				<Source><![CDATA[
    internal void clearQueryParts()
    {
        if (headerRangePart)
        {
            headerRangePart.clear();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQueryFormParts</Name>
				<Source><![CDATA[
    internal void initQueryFormParts()
    {
        str partName = this.parmQueryRangePartMenuName();

        if (partName)
        {
            QueryRun defaultQuery = null;
            if(whsShipConsolidationTemplate)
            {
                defaultQuery = new QueryRun(WHSShipConsolidationTemplate::createNewQuery(whsShipConsolidationTemplate.ShipConsolidationPolicyType));
            }
            str fieldNameQuery = fieldId2Name(tableNum(whsShipConsolidationTemplate), fieldNum(whsShipConsolidationTemplate, CriteriaQuery));
            str fieldNameQueryName = fieldId2Name(tableNum(whsShipConsolidationTemplate), fieldNum(whsShipConsolidationTemplate, ShipConsolidationTemplateName));

            headerRangePart = WHSQueryHelper::initializeQueryFormPart(formRunObj, partName, fieldNameQuery, '%1', fieldNameQueryName, defaultQuery, true, false, null, false, false, false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>OriginSiteId_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clear the origin warehouse field's value if the origin invent site is changed.
    /// </summary>
    /// <param name="sender">Form data object.</param>
    /// <param name="e">Event arguments.</param>
    private void OriginSiteId_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        whsShipConsolidationTemplate.OriginLocationId = this.clearInvalidInventLocationIdFilter(whsShipConsolidationTemplate.OriginLocationId, whsShipConsolidationTemplate.OriginSiteId);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginLocationId)).modified();
    }

]]></Source>
			</Method>
			<Method>
				<Name>DestinationSiteId_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clear the destination warehouse field's value if the destination invent site is changed.
    /// </summary>
    /// <param name="sender">Form data object.</param>
    /// <param name="e">Event arguments.</param>
    private void DestinationSiteId_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        whsShipConsolidationTemplate.DestinationLocationId = this.clearInvalidInventLocationIdFilter(whsShipConsolidationTemplate.DestinationLocationId, whsShipConsolidationTemplate.DestinationSiteId);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationLocationId)).modified();
    }

]]></Source>
			</Method>
			<Method>
				<Name>OriginLocationId_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler
    /// </summary>
    /// <param name="sender">Form data object.</param>
    /// <param name="e">Event arguments.</param>
    private void OriginLocationId_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        if (whsShipConsolidationTemplate.OriginLocationId)
        {
            this.enableOriginAddress(false);
        }
        else
        {
            this.enableOriginAddress(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>DestinationLocationId_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler
    /// </summary>
    /// <param name="sender">Form data object.</param>
    /// <param name="e">Event arguments.</param>
    private void DestinationLocationId_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        if (whsShipConsolidationTemplate.DestinationLocationId)
        {
            this.enableDestinationAddress(false);
        }
        else
        {
            this.enableDestinationAddress(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ShipConsolidationPolicyType_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler
    /// </summary>
    /// <param name="sender">Form data object.</param>
    /// <param name="e">Event arguments.</param>
    private void ShipConsolidationPolicyType_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        whsShipConsolidationTemplate.initCriteriaQueryRun();

        this.setupForm();
    }

]]></Source>
			</Method>
			<Method>
				<Name>QueryButton_OnClicked</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event handler
    /// </summary>
    /// <param name="sender">Form button.</param>
    /// <param name="e">Event arguments.</param>
    private void QueryButton_OnClicked(FormControl sender, FormControlEventArgs e)
    {
        Query query = new Query(whsShipConsolidationTemplate.CriteriaQuery);
        SysQueryRun queryRun = new SysQueryRun(query);

        queryRun.name(whsShipConsolidationTemplate.ShipConsolidationTemplateName);
        queryRun.promptShowSorting(false);
        queryRun.origQueryPack(WHSShipConsolidationTemplate::createNewQuery(whsShipConsolidationTemplate.ShipConsolidationPolicyType).pack());
        queryRun.saveUserSetup(false);
        queryRun.promptLoadLastUsedQuery(false);
        queryRun.promptAllowSave(false);

        if (queryRun.prompt())
        {
            this.updateCriteriaQueryAndRefreshUI(queryRun.query().pack());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCriteriaQueryAndRefreshUI</Name>
				<Source><![CDATA[
    private void updateCriteriaQueryAndRefreshUI(container _packedQuery)
    {
        whsShipConsolidationTemplate.CriteriaQuery = _packedQuery;

        if (!whsShipConsolidationTemplate_ds.validateWrite())
        {
            throw error(strFmt("@WAX:WHSCannotWriteRecordInTable", tablePName(WHSShipConsolidationTemplate)));
        }
        else
        {
            whsShipConsolidationTemplate_ds.write();
        }
      
        if (headerRangePart)
        {
            headerRangePart.refresh();
        }
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>formInitPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes form. Should be executed at the end of the form init method.
    /// </summary>
    internal void formInitPost()
    {
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginSiteId)).OnModified += eventhandler(this.OriginSiteId_OnModified);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationSiteId)).OnModified += eventhandler(this.DestinationSiteId_OnModified);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, OriginLocationId)).OnModified += eventhandler(this.OriginLocationId_OnModified);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, DestinationLocationId)).OnModified += eventhandler(this.DestinationLocationId_OnModified);
        whsShipConsolidationTemplate_ds.object(fieldNum(WHSShipConsolidationTemplate, ShipConsolidationPolicyType)).OnModified += eventhandler(this.ShipConsolidationPolicyType_OnModified);

        if (ctrlQueryButton)
        {
            ctrlQueryButton.OnClicked += eventhandler(this.QueryButton_OnClicked);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>