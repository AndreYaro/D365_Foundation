<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CostCalculationPeriodOrderDrillThrough</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CostCalculationPeriodOrderDrillThrough</c> class opens a details form
/// when drilling through orders charts.
/// </summary>
public class CostCalculationPeriodOrderDrillThrough
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        SysChartDrillThruParameters parameters = _args.parmObject();

        if (parameters)
        {
            CostCalculationPeriodTopVariancesSeriesType seriesType;
            seriesType = str2enum(seriesType, parameters.parameter2());

            switch (seriesType)
            {
                case CostCalculationPeriodTopVariancesSeriesType::ProductionNumber:
                    CostCalculationPeriodOrderDrillThrough::openProdTableForm(parameters.parameter1());
                    break;

                case CostCalculationPeriodTopVariancesSeriesType::ProducedItemName:
                    CostCalculationPeriodOrderDrillThrough::openInventTableForm(parameters.parameter1());
                    break;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>openProdTableForm</Name>
				<Source><![CDATA[
    private static void openProdTableForm(ProdId _prodId)
    {
        ProdTable prodTable = ProdTable::find(_prodId);

        if (prodTable)
        {
            MenuFunction menuFunction;

            if (prodTable.ProdType == ProdType::Process)
            {
                menuFunction = new MenuFunction(menuitemDisplayStr(CostPmfProdTableDetails), MenuItemType::Display);
            }
            else
            {
                menuFunction = new MenuFunction(menuitemDisplayStr(CostProdTableDetails), MenuItemType::Display);
            }

            var args = new Args(menuFunction.object());
            args.record(prodTable);
            menuFunction.run(args);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>openInventTableForm</Name>
				<Source><![CDATA[
    private static void openInventTableForm(ItemId _itemId)
    {
        CostProductDetailsExtendedJumpRefHelper::openCostProductDetailsExtended(InventTable::find(_itemId));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>