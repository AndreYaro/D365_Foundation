<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResEnumerationAttributeTypeValueUpdateService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>EcoResEnumerationAttributeTypeValueUpdateService</c> class updates the values in the EcoResValue table when the translation changes.
/// The query updates all product-related values with the old value and the same attribute type as the enumeration attribute type value we changed.
/// Incoming data contract parameters used in the query:
/// searchValue - the old value of the modified enumeration attribute type value
/// attributeValueId - recId of the changed value (used to get the new value)
/// ecoResAttributeTypeRecId - the attribute type of the changed value
/// </summary>
public final class EcoResEnumerationAttributeTypeValueUpdateService extends SysOperationServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runUpdate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs update values in EcoResValue table.
    /// </summary>
    /// <param name = "_dataContract"> The data contract to be used in the query to update the data. </param>
    public void runUpdate(EcoResEnumerationAttributeTypeValueUpdateContract _dataContract)
    {
        AttributeValueText searchValue = _dataContract.parmSearchValue();
        EcoResTextValueRecId attributeValueId = _dataContract.parmEcoResTextValueRecId();
        EcoResAttributeTypeRecId ecoResAttributeTypeRecId = _dataContract.parmEcoResAttributeTypeRecId();
        AttributeValueText newValue = EcoResTextValue::find(attributeValueId).TextValue;
        Name attributeTypeName = EcoResAttributeType::find(ecoResAttributeTypeRecId).Name;
        
        info(strFmt("@SCM:EcoResEnumerationAttributeTypeValueUpdateServiceStartMessage", attributeTypeName, searchValue, newValue));

        ttsbegin;

        EcoResTextValue ecoResValue;
        EcoResAttributeValue ecoResAttributeValue;
        EcoResAttribute ecoResAttribute;

        update_recordset ecoResValue
        setting
            TextValue = newValue
        where ecoResValue.TextValue == searchValue
        exists join ecoResAttributeValue
            where ecoResAttributeValue.Value == ecoResValue.RecId
        exists join ecoResAttribute
            where ecoResAttribute.RecId == ecoResAttributeValue.Attribute
            && ecoResAttribute.AttributeType == ecoResAttributeTypeRecId;
        
        ttscommit;

        info(strFmt("@SCM:EcoResEnumerationAttributeTypeValueUpdateServiceEndMessage", attributeTypeName, searchValue, newValue));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>