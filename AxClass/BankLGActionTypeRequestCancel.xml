<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLGActionTypeRequestCancel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLGActionTypeRequestCancel</c> class used to do the cancel request action for the letter
///    of guarantee.
/// </summary>
class BankLGActionTypeRequestCancel extends BankLGActionType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doAction</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Does the request action "Cancel" based on the parameters.
    /// </summary>
    protected void doAction()
    {
        ttsbegin;

        // Create request action
        bankLGAction.ActionNum          = NumberSeq::newGetNum(BankParameters::numRefBankLGTransNum()).num();
        bankLGAction.RequestAction      = BankLGRequestActionType::Cancel;
        bankLGAction.ExecutionAction    = BankLGExecutionActionType::None;
        bankLGAction.RequestDateTime    = DateTimeUtil::getSystemDateTime();
        bankLGAction.Guarantee          = bankLGGuarantee.RecId;
        bankLGAction.CancellationReason = actionParms.parmCancellationReasonId();
        bankLGAction.insert();

        // Update LG
        bankLGGuarantee.CurrentAction = bankLGAction.RecId;
        bankLGGuarantee.updateStatus(actionParms.getActionType());
        bankLGGuarantee.update();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateActionParms</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Validates the action parameters.
    /// </summary>
    /// <returns>
    ///     True if all parameters are valid; otherwise, false.
    /// </returns>
    protected boolean validateActionParms()
    {
        boolean     ret = super();
        ReasonTable reasonTable;

        if (ret)
        {
            reasonTable = ReasonTable::find(actionParms.parmCancellationReasonId());
            if (reasonTable.RecId                       == 0
                || reasonTable.BankLGCancellationReason == NoYes::No)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>