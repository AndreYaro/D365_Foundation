<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppStepInfoBuilderWorkMode</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSMobileAppStepInfoBuilderWorkMode</c> class builds step information (icon, title and extra information) for each warehouse mobile app work mode flow.
/// </summary>
public class WHSMobileAppStepInfoBuilderWorkMode extends WHSMobileAppStepInfoBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>stepId</Name>
				<Source><![CDATA[
    protected WHSMobileAppStepId stepId()
    {
        WHSMobileAppStepId stepIdLocal = super();
        if (stepIdLocal == #WMSLocationId && pass.exists(#OverrideFromPutLocation))
        {
            //override put location
            return WHSMobileAppStepIds::OverridePutNewLocation;
        }

        if (stepIdLocal == WHSMobileAppStepIds::Confirmation && this.hasControl(controlsList, #RFPut) && pass.exists(#WorkId))
        {
            // put step
            WHSWorkTable work = WHSWorkTable::find(pass.lookup(#WorkId));
            return work.isInbound() ? WHSMobileAppStepIds::InboundPut : WHSMobileAppStepIds::OutboundPut;
        }

        return stepIdLocal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>extraInfo</Name>
				<Source><![CDATA[
    protected Map extraInfo()
    {
        Map extraInfo = new Map(Types::String, Types::String);
        
        switch (stepId)
        {
            case WHSMobileAppStepIds::LocOrLPCheck:
            case WHSMobileAppStepIds::MovementConfirmCancel:
            case WHSMobileAppStepIds::CycleCountingAddLPOrFinish:
                extraInfo.insert(#XMLQuestionStepAttribute, '1');
                break;

            case WHSMobileAppStepIds::CatchWeightTag:
            case WHSMobileAppStepIds::OutboundCatchWeightTag:
            case WHSMobileAppStepIds::CatchWeightQtyOutboundWeight:
            case WHSMobileAppStepIds::CatchWeight:
            case WHSMobileAppStepIds::OutboundWeight:
            case WHSMobileAppStepIds::CatchWeightTagWeight:
                str counter = this.controlLabel(WHSWorkExecuteDisplayCatchWeightControls::CatchWeightTagCounter);
                if (counter)
                {
                    extraInfo.insert('Value', counter);
                }
                break;
        }

        return extraInfo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getStepTitle</Name>
				<Source><![CDATA[
    internal container getStepTitle(RecId _flowStepRecId, WHSMobileAppStepInputPolicy _inputPolicy)
    {
        boolean found;
        str titleTxt;

        [found, titleTxt] = super(_flowStepRecId, _inputPolicy);

        if (found &&
            primaryInputControlsList.elements() == 1 &&
            (stepId == WHSMobileAppStepIds::CycleCountQty1 ||
            stepId == WHSMobileAppStepIds::CycleCountQty2 ||
            stepId == WHSMobileAppStepIds::CycleCountQty3 ||
            stepId == WHSMobileAppStepIds::CycleCountQty4))
        {
            ListEnumerator le = primaryInputControlsList.getEnumerator();
            le.moveNext();
            Map controlMap = le.current();
            if (controlMap.exists(#XMLControlLabel))
            {
                titleTxt = strFmt("@WAX736", titleTxt, controlMap.lookup(#XMLControlLabel));
            }
        }

        return [found, titleTxt];
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>