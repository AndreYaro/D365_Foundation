<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>rTax25RegCalc_TransportTax</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class rTax25RegCalc_TransportTax extends RTax25RegCalc implements BatchRetryable
{
    RTax25RegisterTrans_TransportTaxCalc    regTrans, header;
    RAssetStandardId        standardId;
    RAssetAssessedAmount    calcTaxSum, allowanceSum, taxSum, prevPaySum;
    RefRecId                                taxCalcRefRecId;
    #define.EndMarch(31)
    #define.EndJune(30)
    #define.EndSeptember(30)
    #define.MarchNum(03)
    #define.JuneNum(06)
    #define.SeptemberNum(09)
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>advancePaymentSum</Name>
				<Source><![CDATA[
    private RAssetNetBookValue advancePaymentSum()
    {
        RTax25RegisterTrans_TransportTax    taxCalcAsset;
        RTax25RegisterJournalTrans          journalTrans;
        RTax25RegisterTrans         rTax25RegTransAsset;
        RTax25RegisterJournalTable  rTax25RegJournalTable;
        TransDate                   fromDate = registerJournalTrans.fromDate();
        RAssetAssessedAmount        ret = 0.0;
        ;
    
        if (registerJournalTrans.toDate() != dateEndYr(fromDate))
            return 0.;
    
        while select rTax25RegTransAsset
            order by LineNumber
            where rTax25RegTransAsset.AssessedTaxRegisterType == RAssetAssessedTaxRegType::TransportTax       &&
                  rTax25RegTransAsset.FiscalYear              == year(fromDate)                         &&
                  rTax25RegTransAsset.BlockType               == ReportBlockType::Body
        exists join rTax25RegJournalTable
            where rTax25RegJournalTable.JournalNum            == rTax25RegTransAsset.JournalNum               &&
                  rTax25RegJournalTable.PeriodNum             == rTax25RegTransAsset.PeriodNum                &&
                  rTax25RegJournalTable.PeriodType            == rTax25RegTransAsset.PeriodType               &&
                  rTax25RegJournalTable.FiscalYear            == rTax25RegTransAsset.FiscalYear               &&
                  rTax25RegJournalTable.RegisterJournalType   == RTax25RegisterJournalType::Asset_RU       &&
                  (rTax25RegJournalTable.EndDate              == mkdate(#EndMarch, #MarchNum, year(fromDate)) ||
                   rTax25RegJournalTable.EndDate              == mkdate(#EndJune, #JuneNum, year(fromDate))   ||
                   rTax25RegJournalTable.EndDate              == mkdate(#EndSeptember, #SeptemberNum, year(fromDate)))
        {
            if (conpeek(rTax25RegTransAsset.RegisterValues, 1) == regTrans.RCOAD             &&
                conpeek(rTax25RegTransAsset.RegisterValues, 2) == regTrans.BudgetRevenueCode &&
                conpeek(rTax25RegTransAsset.RegisterValues, 3) == regTrans.SalesTaxCode)
            {
                ret += conpeek(rTax25RegTransAsset.RegisterValues, 6);
            }
        }
    
        select sum(TaxAmount) from taxCalcAsset
            where taxCalcAsset.BlockType         == ReportBlockType::Body      &&
                  taxCalcAsset.RCOAD             == regTrans.RCOAD             &&
                  taxCalcAsset.BudgetRevenueCode == regTrans.BudgetRevenueCode &&
                  taxCalcAsset.SalesTaxCode      == regTrans.SalesTaxCode
        exists join journalTrans
            where journalTrans.RecId        == taxCalcAsset.JournalTransRefRecId &&
                  journalTrans.RegisterId   == registerId                        &&
                  ! journalTrans.Cancelled
        exists join rTax25RegJournalTable
            where rTax25RegJournalTable.JournalNum          == journalTrans.JournalNum                      &&
                  rTax25RegJournalTable.PeriodNum           == journalTrans.PeriodNum                       &&
                  rTax25RegJournalTable.PeriodType          == journalTrans.PeriodType                      &&
                  rTax25RegJournalTable.FiscalYear          == journalTrans.FiscalYear                      &&
                  rTax25RegJournalTable.RegisterJournalType == RTax25RegisterJournalType::Asset_RU          &&
                  (rTax25RegJournalTable.EndDate            == mkdate(#EndMarch, #MarchNum, year(fromDate)) ||
                   rTax25RegJournalTable.EndDate            == mkdate(#EndJune, #JuneNum, year(fromDate))   ||
                   rTax25RegJournalTable.EndDate            == mkdate(#EndSeptember, #SeptemberNum, year(fromDate)));
    
        return ret + taxCalcAsset.TaxAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calc</Name>
				<Source><![CDATA[
    public void calc()
    {
        try
        {
            GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceRU00083, funcName());

            ttsbegin;
    
            super();
    
            while select regTrans
                order by LineNumber
                where regTrans.JournalTransRefRecId == taxCalcRefRecId
            {
                if (! regTrans.AssetId)
                {
                    if (! header)
                    {
                        this.outHeader();
                    }
    
                    if (header                                                           &&
                        regTrans.RCOAD                              &&
                        (regTrans.RCOAD             != header.RCOAD ||
                         regTrans.BudgetRevenueCode != header.BudgetRevenueCode))
                    {
                        this.outFooter();
                        calcTaxSum = 0; allowanceSum = 0; taxSum= 0; prevPaySum = 0;
                        this.outHeader();
                    }
    
                    if (! regTrans.RefTableId)
                    {
                        header = regTrans.data();
                    }
    
                    this.fillSections();
                }
            }
    
            if (header)
            {
                this.outFooter();
            }
    
            ttscommit;
        }
        catch(Exception::Deadlock)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillSections</Name>
				<Source><![CDATA[
    private void fillSections()
    {
        RTax25RegisterTrans_TransportTaxCalc    taxCalcAsset;
        RAssetAssessedAmount      calcTax, allowance, tax, prevPay;
        ;
    
        while select AssetId, Tax, ExemptionAmount, TaxAllowanceAmount, TaxAmount from taxCalcAsset
            order by LineNumber
            where taxCalcAsset.JournalTransRefRecId == taxCalcRefRecId &&
                  taxCalcAsset.LineNumber           >  regTrans.LineNumber
        {
            if (! taxCalcAsset.AssetId)
                break;
    
            calcTax += taxCalcAsset.Tax;
            allowance += taxCalcAsset.ExemptionAmount;
            allowance += taxCalcAsset.TaxAllowanceAmount;
            tax += taxCalcAsset.TaxAmount;
        }
    
        prevPay = this.advancePaymentSum();
    
        regQuery.addBodyRecord(0, [regTrans.RCOAD,
                                    regTrans.BudgetRevenueCode,
                                    regTrans.SalesTaxCode,
                                   calcTax,
                                   allowance,
                                   tax,
                                   prevPay],
                                    regTrans.RefTableId, regTrans.RefRecId);
    
        calcTaxSum   += calcTax;
        allowanceSum += allowance;
        taxSum       += tax;
        prevPaySum   += prevPay;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public boolean init(RTax25RegisterJournalTrans  _registerJournalTrans = null,
                        Batch                       _batch                = null)
    {
        boolean ret;
    
        ret = super(_registerJournalTrans, _batch);
    
        standardId = RAssetParameters::standardIdDefault();
        taxCalcRefRecId = RTax25RegCalcController::findChildJournalTrans(RTax25RegisterType::PaymentReceipt,
                                                                         _registerJournalTrans,
                                                                         RAssetAssessedTaxRegType::TransportTaxCalc).RecId;
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>outFooter</Name>
				<Source><![CDATA[
    private void outFooter()
    {
        ;
        regQuery.addFooterRecord(0, [header.RCOAD,
                                      header.BudgetRevenueCode,
                                     '',
                                     calcTaxSum, allowanceSum, taxSum, prevPaySum], 0,
                                      strfmt("@GLS116803", header.RCOAD, header.BudgetRevenueCode));
    }

]]></Source>
			</Method>
			<Method>
				<Name>outHeader</Name>
				<Source><![CDATA[
    private void outHeader()
    {
        ;
        regQuery.addHeaderRecord(0, conNull(), 0, strfmt("@SYS76498", regTrans.RCOAD, regTrans.BudgetRevenueCode));
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns true.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>