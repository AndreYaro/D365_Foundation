<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchLineExplodeBOM</Name>
	<SourceCode>
		<Declaration><![CDATA[
class PurchLineExplodeBOM
{
    PurchLine purchLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFormRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the <c>FormRun</c> object.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    /// <param name="_purchLine">
    /// The <c>PurchLine</c> record.
    /// </param>
    /// <returns>
    /// A <c>FormRun</c> object.
    /// </returns>
    public FormRun createFormRun(Args _args, PurchLine _purchLine)
    {
        Args formArgs = new Args(formstr(BOMExpandPurch));
        formArgs.caller(_args.caller());
        formArgs.record(_purchLine);
        return classfactory.formRunClass(formArgs);
    }

]]></Source>
			</Method>
			<Method>
				<Name>explode</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method sets the parameters and opens the form.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public void explode(Args _args)
    {
        if (!_args || !_args.record() || _args.dataset() != tablenum(PurchLine))
        {
            throw error("@SYS25407");
        }

        purchLine = _args.record() as PurchLine;
        InventTable inventTable = purchLine.inventTable();
        if (!inventTable)
        {
            throw error("@SYS25407");
        }

        if (!inventTable.inventItemType().canHaveBOM())
        {
            throw error("@SYS2069");
        }

        FormRun bomExpand = this.createFormRun(_args, purchLine);
        bomExpand.run();
        bomExpand.wait();

        if (bomExpand.closedOk())
        {
            this.updateDataSource(FormDataUtil::getFormDataSource(_args.record()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates header and lines in the caller form.
    /// </summary>
    /// <param name="_purchLineDS">
    /// A reference to PurchLine data source from PurchTable
    /// </param>
    public void updateDataSource(FormDataSource _purchLineDS)
    {
        if (FormDataUtil::isFormDataSource(purchLine))
        {
            _purchLineDS.executeQuery();
            if (formDataSourceHasMethod(_purchLineDS, identifierStr(purchTable_ds)))
            {
                Object purchLineDSObject = _purchLineDS as Object;
                formDataSource purchTableDS = purchLineDSObject.purchTable_ds();
                purchTableDS.reread();
                purchTableDS.refresh();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PurchLineExplodeBOM construct()
    {
        return new PurchLineExplodeBOM();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args _args)
    {
        PurchLineExplodeBOM purchLineExplodeBOM = PurchLineExplodeBOM::construct();
        purchLineExplodeBOM.explode(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>