<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgPayRule_HcmPersonSkill</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>JmgPayRule_HcmPersonSkill</c> class represents the Skills pay rule.
/// </summary>
/// <remarks>
/// This class allows users to delimit pay agreement lines by worker skills.
/// </remarks>
class JmgPayRule_HcmPersonSkill extends JmgPayRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the description of the skills pay rule.
    /// </summary>
    /// <returns>
    ///    The description of the skills pay rule.
    /// </returns>
    public JmgDescription description()
    {
        return "@SYS30086";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDesignTimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the design time data source for the skills pay rule.
    /// </summary>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the design time data source.
    /// </returns>
    /// <remarks>
    ///    The skills pay rule adds an <c>HcmPersonRecId</c> field and a <c>JmgProfileDate</c> field to the
    ///    custom design data source.
    /// </remarks>
    protected ExpressionDataSource getDesignTimeCustomDataSource()
    {
        ExpressionDataSource ret;

        ret = super();

        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(HcmPersonRecId));
        this.addDesignTimeFieldToDataSource(ret, extendedtypestr(JmgProfileDate));
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJoinConditions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the expression for the Skills pay rule.
    /// </summary>
    /// <returns>
    ///    The expression for the Skills pay rule.
    /// </returns>
    /// <remarks>
    ///    The Skills pay rule adds two conditions: The <c>Person</c> field equals the <c>Query.Person</c>
    ///    value, and the <c>ProfileDate</c> field is on or after the <c>Query.RatingLevelDate</c> value.
    /// </remarks>
    public Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement getJoinConditions()
    {
        Query q = this.getQuery();

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionProjection designTimeProjection =
            SysExpression::getDesignTimeProjectionModel(classstr(JmgPayRule_HcmPersonSkill), this.getDesignTimeCustomDataSources());

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement expressionDefinition =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionOrElement::Create();

        Microsoft.Dynamics.AX.Framework.Expression.Model.Field ratingLevelField;
        Microsoft.Dynamics.AX.Framework.Expression.Model.Field transactionDateField;

        Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement andElement =
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionAndElement::Create();

        ratingLevelField = designTimeProjection.GetField(q.dataSourceTable(tablenum(HcmPersonSkill)).name(), fieldstr(HcmPersonSkill, RatingLevelDate));
        transactionDateField = designTimeProjection.GetField(#TransactionDataSourceId, extendedtypestr(JmgProfileDate));

        andElement.AddDesignElement(
            Microsoft.Dynamics.AX.Framework.Expression.Model.ExpressionFieldDateElement::Create(
                ratingLevelField,
                Microsoft.Dynamics.AX.Framework.Expression.Model.DateOperator::OnOrBefore,
                transactionDateField));

        expressionDefinition.AddAndElement(andElement);

        return expressionDefinition;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the name of the <c>JmgPayRuleHcmPersonSkill</c> query.
    /// </summary>
    /// <returns>
    ///    The name of the <c>JmgPayRuleHcmPersonSkill</c> query.
    /// </returns>
    queryName getQueryName()
    {
        return querystr(JmgPayRuleHcmPersonSkill);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRuntimeCustomDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the runtime custom time data source for the skills pay rule.
    /// </summary>
    /// <param name="_jmgStampJournalTrans">
    ///    The transaction from where the values of the runtime fields are initialized.
    /// </param>
    /// <returns>
    ///    An <c>ExpressionDataSource</c> object that contains the run time data source for the skills pay
    ///    rule.
    /// </returns>
    /// <remarks>
    ///    Adds two run time fields: the <c>HcmPersonRecId</c> field, which is initialized from the
    ///    <c>Worker</c> field of the transaction and the <c>JmgProfileDate</c> field, which is initialized
    ///    from the <c>ProfileDate</c> field of the transaction.
    /// </remarks>
    protected ExpressionDataSource getRuntimeCustomDataSource(JmgStampJournalTrans _jmgStampJournalTrans)
    {
        ExpressionDataSource expressionDataSource;

        expressionDataSource = super(_jmgStampJournalTrans);

        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(HcmPersonRecId), HcmWorker::find(_jmgStampJournalTrans.Worker).Person);
        this.addRunTimeFieldToDataSource(expressionDataSource, extendedtypestr(JmgProfileDate), _jmgStampJournalTrans.ProfileDate);
        return expressionDataSource;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>