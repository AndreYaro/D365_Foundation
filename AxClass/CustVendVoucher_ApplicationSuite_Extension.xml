<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendVoucher_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(CustVendVoucher))]
public final class CustVendVoucher_ApplicationSuite_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmCustInvoiceJour_W</Name>
				<Source><![CDATA[
    public CustInvoiceJour parmCustInvoiceJour_W(CustInvoiceJour _custInvoiceJour = custInvoiceJour)
    {
        custInvoiceJour = _custInvoiceJour;
        return custInvoiceJour;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createCustVendTransForPaymSched</Name>
				<Source><![CDATA[
    protected void createCustVendTransForPaymSched(CustVendTrans _custVendTrans)
    {
        #isoCountryRegionCodes

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            if (custVendInvoiceJour.TableId == tableNum(ProjInvoiceJour) && !custVendPaymSchedLine)
            {
                CustVendPaymentSched custVendPaymSchedLocal = this.parmCustVendPaymSched();

                if (custVendPaymSchedLocal.ExtTableId == tableNum(ProjInvoiceJour))
                {
                    ProjInvoiceJour projInvoiceJour = custVendInvoiceJour;
                    ProjProposalJour projProposalJour = ProjProposalJour::find(projInvoiceJour.ProposalId);

                    CustVendPaymentSchedLine custVendPaymSchedLineLocal = this.parmCustVendPaymSchedLine();
                    select firstonly custVendPaymSchedLocal
                        where custVendPaymSchedLocal.ExtTableId == projProposalJour.TableId
                        && custVendPaymSchedLocal.ExtRecId == projProposalJour.RecId;

                    select firstonly custVendPaymSchedLineLocal where custVendPaymSchedLineLocal.ParentRecId == custVendPaymSchedLocal.RecId;

                    this.parmCustVendPaymSched(custVendPaymSchedLocal);
                    this.parmCustVendPaymSchedLine(custVendPaymSchedLineLocal);
                }
            }
        }

        next createCustVendTransForPaymSched(_custVendTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustVendPaymSched</Name>
				<Source><![CDATA[
    public CustVendPaymentSched parmCustVendPaymSched(CustVendPaymentSched _custVendPaymSched = custVendPaymSched)
    {
        custVendPaymSched = _custVendPaymSched;
        return custVendPaymSched;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustVendPaymSchedLine</Name>
				<Source><![CDATA[
    public CustVendPaymentSchedLine parmCustVendPaymSchedLine(CustVendPaymentSchedLine _custVendPaymSchedLine = custVendPaymSchedLine)
    {
        custVendPaymSchedLine = _custVendPaymSchedLine;
        return custVendPaymSchedLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initCustVendPaymSched</Name>
				<Source><![CDATA[
    protected void initCustVendPaymSched(LedgerVoucher _ledgerPostingJournal)
    {
        #ISOCountryRegionCodes
        next initCustVendPaymSched(_ledgerPostingJournal);

        CustVendPaymentSched custVendPaymSchedLocal = this.parmCustVendPaymSched();

        if (!custVendPaymSchedLocal.RecId
            && custVendInvoiceJour
            && SysCountryRegionCode::isLegalEntityInCountryRegion([#isoBR]))
        {
            PurchComplementaryInvoiceHeader purchComplementaryInvoiceHeader;

            select firstonly RecId from purchComplementaryInvoiceHeader
                where purchComplementaryInvoiceHeader.LedgerVoucher == _ledgerPostingJournal.lastVoucher()
                    && purchComplementaryInvoiceHeader.TransDate == _ledgerPostingJournal.lastTransDate()
            join custVendPaymSchedLocal
                where custVendPaymSchedLocal.ExtTableId == purchComplementaryInvoiceHeader.TableId
                    && custVendPaymSchedLocal.ExtRecId == purchComplementaryInvoiceHeader.RecId 
                    && custVendPaymSchedLocal.Name == purchComplementaryInvoiceHeader.PaymSched;
        }

        this.parmCustVendPaymSched(custVendPaymSchedLocal);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>