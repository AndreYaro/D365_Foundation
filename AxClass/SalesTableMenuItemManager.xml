<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesTableMenuItemManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Redirects calls to <c>SalesTableListpage</c> form, to the <c>SalesTable</c> form. 
/// </summary>
internal class SalesTableMenuItemManager
{        
    private FormRun formRun;
        
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    [Hookable(false)]
    public void run()
    {
        if (formRun.name() != formStr(SalesTablelistpage))
        {
            throw error("@SYS104799");
        }
        
        if (SalesParameters::find().IsSalesTableMenuItemManagerEnabled)
        {
            MenuFunction menuFunction = this.newMenuFunction();
            if (menuFunction)
            {
                menuFunction.run(this.newArgsFromArgs(formRun.args()));
                formRun.lifecycleHelper().exitForm();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newMenuFunction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a <c>MenuFunction</c> class object if the SalesTableListPage menu item is shipped as part of the standard application.  
    /// </summary>
    /// <returns>
    /// A <c>MenuFunction</c> class object.
    ///</returns>
    [Hookable(false)]
    private MenuFunction newMenuFunction()
    {
        MenuFunction menuFunction;
            
        void menuFunctionNewFormRun()
        {            
            menuFunction = new MenuFunction(menuItemDisplayStr(SalesTableDetails), MenuItemType::Display);
            menuFunction.objectType(MenuItemObjectType::Form);
            menuFunction.object(formStr(SalesTable));
            menuFunction.formViewOption(FormViewOption::Grid);
        }
        
        switch (formRun.args().menuItemName())
        {
            case menuItemDisplayStr(SalesTableListPage) :
            case menuItemDisplayStr(SalesTableListPageFulfilled) :
            case menuItemDisplayStr(SalesTableListPagePartiallyPicked) :
            case menuItemDisplayStr(SalesTableListPageReturnReplacementOrders) :
            case menuItemDisplayStr(SalesTableListPageReturnReplacementOrdersAll) :
                menuFunctionNewFormRun();
                break;

            case menuItemDisplayStr(SalesTableListPage_Proj) :
            case menuItemDisplayStr(SalesTableListPageJournal) :
                menuFunctionNewFormRun();
                menuFunction.query('');
                break;
           
            case menuItemDisplayStr(SalesTableListPageOpen) :
                menuFunctionNewFormRun();
                menuFunction.query(queryStr(SalesTableGridViewOpen));
                break;
              
            case menuItemDisplayStr(SalesTableListPageReceivedNotCreditedReturns) :
                menuFunctionNewFormRun();
                menuFunction.query(queryStr(SalesTableGridViewReceivedNotCreditedReturns));
                break;
            
            case menuItemDisplayStr(SalesTableListPageShippedNotInvoiced) :
                menuFunctionNewFormRun();
                menuFunction.query(queryStr(SalesTableGridViewShippedNotInvoiced));
                break;

            default : break;
        }

        return menuFunction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected static SalesTableMenuItemManager construct()
    {
        return new SalesTableMenuItemManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFormRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an object of the <c>SalesTableMenuItemManager</c> class.
    /// </summary>
    /// <param name = "_formRun">
    /// A <c>FormRun</c> class object.
    /// </param>
    /// <returns>
    /// A <c>SalesTableMenuItemManager</c> class object.
    /// </returns>
    [Hookable(false)]
    public static SalesTableMenuItemManager newFormRun(FormRun _formRun)
    {
        SalesTableMenuItemManager salesTableMenuItemManager = SalesTableMenuItemManager::construct();
        salesTableMenuItemManager.formRun = _formRun;
        return salesTableMenuItemManager;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newArgsFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates and initiates an new <c>Args</c> class object from an existing <c>Args</c> class object.
    /// </summary>
    /// <param name = "_args">
    /// An <c>Args</c> class object.
    /// </param>
    /// <returns>
    /// An <c>Args</c> class object.
    ///</returns>
    [Hookable(false)]
    internal Args newArgsFromArgs(Args _args)
    {
        Args args = new Args();
        args.parmEnumType(_args.parmEnumType());
        args.parmEnumeration(_args.parmEnumeration());
        args.record(_args.record());

        return args;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>