<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailCDXDownloadSessionReApply</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Retail CDX class used for reapplying download session.
/// </summary>
internal class RetailCDXDownloadSessionReapply extends RunBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    internal static void main(Args _args)
    {
        if (!_args || !_args.record() || _args.dataset() != tableNum(RetailCDXDownloadSessionDataStore))
        {
            return;
        }

        RetailCDXDownloadSessionDataStore downloadSessionDataStore = _args.record();

        RetailCDXDownloadSession downloadSession;
        select firstOnly JobID, Session
            from downloadSession
            where downloadSession.RecId == downloadSessionDataStore.Session;

        if (downloadSessionDataStore.isSuspended())
        {
            if (RetailCDXDownloadSessionReapply::getUserConfirmation(downloadSession.Session, downloadSession.JobID, downloadSessionDataStore.dataStoreName()))
            {
                ttsBegin;
                downloadSessionDataStore.TryCount = 0;
                downloadSessionDataStore.Message = '';
                downloadSessionDataStore.Status = RetailCDXDownloadSessionStatus::Available;
                downloadSessionDataStore.update();
                ttsCommit;
            }
        }
        else
        {
            RetailCDXDownloadSessionReapply::displayWarningMsg(downloadSession.Session, downloadSessionDataStore.Status, downloadSessionDataStore.dataStoreName());
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>getUserConfirmation</Name>
				<Source><![CDATA[
    private static boolean getUserConfirmation(RetailCDXSessionNumber _downloadSessionId, RetailConnJobId _jobId, str _dataStoreName)
    {
        boolean confirmationResult = false;

        // Do you want to reapply download session: %1, Job: %2, on database %3?
        str msg = strFmt("@Retail:ReapplyDownloadSessionConfirmationLabel", _downloadSessionId, _jobId,  _dataStoreName);

        if (Box::yesNo(msg, DialogButton::Yes) == DialogButton::Yes)
        {
            confirmationResult = true;
        }

        return confirmationResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>displayWarningMsg</Name>
				<Source><![CDATA[
    private static void displayWarningMsg(RetailCDXSessionNumber _sessionId, RetailCDXDownloadSessionStatus _status, str _dataStoreName)
    {
        // Download session %1 with status %2 cannot be reapplied on data store %3. Please select a download session that is suspended and try again.
        str msg = strFmt("@Retail:ReapplyDownloadSessionWarningLabel", _sessionId, _status, _dataStoreName);
        Box::warning(msg);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>