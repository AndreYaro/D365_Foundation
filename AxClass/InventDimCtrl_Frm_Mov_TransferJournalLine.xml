<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventDimCtrl_Frm_Mov_TransferJournalLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventDimCtrl_Frm_Mov_TransferJournalLine</c> class controls inventory dimensions on the inventory transfer journal form lines.
/// </summary>
public class InventDimCtrl_Frm_Mov_TransferJournalLine extends InventDimCtrl_Frm_Mov
{
    #ISOCountryRegionCodes
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventDimCtrl_Frm_Mov_TransferJournalLine construct()
    {
        return new InventDimCtrl_Frm_Mov_TransferJournalLine();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromFormMovement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes an instance of this class to be used in forms.
    /// </summary>
    /// <param name = "_formRun">The form containing the inventory dimension fields to be controlled.</param>
    /// <param name = "_movement">Inventory movement whose inventory dimension fields are to be controlled.</param>
    /// <returns>An instance of the <c>InventDimCtrl_Frm_Mov_TransferJournalLine</c> class.</returns>
    public static InventDimCtrl_Frm_Mov_TransferJournalLine newFromFormMovement(
        FormRun         _formRun,
        InventMovement  _movement)
    {
        InventDimCtrl_Frm_Mov inventDimCtrl = InventDimCtrl_Frm_Mov_TransferJournalLine::construct();
        InventDimAxFormAdapter adapter = InventDimAxFormAdapter::newFromForm(_formRun);

        inventDimCtrl.parmCallingElement(adapter);
        inventDimCtrl.parmMovement(_movement);
        inventDimCtrl.init();

        return inventDimCtrl;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canEnableDimensionInCurrentCountryContext</Name>
				<Source><![CDATA[
    protected boolean canEnableDimensionInCurrentCountryContext(FieldId _fieldId)
    {
        return this.isOwnerFieldInConsignmentConfiguration(_fieldId) || super(_fieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustEnableControl</Name>
				<Source><![CDATA[
    protected boolean mustEnableControl(InventDimFormControlInterface _fc, InventDimParm _dimParmEnabled = dimParmEnabled)
    {
        if (this.isInventOwnerReceiptField(_fc))
        {
            if (this.parmMovement()
                && this.parmMovement().inventTable().whsItemInventoryOwnershipEnabled())
            {
                return super(_fc, _dimParmEnabled);
            }

            if (!SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
            {
                return false;
            }
        }

        return super(_fc, _dimParmEnabled);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isInventOwnerReceiptField</Name>
				<Source><![CDATA[
    private boolean isInventOwnerReceiptField(InventDimFormControlInterface _fc)
    {
        return inventDimControls.datasourceName(_fc) == identifierstr(InventDimReceipt)
            && fieldExt2Id(_fc.dataField()) == fieldNum(InventDim, InventOwnerId_RU);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>