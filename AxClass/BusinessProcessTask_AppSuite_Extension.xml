<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BusinessProcessTask_AppSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of the <c>BusinessProcessTask</c> table, which is used for retail business process.
/// </summary>
[ExtensionOf(tableStr(BusinessProcessTask))]
final class BusinessProcessTask_AppSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Additional validation logic for retail business process task
    /// </summary>
    /// <returns>validation result</returns>
    public boolean validateWrite()
    {
        boolean isValid = next validateWrite();

        BusinessProcessHeader header = BusinessProcessHeader::find(this.ProcessHeader);
        if (header && header.GenericSubtype == BusinessProcessGenericSubtype::Retail)
        {
            if (this.Name == '')
            {
                isValid = checkFailed("@RetailTaskManagement:RetailBusinessProcessTaskEmptyNameError") && isValid;
            }
            
            if (this.Status == BusinessProcessTaskStatus::Canceled)
            {
                isValid = checkFailed("@RetailTaskManagement:InvalidTaskStatus") && isValid;
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateStatusByWorker</Name>
				<Source><![CDATA[
    /// <summary>
    ///  Updates the status of the task. Different from the updateStatusFields, the worker here is POS worker, not HQ user.
    /// </summary>
    /// <param name = "_status">The new status.</param>
    /// <param name = "_workerRecId">The record id of the worker.</param>
    public void updateStatusByWorker(BusinessProcessTaskStatus _status, HcmWorkerRecId _workerRecId)
    {
        ttsbegin;
        this.updateStatusFields(_status);
        if (_status == BusinessProcessTaskStatus::Completed
            || _status == BusinessProcessTaskStatus::Canceled)
        {
            this.ResolvedBy = _workerRecId;
        }
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>