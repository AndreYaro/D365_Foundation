<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>McrPriceHistoryLine_SalesQueryBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///		The class <c>McrPriceHistoryLine_SalesQueryBuilder</c> builds the query
///     for selecting the rebate agreement associated with an item.
/// </summary>
public class McrPriceHistoryLine_SalesQueryBuilder
{
    InventDim			salesLineInventDim;
    SalesLine           salesLine;
    SalesTable          salesTable;
    InventTable			inventTable;
    CustTable           custTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newfromQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///		Creates an instance of <c>McrPriceHistoryLine_SalesQueryBuilder</c> class.
    /// </summary>
    /// <param name = "_salesLine">
    ///		A <c>SalesLine</c> record.
    /// </param>
    /// <param name = "_salesTable">
    ///		A <c>SalesTable</c> record.
    /// </param>
    /// <param name = "_inventTable">
    ///		A <c>InventTable</c> record.
    /// </param>
    /// <param name = "_custTable">
    ///		A <c>CustTable</c> record.
    /// </param>
    /// <returns>
    ///		An instance of <c>McrPriceHistoryLine_SalesQueryBuilder</c> class.
    /// </returns>
    internal static McrPriceHistoryLine_SalesQueryBuilder newfromQuery(
        SalesLine		_salesLine,
        SalesTable		_salesTable,
        InventTable		_inventTable,
        CustTable		_custTable)
    {
        McrPriceHistoryLine_SalesQueryBuilder queryBuilder = new McrPriceHistoryLine_SalesQueryBuilder();

        queryBuilder.salesLine				= _salesLine;
        queryBuilder.salesTable				= _salesTable;
        queryBuilder.inventTable			= _inventTable;
        queryBuilder.custTable				= _custTable;
        queryBuilder.salesLineInventDim		= _salesLine.inventDim();

        return queryBuilder;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildRebateAgreementQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///		Builds query object for <c>PdsRebateAgreement</c> record.
    /// </summary>
    /// <param name = "_pdsRebateAgreement">
    ///		A <c>PdsRebateAgreement</c> record.
    /// </param>
    /// <returns>
    ///		A rebate agreement query object.
    /// </returns>
    internal SysDaQueryObject buildRebateAgreementQuery(PdsRebateAgreement _pdsRebateAgreement)
    {
        SysDaQueryObject qePdsRebateAgreement = new SysDaQueryObject(_pdsRebateAgreement);
        qePdsRebateAgreement.orderByClause().add(fieldStr(PdsRebateAgreement, PdsFindNext));
        SysDaQueryExpression whereClause = this.buildWhereClauseForRebateAgreementQuery(_pdsRebateAgreement);
        qePdsRebateAgreement.whereClause(whereClause);

        TAMRebateInventDimExistsJoinQueryBuilder tamRebateInventDimExistsJoinQueryBuilder = new TAMRebateInventDimExistsJoinQueryBuilder();
        tamRebateInventDimExistsJoinQueryBuilder.existsJoinInventDim(qePdsRebateAgreement, new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, InventDimId)), salesLineInventDim);

        return qePdsRebateAgreement;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildWhereClauseForRebateAgreementQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///		Builds where clause for <c>PdsRebateAgreement</c> record.
    /// </summary>
    /// <param name = "_pdsRebateAgreement">
    ///		A <c>PdsRebateAgreement</c> record.
    /// </param>
    /// <returns>
    ///		A rebate agreement query expression.
    /// </returns>
    protected SysDaQueryExpression buildWhereClauseForRebateAgreementQuery(PdsRebateAgreement _pdsRebateAgreement)
    {
        SysDaQueryExpression pdsRebateAgreementWhereClause =
                    (new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, Validated)), new SysDaValueExpression(NoYes::Yes))
                .and(new SysDaNotEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, Verifier)), new SysDaValueExpression('')))

                .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateCode)), new SysDaValueExpression(TableGroupAll::All))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateCode)), new SysDaValueExpression(TableGroupAll::GroupId))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateRelation)), new SysDaFieldExpression(salesTable, fieldStr(SalesTable, PdsCustRebateGroupId)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateCode)), new SysDaValueExpression(TableGroupAll::GroupId))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::Freight)))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateRelation)), new SysDaFieldExpression(custTable, fieldStr(CustTable, PartyCountry)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateCode)), new SysDaValueExpression(TableGroupAll::GroupId))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::TMA)))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateRelation)), new SysDaFieldExpression(salesTable, fieldStr(SalesTable, PdsRebateProgramTMAGroup)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateCode)), new SysDaValueExpression(TableGroupAll::Table))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsCustRebateRelation)), new SysDaFieldExpression(salesTable, fieldStr(SalesTable, CustAccount))))))

                .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateCode)), new SysDaValueExpression(PdsRebateItemCode::All))
                    .or(((new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::Rebate)))
                          .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::TMA))))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateCode)), new SysDaValueExpression(PdsRebateItemCode::GroupId)))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateRelation)), new SysDaFieldExpression(salesLine, fieldStr(SalesLine, PdsItemRebateGroupId)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::Freight))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateCode)), new SysDaValueExpression(PdsRebateItemCode::GroupId)))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateRelation)), new SysDaFieldExpression(inventTable, fieldStr(InventTable, PdsFreightAllocationGroupId))))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(custTable, fieldStr(CustTable, PdsFreightAccrued)), new SysDaValueExpression(NoYes::Yes))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateCode)), new SysDaValueExpression(PdsRebateItemCode::Table))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateRelation)), new SysDaFieldExpression(salesLine, fieldStr(SalesLine, ItemId)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsItemRebateCode)), new SysDaValueExpression(PdsRebateItemCode::Selection))))

                .and(new SysDaNotEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::BillBack)))
                .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, RebateGenericCurrency)), new SysDaValueExpression(NoYes::Yes))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, RebateCurrency)), new SysDaFieldExpression(salesLine, fieldStr(SalesLine, CurrencyCode)))))

                .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, RebateUOMOption)), new SysDaValueExpression(PdsRebateUOMOption::Convert))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsUnitType)), new SysDaValueExpression(PdsUnitType::InventoryUnit))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateUnit)), new SysDaFieldExpression(salesLine, fieldStr(SalesLine, SalesUnit)))))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsUnitType)), new SysDaValueExpression(PdsUnitType::CWUnit))
                        .and(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateUnit)), new SysDaValueExpression(salesLine.pdsCWUnitId())))))
                .and(new SysDaEqualsExpression(new SysDaValueExpression(PdsGlobal::pdsIsCWItem(salesLine.ItemId)), new SysDaValueExpression(true))
                    .or(new SysDaEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsUnitType)), new SysDaValueExpression(PdsUnitType::InventoryUnit))))
                .and(new SysDaEqualsExpression(new SysDaFieldExpression(custTable, fieldStr(CustTable, PdsFreightAccrued)), new SysDaValueExpression(NoYes::Yes))
                    .or(new SysDaNotEqualsExpression(new SysDaFieldExpression(_pdsRebateAgreement, fieldStr(PdsRebateAgreement, PdsRebateProgramType)), new SysDaValueExpression(PdsRebateProgramType::Freight)))));

        return pdsRebateAgreementWhereClause;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>