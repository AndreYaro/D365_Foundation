<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CzAdvanceInvoiceStatusChange</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CzAdvanceInvoiceStatusChange</c> class is responsible for changing advance invoice status for Czech republic.
/// </summary>
abstract class CzAdvanceInvoiceStatusChange extends RunBase
{
    QueryRun               queryRun;

    DialogField            newStatusField;
    CzAdvanceInvoiceStatus newStatus;

    CzAdvanceInvoiceTable  advanceInvoiceTable;

    #localmacro.CurrentList
        newStatus
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    final public Object dialog()
    {
        Dialog ret = super();

        newStatusField = ret.addFieldValue(enumStr(CzAdvanceInvoiceStatus),
                                           newStatus,
                                           "@SYS33665",
                                           "@GLS111701");

        ret.caption("@GLS111701");

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    final public boolean getFromDialog()
    {
        boolean ret = super();

        newStatus = newStatusField.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    public void initParmDefault()
    {
        switch (advanceInvoiceTable.Status)
        {
            case CzAdvanceInvoiceStatus::Open     :
                newStatus = CzAdvanceInvoiceStatus::PartPaid;
                break;

            case CzAdvanceInvoiceStatus::PartPaid :
                newStatus = CzAdvanceInvoiceStatus::Closed;
                break;

            case CzAdvanceInvoiceStatus::Closed   :
                newStatus = CzAdvanceInvoiceStatus::Open;
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAdvanceInvoiceTable</Name>
				<Source><![CDATA[
    final public CzAdvanceInvoiceTable parmAdvanceInvoiceTable(CzAdvanceInvoiceTable _advanceInvoiceTable = advanceInvoiceTable)
    {
        advanceInvoiceTable = _advanceInvoiceTable;

        return advanceInvoiceTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    final public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        CzAdvanceInvoiceTable tmpTable;

        try
        {
            ttsbegin;

            queryRun.reset();
            while (queryRun.next())
            {
                tmpTable = queryRun.get(advanceInvoiceTable.TableId);

                tmpTable.Status = newStatus;
                tmpTable.update();
            }

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    final public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromAdvanceInvoice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>CzAdvanceInvoiceStatusChange</c> class.
    /// </summary>
    /// <param name="_table">
    /// A <c>CzAdvanceInvoiceTable</c> map that represents the advance invoice record to be modified.
    /// </param>
    /// <returns>
    /// An instance of the <c>CzAdvanceInvoiceStatusChange</c> class.
    /// </returns>
    public static CzAdvanceInvoiceStatusChange newFromAdvanceInvoice(CzAdvanceInvoiceTable _table)
    {
        CzAdvanceInvoiceStatusChange ret;

        switch (_table.TableId)
        {
            case tablenum(CzCustAdvanceInvoiceTable) :
                ret = new CzAdvanceInvoiceStatusChange_Cust();
                break;

            case tablenum(CzVendAdvanceInvoiceTable) :
                ret = new CzAdvanceInvoiceStatusChange_Vend();
                break;
        }

        ret.parmAdvanceInvoiceTable(_table);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runFromJournal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Changes the status of a <c>CzAdvanceInvoiceTable</c> record.
    /// </summary>
    /// <param name="_advanceInvoiceTable">
    /// A <c>CzAdvanceInvoiceTable</c> map representing the advance invoice record to be modified.
    /// </param>
    public static void runFromJournal(CzAdvanceInvoiceTable _advanceInvoiceTable)
    {
        CzAdvanceInvoiceStatusChange change = CzAdvanceInvoiceStatusChange::newFromAdvanceInvoice(_advanceInvoiceTable);
        FormDataSource formDataSource;

        if (change.prompt())
        {
            change.runOperation();

            if (FormDataUtil::isFormDataSource(_advanceInvoiceTable))
            {
                formDataSource = FormDataUtil::getFormDataSource(_advanceInvoiceTable);
                formDataSource.research(true);
                formDataSource.refresh();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>