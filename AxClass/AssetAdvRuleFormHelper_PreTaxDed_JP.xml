<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetAdvRuleFormHelper_PreTaxDed_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The asset advanced rule form helper for pre-tax deduction settlement and carry forward rules.
/// </summary>
[AssetAdvRuleFormEntranceAttribute_JP(menuitemDisplayStr(AssetAdvRule_PreTaxDeduction_JP))]
class AssetAdvRuleFormHelper_PreTaxDed_JP extends AssetAdvRuleFormHelper_JP
{
    #AssetAdvRule_PreTaxDed_Settlement_JP
    #AssetAdvRule_PreTaxDed_CarryForward_JP
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createCarryForwardRule</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates carry forward rule
    /// </summary>
    /// <param name="_ruleGroupRecId">
    /// The rule group record id
    /// </param>
    /// <param name="_overUnderType">
    /// The over under type
    /// </param>
    /// <param name="_depType">
    /// The asset depreciation type
    /// </param>
    /// <param name="_carryFwdYears">
    /// The carry forward years
    /// </param>
    protected void createCarryForwardRule(
        RefRecId                                    _ruleGroupRecId,
        AssetDepPreTaxDeductionOverUnderType_JP     _overUnderType,
        AssetDepType_JP                             _depType,
        int                                         _carryFwdYears)
    {
        AssetAdvRuleType_JP     assetAdvRuleType;

        assetAdvRuleType = AssetAdvRuleType_JP::construct(
            enumStr(AssetAdvancedRuleType_PreTaxDeduction_JP),
            AssetAdvancedRuleType_PreTaxDeduction_JP::DepOverDownCarryForward,
            _ruleGroupRecId);

        assetAdvRuleType.setItemValue(#AssetAdvRule_PreTaxDed_CarryFwd_OverUnderType, _overUnderType);
        assetAdvRuleType.setItemValue(#AssetAdvRule_PreTaxDed_CarryFwd_DepType, _depType);
        assetAdvRuleType.setItemValue(#AssetAdvRule_PreTaxDed_CarryFwd_CarryFwdYears, _carryFwdYears);
        assetAdvRuleType.save();
    }
]]></Source>
			</Method>
			<Method>
				<Name>createDefaultRules</Name>
				<Source><![CDATA[
    protected void createDefaultRules()
    {
        AssetAdvancedRuleGroup_JP   assetAdvancedRuleGroupLoc;

        // Create settlement rules
        assetAdvancedRuleGroupLoc = AssetAdvancedRuleGroup_JP::findOrCreate(
            AssetAdvancedRuleGroupType_JP::AssetDepPreTaxDeductionSettlement,
            this.getSourceTableId(),
            this.getSourceRecIdId());

        this.createSettlementRule(assetAdvancedRuleGroupLoc.RecId, AssetDepType_JP::SpecialDepreciationWithDirectOff, AssetDepType_JP::OrdinaryDepreciation);
        this.createSettlementRule(assetAdvancedRuleGroupLoc.RecId, AssetDepType_JP::AdditionalDepreciationWithDirectOff, AssetDepType_JP::OrdinaryDepreciation);
        this.createSettlementRule(assetAdvancedRuleGroupLoc.RecId, AssetDepType_JP::OrdinaryDepreciation, AssetDepType_JP::SpecialDepreciationWithDirectOff);
        this.createSettlementRule(assetAdvancedRuleGroupLoc.RecId, AssetDepType_JP::OrdinaryDepreciation, AssetDepType_JP::AdditionalDepreciationWithDirectOff);

        // Create carry forward rules
        assetAdvancedRuleGroupLoc = AssetAdvancedRuleGroup_JP::findOrCreate(
            AssetAdvancedRuleGroupType_JP::AssetDepPreTaxDeductionCarryForward,
            this.getSourceTableId(),
            this.getSourceRecIdId());

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::OverDepreciation,
            AssetDepType_JP::OrdinaryDepreciation,
            #MaxCarryForwardYearNumber);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::OverDepreciation,
            AssetDepType_JP::SpecialDepreciationWithDirectOff,
            #MaxCarryForwardYearNumber);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::OverDepreciation,
            AssetDepType_JP::AdditionalDepreciationWithDirectOff,
            #MaxCarryForwardYearNumber);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::UnderDepreciation,
            AssetDepType_JP::SpecialDepreciationWithDirectOff,
            1);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::UnderDepreciation,
            AssetDepType_JP::SpecialDepreciationWithReserve,
            1);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::UnderDepreciation,
            AssetDepType_JP::AdditionalDepreciationWithDirectOff,
            1);

        this.createCarryForwardRule(
            assetAdvancedRuleGroupLoc.RecId,
            AssetDepPreTaxDeductionOverUnderType_JP::UnderDepreciation,
            AssetDepType_JP::AdditionalDepreciationWithReserve,
            1);
    }
]]></Source>
			</Method>
			<Method>
				<Name>createSettlementRule</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates settlement rules
    /// </summary>
    /// <param name="_ruleGroupRecId">
    /// The rule group record id
    /// </param>
    /// <param name="_depTypeForOver">
    /// The over depreciation type
    /// </param>
    /// <param name="_depTypeForUnder">
    /// The under depreciation type
    /// </param>
    protected void createSettlementRule(
        RefRecId            _ruleGroupRecId,
        AssetDepType_JP     _depTypeForOver,
        AssetDepType_JP     _depTypeForUnder)
    {
        AssetAdvRuleType_JP     assetAdvRuleType;

        assetAdvRuleType = AssetAdvRuleType_JP::construct(
            enumStr(AssetAdvancedRuleType_PreTaxDeduction_JP),
            AssetAdvancedRuleType_PreTaxDeduction_JP::DepOverDownSettlement,
            _ruleGroupRecId);

        assetAdvRuleType.setItemValue(#AssetAdvRule_PreTaxDed_Settlement_DepTypeForOver, _depTypeForOver);
        assetAdvRuleType.setItemValue(#AssetAdvRule_PreTaxDed_Settlement_DepTypeForUnder, _depTypeForUnder);
        assetAdvRuleType.save();
    }
]]></Source>
			</Method>
			<Method>
				<Name>needsToCreateDefaultRulesIfEmpty</Name>
				<Source><![CDATA[
    protected boolean needsToCreateDefaultRulesIfEmpty()
    {
        boolean     ret;

        // Do you want to import the default set of rules?
        ret = (Box::okCancel("@SYP4881458", DialogButton::Ok) == DialogButton::Ok);

        return ret;
    }
]]></Source>
			</Method>
			<Method>
				<Name>ruleGroupTypeSet</Name>
				<Source><![CDATA[
    public Set ruleGroupTypeSet()
    {
        Set   ret = new Set(Types::Enum);

        ret.add(AssetAdvancedRuleGroupType_JP::AssetDepPreTaxDeductionCarryForward);
        ret.add(AssetAdvancedRuleGroupType_JP::AssetDepPreTaxDeductionSettlement);

        return ret;
    }
]]></Source>
			</Method>
			<Method>
				<Name>ruleGroupTypeSetForCreate</Name>
				<Source><![CDATA[
    public Set ruleGroupTypeSetForCreate()
    {
        Set ret;

        ret = this.ruleGroupTypeSet();

        return ret;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>