<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOutPaymControlController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustOutPaymControlController</c> class is the controller class for the
/// <c>CustOutPaymControl</c> report.
/// </summary>
public class CustOutPaymControlController extends SRSReportRunController implements BatchRetryable
{
    protected CustVendOutPaym custVendOutPaym;

    // source data containers
    protected CustVendOutPaymRecord outPaymRecord;
    protected LedgerJournalTrans ledgerJournalTrans;

    // temporary data stores
    protected CustVendOutTmp custVendOutTmp;

    // internal parameter caches
    protected RecId tmpTableDataRecId;
    protected RecId tmpSummaryDataRecId;

    protected SRSPrintDestinationSettings printDestinationSetting;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public boolean isRetryable()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fetch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fetch the payments.
    /// </summary>
    protected void fetch()
    {
        boolean getTotals = false;
        TmpAccountSum tmpAccountSum;

        // iterate through the list of CustOutPaymRecord
        ListEnumerator enumOutPaymRecords = custVendOutPaym.getOutPaymRecords().getEnumerator();
        while (enumOutPaymRecords.moveNext())
        {
            // access the current CustOutPaymRecord
            outPaymRecord = enumOutPaymRecords.current();

            // access the current LedgerJournalTrans
            ledgerJournalTrans = outPaymRecord.parmCustVendPaym().ledgerJournalTrans();

            // insert this data row into the Temp data store
            this.insert();

            // note, that we'll need to include total calculations
            getTotals = true;
        }

        // cache all of the table data records
        tmpTableDataRecId = this.storeTmpTable(custVendOutTmp);

        // check to see if Account Summary information is required
        if (getTotals)
        {
            // cache all of the account summary data records
            tmpAccountSum.setTmpData(custVendOutPaym.getSumByCurrency());
        }

        tmpSummaryDataRecId = this.storeTmpTable(tmpAccountSum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSummaryDataRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the record ID of the temporary table that is associated with the account summary
    /// information.
    /// </summary>
    /// <returns>
    /// The record ID of the temporary table that is associated with the account summary information.
    /// </returns>
    public RecId getSummaryDataRecId()
    {
        return tmpSummaryDataRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTableDataRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the record ID of the temporary table that is associated with the report body information.
    /// </summary>
    /// <returns>
    /// The record ID of the temporary table that is associated with the report body information.
    /// </returns>
    public RecId getTableDataRecId()
    {
        return tmpTableDataRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes all the required objects.
    /// </summary>
    /// <param name="_custVendOutPaym">
    /// An instance of the <c>CustVendOutPaym</c> class.
    /// </param>
    public void init(CustVendOutPaym _custVendOutPaym)
    {
        // validate input parameters
        if (_custVendOutPaym == null)
        {
            throw error(Error::missingRecord(funcname()));
        }

        this.initPrinterSettings(_custVendOutPaym);

        // initialize the session using input parameters
        custVendOutPaym = _custVendOutPaym;

        // build the report data set
        this.fetch();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initPrinterSettings</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize printer settings.
    /// </summary>
    /// <param name="_custVendOutPaym">
    /// An instance of the <c>CustVendOutPaym</c> class.
    /// </param>
    protected void initPrinterSettings(CustVendOutPaym _custVendOutPaym)
    {
        container printerSetting;

        printDestinationSetting = new SRSPrintDestinationSettings();
        printerSetting = _custVendOutPaym.getPrinterSettings(PaymDocumentType::ControlReport);
        printDestinationSetting.unpack(printerSetting);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    /// Insert one payment line.
    /// </summary>
    protected void insert()
    {
        this.populateLine();
        custVendOutTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Populdate one payment line.
    /// </summary>
    protected void populateLine()
    {
        // store the table line item data
        custVendOutTmp.AccountNum = ledgerJournalTrans.parmAccount();
        custVendOutTmp.Name = CustTable::findByCompany(ledgerJournalTrans.Company, ledgerJournalTrans.parmAccount()).name();
        custVendOutTmp.BankAccountName = outPaymRecord.parmCustVendPaym().bankAccountMap().Name;
        custVendOutTmp.RegistrationNum = outPaymRecord.parmCustVendPaym().bankAccountMap().RegistrationNum;
        custVendOutTmp.BankAccount = outPaymRecord.parmCustVendPaym().bankAccountMap().AccountNum;
        custVendOutTmp.AmountCurDebit = ledgerJournalTrans.AmountCurDebit;
        custVendOutTmp.AmountCur = ledgerJournalTrans.AmountCurCredit;
        custVendOutTmp.CurrencyCode = ledgerJournalTrans.CurrencyCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Changes the report contract before running the report.
    /// </summary>
    protected void preRunModifyContract()
    {
        CustOutPaymControlContract contract;

        this.init(this.parmArgs().caller());

        if (printDestinationSetting)
        {
            this.parmReportContract().parmPrintSettings(printDestinationSetting);
        }

        contract = this.parmReportContract().parmRdpContract() as CustOutPaymControlContract;
        contract.parmSummaryDataRecId(this.getSummaryDataRecId());
        contract.parmTableDataRecId(this.getTableDataRecId());
    }

]]></Source>
			</Method>
			<Method>
				<Name>storeTmpTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Store the temporary table.
    /// </summary>
    /// <param name = "tblToSend">The table that will be stored.</param>
    /// <returns>The key for the temporary dataset.</returns>
    protected RecId storeTmpTable(Common tblToSend)
    {
        RecId recId;

        // storing the temp table data using SRSTmpTblMarshaller
        SRSReportRunPermission permission = new SRSReportRunPermission();
        permission.assert();

        // store the session context in the temporary cache
        recId = SRSTmpTblMarshaller::sendTmpTblToDataProvider(tblToSend);

        // revert the access permissions
        CodeAccessPermission::revertAssert();

        delete_from tblToSend;

        // return the key for the temp dataset
        return recId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        SrsReportRunController controller = CustOutPaymControlController::construct(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SrsReportRunController construct(Args _args)
    {
        SrsReportRunController controller = new CustOutPaymControlController();
        controller.parmReportName(ssrsReportStr(CustOutPaymControl, Report));
        controller.parmArgs(_args);
        controller.parmShowDialog(false);

        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doNotUseWaitPattern</Name>
				<Source><![CDATA[
    public boolean doNotUseWaitPattern()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>