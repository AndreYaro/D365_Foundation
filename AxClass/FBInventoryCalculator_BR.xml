<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBInventoryCalculator_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FBInventoryCalculator_BR</c> class provides methods to calculate inventory
/// data for a specific fiscal establishment.
/// </summary>
/// <remarks>
/// Fiscal Books
/// </remarks>
public class FBInventoryCalculator_BR
{
    RefRecId    fiscalEstablishmentRecId;
    InventQty   qty;
    CostAmount  value;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculateInventoryBalancesForItem</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the inventory balance for an item on a specific date
    /// </summary>
    /// <param name="_balanceDate">
    /// date to calculate inventory balance
    /// </param>
    /// <param name="_inventTable">
    /// buffer of the <c>InventTable</c> contained item to calculate
    /// </param>
    public void calculateInventoryBalancesForItem(date _balanceDate, InventTable _inventTable)
    {
        FiscalEstablishmentInventSite_BR    fiscalEstablishmentInventSite;
        InventSum                           inventSum;
        InventDim                           inventDim;
        InventDimParm                       inventDimParm;
        InventSumFinancial                  inventSumFinancial;

        qty = 0;
        value = 0;

        while select fiscalEstablishmentInventSite
            where fiscalEstablishmentInventSite.FiscalEstablishment_BR == fiscalEstablishmentRecId
        {
            inventSum.ItemId = _inventTable.ItemId;

            inventDim.InventSiteId = fiscalEstablishmentInventSite.InventSite;

            inventDimParm.InventSiteIdFlag = NoYes::Yes;
            inventDimParm.ItemIdFlag = NoYes::Yes;

            inventSumFinancial = InventSumFinancial::newSumReport(_balanceDate, inventSum, inventDim, inventDimParm);

            qty     += inventSumFinancial.qty();
            value   += inventSumFinancial.value();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets quantity of item
    /// </summary>
    /// <returns>
    /// Return qty of item
    /// </returns>
    public InventQty getQty()
    {
        return qty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnitprice</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets Unit price of item
    /// </summary>
    /// <returns>
    /// Returns Unit price of item
    /// </returns>
    public CostAmount getUnitprice()
    {
        return qty > 0 ? value / qty : 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets Value of Item
    /// </summary>
    /// <returns>
    /// Returns value of item
    /// </returns>
    public CostAmount getValue()
    {
        return value;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of <c>FBInventoryCalculator_BR</c> class.
    /// </summary>
    /// <param name="_fiscalEstablishmentRecId">
    /// Fiscal Establishment to calculate inventory balance
    /// </param>
    public void new(RefRecId _fiscalEstablishmentRecId)
    {
        fiscalEstablishmentRecId = _fiscalEstablishmentRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>