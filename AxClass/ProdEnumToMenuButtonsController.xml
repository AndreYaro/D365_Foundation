<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdEnumToMenuButtonsController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The controller class for generating a number of menu buttons based on enum values
/// and attaching them to an existing menu button on a form.
/// </summary>
public class ProdEnumToMenuButtonsController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a number of buttons based on the provided enum
    /// and attached them to the provided menu button.
    /// </summary>
    /// <param name = "_menuButton">The menu button to attach generated buttons based on the enum.</param>
    /// <param name = "_typeId">The enum to use for generating buttons.</param>
    /// <param name = "_selectionChangedHandler">The name of a callback method to call on selection changed event.</param>
    /// <param name = "_overrideObject">The object on which the callback method exists.</param>
    public static void init(
        FormMenuButtonControl   _menuButton,
        EnumId                  _typeId,
        str                     _selectionChangedHandler,
        Object                  _overrideObject)
    {
        DictEnum dictEnum           = new DictEnum(_typeId);
        int      enumValuesCount    = dictEnum.values();
        str      enumName           = dictEnum.name();

        for (int i = 0; i < enumValuesCount; i++)
        {
            FormCommandButtonControl buttonControl = _menuButton.addControl(
                FormControlType::CommandButton,
                strfmt('%1_%2_btn', enumName, dictEnum.index2Symbol(i)));

            buttonControl.text(dictEnum.index2Label(i));
            buttonControl.parameters(int2str(dictEnum.index2Value(i)));

            buttonControl.registerOverrideMethod(
                methodstr(FormButtonControl, clicked),
                _selectionChangedHandler,
                _overrideObject);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>