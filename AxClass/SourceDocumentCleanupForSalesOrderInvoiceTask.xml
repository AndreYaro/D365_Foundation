<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SourceDocumentCleanupForSalesOrderInvoiceTask</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data maintenance task to remove unnecessary source document header and line records
/// related to sales order invoices.
/// </summary>
internal final class SourceDocumentCleanupForSalesOrderInvoiceTask implements IDataMaintenanceTask
{
    #OCCRetryCount
    private DataMaintenanceTaskResult taskResult;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public DataMaintenanceTaskResult run()
    {
        System.Exception ex;

        taskResult = DataMaintenanceTaskResult::construct();

        ttsbegin;

        try
        {
            using (SysInstrumentationActivityContext activityContext = SourceDocumentCleanupForSalesOrderTaskHelper::getActivityContext(
                classStr(SourceDocumentCleanupForSalesOrderInvoiceTask),
                SourceDocumentInstrumentationConstants::CleanupHeaderAndLineRecordsForSalesOrderInvoice))
            {
                this.deleteSourceDocumentLine(activityContext);
                this.deleteSourceDocumentHeader(activityContext);
            }
        }
        catch (Exception::TransientSqlConnectionError)
        {
            if (SysTransientSqlConnectionErrorHandler::retryTransientSqlConnectionError())
            {
                retry;
            }
            else
            {
                throw Exception::Error;
            }
        }
        catch(ex)
        {
            throw ex;
        }

        ttscommit;
       
        return taskResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteSourceDocumentHeader</Name>
				<Source><![CDATA[
    internal int deleteSourceDocumentHeader(SysInstrumentationActivityContext _actvityContext = null)
    {
        int impactedRows = 0;

        str sqlStatement = strfmt(@"
            DELETE FROM SOURCEDOCUMENTHEADER
            WHERE RECID IN
            (
            SELECT TOP %1 SDH.RECID
            FROM SOURCEDOCUMENTHEADER SDH
                JOIN CUSTINVOICEJOUR CIJ ON CIJ.PARTITION = SDH.PARTITION and CIJ.SOURCEDOCUMENTHEADER = SDH.RECID
            WHERE
                SDH.PARTITION = %2
            AND SDH.SOURCERELATIONTYPE = %3
            AND CIJ.CREATEDDATETIME <= DATEADD(MONTH, -%4, GETUTCDATE())
            AND CIJ.SALESID <> ''
            AND CIJ.SALESTYPE = %5
            AND NOT EXISTS 
                (SELECT 1 FROM SOURCEDOCUMENTLINE SDL WHERE SDL.PARTITION = SDH.PARTITION AND SDL.SOURCEDOCUMENTHEADER = SDH.RECID)
             )",
            SourceDocumentCleanupForSalesOrderTaskHelper::MaxRecordsToFix,
            getCurrentPartitionRecId(),
            tableNum(CustInvoiceJour),
            SourceDocumentCleanupForSalesOrderTaskHelper::FixWindowMonths,
            enum2int(SalesType::Sales));

        impactedRows = SourceDocumentCleanupForSalesOrderTaskHelper::executeSQL(sqlStatement);

        if (_actvityContext)
        {
            _actvityContext.addCustomProperty(
                SourceDocumentInstrumentationConstants::NumberOfSourceDocumentHeadersDeletedForSalesOrderInvoice,
                int2Str(impactedRows));
        }

        if (taskResult)
        {
            taskResult.addMessage(DataMaintenanceMessageContract::constructForLabel(Exception::Info,
            literalStr("@SourceDocumentAccountingFramework:SourceDocumentCleanupSourceDocumentHeaderDeletionInfoMessage"),
            [int2Str(impactedRows)]));
        }

        return impactedRows;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteSourceDocumentLine</Name>
				<Source><![CDATA[
    internal int deleteSourceDocumentLine(SysInstrumentationActivityContext _actvityContext = null)
    {
        int impactedRows = 0;

        str sqlStatement = strfmt(@"
            DELETE FROM SOURCEDOCUMENTLINE
            WHERE RECID IN
            (
            SELECT TOP %1 SDL.RECID
            FROM SOURCEDOCUMENTHEADER SDH
                JOIN CUSTINVOICEJOUR CIJ ON CIJ.PARTITION = SDH.PARTITION and CIJ.SOURCEDOCUMENTHEADER = SDH.RECID
                JOIN SOURCEDOCUMENTLINE SDL ON SDL.PARTITION = SDH.PARTITION AND SDL.SOURCEDOCUMENTHEADER = SDH.RECID
            WHERE
                SDH.PARTITION = %2
            AND SDH.SOURCERELATIONTYPE = %3
            AND CIJ.CREATEDDATETIME <= DATEADD(MONTH, -%4, GETUTCDATE())
            AND CIJ.SALESID <> ''
            AND CIJ.SALESTYPE = %5
            )",
            SourceDocumentCleanupForSalesOrderTaskHelper::MaxRecordsToFix,
            getCurrentPartitionRecId(),
            tableNum(CustInvoiceJour),
            SourceDocumentCleanupForSalesOrderTaskHelper::FixWindowMonths,
            enum2int(SalesType::Sales));

        impactedRows = SourceDocumentCleanupForSalesOrderTaskHelper::executeSQL(sqlStatement);

        if (_actvityContext)
        {
            _actvityContext.addCustomProperty(
                SourceDocumentInstrumentationConstants::NumberOfSourceDocumentLinesDeletedForSalesOrderInvoice,
                int2Str(impactedRows));
        }

        if (taskResult)
        {
            taskResult.addMessage(DataMaintenanceMessageContract::constructForLabel(Exception::Info,
            literalStr("@SourceDocumentAccountingFramework:SourceDocumentCleanupSourceDocumentLineDeletionInfoMessage"),
            [int2Str(impactedRows)]));
        }
        
        return impactedRows;
    }

]]></Source>
			</Method>
			<Method>
				<Name>caption</Name>
				<Source><![CDATA[
    public DataMaintenanceCaption caption()
    {
        return literalStr("@SourceDocumentAccountingFramework:SourceDocumentCleanupForSalesOrderInvoiceTaskCaption");
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>