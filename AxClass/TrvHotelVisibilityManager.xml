<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvHotelVisibilityManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class controls visibility for hotel expense type.
/// </summary>
class TrvHotelVisibilityManager extends TrvCommonExpenseTypeVisibilityManager
{
    Map                     transactionHotel;
    const str               transactionHotelGroupName       = "HotelData";
    str                     dataSourceNameHotel;
    TrvEnhancedHotelData    trvEnhancedHotelData;

    Map                     hotelTransData;
    const str               hotelTransDataGroupName         = "HotelTransData";

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmdataSourceNameHotel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets and sets the name of the datasource for hotel enhanced data to set field vibilities on for the class.
    /// </summary>
    /// <param name = "_dataSourceNameHotel">
    /// The name of the hotel enhanced fields dataSource to set field visibilities on the form.
    /// </param>
    /// <returns>
    /// The name of the hotel enhanced fields dataSource to set field visibilities on the form.
    /// </returns>
    public str parmdataSourceNameHotel(str _dataSourceNameHotel = dataSourceNameHotel)
    {
        dataSourceNameHotel = _dataSourceNameHotel;
        return dataSourceNameHotel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionHotel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for hotel field group.
    /// </summary>
    /// <returns>
    /// A map of hotel fields.
    /// </returns>
    Map getTransactionHotel()
    {
        if (!transactionHotel)
        {
            transactionHotel = this.getFieldGroup(trvEnhancedHotelData, transactionHotelGroupName);
        }

        return transactionHotel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getHotelTransData</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for the HotelTransData field group.
    /// </summary>
    /// <returns>
    /// A map of hotel fields from TrvExpTrans to set visibility.
    /// </returns>
    Map getHotelTransData()
    {
        if (!hotelTransData)
        {
            hotelTransData = this.getFieldGroup(trvExpTrans, hotelTransDataGroupName);
        }

        return hotelTransData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructor for an new instance of TrvHotelVisibilityManager.
    /// </summary>
    /// <param name = "_trvExpTrans">
    /// The current record instance of the <c>TrvExpensesEntity</c>.
    /// </param>
    /// <param name = "_trvExpensesForm">
    /// An instance of the calling form object.
    /// </param>
    /// <param name = "_dataSourceName">
    /// Name of the datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameHotel">
    /// Name of the hotel enhanced datasource on the form for which we set the visibility.
    /// </param>
    void new(Common _trvExpTrans, FormRun _trvExpensesForm, str _dataSourceName, str _dataSourceNameHotel, str _dataSourceNameHeader = "")
    {
        super(_trvExpTrans, _trvExpensesForm, _dataSourceName, _dataSourceNameHeader);
        this.parmdataSourceNameHotel(_dataSourceNameHotel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldVisibilities</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets visibility for hotel fields.
    /// </summary>
    /// <param name = "_isVisible">
    /// Determines if we show or hide hotel fields.
    /// </param>
    void setFieldVisibilities(boolean _isVisible = false)
    {
        FormDataSource formDataSource = trvExpenseForm.dataSource(dataSourceName);
        FormDataSource formDataSourceHotel = trvExpenseForm.dataSource(dataSourceNameHotel);

        if (!this.parmExpenseVisibilityManager())
        {
            this.setVisibilityForFieldGroup(this.getTransactionHotel(), dataSourceNameHotel, _isVisible);
            this.setVisibilityForFieldGroup(this.getHotelTransData(),   dataSourceName,      _isVisible);

            if (_isVisible)
            {
                this.setVisibilityForAdminCustomFields(trvAdminCustomFieldsContainer, formDataSourceHotel, trvExpTrans.isEditable());
            }
        }
		else
        {
            this.parmExpenseVisibilityManager().setVisibilityForHotelRelatedFields(formDataSource.table(), formDataSource, _isVisible);
            this.parmExpenseVisibilityManager().setVisibilityForHotelRelatedFields(formDataSourceHotel.table(), formDataSourceHotel, _isVisible);
        }

        super(_isVisible);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>