<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransferTable_IN_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
#ISOCountryRegionCodes
/// <summary>
/// The <c>InventTransferTable_IN_Extension</c> class is an extension class for the InventTransferTable table.
/// </summary>
[ExtensionOf(tableStr(InventTransferTable))]
internal final class InventTransferTable_IN_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initValue</Name>
				<Source><![CDATA[
    public void initValue()
    {
        next initValue();

        if (this.isStockTransferImprovementOn_IN())
        {
            InventParameters inventParameters = InventParameters::find();

            this.TransferType_IN = inventParameters.TransferType_IN;
            if (this.TransferType_IN == TransferType_IN::StockTransfer)
            {
                this.StockTransferCostPriceHandlingImprovement_IN = NoYes::Yes;
                this.PriceType_IN = inventParameters.PriceType_IN;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedField</Name>
				<Source><![CDATA[
    public void modifiedField(FieldId _fieldId)
    {
        next modifiedField(_fieldId);

        switch (_fieldId)
        {
            case fieldNum(InventTransferTable,  TransferType_IN):
                switch (this.TransferType_IN) 
                {
                    case TransferType_IN::StockTransfer:
                        if (StockTransferCostPriceHandlingImprovementFeatureExposure_IN::isEnabled())
                        {
                            InventParameters inventParameters = InventParameters::find();

                            this.StockTransferCostPriceHandlingImprovement_IN = NoYes::Yes;
                            this.PriceType_IN = inventParameters.PriceType_IN;
                        }
                        else
                        {
                            this.StockTransferCostPriceHandlingImprovement_IN = NoYes::No;
                        }
                        break;

                    default:
                        this.StockTransferCostPriceHandlingImprovement_IN = NoYes::No;
                }
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    public void insert()
    {
        if (this.isStockTransferImprovementOn_IN())
        {
            if (this.TransferType_IN == TransferType_IN::StockTransfer)
            {
                this.StockTransferCostPriceHandlingImprovement_IN = NoYes::Yes;
            }
            else
            {
                this.StockTransferCostPriceHandlingImprovement_IN = NoYes::No;
            }
        }

        next insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isStockTransferImprovementOn_IN</Name>
				<Source><![CDATA[
    private boolean isStockTransferImprovementOn_IN()
    {
        return SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN])  &&
               StockTransferCostPriceHandlingImprovementFeatureExposure_IN::isEnabled();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>