<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PriceComponentCacheFactory</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class acts as a factory to initialize instances of classes used for price component caching.
/// </summary>
public final class PriceComponentCacheFactory
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getPriceComponentCacheProvider</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a concrete subclass of <c>PriceComponentCacheProvider</c> based on the class name of the criteria supplied in the parameter.
    /// </summary>
    /// <param name = "_priceComponentCriteriaClassName">A class name of a subclass of the <c>PriceComponentCriteria</c> class.</param>
    /// <returns></returns>
    public PriceComponentCacheProvider getPriceComponentCacheProvider(ClassName _priceComponentCriteriaClassName)
    {
        if ((_priceComponentCriteriaClassName == null) || (_priceComponentCriteriaClassName == ''))
        {
            throw error("@SCM:InvalidPriceComponentCriteriaName");
        }

        SysPluginMetadataCollection metadataCollection = new SysPluginMetadataCollection();
        metadataCollection.SetManagedValue(classStr(PriceComponentCriteria), _priceComponentCriteriaClassName);

        PriceComponentCacheProvider cacheProvider =
            SysPluginFactory::Instance(identifierStr(Dynamics.AX.Application), classStr(PriceComponentCacheProvider), metadataCollection);

        if (cacheProvider == null)
        {
            throw error("@SCM:InvalidPriceComponentCriteriaName");
        }

        return cacheProvider;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPriceComponentResultInstance</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a concrete subclass of <c>PriceComponentResult</c> based on the class name of the cache provider supplied in the parameter.
    /// </summary>
    /// <param name = "_priceComponentCacheProviderClassName">A class name of a subclass of the <c>PriceComponentCacheProvider</c> class.</param>
    /// <returns></returns>
    public PriceComponentResult getPriceComponentResultInstance(ClassName _priceComponentCacheProviderClassName)
    {
        if ((_priceComponentCacheProviderClassName == null) || (_priceComponentCacheProviderClassName == ''))
        {
            throw error("@SCM:InvalidPriceComponentCacheProviderName");
        }

        SysPluginMetadataCollection metadataCollection = new SysPluginMetadataCollection();
        metadataCollection.SetManagedValue(classStr(PriceComponentCacheProvider), _priceComponentCacheProviderClassName);

        PriceComponentResult priceComponentResult =
            SysPluginFactory::Instance(identifierStr(Dynamics.AX.Application), classStr(PriceComponentResult), metadataCollection);

        if (priceComponentResult == null)
        {
            throw error("@SCM:InvalidPriceComponentCacheProviderName");
        }

        return priceComponentResult;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>