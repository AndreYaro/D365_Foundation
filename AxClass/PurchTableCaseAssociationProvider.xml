<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'PurchTable'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PurchTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        PurchTable purchTable;
        select crosscompany OrderAccount from purchTable where purchTable.RecId == _caseAssociation.RefRecId;

        VendTable vendTable = VendTable::find(purchTable.OrderAccount);

        if (vendTable.RecId)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Vendor, vendTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        PurchTable purchTable;
        select crosscompany DataAreaId from purchTable where purchTable.RecId == _parentRecId;

        return purchTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        PurchTable purchTable;
        select crosscompany PurchId from purchTable where purchTable.RecId == _parentRecId;

        return purchTable.PurchId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        PurchTable purchTable;
        select RecId from purchTable where purchTable.PurchId == _naturalKey;

        return purchTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        PurchTable purchTable;
        select crosscompany PurchName from purchTable where purchTable.RecId == _parentRecId;

        return purchTable.PurchName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        PurchTable purchTable;

        //We need this code in order not to lose the localization data in the opened form.
        PurchTable purchTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from purchTableCrossCompany
            where purchTableCrossCompany.RecId == _association.RefRecId;

        if (purchTableCrossCompany.Recid)
        {
            changecompany (purchTableCrossCompany.DataAreaId)
            {
                select firstonly purchTable where purchTable.RecId == _association.RefRecId;
            }
        }

        _args.record(purchTable);
        return menuitemdisplaystr(PurchTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(PurchTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(PurchTable));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, PurchId));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, PurchName));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, InvoiceAccount));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, PurchStatus));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, ProjId));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, BankDocumentType));
        oSysTableLookup.addLookupfield(fieldnum(PurchTable, AddressRefRecId));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation</c> get case entity type event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(RefTableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tableNum(PurchTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::PurchTable);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>