<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PayrollPositionActionDetails_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PayrollPositionActionDetails_ExtensionPayroll</c> is an extension of the <c>PayrollPositionActionDetails</c> table and contains
/// table methods and event subscriber methods.
/// </summary>
[ExtensionOf(tableStr(PayrollPositionActionDetails))]
public final class PayrollPositionActionDetails_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isRecordModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether the selected <c>PayrollPositionActionDetails</c> record is modified or not after staging from <c>HcmPosition</c>.
    /// </summary>
    /// <param name = "_payrollPositionDetails">
    /// Record Id of <c>PayrollPositionDetails</c> record.
    /// </param>
    /// <returns>
    /// true if the specified record is modified; otherwise, false.
    /// </returns>
    public boolean isRecordModified(PayrollPositionDetails _payrollPositionDetails)
    {
        boolean isRecordModified = next isRecordModified(_payrollPositionDetails);

        return isRecordModified
            || this.isExtensionFieldsModified(_payrollPositionDetails);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isExtensionFieldsModified</Name>
				<Source><![CDATA[
    private boolean isExtensionFieldsModified(PayrollPositionDetails _payrollPositionDetails)
    {
        return this.AutoGenerateSalary != _payrollPositionDetails.AutoGenerateSalary
            || this.DefaultEarningCode != _payrollPositionDetails.DefaultEarningCode
            || this.DefaultGLIBenefit != _payrollPositionDetails.DefaultGLIBenefit
            || this.GenerateEarningsFromSchedule != _payrollPositionDetails.GenerateEarningsFromSchedule
            || this.IsCompanyOfficer != _payrollPositionDetails.IsCompanyOfficer
            || this.PayrollOvertimeHours != _payrollPositionDetails.PayrollOvertimeHours
            || this.Schedule != _payrollPositionDetails.Schedule
            || this.ScheduleDataAreaId != _payrollPositionDetails.ScheduleDataAreaId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferActionValuesToPayrollPositionDetail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfers the values from the <c>PayrollPositionActionDetails</c> record to the given <c>HcmPositionDetail</c> record.
    /// </summary>
    /// <param name = "_payrollPositionDetails">The <c>PayrollPositionDetails</c> record.</param>
    public void transferActionValuesToPayrollPositionDetail(PayrollPositionDetails _payrollPositionDetails)
    {
        next transferActionValuesToPayrollPositionDetail(_payrollPositionDetails);

        this.transferExtentionActionValuesToPayrollPositionDetail(_payrollPositionDetails);
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferExtentionActionValuesToPayrollPositionDetail</Name>
				<Source><![CDATA[
    private void transferExtentionActionValuesToPayrollPositionDetail(PayrollPositionDetails _payrollPositionDetails)
    {
        _payrollPositionDetails.AutoGenerateSalary = this.AutoGenerateSalary;
        _payrollPositionDetails.DefaultEarningCode = this.DefaultEarningCode;
        _payrollPositionDetails.DefaultGLIBenefit = this.DefaultGLIBenefit;
        _payrollPositionDetails.GenerateEarningsFromSchedule = this.GenerateEarningsFromSchedule;
        _payrollPositionDetails.IsCompanyOfficer = this.IsCompanyOfficer;
        _payrollPositionDetails.PayrollOvertimeHours = this.PayrollOvertimeHours;
        _payrollPositionDetails.Schedule = this.Schedule;
        _payrollPositionDetails.ScheduleDataAreaId = this.ScheduleDataAreaId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>