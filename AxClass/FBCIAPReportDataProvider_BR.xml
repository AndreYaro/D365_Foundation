<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBCIAPReportDataProvider_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FBCIAPReportDataProvider_BR</c> class is the Report Data Provider class for the <c>FBCIAPReport_BR</c> report.
/// </summary>
[SRSReportParameterAttribute(classStr(FBCIAPReportDataContract_BR))]
class FBCIAPReportDataProvider_BR extends SRSReportDataProviderBase
{
    FBTmpCIAPAReport_BR   tmpCIAPAReport;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to retrieve data filtered by contract parameters.
    /// </summary>
    /// <returns>
    /// The query return to retrieve report data.
    /// </returns>
    protected Query buildQuery()
    {
        Query                               query;
        QueryBuildDataSource                qbds;

        FBCIAPReportDataContract_BR         contract = this.parmDataContract() as FBCIAPReportDataContract_BR;

        query = new Query();

        qbds = query.addDataSource(tableNum(FBTaxAssessment_BR));
        qbds.addRange(fieldNum(FBTaxAssessment_BR, RecId)).value(SysQuery::value(contract.parmFBTaxAssessmentRecId()));

        qbds = qbds.addDataSource(tableNum(FBCIAPAssessment_BR));
        qbds.relations(true);

        qbds = qbds.addDataSource(tableNum(FBCIAPAssetTrans_BR));
        qbds.relations(true);

        qbds = qbds.addDataSource(tableNum(FBCIAPAssetTable_BR));
        qbds.relations(true);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTmpCIAPAReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>TmpCIAPAReport_BR</c> table.
    /// </summary>
    /// <returns>
    /// The <c>TmpCIAPAReport_BR</c>
    /// </returns>
    [SRSReportDataSetAttribute('TmpCIAPAReport_BR')]
    public FBTmpCIAPAReport_BR getTmpCIAPAReport()
    {
        select tmpCIAPAReport;
        return tmpCIAPAReport;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    public void processReport()
    {
        Query               query;
        QueryRun            queryRun;
        FBCIAPAssessment_BR fbCIAPAssessment;
        FBCIAPAssetTrans_BR fbCIAPAssetTrans;
        FBCIAPAssetTable_BR fbCIAPAssetTable;

        query = this.buildQuery();
        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            fbCIAPAssessment = queryRun.get(tableNum(FBCIAPAssessment_BR));
            fbCIAPAssetTrans = queryRun.get(tableNum(FBCIAPAssetTrans_BR));
            fbCIAPAssetTable = queryRun.get(tableNum(FBCIAPAssetTable_BR));

            tmpCIAPAReport.TotalTaxableOutgoingAmount = fbCIAPAssessment.TotalTaxableOutgoingAmount;
            tmpCIAPAReport.TotalOutgoingOperationsAmount = fbCIAPAssessment.TotalOutgoingOperationsAmount;
            tmpCIAPAReport.FBCIAPFactor = fbCIAPAssessment.factor();
            tmpCIAPAReport.ICMSInitialBalance = fbCIAPAssessment.ICMSInitialBalance;
            tmpCIAPAReport.InstallmentSum = fbCIAPAssessment.InstallmentSum;
            tmpCIAPAReport.ICMSAmountRecoverable = fbCIAPAssessment.icmsAmountRecoverable();
            tmpCIAPAReport.FromDate = fbCIAPAssessment.FromDate;
            tmpCIAPAReport.ToDate = fbCIAPAssessment.ToDate;

            tmpCIAPAReport.AssetId = fbCIAPAssetTable.AssetTable;
            tmpCIAPAReport.AssetName = fbCIAPAssetTable.AssetName;
            tmpCIAPAReport.CIAPAssetID_BR = fbCIAPAssetTable.CIAPAssetID;

            tmpCIAPAReport.ICMSAmount = fbCIAPAssetTrans.ICMSAmount;
            tmpCIAPAReport.TransTypeName = enum2str(fbCIAPAssetTrans.TransType);
            tmpCIAPAReport.TransDate = fbCIAPAssetTrans.TransDate;
            tmpCIAPAReport.InstallmentNum = fbCIAPAssetTrans.InstallmentNum;
            tmpCIAPAReport.InstallmentAmount = fbCIAPAssetTrans.InstallmentAmount;
            tmpCIAPAReport.OtherCreditsAmount = fbCIAPAssetTrans.otherCreditsAmount();

            tmpCIAPAReport.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>