<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendPromissoryNoteMarkSentDate</Name>
	<SourceCode>
		<Declaration><![CDATA[
class VendPromissoryNoteMarkSentDate extends RunBase
{
    VendPromissoryNoteID    vendPromissoryNoteID;
    VendPromissoryNoteJour  vendPromissoryNoteJour;
    container               conVendPromissoryNoteID;

    date                    dateSent;

    DialogField             dialogDateSent;

    #define.CurrentVersion(2)
    #localmacro.CurrentList
        dateSent,
        conVendPromissoryNoteID
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    /// interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    /// A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    /// in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        DialogRunbase   dialog = super();

        dialogDateSent = dialog.addField(extendedTypeStr(TransDate),"@SYS10640","@SYS71570");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret;

        dateSent = dialogDateSent.value();

        ret = super();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVendPromissoryNoteId</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void parmVendPromissoryNoteId(VendPromissoryNoteID _vendPromissoryNoteID)
    {
        conVendPromissoryNoteId += _vendPromissoryNoteID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount

        if (! this.validate())
        {
            throw error("@SYS25904");
        }

        try
        {
            ttsbegin;

            this.update();

            ttscommit;
        }

        catch (Exception::Deadlock)
        {
            retry;
        }

        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version     version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedClass;

                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void update()
    {
        Counter    counter;

        for (counter = conlen(conVendPromissoryNoteID);
             counter;
             counter = counter - 1)
        {
            vendPromissoryNoteJour = VendPromissoryNoteJour::find(conpeek(conVendPromissoryNoteID, counter), true);
            if (vendPromissoryNoteJour.DocumentDate != dateSent)
            {
                vendPromissoryNoteJour.DocumentDate = dateSent;
                vendPromissoryNoteJour.update();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ok = true;

        if (!dateSent)
        {
            ok = checkFailed("@SYS2476");
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ClassDescription description()
    {
        return "@SYS10640";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        VendPromissoryNoteMarkSentDate  vendPromissoryNoteMarkSentDate;
        VendPromissoryNoteJour          vendPromissoryNoteJour;
        FormDataSource                  formDS = FormDataUtil::getFormDataSource(args.record());

        vendPromissoryNoteMarkSentDate = new VendPromissoryNoteMarkSentDate();

        if (vendPromissoryNoteMarkSentDate.prompt())
        {
            for (vendPromissoryNoteJour = formDS.getFirst(true) ? formDS.getFirst(true) : args.record();
                 vendPromissoryNoteJour;
                 vendPromissoryNoteJour = formDS.getNext())
            {
                vendPromissoryNoteMarkSentDate.parmVendPromissoryNoteId(vendPromissoryNoteJour.PromissoryNoteId);
            }

            VendPromissoryNoteMarkSentDate::runServer(vendPromissoryNoteMarkSentDate.pack());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runServer</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected static void runServer(container _packedData)
    {
        VendPromissoryNoteMarkSentDate setSentDate;

        setSentDate = new VendPromissoryNoteMarkSentDate();
        setSentDate.unpack(_packedData);
        setSentDate.runOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>