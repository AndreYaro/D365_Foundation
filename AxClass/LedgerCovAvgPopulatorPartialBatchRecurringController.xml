<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerCovAvgPopulatorPartialBatchRecurringController</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>LedgerCovAvgPopulatorPartialBatchRecurringController</c> class schedules and runs the recurring batch job responsible for
/// populating days per month tables for the past few months.
/// </summary>
[Export(identifierStr(Dynamics.AX.Application.LedgerCovAvgBatchController))]
public final class LedgerCovAvgPopulatorPartialBatchRecurringController extends LedgerCovAvgBatchController implements BatchRetryable
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(IdentifierName _className = classStr(LedgerCovAvgPopulatorBatch),
        IdentifierName _methodName = staticMethodStr(LedgerCovAvgPopulatorBatch, main),
        SysOperationExecutionMode _executionMode = SysOperationExecutionMode::ScheduledBatch)
    {
        super(_className, _methodName, _executionMode);
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultCaption</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected ClassDescription defaultCaption()
    {
        return "@CashManagement:LedgerCovAvgPartialBatchCaption";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClassNumber</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal ClassId getClassNumber()
    {
        return classNum(LedgerCovAvgPopulatorPartialBatchRecurringController);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecurrenceData</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal SysRecurrenceData getRecurrenceData()
    {
        // Schedule the recurrence to execute daily.
        TransDate startDate = DateTimeUtil::getToday(DateTimeUtil::getCompanyTimeZone()) + 1;

        SysRecurrenceData recurrenceData = SysRecurrence::defaultRecurrence();
        recurrenceData = this.setRecurranceTimeZone(recurrenceData, DateTimeUtil::getCompanyTimeZone());
        recurrenceData = SysRecurrence::setRecurrenceStartDate(recurrenceData, startDate);
        recurrenceData = SysRecurrence::setRecurrenceTime(recurrenceData, str2Time('02:00:00'));
        recurrenceData = SysRecurrence::setRecurrenceNoEnd(recurrenceData);
        recurrenceData = SysRecurrence::setRecurrenceUnit(recurrenceData, SysRecurrenceUnit::Day);

        return recurrenceData;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getChildRunTimeTasks</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal List getChildRunTimeTasks()
    {
        List tasks = new List(Types::Class);
        ListEnumerator runDates = this.createDateRunList(LedgerCovAvgConstants::NumberOfMonthsForPartialPopulation).getEnumerator();

        while (runDates.moveNext())
        {
            LedgerCovAvgPopulatorPartialBatchRecurringController populatorBatch = this.constructTask();
            populatorBatch.parmStartOfMonth(runDates.current());

            tasks.addEnd(populatorBatch);
        }

        return tasks;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructTask</Name>
				<Source><![CDATA[
    private LedgerCovAvgPopulatorPartialBatchRecurringController constructTask()
    {
        return new LedgerCovAvgPopulatorPartialBatchRecurringController(this.parmClassName(), this.parmMethodName(), this.parmExecutionMode());
    }

]]></Source>
			</Method>
			<Method>
				<Name>callStaticMainMethod</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal void callStaticMainMethod(Args _args)
    {
        LedgerCovAvgPopulatorBatch::main(_args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>