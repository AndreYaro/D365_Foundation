<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailPricingAttributeValueHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// A helper class lookup EcoResEnumerationAttributeTypeValue.
/// When registerOverrideMethod - lookup for a control, there's no way to pass extra parameters, so we have to put a callee of lookup with attribute record id aware.
/// </summary>
internal final class RetailPricingAttributeValueHelper
{
    private RefRecId attributeRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal void new(RefRecId _attributeRecId)
    {
        this.attributeRecId = _attributeRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookup</Name>
				<Source><![CDATA[
    internal void lookup(FormStringControl _control)
    {
        Query query = new Query();
        QueryBuildDataSource qbds;

        EcoResAttribute attribute = EcoResAttribute::find(this.attributeRecId);

        qbds = query.addDataSource(tableNum(EcoResTextValue));
        qbds = qbds.addDataSource(tableNum(EcoResEnumerationAttributeTypeValue));
        qbds.joinMode(JoinMode::InnerJoin);
        qbds.addLink(fieldNum(EcoResTextValue, RecId), fieldNum(EcoResEnumerationAttributeTypeValue, Value));
        qbds.addRange(fieldNum(EcoResEnumerationAttributeTypeValue, AttributeType)).value(queryValue(attribute.AttributeType));
        
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(EcoResTextValue), _control);
        sysTableLookup.addLookupfield(fieldNum(EcoResTextValue, TextValue));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupBoolean</Name>
				<Source><![CDATA[
    internal void lookupBoolean(FormStringControl _control)
    {
        Query query = new Query();

        RetailPricingBooleanAttributePickList tmpPicklist;
        QueryBuildDataSource qbds = query.addDataSource(tableNum(RetailPricingBooleanAttributePickList));

        DictEnum dictEnum = new DictEnum(enumNum(FalseTrue));
        for (int idx = 0; idx < dictEnum.values(); idx++)
        {
            tmpPicklist.clear();
            tmpPicklist.Value = dictEnum.index2Value(idx);
            tmpPicklist.insert();
        }
        
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(RetailPricingBooleanAttributePickList), _control);
        sysTableLookup.addLookupfield(fieldNum(RetailPricingBooleanAttributePickList, Value));
        sysTableLookup.parmQuery(query);
        sysTableLookup.parmTmpBuffer(tmpPicklist);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateInt</Name>
				<Source><![CDATA[
    internal boolean validateInt(FormStringControl _control)
    {
        boolean ret = true;
        str value = _control.text();

        System.Int32 result;
        if (!System.Int32::TryParse(value, byref result))
        {
            ret = checkFailed(strFmt('@RetailPricing:PricingValueOutOfRange', intMin(), intMax()));
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>