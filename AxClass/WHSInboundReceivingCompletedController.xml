<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSInboundReceivingCompletedController</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSInboundReceivingCompletedController extends SysOperationServiceController implements BatchRetryable
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromExecutionMode</Name>
				<Source><![CDATA[
    public static WHSInboundReceivingCompletedController newFromExecutionMode(SysOperationExecutionMode _executionMode)
    {
        WHSInboundReceivingCompletedController controller = new WHSInboundReceivingCompletedController(
                classStr(WHSInboundReceivingCompletedService),
                methodStr(WHSInboundReceivingCompletedService, receiptConfirm),
                _executionMode);

        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    internal static void main(Args _args)
    {
        NoYes openDialog = _args.parmEnum();

        if (openDialog)
        {
            WHSInboundReceivingCompletedController controller = WHSInboundReceivingCompletedController::newFromExecutionMode(SysOperationExecutionMode::Synchronous);

            controller.parmDialogCaption("@WAX:WHSInboundReceivingCompletedController");

            controller.parmArgs(_args);

            if (_args.caller() is WHSILoadIdRangeProvider)
            {
                WHSILoadIdRangeProvider rangeProvider = _args.caller() as WHSILoadIdRangeProvider;
                controller.getDataContractObject().setLoadIdRange(rangeProvider.loadIdRange());
            }

            controller.startOperation();

            WHSInboundReceivingCompletedController::researchCallerDataSource(_args);
        }
        else
        {
            FormDataSource whsLoadTableDS = FormDataUtil::getFormDataSource(_args.record());
        
            ListEnumerator loadIds = WhsLoadPlanningListPageForm::getLoadIdsFromDataSource(whsLoadTableDS).getEnumerator();
            WHSInboundReceivingCompletedService::receiptConfirmLoads(loadIds, true);

            whsLoadTableDS.research(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    [Hookable(false)]
    final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>researchCallerDataSource</Name>
				<Source><![CDATA[
    private static void researchCallerDataSource(Args _args)
    {
        FormDataSource formDataSource = FormDataUtil::getFormDataSource(_args.record());
        if (formDataSource && formDataSource.table() == tableNum(WHSLoadTable))
        {
            formDataSource.research(true);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>