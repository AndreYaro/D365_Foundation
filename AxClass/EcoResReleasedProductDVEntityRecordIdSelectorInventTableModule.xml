<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResReleasedProductDVEntityRecordIdSelectorInventTableModule</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

[ExportMetadata(identifierStr(TableName), tableStr(InventTableModule))]
[ExportMetadata(identifierStr(EntityName), dataentityviewstr(EcoResReleasedProductDVEntity))]
[Export(identifierstr(Dynamics.AX.Application.IBusinessEventsTableToEntityRecordIdSelector))]
internal final class EcoResReleasedProductDVEntityRecordIdSelectorInventTableModule implements IBusinessEventsTableToEntityRecordIdSelector
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEntityRecordIdsImpactedByTableChange</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public List getEntityRecordIdsImpactedByTableChange(BusinessEventsDefinition _eventDefinition, BusinessEventsActions _changeType, RecId _changedRecordId)
    {
        if (!EcoResReleasedProductDVEntitiesUseRecordIdSelectorsFlight::instance().isEnabled())
        {
            return null;
        }

        List impactedRecIds = new List(Types::Int64);

        ModuleInventPurchSales moduleType;

        if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(EcoResReleasedProductDVEntity, InventTableModuleSales))
        {
            moduleType = ModuleInventPurchSales::Sales;
        }
        else if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(EcoResReleasedProductDVEntity, InventTableModuleInvent))
        {
            moduleType = ModuleInventPurchSales::Invent;
        }
        else if (_eventDefinition.DataSourceName == dataEntityDataSourceStr(EcoResReleasedProductDVEntity, InventTableModulePurch))
        {
            moduleType = ModuleInventPurchSales::Purch;
        }
        else
        {
            return null;
        }

        InventTable inventTable;
        InventTableModule inventTableModule;
        
        while select RecId from inventTable
            exists join inventTableModule
            where inventTable.ItemId == inventTableModule.ItemId
                && inventTableModule.RecId == _changedRecordId
                && inventTableModule.ModuleType == moduleType
        {
            impactedRecIds.addEnd(inventTable.RecId);
        }

        return impactedRecIds;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>