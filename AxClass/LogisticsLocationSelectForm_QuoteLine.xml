<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_QuoteLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_QuoteLine extends LogisticsLocationSelectForm
{
    SalesQuotationLine salesQuotationLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates the entities from which addresses can be selected.
    /// </summary>
    /// <returns>
    ///    A set of <c>LogisticsLocationSelectSourceTypes</c> enumeration values from which addresses can be
    ///    selected.
    /// </returns>
    public Set allowedSources()
    {
        Set ret;

        ret = super();

        ret.add(LogisticsLocationSelectSourceType::BusinessRelation);
        ret.add(LogisticsLocationSelectSourceType::SalesQuotation);
        ret.add(LogisticsLocationSelectSourceType::SalesQuotationLine);

        return ret;
    }
]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling sales quote line.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        LogisticsPostalAddress address = LogisticsPostalAddress::findRecId(_addressRecId);

        salesQuotationLine.DeliveryPostalAddress = _addressRecId;
        salesQuotationLine.setDeliveryName();

        salesQuotationLine.AddressRefRecId   = _transferFromTable.RecId;
        salesQuotationLine.AddressRefTableId = _transferFromTable.TableId;

        salesQuotationLine.TaxGroup = TaxSales::custTaxGroup(salesQuotationLine.DlvTerm, salesQuotationLine.TaxGroup, salesQuotationLine.deliveryLocation());
        // <GBR>
        if (BrazilParameters::isEnabled())
        {
            salesQuotationLine.cfopTable_BR = this.getDefaultCFOP_BR(salesQuotationLine);
            salesQuotationLine.setTaxGroupAndTaxItemGroupFields_BR();
        }
        // </GBR>
        salesQuotationLine.setListCode();
    }
]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the initial source that is selected for the addresses on the form.
    /// </summary>
    /// <returns>
    /// The <c>LogisticslocationSelectSourceType</c> enumeration value that corresponds to the address
    /// source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;
        SalesQuotationTable                 salesQuotationTable;

        if (salesQuotationLine.CustAccount)
        {
            sourceType = LogisticsLocationSelectSourceType::Customer;
        }
        else
        {
            salesQuotationTable = SalesQuotationTable::find(salesQuotationLine.QuotationId);
            if (salesQuotationTable.BusRelAccount)
            {
                sourceType = LogisticsLocationSelectSourceType::BusinessRelation;
            }
        }

        addressOwner = LogisticsLocationSelectForm::findByRecId(
            salesQuotationLine.AddressRefTableId,
            salesQuotationLine.AddressRefRecId
        );

        if (addressOwner)
        {
            sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                salesQuotationLine.AddressRefTableId,
                sourceType
            );
        }

        return sourceType;
    }
]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record ID of the <c>LogisticsPostalAddress</c> record that is currently selected.
    /// </summary>
    /// <returns>
    /// The record ID of the <c>LogisticsPostalAddress</c> record.
    /// </returns>
    public RecId getLogisticsPostalAddressRecId()
    {
        return salesQuotationLine.DeliveryPostalAddress;
    }
]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the record that the target form grid selects upon opening.
    /// </summary>
    /// <returns>
    /// The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common              addressOwner;
        SalesQuotationTable salesQuotationTable;

        if (salesQuotationLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                salesQuotationLine.AddressRefTableId,
                salesQuotationLine.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            if (salesQuotationLine.CustAccount)
            {
                addressOwner = CustTable::find(salesQuotationLine.CustAccount);
            }
            else
            {
                salesQuotationTable = SalesQuotationTable::find(salesQuotationLine.QuotationId);
                if (salesQuotationTable.BusRelAccount)
                {
                    addressOwner = smmBusRelTable::find(salesQuotationTable.BusRelAccount);
                }
            }
        }

        return addressOwner;
    }
]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new(SalesQuotationLine _quoteLine)
    {
        salesQuotationLine = _quoteLine;
    }
]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new <c>LogisticsLocationSelectForm_QuoteLine</c> object.
    /// </summary>
    /// <param name="_buffer">
    /// A <c>LogisticsLocationSelectForm_QuoteLine</c> record that is used to initialize the object.
    /// </param>
    /// <returns>
    /// A new <c>LogisticsLocationSelectForm_QuoteLine</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_QuoteLine construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_QuoteLine(_buffer);
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>