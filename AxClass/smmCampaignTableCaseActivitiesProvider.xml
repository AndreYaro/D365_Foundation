<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>smmCampaignTableCaseActivitiesProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case activities
/// </summary>
[ExportMetadataAttribute('CaseIActivities', tableStr(smmCampaignTable)), ExportAttribute('Dynamics.AX.Application.CaseIActivities')]
class smmCampaignTableCaseActivitiesProvider implements CaseIActivities
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromCommon</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommon(smmActivities _activities, Common _table)
    {
        smmCampaignTable smmCampaignTable = _table;
        this.initFromCampaignParameters(_activities, smmCampaignTable);
        return smmActivityParentType::Campaign;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parentRecId</Name>
				<Source><![CDATA[
    public RefRecId parentRecId(Common _table)
    {
        return _table.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCommonUsingTemplate</Name>
				<Source><![CDATA[
    public smmActivityParentType initFromCommonUsingTemplate(smmActivities _activities, Common _table, smmActivities _templateActivity)
    {
        smmCampaignTable smmCampaignTable = _table;
        _activities.initFromHierarchyTemplate(_templateActivity, smmCampaignTable.CampaignDate);
        return smmActivityParentType::Campaign;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateExternalMemo</Name>
				<Source><![CDATA[
    public smmOutlookMemo updateExternalMemo(RefRecId _parentLink)
    {
        smmCampaignTable smmCampaignTable = smmCampaignTable::findRec(_parentLink);
        str tail;

        const str colonspace = ': ';
        const str colon = ':';
        const str newline = '\n';

        tail += newline + "@SYS79883" + colonspace + smmCampaignTable.CampaignId + newline + "@SYS79963" + colonspace + smmCampaignTable.CampaignName + newline;

        if (smmCampaignTable.CampaignStatus)
        {
            tail += "@SYS82466" + colonspace + enum2str(smmCampaignTable.CampaignStatus) + newline;
        }

        if (smmCampaignTable.CampaignTypeId)
        {
            tail += "@SYS79882" + colonspace + smmCampaignTable.CampaignTypeId;
        }

        return tail;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldActivityBeCreated</Name>
				<Source><![CDATA[
    public CaseYesNoPrompt shouldActivityBeCreated(Common _table, boolean _isBatch)
    {
        if (_isBatch)
        {
            return (smmParametersTable::find().RunBaseBatchCreateActivity == smmRunBaseBatchCreateActivity::Yes) ? (CaseYesNoPrompt::Yes) : (CaseYesNoPrompt::No);
        }
        else
        {
            return smmActivities::shouldActivityBeCreatedGeneric(smmParametersTable::find().CampaignCreateActivity);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromCampaignParameters</Name>
				<Source><![CDATA[
    private void initFromCampaignParameters(smmActivities _activities, smmCampaignTable _campaign)
    {
        smmParametersTable parameters = smmParametersTable::find();

        _activities.initValue(parameters.CampaignActivityCategory);

        // Get default values from CRM parameters
        _activities.PhaseId                = parameters.CampaignActivityPhaseId;
        _activities.TypeId                 = parameters.CampaignActivityTypeId;
        _activities.Purpose                = parameters.CampaignActivityPurpose;

        // Copy values from campaign to activity
        _activities.setStartDate(_campaign.CampaignFollowUpDate);
        _activities.setEndDate(_campaign.CampaignFollowUpDate);
        _activities.setReminderDate(_campaign.CampaignFollowUpDate);
        _activities.ResponsibleWorker    = _campaign.CampaignRespWorker;

        _activities.initFromWorkerTable(_activities.ResponsibleWorker);

        if (_activities.ReminderMinutes > 0)
        {
            _activities.ReminderActive     = NoYes::Yes;
        }
        else
        {
            _activities.ReminderActive     = NoYes::No;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getParentReferenceFieldNum</Name>
				<Source><![CDATA[
    public FieldId getParentReferenceFieldNum(TableId _tableId, smmActivityParentRefTableId _parentTableId)
    {
        FieldId fieldId;

        switch (_parentTableId)
        {
            case tableNum(ProjTable) :
                fieldId = fieldNum(smmCampaignTable,ProjId);
                break;

            case tableNum(smmTMCallListGroup) :
                fieldId = fieldNum(smmCampaignTable,tmCallListID);
                break;
        }

        return fieldId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>