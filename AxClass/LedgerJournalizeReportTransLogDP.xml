<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalizeReportTransLogDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerJournalizeReportTransLogDP</c> class is the Report Data Provider class for the
///    <c>LedgerJournalizeReport2</c> SSRS report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(LedgerJournalizeReportTransLog))
]
public class LedgerJournalizeReportTransLogDP extends SRSReportDataProviderPreProcessTempDB
{
    LedgerJournalizeReportTmp ledgerJournalizeReportTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getLedgerJournalizeReportTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fetches the data from the <c>LedgerJournalizeReportTmp</c> temporary table.
    /// </summary>
    /// <returns>
    ///    Data from the <c>LedgerJournalizeReportTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute(tablestr(LedgerJournalizeReportTmp))]
    public LedgerJournalizeReportTmp getLedgerJournalizeReportTmp()
    {
        select ledgerJournalizeReportTmp;
        return ledgerJournalizeReportTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the SSRS report business logic.
    /// </summary>
    /// <remarks>
    ///    This method is used to process the report business logic that is used by
    ///    <c>LedgerJournalizeReportTransLog</c> SRS report.
    /// </remarks>
    public void processReport()
    {
        QueryRun queryRun;
        AmountMST accumulatedDebit;
        AmountMST accumulatedCredit;
        utcdatetime createdDate;

        GeneralJournalEntry generalJournalEntry;
        GeneralJournalAccountEntry generalJournalAccountEntry;
        LedgerEntryJournalizing ledgerEntryJournalizing;
        MainAccountLedgerDimensionView mainAccountLedgerDimensionView;
        TransactionLog transactionLog;

        queryRun = new QueryRun(this.parmQuery());

        while (queryRun.next())
        {
            transactionLog = queryRun.get(tablenum(TransactionLog)) as TransactionLog;
            generalJournalEntry = queryRun.get(tablenum(GeneralJournalEntry)) as  GeneralJournalEntry ;
            generalJournalAccountEntry = queryRun.get(tablenum(GeneralJournalAccountEntry)) as GeneralJournalAccountEntry;
            ledgerEntryJournalizing = queryRun.get(tablenum(LedgerEntryJournalizing)) as LedgerEntryJournalizing;
            mainAccountLedgerDimensionView = queryRun.get(tablenum(MainAccountLedgerDimensionView)) as MainAccountLedgerDimensionView;

            createdDate = transactionLog.CreatedDateTime;
            ledgerJournalizeReportTmp.CreatedDate = createdDate;
            ledgerJournalizeReportTmp.UserId = transactionLog.CreatedBy;
            ledgerJournalizeReportTmp.JournalizeSeqNum = ledgerEntryJournalizing.SequenceNumber;
            ledgerJournalizeReportTmp.JournalNumber = generalJournalEntry.JournalNumber;
            ledgerJournalizeReportTmp.Text = generalJournalAccountEntry.Text;
            ledgerJournalizeReportTmp.Journal = ledgerEntryJournalizing.Journal;
            ledgerJournalizeReportTmp.AccountingDate = generalJournalEntry.AccountingDate;
            ledgerJournalizeReportTmp.DisplayValue = mainAccountLedgerDimensionView.FullyQualifiedAccount;

            if (generalJournalAccountEntry.IsCredit == NoYes::Yes)
            {
                ledgerJournalizeReportTmp.AmountCredit = -generalJournalAccountEntry.AccountingCurrencyAmount;
                ledgerJournalizeReportTmp.AmountDebit = 0;
                accumulatedCredit -= generalJournalAccountEntry.AccountingCurrencyAmount;
            }
            else
            {
                ledgerJournalizeReportTmp.AmountDebit = generalJournalAccountEntry.AccountingCurrencyAmount;
                ledgerJournalizeReportTmp.AmountCredit = 0;
                accumulatedDebit += generalJournalAccountEntry.AccountingCurrencyAmount;
            }

            ledgerJournalizeReportTmp.AccumulatedDebit = accumulatedDebit;
            ledgerJournalizeReportTmp.AccumulatedCredit = accumulatedCredit;

            ledgerJournalizeReportTmp.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>