<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendExchAdj</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class VendExchAdj extends CustVendExchAdj implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildBaseQuery</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected Query buildBaseQuery()
    {
        Query userDefinedQuery = new Query(querystr(VendExchAdj));

        QueryBuildDataSource vendTransDs = userDefinedQuery.dataSourceTable(tablenum(VendTrans));
        vendTransDs.addRange(fieldNum(VendTrans, PostingProfile));
        vendTransDs.update(true);

        VendTrans_W::addToQuery(vendTransDs, JoinMode::OuterJoin);

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            vendTransDs.addRange(fieldNum(VendTrans, Prepayment)).value(queryValue(NoYes::No));
        }

        return userDefinedQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransTableId</Name>
				<Source><![CDATA[
    protected TableId getCustVendTransTableId()
    {
        return tableNum(VendTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransOpenTableId</Name>
				<Source><![CDATA[
    protected TableId getCustVendTransOpenTableId()
    {
        return tableNum(VendTransOpen);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransWTableId</Name>
				<Source><![CDATA[
    protected TableId getCustVendTransWTableId()
    {
        return tableNum(VendTrans_W);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransAccountNum</Name>
				<Source><![CDATA[
    protected FieldId getCustVendTransAccountNum()
    {
        return fieldNum(VendTrans, AccountNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransOpenRecId</Name>
				<Source><![CDATA[
    protected FieldId getCustVendTransOpenRecId()
    {
        return fieldNum(VendTransOpen, RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransRecId</Name>
				<Source><![CDATA[
    protected FieldId getCustVendTransRecId()
    {
        return fieldNum(VendTrans, RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransDate</Name>
				<Source><![CDATA[
    protected FieldId getCustVendTransDate()
    {
        return fieldNum(VendTrans, TransDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTransOpenRefRecId</Name>
				<Source><![CDATA[
    protected FieldId getCustVendTransOpenRefRecId()
    {
        return fieldNum(VendTransOpen, RefRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canGoBatchJournal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether the class is shown in the <c>Journal</c> types list.
    /// </summary>
    /// <returns>
    /// true if the class is shown in the list of <c>Journal</c> types; otherwise, false.
    /// </returns>
    /// <remarks>
    /// A class that can be used in a batch journal is a class where the same parameters can be used
    /// repeatedly. The dialog box can be shown and the parameters can be changed, but parameters of some
    /// classes might build on data that is only valid for a short time. Running a class two times with the
    /// same parameters is not always possible. If the <c>M:RunBaseBatch.canGoBatch</c> is false, this
    /// method will not have any effect.
    /// </remarks>
    public boolean canGoBatchJournal()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        super();

        queryRun = new QueryRun(this.buildBaseQuery());
        postingProfile = VendParameters::find().PostingProfile;
    }

]]></Source>
			</Method>
			<Method>
				<Name>outputReport</Name>
				<Source><![CDATA[
    protected void outputReport(createdTransactionId _transactionId)
    {
        TmpCustVendTransReorg tmpCustVendTransReorg;
        MenuFunction menuFunction;
        Args args;

        tmpCustVendTransReorg.RefRecId = _transactionId;
        tmpCustVendTransReorg.DueDate = this.postingDate();
        tmpCustVendTransReorg.TransDate = this.exchRateDate();

        args = new Args();
        args.record(tmpCustVendTransReorg);
        menuFunction = new MenuFunction(menuItemOutputStr(VendExchRateAdjustment), MenuItemType::Output);
        menuFunction.run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        container packed = [CurrentVersion, dimSpec, printExchAdj, postingSpec, postingProfile, postingDate, exchRateDate, exchPrinciples, transExchAdjTxt, notes, hasBatchBeenSplit, fromCustVendAccountNum, toCustVendAccountNum, queryRun.pack()];
        if (FeatureStateProvider::isFeatureEnabled(CustVendExchAdjTopPickingFeature::instance()))
        {
            packed = SysPackExtensions::appendVariableToPack(packed, this.name(), varStr(hidden), this.parmHidden());
            packed = SysPackExtensions::appendVariableToPack(packed, this.name(), varStr(executionId), this.parmExecutionId());
        }
        return packed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version version = RunBase::getVersion(_packedClass);
        container packedQuery;

        switch (version)
        {
            case CurrentVersion:
                [version, dimSpec, printExchAdj, postingSpec, postingProfile, postingDate, exchRateDate, exchPrinciples, transExchAdjTxt, notes, hasBatchBeenSplit, fromCustVendAccountNum, toCustVendAccountNum, packedQuery] = _packedClass;

                if (packedQuery)
                {
                    queryRun = new QueryRun(packedQuery);
                }
                break;

            case 7:
                [version, dimSpec, printExchAdj, postingSpec, postingProfile, postingDate, exchRateDate, exchPrinciples, transExchAdjTxt, notes, packedQuery] = _packedClass;

                if (packedQuery)
                {
                    queryRun = new QueryRun(packedQuery);
                }
                break;

            default:
                return false;
        }

        if (FeatureStateProvider::isFeatureEnabled(CustVendExchAdjTopPickingFeature::instance()))
        {
            this.parmHidden(SysPackExtensions::extractPackedVariable(_packedClass, this.name(), varStr(hidden)));
            this.parmExecutionId(SysPackExtensions::extractPackedVariable(_packedClass, this.name(), varStr(executionId)));
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret;

        if (!postingDate)
        {
            throw error(strfmt("@SYS84378", "@SYS95471"));
        }

        ret = super();

        if (ret && exchRateDate != postingDate)
        {
            ret = (Box::yesNo("@SYS95677", DialogButton::Yes, "@SYS8879", "@SYS95678") == DialogButton::Yes);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ClassDescription description()
    {
        return "@SYS55328";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        VendExchAdj vendExchAdj = new VendExchAdj();

        if (vendExchAdj.prompt())
        {
            vendExchAdj.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPostingProfileControlToDialog</Name>
				<Source><![CDATA[
    protected DialogField addPostingProfileControlToDialog()
    {
        return dialogRunbase.addFieldValue(extendedTypeStr(VendPostingProfile), postingProfile, "@SYS21530", "@SYS95476");
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPayloadOpenTrans</Name>
				<Source><![CDATA[
    internal protected Query getPayloadOpenTrans()
    {
        Query query = new Query(queryRun.query());

        QueryBuildDataSource vendTransDs = query.dataSourceTable(tableNum(VendTrans));
        vendTransDs.update(false);
        vendTransDs.addRange(fieldNum(VendTrans, TransDate)).value(queryRange(dateNull(), postingDate));

        Ledger ledger = Ledger::findByLegalEntity(CompanyInfo::current());

        vendTransDs.addRange(fieldNum(VendTrans, CurrencyCode)).value(SysQuery::valueNot(ledger.AccountingCurrency));
        if (!CustVendExchAdjVoucherLookupFlight::instance().isEnabled() || ledger.ReportingCurrency)
        {
            vendTransDs.addRange(fieldNum(VendTrans, CurrencyCode)).value(SysQuery::valueNot(ledger.ReportingCurrency));
        }

        QueryBuildDataSource vendTransOpenDs = vendTransDs.addDataSource(tableNum(VendTransOpen));
        vendTransOpenDs.addLink(fieldNum(VendTrans, RecId), fieldNum(VendTransOpen, RefRecId), vendTransDs.name());
        vendTransOpenDs.joinMode(JoinMode::ExistsJoin);
        vendTransOpenDs.relations(false);

        for (int dataSourceNo = 1; dataSourceNo <= query.dataSourceCount(); dataSourceNo ++)
        {
            query.dataSourceNo(dataSourceNo).fields().clearFieldList();
            query.dataSourceNo(dataSourceNo).sortClear();
        }

        vendTransDs.addSortField(fieldNum(VendTrans, RecId));
        vendTransDs.addSelectionField(fieldNum(VendTrans, RecId));

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPayloadSettleTrans</Name>
				<Source><![CDATA[
    internal protected Query getPayloadSettleTrans()
    {
        Query query = new Query(queryRun.query());

        // <GEERU>
        boolean countryRegion_RU = SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]);
        // </GEERU>

        QueryBuildDataSource vendTransDs = query.dataSourceTable(tableNum(VendTrans));
        vendTransDs.update(false);
        vendTransDs.addRange(fieldNum(VendTrans, TransDate)).value(queryRange(dateNull(), postingDate));

        Ledger ledger = Ledger::findByLegalEntity(CompanyInfo::current());

        vendTransDs.addRange(fieldNum(VendTrans, CurrencyCode)).value(SysQuery::valueNot(ledger.AccountingCurrency));
        if (!CustVendExchAdjVoucherLookupFlight::instance().isEnabled() || ledger.ReportingCurrency)
        {
            vendTransDs.addRange(fieldNum(VendTrans, CurrencyCode)).value(SysQuery::valueNot(ledger.ReportingCurrency));
        }

        // <GEERU>
        if (countryRegion_RU)
        {
            vendTransDs.addRange(fieldNum(VendTrans, TransType)).value(queryNotValueCon_W([LedgerTransType::ExchAdjustment, LedgerTransType::AdvanceAdjustment_RU]));
        }
        // </GEERU>

        QueryBuildDataSource vendSettlementDs = vendTransDs.addDataSource(tableNum(VendSettlement));
        vendSettlementDs.addLink(fieldNum(VendTrans, RecId), fieldNum(VendSettlement, TransRecId), vendTransDs.name());
        vendSettlementDs.joinMode(JoinMode::ExistsJoin);
         
        // <GEERU>
        if (countryRegion_RU)
        {
            vendSettlementDs.addRange(fieldNum(VendSettlement, TransDate)).value(SysQuery::range(postingDate, null));
            QueryBuildDataSource vendTrans_W = VendTrans_W::addToQuery(vendTransDs, JoinMode::NoExistsJoin);
            vendTrans_W.addRange(fieldNum(VendTrans_W, BankCurrencyTransferId_RU)).value(SysQuery::valueNotEmptyString());
        }
        else
        {
            // </GEERU>
            vendSettlementDs.addRange(fieldNum(VendSettlement, TransDate)).value(strfmt('> %1', queryValue(postingDate)));
            // <GEERU>
        }
        // </GEERU>
        vendSettlementDs.addRange(fieldNum(VendSettlement, SettleAmountCur)).value(SysQuery::valueNot(0));
        vendSettlementDs.addSortField(fieldNum(VendSettlement, TransDate), SortOrder::Ascending);
        vendSettlementDs.relations(false);

        for (int dataSourceNo = 1; dataSourceNo <= query.dataSourceCount(); dataSourceNo ++)
        {
            query.dataSourceNo(dataSourceNo).fields().clearFieldList();
            query.dataSourceNo(dataSourceNo).sortClear();
        }

        vendTransDs.addSortField(fieldNum(VendTrans, RecId));
        vendTransDs.addSelectionField(fieldNum(VendTrans, RecId));

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>