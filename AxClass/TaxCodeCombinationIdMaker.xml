<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxCodeCombinationIdMaker</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is used by the Tax framework to determine unique ids per tax code combinations
/// </summary>
final class TaxCodeCombinationIdMaker
{
    // Maps tax code combination to unique id
    Map                 taxCodeCombinationMap;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        taxCodeCombinationMap = new Map(typeName2Type(extendedTypeStr(TaxCodeCombinationId)), Types::Class);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Static constructor
    /// </summary>
    /// <returns>An instance of <c>TaxComputeTaxCodeCombinationId</c></returns>
    public static TaxCodeCombinationIdMaker construct()
    {
        return new TaxCodeCombinationIdMaker();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUniqueId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a unique identifier per set combination
    /// </summary>
    /// <param name = "_taxCodeCombination">The set of tax codes</param>
    /// <returns>A <c>TaxCodeCombinationId</c> for the given set</returns>
    public TaxCodeCombinationId getUniqueId(Set _taxCodeCombination)
    {
        MapIterator             mapIterator;
        TaxCodeCombinationId    taxCodeCombinationId;
        Set                     taxCodeCombination;

        mapIterator = new MapIterator(taxCodeCombinationMap);
        while (mapIterator.more())
        {
            taxCodeCombination = mapIterator.value();

            if (taxCodeCombination.toString() == _taxCodeCombination.toString()) // s.equal(_set) does not work
            {
                return mapIterator.key();
            }
            mapIterator.next();
            taxCodeCombinationId++;
        }
        taxCodeCombinationId++;
        taxCodeCombinationMap.insert(taxCodeCombinationId, _taxCodeCombination);
        return taxCodeCombinationId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>