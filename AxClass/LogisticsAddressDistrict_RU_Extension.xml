<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsAddressDistrict_RU_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of table <c>LogisticsAddressDistrict</c>
/// </summary>
[ExtensionOf(tableStr(LogisticsAddressDistrict))]
final class LogisticsAddressDistrict_RU_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupDistrictId_RU_Extension</Name>
				<Source><![CDATA[
    protected client static QueryBuildDataSource lookupDistrictId_RU_Extension(QueryBuildDataSource _qbds, FieldId _recIdNum, FieldId _tableIdNum)
    {
        next lookupDistrictId_RU_Extension(_qbds, _recIdNum, _tableIdNum);
        
        return LogisticsAddressNotFoundRecords_RU::filterQueryByLog(_qbds, _recIdNum, _tableIdNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>propertiesOKTMO_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays or sets OKTMO for address element
    /// </summary>
    /// <param name="_set">
    /// true when editing; false when viewing.
    /// </param>
    /// <param name="_oktmo">
    /// New value
    /// </param>
    /// <returns>
    /// Current (edited if _set is true) value
    /// </returns>
    [SysClientCacheDataMethodAttribute(true)]
    edit OKTMO_RU propertiesOKTMO_RU(
        boolean _set,
        OKATO_RU _oktmo)
    {
        if (!this.RecId && _set)
        {
            this.insert();
        }
        return LogisticsAddressFiasGuidCodes_RU::parmOKTMOByAddressTableId(this, _oktmo, _set);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupDistrictIdWithGARLevel_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens lookup form for districts using PK reference
    /// </summary>
    /// <param name="_ctrlDistrict">
    /// District reference control
    /// </param>
    /// <param name="_countryRegionId">
    /// Related country/region ID
    /// </param>
    /// <param name="_stateId">
    /// Related state ID
    /// </param>
    /// <param name="_countyId">
    /// Related county ID
    /// </param>
    /// <param name="_settlementId">
    /// Related Urban/Rural settlement ID
    /// </param>
    /// <param name="_cityId">
    /// Related city ID
    /// </param>
    /// <param name="_localityId">
    /// Related locality ID
    /// </param>
    /// <returns>
    /// District record selected by user
    /// </returns>
    internal client static LogisticsAddressDistrict lookupDistrictIdWithGARLevel_RU(
        FormReferenceControl _ctrlDistrict,
        LogisticsAddressCountryRegionId _countryRegionId,
        LogisticsAddressStateId _stateId,
        LogisticsAddressCountyId _countyId,
        LogisticsAddressCityRecId _settlementId,
        LogisticsAddressCityRecId _cityId,
        LogisticsAddressCityRecId _localityId)
    {
        SysReferenceTableLookup lookup = SysReferenceTableLookup::newParameters(
            tableNum(LogisticsAddressDistrict),
            _ctrlDistrict,
            true);

        lookup.addLookupfield(fieldNum(LogisticsAddressDistrict, Name));
        lookup.addLookupfield(fieldNum(LogisticsAddressDistrict, Description));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressDistrict, fullName_RU));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressDistrict, propertiesZipCode_RU));
        lookup.addLookupMethod(tableMethodStr(LogisticsAddressDistrict, propertiesGniCode_RU));

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(LogisticsAddressDistrict));
        qbds = LogisticsAddressDistrict::lookupDistrictId_RU_Extension(qbds, fieldNum(LogisticsAddressDistrict, RecId), fieldNum(LogisticsAddressDistrict, TableId));
  
        LogisticsAddressCityRecId cityTableRecId = _settlementId;
        if (_cityId)
        {
            cityTableRecId = _cityId;
        }

        if (_localityId)
        {
            cityTableRecId = _localityId;
        }
        qbds.addRange(fieldNum(LogisticsAddressDistrict, City)).value(queryValue(cityTableRecId));

        if (!cityTableRecId)
        {
            qbds.addRange(fieldNum(LogisticsAddressDistrict, CountryRegionId_RU)).value(queryValue(_countryRegionId));
            qbds.addRange(fieldNum(LogisticsAddressDistrict, StateId_RU)).value(queryValue(_stateId));
            qbds.addRange(fieldNum(LogisticsAddressDistrict, CountyId_RU)).value(queryValue(_countyId));
        }

        lookup.parmQuery(query);
        LogisticsAddressDistrict selectedRecord = lookup.performFormLookup();
        return selectedRecord;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>