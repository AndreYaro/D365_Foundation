<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchLineCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'PurchLine'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PurchLineCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        PurchLine purchLine;
        select crosscompany * from purchLine where purchLine.RecId == _caseAssociation.RefRecId;

        VendTable vendTable;
        select firstonly RecId from vendTable where vendTable.AccountNum == purchLine.orderAccount();

        if (vendTable)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Vendor, vendTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        PurchLine purchLine;
        select crosscompany DataAreaId from purchLine where purchLine.RecId == _parentRecId;

        return purchLine.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        PurchLine purchLine;
        select crosscompany PurchId from purchLine where purchLine.RecId == _parentRecId;

        return purchLine.PurchId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        PurchLine purchLine = PurchLine::find(_naturalKey);

        if (!purchLine)
        {
            // Since this table doesn't have a single field natural key
            // RecId might be sent instead.  See performLookup().
            purchLine = PurchLine::findRecId(str2Int64(_naturalKey));
        }

        return purchLine.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        PurchLine purchLine;
        select crosscompany Name from purchLine where purchLine.RecId == _parentRecId;

        return purchLine.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        PurchLine purchLine;
        
        //We need this code in order not to lose the localization data in the opened form.
        PurchLine purchLineCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from purchLineCrossCompany
            where purchLineCrossCompany.RecId == _association.RefRecId;

        if (purchLineCrossCompany.Recid)
        {
            changecompany (purchLineCrossCompany.DataAreaId)
            {
                select firstonly purchLine where purchLine.RecId == _association.RefRecId;
            }
        }

        _args.record(purchLine);
        return menuitemdisplaystr(PurchLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(PurchLine), callerControl);

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(PurchLine));
        sysTableLookup.addLookupfield(fieldNum(PurchLine, RecId), true);
        sysTableLookup.addLookupfield(fieldnum(PurchLine, PurchId));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, ItemId));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, PurchQty));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, PurchPrice));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, LineAmount));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, CurrencyCode));
        sysTableLookup.addLookupfield(fieldnum(PurchLine, PurchStatus));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(PurchLine))
        {
            _eventArgs.parmEntityType(CaseEntityType::PurchLine);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>