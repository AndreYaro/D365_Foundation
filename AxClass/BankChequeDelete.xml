<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankChequeDelete</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class BankChequeDelete extends BankNegInstDelete
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>deleteDocuments</Name>
				<Source><![CDATA[
    protected void deleteDocuments()
    {
        BankChequeTable bankChequeTable;

        delete_from bankChequeTable
            where bankChequeTable.AccountID == accountID
                && bankChequeTable.ChequeNum >= fromNegInstNum
                && bankChequeTable.ChequeNum <= toNegInstNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        DialogRunbase dialog = super();

        dialog.addGroup("@SYS23068");

        dialogFromNegInstNum = dialog.addField(extendedTypeStr(BankChequeNum), "@SYS4083");
        dialogToNegInstNum = dialog.addField(extendedTypeStr(BankChequeNum), "@SYS8828");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mayBeDeleted</Name>
				<Source><![CDATA[
    protected boolean mayBeDeleted()
    {
        BankChequeTable bankChequeTable;

        select firstonly RecId from bankChequeTable
            where bankChequeTable.AccountID == accountID
                && bankChequeTable.ChequeNum >= fromNegInstNum
                && bankChequeTable.ChequeNum <= toNegInstNum
                && bankChequeTable.ChequeStatus != ChequeStatus::Created;

        return bankChequeTable.RecId == 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doAnyChequesExistInRange</Name>
				<Source><![CDATA[
    private boolean doAnyChequesExistInRange()
    {
        BankChequeTable bankChequeTable;

        select firstonly RecId from bankChequeTable
            where bankChequeTable.AccountID == accountID
                && bankChequeTable.ChequeNum >= fromNegInstNum
                && bankChequeTable.ChequeNum <= toNegInstNum;

        return bankChequeTable.RecID != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtDocumentsDeletedOk</Name>
				<Source><![CDATA[
    protected str txtDocumentsDeletedOk()
    {
        return strfmt("@CashManagement:BankChequeDeleteComplete", fromNegInstNum, toNegInstNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret = super();

        if (ret && !this.doAnyChequesExistInRange())
        {
            return checkFailed("@CashManagement:BankChequeDeleteNoChequesToDelete");
        }

        if (ret && !this.mayBeDeleted())
        {
            return checkFailed(strfmt("@SYS23383", ChequeStatus::Created));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS22508";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        BankChequeDelete chequeDelete;

        switch (args.dataset())
        {
            case tablenum(BankAccountTable):
                chequeDelete = BankNegInstDelete::newBankAccountTable(BankNegInst::Check, args.record());
                chequeDelete.setAccountIdInactive();
                break;

            case tablenum(BankChequeTable):
                chequeDelete = BankNegInstDelete::newBankNegInstTableMap(BankNegInst::Check, args.record());
                break;

            default:
                throw error(Error::missingRecord(BankChequeDelete::description()));
        }

        if (chequeDelete.prompt())
        {
            chequeDelete.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>