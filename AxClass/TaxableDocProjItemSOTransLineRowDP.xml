<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocProjItemSOTransLineRowDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Row data provider for project item transaction
/// </summary>
public class TaxableDocProjItemSOTransLineRowDP extends TaxableDocumentRowDataProviderLine
{
    ProjProposalItem        projProposalItem;
    ProjProposalItemDetail  projProposalItemDetail;
    ProjProposalJour        projProposalJour;
    EcoResProduct           ecoResProduct;
    SalesLine               salesLine;
    SalesTable              salesTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillInFields</Name>
				<Source><![CDATA[
    public void fillInFields(TaxableDocumentLineObject _lineObj)
    {
        super(_lineObj);

        this.addFieldValue(_lineObj, TaxEngineModelFields::ProductType, ecoResProduct.ProductType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initMainTable</Name>
				<Source><![CDATA[
    protected void initMainTable()
    {
        projProposalItem  = mainRecord as ProjProposalItem;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initRelatedTables</Name>
				<Source><![CDATA[
    protected void initRelatedTables()
    {
        projProposalJour = projProposalItem.projProposalJour();
        salesLine        = SalesLine::findInventTransId(projProposalItem.InventTransId);

        select firstOnly projProposalItemDetail
        where projProposalItemDetail.ProposalRefRecId == projProposalItem.RecId;

        if (salesLine.inventTable())
        {
            ecoResProduct = EcoResProduct::find(salesLine.inventTable().Product);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValidFields</Name>
				<Source><![CDATA[
    protected void initValidFields()
    {
        super();
        validFields.add(TaxEngineModelFields::ProductType, Types::Enum, enumNum(EcoResProductType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>isCustomsExportOrder_IN</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if this is an import order or not.
    /// </summary>
    /// <returns>
    /// True if this transaction is an import order; otherwise, false.
    /// </returns>
    // <GIN>
    [Replaceable]
    protected boolean isCustomsExportOrder_IN()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountingCurrencyExchangeRate</Name>
				<Source><![CDATA[
    // </GIN>

    /// <summary>
    /// Gets accounting currency exchange rate
    /// </summary>
    /// <returns>
    /// Accounting currency exchange rate
    /// </returns>
    [Replaceable]
    protected ExchRate getAccountingCurrencyExchangeRate(TaxableDocumentLineObject _lineObj)
    {
        if (this.isCustomsExportOrder_IN())
        {
            return super(_lineObj);
        }
        else
        {
            ExchRate exchangeRate = this.getFixedExchRate();
            return exchangeRate ? exchangeRate : super(_lineObj);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFixedExchRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets fixed exchange rate from SO header
    /// </summary>
    /// <returns>Fixed exchange rate from SO header</returns>
    private ExchRate getFixedExchRate()
    {
        if (!salesTable)
        {
            salesTable = relatedRecordsQueryRun.getNo(1) as SalesTable;
            if (!salesTable)
            {
                salesTable = SalesTable::find(salesLine.SalesId);
            }
        }

        return salesTable.FixedExchRate / 100;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>