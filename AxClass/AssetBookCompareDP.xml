<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetBookCompareDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    Declares variables and tables for the <c>AssetBookCompare</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(AssetBookCompareBaseData)),
    SRSReportParameterAttribute(classstr(AssetBookCompareContract))
]
public class AssetBookCompareDP extends SRSReportDataProviderPreProcessTempDb
{
    #ISOCountryRegionCodes
    AssetSumCalc assetSumCalc;
    AssetSumCalc assetSumCalcYTD;
    AssetBookCompareTmp assetBookCompareTmp;
    AssetTable assetTable;
    AssetBookId book1;
    AssetBookId book2;
    AssetBook assetBook;
    date fromDate, fromAcquisitionDate, fromPlacedInService;
    date toDate, toAcquisitionDate, toPlacedInService;
    AssetBookCompareContract contract;

    // <GIN>
    AssetSumCalc_Trans_IN           assetSumCalc_Trans;

    boolean                         groupnetVisibilityFlag;
    boolean                         isDepreciationIncomeTaxActEnable;
    AssetGroupId                    assetGroupId;
    AssetBookId                     bookId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>depreciationMethod</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the accumulated depreciation value.
    /// </summary>
    /// <returns>
    ///    The corresponding <c>assetDepreciationProfile</c> method name, which is based on the specified
    ///    conditions.
    /// </returns>
    private AssetDepreciationMethod depreciationMethod()
    {
        return assetBook.depreciationMethod();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssetBookCompareTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets data from the <c>AssetBookCompareTmp</c> temporary table.
    /// </summary>
    /// <returns>
    ///    The object from the <c>AssetBookCompareTmp</c> temporary table.
    /// </returns>
    [
        SrsReportDataSetAttribute(tablestr(AssetBookCompareTmp))
    ]
    public AssetBookCompareTmp getAssetBookCompareTmp()
    {
        select assetBookCompareTmp;
        return assetBookCompareTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAssetGroupNetBookValue_IN</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Calculates NetBookValue.
    /// </summary>
    /// <param name="_bookId">
    ///     The <c>BookId</c> field to which value is assigned.
    /// </param>
    /// <param name="_assetGroupId">
    ///     The <c>AssetGroupId</c> field to which value is assigned.
    /// </param>
    /// <returns>
    ///     The NetBookValue.
    /// </returns>
    public AssetNetBookValue getAssetGroupNetBookValue_IN(
        AssetBookId     _bookId,
        AssetGroupId    _assetGroupId)
    {
        AssetSumCalc    assetGroupSumCalc;

        assetGroupSumCalc = assetSumCalc_Trans;
        assetGroupSumCalc.parmAllAssetsSold_IN(AssetBook::checkAllAssetsSold_IN(_assetGroupId, _bookId));

        assetGroupSumCalc.parmGroup_IN(NoYes::Yes);
        return assetGroupSumCalc.netBookValue();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssetBookCompareTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Inserts data into the <c>AssetBookCompareTmp</c> temporary table.
    /// </summary>
    private void insertAssetBookCompareTmp()
    {
        // <GIN>
        AssetNetBookValue   assetGroupNetBookValue;

        assetBookCompareTmp.clear();
        // </GIN>

        assetBookCompareTmp.AssetGroup = assetBook.AssetGroup;
        assetBookCompareTmp.AssetId = assetBook.AssetId;
        assetBookCompareTmp.BookId = assetBook.BookId;
        assetBookCompareTmp.CostBasis = assetSumCalc.acquisitionValueAll() + assetSumCalc.writeUpDownAdjValue() + assetSumCalc.revaluationValue();
        assetBookCompareTmp.AccDepreciation = assetSumCalc.depreciationValueAll() + assetSumCalc.extraDepreciationValue() + assetSumCalc.bonusDepreciationValue();
        assetBookCompareTmp.NetBook = assetSumCalc.netBookValue();
        assetBookCompareTmp.Name = assetTable.Name;
        assetBookCompareTmp.Status = enum2str(assetBook.Status);
        assetBookCompareTmp.LastDepreciationDate = assetBook.LastDepreciationDate;
        assetBookCompareTmp.DepMethod = enum2str(this.depreciationMethod());
        assetBookCompareTmp.DepreciationConvention = enum2str(assetBook.DepreciationConvention);

        // Spin up the correct instance of AssetSumCalc.
        // This time it will just be for the current year.
        // Use a standard calendar.

        // <GIN>
        if (isDepreciationIncomeTaxActEnable && AssetBook::find(assetBook.AssetId, assetBook.BookId).AssetGroupDepreciation_IN)
        {
            assetSumCalcYTD = AssetSumCalc_Trans_IN::newAssetPeriod(assetBook.AssetGroup,assetBook.BookId,fromDate,toDate);
        }
        else
        {
        // </GIN>
            if (contract.parmCurrencySelection() == LedgerCurrencySelection::Accounting)
            {
                assetSumCalcYTD = AssetSumCalc_Trans::newAssetPeriod(assetBook.AssetId,assetBook.BookId,fromDate,toDate);
            }
            else if (contract.parmCurrencySelection() == LedgerCurrencySelection::Reporting)
            {
                assetSumCalcYTD = AssetSumCalc_Trans::newAssetPeriodByCurrency(assetBook.AssetId, assetBook.BookId, fromDate, toDate, AssetSumCalcType::Reporting);
            }
        // <GIN>
        }
        // </GIN>

        assetBookCompareTmp.ytdDepreciation  = assetSumCalcYTD.depreciationValueAll() + assetSumCalcYTD.extraDepreciationValue() + assetSumCalcYTD.bonusDepreciationValue();

        if (assetBook.BookId == book1)
        {
            assetBookCompareTmp.DiffCostBasis = assetSumCalc.acquisitionValueAll() + assetSumCalc.writeUpDownAdjValue() + assetSumCalc.revaluationValue();
            assetBookCompareTmp.DiffAccDepreciation = assetSumCalc.depreciationValueAll() + assetSumCalc.extraDepreciationValue() + assetSumCalc.bonusDepreciationValue();
            assetBookCompareTmp.DiffNetBookValue = assetSumCalc.netBookValue();
            assetBookCompareTmp.DiffYTDDepreciation = assetSumCalcYTD.depreciationValueAll() + assetSumCalcYTD.extraDepreciationValue() + assetSumCalcYTD.bonusDepreciationValue();
            assetBookCompareTmp.BookNumber = 1;
        }
        else
        {
            assetBookCompareTmp.DiffCostBasis = -(assetSumCalc.acquisitionValueAll() + assetSumCalc.writeUpDownAdjValue() + assetSumCalc.revaluationValue());
            assetBookCompareTmp.DiffAccDepreciation = -(assetSumCalc.depreciationValueAll() + assetSumCalc.extraDepreciationValue() + assetSumCalc.bonusDepreciationValue());
            assetBookCompareTmp.DiffNetBookValue = -(assetSumCalc.netBookValue());
            assetBookCompareTmp.DiffYTDDepreciation = -(assetSumCalcYTD.depreciationValueAll() + assetSumCalcYTD.extraDepreciationValue() + assetSumCalcYTD.bonusDepreciationValue());
            assetBookCompareTmp.BookNumber = 2;
        }

        assetBookCompareTmp.SumCostBasis = assetSumCalc.acquisitionValueAll() + assetSumCalc.writeUpDownAdjValue() + assetSumCalc.revaluationValue();
        assetBookCompareTmp.SumYTDDepreciation = assetSumCalcYTD.depreciationValueAll() + assetSumCalcYTD.extraDepreciationValue() + assetSumCalcYTD.bonusDepreciationValue();
        assetBookCompareTmp.SumAccDepreciation = assetSumCalc.depreciationValueAll() + assetSumCalc.extraDepreciationValue() + assetSumCalc.bonusDepreciationValue();
        assetBookCompareTmp.SumNetBook = assetSumCalc.netBookValue();

        // <GIN>
        if (isDepreciationIncomeTaxActEnable)
        {
            assetGroupNetBookValue = this.getAssetGroupNetBookValue_IN(
                assetBook.BookId,
                assetBook.AssetGroup);

            assetBookCompareTmp.GroupNetBookValueFlag_IN        = groupnetVisibilityFlag;
            assetBookCompareTmp.AssetGroupNetBookValue_IN       = assetGroupNetBookValue;
            assetBookCompareTmp.DiffAssetGroupNetBookValue_IN   = assetBook.BookId == book1
                    ? assetGroupNetBookValue
                    : -assetGroupNetBookValue;

            if (assetGroupId != assetTable.AssetGroup || bookId != assetBook.BookId)
            {
                assetGroupId = assetTable.AssetGroup;
                bookId       = assetBook.BookId;

                assetBookCompareTmp.SumAssetGroupNetBookValue_IN     = assetGroupNetBookValue;
                assetBookCompareTmp.SumDiffAssetGroupNetBookValue_IN = assetBook.BookId == book1
                    ? assetGroupNetBookValue
                    : -assetGroupNetBookValue;
            }
        }
        // </GIN>
        assetBookCompareTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fetches records based on the specified parameter values and inserts the records into the
    ///    <c>AssetBookCompareTmp</c> temporary table.
    /// </summary>
    public void processReport()
    {
        Query query = new Query(this.parmQuery());
        QueryRun queryRun;

        contract = this.parmDataContract() as  AssetBookCompareContract;
        str preferredLocal = new Session().preferredLocale();

        fromDate = DateTimeUtil::getStartOfYearDate(preferredLocal, contract.parmStartdate());
        toDate = DateTimeUtil::getEndOfYearDate(preferredLocal, contract.parmStartdate());

        book1 = contract.parmFirstAssetBookId();
        book2 = contract.parmSecondAssetBookId();

        // <GIN>
        isDepreciationIncomeTaxActEnable = AssetParameters::isDepreciationIncomeTaxActEnable_IN();
        if (isDepreciationIncomeTaxActEnable)
        {
            groupnetVisibilityFlag = NoYes::yes;
            query.clearOrderBy();
            query.dataSourceTable(tableNum(AssetTable)).addOrderByField(fieldNum(AssetTable, AssetGroup));
            query.dataSourceTable(tableNum(AssetBook)).addOrderByField(fieldNum(AssetBook, BookId));
            query.dataSourceTable(tableNum(AssetTable)).addOrderByField(fieldNum(AssetTable, AssetId));
        }
        else
        {
            groupnetVisibilityFlag = NoYes::no;
        }
        // </GIN>
        queryRun = new QueryRun(query);

        while (queryRun.next())
        {
            assetBook = queryRun.get(tablenum(AssetBook));
            assetTable = queryRun.get(tablenum(AssetTable));

            if ((assetBook.BookId == book1 && AssetBook::exist(assetBook.AssetId, book2))
                || (assetBook.BookId == book2 && AssetBook::exist(assetBook.AssetId, book1)))
            {
                if (contract.parmCurrencySelection() == LedgerCurrencySelection::Accounting)
                {
                    assetSumCalc = AssetSumCalc_Trans::newAssetYear(assetBook.AssetId, assetBook.BookId);
                }
                else if (contract.parmCurrencySelection() == LedgerCurrencySelection::Reporting)
                {
                    assetSumCalc = AssetSumCalc_Trans::newAssetYearByCurrency(assetBook.AssetId, assetBook.BookId, DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()), AssetYear::All, AssetSumCalcType::Reporting);
                }
                // <GIN>
                if (isDepreciationIncomeTaxActEnable)
                {
                    assetSumCalc_Trans = AssetSumCalc_Trans_IN::newAssetYear(
                        assetBook.AssetGroup,
                        assetBook.bookId);
                }
                // </GIN>

                // Grab the amounts for the book and save in temp table.
                this.insertAssetBookCompareTmp();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>