<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventStdCostConvUndo_Ready</Name>
	<SourceCode>
		<Declaration><![CDATA[
class InventStdCostConvUndo_Ready extends InventStdCostConvUndo
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Reverts the <c>Status</c> field of the <c>InventStdCostConv</c> record buffer from a value of
    ///    <c>InventStdCostConvStatus::Ready</c> to a value of <c>InventStdCostConvStatus::Planned</c>.
    /// </summary>
    /// <remarks>
    ///    This method updates all related <c>InventStdCostConvItem</c> records to a <c>Status</c> value of
    ///    Planned and the <c>CheckedDateTime</c> value is cleared.
    /// </remarks>
    /// <exception cref="M:Exception::UpdateConflict">
    ///    The update conflict was caught within a transaction scope.
    /// </exception>
    /// <exception cref="M:Exception::UpdateConflictNotRecovered">
    ///    The update conflict could not be recovered.
    /// </exception>
    /// <exception cref="M:Exception::Error">
    ///    The validation failed.
    /// </exception>
    public void run()
    {
        #OCCRetryCount

        try
        {
            ttsbegin;

            inventStdCostConv = InventStdCostConv::find(inventStdCostConv.RecId, true);

            if (!this.validate())
            {
                throw Exception::Error;
            }

            inventStdCostConv.Status = InventStdCostConvStatus::Planned;
            inventStdCostConv.update();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the status can be reverted.
    /// </summary>
    /// <returns>
    /// true if the validation was successful; otherwise, false.
    /// </returns>
    public boolean validate()
    {
        boolean ret = true;

        if (!inventStdCostConv.allowUndoReadyConversion())
        {
            return checkFailed(strFmt("@SYS120300",InventStdCostConvStatus::Planned));
        }

        if (!ret)
        {
            ret = checkFailed(strFmt("@SYS120301",InventStdCostConvStatus::Planned));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Instantiates an <c>InventStdCostConvUndo_Ready</c> object.
    /// </summary>
    /// <param name="_inventStdCostConv">
    ///    An <c>InventStdCostConv</c> record buffer that must be have the status reverted.
    /// </param>
    /// <returns>
    ///    An <c>InventStdCostConvUndo_Ready</c> object.
    /// </returns>
    /// <remarks>
    ///    This method should not be called directly. An <c>InventStdCostConvUndo</c> type object should be
    ///    instantiated by using the constructor in the <c>InventStdCostConvUndo</c> class.
    /// </remarks>
    public static InventStdCostConvUndo_Ready construct(InventStdCostConv _inventStdCostConv)
    {
        return new InventStdCostConvUndo_Ready(_inventStdCostConv);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>