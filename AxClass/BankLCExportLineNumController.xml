<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLCExportLineNumController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLCExportLineNumController</c> class is the controller helper class for the
///    <c>LineNum</c> field of the <c>BankLCExportLine</c> table.
/// </summary>
class BankLCExportLineNumController extends BankLCLineNumController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createReferenceTableLookup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the reference table lookup.
    /// </summary>
    /// <param name="_control">
    ///    The form control to perform the lookup.
    /// </param>
    /// <returns>
    ///    The created <c>SysReferenceTableLookup</c> object.
    /// </returns>
    protected SysReferenceTableLookup createReferenceTableLookup(FormReferenceControl _control)
    {
        SysReferenceTableLookup sysReferenceTableLookup;

        sysReferenceTableLookup = SysReferenceTableLookup::newParameters(tablenum(BankLCExportLine), _control);
        sysReferenceTableLookup.addLookupfield(fieldnum(BankLCExportLine, LineNum));
        sysReferenceTableLookup.addLookupfield(fieldnum(BankLCExportLine, ShipmentAmountCur));
        sysReferenceTableLookup.addLookupfield(fieldnum(BankLCExportLine, ActualMaturityDateTime));
        sysReferenceTableLookup.addLookupfield(fieldnum(BankLCExportLine, ExportShipmentStatus));

        return sysReferenceTableLookup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createSysTableLookup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the table lookup
    /// </summary>
    /// <param name="_control">
    ///    The form control to perform the lookup.
    /// </param>
    /// <returns>
    ///    The created <c>SysTableLookup</c> object.
    /// </returns>
    protected SysTableLookup createSysTableLookup(FormIntControl _control)
    {
        SysTableLookup sysTableLookup;

        sysTableLookup = SysTableLookup::newParameters(tablenum(BankLCExportLine), _control);
        sysTableLookup.addLookupfield(fieldnum(BankLCExportLine, LineNum), true);
        sysTableLookup.addLookupfield(fieldnum(BankLCExportLine, ShipmentAmountCur));
        sysTableLookup.addLookupfield(fieldnum(BankLCExportLine, ActualMaturityDateTime));
        sysTableLookup.addLookupfield(fieldnum(BankLCExportLine, ExportShipmentStatus));

        return sysTableLookup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>filterRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Filters the returned record based on the status.
    /// </summary>
    /// <param name="_bankLCExportLine">
    ///    The record to filter.
    /// </param>
    /// <returns>
    ///    The returned record; otherwise, an empty record.
    /// </returns>
    protected BankLCExportLine filterRecord(BankLCExportLine _bankLCExportLine)
    {
        BankLCExportLine ret;

        ret = _bankLCExportLine;

        if (ret.RecId != 0
            && (isAllowOpened
                || isAllowUpdatedPackingSlip))
        {
            switch (ret.ExportShipmentStatus)
            {
                case BankLCExportShipmentStatusType::Open :
                    if (!isAllowOpened)
                    {
                        ret = null;
                    }
                    break;
                case BankLCExportShipmentStatusType::Shipped :
                    if (!isAllowUpdatedPackingSlip)
                    {
                        ret = null;
                    }
                    break;
                default :
                    ret = null;
                    break;
            }
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefForms</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the reference jumper form.
    /// </summary>
    /// <returns>
    ///    The created form arguments.
    /// </returns>
    protected Args getJumpRefForms()
    {
        Args    args;

        args = new Args(formstr(BankLCExport));

        args.menuItemName(menuItemDisplayStr(BankLCExport));

        if (bankLCRecId != 0)
        {
            args.caller(this);
            args.record(BankLCExport::find(bankLCRecId).getSalesTable());
        }

        return args;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the lookup query.
    /// </summary>
    /// <returns>
    ///    The query.
    /// </returns>
    protected Query getQuery()
    {
        Query                   query;
        QueryBuildDataSource    queryBuildDataSource;
        QueryBuildRange         queryBuildRange;

        query = new Query();
        queryBuildDataSource = query.addDataSource(tablenum(BankLCExportLine));

        queryBuildRange = queryBuildDataSource.addRange(fieldnum(BankLCExportLine, BankLC));
        queryBuildRange.value(queryValue(bankLCRecId));

        if (isAllowOpened)
        {
            queryBuildRange = queryBuildDataSource.addRange(fieldnum(BankLCExportLine, ExportShipmentStatus));
            queryBuildRange.value(queryValue(BankLCExportShipmentStatusType::Open));
        }

        if (isAllowUpdatedPackingSlip)
        {
            queryBuildRange = queryBuildDataSource.addRange(fieldnum(BankLCExportLine, ExportShipmentStatus));
            queryBuildRange.value(queryValue(BankLCExportShipmentStatusType::Shipped));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolveReference</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Performs the resolve reference.
    /// </summary>
    /// <param name="_formReferenceControl">
    ///    The form control to request resolve reference.
    /// </param>
    /// <returns>
    ///    The selected record.
    /// </returns>
    public Common resolveReference(FormReferenceGroupControl _formReferenceControl)
    {
        BankLCExportLine bankLCExportLine;

        bankLCExportLine = BankLCExportLine::find(super(_formReferenceControl).RecId);

        return this.filterRecord(bankLCExportLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Constructs an instance of the <c>BankLCExportLineNumController</c> class.
    /// </summary>
    /// <returns>
    ///    The instance of the <c>BankLCExportLineNumController</c> class.
    /// </returns>
    public static BankLCExportLineNumController construct()
    {
        BankLCExportLineNumController bankLCExportLineNumController;

        bankLCExportLineNumController = new BankLCExportLineNumController();
        return bankLCExportLineNumController;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>