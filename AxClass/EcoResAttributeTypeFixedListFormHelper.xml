<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResAttributeTypeFixedListFormHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>EcoResAttributeTypeFixedListFormHelper</c> class is used to start the translation list form
/// for a fixed list of text values of an attribute type.
/// </summary>
class EcoResAttributeTypeFixedListFormHelper
{
    recId                attributeTypeRecId;
    SysTranslationHelper sysTranslationHelper;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>constructSysTranslationHelper</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>SysTranslationHelper</c> class.
    /// </summary>
    /// <param name="_args">
    /// The caller arguments.
    /// </param>
    protected void constructSysTranslationHelper(Args _args)
    {
        Common               mainRecord = _args.record();
        tableId              mainTableId = _args.dataset();

        if (_args.record() is EcoResEnumerationAttributeTypeValue)
        {
            EcoResEnumerationAttributeTypeValue ecoResEnumerationAttributeTypeValue = _args.record();
            attributeTypeRecId = ecoResEnumerationAttributeTypeValue.AttributeType;

            sysTranslationHelper = new SysTranslationHelper(
                    tableNum(EcoResTextValue),
                    tableNum(EcoResTextValueTranslation)
                );

            sysTranslationHelper.setMainTableNaturalKeyFieldId(fieldNum(EcoResTextValue, TextValue));
            sysTranslationHelper.parmLookupFieldId(fieldNum(EcoResTextValue, RecId));
            sysTranslationHelper.parmLookupFieldValue(int642Str(ecoResEnumerationAttributeTypeValue.Value));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches the translation list form by using the given data.
    /// </summary>
    /// <param name="_args">
    /// The input arguments.
    /// </param>
    protected void run(Args _args)
    {
        Object caller = _args.caller();

        this.constructSysTranslationHelper(_args);
        this.setBaseTableQuery();

        if (caller)
        {
            sysTranslationHelper.launchTranslationListForm();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setBaseTableQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the base table query on the <c>SysTranslationHelper</c> object.
    /// </summary>
    protected void setBaseTableQuery()
    {
        Query baseTableQuery = sysTranslationHelper.parmSystemLanguageBaseTableQuery();

        // ****************************************
        // EcoResTextValue XV
        //   JOIN EcoResEnumerationAttributeTypeValue EATV
        //     ON XV.RecId = EATV.Value
        //       WHERE EATV.AttributeType = mainRecId
        // (with above WHERE, there is no need to join below:
        //     JOIN EcoResAttributeType AT
        //       ON AT.RecId = EATV.AttributeType
        // )
        // ****************************************
        QueryBuildDataSource qbdsTextValue = baseTableQuery.dataSourceTable(tableNum(EcoResTextValue));
        QueryBuildDataSource qbdsAttrTypeEnumValue = qbdsTextValue.addDataSource(
            tableNum(EcoResEnumerationAttributeTypeValue),
            tableStr(EcoResEnumerationAttributeTypeValue)
        );
        qbdsAttrTypeEnumValue.relations(true);
        qbdsAttrTypeEnumValue.addLink(
            fieldNum(EcoResTextValue, RecId),
            fieldNum(EcoResEnumerationAttributeTypeValue, Value)
        );
        qbdsAttrTypeEnumValue.joinMode(JoinMode::InnerJoin);
        qbdsAttrTypeEnumValue.fetchMode(QueryFetchMode::One2One);

        qbdsAttrTypeEnumValue.addRange(fieldNum(EcoResEnumerationAttributeTypeValue, AttributeType))
            .value(queryValue(attributeTypeRecId));

        sysTranslationHelper.parmSystemLanguageBaseTableQuery(baseTableQuery);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        EcoResAttributeTypeFixedListFormHelper  formHelper = new EcoResAttributeTypeFixedListFormHelper();
        formHelper.run(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
