<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBGIAReportController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// A controller class for Fiscal book GIA report.
/// </summary>
[
    FBTaxStatement_BRAttribute("@FBK115", menuitemDisplayStr(FBGiaSpFileExporter_BR))
]
    public class FBGIAReportController extends FBTaxStatementController_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        FBGIAReportController controller;
        
        controller = new FBGIAReportController(
            classStr(FBGIAReportController),
            methodStr(FBGIAReportController, runER),
            SysOperationExecutionMode::Synchronous);

        FBGIAReportContract contract = controller.getBaseDataContract();

        contract.parmBookingPeriod(_args.record());

        controller.parmArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Microsoft internal use only.
    /// </summary>
    /// <param name="_args">An instance of the <c>Args</c> class.</param>
    /// <returns>An instance of the <c>Args</c> class.</returns>
    public Args parmArgs(Args _args = args)
    {
        args = _args;
        return args;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultCaption</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets caption of the dialog.
    /// </summary>
    /// <returns>Caption of the dialog</returns>
    protected ClassDescription defaultCaption()
    {
        ClassDescription batchDescription;

        batchDescription = "@FBK115";

        return batchDescription;
    }

]]></Source>
			</Method>
			<Method>
				<Name>afterOperation</Name>
				<Source><![CDATA[
    protected void afterOperation(SysOperationExecutionMode _executionMode, AifAsyncResult _asyncResult)
    {
		//not applicable logic in base class
    }

]]></Source>
			</Method>
			<Method>
				<Name>runER</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs chosen format with filters by booking period from contract.
    /// </summary>
    /// <param name="_contract">An instance of the <c>FBGIAReportContract</c> class.</param>
    public void runER(FBGIAReportContract _contract)
    {
        try
        {
            FBBookingPeriod_BR period = _contract.parmBookingPeriod();

            ERObjectsFactory::createFormatMappingRunByFormatMappingId(_contract.parmFormatMapping(),
                strFmt('%1 %2 %3 %4',"@FBK115", period.FiscalEstablishmentId, period.PeriodMonth, period.PeriodYear), true)
                .withFileDestination(ERObjectsFactory::createFileDestinationBrowser())
                .withParameter(new ERModelDefinitionDatabaseContext()
                    .addRange(tableNum(FBBookingPeriod_BR), fieldNum(FBBookingPeriod_BR, PeriodYear), int2Str(period.PeriodYear))
                    .addRange(tableNum(FBBookingPeriod_BR), fieldNum(FBBookingPeriod_BR, PeriodMonth), int2Str(period.PeriodMonth))
                    .addRange(tableNum(FBBookingPeriod_BR), fieldNum(FBBookingPeriod_BR, FiscalEstablishmentDataArea), period.FiscalEstablishmentDataArea)
                    .addRange(tableNum(FBBookingPeriod_BR), fieldNum(FBBookingPeriod_BR, FiscalEstablishmentID), period.FiscalEstablishmentId)
                ).run();
        }
        catch
        {
            error("@SYP4861341");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>