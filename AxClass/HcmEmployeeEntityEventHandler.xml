<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmEmployeeEntityEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Contains event handlers for the HcmEmployeeEntity entity.
/// </summary>
class HcmEmployeeEntityEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmEmployeeEntity_onMappedEntityToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity onMappedEntityToDataSource event handler for HcmEmployeeEntity extension
    /// </summary>
    /// <param name="_sender">HcmEmployee Entity raising the event</param>
    /// <param name="_eventArgs">The args of this event</param>
    [DataEventHandler(tableStr(HcmEmployeeEntity), DataEventType::MappedEntityToDataSource)]
    public static void HcmEmployeeEntity_onMappedEntityToDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();
        HcmEmployeeEntity employeeEntity = _sender as HcmEmployeeEntity;

        if (dataSourceCtx.name() == dataentitydatasourcestr(HcmEmployeeEntity, WorkCalendarEmployment))
        {
            employeeEntity.skipDataSourceValidateField(fieldNum(HcmEmployeeEntity, CalendarId), true);
            employeeEntity.skipDataSourceValidateField(fieldNum(HcmEmployeeEntity, CalendarDataAreaId), true);

            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();
            workCalendarEmployment.CalendarDataAreaId = employeeEntity.EmploymentLegalEntityId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmployeeEntity_ValidatingField</Name>
				<Source><![CDATA[
    /// <summary>
    /// Event Handler for validate Field to return if company context should be changed while validating CalendarId field.
    /// </summary>
    /// <param name="_contractorEntity"><c>HcmEmployeeEntity</c> raising the event</param>
    /// <param name="_fieldIdToCheck">FieldId to validate</param>
    /// <param name="_changeCompanyContext">The EventHandlerResult the handler will use to return if company context should be changed before performing validation</param>
    [SubscribesTo(tableStr(HcmEmployeeEntity), delegateStr(HcmEmployeeEntity, ValidatingField))]
    public static void HcmEmployeeEntity_ValidatingField(Common _employeeEntity, FieldId _fieldIdToCheck, EventHandlerResult _changeCompanyContext)
    {
        HcmEmployeeEntity employeeEntity = _employeeEntity as HcmEmployeeEntity;

        if (_fieldIdToCheck == fieldNum(HcmEmployeeEntity, CalendarId)
             && employeeEntity.CalendarId != '')
        {
            _changeCompanyContext.booleanResult(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmployeeEntity_onInsertingEntityDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entity InsertingEntityDataSource event handler for <c>HcmEmployeeEntity</c> extension.
    /// </summary>
    /// <param name="_sender">The <c>HcmEmployeeEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The event args.</param>
    [DataEventHandler(tableStr(HcmEmployeeEntity), DataEventType::InsertingEntityDataSource)]
    public static void HcmEmployeeEntity_onInsertingEntityDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (dataSourceCtx.name() == dataEntityDataSourceStr(HcmEmployeeEntity, WorkCalendarEmployment))
        {
            WorkCalendarEmployment workCalendarEmployment = dataSourceCtx.getBuffer();

            if (workCalendarEmployment.Employment)
            {
                //
                // Conflict detection to resolve inserts into updates. This is required since <c>WorkCalendarEmployment</c> is 1-1 joined to <c>HcmEmployment</c>,
                // and the entity framework only resolves inserts into updates automatically for datasources in the 1 side on a 1-many relationship.
                //
                WorkCalendarEmployment existingRecord = WorkCalendarEmployment::findByEmploymentRecId(workCalendarEmployment.Employment, true);

                if (existingRecord)
                {
                    // Merge user modified fields into the existing record (excluding system fields).
                    buf2Buf(workCalendarEmployment, existingRecord);

                    // Set the datasource context buffer to the final merged record.
                    dataSourceCtx.setBuffer(existingRecord);
                    dataSourceCtx.setDatabaseOperation(DataEntityDatabaseOperation::Update);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>