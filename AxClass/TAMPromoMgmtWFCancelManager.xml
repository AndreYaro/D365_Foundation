<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TAMPromoMgmtWFCancelManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMPromoMgmtWFCancelManager</c> class handles cancelling
/// a TAMPromotionsManagement workflow.
/// </summary>
class TAMPromoMgmtWFCancelManager extends WorkflowCancelManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateDocument</Name>
				<Source><![CDATA[
    public void updateDocument(Args _args)
    {
        TAMTradePromotion tamTradePromotion;

        if (_args.record() is TAMTradePromotion)
        {
            tamTradePromotion = _args.record() as TAMTradePromotion;
            tamTradePromotion.reread();
        }
        else if (_args.record() is WorkflowTrackingStatusTable)
        {
            WorkflowTrackingStatusTable workflowTrackingStatusTable = _args.record() as WorkflowTrackingStatusTable;
            workflowTrackingStatusTable.reread();

            if (workflowTrackingStatusTable.ContextTableId == tableNum(TAMTradePromotion))
            {
                tamTradePromotion = TAMTradePromotion::findByRecId(workflowTrackingStatusTable.ContextRecId);
            }
        }

        if (tamTradePromotion)
        {
            ttsbegin;
            tamTradePromotion.selectForUpdate(true);
            tamTradePromotion.WFApprovalStatus = TAMPromoMgmtApprovalStatus::NotSubmitted;
            tamTradePromotion.update();
            ttscommit;

            FormDataSource tamTradePromotion_ds = FormDataUtil::getFormDataSource(tamTradePromotion);

            if (tamTradePromotion_ds)
            {
                tamTradePromotion_ds.research(true);
            }
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TAMPromoMgmtWFCancelManager construct()
    {
        return new TAMPromoMgmtWFCancelManager();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        TAMPromoMgmtWFCancelManager  workflowCancelManager = TAMPromoMgmtWFCancelManager::construct();
        workflowCancelManager.run(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>