<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PrintMgmtNode_VendTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PrintMgmtNode_VendTable</c> is responsible for managing print management settings at the
///    <c>VendTable</c> account-level in the accounts payable module.
/// </summary>
public class PrintMgmtNode_VendTable extends PrintMgmtNode
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDisplayCaptionImplementation</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the display caption for the node.
    /// </summary>
    /// <param name="_tableBuffer">
    ///    The table buffer that is referenced by the node.
    /// </param>
    /// <returns>
    ///    The display caption used to identify the reference of the node.
    /// </returns>
    /// <remarks>
    ///    The caption typically includes a prefix like module, account, or transaction, followed by the
    ///    caption property of the <paramref name="_tableBuffer" /> parameter.
    /// </remarks>
    protected str getDisplayCaptionImplementation(Common _tableBuffer)
    {
        VendTable vendTable = _tableBuffer;

        return(strfmt("@SYS108943", strfmt('%1, %2', vendTable.AccountNum, vendTable.name())));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentTypes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a list of valid document types for the node.
    /// </summary>
    /// <returns>
    ///    A list of valid document types for the node.
    /// </returns>
    /// <remarks>
    /// The results can vary based on what is configured in the application because configuration keys are
    ///    used to determine this list.
    /// </remarks>
    public List getDocumentTypes()
    {
        // <GEERU>
        #ISOCountryRegionCodes
        // </GEERU>
        List docTypes;

        docTypes = new List(Types::Enum);

        if (isConfigurationkeyEnabled(configurationkeynum(LogisticsBasic)))
        {
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderInvoice);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderPackingSlip);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderReceiptsList);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderRequisition);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderConfirmationRequest);
            docTypes.addEnd(PrintMgmtDocumentType::PurchRFQ);
            docTypes.addEnd(PrintMgmtDocumentType::PurchRFQAccept);
            docTypes.addEnd(PrintMgmtDocumentType::PurchRFQReject);
            docTypes.addEnd(PrintMgmtDocumentType::PurchRFQReturn);
        }

        // <GEERU>
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([ #isoRU ]))
        {
            docTypes.addEnd(PrintMgmtDocumentType::PurchOrderInvoice4Paym_RU);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderFacture);
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseOrderBailee);
        }

        // </GEERU>
        if (isConfigurationkeyEnabled(configurationkeynum(TradeBlanketOrder)))
        {
            docTypes.addEnd(PrintMgmtDocumentType::PurchAgreementConfirmation);
        }

        // <GBR>
        if (BrazilParameters::isEnabled())
        {
            docTypes.addEnd(PrintMgmtDocumentType::ComplementaryInvoice_BR);
        }
        // </GBR>

        // <GEEU>
        if (isConfigurationkeyEnabled(configurationKeyNum(CzCustVendAdvanceInvoices)))
        {
            docTypes.addEnd(PrintMgmtDocumentType::PurchaseAdvanceInvoice);
        }
        // </GEEU>

        if (isConfigurationkeyEnabled(configurationKeyNum(Bank)))
        {
            docTypes.addEnd(PrintMgmtDocumentType::VendPaymAdvice);
        }

        return docTypes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getIconImageResNum</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the resource ID that is used to display the icon that represents this node.
    /// </summary>
    /// <returns>
    ///    The resource ID that is used to display the icon that represents this node.
    /// </returns>
    /// <remarks>
    ///    The <c>resAppl</c> macro contains the resource ID definitions for icon images. This method returns
    ///    one of those resource IDs. If additional resource IDs are used, they should also be added to the
    ///    build method of the <c>ImageListAppl_PrintMgmt</c> class.
    /// </remarks>
    public int getIconImageResNum()
    {
        #resAppl

        return #ImagePrintManagementAccount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNodeType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>PrintMgmtNodeType</c> enumeration value of the node.
    /// </summary>
    /// <returns>
    /// The <c>PrintMgmtNodeType</c> enumeration value of the node.
    /// </returns>
    /// <remarks>
    /// New node types should be defined in the <c>PrintMgmtNodeType</c> enumeration and returned through
    /// this method.
    /// </remarks>
    public PrintMgmtNodeType getNodeType()
    {
        return PrintMgmtNodeType::VendTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReferencedTableId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the table ID that is referenced by this node.
    /// </summary>
    /// <returns>
    /// The <c>VendTable</c> table ID that is referenced by this node.
    /// </returns>
    /// <remarks>
    /// Module-level settings are an example of a node that does not actually reference a table in the
    /// system. In this case, you should return a value of 0. If the node references the <c>CustTable</c>
    /// table, it would mean that Print Management settings are related to records in the <c>CustTable</c>
    /// table. In this case, you should return the table ID of the <c>CustTable</c> table.
    /// </remarks>
    public RefTableId getReferencedTableId()
    {
        return tablenum(VendTable);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>