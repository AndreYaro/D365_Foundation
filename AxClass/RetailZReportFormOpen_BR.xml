<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailZReportFormOpen_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailZReportFormOpen_BR</c> class applies the form extensions to <c>RetailZReport_BR</c> form.
/// </summary>
class RetailZReportFormOpen_BR
{
    Args args;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>applyExtensionsBeforeRunningForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Apply the implemented extensions prior to opening the Z Report form.
    /// </summary>
    /// <param name="_form">
    ///     A reference to the Z Report <c>Form</c> instance.
    /// </param>
    protected void applyExtensionsBeforeRunningForm(Form _form)
    {
        RetailZReportExtensionsProvider_BR  provider;
        List                                classInstances;
        ListEnumerator                      enumerator;

        // Find all providers
        classInstances      = SysExtensionAppClassFactory::getClassInstanceListFromSysAttribute(classStr(RetailZReportExtensionsProvider_BR), new RetailZReportExtensionsProviderAttrib_BR());

        enumerator = classInstances.getEnumerator();
        while (enumerator.moveNext())
        {
            provider = enumerator.current();
            if (provider && provider.isEnabled())
            {
                provider.configureFormDataSources(_form);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Creates an instance of <c>RetailZReportFormOpen_BR</c> class.
    /// </summary>
    /// <param name="_args">
    ///     An instance of <c>Args</c> containing the parameters to be used to open the form.
    /// </param>
    protected void new(Args _args)
    {
        args = _args;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///     COnfigures the Z report form and then opens it.
    /// </summary>
    protected void run()
    {
        Form    form;
        FormRun formRun;

        form = new Form(formStr(RetailZReport_BR));

        this.applyExtensionsBeforeRunningForm(form);

        args.object(form);

        formRun = classFactory.formRunClass(args);

        formRun.run();
        formRun.detach();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static RetailZReportFormOpen_BR construct(Args _args)
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR10001, funcName());

        return new RetailZReportFormOpen_BR(_args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args _args)
    {
        RetailZReportFormOpen_BR zReportFormOpen = RetailZReportFormOpen_BR::construct(_args);
        zReportFormOpen.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>