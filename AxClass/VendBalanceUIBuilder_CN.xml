<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendBalanceUIBuilder_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The user interface builder of Vendor balance report.
/// </summary>
class VendBalanceUIBuilder_CN extends SysOperationAutomaticUIBuilder
{
    DialogField dimensionAttributesField;

    const int ColumnNumber = 2;
    const str ProfileUIGroup = 'ProfileUIGroup';
    const str VendGroupUIGroup = 'VendGroupUIGroup';
    const str BuyerGroupUIGroup = 'BuyerGroupUIGroup';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Action to build dialog UI.
    /// </summary>
    public void build()
    {
        super();

        this.buildGroupWithColumns(ProfileUIGroup);
        this.buildGroupWithColumns(VendGroupUIGroup);
        this.buildGroupWithColumns(BuyerGroupUIGroup);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGroupWithColumns</Name>
				<Source><![CDATA[
    /// <summary>
    /// Build UI group for controls in multi columns.
    /// </summary>
    /// <param name = "_groupName">UI group name</param>
    private void buildGroupWithColumns(str _groupName)
    {
        FormBuildGroupControl   formBuildGroupControl;

        formBuildGroupControl = this.dialog().formBuildDesign().control(_groupName);
        formBuildGroupControl.columns(ColumnNumber);
        formBuildGroupControl.frameType(FormFrameType::None);

        //sets MainGrp back to main dialog form group.
        this.dialog().mainFormGroup(formBuildGroupControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Action after build of the dialog UI.
    /// </summary>
    public void postBuild()
    {
        DialogField formatMappingControl;
        VendBalanceContract_CN contract;

        super();

        contract = this.dataContractObject() as VendBalanceContract_CN;

        formatMappingControl = this.bindInfo().getDialogField(contract, methodStr(VendBalanceContract_CN, parmERBinding));
        formatMappingControl.registerOverrideMethod(methodStr(FormReferenceControl, lookupReference), methodStr(VendBalanceUIBuilder_CN, formatMappingLookup), this);

        dimensionAttributesField = this.bindInfo().getDialogField(contract, methodStr(VendBalanceContract_CN, parmLocalizedAttributeNames));
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Action after run the dialog.
    /// </summary>
    public void postRun()
    {
        VendBalanceContract_CN contract = this.dataContractObject() as VendBalanceContract_CN;;
        Object control = dimensionAttributesField.control();

        if (dimensionAttributesField)
        {
            dimensionAttributesField.lookupButton(FormLookupButton::Always);
            ERFinancialDimensionsHelper::lookupDimensionAttributes(
                this.dialog().dialogForm().formRun(),
                dimensionAttributesField.control(),
                !contract.getAskForMainAccount(),
                contract.getDimSelectType());
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatMappinglookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs the lookup form for format mapping.
    /// </summary>
    /// <param name = "_formControl"></param>
    /// <returns></returns>
    private void formatMappinglookup(FormReferenceControl _formControl)
    {
        ERObjectsFactory::createFormatMappingTableLookupForControlAndModel(_formControl, ReportConstants_CN::getCustVendBalanceReportModelName()).performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>