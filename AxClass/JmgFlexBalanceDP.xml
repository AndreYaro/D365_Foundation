<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgFlexBalanceDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    Fetches records for the <c>JmgPayAgreement</c> report.
/// </summary>
[SRSReportQueryAttribute(querystr(JmgFlexBalance))]
class JmgFlexBalanceDP extends SRSReportDataProviderBase
{
    JmgTmpFlexBalance   tmpFlexBalance;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getJmgTmpFlexBalance</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method is used to query the temporary table and returns a data table.
    /// </summary>
    /// <returns>
    /// The required report data from the temporary table.
    /// </returns>
    [SRSReportDataSetAttribute(tablestr(JmgTmpFlexBalance))]
    public JmgTmpFlexBalance getJmgTmpFlexBalance()
    {
        select tmpFlexBalance;
        return tmpFlexBalance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the report business logic.
    /// </summary>
    public void processReport()
    {
        QueryRun                queryRun         = new SysQueryRun(this.parmQuery());
        HcmWorker               hcmWorker;
        JmgStampJournalTable    jmgStampJournalTable;

        while (queryRun.next())
        {
            hcmWorker = queryRun.get(tablenum(HcmWorker));
            jmgStampJournalTable = queryRun.get(tablenum(JmgStampJournalTable));

            if (   jmgStampJournalTable.PayFlexAddSeconds
                || jmgStampJournalTable.PayFlexSubSeconds
                || jmgStampJournalTable.PayFlexCorrectionSeconds
                || jmgStampJournalTable.PayFlexBalanceSeconds)
            {
                // Make lines
                tmpFlexBalance.PersonnelNumber      = hcmWorker.PersonnelNumber;
                tmpFlexBalance.ProfileDate          = jmgStampJournalTable.ProfileDate;
                tmpFlexBalance.PayFlexAdd           = JmgProfileSeconds::formattedSeconds(jmgStampJournalTable.PayFlexAddSeconds,JmgSecondPresentationEnum::Centitime);
                tmpFlexBalance.PayFlexBalance       = JmgProfileSeconds::formattedSeconds(jmgStampJournalTable.PayFlexBalanceSeconds,JmgSecondPresentationEnum::Centitime);
                tmpFlexBalance.PayFlexCorrection    = JmgProfileSeconds::formattedSeconds(jmgStampJournalTable.PayFlexCorrectionSeconds,JmgSecondPresentationEnum::Centitime);
                tmpFlexBalance.PayFlexSub           = JmgProfileSeconds::formattedSeconds(jmgStampJournalTable.PayFlexSubSeconds,JmgSecondPresentationEnum::Centitime);
                tmpFlexBalance.insert();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
