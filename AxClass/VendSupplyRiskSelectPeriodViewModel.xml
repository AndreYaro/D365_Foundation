<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendSupplyRiskSelectPeriodViewModel</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>VendSupplyRiskSelectPeriodViewModel</c> is a form view model for selecting a date inverval code, from date and to date.
/// </summary>
internal final class VendSupplyRiskSelectPeriodViewModel implements VendSupplyRiskISelectPeriodStorage
{
    private FormRun                         parentForm;
    private VendSupplyRiskSelectPeriodState currentPeriodState;
    private boolean                         persistPeriodState;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmParentForm</Name>
				<Source><![CDATA[
    private FormRun parmParentForm(FormRun _parentForm = parentForm)
    {
        parentForm = _parentForm;
        return parentForm;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setPeriod</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void setPeriod(VendSupplyRiskSelectPeriodState _newState)
    {
        currentPeriodState = _newState;

        if (persistPeriodState)
        {
            VendSupplyRiskSelectPeriodStateLastValueContainer::saveInLastValue(currentPeriodState, parentForm.name());
        }

        this.onPeriodChange(currentPeriodState);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPeriod</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public VendSupplyRiskSelectPeriodState getPeriod()
    {
        return currentPeriodState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the form.
    /// </summary>
    internal void init()
    {
        if (persistPeriodState)
        {
            var periodState = VendSupplyRiskSelectPeriodStateLastValueContainer::readStateFromLastValue(parentForm.name());
            if (periodState)
            {
                this.setPeriod(periodState);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs new instance of the <c>VendSupplyRiskSelectPeriodViewModel</c> using provided parameters.
    /// </summary>
    /// <param name="_parentForm">The <c>FormRun</c> class instance.</param>
    /// <returns>The <c>VendSupplyRiskSelectPeriodViewModel</c> class instance.</returns>
    internal static VendSupplyRiskSelectPeriodViewModel newFromParameters(FormRun _parentForm)
    {
        VendSupplyRiskSelectPeriodViewModel vendSupplyRiskSelectPeriodViewModel = new VendSupplyRiskSelectPeriodViewModel();

        vendSupplyRiskSelectPeriodViewModel.parmParentForm(_parentForm);
        vendSupplyRiskSelectPeriodViewModel.persistPeriodState = true;

        return vendSupplyRiskSelectPeriodViewModel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromPeriodState</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs new instance of the <c>VendSupplyRiskSelectPeriodViewModel</c> using provided parameters.
    /// </summary>
    /// <param name="_parentForm">The <c>FormRun</c> class instance.</param>
    /// <param name="_periodState">The period state class instance used for initialization.</param>
    /// <returns>The <c>VendSupplyRiskSelectPeriodViewModel</c> class instance.</returns>
    internal static VendSupplyRiskSelectPeriodViewModel newFromPeriodState(FormRun _parentForm, VendSupplyRiskSelectPeriodState _periodState)
    {
        VendSupplyRiskSelectPeriodViewModel vendSupplyRiskSelectPeriodViewModel = new VendSupplyRiskSelectPeriodViewModel();

        vendSupplyRiskSelectPeriodViewModel.parmParentForm(_parentForm);
        vendSupplyRiskSelectPeriodViewModel.persistPeriodState = _periodState == null;

        if (_periodState)
        {
            vendSupplyRiskSelectPeriodViewModel.currentPeriodState = VendSupplyRiskSelectPeriodState::create(_periodState.pack());
        }

        return vendSupplyRiskSelectPeriodViewModel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>onPeriodChange</Name>
				<Source><![CDATA[
    delegate void onPeriodChange(VendSupplyRiskSelectPeriodState _state)
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>