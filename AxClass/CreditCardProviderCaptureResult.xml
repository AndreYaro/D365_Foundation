<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CreditCardProviderCaptureResult</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CreditCardProviderCaptureResult</c> stores result from a credit card capture operation.
/// </summary>
class CreditCardProviderCaptureResult extends CreditCardProviderResult
{
    CreditCardCaptureResult creditCardCaptureResult;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the object from an array of properties.
    /// </summary>
    /// <param name="_properties">
    /// An array of properties.
    /// </param>
    protected void init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty[] _properties)
    {
        str ns = Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.GenericNamespace::get_CaptureResponse();
        CreditCardPaymentProperty property;

        super(_properties);

        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_UniqueCardId());
        this.parmCreditCardUniqueCardId(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_Last4Digits());
        this.parmCreditCardNumber(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_CardType());
        this.parmCreditCardTypeName(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_ProviderTransactionId());
        this.parmAuthorization(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_CaptureResult());
        this.parmCaptureResult(property ? CreditCardProviderCaptureResult::getCaptureResult(property.parmStringValue()) : CreditCardCaptureResult::None);
        this.parmAvsResult(CreditCardAddressVerificationResult::None);
        this.parmAvsDetail(CreditCardMSAVSDetailCode::None);
        this.parmCvcResult(CreditCardMSCVCResultCode::Unknown);
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_PaymentTrackingId());
        this.parmMCRPaymentTrackingId(property ? property.parmStringValue() : '');

        if (EnhancedWalletSupportAndPaymentsImprovementFeature::isEnabled())
        {
            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_ApprovalCode());
            this.parmApprovalCode(property ? property.parmStringValue() : '');

            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_PaymentReference());
            this.parmProcessorReference(property ? property.parmStringValue() : '');

            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.CaptureResponseProperties::get_PaymentMethodName());
            this.parmProcessorPayment(property ? property.parmStringValue() : '');
        }

        this.parmResultOK(this.parmCaptureResult() == CreditCardCaptureResult::Success);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCaptureResult</Name>
				<Source><![CDATA[
    public CreditCardCaptureResult parmCaptureResult(CreditCardCaptureResult _value = creditCardCaptureResult)
    {
        creditCardCaptureResult = _value;
        return creditCardCaptureResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromResponse</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a response object.
    /// </summary>
    /// <param name="_response">
    /// A response.
    /// </param>
    /// <param name="_amountCur">
    /// The amount captured.
    /// </param>
    /// <param name="_authorization">
    /// An authorization.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderCaptureResult constructFromResponse(Microsoft.Dynamics.Retail.PaymentSDK.Portable.Response _response, AmountCur _amountCur, CreditCardAuthorization _authorization)
    {
        CreditCardProviderCaptureResult result = new CreditCardProviderCaptureResult();
        result.init(_response.get_Properties());
        result.parmTransactionAmount(_amountCur);
        result.parmAuthorization(_authorization);
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromXmlString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a XML string.
    /// </summary>
    /// <param name="_xmlString">
    /// A XML string.
    /// </param>
    /// <param name="_amountCur">
    /// The amount captured.
    /// </param>
    /// <param name="_authorization">
    /// An authorization.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderCaptureResult constructFromXmlString(str _xmlString, AmountCur _amountCur, CreditCardAuthorization _authorization)
    {
        CreditCardProviderCaptureResult result = new CreditCardProviderCaptureResult();
        result.init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty::ConvertXMLToPropertyArray(_xmlString));
        result.parmTransactionAmount(_amountCur);
        result.parmAuthorization(_authorization);
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaptureResult</Name>
				<Source><![CDATA[
    private static CreditCardCaptureResult getCaptureResult(str _code)
    {
        str toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.CaptureResult _value)
        {
            str s = System.Convert::ToString(_value);

            return s;
        }

        if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.CaptureResult::None))
            return CreditCardCaptureResult::None;
        else if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.CaptureResult::Success))
            return CreditCardCaptureResult::Success;
        else if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.CaptureResult::Failure))
            return CreditCardCaptureResult::Failure;
        // ********************************************************
        // TO DO: remove this when Martin maps it to success
        // ********************************************************
        else if (_code == 'QueuedForBatch')
            return CreditCardCaptureResult::Success;

        throw error(Error::wrongUseOfFunction(funcName()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>