<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SplitPaymentMandatoryRuleEngine_VendInvoiceApprovalJournal</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SplitPaymentMandatoryRuleEngine_VendInvoiceApprovalJournal</c> class is extended class for updating paymMode
/// in Invoice approval journal.
/// </summary>
internal class SplitPaymentMandatoryRuleEngine_VendInvoiceApprovalJournal extends SplitPaymentMandatoryRuleEngine
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>findRuleForOrder</Name>
				<Source><![CDATA[
    protected SplitPaymentMandatoryRule_PL findRuleForOrder(Amount _totalAmount)
    {
        SplitPaymentMandatoryRule_PL rule;
        SplitPaymentMandatoryRule_PL voluntaryRule;
        LedgerJournalTrans ledgerJournalTransVendTransId;

        while select VendTransId, TaxItemGroup, CurrencyCode from ledgerJournalTransVendTransId
            where ledgerJournalTransVendTransId.JournalNum == this.getLedgerJournalTrans().JournalNum
                && ledgerJournalTransVendTransId.VendTransId == this.getLedgerJournalTrans().VendTransId
        {
            rule = this.doFindRuleForOrder(_totalAmount, ledgerJournalTransVendTransId);

            if (rule.VoluntarySplitPayment == NoYes::Yes)
            {
                voluntaryRule = rule;
            }

            if (rule && rule.VoluntarySplitPayment == NoYes::No)
            {
                break;
            }

            rule = null;
        }

        if (!rule && voluntaryRule)
        {
            rule = voluntaryRule;
        }

        return rule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doFindRuleForOrder</Name>
				<Source><![CDATA[
    private SplitPaymentMandatoryRule_PL doFindRuleForOrder(Amount _totalAmount, LedgerJournalTrans _ledgerJournalTrans)
    {
        PurchLine purchLine;
        SplitPaymentMandatoryRule_PL rule;
        SplitPaymentMandatoryRule_PL voluntaryRule;
        LedgerJournalTrans ledgerJournalTrans = _ledgerJournalTrans;
        IntrastatCountryRegionParameters intrastatCountryRegionParameters = IntrastatCountryRegionParameters::find(VendTable::find(VendTrans::find(ledgerJournalTrans.VendTransId).AccountNum).postalAddress().CountryRegionId);
        boolean isDomesticPartner = intrastatCountryRegionParameters && intrastatCountryRegionParameters.CountryRegionType == CountryRegionType::Domestic;

        SplitPaymentMandatoryItemGroupId_PL itemGroupId = SplitPaymentMandatoryItemGroup_PL::findGroupByItem(this.getDocumentType());

        if (ledgerJournalTrans.TaxItemGroup != '')
        {
            if (isDomesticPartner)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withTaxItemGroup(ledgerJournalTrans.TaxItemGroup)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::Domestic)
                    .withCurrencyCode(ledgerJournalTrans.CurrencyCode)
                    .withSearchByTaxItemGroup(true));
            }

            if (!rule)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::All)
                    .withCurrencyCode(ledgerJournalTrans.CurrencyCode)
                    .withTaxItemGroup(ledgerJournalTrans.TaxItemGroup)
                    .withSearchByTaxItemGroup(true));
            }

            if (rule.VoluntarySplitPayment == NoYes::Yes)
            {
                voluntaryRule = rule;
            }
        }

        if (!rule || rule.VoluntarySplitPayment == NoYes::Yes)
        {
            if (isDomesticPartner)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::Domestic)
                    .withCurrencyCode(ledgerJournalTrans.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }

            if (!rule)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::All)
                    .withCurrencyCode(ledgerJournalTrans.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }
        }

        if ((!rule || rule.VoluntarySplitPayment == NoYes::Yes) && voluntaryRule)
        {
            rule = voluntaryRule;
        }

        return rule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAmount</Name>
				<Source><![CDATA[
    protected Amount getAmount()
    {
        return (-1)*VendTrans::find(this.getLedgerJournalTrans().VendTransId).AmountCur;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLedgerJournalTrans</Name>
				<Source><![CDATA[
    private LedgerJournalTrans getLedgerJournalTrans()
    {
        return this.parmCommonTable() as LedgerJournalTrans;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentType</Name>
				<Source><![CDATA[
    protected SplitPaymentDocumentType_PL getDocumentType()
    {
        return SplitPaymentDocumentType_PL::Vendor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymMode</Name>
				<Source><![CDATA[
    protected PaymMode getCurrentPaymMode()
    {
        return this.getLedgerJournalTrans().PaymMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymModeSpec</Name>
				<Source><![CDATA[
    protected PaymSpec getCurrentPaymModeSpec()
    {
        return this.getLedgerJournalTrans().PaymSpec;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SplitPaymentMandatoryRuleEngine_VendInvoiceApprovalJournal construct()
    {
        return new SplitPaymentMandatoryRuleEngine_VendInvoiceApprovalJournal();
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isSplitPayment</Name>
				<Source><![CDATA[
    protected boolean isSplitPayment()
    {
        boolean ret;
        LedgerJournalTrans ledgerJournalTransVendTransId;

        while select PaymMode, PaymSpec from ledgerJournalTransVendTransId
            where ledgerJournalTransVendTransId.JournalNum == this.getLedgerJournalTrans().JournalNum
                && ledgerJournalTransVendTransId.VendTransId == this.getLedgerJournalTrans().VendTransId
        {
            ret = SplitPaymentMandatoryRuleEngine::getSplitPaymentFromPaymModeSpec(
                this.getDocumentType(),
                ledgerJournalTransVendTransId.PaymMode,
                ledgerJournalTransVendTransId.PaymSpec) == NoYes::Yes;

            if (ret)
            {
                break;
            }
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCommonTable</Name>
				<Source><![CDATA[
    protected void updateCommonTable(PaymMode _paymMode, PaymSpec _paymSpec)
    {
        LedgerJournalTrans ledgerJournalTransVendTransId;

        update_recordset ledgerJournalTransVendTransId
            setting PaymMode = _paymMode, 
                PaymSpec = _paymSpec
            where ledgerJournalTransVendTransId.JournalNum == this.getLedgerJournalTrans().JournalNum
                && ledgerJournalTransVendTransId.VendTransId == this.getLedgerJournalTrans().VendTransId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>