<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjAdvancedJournalAccountingDistributionProcessorExtension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[SourceDocumentEnumExtensionAttribute(enumStr(SourceDocument_ProjAdvancedJournal), SourceDocument_ProjAdvancedJournal::ProjAdvancedJournal)]
public class ProjAdvancedJournalAccountingDistributionProcessorExtension extends AccDistProcessorExtension
{
    AccountingDistributionRecId extendedPriceAccountingDistributionRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addingAccountingDistribution</Name>
				<Source><![CDATA[
    /// <summary>
    /// Keeps track of the extended price accounting distribution in order to set the ReferenceDistribution on the WorkInProcess record.
    /// </summary>
    /// <param name = "_accountingDistribution">The AccountingDistribution being added.</param>
    public void addingAccountingDistribution(AccountingDistribution _accountingDistribution)
    {
        if (_accountingDistribution.MonetaryAmount == MonetaryAmount::ExtendedPrice)
        {
            extendedPriceAccountingDistributionRecId = _accountingDistribution.RecId;
        }
        else if (_accountingDistribution.MonetaryAmount == MonetaryAmount::WorkInProcess)
        {
            _accountingDistribution.ReferenceDistribution = extendedPriceAccountingDistributionRecId;
            _accountingDistribution.ReferenceRole = AccountingDistributionReferenceRole::WorkInProcess;
        }

    }

]]></Source>
			</Method>
			<Method>
				<Name>subscribe</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the accounting distribution processor events.
    /// </summary>
    protected void subscribe()
    {
        if (isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            accountingDistributionProcessor.addingAccountingDistribution += eventhandler(this.addingAccountingDistribution);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>