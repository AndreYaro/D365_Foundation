<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendTransactionReversal</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

[ExportAttribute(identifierStr(Dynamics.AX.Application.TransactionReversalBase))]
internal final class VendTransactionReversal extends TransactionReversalBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>reverse</Name>
				<Source><![CDATA[
    internal boolean reverse(TransactionReversalExecutionDetail _reversalExecutionDetail, TransactionReversalContract _contract)
    {
        VendTrans vendTrans = VendTrans::findVoucherDate(_reversalExecutionDetail.SubledgerVoucher, _reversalExecutionDetail.TransactionDate);

        boolean isReversed = false;

        if (vendTrans)
        {
            TransactionReversal_Vend vendReversal = TransactionReversal_Vend::construct();
            vendReversal.parmReversalDate(_contract.parmReversalDate());
            vendReversal.parmContract(_contract);

            VendInvoiceJour vendInvoiceJour = vendTrans.vendInvoiceJour();
            if (vendInvoiceJour 
                && vendInvoiceJour.InvoiceType == PurchInvoiceType::VendorAdvance)
            {
                vendReversal.parmPurchInvoiceType(PurchInvoiceType::VendorAdvance);
            }

            isReversed =  vendReversal.reverseTransaction(vendTrans);
        }

        return isReversed;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidJournalCategory</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected boolean isValidJournalCategory(TransactionReversalExecutionDetail _detail)
    {
        boolean isValid;

        switch (_detail.JournalCategory)
        {
            case LedgerTransType::GeneralJournal, LedgerTransType::Payment:

                // Bank payments are reversed by bank not AP.
                if (BankAccountTrans::findByVoucher(_detail.SubledgerVoucher, _detail.TransactionDate).RecId == 0)
                {
                    isValid = (VendTrans::findVoucherDate(_detail.SubledgerVoucher, _detail.TransactionDate).RecId != 0);
                }
                break;

            case LedgerTransType::FixedAssets:

                // AR transactions can generate FA journals in GL
                // with the same voucher so we need to handle both.
                isValid = (VendTrans::findVoucherDate(_detail.SubledgerVoucher, _detail.TransactionDate).RecId != 0);
                break;

            case LedgerTransType::Vend, 
                 LedgerTransType::ExchAdjustment,
                 LedgerTransType::Purch:
                
                isValid = true;

                // Do not consider General ledger revaluations (LedgerTransType::ExchAdjustment)
                if (_detail.ReferenceTableId == tableNum(LedgerExchAdjHist))
                {
                    isValid = false;
                }
                break;
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSecurableType</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected SecurableType getSecurableType()
    {
        return SecurableType::MenuItemAction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMenuItemName</Name>
				<Source><![CDATA[
    [Hookable(false)]
    protected MenuItemName getMenuItemName()
    {
        return menuItemActionStr(TransactionReversal_Vend);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>