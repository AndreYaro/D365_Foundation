<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSSysQueryForm_PreviewControl_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formControlStr(SysQueryForm, Preview))]
internal final class WHSSysQueryForm_PreviewControl_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>clicked</Name>
				<Source><![CDATA[
    public void clicked()
    {
        next clicked();
       
        Query previewQuery = new Query(this.userModifiedQuery());
        previewQuery = this.processSysQueryFormQuery(previewQuery);

        WHSEditQueryBrowser editQueryBrowser = new WHSEditQueryBrowser();
        editQueryBrowser.parmQuery(previewQuery);
        editQueryBrowser.run();
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>userModifiedQuery</Name>
				<Source><![CDATA[
    private Query userModifiedQuery()
    {
        SysQueryForm queryForm = element.sysQueryForm;
        
        if (queryForm.query().dataSourceCount())
        {
            if (queryForm.userUpdate())
            {
                element.updateTable(element.range, element.rangeUpdate);
                element.updateTable(element.sorting, element.sortingUpdate);
                element.updateTable(element.companyRanges, element.companyRangesUpdate);
                element.updateTable(element.aggregate, true);
                element.updateTable(element.groupBy, true);
                element.updateTable(element.having, true);
                    
                queryForm.queryBuild(element.range, element.sorting, element.companyRanges, element.validTimeRanges, element.groupBy, element.aggregate, element.having);
                    
                if (queryForm.allowSave())
                {
                    element.querySave(queryForm.queryLastUsedLabel(), true);
                }

                queryForm.queryUpdate();
            }
            
            if (queryForm.allowSave())
            {
                xSysLastValue::saveLast(element);
            }
        }

        return queryForm.query();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processSysQueryFormQuery</Name>
				<Source><![CDATA[
    private Query processSysQueryFormQuery(Query _query)
    {
        SysQueryForm queryForm = element.sysQueryForm;
        SysQueryRun run = queryForm.selectedSysQueryRun();
        WHSEditQueryFormParameter editQueryFormParm = run.editQueryFormParameter();

        Common buffer = editQueryFormParm.buffer;

        if (buffer.TableId)
        {
            DictTable dictTable = new DictTable(buffer.TableId);

            if (tableHasStaticMethod(dictTable, identifierStr(processEditQueryForPreview)))
            {
                return dictTable.callStatic(identifierStr(processEditQueryForPreview), _query, editQueryFormParm.buffer);
            }
        }
        
        return _query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>