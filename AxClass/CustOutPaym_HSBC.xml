<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustOutPaym_HSBC</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class CustOutPaym_HSBC extends CustOutPaym
{
    private str 10 eftOrigID;
    private str 4 fileCreationNum;
    private Counter recordCount;
    private AmountCur totalTransactionsAmountCur;
    private Counter totalTransactions;

    private CurrencyCanadaUS currencyCanadaUS;
    private DialogField dialogCurrency;

    const str Zero = '0';
    const str Space = ' ';
    const str AllZeros = '0000000000';

    #DEFINE.SysLastValuesCurrentVersion(1)
    #LOCALMACRO.SysLastValuesList
        currencyCanadaUS
    #ENDMACRO

    #DEFINE.CurrentVersion(2)
    #LOCALMACRO.CurrentList
        currencyCanadaUS,
        totalTransactionsAmountCur,
        totalTransactions,
        recordCount,
        eftOrigID,
        fileCreationNum
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>close</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the epilogue of the media, such as an end record for a file.
    /// </summary>
    public void close()
    {
        file.writeExp(this.createFileControl());
        setprefix(strFmt("@SYS73667", this.parmFilename()));
        info(strFmt("@SYS73669", this.parmTotalTransactions(0)));
        info(strFmt("@SYS73670", this.parmTotalTransactionsAmountCur(0), this.getBankAccountTable().CurrencyCode));

        if (printControlReport)
        {
            this.printControlReport();
        }

        if (printPaymAdvice)
        {
            this.printPaymAdvice();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>codepage</Name>
				<Source><![CDATA[
    private int codepage()
    {
        #Localcodepage

        return #cp_1252;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createFileControl</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares the following trailer record for output to the export file: HSBC Distribution Format (CA).
    /// </summary>
    /// <returns>
    /// The trailer record.
    /// </returns>
    protected container createFileControl()
    {
        // HSBC Distribution Format (CA) File Format Specififcation
        // Trailer Record
        const str RecType = 'Z';

        str 9 recordCountStr;
        str 10 hsbcID;
        str 4 fileCreationNumber;
        str 14 totalAmountDebitTransactions;
        str 8 totalNumberDebitTransactions;
        str 1 recordType = RecType;
        str 14 totalAmountCreditTransactions = strRep(Zero, 14);
        str 8 totalNumberCreditTransactions = strRep(Zero, 8);
        str 14 valueOfErrorCodeE = strRep(Zero, 14);
        str 8 numberOfErrorCodeE = strRep(Zero, 8);
        str 14 valueOfErrorCodeF = strRep(Zero, 14);
        str 8 numberOfErrorCodeF = strRep(Zero, 8);
        str 1352 filler = strRep(Space, 1352);

        container trailerRecord = connull();

        // Add 1 to the record counter.
        recordCountStr = num2Str0(this.parmRecordCount(1), 9);
        hsbcID = this.parmEFTOrigID();
        fileCreationNumber = this.parmFileCreationNum();
        totalNumberDebitTransactions = num2Str0(this.parmTotalTransactions(0), 8);
        totalAmountDebitTransactions = strRFix(stralpha(num2Str0(this.parmTotalTransactionsAmountCur(0), 14, 2)), 14, Zero);

        trailerRecord = [recordType
            + recordCountStr
            + hsbcID
            + fileCreationNumber
            + totalAmountDebitTransactions
            + totalNumberDebitTransactions
            + totalAmountCreditTransactions
            + totalNumberCreditTransactions
            + valueOfErrorCodeE
            + numberOfErrorCodeE
            + valueOfErrorCodeF
            + numberOfErrorCodeF
            + filler];

        return trailerRecord;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createFileHeader</Name>
				<Source><![CDATA[
    /// <summary>
    /// Prepares the following header record for output to the export file: HSBC Distribution Format (CA).
    /// </summary>
    /// <returns>
    /// The header record.
    /// </returns>
    protected container createFileHeader()
    {
        const str RecType = 'A';
        const str HsbcInstitutionID = '01600';

        str julianDate;
        str 9 recordCountStr;
        str 10 hsbcdID;
        str 4 fileCreationNumber;
        str 6 fileCreationDate;
        str 3 currencyType;
        str 1 recordType = RecType;
        str 5 hsbcInstID = HsbcInstitutionID;
        str 20 reserved1 = strRep(Space, 20);
        str 1406 filler1 = strRep(Space, 1406);

        container headerRecord;

        // Add 1 to the record counter.
        recordCountStr = num2Str0(this.parmRecordCount(1), 9);
        hsbcdID = this.parmEFTOrigID();
        fileCreationNumber = this.parmFileCreationNum();

        julianDate = date2Julian(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()));
        fileCreationDate = Zero + subStr(julianDate, strlen(julianDate)-4, 5);

        currencyType = (currencyCanadaUS == CurrencyCanadaUS::US) ?
            ISOCurrencyCodeHelper::getISOCurrencyCodeAlpha(ISOCurrencyCodes::USD) :
            ISOCurrencyCodeHelper::getISOCurrencyCodeAlpha(ISOCurrencyCodes::CAD);

        headerRecord = [recordType
            + recordCountStr
            + hsbcdID
            + fileCreationNumber
            + fileCreationDate
            + hsbcInstID
            + reserved1
            + currencyType
            + filler1];

        return headerRecord;
    }

]]></Source>
			</Method>
			<Method>
				<Name>custVendOutPaymRecordRootClassId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the class ID of the corresponding parent of <c>CustVendOutPaymRecord</c> classes.
    /// </summary>
    /// <returns>
    /// The class ID.
    /// </returns>
    /// <remarks>
    /// The class must be inherited from the <c>CustVendOutPaymRecord</c> class.
    /// </remarks>
    public classId custVendOutPaymRecordRootClassId()
    {
        return classnum(CustOutPaymRecord_HSBC);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </summary>
    /// <returns>
    /// A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    /// A dialog box can be built either by using the <c>Dialog</c> class or by using a class that is
    /// created in the Application Object Tree (AOT).
    /// </remarks>
    public Object dialog()
    {
        DialogRunbase dialog = super();

        dialog.caption(this.interfaceName());
        this.dialogAddFileName(dialog);
        this.dialogAddCurrency(dialog);
        this.dialogAddPrintDocument(PaymDocumentType::ControlReport, dialog, true);
        this.dialogAddPrintDocument(PaymDocumentType::PaymAdvice, dialog, true);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogAddCurrency</Name>
				<Source><![CDATA[
    protected void dialogAddCurrency(DialogRunbase _dialog)
    {
        DialogRunbase dialog = _dialog;

        dialog.addGroup("@SYS62786");
        dialogCurrency = dialog.addFieldValue(enumstr(CurrencyCanadaUS), currencyCanadaUS, "@SYS7572");
    }

]]></Source>
			</Method>
			<Method>
				<Name>generatePaymentLines</Name>
				<Source><![CDATA[
    public void generatePaymentLines()
    {
        #OCCRetryCount
        LedgerJournalTrans ledgerJournalTrans;
        CustOutPaymRecord_HSBC custOutPaymRecord_HSBC;
        boolean ok = true;

        int64 paymentLinesToProcess = this.findQueryRecordCount();
        int64 paymentLinesProcessed;

        SysInstrumentationProgressLogger progressLogger = this.createProgressLogger();
        SysInstrumentationProgressStage progressStage = progressLogger.createStage(CustVendInstrumentationConstants::PaymentGenerationGeneratePaymentLines, paymentLinesToProcess);

        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextWithProgressMonitor(progressStage))
        {
            activityContext.addCustomProperty(CustVendInstrumentationConstants::ClassName, this.getClassName());

            try
            {
                ttsbegin;

                setprefix("@SYS17561");

                queryRun.reset();
                outPaymRecordList = new List(Types::Class);

                while (queryRun.next())
                {
                    ledgerJournalTrans = queryRun.get(tablenum(LedgerJournalTrans));

                    paymentLinesProcessed++;
                    progressLogger.trackProgress(paymentLinesProcessed);

                    if (!this.isOffsetAccountValid(ledgerJournalTrans))
                        continue;

                    custOutPaymRecord_HSBC = CustVendOutPaymRecord::construct(
                        ledgerJournalTrans.PaymMode,
                        ledgerJournalTrans.PaymSpec,
                        this,
                        this.custPaym(ledgerJournalTrans));

                    custOutPaymRecord_HSBC.parmFile(file);

                    setprefix(strFmt(
                        "@SYS66048",
                        ledgerJournalTrans.TransDate,
                        ledgerJournalTrans.Voucher,
                        ledgerJournalTrans.parmAccount()));

                    if (custOutPaymRecord_HSBC.checkValues(ledgerJournalTrans, this.parmProgress()))
                    {
                        if (!custOutPaymRecord_HSBC.parmSkipPaymRecord())
                        {
                            custOutPaymRecord_HSBC.output();
                            outPaymRecordList.addEnd(custOutPaymRecord_HSBC);
                            this.updateLedgerJournalTrans(ledgerJournalTrans, custOutPaymRecord_HSBC, true);
                            this.trackSequenceOnPaymMode(CustVendPaymModeTable::findCommon(ledgerJournalTrans, true));
                        }
                        else
                        {
                            continue;
                        }
                    }
                    else
                    {
                        ok = false;
                    }
                }

                if (!ok)
                {
                    throw error("@SYS18447");
                }

                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                progressLogger.logRetryOnDeadlock();

                retry;
            }
            catch (Exception::UpdateConflict)
            {
                if (appl.ttsLevel() == 0)
                {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        progressLogger.logRetryOnUpdateConflict();

                        retry;
                    }
                }
                else
                {
                    throw Exception::UpdateConflict;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBankAccountTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns a <c>BankAccountTable</c> record.
    /// </summary>
    /// <returns>
    /// The <c>BankAccountTable</c> record.
    /// </returns>
    public BankAccountTable getBankAccountTable()
    {
        return BankAccountTable::find(this.parmBankAccountID());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        super();

        if (dialogCurrency)
        {
            currencyCanadaUS = dialogCurrency.value();
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>interfaceName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the name of the format to display in the user interface.
    /// </summary>
    /// <returns>
    /// The name of the format to display in the user interface.
    /// </returns>
    /// <remarks>
    /// This method must return the name of the format to display in the user interface. Formats without an
    /// interface name cannot be used in electronic banking as export formats. However, they can serve as
    /// parents for other formats.
    /// </remarks>
    public PaymInterfaceName interfaceName()
    {
        const str InterfaceName = 'HSBC Distribution Format (CA)';

        return InterfaceName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>open</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens the media and creates the header of the media.
    /// </summary>
    public void open()
    {
        file = CustVendOutPaym::newFile(this.codepage());

        if (!file || file.status() != IO_Status::Ok)
        {
            throw error(strFmt("@SYS73665", filename));
        }

        this.parmEFTOrigID(strLFix(this.getBankAccountTable().CompanyPaymId, 10, Zero));
        this.parmFileCreationNum(num2Str0(this.getUniquePaymModeTable().LastSequenceNumber + 1, 4));

        if (this.validateHeaderValues())
        {
            file.writeExp(this.createFileHeader());
        }
        else
        {
            throw(error(strFmt("@SYS18738", this.getUniquePaymModeTable().PaymMode)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList] + [super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>packSysLastValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Packs the data required for the <c>SysLastValues</c> value.
    /// </summary>
    /// <returns>
    /// A container that contains the data to be persisted by the <c>SysLastValues</c> object.
    /// </returns>
    /// <remarks>
    /// The Pack and Unpack design pattern does not have provisions to support different serialization and
    /// <c>SysLastValues</c> pack lists. This separate packing mechanism has been implemented for
    /// <c>SysLastValues</c> and is first defined on the class one level above the <c>RunBase</c> or
    /// <c>RunBaseBatch</c> class. Derived classes also implement this method so that an object can be
    /// packed for the <c>SysLastValues</c> object.
    /// </remarks>
    public container packSysLastValues()
    {
        return [#SysLastValuesCurrentVersion, #SysLastValuesList, super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEFTOrigID</Name>
				<Source><![CDATA[
    /// <summary>
    /// Formats the HSBC EFT Originator ID from the <c>CompanyID</c> property.
    /// </summary>
    /// <param name="_eftOrigID">
    /// The value to set; optional.
    /// </param>
    /// <returns>
    /// The value of the <c>eftOrigID</c> parameter.
    /// </returns>
    /// <remarks>
    /// Formats the HSBC EFT Originator ID based on the following: HSBC Distribution Format (CA) File
    /// Format Specifications.
    /// </remarks>
    public CompanyPaymId parmEFTOrigID(str _eftOrigID = eftOrigID)
    {
        eftOrigID = _eftOrigID;
        return eftOrigID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFileCreationNum</Name>
				<Source><![CDATA[
    /// <summary>
    /// Formats the <c>FileCreationNumber</c> value.
    /// </summary>
    /// <param name="_fileCreationNum">
    /// The value to set; optional.
    /// </param>
    /// <returns>
    /// The value of the <c>FileCreationNum</c> parameter.
    /// </returns>
    /// <remarks>
    /// This method formats the <c>FileCreationNumber</c> value according to the following: HSBC
    /// Distribution Format (CA) File Format Specifications.
    /// </remarks>
    public CompanyPaymId parmFileCreationNum(str _fileCreationNum = fileCreationNum)
    {
        fileCreationNum = _fileCreationNum;
        return fileCreationNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRecordCount</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the <c>RecordCount</c> parameter.
    /// </summary>
    /// <param name="_recordCount">
    /// The value to set; optional.
    /// </param>
    /// <returns>
    /// The value of the <c>RecordCount</c> parameter.
    /// </returns>
    public Counter parmRecordCount(Counter _recordCount = recordCount)
    {
        recordCount += _recordCount;
        return recordCount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTotalTransactionsAmountCur</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the <c>TotalTransactionsAmountCur</c> parameter.
    /// </summary>
    /// <param name="_totalTransactionsAmountCur">
    /// The value to set; optional.
    /// </param>
    /// <returns>
    /// The value of the <c>TotalTransactionsAmountCur</c> parameter.
    /// </returns>
    public AmountCur parmTotalTransactionsAmountCur(AmountCur _totalTransactionsAmountCur = totalTransactionsAmountCur)
    {
        totalTransactionsAmountCur += _totalTransactionsAmountCur;
        return totalTransactionsAmountCur;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTotalTransactions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the <c>TotalTransactions</c> parameter.
    /// </summary>
    /// <param name="_totalTransactions">
    /// The value to set; optional.
    /// </param>
    /// <returns>
    /// The value of the <c>TotalTransactions</c> parameter.
    /// </returns>
    public Counter parmTotalTransactions(Counter _totalTransactions = totalTransactions)
    {
        totalTransactions += _totalTransactions;
        return totalTransactions;
    }

]]></Source>
			</Method>
			<Method>
				<Name>total</Name>
				<Source><![CDATA[
    public void total(AmountCur _amountCur)
    {
        this.parmTotalTransactions(1);
        this.parmTotalTransactionsAmountCur(_amountCur);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version version = RunBase::getVersion(_packedClass);
        boolean ret;
        container base;

        #LOCALMACRO.CurrentListV1
            currencyCanadaUS
        #ENDMACRO

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, base] = _packedClass;
                ret = super(base);
                break;

            case 1:
                [version, #CurrentListV1, base] = _packedClass;
                ret = super(base);
                break;

            default:
                ret = false;
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpackSysLastValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the data that is required for the <c>SysLastValues</c> object.
    /// </summary>
    /// <param name="_packedValues">
    /// The values stored by the <c>SysLastValues</c> framework that are to be unpacked.
    /// </param>
    /// <remarks>
    /// The pack and unpack pattern does not have provisions for supporting different serialization and
    /// <c>SysLastValues</c> pack lists. This separate packing mechanism has been implemented for
    /// <c>SysLastValues</c> and is first defined on the class one level above the <c>RunBase</c> or
    /// <c>RunBaseBatch</c> class. Derivative classes also implement this method so that an object can be
    /// unpacked for <c>SysLastValues</c> .
    /// </remarks>
    public void unpackSysLastValues(container _packedValues)
    {
        container baseClassPackedValues;
        Version version = RunBase::getVersion(_packedValues);

        switch (version)
        {
            case #SysLastValuesCurrentVersion:
                [version, #SysLastValuesList, baseClassPackedValues] = _packedValues;
                super(baseClassPackedValues);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the state of the internal variables.
    /// </summary>
    /// <param name="_calledFrom">
    /// Identifies the object from which this method is called. Default value is null.
    /// </param>
    /// <returns>
    /// true if the internal variables are valid; otherwise, false.
    /// </returns>
    /// <remarks>
    /// This method must validate the state of the internal variables. It is called before closing the
    /// dialog, and should be called in the <c>run</c> method.It must only contain code that adds warnings
    /// to the infolog.Throw is not enabled within this method.The method should generally return errors
    /// using the <c>Global::CheckFailed</c> method.Export formats that are designed to handle only one
    /// method of payment per payment media must call the <c>checkUniquePaymMode</c> method.
    /// </remarks>
    public boolean validate(Object _calledFrom = null)
    {
        Currency currency;
        BankAccountTable bankAccountTable;
        boolean ok = true;
        boolean currencyOk = false;
        boolean noCanadianCurrency = true;
        boolean noUSCurrency = true;

        bankAccountTable = this.getBankAccountTable();
        if (bankAccountTable)
        {
            ok = BankAccountTable::checkIsActive(bankAccountTable.AccountID);
        }
        else
        {
            ok = checkFailed(strFmt("@SYS26720", accountId));
        }

        if (ok)
        {
            // If we are only paying Canadian currency transactions
            if (currencyCanadaUS == CurrencyCanadaUS::Canada)
            {
                // Verify that the currency of the bank is the same as the currency code for the Canadian Dollar ISO code (CAD).
                currency.selectForUpdate(false);

                while select CurrencyCode from currency
                    where currency.CurrencyCodeISO == ISOCurrencyCodeHelper::getISOCurrencyCodeAlpha(ISOCurrencyCodes::CAD)
                {
                    noCanadianCurrency = false;
                    if (currency.CurrencyCode == bankAccountTable.CurrencyCode)
                    {
                        currencyOk = true;
                        break;
                    }
                }

                if (noCanadianCurrency)
                {
                    ok = checkFailed("@SYS98121");
                }

                if (!currencyOk)
                {
                    ok = checkFailed(strFmt("@SYS98120", this.parmBankAccountID()));
                }
            }
            else if (currencyCanadaUS == CurrencyCanadaUS::US)
            {
                // Verify that the currency of the bank is the same as the currency code for the US Dollar ISO code (USD).
                currency.selectForUpdate(false);

                while select CurrencyCode from currency
                    where currency.CurrencyCodeISO == ISOCurrencyCodeHelper::getISOCurrencyCodeAlpha(ISOCurrencyCodes::USD)
                {
                    noUSCurrency = false;
                    if (currency.CurrencyCode == bankAccountTable.CurrencyCode)
                    {
                        currencyOk = true;
                        break;
                    }
                }

                if (noUSCurrency)
                {
                    ok = checkFailed("@SYS115581");
                }

                if (!currencyOk)
                {
                    ok = checkFailed(strFmt("@SYS115583", this.parmBankAccountID()));
                }
            }
        }

        if (ok)
        {
            if (!filename)
            {
                ok = checkFailed("@SYS18624");
            }
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateHeaderValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the details that are required by the export format.
    /// </summary>
    /// <returns>
    /// true if the validation was successful; otherwise, false.
    /// </returns>
    /// <remarks>
    /// Validates the following details that are required by the export format:
    /// <list type="bullet">
    /// <item>
    /// <description>Methods of payment - Vendor: Last file number is not equal or more than
    /// 9999.</description>
    /// </item>
    /// <item>
    /// <description>Company information: Company name is not blank.</description>
    /// </item>
    /// <item>
    /// <description>Bank account: Company ID is not blank.</description>
    /// </item>
    /// </list>
    /// If any one of these validations fail, an appropriate error message is provided and the file export
    /// will not occur.
    /// </remarks>
    protected boolean validateHeaderValues()
    {
        boolean ok = true;

        // The next last file number used for this format will exceed 9999.
        if (this.getUniquePaymModeTable().LastSequenceNumber + 1 > 9999)
        {
            ok = checkFailed(strFmt("@SYS98755", this.getUniquePaymModeTable().PaymMode));
        }

        // Company Name field is blank.
        if (CompanyInfo::find().name() == '')
        {
            ok = checkFailed(strFmt("@SYS98756", this.getUniquePaymModeTable().PaymMode));
        }

        // The Comany ID value for the selected bank is not valid (value was empty).
        if (this.parmEFTOrigID() == AllZeros)
        {
            ok = checkFailed(strFmt(
                "@SYS98757",
                this.getUniquePaymModeTable().PaymMode,
                this.getBankAccountTable().AccountID));
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes whether the class is designed for execution in a new session.
    /// </summary>
    /// <returns>
    /// true if the class is designed for executing the operation in a new session; otherwise, false.
    /// </returns>
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>