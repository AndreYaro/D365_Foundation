<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WmsArrivalOverviewGeneration_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(WMSArrivalOverviewGeneration))]
public final class WMSArrivalOverviewGeneration_ApplicationSuite_Extension
{
    private Map headerDlvModeMap;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillTables</Name>
				<Source><![CDATA[
    protected container fillTables()
    {
        headerDlvModeMap = null;
        return next fillTables();
    }

]]></Source>
			</Method>
			<Method>
				<Name>filterOnPurchHeaderDlvMode</Name>
				<Source><![CDATA[
    protected boolean filterOnPurchHeaderDlvMode()
    {
        boolean ret = next filterOnPurchHeaderDlvMode();

        if (ret)
        {
            ret = false;
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustInsertArrivalDetailForInventTransId</Name>
				<Source><![CDATA[
    protected boolean mustInsertArrivalDetailForInventTransId(InventTransOrigin _inventTransOrigin, InventDim _inventDim)
    {
        boolean ret = next mustInsertArrivalDetailForInventTransId(_inventTransOrigin, _inventDim);
        DlvModeId filterDlvModeId = this.wmsArrivalOverviewOptions.parmDlvModeId();

        if (ret
            && filterDlvModeId
            && _inventTransOrigin.ReferenceCategory == InventTransType::Purch)
        {
            PurchLine purchLineLocal = PurchLine::findInventTransId(_inventTransOrigin.InventTransId);
            if (purchLineLocal.DlvMode)
            {
                if (purchLineLocal.DlvMode != filterDlvModeId)
                {
                    ret = false;
                }
            }
            else
            {
                DlvModeId headerDlvModeId;
                if (headerDlvModeMap && headerDlvModeMap.exists(purchLineLocal.PurchId))
                {
                    headerDlvModeId = headerDlvModeMap.lookup(purchLineLocal.PurchId);
                }
                else
                {
                    headerDlvModeId = purchLineLocal.purchTable().DlvMode;
                    if (!headerDlvModeMap)
                    {
                        headerDlvModeMap = new Map(Types::String, Types::String);
                    }

                    headerDlvModeMap.insert(purchLineLocal.PurchId, headerDlvModeId);
                }

                if (headerDlvModeId != filterDlvModeId)
                {
                    ret = false;
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>