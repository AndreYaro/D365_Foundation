<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmRatingLevelIdLookUpFormEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the ApplicationSuite module form events for the HcmRatingLevelIdLookUp form.
/// </summary>
class HcmRatingLevelIdLookUpFormEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmRatingLevelIdLookUp_Pre_init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Form OnInitializing event handler for HcmRatingLevelIdLookUp form.
    /// </summary>
    /// <param name="_args">The args for the event.</param>
    [PreHandlerFor(formStr(HcmRatingLevelIdLookUp), formMethodStr(HcmRatingLevelIdLookUp, init))]
    public static void HcmRatingLevelIdLookUp_Pre_init(XppPrePostArgs _args)
    {
        Common callerRecord;
        FormDataSource callerDataSource;
        FormControl callerFormControl;
        // Shop Floor Control - Begin
        JmgPayAgreementLineMap          jmgPayAgreementLineMap;
        // Shop Floor Control - End
        WrkCtrActivitySkillRequirement  wrkCtrActivitySkillRequirement;
        HcmSkillRecId skill;
        FormRun ratingLevelIdLookUpForm = _args.getThis();

        if (!ratingLevelIdLookUpForm.args() || !ratingLevelIdLookUpForm.args().caller())
        {
            throw error("@SYS22539");
        }

        callerFormControl = ratingLevelIdLookUpForm.selectTarget();

        if (callerFormControl)
        {
            callerDataSource = callerFormControl.dataSourceObject();

            if (callerDataSource)
            {
                callerRecord = callerDataSource.cursor();
            }
        }

        if (callerRecord.TableId)
        {
            switch (callerRecord.TableId)
            {
                // Shop Floor Control - Begin
                case tableNum(JmgPayAgreementLine):
                case tableNum(JmgPayAgreementOverrideLine):
                    jmgPayAgreementLineMap = callerRecord;
                    skill = HcmSkill::findBySkill(jmgPayAgreementLineMap.SkillId).RecId;
                    break;
                // Shop Floor Control - End

                case tableNum(WrkCtrActivitySkillRequirement) :
                    wrkCtrActivitySkillRequirement = callerRecord as WrkCtrActivitySkillRequirement;
                    skill = wrkCtrActivitySkillRequirement.Skill;
                    break;
            }
        }

        ratingLevelIdLookUpForm.parmHcmSkillRecId(skill);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>