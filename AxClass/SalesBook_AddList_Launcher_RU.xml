<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesBook_AddList_Launcher_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SalesBook_AddList_Launcher_RU extends RunBaseBatch implements BatchRetryable
{
    Dialog                  dialog;
    SalesPurchBookRecId_RU  bookRecId;
    QueryRun                queryRun;
    FromDate                fromDate;
    ToDate                  toDate;
    NoYes                   exportToXml;

    DialogField             periodCodeField;
    DialogField             fromDateField;
    DialogField             toDateField;
    DialogField             exportToXmlDlg;

    protected const int dialogPeriodCodeFieldNo = 900;
    protected const int currentVersion = 4;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        dialog = super();

        if (dialog)
        {
            dialog.addGroup("@SYS23339");
            periodCodeField = new DialogField(dialog, extendedTypeStr(DateCode), dialogPeriodCodeFieldNo);
            dialog.addCtrlDialogField(periodCodeField.name());
            periodCodeField.init(dialog);

            fromDateField = dialog.addFieldValue(extendedTypeStr(FromDate), fromDate);
            toDateField   = dialog.addFieldValue(extendedTypeStr(ToDate), toDate);
            exportToXmlDlg= dialog.addFieldValue(enumStr(NoYes), exportToXml, "@SYS114646");
        }

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogPostRun</Name>
				<Source><![CDATA[
    public void dialogPostRun(DialogRunbase _dialog)
    {
        super(_dialog);
        _dialog.dialogForm().formRun().controlMethodOverload(true);
        _dialog.dialogForm().formRun().controlMethodOverloadObject(this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fld900_1_modified</Name>
				<Source><![CDATA[
    private boolean fld900_1_modified()
    {
        Object control = dialog.formRun().controlCallingMethod();
        boolean ret = control.modified();

        if (ret && periodCodeField.value())
        {
            LedgerPeriodCode ledgerPeriodCode = LedgerPeriodCode::find(periodCodeField.value());
            fromDateField.value(ledgerPeriodCode.fromDate());
            toDateField.value(ledgerPeriodCode.toDate());
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = super();

        if (ret)
        {
            fromDate    = fromDateField.value();
            toDate      = toDateField.value();
            exportToXml     = exportToXmlDlg.value();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParmDefault</Name>
				<Source><![CDATA[
    public void initParmDefault()
    {
        Query                   query;
        SalesBookTable_RU       salesBookTable = SalesBookTable_RU::find(bookRecId);

        super();

        fromDate = salesBookTable.getFromDate();
        toDate   = salesBookTable.getToDate();

        query = SalesBook_AddList_Launcher_RU::createDefaultQuery(bookRecId, fromDate, toDate);

        queryRun = new QueryRun(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [currentVersion, 
                bookRecId,
                fromDate,
                toDate, 
                queryRun.pack(), 
                super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmBookRecId</Name>
				<Source><![CDATA[
    public SalesPurchBookRecId_RU parmBookRecId(SalesPurchBookRecId_RU _bookRecId = bookRecId)
    {
        bookRecId = _bookRecId;
        return bookRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        SalesBookAddListReportExcel_RU      salesBookPrint;
        container                           bookRecIdCon;
        container                           bookRecIdConPurch;
        TransDate                           periodStart, periodEnd;
        SetEnumerator                       setEnumerator;
        Set                                 setPeriods;
        Map                                 mapPeriodBooks  = new Map(Types::Container, Types::Container);
        Map                                 mapBookPeriod   = new Map(Types::Int64,     Types::Container);
        Map                                 mapPeriodPurchBooks  = new Map(Types::Container, Types::Container);
        Map                                 mapPurchBookPeriod   = new Map(Types::Int64,    Types::Container);
        int                                 bookCount;
        container                           setPeriodsPacked;

        [setPeriodsPacked,
         bookRecIdCon,
         bookRecIdConPurch] = SalesBook_AddList_Launcher_RU::initParameters(bookRecId,
                                                                        fromDate,
                                                                        toDate,
                                                                        queryRun.query(),
                                                                        mapPeriodBooks,
                                                                        mapBookPeriod,
                                                                        mapPeriodPurchBooks,
                                                                        mapPurchBookPeriod);

        setPeriods = Set::create(setPeriodsPacked);

        setEnumerator = setPeriods.getEnumerator();
        while (setEnumerator.moveNext())
        {
            [periodStart, periodEnd] = setEnumerator.current();
            salesBookPrint = SalesBookAddListReportExcel_RU::construct();
            salesBookPrint.parmFromDate(periodStart);
            salesBookPrint.parmToDate(periodEnd);
            salesBookPrint.parmStartDate(fromDate);
            salesBookPrint.parmReportDate(toDate);
            salesBookPrint.parmExportToXml(exportToXml);
            salesBookPrint.parmBookRecIdCon(bookRecIdCon);
            salesBookPrint.parmBookRecIdConPurch(bookRecIdConPurch);
            salesBookPrint.parmCorrectedBookRecIdCon(mapPeriodBooks.lookup(setEnumerator.current()));
            if (conlen(bookRecIdConPurch) > 0)
            {
                salesBookPrint.parmCorrectedPurchBookRecIdCon(mapPeriodPurchBooks.lookup(setEnumerator.current()));
            }
            salesBookPrint.parmQueryRun(queryRun);
            //salesBookPrint.parmPrintDestinationSettings(this.parmPrintDestinationSettings());
            salesBookPrint.parmAddListNumber(SalesPurchBookDataMap_RU::getAddListNumber(periodStart,
                                                                                    periodEnd,
                                                                                    mapPeriodBooks,
                                                                                    queryRun.query(),
                                                                                    SalesPurch::Sales));
            salesBookPrint.runOperation();
            bookCount++;
        }

        if (! bookCount)
        {
            warning("@GLS101546");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer                version     = conPeek(_packedClass,1);
        container              packedQuery, base;
        SalesBookTable_RU      salesBookTable = SalesBookTable_RU::find(bookRecId);

        switch (version)
        {
            case currentVersion :

                SalesPurchBookRecId_RU bookRecIdNew = this.parmBookRecId();
                
                [   version, 
                    bookRecId,
                    fromDate,
                    toDate, 
                    packedQuery, 
                    base] = _packedClass;
                
                if (packedQuery)
                {
                    queryRun = new QueryRun(packedQuery);
                }
                if (base)
                {
                    super(base);
                }
                if (bookRecIdNew && bookRecIdNew != bookRecId)
                {
                    salesBookTable = SalesBookTable_RU::find(bookRecIdNew);
                    fromDate = salesBookTable.getFromDate();
                    toDate   = salesBookTable.getToDate();

                    this.parmBookRecId(bookRecIdNew);
                    this.initParmDefault();
                }
                break;

            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ret = super(_calledFrom);

        if (! toDate)
        {
            ret = checkFailed("@GLS105013");
        }
        else if (fromDate > toDate)
        {
            ret = checkFailed("@GLS101164");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createDefaultQuery</Name>
				<Source><![CDATA[
    public static Query createDefaultQuery(SalesPurchBookRecId_RU  _bookRecId,
                                       TransDate               _fromDate,
                                       TransDate               _toDate)
    {
        Query                   query;
        QueryBuildDataSource    qbdsSalesBookTrans;
        QueryBuildDataSource    qbdsPurchBookTrans;
        QueryBuildRange         qbrBookRecId, qbrCorrectedBookRecId;
        QueryBuildRange         qbrExt;
        container               bookRecIdCon;
        container               bookRecIdConPurch;

        bookRecIdCon = SalesBookTable_RU::booksInPeriod(_bookRecId, _fromDate, _toDate);

        query = new Query();
        qbdsSalesBookTrans = query.addDataSource(tableNum(SalesBookTrans_RU));
        qbrBookRecId          = qbdsSalesBookTrans.addRange(fieldNum(SalesBookTrans_RU, SalesBookTable_RU));
        qbrBookRecId.value(con2Str(bookRecIdCon));
        qbrBookRecId.status(RangeStatus::Hidden);

        bookRecIdCon += 0;
        qbrCorrectedBookRecId = qbdsSalesBookTrans.addRange(fieldNum(SalesBookTrans_RU, CorrectedSalesBookTable_RU));
        qbrCorrectedBookRecId.value(queryNotValueCon_W(bookRecIdCon));
        qbrCorrectedBookRecId.status(RangeStatus::Hidden);

        bookRecIdCon = condel(bookRecIdCon, conlen(bookRecIdCon), 1);

        qbrExt = qbdsSalesBookTrans.addRange(fieldnum(SalesBookTrans_RU, TransType));
        qbrExt.status(RangeStatus::Hidden);

        qbdsPurchBookTrans = query.addDataSource(tablenum(PurchBookTrans_RU));

        bookRecIdConPurch = SalesBook_AddList_Launcher_RU::getPurchBookRecIdCon(bookRecIdCon);

        qbrBookRecId = qbdsPurchBookTrans.addRange(fieldnum(PurchBookTrans_RU, PurchBookTable_RU));

        if (conlen(bookRecIdConPurch) > 0)
        {
            qbrBookRecId.value(con2Str(bookRecIdConPurch));
        }
        else
        {
            qbrBookRecId.value(SysQuery::valueEmptyString());
        }

        qbrBookRecId.status(RangeStatus::Locked);

        bookRecIdConPurch += SysQuery::valueEmptyString();
        qbrCorrectedBookRecId = qbdsPurchBookTrans.addRange(fieldnum(PurchBookTrans_RU, CorrectedPurchBookTable_RU));
        qbrCorrectedBookRecId.value(queryNotValueCon_W(bookRecIdConPurch));
        qbrCorrectedBookRecId.status(RangeStatus::Hidden);

        qbrExt = qbdsPurchBookTrans.addRange(fieldnum(PurchBookTrans_RU, RevisionType));
        qbrExt.status(RangeStatus::Hidden);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@Facture:AddListPrint";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPurchBookRecIdCon</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns container with purchase books RecIds corresponding to the container with sales books RecIds.
    /// </summary>
    /// <param name="_salesBookRecIdCon">
    ///    Container with sales books RecIds.
    /// </param>
    /// <returns>
    ///    Container with purchase books RecIds corresponding to the container with sales books RecIds.
    /// </returns>

    public static container getPurchBookRecIdCon(container _salesBookRecIdCon)
    {
        int                     i;
        container               bookRecIdCon;

        for (i = 1; i <= conlen(_salesBookRecIdCon); i++)
        {
            SalesBookTable_RU salesBookTable = SalesBookTable_RU::find(conpeek(_salesBookRecIdCon, i));
            PurchBookTable_RU purchBookTable = salesBookTable.findPurchBook();

            if (purchBookTable.RecId)
            {
                bookRecIdCon += purchBookTable.RecId;
            }
        }
        return bookRecIdCon;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSalesBookPeriodMaps</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fills maps with values of corrected purch book tables and corrected periods.
    /// </summary>
    /// <param name="_mapPeriodBooks">
    /// Map which holds relationships between periods and books.
    /// </param>
    /// <param name="_mapBookPeriod">
    /// Map which holds relationships between books and periods.
    /// </param>
    /// <param name="_bookRecId">
    /// Source book record id.
    /// </param>
    /// <param name="_fromDate">
    /// Start date of correction period.
    /// </param>
    /// <param name="_toDate">
    /// End date of correction period.
    /// </param>
    public static void getSalesBookPeriodMaps(Map                       _mapPeriodBooks  = new Map(Types::Container, Types::Container),
                                              Map                       _mapBookPeriod   = new Map(Types::Int64,    Types::Container),
                                              RefRecId                  _bookRecId       = 0,
                                              FromDate                  _fromDate        = dateNull(),
                                              ToDate                    _toDate          = dateNull())
    {
        TransDate                       periodStart;
        TransDate                       periodEnd;
        container                       bookRecIdPeriodCon;
        int                             bookCount, idx;
        SalesBookTable_RU               salesBookTable, tmpSalesBookTable;
        Map                             tmpBooksMap = new Map(Types::Int64, Types::Int64);

        void updatePeriodMaps()
        {
            _mapPeriodBooks.insert([periodStart, periodEnd], bookRecIdPeriodCon);

            for (idx = 1; idx <= conlen(bookRecIdPeriodCon); idx++)
            {
                _mapBookPeriod.insert(conpeek(bookRecIdPeriodCon, idx), [periodStart, periodEnd]);
            }
        }

        container bookRecIdCon = SalesBookTable_RU::booksInPeriod(_bookRecId, _fromDate, _toDate);

        tmpSalesBookTable.setTmp();
        while select salesBookTable
        {
            buf2Buf(salesBookTable, tmpSalesBookTable);
            if (! salesBookTable.ClosingDate)
            {
                tmpSalesBookTable.ClosingDate = salesBookTable.getToDate();
            }
            tmpSalesBookTable.insert();

            tmpBooksMap.insert(tmpSalesBookTable.RecId, salesBookTable.RecId);
        }

        while select tmpSalesBookTable
            order by ClosingDate desc
            where tmpSalesBookTable.ClosingDate < _fromDate
        {
            bookCount++;
            bookRecIdPeriodCon += tmpBooksMap.lookup(tmpSalesBookTable.RecId);

            if (bookCount == 1)
            {
                periodEnd = tmpSalesBookTable.getToDate();
            }

            if (bookCount == conlen(bookRecIdCon))
            {
                periodStart = tmpSalesBookTable.getFromDate();
                updatePeriodMaps();
                bookCount = 0;
                bookRecIdPeriodCon = connull();
            }
        }

        if (bookCount > 0)
        {
            select firstonly tmpSalesBookTable order by ClosingDate;
            periodStart = tmpSalesBookTable.getFromDate();
            updatePeriodMaps();
            bookCount = 0;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (! _args || ! _args.record() || _args.dataset() != tableNum(SalesBookTable_RU))
        {
           throw error(Error::missingRecord(funcName()));
        }

        SalesBookTable_RU salesBookTable = _args.record();
        SalesPurchBookRecId_RU bookRecId = salesBookTable.RecId;

        SalesBook_AddList_Launcher_RU salesBookLauncher = new SalesBook_AddList_Launcher_RU();
        salesBookLauncher.parmBookRecId(bookRecId);

        salesBookLauncher.initParmDefault();
        if (salesBookLauncher.prompt())
        {
            salesBookLauncher.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initParameters</Name>
				<Source><![CDATA[
    public static container initParameters(SalesPurchBookRecId_RU    _bookRecId,
                                           FromDate                  _fromDate,
                                           ToDate                    _toDate,
                                           Query                     _query,
                                           Map                       _mapPeriodBooks = new Map(Types::Container, Types::Container),
                                           Map                       _mapBookPeriod = new Map(Types::Int64, Types::Container),
                                           Map                       _mapPeriodPurchBooks = new Map(Types::Container, Types::Container),
                                           Map                       _mapPurchBookPeriod = new Map(Types::Int64, Types::Container))
    {

        SalesBookTable_RU                   salesBookTable;
        PurchBookTable_RU                   purchBookTable;
        container                           bookRecIdCon;
        container                           bookRecIdConPurch;
        Query                               correctedBookQuery;
        QueryBuildDataSource                qbdsSalesBookTrans;
        QueryBuildDataSource                qbdsPurchBookTrans;
        QueryRun                            correctedBookQueryRun;
        SalesBookTrans_RU                   salesBookTrans;
        PurchBookTrans_RU                   purchBookTrans;
        Set                                 setPeriods = new Set(Types::Container);
        TransDate                           periodStart;
        TransDate                           periodEnd;

        #macrolib.SalesBook_RU

        SalesBook_AddList_Launcher_RU::getSalesBookPeriodMaps(
            _mapPeriodBooks,
            _mapBookPeriod,
            _bookRecId,
            _fromDate,
            _toDate);

        salesBookTable = SalesBookTable_RU::find(_bookRecId);
        purchBookTable = salesBookTable.findPurchBook();

        PurchBook_AddList_Launcher_RU::getPurchBookPeriodMaps(
            _mapPeriodPurchBooks,
            _mapPurchBookPeriod,
            purchBookTable.RecId,
            _fromDate,
            _toDate);

        bookRecIdCon = SalesBookTable_RU::booksInPeriod(_bookRecId, _fromDate, _toDate);

        correctedBookQuery = new Query(_query);
        qbdsSalesBookTrans = correctedBookQuery.dataSourceTable(tableNum(SalesBookTrans_RU));
        qbdsSalesBookTrans.addSortField(fieldNum(SalesBookTrans_RU, CorrectedSalesBookTable_RU));
        qbdsSalesBookTrans.orderMode(OrderMode::GroupBy);

        findOrCreateRange_W(qbdsSalesBookTrans, fieldNum(SalesBookTrans_RU, SalesBookTable_RU), con2Str(bookRecIdCon));

        bookRecIdCon += 0;
        findOrCreateRange_W(qbdsSalesBookTrans, fieldNum(SalesBookTrans_RU, CorrectedSalesBookTable_RU), queryNotValueCon_W(bookRecIdCon));
        bookRecIdCon = conDel(bookRecIdCon, conLen(bookRecIdCon), 1);

        findOrCreateRange_W(qbdsSalesBookTrans, fieldnum(SalesBookTrans_RU, CorrectionType),
            '('
            + SalesBookTable_RU::extQueryStatement(qbdsSalesBookTrans.name(), fieldstr(SalesBookTrans_RU, FactureDate_External), '<', date2StrXpp(#correctionAddListDate))
            + ' || '
            + SalesBookTable_RU::extQueryStatement(qbdsSalesBookTrans.name(), fieldstr(SalesBookTrans_RU, CorrectionType), '!=', int2str(FactureCorrectionType_RU::Correction))
            + ')'
        );

        qbdsPurchBookTrans = correctedBookQuery.dataSourceTable(tablenum(PurchBookTrans_RU));
        qbdsPurchBookTrans.addSortField(fieldnum(PurchBookTrans_RU, CorrectedPurchBookTable_RU));
        qbdsPurchBookTrans.orderMode(OrderMode::GroupBy);

        bookRecIdConPurch = SalesBook_AddList_Launcher_RU::getPurchBookRecIdCon(bookRecIdCon);

        if (conlen(bookRecIdConPurch) > 0)
        {
            findOrCreateRange_W(qbdsPurchBookTrans, fieldnum(PurchBookTrans_RU, PurchBookTable_RU), con2Str(bookRecIdConPurch));
        }
        else
        {
            findOrCreateRange_W(qbdsPurchBookTrans, fieldnum(PurchBookTrans_RU, PurchBookTable_RU), SysQuery::valueEmptyString());
        }

        bookRecIdConPurch += 0;
        findOrCreateRange_W(qbdsPurchBookTrans, fieldnum(PurchBookTrans_RU, CorrectedPurchBookTable_RU), queryNotValueCon_W(bookRecIdConPurch));
        bookRecIdConPurch = condel(bookRecIdConPurch, conlen(bookRecIdConPurch), 1);

        SalesBookAddListReportExcel_RU::setExtQueryRanges(correctedBookQuery);

        correctedBookQueryRun = new QueryRun(correctedBookQuery);
        while (correctedBookQueryRun.next())
        {
            if (correctedBookQueryRun.changed(tablenum(SalesBookTrans_RU)))
            {
                salesBookTrans = correctedBookQueryRun.get(tablenum(SalesBookTrans_RU));
                if (_mapBookPeriod.exists(salesBookTrans.CorrectedSalesBookTable_RU))
                {
                    setPeriods.add(_mapBookPeriod.lookup(salesBookTrans.CorrectedSalesBookTable_RU));
                    [periodStart, periodEnd] = _mapBookPeriod.lookup(salesBookTrans.CorrectedSalesBookTable_RU);
                }
            }

            if (correctedBookQueryRun.changed(tablenum(PurchBookTrans_RU)))
            {
                purchBookTrans = correctedBookQueryRun.get(tablenum(PurchBookTrans_RU));
                if (_mapPurchBookPeriod.exists(purchBookTrans.CorrectedPurchBookTable_RU))
                {
                    setPeriods.add(_mapPurchBookPeriod.lookup(purchBookTrans.CorrectedPurchBookTable_RU));
                    [periodStart, periodEnd] = _mapPurchBookPeriod.lookup(purchBookTrans.CorrectedPurchBookTable_RU);
                }
            }
        }

        return [setPeriods.pack(),
            bookRecIdCon,
            bookRecIdConPurch];
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>