<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesQuotationCopyingTreeControlBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is responsible for building the tree control for the selected transactions in the <c>SalesQuotationCopying</c>.
/// </summary>
class SalesQuotationCopyingTreeControlBuilder
{
    #resAppl
    ImageListAppl_Sales    imageListAppl_Sales;
    CustQuotationTrans     custQuotationTrans;
    SalesQuotationTable    callerSalesQuotationTable;

    int                 treeEntry;

    QuotationIdBase     quotationIdBase;
    QuotationConfirmId  quotationConfirmId;
    QuotationId         quotationId;

    QuotationProjTransType  projTransType;
    ItemId                  itemId;
    ItemNameDisplay         itemName;
    SalesCategory           salesCategory;
    ItemFreeTxt             name;
    private TmpFrmVirtual   tmpFrmVirtualLinesLocal;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new ()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SalesQuotationCopyingTreeControlBuilder construct()
    {
        return new SalesQuotationCopyingTreeControlBuilder();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the internal state.
    /// </summary>
    /// <param name = "_imageListAppl_Sales">An instance of <c>ImageListAppl_Sales</c>.</param>
    /// <param name = "_custQuotationTrans">A <c>CustQuotationTrans</c> record.</param>
    /// <param name = "_callerSalesQuotationTable">The <c>SalesQuotationTable</c> record used to call the form.</param>
    public void initialize(ImageListAppl_Sales _imageListAppl_Sales,
                           CustQuotationTrans  _custQuotationTrans,
                           SalesQuotationTable _callerSalesQuotationTable)
    {        
        imageListAppl_Sales = _imageListAppl_Sales;
        custQuotationTrans = _custQuotationTrans;
        callerSalesQuotationTable = _callerSalesQuotationTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the tree control for the selected transactions in the <c>SalesQuotationCopying</c>
    /// </summary>
    /// <param name = "_treeControl">The <c>SalesQuotationCopying</c> tree control to be built.</param>
    public void build(FormTreeControl _treeControl)
    {
        _treeControl.deleteAll();
        if (SalesQuotationCopyingDisplaySelectedLinesFlight::instance().isEnabled())
        {
            while select tmpFrmVirtualLinesLocal order by Id, TransDate
            {
                this.addItemsToTreeFromTmpFrmVirtual(tmpFrmVirtualLinesLocal, _treeControl);
                this.addItemsToTreeFromQuotationType(tmpFrmVirtualLinesLocal, _treeControl);
            }
        }
        else
        {
            TmpFrmVirtual tmpFrmVirtualLines;
            while select tmpFrmVirtualLines order by Id, TransDate
            {
                this.addItemsToTreeFromTmpFrmVirtual(tmpFrmVirtualLines, _treeControl);
                this.addItemsToTreeFromQuotationType(tmpFrmVirtualLines, _treeControl);
            }
        }
        _treeControl.expand(treeEntry);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addItemsToTreeFromTmpFrmVirtual</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add items to the tree control based on the table number of the specified <c>TmpFrmVirtual</c>.
    /// </summary>
    /// <param name = "_tmpFrmVirtualLines">A <c>TmpFrmVirtual</c> used to create the items.</param>
    /// <param name = "_treeControl">The tree control to which the items are added.</param>
    protected void addItemsToTreeFromTmpFrmVirtual(TmpFrmVirtual _tmpFrmVirtualLines, FormTreeControl _treeControl)
    {
        switch (_tmpFrmVirtualLines.TableNum)
        {
            case tablenum(CustQuotationTrans):
                CustQuotationTrans localCustQuotationTrans = CustQuotationTrans::findRecId(_tmpFrmVirtualLines.RecordNo);
                if (quotationId != localCustQuotationTrans.QuotationId)
                {
                    _treeControl.expand(treeEntry);
                    treeEntry   = _treeControl.addItem(0, 
                                                       0, 
                                                       new FormTreeItem(strfmt('%1 %2 %3 %4',
                                                                                "@SYS5864",
                                                                                localCustQuotationTrans.QuotationId,
                                                                                "@SYS84648",
                                                                                localCustQuotationTrans.custQuotationJour().ProjIdRef),
                                                                            imageListAppl_Sales.image(#ImageQuotation)));
                    quotationId = localCustQuotationTrans.QuotationId;
                }

                itemName        = localCustQuotationTrans.itemName();
                itemId          = localCustQuotationTrans.ItemId;
                salesCategory   = localCustQuotationTrans.SalesCategory;
                name            = localCustQuotationTrans.Name;
                projTransType   = localCustQuotationTrans.ProjTransType;
                break;

            case tablenum(CustQuotationConfirmTrans):
                CustQuotationConfirmTrans localCustQuotationConfirmTrans = CustQuotationConfirmTrans::findRecId(_tmpFrmVirtualLines.RecordNo);
                if (quotationConfirmId != localCustQuotationConfirmTrans.QuotationConfirmId)
                {
                    _treeControl.expand(treeEntry);
                    treeEntry   = _treeControl.addItem(0,
                                                       0,
                                                       new FormTreeItem(strfmt('%1 %2 %3 %4',
                                                                                "@SYS5864",
                                                                                localCustQuotationConfirmTrans.QuotationConfirmId,
                                                                                "@SYS84648",
                                                                                localCustQuotationConfirmTrans.custQuotationConfirmJour().ProjIdRef),
                                                                            imageListAppl_Sales.image(#ImageQuotation)));
                    quotationConfirmId = localCustQuotationConfirmTrans.QuotationConfirmId;
                }

                itemName        = localCustQuotationConfirmTrans.itemName();
                itemId          = localCustQuotationConfirmTrans.ItemId;
                salesCategory   = localCustQuotationConfirmTrans.SalesCategory;
                name            = localCustQuotationConfirmTrans.Name;
                projTransType   = localCustQuotationConfirmTrans.ProjTransType;
                break;

            case tablenum(SalesQuotationLine):
                SalesQuotationLine localSalesQuotationLine = SalesQuotationLine::findRecId(_tmpFrmVirtualLines.RecordNo);
                if (quotationIdBase != localSalesQuotationLine.QuotationId)
                {
                    _treeControl.expand(treeEntry);
                    if (localSalesQuotationLine.salesQuotationTable().ProjIdRef)
                    {
                        treeEntry           = _treeControl.addItem(0,
                                                                   0,
                                                                   new FormTreeItem(strfmt('%1 %2 %3 %4',
                                                                                            "@SYS5864",
                                                                                            localSalesQuotationLine.QuotationId,
                                                                                            "@SYS84648",
                                                                                            localSalesQuotationLine.salesQuotationTable().ProjIdRef),
                                                                                        imageListAppl_Sales.image(#ImageReqRefSales)));
                    }
                    else
                    {
                        treeEntry           = _treeControl.addItem(0,
                                                                   0,
                                                                   new FormTreeItem(strfmt('%1 %2',
                                                                                            "@SYS5864",
                                                                                            localSalesQuotationLine.QuotationId),
                                                                                        imageListAppl_Sales.image(#ImageReqRefSales)));
                    }       
             
                    quotationIdBase         = localSalesQuotationLine.QuotationId;
                }
                itemName        = localSalesQuotationLine.itemName();
                itemId          = localSalesQuotationLine.ItemId;
                salesCategory   = localSalesQuotationLine.SalesCategory;
                name            = localSalesQuotationLine.Name;
                projTransType   = localSalesQuotationLine.ProjTransType;
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addItemsToTreeFromQuotationType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add items to the tree control based on the quotation type of the caller sales quotation table.
    /// </summary>
    /// <param name = "_tmpFrmVirtualLines">A <c>TmpFrmVirtual</c> used to create the items.</param>
    /// <param name = "_treeControl">The tree control to which the items are added.</param>
    protected void addItemsToTreeFromQuotationType(TmpFrmVirtual _tmpFrmVirtualLines, FormTreeControl _treeControl)
    {
        switch (callerSalesQuotationTable.QuotationType)
        {
            case QuotationType::Sales             :
                if (itemId != '')
                {
                    if (!PdsGlobal::pdsIsCWItem(custQuotationTrans.ItemId))
                    {
                        _treeControl.addItem(treeEntry,
                                                0,
                                                new FormTreeItem(strfmt('%1 %2 %3 %4',
                                                                        "@SYS14578",
                                                                        _tmpFrmVirtualLines.Qty,
                                                                        "@SYS7407",
                                                                        itemName),
                                                                    imageListAppl_Sales.image(#ImageItem)));
                    }
                    else
                    {
                        _treeControl.addItem(treeEntry,
                                                0,
                                                new FormTreeItem(strfmt('%1 %2 %3 %4',
                                                                        "@PDS1059",
                                                                        custQuotationTrans.PdsCWQty,
                                                                        "@SYS7407",
                                                                        custQuotationTrans.itemName()),
                                                                    imageListAppl_Sales.image(#ImageItem)));
                    }
                }
                else
                {
                    _treeControl.addItem(treeEntry,
                                            0,
                                            new FormTreeItem(strfmt('%1 %2 %3 %4 %5',
                                                                    "@SYS14578",
                                                                    _tmpFrmVirtualLines.Qty,
                                                                    "@SYS50039",
                                                                    EcoResCategory::find(salesCategory).Name,
                                                                    name),
                                                                imageListAppl_Sales.image(#ImageItem)));
                }
                break;

            case QuotationType::Project           :
                HcmWorkerRecId worker = ResourceFacade::getWorker(custQuotationTrans.ProjectResource);
                _treeControl.addItem(treeEntry,
                                        0,
                                        new FormTreeItem(strfmt('%1 %2 %3 %4 %5 %6',
                                                                "@SYS14578",
                                                                _tmpFrmVirtualLines.Qty,
                                                                "@SYS54161",
                                                                enum2str(projTransType),
                                                                "@SYS80392",
                                                                HcmWorker::find(worker).PersonnelNumber),
                                                            imageListAppl_Sales.image(#ImageItem)));
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTmpFrmVirtual</Name>
				<Source><![CDATA[
    internal TmpFrmVirtual parmTmpFrmVirtual(TmpFrmVirtual _tmpFrmVirtualLines = tmpFrmVirtualLinesLocal)
    {
        tmpFrmVirtualLinesLocal = _tmpFrmVirtualLines;
        return tmpFrmVirtualLinesLocal;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>