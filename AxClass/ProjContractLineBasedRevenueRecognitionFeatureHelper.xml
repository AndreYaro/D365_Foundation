<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjContractLineBasedRevenueRecognitionFeatureHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Helper class for the 'Enable multiple contract lines for a project' feature.
/// </summary>
public final class ProjContractLineBasedRevenueRecognitionFeatureHelper
{ 
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isFeatureEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the feature is enabled for given company.
    /// </summary>
    /// <param name = "_dataAreaId">Company data area ID, optional, default to current company when not specified.</param>
    /// <returns>
    /// true if the feature is enabled for the company; otherwise, false.
    /// </returns>
    [Hookable(false)]
    public static boolean isFeatureEnabled(DataAreaId _dataAreaId = curExt())
    {
        boolean ret = false;

        ProjParameters projParameters;

        if (prmIsDefault(_dataAreaId))
        {
            // Leverage the entire table cache that is set on the ProjParameters table since it is using the current company context.
            select firstonly ProjectOperationsIntegrationFeatureStatus, ContractLineBasedRevenueRecognition from projParameters;
        }
        else
        {
            select firstonly crossCompany ProjectOperationsIntegrationFeatureStatus, ContractLineBasedRevenueRecognition from projParameters
                where projParameters.DataAreaId == _dataAreaId;
        }

        ret = projParameters.ProjectOperationsIntegrationFeatureStatus && projParameters.ContractLineBasedRevenueRecognition;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setCompanyParameterStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates feature status to expected status.
    /// </summary>
    /// <param name = "_parameterFeatureStatus">Expected feature status</param>
    public static void setCompanyParameterStatus(NoYes _parameterFeatureStatus)
    {
        ttsbegin;
        ProjParameters projParameters = ProjParameters::find(true);
        projParameters.ContractLineBasedRevenueRecognition = _parameterFeatureStatus;
        projParameters.update();
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>