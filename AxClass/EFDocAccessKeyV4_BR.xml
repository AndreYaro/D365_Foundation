<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EFDocAccessKeyV4_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Utility class that generate access key for NF-e version 4.
/// </summary>
class EFDocAccessKeyV4_BR extends EFDocAccessKeyBase_BR
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates an access key.
    /// </summary>
    /// <param name="_numericCode">The numeric code for the electronic fiscal document</param>
    /// <param name="_fiscalDocument">A fiscal document associated with the NF-e</param>
    /// <returns>Generated access key</returns>
    public str generate(int _numericCode, FiscalDocument_BR _fiscalDocument)
    {
        str         key;
        FiscalEstablishment_BR fiscalEstablishment;
        LogisticsPostalAddress postalAddress;

        fiscalEstablishment = FiscalEstablishment_BR::findById(_fiscalDocument.FiscalEstablishment);
        postalAddress = fiscalEstablishment.postalAddress();

        key += substr(LogisticsAddressCity::findLogisticsAddressMap(postalAddress).IBGECode_BR, 1, 2);
        key += substr(int2str(year(_fiscalDocument.FiscalDocumentDate)), 3, 2);
        key += EFDocAccessKeyBase_BR::fillLeft(int2str(mthofyr(_fiscalDocument.FiscalDocumentDate)), 2);
        key += EFDocAccessKeyBase_BR::fillLeft(strkeep(fiscalEstablishment.CNPJCPFNum_BR, '0123456789'), 14);
        key += EFDocAccessKeyBase_BR::fillLeft(_fiscalDocument.Model, 2);
        key += EFDocAccessKeyBase_BR::fillLeft(_fiscalDocument.FiscalDocumentSeries, 3);
        key += EFDocAccessKeyBase_BR::fillLeft(_fiscalDocument.FiscalDocumentNumber, 9);
        key += EFDocAccessKeyBase_BR::fillLeft(int2str(EFDocContingencyMode_BR::getCurrentContingencyMode(_fiscalDocument.FiscalEstablishment).Mode), 1);
        key += EFDocAccessKeyBase_BR::fillLeft(int2str(_numericCode), 8);

        key += int2str(EFDocAccessKeyBase_BR::generateVerifierDigit(key));

        return key;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Instantiates a EFDocAccessKeyV4_BR object.
    /// </summary>
    /// <returns>An <c>EFDocAccessKeyV4_BR object</c>.</returns>
    public static EFDocAccessKeyV4_BR construct()
    {
        return new EFDocAccessKeyV4_BR();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>