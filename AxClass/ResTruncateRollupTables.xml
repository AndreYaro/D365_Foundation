<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResTruncateRollupTables</Name>
	<SourceCode>
		<Declaration><![CDATA[
class ResTruncateRollupTables
{        
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the class to truncate resource capacity roll-up tables data.
    /// </summary>
    /// <param name = "_args">No arguments will be needed.</param>
    public static void main(Args _args)
    {      
        ResRollup resRollup;
        int resRollupTableId = resRollup.TableId;
        str resRollupTableName = tableId2Name(resRollupTableId);

        ResRollupCalendarShared resRollupCalendar;
        int resRollupCalendarTableId = resRollupCalendar.TableId;
        str resRollupCalendarTableName = tableId2Name(resRollupCalendarTableId);

        if (Box::yesNo((
                strFmt("@Res:ValidateRollupTablesTruncation",
                    resRollupTableName,
                    resRollupCalendarTableName)
                    ), DialogButton::No) == DialogButton::Yes)
        {
            str deleteStatement = 'TRUNCATE TABLE ' + resRollupTableName + ' TRUNCATE TABLE ' + resRollupCalendarTableName;
            
            ttsbegin;
            Connection userConnection = new Connection();
            Statement stmt = userConnection.createStatement();
            new SqlStatementExecutePermission(deleteStatement).assert();
            stmt.executeUpdate(deleteStatement);
            CodeAccessPermission::revertAssert();
            ttscommit;

            select firstonly RecId from resRollup;
            boolean truncateRollUpSucceed = resRollup.RecId ? false : true;

            if (truncateRollUpSucceed)
            {
                info(strFmt("@Res:TruncateTableSucceed", resRollupTableName));
            }
            else
            {
                warning(strFmt("@Res:TruncateTableFail", resRollupTableName));
            }

            select firstonly RecID from resRollupCalendar;
            boolean truncateRollUpCalendarSucceed = resRollupCalendar.RecId ? false : true;

            if (truncateRollUpCalendarSucceed)
            {
                info(strFmt("@Res:TruncateTableSucceed", resRollupCalendarTableName));
            }
            else
            {
                warning(strFmt("@Res:TruncateTableFail", resRollupCalendarTableName));
            }

            if (truncateRollUpSucceed && truncateRollUpCalendarSucceed)
            {
                info("@Res:TruncateTablesSucceed");
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>