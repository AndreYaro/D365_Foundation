<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocSalesLineRowDataProvider_IN_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(TaxableDocSalesLineRowDataProvider))]
final class TaxableDocSalesLineRowDataProvider_IN_Extension
{
    #ISOCountryRegionCodes

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillInFields</Name>
				<Source><![CDATA[
    public void fillInFields(TaxableDocumentLineObject _lineObj)
    {
        next fillInFields(_lineObj);

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            if (this.isCustomsExportOrder_IN())
            {
                this.addFieldValue(_lineObj, TaxEngineModelFields::ExportCustomsTariffCode, indirectTaxModelTaxable.getCustomsTariffCodeTable().TariffCode);
            }

            if (gstTaxable)
            {
                Debug::assert(gstTaxable is TaxModelDocLineBaseImpl_IN);
                TaxModelDocLineBaseImpl_IN taxModelDocLineBaseImpl_IN = gstTaxable;
                //GST fields
                this.addFieldValue(_lineObj, TaxEngineModelFields::IsScrap, gstTaxable.getGSTScrap());
                this.addFieldValue(_lineObj, TaxEngineModelFields::DeemedExport, taxModelDocLineBaseImpl_IN.getIsDeemedExport());
                this.addFieldValue(_lineObj, TaxEngineModelFields::SEZParty, taxModelDocLineBaseImpl_IN.getIsSEZParty());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValidFields</Name>
				<Source><![CDATA[
    protected void initValidFields()
    {
        next initValidFields();

        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]))
        {
            validFields.add(TaxEngineModelFields::ExportCustomsTariffCode, Types::String);
            validFields.add(TaxEngineModelFields::IsScrap, Types::Enum, enumNum(NoYes));
            validFields.add(TaxEngineModelFields::DeemedExport, Types::Enum, enumNum(NoYes));
            validFields.add(TaxEngineModelFields::SEZParty, Types::Enum, enumNum(NoYes));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isCustomsExportOrder_IN</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if this is an import order or not.
    /// </summary>
    /// <returns>
    /// True if this transaction is an import order; otherwise, false.
    /// </returns>
    protected boolean isCustomsExportOrder_IN()
    {
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoIN]) && salesLine && salesLine.salesTable().CustomsExportOrder_IN)
        {
            return true;
        }
        else
        {
            return next isCustomsExportOrder_IN();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>