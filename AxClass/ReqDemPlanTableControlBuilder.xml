<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqDemPlanTableControlBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ReqDemPlanTableControlBuilder</c> class is responsible for setting the content of the table control
/// based on provided parameters.
/// </summary>
public class ReqDemPlanTableControlBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildFromMatrix</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the content of the table control based on the provided data matrix.
    /// </summary>
    /// <param name = "_table">The table control to update.</param>
    /// <param name = "_matrix">The data matrix.</param>
    public static void buildFromMatrix(FormTableControl _table, ReqDemPlanMatrix _matrix)
    {
        MapEnumerator       cellMatrixEnum;
        List                rowLabels, columnLabels;
        ListEnumerator      listEnum;
        int                 enumCounter = 1;
        container           coordinates;

        ReqDemPlanTableControlBuilder::clearTableControl(_table);

        if (_table != null && _matrix!= null && !_matrix.parmRowLabels().empty())
        {
            //Create columns
            columnLabels = _matrix.parmColumnLabels();
            _table.insertCols(0, columnLabels.elements());
            listEnum = columnLabels.getEnumerator();
            while (listEnum.moveNext())
            {
                _table.setColLabel(enumCounter, listEnum.current());
                enumCounter++;
            }

            //Create rows
            rowLabels = _matrix.parmRowLabels();
            _table.insertRows(0, rowLabels.elements());
            enumCounter = 1;
            listEnum = rowLabels.getEnumerator();
            while (listEnum.moveNext())
            {
                _table.setRowLabel(enumCounter, listEnum.current());
                enumCounter++;
            }

            //Insert Data
            cellMatrixEnum = _matrix.parmCells().getEnumerator();

            while (cellMatrixEnum.moveNext())
            {
                coordinates = cellMatrixEnum.currentKey();
                ReqDemPlanMatrixCell cell = cellMatrixEnum.currentValue();

                _table.cell(conPeek(coordinates, 2), conPeek(coordinates, 1)).data(cell.value());
            }

            _table.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>clearTableControl</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears table control.
    /// </summary>
    /// <param name = "_table">The table control to update.</param>
    public static void clearTableControl(FormTableControl _table)
    {
        if (_table != null)
        {
            _table.clear();
            _table.deleteRows(0, _table.rows());
            _table.deleteCols(0, _table.columns());
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>