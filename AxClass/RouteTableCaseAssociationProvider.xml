<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RouteTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Route'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class RouteTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        RouteTable routeTable;
        select crosscompany DataAreaId from routeTable where routeTable.RecId == _parentRecId;

        return routeTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        RouteTable routeTable;
        select crosscompany RouteId from routeTable where routeTable.RecId == _parentRecId;

        return routeTable.RouteId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        RouteTable routeTable;
        select RecId from routeTable where routeTable.RouteId == _naturalKey;

        return routeTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        RouteTable routeTable;
        select crosscompany Name from routeTable where routeTable.RecId == _parentRecId;

        return routeTable.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        RouteTable routeTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        RouteTable routeTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from routeTableCrossCompany
            where routeTableCrossCompany.RecId == _association.RefRecId;

        if (routeTableCrossCompany.Recid)
        {
            changecompany (routeTableCrossCompany.DataAreaId)
            {
                select firstonly routeTable where routeTable.RecId == _association.RefRecId;
            }
        }

        _args.record(routeTable);
        return menuitemdisplaystr(RouteTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(RouteTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(RouteTable));
        oSysTableLookup.addLookupfield(fieldnum(RouteTable, RouteId));
        oSysTableLookup.addLookupfield(fieldnum(RouteTable, Name));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(Route)
                || _refTableId == tablenum(RouteTable)
                || _refTableId == tablenum(RouteVersion))
        {
            _eventArgs.parmEntityType(CaseEntityType::Route);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>