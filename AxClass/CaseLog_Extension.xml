<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CaseLog_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
public static class CaseLog_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>editSourceLinkedId</Name>
				<Source><![CDATA[
    public static edit CaseSourceLinkId editSourceLinkedId(CaseLog _log = null, boolean _set = false, CaseSourceLinkId _linkId = '')
    {
        FieldId                 fieldId;
        TableId                 tableId;
        DictTable               dictTable;
        Common                  common;
        smmSourceTypeOptions    sourceOptionTable;

        tableId = smmSourceType::type2TableId(smmSourceTypeTable::find(_log.SourceTypeId).Type);
        fieldId = smmSourceType::tableId2FieldId(tableId);
        dictTable = new DictTable(tableId);

        if (dictTable && dictTable.rights() >= AccessType::View)
        {
            common = dictTable.makeRecord();
        }
        else
        {
            return '';
        }

        if (_set)
        {
            if (_linkId)
            {
                select common where common.(fieldId) == _linkId;
                if (common)
                {
                    _log.SourceRefRecId = common.RecId;
                }
                else
                {
                    // Verify if Allow-free form text is set for User-defined type
                    if (common.TableId == tableNum(smmSourceTypeOptions) && smmSourceTypeTable::find(_log.SourceTypeId).AllowFreeformText)
                    {
                        sourceOptionTable = smmSourceTypeOptions::createFromAllowFreeFormText(_log.SourceTypeId, _linkId);
                        _log.SourceRefRecId = sourceOptionTable.RecId;
                    }
                    else
                    {
                        _log.orig();
                        checkFailed(strFmt("@SYS31784",_linkId,fieldId2pname(tableId,fieldId),tableId2pname(tableId)));
                    }
                }
            }
            else
            {
                _log.SourceRefRecId = 0;
            }
        }
        else if (_log.SourceRefRecId)
        {
            select common where common.RecId == _log.SourceRefRecId;
            if (!common)
            {
                _log.SourceRefRecId = 0;
            }
        }

        if (common)
        {
            return common.(fieldId);
        }
        else
        {
            return '';
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>