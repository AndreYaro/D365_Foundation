<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DimensionAttributeExtensionProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Provides access to dimension attribute table extension related methods.
/// </summary>
class DimensionAttributeExtensionProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>balancingDimensionsExist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if balancing dimensions exist for the current ledger.
    /// </summary>
    /// <returns>true if balancing dimensions exist; otherwise, false.</returns>
    public static boolean balancingDimensionsExist()
    {
        container cachedResult;
        LedgerRecId ledgerRecId = Ledger::current();

        cachedResult = DimensionCache::getValue(
            DimensionCacheScope::BalancingDimensionsExist,
            [ledgerRecId]);

        if (cachedResult == conNull())
        {
            DimensionAttribute dimAttribute;
            DimensionAttributeSetItem dimAttributeSetItem;
            boolean balancingDimensionExists;
            DimensionEnumeration dimensionEnumeration = DimensionCache::getDimensionAttributeSetForLedger();

            // See if any balancing dimensions exist for the current ledger.
            select firstOnly RecId from dimAttribute where
                dimAttribute.IsBalancing_PSN == true
                exists join dimAttributeSetItem where
                    dimAttributeSetItem.DimensionAttributeSet == dimensionEnumeration &&
                    dimAttributeSetItem.DimensionAttribute == dimAttribute.RecId;

            // If any dimension attributes were found, then balancing dimensions do exist for the current ledger.
            balancingDimensionExists = dimAttribute.RecId != 0;

            // See if any balancing dimension values exist for the current ledger.
            if (!balancingDimensionExists)
            {
                DimensionAttributeValue dimensionAttributeValue;

                select firstOnly RecId from dimensionAttributeValue where
                    dimensionAttributeValue.IsBalancing_PSN == true
                    exists join dimAttributeSetItem where
                        dimAttributeSetItem.DimensionAttributeSet == dimensionEnumeration &&
                        dimAttributeSetItem.DimensionAttribute == dimensionAttributeValue.DimensionAttribute;

                balancingDimensionExists = dimensionAttributeValue.RecId != 0;
            }

            cachedResult = [balancingDimensionExists];

            // Cache the result.
            DimensionCache::insertValue(
                DimensionCacheScope::BalancingDimensionsExist,
                [ledgerRecId],
                cachedResult);
        }

        return conPeek(cachedResult, 1);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>