<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventCountingReasonCodePolicyAssignerInventTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventCountingReasonCodePolicyAssignerInventTable</c> class is used for assigning counting reason code for an item.
/// </summary>
[SysTableNameFactory(tableStr(InventTable))]
public class InventCountingReasonCodePolicyAssignerInventTable extends InventCountingReasonCodePolicyAssigner
{
    private InventTable inventTable; 

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initiates invent table buffer.
    /// </summary>
    /// <param name = "_common">
    /// <c>Common</c> buffer used for this instance of the table.
    /// </param>
    public void initFromBuffer(Common _common)
    {
        inventTable = _common as InventTable;

        if (!inventTable.ItemId)
        {
            this.throwRequiredFieldIdHasNoValue(fieldId2PName(tableNum(InventTable),fieldNum(InventTable, ItemId)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>assignPolicy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Assigns counting reason code policy rec id to an item.
    /// </summary>
    /// <param name = "_inventCountingReasonCodePolicyRecId">
    /// The counting reason code policy rec id.
    /// </param>
    /// <returns>
    /// true if the <c>InventCountingReasonCodePolicyRecId</c> is updated on the item; otherwise false
    /// </returns>
    public boolean assignPolicy(InventCountingReasonCodePolicyRecId _inventCountingReasonCodePolicyRecId)
    {
        ttsbegin;

        InventTableInventCountingReasonCodePolicy inventTableInventCountingReasonCodePolicy = InventTableInventCountingReasonCodePolicy::findByItem(inventTable.ItemId, true);

        boolean ret;

        if (_inventCountingReasonCodePolicyRecId != inventTableInventCountingReasonCodePolicy.CountingReasonCodePolicy)
        {
            ret = true;

            if (_inventCountingReasonCodePolicyRecId != 0)
            {
                if (!inventTableInventCountingReasonCodePolicy)
                {
                    inventTableInventCountingReasonCodePolicy.ItemId = inventTable.ItemId;
                }

                inventTableInventCountingReasonCodePolicy.CountingReasonCodePolicy = _inventCountingReasonCodePolicyRecId;
                inventTableInventCountingReasonCodePolicy.write();
            }
            else
            {
                inventTableInventCountingReasonCodePolicy.delete();
            }
        }

        ttscommit;

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventCountingReasonCodePolicyRecId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the counting reason code policy for the selected item.
    /// </summary>
    /// <returns>
    /// The counting reason code policy rec id assigned to the item.
    /// </returns>
    public InventCountingReasonCodePolicyRecId inventCountingReasonCodePolicyRecId()
    {
        return InventTableInventCountingReasonCodePolicy::findByItem(inventTable.ItemId).CountingReasonCodePolicy;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>