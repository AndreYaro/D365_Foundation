<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BOMAndVersionCreate</Name>
	<SourceCode>
		<Declaration><![CDATA[
public final class BOMAndVersionCreate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (!_args.parmObject() || !(_args.parmObject() is BOMAndVersionCreateParms))
        {
            throw error(Error::wrongUseOfFunction(classStr(BOMAndVersionCreate)));
        }

        var createParms = _args.parmObject() as BOMAndVersionCreateParms;

        bomId newBOMId;
        InventSiteId newInventSiteId;
        boolean dialogCopy;

        [newBOMId, newInventSiteId, dialogCopy] = BOMCreate::newBOMCreateDialog().promptCreateBOM(
                createParms.parmInventTable().ItemId,
                createParms.parmCopyBomFromBomId(),
                createParms.parmFromSiteId(),
                createParms.parmIsFormula());

        if (newBOMId)
        {
            BOMTable newBOMTable = BOMTable::find(newBOMId);
            bomVersion newBomVersion;
            newBomVersion.initValue();
            newBomVersion.initFromBOMTable(newBOMTable);
            newBomVersion.initFromInventTable(createParms.parmInventTable());
            newBomVersion.insert();

            if (dialogCopy && createParms.parmIsFormula())
            {
                PmfFormulaCoBy::postUpdateBOMVersion(newBomVersion);
            }

            createParms.setOutParms(newBOMTable, newBomVersion);

            if (createParms.parmOpenBomTableAfterSuccessfulCreation())
            {
                BOMAndVersionCreate::openBomTable(newBOMTable);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>openBomTable</Name>
				<Source><![CDATA[
    private static void openBomTable(BOMTable _bomTable)
    {
        Args argsBomTable = new Args();
        argsBomTable.lookupRecord(_bomTable);
        argsBomTable.formViewOption(FormViewOption::Details);
        argsBomTable.openMode(OpenMode::Edit);
        MenuFunction menuFunctionBomTable = new MenuFunction(menuItemDisplayStr(PmfBomFormulaGoToMainTable), MenuItemType::Display);
        menuFunctionBomTable.run(argsBomTable);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>