<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmEmpoymentHistoryApplicationSuiteEventHandlers</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>HcmEmpoymentHistoryApplicationSuiteEventHandlers</c> class contains event handlers for the <c>HcmEmploymentHistory</c> form.
/// </summary>
public final class HcmEmpoymentHistoryApplicationSuiteEventHandlers
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>WorkCalendarEmploymentDS_InitValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post event handler for <c>WorkCalendarEmployment.InitValue</c>.
    /// </summary>
    /// <param name = "_formDS">
    /// The form datasource that raised the event.
    /// </param>
    /// <param name = "_eventArgs">
    /// The args associated to the event.
    /// </param>
    [
    FormDataSourceEventHandler(formDataSourceStr(HcmEmploymentHistory, WorkCalendarEmployment), FormDataSourceEventType::InitValue),
    SysObsolete('This handler was replaced by a handler on the Created event of the WorkCalendarEmployment data source', true, 03\03\2023)
    ]
    public static void WorkCalendarEmploymentDS_InitValue(FormDataSource _formDS, FormDataSourceEventArgs _eventArgs)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>WorkCalendarEmploymentDS_Created</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post event handler for <c>WorkCalendarEmployment.Created</c>.
    /// </summary>
    /// <param name = "_formDS">
    /// The form datasource that raised the event.
    /// </param>
    /// <param name = "_eventArgs">
    /// The args associated to the event.
    /// </param>
    [FormDataSourceEventHandler(formDataSourceStr(HcmEmploymentHistory, WorkCalendarEmployment), FormDataSourceEventType::Created)]
    public static void WorkCalendarEmploymentDS_Created(FormDataSource _formDS, FormDataSourceEventArgs _eventArgs)
    {
        WorkCalendarEmployment workCalendarEmployment =_formDS.cursor();
        HcmEmployment hcmEmployment = _formDS.formRun().dataSource(formDataSourceStr(HcmEmploymentHistory, HcmEmployment)).cursor();

        if (workCalendarEmployment.Employment)
        {
            workCalendarEmployment.Employment = hcmEmployment.RecId;
            workCalendarEmployment.CalendarDataAreaId = CompanyInfo::getDataArea(hcmEmployment.LegalEntity);

            _formDS.forceWrite(true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmEmploymentDS_Activated</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post event handler for <c>HcmEmployment.Activated</c>.
    /// </summary>
    /// <param name = "_formDS">
    /// The form datasource that raised the event.
    /// </param>
    /// <param name = "_eventArgs">
    /// The args associated to the event.
    /// </param>
    [FormDataSourceEventHandler(formDataSourceStr(HcmEmploymentHistory, HcmEmployment), FormDataSourceEventType::Activated)]
    public static void HcmEmploymentDS_Activated(FormDataSource _formDS, FormDataSourceEventArgs _eventArgs)
    {
        HcmEmployment employment = _formDS.cursor();
        boolean isEmploymentInCurrentCompany = CompanyInfo::current() == employment.LegalEntity;

        FormRun historyFormRun = _formDS.formRun();
        FormStringControl calendarIdControl = historyFormRun.control(
            historyFormRun.controlId(formControlStr(HcmEmploymentHistory, WorkCalendarEmployment_CalendarId))) as FormStringControl;

        calendarIdControl.allowEdit(isEmploymentInCurrentCompany);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>