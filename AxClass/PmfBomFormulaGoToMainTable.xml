<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PmfBomFormulaGoToMainTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class selects the correct user interface to show depending
/// if the source record is a BOM or a formula.
/// </summary>
public class PmfBomFormulaGoToMainTable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method selects the correct user interface to show depending
    /// if the source record is a BOM or a formula.
    /// </summary>
    /// <param name="_args">
    /// Any arguments passed
    /// </param>
    public static void main(Args _args)
    {
        BOMTable              bomTable;
        FormControl           callerControl;
        BOMId                 bomId;
        Args                  args = _args;
        Object                objCaller;
        #define.pmfMethodStr('pmfIsEnabled')

        if (!_args.lookupRecord())
        {
            callerControl = SysTableLookup::getCallerControl(_args);

            if (callerControl is FormStringControl)
            {
                bomId = callerControl.valueStr();
            }

            if (!bomId)
            {
                bomId = _args.lookupValue();
            }

            bomTable = BOMTable::find(bomId);
        }
        else
        {
            bomTable = _args.lookupRecord();
            bomId = bomTable.BOMId;
        }

        if (#PmfEnabled && bomTable.RecId)
        {
            if (!hasMenuItemAccess(menuitemdisplaystr(BomTable), MenuItemType::Display))
            {
                throw error(strfmt("@SYS339383", MenuItemType::Display, menuitemdisplaystr(BomTable)));
            }

            switch (bomTable.PmfBOMFormula)
            {
                case PmfBOMFormula::BOM:
                    new MenuFunction(
                            menuitemdisplaystr(BomTable),
                            MenuItemType::Display).run(_args);
                    break;

                case PmfBOMFormula::Formula :
                    _args.record(bomTable);
                    new MenuFunction(
                            menuitemdisplaystr(PmfFormulaTable),
                            MenuItemType::Display).run(_args);
                    break;
            }
        }
        else
        {
            try
            {
                objCaller = _args.caller();
                if ((objCaller is SysFormRun)
                    && SysFormRun::hasMethod(objCaller,#pmfMethodStr)
                    && objCaller.pmfIsEnabled())
                {
                    args.menuItemName(menuitemdisplaystr(PmfFormulaTable));
                    new MenuFunction(
                            menuitemdisplaystr(PmfFormulaTable),
                            MenuItemType::Display).run(args);
                }
                else
                {
                    args.menuItemName(menuitemdisplaystr(BomTable));
                    new MenuFunction(
                        menuitemdisplaystr(BomTable),
                        MenuItemType::Display).run(_args);
                }
            }
            catch (Exception::Error)
            {
                new MenuFunction(
                        menuitemdisplaystr(BomTable),
                        MenuItemType::Display).run(_args);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>