<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PCRuntimeXmlParseEnumDomain</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PCRuntimeXmlParseEnumDomain</c> class parses a runtime XML for enumeration domain.
/// </summary>
class PCRuntimeXmlParseEnumDomain extends PCRuntimeXmlParseBase
{
    #PC

    PCRuntimeXmlParseEnumDomainValue    runtimeXmlParseEnumDomainValue;
    PCRuntimeCacheController            runtimeCacheController;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isCachedCandidate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if the enum domain qualifies for being cached.
    /// </summary>
    /// <param name="_enumDomainId">
    /// An enum domain ID.
    /// </param>
    /// <returns>
    /// True if the enum domain could potentially be cached; false, otherwise.
    /// </returns>
    protected boolean isCachedCandidate(PCName _enumDomainId)
    {
        #PCTableConstraint

        return strStartsWith(_enumDomainId, #TableConstraintEnumPrefix);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        runtimeXmlParseEnumDomainValue  = PCRuntimeXmlParseEnumDomainValue::construct();
        runtimeCacheController          = PCRuntimeCacheController::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRuntimeCacheController</Name>
				<Source><![CDATA[
        public PCRuntimeCacheController parmRuntimeCacheController(PCRuntimeCacheController _runtimeCacheController = runtimeCacheController)
    {
        runtimeCacheController = _runtimeCacheController;

        return runtimeCacheController;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmRuntimeXmlParseEnumDomainValue</Name>
				<Source><![CDATA[
        public PCRuntimeXmlParseEnumDomainValue parmRuntimeXmlParseEnumDomainValue(PCRuntimeXmlParseEnumDomainValue _runtimeXmlParseEnumDomainValue = runtimeXmlParseEnumDomainValue)
    {
        runtimeXmlParseEnumDomainValue = _runtimeXmlParseEnumDomainValue;

        return runtimeXmlParseEnumDomainValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parse</Name>
				<Source><![CDATA[
        public void parse(XmlElement _xmlElement, PCRuntimeInterface _runtimeParent)
    {
        PCRuntimeModel runtimeModel = _runtimeParent as PCRuntimeModel;

        this.parseEnumDomainDetails(_xmlElement, runtimeModel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parseEnumDomainDetails</Name>
				<Source><![CDATA[
    /// <summary>
    /// Parses the details of the enum domain.
    /// </summary>
    /// <param name="_xmlElement">
    /// The xml element for the enum domain.
    /// </param>
    /// <param name="_runtimeModel">
    /// The runtime model data structure class.
    /// </param>
    protected void parseEnumDomainDetails(XmlElement _xmlElement, PCRuntimeModel _runtimeModel)
    {
        container packedEnumDomain;

        PCRuntimeEnumDomain runtimeEnumDomain = PCRuntimeEnumDomain::construct();

        runtimeEnumDomain.parmEnumDefinitionId(_xmlElement.getAttribute(#enumDomainId));

        _runtimeModel.addEnumDomain(runtimeEnumDomain);

        if (this.isCachedCandidate(runtimeEnumDomain.parmEnumDefinitionId()))
        {
            packedEnumDomain = runtimeCacheController.getRuntimePackedClass(runtimeEnumDomain.parmEnumDefinitionId());

            if (packedEnumDomain != conNull())
            {
                runtimeEnumDomain.unpack(packedEnumDomain);
            }
            else
            {
                this.parseXmlElementCollection(_xmlElement, #EnumValueElement, runtimeXmlParseEnumDomainValue, runtimeEnumDomain);
                runtimeCacheController.createObjectEntry(runtimeEnumDomain.parmEnumDefinitionId(), runtimeEnumDomain.pack());
            }
        }
        else
        {
            this.parseXmlElementCollection(_xmlElement, #EnumValueElement, runtimeXmlParseEnumDomainValue, runtimeEnumDomain);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static PCRuntimeXmlParseEnumDomain construct()
    {
        return new PCRuntimeXmlParseEnumDomain();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>