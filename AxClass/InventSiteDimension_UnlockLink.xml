<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventSiteDimension_UnlockLink</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class InventSiteDimension_UnlockLink
{
    InventParameters    inventParameters;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        inventParameters    = InventParameters::find();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount

        if (!this.validate())
        {
            return;
        }

        try
        {
            ttsbegin;

            inventParameters = InventParameters::find(true);
            inventParameters.MultiSiteDimensionLink = InventMultiSiteDimensionLink::Active;
            inventParameters.update();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate()
    {
        boolean ret = true;

        if (ret && !inventParameters.allowUnlockDimensionLink())
        {
            ret = checkFailed("@SYS109419");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static InventSiteDimension_UnlockLink construct()
    {
        return new InventSiteDimension_UnlockLink();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        InventSiteDimension_UnlockLink      inventSiteDimension_UnlockLink = InventSiteDimension_UnlockLink::construct();

        inventSiteDimension_UnlockLink.run();

        if (args)
        {
            Common common = args.record();
            if (FormDataUtil::isFormDataSource(common))
            {
                FormDataSource formDataSource = FormDataUtil::getFormDataSource(common);
                formDataSource.executeQuery();
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
