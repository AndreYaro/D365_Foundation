<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CostEntrySource</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CostEntrySource</c> class represents the source of the cost for a <c>CostEntry</c>.
/// </summary>
public class CostEntrySource
{
    RefTableId          sourceRelationType;
    RefRecId            sourceRecId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>key</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a unique key for the instance of <c>CostEntrySource</c> class.
    /// </summary>
    /// <returns>
    /// A unique key for the instance of <c>CostEntrySource</c> class.
    /// </returns>
    public str key()
    {
        return int2str(sourceRelationType) + '#' + int642str(sourceRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceRecId</Name>
				<Source><![CDATA[
    public RefRecId parmSourceRecId(RefRecId _sourceRecId = sourceRecId)
    {
        sourceRecId = _sourceRecId;

        return sourceRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSourceRelationType</Name>
				<Source><![CDATA[
    public RefTableId parmSourceRelationType(RefTableId _sourceRelationType = sourceRelationType)
    {
        sourceRelationType = _sourceRelationType;

        return sourceRelationType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    protected static CostEntrySource construct()
    {
        return new CostEntrySource();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCommon</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>CostEntrySource</c> class for a <c>Common</c> buffer.
    /// </summary>
    /// <param name="_common">
    /// The <c>Common</c> buffer.
    /// </param>
    /// <returns>
    /// An instance of the <c>CostEntrySource</c> class
    /// </returns>
    public static CostEntrySource newCommon(Common _common)
    {
        return CostEntrySource::newParameters(_common.TableId, _common.RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newCostEntryTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>CostEntrySource</c> class for a <c>CostEntryTable</c> buffer.
    /// </summary>
    /// <param name="_entry">
    /// The <c>CostEntryTable</c> buffer.
    /// </param>
    /// <returns>
    /// An instance of the <c>CostEntrySource</c> class
    /// </returns>
    public static CostEntrySource newCostEntryTable(CostEntryTable _entry)
    {
        return CostEntrySource::newParameters(_entry.SourceRelationType, _entry.SourceMarkupTrans);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>CostEntrySource</c> class for a <c>RefTableId</c>, <c>RefRecId</c> relation.
    /// </summary>
    /// <param name="_sourceRelationType">
    /// The <c>RefTableId</c> for the source relation.
    /// </param>
    /// <param name="_sourceRecId">
    /// The <c>RefRecId</c>for the source relation.
    /// </param>
    /// <returns>
    /// An instance of the <c>CostEntrySource</c> class.
    /// </returns>
    protected static CostEntrySource newParameters(RefTableId _sourceRelationType, RefRecId _sourceRecId)
    {
        CostEntrySource             source;

        str key = int642str(_sourceRelationType) + '#' + int642str(_sourceRecId);

        source = SysTransactionScopeCache::get(classStr(CostEntrySource), key, source);

        if (!source)
        {
            source = CostEntrySource::construct();
            source.parmSourceRelationType(_sourceRelationType);
            source.parmSourceRecId(_sourceRecId);
            SysTransactionScopeCache::set(classStr(CostEntrySource), key, source);
        }

        return source;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
