<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSLicensePlateLabelBuildConfigForm</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSLicensePlateLabelBuildConfigForm</c> class is used to create and update form controls for <c>WHSLicensePlateLabelBuildConfig</c> form.
/// </summary>
internal class WHSLicensePlateLabelBuildConfigForm
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillAvailable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fill available fields control for <c>WHSLicensePlateLabelBuildConfig</c> form.
    /// </summary>
    /// <param name = "_availableFields">List control for available fields.</param>
    /// <param name = "_licensePlateLabelBuildConfig">Instance of <c>WHSLicensePlateLabelBuildConfig</c></param>
    /// <param name = "_listOfFieldsToHideFromUI">Field list to hide from UI.</param>
    [Hookable(false)]
    internal static void fillAvailable(FormListControl _availableFields, WHSLicensePlateLabelBuildConfig _licensePlateLabelBuildConfig, container _listOfFieldsToHideFromUI)
    {
        SysDictTable    dictTableLicensePlateLabel;
        Set             setLicensePlateLabelFields;

        FormListItem  formListItem;
        
        _availableFields.deleteAll();

        if (!dictTableLicensePlateLabel || !setLicensePlateLabelFields)
        {
            dictTableLicensePlateLabel = SysDictTable::newName(tableStr(WHSLicensePlateLabel));
            setLicensePlateLabelFields = dictTableLicensePlateLabel.fields();
        }
                
        SetEnumerator se = setLicensePlateLabelFields.getEnumerator();
        while (se.moveNext())
        {
            SysDictField df = se.current();

            if (df.isSystem()
                || df.mandatory()
                || (conFind(_listOfFieldsToHideFromUI, df.name()) > 0)
                || WHSLicensePlateLabelBuildConfigSkipField::exist(_licensePlateLabelBuildConfig.RecId, df.name()))
            {
                continue;
            }
            
            formListItem = new FormListItem(strFmt('%1 (%2)', df.label(), df.name()), 0, df.name());
            _availableFields.addItem(formListItem);
        }

        // Add field group : To Address
        if (!WHSLicensePlateLabelBuildConfigSkipField::exist(_licensePlateLabelBuildConfig.RecId, WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress))
        {
            formListItem = new FormListItem(strFmt('%1 (%2)', "@WAX1371", WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress);
            _availableFields.addItem(formListItem);
        }

        // Add field group : From Address
        if (!WHSLicensePlateLabelBuildConfigSkipField::exist(_licensePlateLabelBuildConfig.RecId, WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress))
        {
            formListItem = new FormListItem(strFmt('%1 (%2)', "@WAX1377", WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress);
            _availableFields.addItem(formListItem);
        }
           
        // Add field group : Tracking dimensions
        if (!WHSLicensePlateLabelBuildConfigSkipField::exist(_licensePlateLabelBuildConfig.RecId, WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions))
        {
            formListItem = new FormListItem(strFmt('%1 (%2)', "@SYS317798", WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions);
            _availableFields.addItem(formListItem);
        }

        // Add field group : Product dimensions
        if (!WHSLicensePlateLabelBuildConfigSkipField::exist(_licensePlateLabelBuildConfig.RecId, WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions))
        {
            formListItem = new FormListItem(strFmt('%1 (%2)', "@SYS70071", WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions);
            _availableFields.addItem(formListItem);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillSelected</Name>
				<Source><![CDATA[
    /// <summary>
    /// Fill selected fields control for <c>WHSLicensePlateLabelBuildConfig</c> form.
    /// </summary>
    /// <param name = "_selectedFields">List control for selected fields.</param>
    /// <param name = "_licensePlateLabelBuildConfig">Instance of <c>WHSLicensePlateLabelBuildConfig</c></param>
    [Hookable(false)]
    internal static void fillSelected(FormListControl _selectedFields, WHSLicensePlateLabelBuildConfig _licensePlateLabelBuildConfig )
    {
        TableId  tableIdWHSLicensePlateLabel = tableNum(WHSLicensePlateLabel);
        _selectedFields.deleteAll();
        
        FormListItem                             formListItem;
        WHSLicensePlateLabelBuildConfigSkipField skipList;
        
        while select SkipFieldName from skipList
            where skipList.LicensePlateLabelBuildConfigRecId == _licensePlateLabelBuildConfig.RecId
        {
            switch(skipList.SkipFieldName)
            {
                case WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress:
                    formListItem = new FormListItem(strFmt('%1 (%2)', "@WAX1377", WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupFromAddress);
                    break;
                case WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress:
                    formListItem = new FormListItem(strFmt('%1 (%2)', "@WAX1371", WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupToAddress);
                    break;
                case WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions:
                    formListItem = new FormListItem(strFmt('%1 (%2)', "@SYS70071", WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupProductDimensions);
                    break;
                case WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions:
                    formListItem = new FormListItem(strFmt('%1 (%2)', "@SYS317798", WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions), 0, WhsLicensePlateLabelBuildConfiguration::FieldGroupTrackingDimensions);
                    break;
                default:
                    formListItem = new FormListItem(strFmt('%1 (%2)', fieldId2PName(tableIdWHSLicensePlateLabel, fieldName2Id(tableIdWHSLicensePlateLabel, skipList.SkipFieldName)),  skipList.SkipFieldName), 0, skipList.SkipFieldName);
                    break;
            }

            _selectedFields.addItem(formListItem);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>