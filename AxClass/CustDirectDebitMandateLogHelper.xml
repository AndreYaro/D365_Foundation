<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustDirectDebitMandateLogHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustDirectDebitMandateLogHelper</c> class manages the logging of data of recurring SEPA Direct Debit payments.
/// </summary>
public class CustDirectDebitMandateLogHelper
{
    RefRecId custDirectDebitMandateLogRecId;
    RecordInsertList recordInsertList;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>logBankAccountDirectDebitId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>CustDirectDebitMandateLogRecord</c> record to recordInsertList with BankAccountDirectDebitId key.
    /// </summary>
    /// <param name="_value">
    /// The value of Bank account Direct Debit Id.
    /// </param>
    public void logBankAccountDirectDebitId(CustDirectDebitMandateLogRecordValue _value)
    {
        this.logKeyValue(CustDirectDebitMandateLogRecordKey::BankAccountDirectDebitId, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logBankAccountSwift</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>CustDirectDebitMandateLogRecord</c> record to recordInsertList with BankAccountSwift key.
    /// </summary>
    /// <param name="_value">
    /// The value of Bank account Swift No.
    /// </param>
    public void logBankAccountSwift(CustDirectDebitMandateLogRecordValue _value)
    {
        this.logKeyValue(CustDirectDebitMandateLogRecordKey::BankAccountSwift, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logCustomerBankAccountIBAN</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>CustDirectDebitMandateLogRecord</c> record to recordInsertList with CustomerBankAccountIBAN key.
    /// </summary>
    /// <param name="_value">
    /// The value of Customer's bank account IBAN.
    /// </param>
    public void logCustomerBankAccountIBAN(CustDirectDebitMandateLogRecordValue _value)
    {
        this.logKeyValue(CustDirectDebitMandateLogRecordKey::CustomerBankAccountIBAN, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logCustomerBankAccountSwift</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>CustDirectDebitMandateLogRecord</c> record to recordInsertList with CustomerBankAccountSwift key.
    /// </summary>
    /// <param name="_value">
    /// The value of Customer's bank account Swift No.
    /// </param>
    public void logCustomerBankAccountSwift(CustDirectDebitMandateLogRecordValue _value)
    {
        this.logKeyValue(CustDirectDebitMandateLogRecordKey::CustomerBankAccountSwift, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logKeyValue</Name>
				<Source><![CDATA[
        private void logKeyValue(CustDirectDebitMandateLogRecordKey _key, CustDirectDebitMandateLogRecordValue _value)
    {
        CustDirectDebitMandateLogRecord custDirectDebitMandateLogRecord;

        custDirectDebitMandateLogRecord.CustDirectDebitMandateLog    = custDirectDebitMandateLogRecId;
        custDirectDebitMandateLogRecord.Key        = _key;
        custDirectDebitMandateLogRecord.Value      = _value;

        recordInsertList.add(custDirectDebitMandateLogRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logLegalEntityName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds <c>CustDirectDebitMandateLogRecord</c> record to recordInsertList with LegalEntityName key.
    /// </summary>
    /// <param name="_value">
    /// The value of Legal entity name.
    /// </param>
    public void logLegalEntityName(CustDirectDebitMandateLogRecordValue _value)
    {
        this.logKeyValue(CustDirectDebitMandateLogRecordKey::LegalEntityName, _value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        recordInsertList = new RecordInsertList(tableNum(CustDirectDebitMandateLogRecord));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustDirectDebitMandateLogRecId</Name>
				<Source><![CDATA[
        public RefRecId parmCustDirectDebitMandateLogRecId(RefRecId _custDirectDebitMandateLogRecId = custDirectDebitMandateLogRecId)
    {
        custDirectDebitMandateLogRecId = _custDirectDebitMandateLogRecId;
        return custDirectDebitMandateLogRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>save</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts content of recordInsertList into database
    /// </summary>
    public void save()
    {
        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>CustDirectDebitMandateLogHelper</c> class that has default information.
    /// </summary>
    /// <returns>
    /// An instance of the <c>CustDirectDebitMandateLogHelper</c> class.
    /// </returns>
    /// <remarks>
    /// The <c>parmCustDirectDebitMandateLogRecId</c> properties must be set before the code calls the instance methods.
    /// </remarks>
    public static CustDirectDebitMandateLogHelper construct()
    {
        return new CustDirectDebitMandateLogHelper();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newMandateLog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the <c>CustDirectDebitMandateLogHelper</c> class.
    /// </summary>
    /// <param name="_custDirectDebitMandateLogRecId">
    /// The <c>CustDirectDebitMandateLog</c> RefRecId to which data is related.
    /// </param>
    /// <returns>
    /// An instance of the <c>CustDirectDebitMandateLogHelper</c> class.
    /// </returns>
    /// <remarks>
    /// The resulting instance has valid default values and is ready for use.
    /// </remarks>
    public static CustDirectDebitMandateLogHelper newMandateLog(RefRecId _custDirectDebitMandateLogRecId)
    {
        CustDirectDebitMandateLogHelper instance = CustDirectDebitMandateLogHelper::construct();

        instance.parmCustDirectDebitMandateLogRecId(_custDirectDebitMandateLogRecId);

        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>