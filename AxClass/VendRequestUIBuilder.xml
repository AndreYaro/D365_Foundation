<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendRequestUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendRequestUIBuilder</c> class is the UI builder class for <c>VendRequest</c> and
/// <c>VendRequestRejected</c>Microsoft SQL Server Reporting Services report.
/// </summary>

public class VendRequestUIBuilder extends SrsReportDataContractUIBuilder
{
    VendRequestContract         rdpContract;
    VendTmpCompanyInfo          vendTmpCompanyInfo;

    DialogField     companyField;
    DialogField     fromDateField;
    DialogField     toDateField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the dialog box for the report.
    /// </summary>

    public void build()
    {
        rdpContract = this.dataContractObject() as VendRequestContract;

        vendTmpCompanyInfo.initTable();

        companyField = this.addDialogField(methodstr(VendRequestContract, parmCompany), rdpContract);
        companyField.lookupButton(2);
        fromDateField = this.addDialogField(methodstr(VendRequestContract, parmFromDate), rdpContract);
        toDateField = this.addDialogField(methodstr(VendRequestContract, parmToDate), rdpContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>companyLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Handles the UI modified event for the detail dialog field.
    /// </summary>
    /// <param name="_control">
    /// The control behind the dialog field that is changed.
    /// </param>
    /// <returns>
    /// true if the control value has been modified; otherwise false.
    /// </returns>
    /// <remarks>
    /// The level must be enable or disabled according to value in detail field.
    /// </remarks>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public boolean companyLookup(FormStringControl _control)
    {
        boolean ret = true;

        SysTableLookup          sysTableLookup = SysTableLookup::newParameters(tablenum(VendTmpCompanyInfo),this.dialog().formRun().controlCallingMethod());

        sysTableLookup.addLookupfield(fieldnum(VendTmpCompanyInfo, DataArea));
        sysTableLookup.addLookupfield(fieldnum(VendTmpCompanyInfo, DataAreaRecId));
        sysTableLookup.parmTmpBuffer(VendTmpCompanyInfo);
        sysTableLookup.performFormLookup();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Transfers data from the dialog into the data contract object.
    /// </summary>
    public void getFromDialog()
    {
        super();

        select firstOnly DataAreaRecId from vendTmpCompanyInfo
            where VendTmpCompanyInfo.DataArea == companyField.value();

        rdpContract.parmCompanyAccount(vendTmpCompanyInfo.DataAreaRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Registers method override in order to capture dialog events.
    /// </summary>
    public void postRun()
    {
        super();

        dialog.dialogForm().formRun().controlMethodOverload(false);
        companyField.registerOverrideMethod(methodstr(FormStringControl, lookup), methodstr(VendRequestUIBuilder, companyLookup), this);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>