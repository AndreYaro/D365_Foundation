<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankDocumentReconciliationClearingCheque</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BankDocumentReconciliationClearingCheque</c> class contains business logic to clear cancelled bank checks as reconciled.
/// </summary>
public class BankDocumentReconciliationClearingCheque extends BankDocumentReconciliationClearing
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doClearBankDocument</Name>
				<Source><![CDATA[
    protected void doClearBankDocument()
    {
        if (listOfBankDocumentRecId && (listOfBankDocumentRecId.elements() == 1))
        {
            ListEnumerator le = listOfBankDocumentRecId.getEnumerator();
            le.moveNext();

            BankDocumentTable bankDocumentTable = BankDocumentTable::find(le.current());
            BankChequeTable bankChequeTable = BankChequeTable::findByRecId(bankDocumentTable.SourceRecId);

            if (this.validateChequeIsCancelled(bankChequeTable)
                && this.validateDocumentNotAlreadyMatched(bankDocumentTable))
            {
                BankDocumentProcessor::submitBankDocumentTable(bankChequeTable, BankDocumentStatusChangeType::Reconcile);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateChequeIsCancelled</Name>
				<Source><![CDATA[
    private boolean validateChequeIsCancelled(BankChequeTable _bankChequeTable)
    {
        boolean isValid = true;

        if (_bankChequeTable.ChequeStatus != ChequeStatus::Cancelled)
        {
            isValid = checkFailed("@CashManagement:ReconcileReversedChequeMustBeCanceled");
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDocumentNotAlreadyMatched</Name>
				<Source><![CDATA[
    private boolean validateDocumentNotAlreadyMatched(BankDocumentTable _bankDocumentTable)
    {
        boolean isValid = true;

        if (_bankDocumentTable.IsReconciled)
        {
            isValid = checkFailed("@GLS221892");
        }

        return isValid;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>