<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>rPayJournalCheckUnPost_ReverseTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// A class for unposting posted journals with creating reverse transactions.
/// </summary>
class rPayJournalCheckUnPost_ReverseTrans extends RPayJournalCheckUnPost implements BatchRetryable
{
    Set     journalTrans2reverse; //recIDs of journal trans to be reversed
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks transactions are valid for reversing.
    /// </summary>
    /// <param name="_journalTransData">
    /// The <c>JournalTransData</c> class instance.
    /// </param>
    /// <returns>
    /// True if validation passed without any errors.
    /// </returns>
    protected boolean checkTrans(JournalTransData _journalTransData)
    {
        boolean ret;

        RPayTrans           rPayTrans;
        RPayJournalTrans    rPayJournalTrans = _journalTransData.journalTrans();

        ret = super(_journalTransData);

        while select * from rPayTrans
            where rPayTrans.SourceTableId == tableNum(RPayJournalTrans) &&
                  rPayTrans.SourceRecId == rPayJournalTrans.RecId       &&
                  rPayTrans.CounterUnit == rPayJournalTrans.JournalId
        {
            if (rPayTrans.PayPeriod < RPayParameters::payPeriod(rPayTrans.Emplid))
            {
                ret = TransLink_RU::canReverse(rPayTrans.TableId, rPayTrans.RecId);

                if (ret)
                {
                    journalTrans2reverse.add(rPayJournalTrans.RecId);
                }
            }
            else
            {
                ret = rPayTrans.validateDelete() && ret;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Posts transactions.
    /// </summary>
    /// <param name="_journalTransData">
    /// The <c>JournalTransData</c> class instance.
    /// </param>
    protected void postTrans(JournalTransData _journalTransData)
    {
        RPayJournalTrans journalTrans = _journalTransData.journalTrans();
        RPayTrans        payTrans;

        super(_journalTransData);

        if (!journalTrans2reverse.in(journalTrans.RecId))
        {
            delete_from payTrans
                where payTrans.SourceTableId == tableNum(RPayJournalTrans)                      &&
                      payTrans.PayModuleCode == journalTableData.journalTable().JournalNameId   &&
                      payTrans.SourceRecId   == journalTrans.RecId                              &&
                      payTrans.CounterUnit   == journalTableData.journalTable().JournalId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runInit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs initialization operations.
    /// </summary>
    /// <returns>
    /// Returns run result (#runLoad)
    /// </returns>
    protected Integer runInit()
    {
        Integer ret;

        ret = super();

        journalTrans2reverse = new Set(Types::Int64);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>runPostEnd</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs post-end operations.
    /// </summary>
    /// <returns>
    /// Returns run result (#runEnd)
    /// </returns>
    protected Integer runPostEnd()
    {
        RHRMOrderTable   orderTable;
        RPayReverse payReverse;
        Args        args;
        Integer ret;

        ret = super();

        if (ret)
        {
            if (journalTrans2reverse.elements())
            {
                args = new Args();
                args.parmEnumType(enumNum(RPayReverseType));
                args.parmEnum(RPayReverseType::Reverse);
                args.record(journalTableData.journalTable());

                payReverse = RPayReverse::newFromTable(args);
                payReverse.runOperation();
            }

            orderTable = this.orderTable(true);
            if (orderTable)
            {
                orderTable.PayJournalNum = '';
                orderTable.update();
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateJournalTablePosted</Name>
				<Source><![CDATA[
    protected void updateJournalTablePosted(JournalTableMap _journalTable)
    {
        //do not update posted status
        //super(_journalTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return false;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>