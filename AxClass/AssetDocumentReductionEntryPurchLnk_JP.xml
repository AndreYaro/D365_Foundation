<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDocumentReductionEntryPurchLnk_JP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>AssetDocumentReductionEntryPurchLnk_JP</c> is an asset reduction entry purchase order line link.
/// </summary>
class AssetDocumentReductionEntryPurchLnk_JP extends AssetDocumentPurchLink_JP
{
    AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
        public void create()
    {
        if (!linkReductionEntryTable.RecId)
        {
            ttsBegin;

            super();
            linkReductionEntryTable.AssetDocumentLinkTable_JP = this.parmLinkTable().RecId;
            linkReductionEntryTable.insert();

            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>delete</Name>
				<Source><![CDATA[
        public void delete()
    {
        AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTableLoc;

        if (this.parmRecId())
        {
            linkReductionEntryTableLoc = AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId(), true);

            ttsBegin;

            if (linkReductionEntryTableLoc)
            {
                linkReductionEntryTableLoc.delete();
            }
            super();

            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByReference</Name>
				<Source><![CDATA[
        public void findByReference(
        RefTableId            _refTableId,
        RefRecId              _refRecId,
        AssetDocumentRecId_JP _docRecId)
    {
        super(_refTableId, _refRecId, _docRecId);

        if (this.parmRecId())
        {
            this.parmExtReductionEntryTable(AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmExtReductionEntryTable</Name>
				<Source><![CDATA[
        public AssetDocumentLinkReductionEntryTable_JP parmExtReductionEntryTable(AssetDocumentLinkReductionEntryTable_JP _linkReductionEntryTable = linkReductionEntryTable)
    {
        linkReductionEntryTable = _linkReductionEntryTable;
        return linkReductionEntryTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryAmount</Name>
				<Source><![CDATA[
        public AssetReductionEntryAmount_JP parmReductionEntryAmount(AssetReductionEntryAmount_JP _reductionEntryAmount = linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount)
    {
        linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount = _reductionEntryAmount;
        return linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmReductionEntryCurrencyCode</Name>
				<Source><![CDATA[
        public AssetReductionEntryCurrencyCode_JP parmReductionEntryCurrencyCode(AssetReductionEntryCurrencyCode_JP  _reductionEntryCurrency = linkReductionEntryTable.ReductionEntryCurrencyCode)
    {
        linkReductionEntryTable.ReductionEntryCurrencyCode = _reductionEntryCurrency;
        return linkReductionEntryTable.ReductionEntryCurrencyCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
        public void update()
    {
        AssetDocumentLinkReductionEntryTable_JP   linkReductionEntryTableLoc;
        super();

        if (this.parmRecId())
        {
            linkReductionEntryTableLoc = AssetDocumentLinkReductionEntryTable_JP::findByDocumentExtension(this.parmRecId(),true);
            linkReductionEntryTableLoc.ReductionEntryTransactionCurrencyAmount = linkReductionEntryTable.ReductionEntryTransactionCurrencyAmount;
            linkReductionEntryTableLoc.ReductionEntryCurrencyCode = linkReductionEntryTable.ReductionEntryCurrencyCode;
            ttsBegin;
            linkReductionEntryTableLoc.update();
            ttsCommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDocumentExtensionsByRef</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes all the reduction entry links on specific reference record.
    /// </summary>
    /// <param name="_refTableId">
    /// Reference record table id.
    /// </param>
    /// <param name="_refRecId">
    /// Reference recod recid.
    /// </param>
    public static void deleteDocumentExtensionsByRef(
        RefTableId            _refTableId,
        RefRecId              _refRecId)
    {
        AssetDocumentLinkTable_JP         linkTable;
        AssetDocumentLinkReductionEntryTable_JP linkReductionEntry;
        AssetDocumentLinkReductionEntryTable_JP linkReductionEntryLoc;

        ttsBegin;
        while select forUpdate linkTable
            where linkTable.RefTableId == _refTableId &&
                linkTable.RefRecId     == _refRecId
            exists join linkReductionEntry
            where linkReductionEntry.AssetDocumentLinkTable_JP == linkTable.RecId
        {
            linkTable.delete();
            delete_from linkReductionEntryLoc
                where linkReductionEntryLoc.AssetDocumentLinkTable_JP == linkTable.RecId;
        }

        ttsCommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>