<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventCleanupWHSInventSumUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventCleanupWHSInventSumUIBuilder</c> class handles the UI creation and maintenance for the Warehouse management on-hand entries cleanup process.
/// </summary>
internal final class InventCleanupWHSInventSumUIBuilder extends SysOperationAutomaticUIBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void postBuild()
    {
        super();

        Dialog      dialogLocal = this.dialog();
        DialogText  dialogText = dialogLocal.addText(this.buildDialogTextMessage());

        if (dialogLocal.formBuildDesign() && dialogText.control() is FormBuildControl)
        {
            FormBuildControl formBuildControl = dialogText.control();

            dialogLocal.formBuildDesign().moveControl(formBuildControl.id());
            dialogText.widthMode(FormWidth::SizeToAvailable);
            dialogText.heightMode(FormHeight::SizeToContent);
        }
        else
        {
            // FormWidth::SizeToAvailable doesn't work here, so set the width manually
            dialogText.width(400);
        }

        DialogField maximumExecutionTimeInHoursField = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventCleanupWHSInventSumContract, parmMaximumExecutionTimeInHours));

        if (maximumExecutionTimeInHoursField.control())
        {
            // The maximum length of a help text provideded usin the SysOperationHelpText attribute or the helpText method of the DialogField class is limited by 255, so set the long help text directly to the control.
            maximumExecutionTimeInHoursField.control().helpText("@FieldDescriptions_WHS:MaximumExecutionTimeHoursHelpText");
        }

        DialogField commitSize = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(InventCleanupInventSumContract, parmCommitSize));
        
        if (InventCleanupWHSInventSumAllowUserToEnterCommitSizeFlight::instance().isEnabled() && commitSize.control())
        {
            commitSize.control().helpText("@WAX:InventCleanupInventSumCommitSizeHelpText");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildDialogTextMessage</Name>
				<Source><![CDATA[
    private str buildDialogTextMessage()
    {
        str ret = "@WAX:WarehouseManagementOnHandEntriesCleanupNoteToUser";

        if (InventCleanupWHSInventSumWithInventDimFlight::instance().isEnabled())
        {
            ret = strFmt('%1\n%2', "@WAX:OnHandCleanupInventSumObsolete", ret);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    public void build()
    {
        if (InventCleanupWHSInventSumAllowUserToEnterCommitSizeFlight::instance().isEnabled())
        {
            this.makeParameterVisible(methodStr(InventCleanupInventSumContract, parmCommitSize));
        }

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>makeParameterVisible</Name>
				<Source><![CDATA[
    private void makeParameterVisible(identifierName _parameterName)
    {
        SysOperationDataContractInfo contractInfo = this.dataContractInfo().getMemberObjectInfo(this.dataContractObject());
        SysOperationDataMemberInfo memberInfo = contractInfo.getMembers().lookup(_parameterName);

        memberInfo.parmVisible(true);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>