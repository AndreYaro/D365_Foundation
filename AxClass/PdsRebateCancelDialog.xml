<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PdsRebateCancelDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class that serves to cancel claims.
/// </summary>
class PdsRebateCancelDialog extends RunBaseBatch implements BatchRetryable
{
    QueryRun                      queryRun;
    container                     packedQuery;

    private const int currentVersion = 1;

    #LOCALMACRO.CurrentList
        packedQuery
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
        queryRun = new QueryRun(querystr(PdsRebateTableListPage));
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        packedQuery = queryRun.pack();
        return [currentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches rebate reversal process.
    /// </summary>
    public void run()
    {
        PdsRebateTable::runCancel(queryRun);
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer     version     = conpeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case currentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            default :
                ret = false;
                break;
        }

        if (packedQuery)
        {
            queryRun = new QueryRun(packedQuery);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update query setting for related data source.
    /// </summary>
    protected void updateQuery()
    {
        QueryBuildDataSource pdsRebateTableDataSource;
        QueryBuildDataSource pdsRebateAgreementDataSource;

        pdsRebateAgreementDataSource = queryRun.query().dataSourceTable(tablenum(PdsRebateAgreement));

        if (pdsRebateAgreementDataSource)
        {
            pdsRebateAgreementDataSource.enabled(false);
        }

        pdsRebateTableDataSource = queryRun.query().dataSourceTable(tablenum(PdsRebateTable));

        if (pdsRebateTableDataSource)
        {
            SysQuery::findOrCreateRange(pdsRebateTableDataSource, fieldnum(PdsRebateTable, PdsRebateId));
            SysQuery::findOrCreateRange(pdsRebateTableDataSource, fieldnum(PdsRebateTable, PdsRebateType));
            SysQuery::findOrCreateRange(pdsRebateTableDataSource, fieldnum(PdsRebateTable, PdsProcessDate));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PdsRebateCancelDialog construct()
    {
        return new PdsRebateCancelDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SCM:CancelRebates";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        PdsRebateCancelDialog pdsRebateCancelDialog = PdsRebateCancelDialog::construct();

        try
        {
            pdsRebateCancelDialog.updateQuery();

            if (pdsRebateCancelDialog.prompt())
            {
                if ((Box::yesNo(
                        strfmt("@SCM:CancelRebatesPrompt",
                        PdsRebateStatus::MarkForCredit, PdsRebateStatus::Canceled),
                        DialogButton::No,
                        PdsRebateCancelDialog::description())) == DialogButton::Yes)
                {
                    pdsRebateCancelDialog.runOperation();

                    if (_args
                        &&  _args.caller()
                        &&  _args.caller() is FormRun
                        &&  formHasMethod(_args.caller(), identifierStr(refreshData)))
                    {
                        _args.caller().refreshData();
                    }
                }
            }
        }
        catch (Exception::Error)
        {
            warning("@SCM:CancelRebatesFailed");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>