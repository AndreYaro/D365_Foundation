<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InfAdjSimulationController_MX</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InfAdjSimulationController_MX</c> class is the controller class for the <c>Simulation_MX</c>
///    report.
/// </summary>
class InfAdjSimulationController_MX extends SrsReportRunController implements BatchRetryable
{
    #InfAdj_SSRSParameters_MX

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        #ISOCountryRegionCodes
        super();
        GlobalizationInstrumentationHelper::featureRunByCountryRegionCodes([
            [#isoMX, GlobalizationConstants::FeatureReferenceMX00002] ],
            funcName()
        );
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the ability to modify the report contract before a dialog box is displayed to the user.
    /// </summary>
    /// <remarks>
    /// Use this method to change the report contract. This method is called only during an interactive
    /// scenario before rendering the UI to the user.In a batch scenario, this method is not called because
    /// there is no user interaction and contract values are already saved in batch.Do not initialize any
    /// instance variables in this code if they are used during the lifetime of the controller because they
    /// will fail in batch cases.
    /// </remarks>
    protected void prePromptModifyContract()
    {
        InfAdjSimulationContract_MX contract = this.parmReportContract().parmRdpContract() as InfAdjSimulationContract_MX;
        InfAdjSimulationController_MX::modifyContract(contract, this.parmArgs());
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        protected static InfAdjSimulationController_MX construct()
    {
        return new InfAdjSimulationController_MX();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays the customized <c>Simulation_MX</c> SRS Report.
    /// </summary>
    /// <param name="_args">
    /// Args object.
    /// </param>
    public static void main(Args _args)
    {
        InfAdjSimulationController_MX controller = InfAdjSimulationController_MX::newFromArgs(_args);
        controller.parmReportName(#SimulationReportName);
        controller.parmDialogCaption(#SimulationReportCaption);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Extracts the dates from the <c>args</c> object and modifies the dates range in the given contract.
    /// </summary>
    /// <param name="_contract">
    ///    The report contract.
    /// </param>
    /// <param name="_args">
    ///    The <c>args</c> object from the caller.
    /// </param>
    public static void modifyContract(InfAdjSimulationContract_MX _contract, Args _args)
    {
        FromDate                        fromDate;
        ToDate                          toDate;
        InflationAdjJournal_MX inflationAdjJournal;

        if (_args && _args.dataset() == tablenum(InflationAdjJournal_MX))
        {
            inflationAdjJournal = _args.record();
            fromDate            = inflationAdjJournal.FromDate;
            toDate              = inflationAdjJournal.ToDate;
            _contract.parmFromDate(fromDate);
            _contract.parmToDate(toDate);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new instance of the <c>InfAdjSimulationController_MX</c> class and sets the
    ///    <c>reportName</c>, <c>dialogCaption</c>, and <c>args</c> properties.
    /// </summary>
    /// <param name="_args">
    ///    An <c>args</c> object.
    /// </param>
    /// <returns>
    ///    A new instance of the <c>InfAdjSimulationController_MX</c> class.
    /// </returns>
    public static InfAdjSimulationController_MX newFromArgs(Args _args)
    {
        InfAdjSimulationController_MX controller = InfAdjSimulationController_MX::construct();

        controller.parmArgs(_args);

        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns true.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>