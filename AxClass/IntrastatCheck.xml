<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntrastatCheck</Name>
	<SourceCode>
		<Declaration><![CDATA[
class IntrastatCheck extends RunBase
{
    private const str LocalizationFeatureReference = 'EUR-00002';
    QueryRun     qEIntrastat;
    Intrastat    intrastat;

    boolean      errorFound;

    Counter      infoLogCounter;
    Counter      progressCounter;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    Object dialog()
    {
        FormRun formRun = classFactory.createQueryForm(this);

        formRun.init();
        return formRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    void new()
    {
        super();

        GlobalizationInstrumentationHelper::featureRun(LocalizationFeatureReference, funcName());

        qEIntrastat = new QueryRun(QueryStr(Intrastat));
        qEIntrastat.query().dataSourceTable(TableNum(Intrastat)).addRange(fieldnum(Intrastat, transDate));
        qEIntrastat.query().dataSourceTable(TableNum(Intrastat)).addRange(fieldnum(Intrastat, dispatchId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,qEIntrastat.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>progressUpdate</Name>
				<Source><![CDATA[
    void progressUpdate()
    {
        progressCounter++;
        progress.setCount(progressCounter);
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </summary>
    /// <returns>
    ///    The instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </returns>
    /// <remarks>
    ///    This method is used if a query prompt is the dialog, and if a Select menu item is added to a
    ///    dialog. Do not create the instance of the <c>QueryRun</c> class when this method is called. Do it
    ///    in the <c>unpack</c> method and the <c>initParmDefault</c> method, or in the <c>init</c> method.
    /// </remarks>
    QueryRun queryRun()
    {
        return  qEIntrastat;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTransactionPrefix</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the prefix string for the current transaction.
    /// </summary>
    /// <remarks>
    /// This is an extension point made to enable setting a custom transaction prefix.
    /// </remarks>
    protected str setTransactionPrefix()
    {
        return #PreFixField(Intrastat,ItemId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    void run()
    {
        infoLogCounter = infolog.num();
        errorFound = false;

        this.progressInit("@SYS13894",(select maxof(seqNum) from intrastat).seqNum, #AviSearch);

        setprefix("@SYS28715");

        ttsbegin;

        while (qEIntrastat.next())
        {
            intrastat = qEIntrastat.get(TableNum(Intrastat));
            setprefix(this.setTransactionPrefix());
            this.progressUpdate();
            this.validateRecord();
        }
        ttscommit;

        if (!errorFound)
            info(strfmt("@SYS11281"));
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    boolean unpack(container packedClass)
    {
        Integer     version = conpeek(packedClass,1);
        container   packedQuery;

        switch (version)
        {
            case #CurrentVersion:
                [version, packedQuery] = packedClass;

                if (packedQuery)
                {
                    qEIntrastat = new QueryRun(packedQuery);
                }

                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateRecord</Name>
				<Source><![CDATA[
    boolean validateRecord()
    {
        boolean     ok = true;

        ok = intrastat.validateRecord();

        if (ok)
            intrastat.log = '';
        else
        {
            intrastat.log = Info::infoCon2Str(infolog.copy(infoLogCounter+1,infolog.num()));
            infoLogCounter = infolog.num();
            errorFound = true;
        }

        intrastat.update();
        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static IntrastatCheck construct()
    {
        return new IntrastatCheck();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args args)
    {
        IntrastatCheck intraCheck;
        FormDataSource intrastat_ds;

        intraCheck = IntrastatCheck::construct();
        intrastat_ds  = FormDataUtil::getFormDataSource(args.record());
        intraCheck.getLast();

        if (intraCheck.prompt())
            intraCheck.runOperation();

        intrastat_ds.executeQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>