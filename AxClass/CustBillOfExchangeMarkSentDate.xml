<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBillOfExchangeMarkSentDate</Name>
	<SourceCode>
		<Declaration><![CDATA[
class CustBillOfExchangeMarkSentDate extends RunBase
{
    CustBillOfExchangeID    custBillOfExchangeID;
    CustBillOfExchangeJour  custBillOfExchangeJour;
    container               conCustBillOfExchangeID;

    date                    dateSent;

    DialogField             dialogDateSent;

    #define.CurrentVersion(2)
    #localmacro.CurrentList
        dateSent,
        conCustBillOfExchangeID
    #endmacro
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the AOT.
    /// </remarks>
    public Object dialog()
    {
        Dialog   dialog = super();

        dialogDateSent = dialog.addField(extendedTypeStr(TransDate),"@SYS10640","@SYS71569");

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret;

        dateSent = dialogDateSent.value();

        ret = super();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmCustBillOfExchangeID</Name>
				<Source><![CDATA[
    public void parmCustBillOfExchangeID(CustBillOfExchangeID _custBillOfExchangeID)
    {
        conCustBillOfExchangeID += _custBillOfExchangeID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        #OCCRetryCount

        if (! this.validate())
        {
            throw error("@SYS25904");
        }

        try
        {
            ttsbegin;

            this.update();

            ttscommit;
        }

        catch (Exception::Deadlock)
        {
            retry;
        }

        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Version     version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedClass;
                break;

            default:
                return false;
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    public void update()
    {
        Counter    counter;

        for (counter = conlen(conCustBillOfExchangeID);
             counter;
             counter = counter - 1)
        {
            custBillOfExchangeJour = CustBillOfExchangeJour::find(conpeek(conCustBillOfExchangeID, counter), true);
            if (custBillOfExchangeJour.DocumentDate != dateSent)
            {
                custBillOfExchangeJour.DocumentDate = dateSent;
                custBillOfExchangeJour.update();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object _calledFrom = null)
    {
        boolean ok = true;

        if (!dateSent)
        {
            ok = checkFailed("@SYS2476");
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS10640";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        CustBillOfExchangeMarkSentDate  custBillOfExchangeMarkSentDate;
        CustBillOfExchangeJour          custBillOfExchangeJour;
        FormDataSource                  formDS = FormDataUtil::getFormDataSource(args.record());

        custBillOfExchangeMarkSentDate = new CustBillOfExchangeMarkSentDate();

        if (custBillOfExchangeMarkSentDate.prompt())
        {
            for (custBillOfExchangeJour = formDS.getFirst(true) ? formDS.getFirst(true) : args.record();
                 custBillOfExchangeJour;
                 custBillOfExchangeJour = formDS.getNext())
            {
                custBillOfExchangeMarkSentDate.parmCustBillOfExchangeID(custBillOfExchangeJour.BillOfExchangeId);
            }
            CustBillOfExchangeMarkSentDate::runServer(custBillOfExchangeMarkSentDate.pack());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runServer</Name>
				<Source><![CDATA[
    protected static void runServer(container _packedData)
    {
        CustBillOfExchangeMarkSentDate setSentDate;

        setSentDate = new CustBillOfExchangeMarkSentDate();
        setSentDate.unpack(_packedData);
        setSentDate.runOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>