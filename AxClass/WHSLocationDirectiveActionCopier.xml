<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSLocationDirectiveActionCopier</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class WHSLocationDirectiveActionCopier
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyLocationDirectiveActions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies a location directive action.
    /// </summary>
    /// <param name="_locDirActions">
    /// Location directive action records to be copied.
    /// </param>
    /// <returns>
    /// The copied location directive actions.
    /// </returns>
    /// <remarks>
    /// This method assumes that the list of actions consists of actions
    /// all from the same location directive line.
    /// </remarks>
    internal static void copyLocationDirectiveActions(List _locDirActions)
    {
        if (!_locDirActions)
        {
            return;
        }
        
        ListEnumerator le = _locDirActions.getEnumerator();

        if(le.moveNext())
        {
            WHSLocDirAction selectedRecord = le.current();
            WHSLocSeqNum seqCounter = WHSLocationDirectiveActionCopier::getMaxSequenceNumber(selectedRecord) + 1;

            le.reset();

            ttsbegin;

            while(le.moveNext())
            {
                selectedRecord = le.current();
                WHSLocationDirectiveActionCopier::copyLocationDirectiveAction(selectedRecord, seqCounter);
                seqCounter += 1;
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyLocationDirectiveAction</Name>
				<Source><![CDATA[
    private static void copyLocationDirectiveAction(WHSLocDirAction _locDirAction, WHSLocSeqNum seqenceNumber)
    {
        WHSLocDirAction     copyLocDirAction;
        buf2Buf(_locDirAction, copyLocDirAction);
        copyLocDirAction.LocDirName = _locDirAction.LocDirName + ' - ' + "@SYS6896";
        copyLocDirAction.LocSeqNum = seqenceNumber;

        if (!copyLocDirAction.validateWrite())
        {
            throw error(strFmt("@WAX:WHSCannotInsertRecordInTable", tablePName(WHSLocDirAction)));
        }
        copyLocDirAction.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMaxSequenceNumber</Name>
				<Source><![CDATA[
    private static WHSLocSeqNum getMaxSequenceNumber(WHSLocDirAction _locDirAction)
    {
        WHSLocDirAction     lastLocDirAction;

        select maxof(LocSeqNum) from lastLocDirAction
            where lastLocDirAction.RefRecId == _locDirAction.RefRecId;

        return lastLocDirAction.LocSeqNum;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>