<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetControlSourceDocumentQueryableFactory</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BudgetControlSourceDocumentQueryableFactory</c> is responsible for instantiating 
/// <c>BudgetControlISourceDocumentQueryable</c> objects.
/// </summary>
internal static class BudgetControlSourceDocumentQueryableFactory
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSourceIntegratorInstance</Name>
				<Source><![CDATA[
    internal static BudgetControlISourceDocumentQueryable getSourceIntegratorInstance(BudgetControlSourceIntegrator _sourceIntegrator)
    {
        SysPluginMetadataCollection metadataCollection = new SysPluginMetadataCollection();
        metadataCollection.SetManagedValue(enumstr(BudgetControlSourceIntegrator), _sourceIntegrator);

        return SysPluginFactory::Instance(
            identifierStr(Dynamics.AX.Application), 
            classStr(BudgetControlISourceDocumentQueryable), 
            metadataCollection);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInstancesByLabel</Name>
				<Source><![CDATA[
    internal static Map getInstancesByLabel()
    {
        Array instances = SysPluginFactory::Instances(
            identifierStr(Dynamics.AX.Application),
            classStr(BudgetControlISourceDocumentQueryable),
            new SysPluginMetadataCollection());

        Map queryableInstances = new Map(Types::String, Types::Class);
        for (int i = 1; i <= instances.lastIndex(); i++)
        {
            BudgetControlISourceDocumentQueryable queryable = instances.value(i);
            if (!queryableInstances.exists(queryable.getLabel()))
            {
                queryableInstances.add(queryable.getLabel(), queryable);
            }
            else
            {
                // Labels must be unique for each queryable.
                throw error(Error::wrongUseOfFunction(funcname()));
            }
        }

        return queryableInstances;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>