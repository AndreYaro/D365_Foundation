<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AxInventDim_PurchRFQCaseLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
class AxInventDim_PurchRFQCaseLine extends AxInventDim
{
    AxPurchRFQCaseLine axPurchRFQCaseLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>axPurchRFQCaseLine</Name>
				<Source><![CDATA[
    public AxPurchRFQCaseLine axPurchRFQCaseLine(AxPurchRFQCaseLine _axPurchRFQCaseLine = axPurchRFQCaseLine)
    {
        axPurchRFQCaseLine = _axPurchRFQCaseLine;
        return axPurchRFQCaseLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inputStatus</Name>
				<Source><![CDATA[
    protected int inputStatus(InternalExternal _inputStatus = axPurchRFQCaseLine.callInputStatus())
    {
        if (axPurchRFQCaseLine)
        {
            return axPurchRFQCaseLine.callInputStatus(_inputStatus);
        }

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modify</Name>
				<Source><![CDATA[
    public void modify()
    {
        axPurchRFQCaseLine.modify();
    }

]]></Source>
			</Method>
			<Method>
				<Name>save</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Inserts or updates the current record for the class.
    /// </summary>
    /// <remarks>
    ///    Execute this method on the AxBC class after new values are assigned to fields.The actual work to
    ///    save and update a record is performed in the <c>doSave</c> method, which is called by this method.
    /// </remarks>
    public void save()
    {
        axPurchRFQCaseLine.updateFromInventDim();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setConfigId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>ConfigId</c> parameter.
    /// </summary>
    public void setConfigId()
    {
        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, ConfigId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet())
        {
            this.parmConfigId(axPurchRFQCaseLine.inventTableRecord().StandardConfigId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventColorId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventColorId</c> parameter.
    /// </summary>
    public void setInventColorId()
    {
        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, InventColorId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet())
        {
            this.parmInventColorId(axPurchRFQCaseLine.inventTableRecord().StandardInventColorId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventVersionId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventVersionId</c> parameter.
    /// </summary>
    public void setInventVersionId()
    {
        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, InventVersionId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet())
        {
            this.parmInventVersionId(axPurchRFQCaseLine.inventTableRecord().StandardInventVersionId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventLocationId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventLocationId</c> parameter.
    /// </summary>
    public void setInventLocationId()
    {
        InventDim   inventDimLocal;

        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, InventLocationId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();
        axPurchRFQCaseLine.setAxPurchRFQCaseTableFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet()  ||
            axPurchRFQCaseLine.isAxPurchRFQCaseTableFieldsSet()       ||
            axPurchRFQCaseLine.axPurchRFQCaseTable().isFieldModified(fieldnum(PurchRFQCaseTable, InventLocationId)))
        {
            inventDimLocal = InventDim::find(axPurchRFQCaseLine.parmInventDimId());
            if (this.parmInventSiteId())
            {
                inventDimLocal.InventSiteId = this.parmInventSiteId();
                inventDimLocal              = InventDim::findOrCreate(inventDimLocal);
            }
            this.parmInventLocationId(axPurchRFQCaseLine.inventTableRecord().purchInventLocationId(axPurchRFQCaseLine.axPurchRFQCaseTable().parmInventLocationId(),
                                                                                                   inventDimLocal.InventDimId,
                                                                                                   inventDimLocal.InventSiteId));
        }
        if (!this.parmInventLocationId() && EcoResDimensionGroupSetup::isWarehouseDimensionMandatoryForItem(axPurchRFQCaseLine.parmItemId()))
        {
            this.parmInventLocationId(InventFallbackWarehouse::transportWarehouse(this.parmInventSiteId()));
            if (!this.parmInventLocationId())
            {
                this.parmInventLocationId(InventParameters::find().FallbackInventLocationId);
                this.parmInventSiteId(InventLocation::find(this.parmInventLocationId()).InventSiteId);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventSiteId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventSiteId</c> parameter.
    /// </summary>
    public void setInventSiteId()
    {
        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, InventSiteId)))
        {
            return;
        }

        if (this.isFieldSet(fieldnum(InventDim,InventLocationId)))
        {
            this.parmInventSiteId(axPurchRFQCaseLine.inventTableRecord().purchInventSiteId(InventLocation::find(this.parmInventLocationId()).InventSiteId, this.parmInventDimId()));
        }

        if (!this.isFieldSet(fieldnum(InventDim,InventSiteId)))
        {
            axPurchRFQCaseLine.setInventTableRecordFields();
            axPurchRFQCaseLine.setAxPurchRFQCaseTableFields();

            if (axPurchRFQCaseLine.isInventTableRecordFieldsSet()   ||
                axPurchRFQCaseLine.isAxPurchRFQCaseTableFieldsSet() ||
                axPurchRFQCaseLine.axPurchRFQCaseTable().isFieldModified(fieldnum(PurchRFQCaseTable, InventSiteId)))
            {
                this.parmInventSiteId(axPurchRFQCaseLine.inventTableRecord().purchInventSiteId(axPurchRFQCaseLine.axPurchRFQCaseTable().parmInventSiteId(), axPurchRFQCaseLine.parmInventDimId()));
            }
        }

        if (!this.parmInventSiteId())
        {
            this.parmInventSiteId(InventParameters::find().fallbackSiteId());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventSizeId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>InventSizeId</c> parameter.
    /// </summary>
    public void setInventSizeId()
    {
        if (this.isMethodExecuted(funcname(), fieldnum(InventDim, InventSizeId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet())
        {
            this.parmInventSizeId(axPurchRFQCaseLine.inventTableRecord().StandardInventSizeId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventStyleId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the value of the <c>InventStyleId</c> field.
    /// </summary>
    public void setInventStyleId()
    {
        if (this.isMethodExecuted(funcName(), fieldNum(InventDim, InventStyleId)))
        {
            return;
        }

        super();

        axPurchRFQCaseLine.setInventTableRecordFields();

        if (axPurchRFQCaseLine.isInventTableRecordFieldsSet())
        {
            this.parmInventStyleId(axPurchRFQCaseLine.inventTableRecord().StandardInventStyleId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>newAxPurchRFQCaseLine</Name>
				<Source><![CDATA[
    public static AxInventDim_PurchRFQCaseLine newAxPurchRFQCaseLine(AxPurchRFQCaseLine _axPurchRFQCaseLine)
    {
        AxInventDim_PurchRFQCaseLine   axInventDim_PurchRFQCaseLine;

        axInventDim_PurchRFQCaseLine = new AxInventDim_PurchRFQCaseLine();
        axInventDim_PurchRFQCaseLine.axPurchRFQCaseLine(_axPurchRFQCaseLine);
        return axInventDim_PurchRFQCaseLine;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>