<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>Global_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>Global_ApplicationSuite_Extension</c> class represent extension for the Application Suite model.
/// </summary>
[ExtensionOf(classStr(Global))]
final static class Global_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>assignSecurityRoleToUser</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method assigns a role to the user.
    /// </summary>
    /// <param name = "_userId">The user to whom a role needs to be assigned.</param>
    /// <param name = "_roleName">The role that needs to be assigned to a user.</param>
    public static void assignSecurityRoleToUser(SysUserId _userId, str _roleName)
    {
        SecurityRole securityRole;

        select firstonly securityRole
            where securityRole.AotName == _roleName
                || securityRole.Name == _roleName;

        Debug::assert(securityRole.RecId != 0);

        SecurityUserRole securityUserRole;

        select firstonly securityUserRole
            where securityUserRole.User == _userId
                && securityUserRole.SecurityRole == securityRole.RecId;

        if (!securityUserRole && securityRole)
        {
            securityUserRole.SecurityRole = securityRole.RecId;
            securityUserRole.User = _userId;
            securityUserRole.AssignmentMode = RoleAssignmentMode::Manual;
            securityUserRole.AssignmentStatus = RoleAssignmentStatus::Enabled;
            ttsbegin;
            securityUserRole.insert();
            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>