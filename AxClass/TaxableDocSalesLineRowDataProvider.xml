<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocSalesLineRowDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Row data provider for sales line.
/// </summary>
public class TaxableDocSalesLineRowDataProvider extends TaxableDocumentRowDataProviderLine
{
    SalesTable      salesTable;
    SalesLine       salesLine;
    EcoResProduct   ecoResProduct;


}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillInFields</Name>
				<Source><![CDATA[
    public void fillInFields(TaxableDocumentLineObject _lineObj)
    {
        super(_lineObj);

        this.addFieldValue(_lineObj, TaxEngineModelFields::ProductType, ecoResProduct.ProductType);
        if (taxModelTaxable is TaxModelTaxableReturn)
        {
            this.addFieldValue(_lineObj, TaxEngineModelFields::DispositionAction, this.getDispositionAction());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValidFields</Name>
				<Source><![CDATA[
    protected void initValidFields()
    {
        super();
        validFields.add(TaxEngineModelFields::ProductType, Types::Enum, enumNum(EcoResProductType));
        validFields.add(TaxEngineModelFields::DispositionAction, Types::Enum, enumNum(DispositionAction));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDispositionAction</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get disposition action from the transaction.
    /// </summary>
    /// <returns>
    /// The disposition action.
    /// </returns>
    protected DispositionAction getDispositionAction()
    {
        return salesLine.returnDispositionCode().DispositionAction;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initMainTable</Name>
				<Source><![CDATA[
    protected void initMainTable()
    {
        salesLine = mainRecord as SalesLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initRelatedTables</Name>
				<Source><![CDATA[
    protected void initRelatedTables()
    {
        if (salesLine.inventTable())
        {
            ecoResProduct = EcoResProduct::find(salesLine.inventTable().Product);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isCustomsExportOrder_IN</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if this is an import order or not.
    /// </summary>
    /// <returns>
    /// True if this transaction is an import order; otherwise, false.
    /// </returns>
    // <GIN>
    [Replaceable]
    protected boolean isCustomsExportOrder_IN()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReportingCurrencyExchangeRate</Name>
				<Source><![CDATA[
    // </GIN>

    /// <summary>
    /// Gets reporting currency exchange rate
    /// </summary>
    /// <returns>
    /// Reporting currency exchange rate
    /// </returns>
    [Replaceable]
    protected ExchRate getReportingCurrencyExchangeRate(TaxableDocumentLineObject _lineObj)
    {
        return super(_lineObj);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountingCurrencyExchangeRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets accounting currency exchange rate
    /// </summary>
    /// <returns>
    /// Accounting currency exchange rate
    /// </returns>
    [Replaceable]
    protected ExchRate getAccountingCurrencyExchangeRate(TaxableDocumentLineObject _lineObj)
    {
        return super(_lineObj);
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasFixedExchangeRateAccountCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Has fixed exchange rate of accounting currency or not.
    /// </summary>
    /// <returns>True if has fixed exchange rate of accounting currency, otherwise false.</returns>
    protected boolean hasFixedExchangeRateAccountCurrency()
    {
        return this.getFixedExchRate() != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFixedExchangeRateAccountCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets fixed exchange rate of accounting currency.
    /// </summary>
    /// <returns>Fixed exchange rate of accounting currency.</returns>
    protected ExchRate getFixedExchangeRateAccountCurrency()
    {
        return this.getFixedExchRate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasFixedExchangeRateReportCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Has fixed exchange rate of reporting currency or not.
    /// </summary>
    /// <returns>True if has fixed exchange rate of reporting currency, otherwise false.</returns>
    protected boolean hasFixedExchangeRateReportCurrency()
    {
        return this.getReportingCurrencyFixedExchRate() != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFixedExchangeRateReportCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets fixed exchange rate of reporting currency.
    /// </summary>
    /// <returns>Fixed exchange rate of reporting currency.</returns>
    protected ExchRate getFixedExchangeRateReportCurrency()
    {
        return this.getReportingCurrencyFixedExchRate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFixedExchRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets fixed exchange rate from SO header
    /// </summary>
    /// <returns>Fixed exchange rate from SO header</returns>
    private ExchRate getFixedExchRate()
    { 
        if (!salesTable)
        {
            salesTable = relatedRecordsQueryRun.getNo(1) as SalesTable;
            if (!salesTable)
            {
                salesTable = SalesTable::find(salesLine.SalesId);
            }
        }

        return salesTable.FixedExchRate / 100;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReportingCurrencyFixedExchRate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets reporting currency fixed exchange rate from SO header
    /// </summary>
    /// <returns>Reporting currency fixed exchange rate from SO header</returns>
    private ExchRate getReportingCurrencyFixedExchRate()
    {
        if (!salesTable)
        {
            salesTable = relatedRecordsQueryRun.getNo(1) as SalesTable;
            if (!salesTable)
            {
                salesTable = SalesTable::find(salesLine.SalesId);
            }
        }

        return salesTable.ReportingCurrencyFixedExchRate / 100;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>