<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventConsistencyCheck_Table</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class InventConsistencyCheck_Table extends SysConsistencyCheck implements BatchRetryable
{
    QueryRun    queryRun;
    boolean     dummy;

    #DEFINE.CurrentVersion(2)

    #LOCALMACRO.CurrentList
        Dummy
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkFixInventTable</Name>
				<Source><![CDATA[
        void checkFixInventTable()
    {
        boolean         hasCheckFixMethod = this.hasTableCheckFixMethod(tableNum(InventTable));
        boolean         handleExceptionFlightEnabled =InventConsistencyCheckTableHandleExceptionFlight::instance().isEnabled();

        DictTable dictTable = new DictTable(tableNum(InventTable));
        setPrefix(strFmt("@SYS26373", dictTable.label()));
        while (queryRun.next())
        {
            if (handleExceptionFlightEnabled)
            {
                try
                {
                    InventTable inventTable = queryRun.get(tableNum(InventTable)) as InventTable;

                    this.kernelCheckRecord(inventTable, dictTable, hasCheckFixMethod);
                    inventTable.updateCheckFix(this);
                }
                catch
                {
                    warning("@SYS70403");
                }
            }
            else
            {
                InventTable inventTable = queryRun.get(tableNum(InventTable)) as InventTable;

                this.kernelCheckRecord(inventTable, dictTable, hasCheckFixMethod);
                inventTable.updateCheckFix(this);
            }
        }

        if (handleExceptionFlightEnabled)
        {
            this.updateNotCorrected();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>configurationKey</Name>
				<Source><![CDATA[
        ConfigurationKeyId configurationKey()
    {
        return configurationKeyNum(LogisticsBasic);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns a class that contains the methods that are described by the <c>RunBaseDialogable</c>
    ///    interface.
    /// </summary>
    /// <returns>
    ///    A class that contains the methods that are described by the <c>RunBaseDialogable</c> interface.
    /// </returns>
    /// <remarks>
    ///    A dialog can be either built by using the <c>Dialog</c> class or by using a class that is created
    ///    in the Application Object Tree (AOT).
    /// </remarks>
    Object dialog()
    {
        FormRun formRun = classfactory.createQueryForm(this);

        formRun.init();
        return formRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executionOrder</Name>
				<Source><![CDATA[
        Integer executionOrder()
    {
        return 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasDialog</Name>
				<Source><![CDATA[
        boolean hasDialog()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new()
    {
        if (this.showInTree())
        {
            queryRun = new QueryRun(queryStr(InventTable));
        }

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
        public container pack()
    {
        return [#CurrentVersion,#CurrentList, queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </summary>
    /// <returns>
    ///    The instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </returns>
    /// <remarks>
    ///    This method is used if a query prompt is in the dialog and if a select menu item is added to a
    ///    dialog. Do not create the instance of the <c>QueryRun</c> class when this method is called. Do it
    ///    in the <c>unpack</c> method and the <c>initParmDefault</c> method, or in the <c>init</c> method.
    /// </remarks>
    QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    void run()
    {
        // InventTable
        this.checkFixInventTable();

        // ConfigChoice
        this.kernelCheckRecords(
            new QueryRun(SysQuery::addTableRelation(queryRun.query(),tableNum(ConfigChoice))),2);

        // InventItemLocation
        this.kernelCheckRecords(
            new QueryRun(SysQuery::addTableRelation(queryRun.query(),tableNum(InventItemLocation))),2);

        // InventBatch
        this.kernelCheckRecords(
            new QueryRun(SysQuery::addTableRelation(queryRun.query(),tableNum(InventBatch))),2);

        // CustVendExternalItem
        this.kernelCheckRecords(
            new QueryRun(SysQuery::addTableRelation(queryRun.query(),tableNum(CustVendExternalItem))),2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>treeParentClassId</Name>
				<Source><![CDATA[
        ClassId treeParentClassId()
    {
        InventConsistencyCheck  inventConsistencyCheck;

        return classIdGet(inventConsistencyCheck);
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
        public boolean unpack(container _packedClass)
    {
        Integer     version      = RunBase::getVersion(_packedClass);
        container   packedQuery;

        switch (version)
        {
            case #CurrentVersion :
                [version,#CurrentList, packedQuery] = _packedClass;
                if (packedQuery)
                {
                    queryRun = new QueryRun(packedQuery);
                }
                break;
            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
        static ClassDescription description()
    {
        return "@SYS7407";
    }

]]></Source>
			</Method>
			<Method>
				<Name>helpText</Name>
				<Source><![CDATA[
        static HelpTxt helpText()
    {
        return "@SYS57926";
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>
