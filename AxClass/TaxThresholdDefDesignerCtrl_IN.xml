<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxThresholdDefDesignerCtrl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Manages display of application in tax for form threshold designer
/// </summary>
[ThresholdDefDesignerCtrlAttribute_IN(tableStr(TaxValueThresholdProfile_IN))]
class TaxThresholdDefDesignerCtrl_IN extends ThresholdDefDesignerCtrl_IN
{
    boolean                     concessionCertificateEnable;
    boolean                     concessionCertificateVisible;

    #define.Tab_TaxValue('TaxValue')
    #define.MenuItemConssCert('ConcessionCertificateDetails_IN')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initDesigner</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes application in tax for form threshold designer
    /// </summary>
    public void initDesigner()
    {
        TaxValueThresholdProfile_IN taxValueThresholdProfileLoc;
        TaxInformationVendTable_IN  taxInformationVendTableLoc;

        concessionCertificateEnable     = false;
        concessionCertificateVisible    = false;

        if (sourceReferenceTableId == tableNum(TaxValueThresholdProfile_IN))
        {
            concessionCertificateVisible = true;
        }

        // control enable/disable of ConcessionCertificateDetails_IN
        taxValueThresholdProfileLoc = TaxValueThresholdProfile_IN::findByRecId(sourceReferenceRecId);
        if ((taxValueThresholdProfileLoc.AccountType             == AccountType_IN::Vend || taxValueThresholdProfileLoc.AccountType == AccountType_IN::Cust)
            && taxValueThresholdProfileLoc.AccountTableGroupAll == TableGroupAll::Table
            && taxValueThresholdProfileLoc.CustVendGroupOrTable)
        {
            if (taxValueThresholdProfileLoc.AccountType  == AccountType_IN::Vend)
            {
                taxInformationVendTableLoc = TaxInformationVendTable_IN::findByVendTable(taxValueThresholdProfileLoc.CustVendGroupOrTable);
                if (taxInformationVendTableLoc.RecId && taxInformationVendTableLoc.getPANTaxStatus_IN()  == PANTaxStatus_IN::Received)
                {
                    concessionCertificateEnable = true;
                }
            }
            else
            {
                concessionCertificateEnable = true;
            }
        }

        caller.design().controlName(#MenuItemConssCert).visible(concessionCertificateVisible);
        caller.design().controlName(#MenuItemConssCert).enabled(concessionCertificateEnable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes <c>TaxThresholdDefDesignerCtrl</c> instance.
    /// </summary>
    /// <param name="_caller">
    ///     Indicates the caller.
    /// </param>
    /// <param name="_threshold">
    ///     Indicates <c>Threhold_IN</c>.
    /// </param>
    /// <param name="_tree">
    ///     Indicates threshold tree control.
    /// </param>
    /// <param name="_thresholdSections_ds">
    ///     Indicates datasource of <c>ThresholdSection_IN</c>.
    /// </param>
    /// <param name="_sourceReferenceTableId">
    ///     Indicates tableId of the source reference.
    /// </param>
    /// <param name="_sourceReferenceRecId">
    ///     Indicates recId of the source reference.
    /// </param>
    public void initialize(
        FormRun               _caller,
        Threshold_IN          _threshold,
        FormTreeControl       _tree,
        FormDataSource        _thresholdSections_ds,
        RefTableId            _sourceReferenceTableId,
        RefRecId              _sourceReferenceRecId)
    {
        super(_caller,_threshold,_tree,_thresholdSections_ds,_sourceReferenceTableId,_sourceReferenceRecId);

        this.initDesigner();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDesigner</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates application in tax for form threshold designer
    /// </summary>
    /// <param name="_thresholdSection">
    /// threshold section
    /// </param>
    public void updateDesigner(ThresholdSections_IN _thresholdSection)
    {
        super(_thresholdSection);
        caller.design().controlName(#Tab_TaxValue).visible(_thresholdSection.IsLeaf);
        caller.design().controlName(#MenuItemConssCert).enabled(_thresholdSection.IsLeaf && concessionCertificateEnable);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>