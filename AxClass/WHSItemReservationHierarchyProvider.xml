<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSItemReservationHierarchyProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSItemReservationHierarchyProvider</c> class provides information
/// about the reservation hierachy of an item.
/// </summary>
class WHSItemReservationHierarchyProvider
{
    InventTable                     inventTable;
    WHSReservationHierarchyProvider hierarchyProvider;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>batchLevelAboveLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the level of the batch dimension in the reservation hierarchy if this dimension is above location.
    /// </summary>
    /// <returns>
    /// The level of the batch dimension in the reservation hierarchy if this dimension is above location.
    /// </returns>
    public WHSReservationHierarchyLevel batchLevelAboveLocation()
    {
        return this.parmHierarchyProvider().getBatchLevelAboveLocation(inventTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dimensionLevel</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the level of the specified dimension in the reservation hierarchy.
    /// </summary>
    /// <param name="_inventDimFieldId">
    /// The ID of the field to search for.
    /// </param>
    /// <returns>
    /// The level of the specified dimension in the reservation hierarchy.
    /// </returns>
    public WHSReservationHierarchyLevel dimensionLevel(FieldId _inventDimFieldId)
    {
        return this.parmHierarchyProvider().getDimLevel(inventTable, _inventDimFieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>deepestLevelFromInventDimParm</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the deepest hierarchy level that is enabled by the specified <c>InventDimParm</c>.
    /// </summary>
    /// <param name="_inventDimParm">
    /// The <c>InventDimParm</c> record specifying which dimensions to consider.
    /// </param>
    /// <returns>
    /// The deepest hierarchy level that is enabled by the specified <c>InventDimParm</c>.
    /// </returns>
    public WHSReservationHierarchyLevel deepestLevelFromInventDimParm(InventDimParm _inventDimParm)
    {
        List hierarchyList = this.parmHierarchyProvider().getHierarchyListAllFromInventTable(inventTable, WHSReservationHierarchySortOrder::BottomUp);
        return WHSReservationHierarchyInventDimUtil::deepestLevelFromInventDimParm(hierarchyList, _inventDimParm);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isBatchAboveLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if batch is above location in the reservation hierarchy of the item.
    /// </summary>
    /// <returns>
    /// true if batch is above location in the reservation hierarchy; otherwise, false.
    /// </returns>
    public boolean isBatchAboveLocation()
    {
        return this.parmHierarchyProvider().isBatchAboveLocation(inventTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmHierarchyProvider</Name>
				<Source><![CDATA[
    public WHSReservationHierarchyProvider parmHierarchyProvider(WHSReservationHierarchyProvider _hierarchyProvider = null)
    {
        if (!prmisDefault(_hierarchyProvider))
        {
            hierarchyProvider = _hierarchyProvider;
        }
        else if (!hierarchyProvider)
        {
            hierarchyProvider = WHSReservationHierarchyProvider::construct();
        }

        return hierarchyProvider;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventTable</Name>
				<Source><![CDATA[
    public InventTable parmInventTable(InventTable _inventTable = inventTable)
    {
        inventTable = _inventTable;
        return inventTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    private static WHSItemReservationHierarchyProvider construct()
    {
        return new WHSItemReservationHierarchyProvider();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromInventTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>WHSItemReservationHierarchyProvider</c> class for the specified item.
    /// </summary>
    /// <param name="_inventTable">
    /// An item.
    /// </param>
    /// <returns>
    /// A new instance of the <c>WHSItemReservationHierarchyProvider</c> class for the specified item.
    /// </returns>
    /// <exception cref="Exception::Error">
    /// Throw an error if the item is not specified.
    /// </exception>
    public static WHSItemReservationHierarchyProvider newFromInventTable(InventTable _inventTable)
    {
        if (!_inventTable.ItemId)
        {
            throw error(error::wrongUseOfFunction(funcName()));
        }

        WHSItemReservationHierarchyProvider provider = WHSItemReservationHierarchyProvider::construct();
        provider.parmInventTable(_inventTable);

        return provider;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
