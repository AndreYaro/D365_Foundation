<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FiscalDocumentTextParmDataCreator_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is reponsible for creating a obeject's list of fiscal document texts.
/// </summary>
class FiscalDocumentTextParmDataCreator_BR
{
    FiscalDocumentTextListParmData_BR textList;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copyFromRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create a Fiscal document text list from a record that has Fiscal document texts attached to.
    /// </summary>
    /// <param name="record">
    /// A <c>Connib</c> record to be used as source.
    /// </param>
    /// <remarks>
    /// The object will be use only the <c>DocuRef</c> records has a corresponding <c>DocuRefExt_BR</c> record.
    /// </remarks>
    private void copyFromRecord(Common record)
    {
        DocuTypeId docuTypeId = BrazilParameters::find().FiscalDocumentTextDocuTypeId;

        if (docuTypeId)
        {
            DocuRef docuRef;
            DocuRefExt_BR docuRefExt;

            while select docuRef
                join DocuRef, FiscalInformation, TextID, Type from docuRefExt
                    where docuRef.RefTableId == record.TableId
                        && docuRef.RefRecId == record.RecId
                        && docuRef.RefCompanyId == record.dataAreaId
                        && docuRef.TypeId == docuTypeId
                        && docuRefExt.DocuRef == docuRef.RecId
            {
                FiscalDocumentTextParmData_BR textParmData = FiscalDocumentTextParmData_BR::construct();

                textParmData.parmName(docuRef.Name);
                textParmData.parmNotes(docuRef.Notes);
                textParmData.parmRestriction(docuRef.Restriction);
                textParmData.parmFiscalInformation(docuRefExt.FiscalInformation);
                textParmData.parmTextID(docuRefExt.TextID);
                textParmData.parmType(docuRefExt.Type);

                FiscalDocumentReferencedProcess_BR fiscalDocumentReferencedProcess;

                while select fiscalDocumentReferencedProcess
                    where fiscalDocumentReferencedProcess.DocuRefExt_BR == docuRefExt.RecId
                {
                    FiscalDocumentRefProcessParmData_BR refProcess = new FiscalDocumentRefProcessParmData_BR();

                    refProcess.parmAgency(fiscalDocumentReferencedProcess.Agency);
                    refProcess.parmProcessNumber(fiscalDocumentReferencedProcess.ProcessNumber);
                    refProcess.parmConcessionaryActType(fiscalDocumentReferencedProcess.ConcessionaryActType);

                    textParmData.addFiscalRefProcess(refProcess);
                }

                textList.add(textParmData);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the class.
    /// </summary>
    /// <param name="_textList">
    /// <c>FiscalDocumentTextListParmData_BR</c> to be filled; optional.
    /// </param>
    protected void new(FiscalDocumentTextListParmData_BR _textList = null)
    {
        if (_textList == null)
        {
            textList = FiscalDocumentTextListParmData_BR::construct();
        }
        else
        {
            textList = _textList;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTextList</Name>
				<Source><![CDATA[
        public FiscalDocumentTextListParmData_BR parmTextList(FiscalDocumentTextListParmData_BR _textList = textList)
    {
        textList = _textList;

        return textList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>create</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the class.
    /// </summary>
    /// <param name="_textList">
    /// <c>FiscalDocumentTextListParmData_BR</c> to be filled; optional.
    /// </param>
    /// <returns>
    /// A new <c>FiscalDocumentTextParmDataCreator_BR</c>.
    /// </returns>
    static public FiscalDocumentTextParmDataCreator_BR create(FiscalDocumentTextListParmData_BR _textList = null)
    {
        return new FiscalDocumentTextParmDataCreator_BR(_textList);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createFromRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of <c>FiscalDocumentTextParmDataCreator_BR</c> for a given
    /// record.
    /// </summary>
    /// <param name="_record">
    /// The record that the <c>DocuRef</c> references point to.
    /// </param>
    /// <returns>
    /// A new <c>FiscalDocumentTextParmDataCreator_BR</c> for the given record, with the
    /// <c>FiscalDocumentTextListParmData_BR</c> ready to be retrieved.
    /// </returns>
    static public FiscalDocumentTextParmDataCreator_BR createFromRecord(Common _record)
    {
        FiscalDocumentTextParmDataCreator_BR textCreator;

        textCreator = new FiscalDocumentTextParmDataCreator_BR();
        textCreator.copyFromRecord(_record);

        return textCreator;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>