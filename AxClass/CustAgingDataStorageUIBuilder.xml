<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustAgingDataStorageUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustAgingDataStorageUIBuilder</c> class builds the UI for the <c>CustAgingDataStorage</c> parameters.
/// </summary>
internal final class CustAgingDataStorageUIBuilder extends SysOperationAutomaticUIBuilder
{

    private DialogField billingClassificationField;

    private const int LimitTextLength = 59;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>billingClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a Billing Classification lookup.
    /// </summary>
    internal void billingClassificationLookup()
    {
        Query query = new Query(queryStr(CustBillingClassificationLookup));
        TableId multiSelectTableNum = tableNum(CustBillingClassification);
        container selectedFields = [multiSelectTableNum, fieldName2id(multiSelectTableNum, fieldStr(CustBillingClassification, BillingClassification))];

        SysLookupMultiSelectCtrl::constructWithQuery(this.dialog().dialogForm().formRun(), billingClassificationField.control(), query, false, selectedFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the <c>CustAgingDataStorage</c> parameter form that is based on billing classification.
    /// </summary>
    [Hookable(false)]
    public void build()
    {
        boolean isBillingClassEnabled;
        DialogField inclTransWithNoBillingClassField;
        DialogField batchNameField;
        CustAgingDataStorageContract custAgingDataStorageContract;

        super();

        custAgingDataStorageContract = this.dataContractObject() as CustAgingDataStorageContract;

        billingClassificationField = this.bindInfo().getDialogField(CustAgingDataStorageContract, methodStr(CustAgingDataStorageContract, parmBillingClassification));
        inclTransWithNoBillingClassField = this.bindInfo().getDialogField(CustAgingDataStorageContract, methodStr(CustAgingDataStorageContract, parmInclTransWithNoBillingClass));

        // controlling visibility
        if (billingClassificationField && inclTransWithNoBillingClassField)
        {
            isBillingClassEnabled = CustParameters::isBillingClassificationEnabled();
            billingClassificationField.visible(isBillingClassEnabled);
            inclTransWithNoBillingClassField.visible(isBillingClassEnabled);
            billingClassificationField.lookupButton(FormLookupButton::Always);
        }

        batchNameField = this.bindInfo().getDialogField(CustAgingDataStorageContract, methodStr(CustAgingDataStorageContract, parmBatchName));
        batchNameField.mandatory_RU(true);
        batchNameField.registerOverrideMethod(methodStr(FormStringControl, validate), methodStr(CustAgingDataStorageUIBuilder, validateBatchNameFormControl),this);
        batchNameField.value('');
        batchNameField.limitText(LimitTextLength);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implements the functionality that is supposed to execute after run.
    /// </summary>
    [Hookable(false)]
    public void postRun()
    {
        if (CustParameters::isBillingClassificationEnabled())
        {
            this.billingClassificationLookup();
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateBatchNameFormControl</Name>
				<Source><![CDATA[
    private boolean validateBatchNameFormControl(FormStringControl  _control)
    {
        return CustAgingDataStorageContract::isBatchNameValid(_control.valueStr());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>