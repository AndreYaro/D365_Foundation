<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>DefaultDashboard_ApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(DefaultDashboard))]
internal final class DefaultDashboard_ApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        next init();

        this.buildAppTilesGroup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildAppTilesGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds out the tile group "ExternalApps".  It will dynamically add a tile for each class that extends "DefaultDashboardAppTileBuilderBase"
    /// and will display them in alphabetical order based on their label.
    /// </summary>
    private void buildAppTilesGroup()
    {
        Array instances = SysPluginFactory::Instances(
            identifierStr(Dynamics.AX.Application),
            classStr(DefaultDashboardAppTileBuilderBase),
            new SysPluginMetadataCollection());

        Map appTiles = new Map(Types::String, Types::Class);

        for (int i = 1; i <= instances.lastIndex(); i++)
        {
            DefaultDashboardAppTileBuilderBase instance = instances.value(i);

            if (instance.isEnabled())
            {
                appTiles.add(instance.getTileLabel(), instance);
            }
        }

        if (!appTiles.empty())
        {
            FormGroupControl externalAppContainer = this.ExternalApps;
            externalAppContainer.visible(true);

            FormGroupControl tileContainer = this.TilePageContainer;
            tileContainer.caption("@BusinessPerformanceAnalytics:DefaultDashBoardWorkspaces");

            MapEnumerator mapEnum = appTiles.getEnumerator();

            int countOfTiles = 1;

            while (mapEnum.moveNext())
            {
                mapEnum.currentValue().addTile(externalAppContainer, countOfTiles);
                countOfTiles++;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>