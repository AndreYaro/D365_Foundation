<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjConsumptionProjDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjConsumptionProjDP</c> class provides data for the
///    <c>ProjListProjConsumptionProj</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjConsumptionContract))

]
class ProjListProjConsumptionProjDP extends SRSReportDataProviderBase
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    TransDate               projFromDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    ProjListColumnData      actualVsBudget;
    ForecastModelId         forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;

    ProjListProjTrans_Consumption   listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjHelpClass           projHelpClass;
    ProjListProjConsumptionTmp      projListProjConsumptionTmp;

    ProjTable               projTable;

    QueryRun                qrun;

    ProjId                  projIdFound, projId;
    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    ProjQty                 tmpQtyHour;
    AmountMST               tmpSalary, tmpExpenses, tmpInventory;

    int                     reportType;
    int                     hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals;
    int                     showCostCost, showEmplCost, showItemCost, showQtyHour, showTotalCost;
    int                     showRevenue, showContributionMargin, showGrossMarginPct, showValueAdded, showValueFactor;
    str                     headerLabel, periodLabel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the consumption amount for hour, expense, and item values.
    /// </summary>
    void calculate()
    {
        projHelpClass.parmProjStatement().parmProjId(projIdFound);
        projHelpClass.setParmField();

        switch (amountQty)
        {
            case ProjAmountQty::Amount:
                tmpSalary       += projHelpClass.parmProjStatement().c105_Consumption_Hour();
                break;

            case ProjAmountQty::Qty:
                tmpQtyHour      += projHelpClass.parmProjStatement().c105_Consumption_Hour_qty();
                break;

            case ProjAmountQty::AmountQty:
                tmpSalary       += projHelpClass.parmProjStatement().c105_Consumption_Hour();
                tmpQtyHour      += projHelpClass.parmProjStatement().c105_Consumption_Hour_qty();
                break;
        }

        tmpExpenses    += projHelpClass.parmProjStatement().c125_Consumption_Cost();
        tmpInventory   += projHelpClass.parmProjStatement().c120_Consumption_Item();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks the parameters to determines whether the line should be permitted.
    /// </summary>
    /// <returns>
    ///    true if the line should be printed; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(projHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should be printed and inserts it into the temporary table.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record buffer that contains information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (stateProjType == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= level) ||
                (projTableLocal.levelFormat(level)   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    projHelpClass.setName();
                    this.insertProjListProjConsumptionTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            projHelpClass.setName();
            this.insertProjListProjConsumptionTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjConsumptionTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the data from the <c>projListProjConsumptionTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>projListProjConsumptionTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('ProjListProjConsumptionTmp')]
    public ProjListProjConsumptionTmp getProjListProjConsumptionTmp()
    {
        select * from projListProjConsumptionTmp;

        return projListProjConsumptionTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the <c>ProjListProjTrans</c> class by using the parameters.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans = ProjListProjTrans::construct(ProjListStateType::Consumption, ProjListStateId::Project);
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);
        listProjTrans.parmtime(time);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjToDate(projToDate);
        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmColumnData(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjConsumptionTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjConsumptionTmp()
    {
        projListProjConsumptionTmp.clear();

        projListProjConsumptionTmp.Id            = projHelpClass.id();
        projListProjConsumptionTmp.IdLabel       = enum2str(stateProjType);
        projListProjConsumptionTmp.Name          = projHelpClass.name();

        projListProjConsumptionTmp.QtyHour       = tmpQtyHour;

        projListProjConsumptionTmp.EmplCost      = listProjTransLayout.showAmountReport(tmpSalary, showAmount);
        projListProjConsumptionTmp.CostCost      = listProjTransLayout.showAmountReport(tmpExpenses, showAmount);
        projListProjConsumptionTmp.ItemCost      = listProjTransLayout.showAmountReport(tmpInventory, showAmount);
        projListProjConsumptionTmp.TotalCost     = listProjTransLayout.showAmountReport((tmpSalary + tmpExpenses + tmpInventory), showAmount);

        // set labels
        projListProjConsumptionTmp.HeaderLabel   = headerLabel;
        projListProjConsumptionTmp.PeriodLabel   = periodLabel;

        // set decimal values
        projListProjConsumptionTmp.DecimalHourQty     = hourQtyDecimals;
        projListProjConsumptionTmp.DecimalAmount      = amountDecimals;

        // set field visibility
        projListProjConsumptionTmp.ShowCostCost           = showCostCost;
        projListProjConsumptionTmp.ShowEmplCost           = showEmplCost;
        projListProjConsumptionTmp.ShowItemCost           = showItemCost;
        projListProjConsumptionTmp.ShowQtyHour            = showQtyHour;
        projListProjConsumptionTmp.ShowTotalCost          = showTotalCost;

        projListProjConsumptionTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the consumption amounts for hour, expense, and item values are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if all the values are not zero; otherwise false.
    /// </returns>
    boolean lineValue()
    {
        return (tmpExpenses  != 0 || tmpSalary != 0 || tmpInventory != 0 || tmpQtyHour != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = qrun.query().dataSourceTable(tableNum(ProjTable));
        qbdsProjTable.addSortField(fieldNum(ProjTable,ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = qrun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
            PSAContractLineItemRangesHelper::addTransactionTypeRangesToDS(
                qbdsPSAContractLineItems,
                hour,
                expenses,
                false,
                item,
                false);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Internal));
            }

            if (time)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Time));
            }

            if (!(timeMaterial || fixedPrice || investment || internalCost || time))
            {
                QueryBuildRange rangeEmptyString = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
                rangeEmptyString.value(queryValue(SysQuery::valueEmptyString()));
            }
        }

        if (skipZero)
        {
            QueryBuildDataSource qbdsProjectTrans;

            if (actualVsBudget == ProjListColumnData::Actual)
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransPosting)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransPosting, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, ProjTransDate)).value(queryValue(listProjTrans.parmProjFromDate()) + '..' + queryValue(listProjTrans.parmProjToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans.parmLedgerFromDate()) + '..' + queryValue(listProjTrans.parmLedgerToDate()));
            }
            else
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransBudget)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransBudget, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, ProjTransDate)).value(queryValue(listProjTrans.parmProjFromDate()) + '..' + queryValue(listProjTrans.parmProjToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans.parmLedgerFromDate()) + '..' + queryValue(listProjTrans.parmLedgerToDate()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = qrun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = qrun.get(tablenum(ProjFundingSource));
        }
        projHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);

        if (! listProjTransLayout.canPrintId(projHelpClass.idNew()))
        {
            projHelpClass.setOld();
            return;
        }

        if (projHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        projHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(level);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SQL
    ///    Server Reporting Services (SSRS) at run time. This method should compute data and populate the data
    ///    tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjConsumptionContract contract = this.parmDataContract() as ProjListProjConsumptionContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();
        time = contract.parmTime();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        projFromDate = contract.parmProjFromDate();
        projToDate = contract.parmProjToDate();
        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();

        actualVsBudget = contract.parmActualVsBudget();
        forecastModelId = contract.parmForecastModelIdV2();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();

        reportType = contract.parmReportType();

        firstRun = true;
        this.initProjListTrans();

        headerLabel   = listProjTrans.formatSelection(showAmount);
        periodLabel   = listProjTrans.formatPeriodLabel('',DateFlags::None);

        [showQtyHour, showEmplCost, showCostCost,
         showItemCost, showTotalCost] = listProjTrans.setDisplayFlags(amountQty, hour, expenses, item);

        [hourQtyDecimals, amountDecimals,
        grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        projHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                projStatement::newListProjConsumption(listProjTrans,
                                projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                                amountQty,
                                forecastModelId),
                                stateProjType);

        qrun = new QueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (qrun.next())
        {
            this.printProject();
            firstRun = false;
        }

        if (this.checkPrintLine())
        {
            projHelpClass.setName();
            this.insertProjListProjConsumptionTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the variables to zero.
    /// </summary>
    void setFieldZero()
    {
        tmpSalary      = 0;
        tmpExpenses    = 0;
        tmpInventory   = 0;
        tmpQtyHour     = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        qrun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>