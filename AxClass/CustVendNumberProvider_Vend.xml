<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendNumberProvider_Vend</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustVendNumberProvider</c> class generates vendors numbers for the import from data entities.
/// </summary>
internal final class CustVendNumberProvider_Vend extends CustVendNumberProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static CustVendNumberProvider_Vend construct()
    {
        return new CustVendNumberProvider_Vend();
    }

]]></Source>
			</Method>
			<Method>
				<Name>loadCustVendNumberTmp</Name>
				<Source><![CDATA[
    protected void loadCustVendNumberTmp(DMFDefinitionGroupExecution _dmfDefinitionGroupExecution,
        CustVendStagingMap _staging,
        CustVendNumbersTmp _custVendNumbersTmp)
    {
        VendGroup vendGroupPrev;
        VendTable vendTable;
        RecordInsertList custVendNumbersTmpList = new RecordInsertList(tableNum(CustVendNumbersTmp), true, true, true, false, false, _custVendNumbersTmp);

        while select _staging
            order by _staging.CustVendGroup
            where _staging.DefinitionGroup == _dmfDefinitionGroupExecution.DefinitionGroup
                && _staging.ExecutionId     == _dmfDefinitionGroupExecution.ExecutionId
                && (_staging.TransferStatus == DMFTransferStatus::NotStarted || _staging.TransferStatus == DMFTransferStatus::Validated)
                && _staging.CustVendGroup
            notexists join vendTable
                where vendTable.AccountNum == _staging.CustVendAccount

        {
            _custVendNumbersTmp.clear();
            _custVendNumbersTmp.RefRecId = _staging.RecId;
            _custVendNumbersTmp.CustVendGroup = _staging.CustVendGroup;

            VendGroup vendGroup = vendGroupPrev.VendGroup == _staging.CustVendGroup ? vendGroupPrev
                : VendGroup::find(_staging.CustVendGroup);

            if (vendGroup.VendAccountNumSeq)
            {
                NumberSequenceTable numberSequenceTable = NumberSequenceTable::find(vendGroup.VendAccountNumSeq);

                if (numberSequenceTable && !numberSequenceTable.Manual)
                {
                    _custVendNumbersTmp.CustVendAccount = NumberSeq::newGetNumFromId(vendGroup.VendAccountNumSeq).num();
                    custVendNumbersTmpList.add(_custVendNumbersTmp);
                }
            }
            
            vendGroupPrev = vendGroup;
        }

        custVendNumbersTmpList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>