<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PcGlobalTableConstraintDataSourceBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class is responsible for adding the <c>FormDataSource</c> instances for the user defined table constraint form.
/// </summary>
class PcGlobalTableConstraintDataSourceBuilder
{
    Map                     dataSourceReference;
    DictTable               dictTable;
    int                     dataSourceNo;
    Form                    form;

    #define.firstDataSourceNo(0)
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a data source for the targeted form and returns it.
    /// </summary>
    /// <param name="_dataSourceName">
    /// A data source name.
    /// </param>
    /// <returns>
    /// An instance of the <c>FormBuildDataSource</c> class.
    /// </returns>
    public FormBuildDataSource addDataSource(str _dataSourceName)
    {
        FormBuildDataSource formDataSource;

        if (dataSourceNo == 0)// first
        {
            formDataSource = form.dataSource(1); // the datasource for the table constraint content is the first one in the target form
            formDataSource.linkType(FormLinkType::Delayed);
        }
        else
        {
            formDataSource  = form.addDataSource(_dataSourceName);
            formDataSource.joinSource(dataSourceReference.lookup(dataSourceNo - 1));// join with previous
            formDataSource.linkType(FormLinkType::InnerJoin);
        }

        formDataSource.name(_dataSourceName);
        formDataSource.table(dictTable.id());
        formDataSource.index(indexNum(PCGlobalTableConstraintEditTmp,RowIdx));
        formDataSource.autoSearch(true);
        formDataSource.autoQuery(true);
        formDataSource.allowCreate(true);
        formDataSource.allowDelete(true);
        formDataSource.allowEdit(true);

        dataSourceReference.insert(dataSourceNo, formDataSource.name());

        dataSourceNo ++;

        return formDataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstDataSourceName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the first data source name of the form.
    /// </summary>
    /// <returns>
    /// A data source name.
    /// </returns>
    public str getFirstDataSourceName()
    {
        return dataSourceReference.lookup(#firstDataSourceNo);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the data source builder.
    /// </summary>
    /// <param name="_form">
    /// The targeted form.
    /// </param>
    public void initialize(Form _form)
    {
        PCContractNotNull::assertObject(_form, funcName());

        form = _form;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        dictTable = new DictTable(tableNum(PCGlobalTableConstraintEditTmp));

        dataSourceReference = new Map(Types::Integer, Types::String);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static PCGlobalTableConstraintDataSourceBuilder construct()
    {
        return new PCGlobalTableConstraintDataSourceBuilder();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>