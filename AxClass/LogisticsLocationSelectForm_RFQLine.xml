<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsLocationSelectForm_RFQLine</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class LogisticsLocationSelectForm_RFQLine extends LogisticsLocationSelectForm
{
    PurchRFQLine purchRFQLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowedSources</Name>
				<Source><![CDATA[
        public Set allowedSources()
    {
        Set ret;

        ret = super();

        ret.add(LogisticsLocationSelectSourceType::RequestForQuote);
        ret.add(LogisticsLocationSelectSourceType::RequestForQuoteLine);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyAddressToCaller</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies the selected address to the calling RFQ reply line.
    /// </summary>
    /// <param name="_transferFromTable">
    /// The source of the address.
    /// </param>
    /// <param name="_addressRecId">
    /// The selected address.
    /// </param>
    public void copyAddressToCaller(Common _transferFromTable, LogisticsPostalAddressRecId _addressRecId)
    {
        purchRFQLine.DeliveryPostalAddress = _addressRecId;
        purchRFQLine.AddressRefRecId   = _transferFromTable.RecId;
        purchRFQLine.AddressRefTableId = _transferFromTable.TableId;

        // <GBR>
        if (BrazilParameters::isEnabled())
        {
            purchRFQLine.cfopTable_BR = this.getDefaultCFOP_BR(purchRFQLine);
        }
        // </GBR>
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// The initial selected source for addresses on the form.
    /// </summary>
    /// <returns>
    /// LogisticslocationSelectSourceType corresponding to the address source.
    /// </returns>
    public LogisticsLocationSelectSourceType defaultSource()
    {
        LogisticsLocationSelectSourceType   sourceType = super();
        Common                              addressOwner;

        if (purchRFQLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchRFQLine.AddressRefTableId,
                purchRFQLine.AddressRefRecId
            );

            if (addressOwner)
            {
                sourceType = LogisticsLocationSelectForm::tableIdToSourceType(
                    purchRFQLine.AddressRefTableId,
                    sourceType
                );
            }
        }

        return sourceType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLogisticsPostalAddressRecId</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the record ID of the <c>LogisticsPostalAddress</c> that is currently selected.
    /// </summary>
    /// <returns>
    ///   The <c>LogisticsPostalAddress</c> record ID.
    /// </returns>
    public recId getLogisticsPostalAddressRecId()
    {
        return purchRFQLine.DeliveryPostalAddress;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the record the target form's grid should have selected upon openning.
    /// </summary>
    /// <returns>
    ///   The record to be selected.
    /// </returns>
    public Common getLookupRecord()
    {
        Common addressOwner;

        if (purchRFQLine.AddressRefTableId)
        {
            addressOwner = LogisticsLocationSelectForm::findByRecId(
                purchRFQLine.AddressRefTableId,
                purchRFQLine.AddressRefRecId
            );
        }

        if (!addressOwner)
        {
            addressOwner = CompanyInfo::find();
        }

        return addressOwner;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMainTableBuffer_BR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns <c>PurchRFQLine</c> table.
    /// </summary>
    /// <returns>
    /// Table buffer PurchRFQLine.
    /// </returns>
    public PurchRFQLine getMainTableBuffer_BR()
    {
        return purchRFQLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        void new(PurchRFQLine _purchRFQLine)
    {
        purchRFQLine = _purchRFQLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a new <c>LogisticsLocationSelectForm_RFQLine</c> object.
    /// </summary>
    /// <param name="_buffer">
    ///    A record to use to initialize the <c>LogisticsLocationSelectForm_RFQLine</c> object.
    /// </param>
    /// <returns>
    ///    A new <c>LogisticsLocationSelectForm_RFQLine</c> object.
    /// </returns>
    public static LogisticsLocationSelectForm_RFQLine construct(Common _buffer)
    {
        return new LogisticsLocationSelectForm_RFQLine(_buffer);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>