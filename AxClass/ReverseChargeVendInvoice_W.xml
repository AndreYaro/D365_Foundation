<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReverseChargeVendInvoice_W</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///   The class <c>ReverseChargeVendInvoice_W</c> contains logic of appliyng reverse charge tax groups to vendor invoice lines.
/// </summary>
public class ReverseChargeVendInvoice_W extends ReverseCharge_W
{
    private VendInvoiceInfoTable   vendInvoiceInfoTable;
    private VendInvoiceInfoLine    vendInvoiceInfoLine;

    private VendAccount                 lastInvoiceAccount;
    private LogisticsPostalAddressRecId lastDeliveryAddress;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>documentType</Name>
				<Source><![CDATA[
    protected ReverseChargeDocumentType_W documentType()
    {
        return ReverseChargeDocumentType_W::VendInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>headerChanged</Name>
				<Source><![CDATA[
    protected void headerChanged()
    {
        super();

        if (vendInvoiceInfoTable.isNonPO())
        {
            partnerCountryRegionType = IntrastatCountryRegionParameters::getCountryRegionType(vendInvoiceInfoTable.vendTable_InvoiceAccount().postalAddress());

            if (vendInvoiceInfoTable.DeliveryPostalAddress)
            {
                deliveryCountryRegionType = IntrastatCountryRegionParameters::getCountryRegionType(LogisticsPostalAddress::findRecId(vendInvoiceInfoTable.DeliveryPostalAddress));
            }
            else
            {
                deliveryCountryRegionType = IntrastatCountryRegionParameters::getCountryRegionType(CompanyInfo::find().postalAddress());
            }

            lastInvoiceAccount  = vendInvoiceInfoTable.InvoiceAccount;
            lastDeliveryAddress = vendInvoiceInfoTable.DeliveryPostalAddress;
        }
        else
        {
            isHeaderInitialized = vendInvoiceInfoLine.OrigPurchId == null;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isHeaderChanged</Name>
				<Source><![CDATA[
    protected boolean isHeaderChanged()
    {
        boolean ret;

        if (super()
            || lastInvoiceAccount  != vendInvoiceInfoTable.InvoiceAccount
            || lastDeliveryAddress != vendInvoiceInfoTable.DeliveryPostalAddress)
        {
            ret = true;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isReverseChargeGroupSet</Name>
				<Source><![CDATA[
    protected boolean isReverseChargeGroupSet()
    {
        return vendInvoiceInfoLine.TaxGroup == purchTaxGroup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineAmount</Name>
				<Source><![CDATA[
    protected AmountMST lineAmount(RecId _lineRecId, Common _line = null)
    {
        AmountMST   lineAmount;
        VendInvoiceInfoLine vendInvoiceInfoLineLocal;

        if (_lineRecId
            && _lineRecId != vendInvoiceInfoLine.RecId
            && lineAmountMap.exists(_lineRecId))
        {
            lineAmount = lineAmountMap.lookup(_lineRecId);
        }
        else
        {
            if (prmisDefault(_line))
            {
                vendInvoiceInfoLineLocal = VendInvoiceInfoLine::findRecId(_lineRecId);
            }
            else
            {
                vendInvoiceInfoLineLocal = _line;
            }

            lineAmount = CurrencyExchangeHelper::mstAmount(vendInvoiceInfoLineLocal.lineAmountExclTax(), vendInvoiceInfoLineLocal.CurrencyCode);

            if (_lineRecId)
            {
                lineAmountMap.insert(_lineRecId, lineAmount);
            }
            else
            {
                currentlineAmount = lineAmount;
            }
        }

        return lineAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineChanged</Name>
				<Source><![CDATA[
    protected void lineChanged()
    {
        super();

        itemGroupId = this.findGroupByItemCategory(
            ReverseChargePurchSales_W::Purch,
            vendInvoiceInfoLine.ItemId,
            vendInvoiceInfoLine.ProcurementCategory);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setHeaderRecord</Name>
				<Source><![CDATA[
    protected void setHeaderRecord(Common _headerRecord)
    {
        super(_headerRecord);

        vendInvoiceInfoTable = _headerRecord as VendInvoiceInfoTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setLineRecord</Name>
				<Source><![CDATA[
    protected void setLineRecord(Common _lineRecord)
    {
        super(_lineRecord);

        vendInvoiceInfoLine = _lineRecord as VendInvoiceInfoLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReverseChargeGroup</Name>
				<Source><![CDATA[
    protected void setReverseChargeGroup()
    {
        vendInvoiceInfoLine.TaxGroup = purchTaxGroup;
        this.setOverrideSalesTax(vendInvoiceInfoLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReverseChargeGroupMatchingLines</Name>
				<Source><![CDATA[
    public void setReverseChargeGroupMatchingLines(boolean _fromHeader = false)
    {
        #OCCRetryCount

        VendInvoiceInfoLine vendInvoiceInfoLineLocal;
        SetEnumerator       se;
        boolean             researchDS;

        if (updateMatchingLines)
        {
            try
            {
                ttsBegin;

                if (ruleItemGroupId &&
                    lineGroupMap &&
                    lineGroupMap.exists(ruleItemGroupId))
                {
                    se = lineGroupMap.lookup(ruleItemGroupId).getEnumerator();

                    while (se.moveNext())
                    {
                        if (_fromHeader || se.current() != vendInvoiceInfoLine.RecId)
                        {
                            vendInvoiceInfoLineLocal = VendInvoiceInfoLine::findRecId(se.current(), true);

                            if (vendInvoiceInfoLineLocal.TaxGroup != purchTaxGroup || this.needSetOverrideSalesTax(vendInvoiceInfoLineLocal))
                            {
                                vendInvoiceInfoLineLocal.TaxGroup = purchTaxGroup;
                                this.setOverrideSalesTax(vendInvoiceInfoLineLocal);
                                vendInvoiceInfoLineLocal.update();

                                researchDS = true;
                            }
                        }
                    }
                }
                else
                {
                    while select forUpdate vendInvoiceInfoLineLocal
                        where vendInvoiceInfoLineLocal.ParmId == vendInvoiceInfoLine.ParmId
                            && vendInvoiceInfoLineLocal.TableRefId == vendInvoiceInfoLine.TableRefId
                            && (vendInvoiceInfoLineLocal.TaxGroup != purchTaxGroup || this.needSetOverrideSalesTax(vendInvoiceInfoLineLocal))
                            && (_fromHeader || vendInvoiceInfoLineLocal.RecId != vendInvoiceInfoLine.RecId)
                    {
                        if (this.checkItemGroupNoThreshold(
                                ReverseChargePurchSales_W::Purch,
                                vendInvoiceInfoLineLocal.ItemId,
                                vendInvoiceInfoLineLocal.ProcurementCategory))
                        {
                            vendInvoiceInfoLineLocal.TaxGroup = purchTaxGroup;
                            this.setOverrideSalesTax(vendInvoiceInfoLineLocal);
                            vendInvoiceInfoLineLocal.update();

                            researchDS = true;
                        }
                    }
                }

                ttsCommit;
            }
            catch (Exception::Deadlock)
            {
                retry;
            }
            catch (Exception::UpdateConflict)
            {
                if (appl.ttsLevel() == 0)
                {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        retry;
                    }
                }
                else
                {
                    throw Exception::UpdateConflict;
                }
            }
            catch (Exception::Error)
            {
                throw error("@SYS93835");
            }

            FormObjectSet dataSource = FormDataUtil::getFormDataSource(vendInvoiceInfoLine);
            if (!_fromHeader && researchDS && dataSource)
            {
                dataSource.research(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>thresholdByGroupUseDB</Name>
				<Source><![CDATA[
    protected AmountMST thresholdByGroupUseDB(ReverseChargeItemGroupId_W _ruleItemGroupId)
    {
        VendInvoiceInfoLine         vendInvoiceInfoLineLocal;
        ReverseChargeItemGroupId_W  lineGroupId;
        AmountMST                   threshold;

        if (_ruleItemGroupId)
        {
            lineGroupMap = new Map(Types::Int64, Types::Class);
        }

        while select vendInvoiceInfoLineLocal
            where vendInvoiceInfoLineLocal.ParmId     == vendInvoiceInfoLine.ParmId     &&
                  vendInvoiceInfoLineLocal.TableRefId == vendInvoiceInfoLine.TableRefId &&
                  vendInvoiceInfoLineLocal.RecId      != vendInvoiceInfoLine.RecId

        {
            if (_ruleItemGroupId)
            {
                lineGroupId = this.findGroupByItemCategory(ReverseChargePurchSales_W::Purch, vendInvoiceInfoLineLocal.ItemId, vendInvoiceInfoLineLocal.ProcurementCategory);

                this.addLineToCache(lineGroupId, vendInvoiceInfoLineLocal.RecId);
            }

            if (!_ruleItemGroupId || lineGroupId == _ruleItemGroupId)
            {
                threshold += this.lineAmount(vendInvoiceInfoLineLocal.RecId, vendInvoiceInfoLineLocal);
            }
        }

        return threshold;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>