<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsAddressCity_RU_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of table <c>LogisticsAddressCity</c>
/// </summary>
[ExtensionOf(tableStr(LogisticsAddressCity))]
final class LogisticsAddressCity_RU_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupCityId_RU_Extension</Name>
				<Source><![CDATA[
    protected client static QueryBuildDataSource lookupCityId_RU_Extension(QueryBuildDataSource _qbds, FieldId _recIdNum, FieldId _tableIdNum)
    {
        next lookupCityId_RU_Extension(_qbds, _recIdNum, _tableIdNum);

        return LogisticsAddressNotFoundRecords_RU::filterQueryByLog(_qbds, _recIdNum, _tableIdNum);
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookUpByAddressLevel_RU</Name>
				<Source><![CDATA[
    internal client static LogisticsAddressCity lookUpByAddressLevel_RU(
        FormReferenceControl _control,
        LogisticsCityAddressLevel_RU _addressLevel,
        LogisticsAddressCountryRegionId _countryRegionId,
        LogisticsAddressStateId _stateId,
        LogisticsAddressCountyId _countyId,
        LogisticsAddressCityRecId _settlementId = 0,
        LogisticsAddressCityRecId _cityId = 0,
        boolean _isAddressSetup = true)
    {
        SysReferenceTableLookup lookup = SysReferenceTableLookup::newParameters(
            tablenum(LogisticsAddressCity),
            _control,
            true);

        lookup.addLookupfield(fieldnum(LogisticsAddressCity, Name));
        lookup.addLookupfield(fieldnum(LogisticsAddressCity, Description));
        lookup.addLookupMethod(tablemethodstr(LogisticsAddressCity, fullName_RU));

        if (!_isAddressSetup)
        {
            lookup.addLookupMethod(tablemethodstr(LogisticsAddressCity, propertiesZipCode_RU));
            lookup.addLookupMethod(tablemethodstr(LogisticsAddressCity, propertiesGniCode_RU));
        }

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(LogisticsAddressCity));
        qbds = LogisticsAddressCity::lookupCityId_RU_Extension(qbds, fieldNum(LogisticsAddresssCity, RecId), fieldNum(LogisticsAddresssCity, TableId));
        qbds.addRange(fieldnum(LogisticsAddressCity, CountryRegionId)).value(queryValue(_countryRegionId));
        qbds.addRange(fieldnum(LogisticsAddressCity, StateId)).value(queryValue(_stateId));
        qbds.addRange(fieldnum(LogisticsAddressCity, CountyId)).value(queryValue(_countyId));
        qbds.addRange(fieldnum(LogisticsAddressCity, AddressLevel_RU)).value(queryValue(_addressLevel));

        if (_addressLevel == LogisticsCityAddressLevel_RU::City)
        {
            qbds.addRange(fieldnum(LogisticsAddressCity, SettlementRecId)).value(queryValue(_settlementId));
        }
        else if (_addressLevel == LogisticsCityAddressLevel_RU::Locality)
        {
            qbds.addRange(fieldnum(LogisticsAddressCity, SettlementRecId)).value(queryValue(_settlementId));
            qbds.addRange(fieldnum(LogisticsAddressCity, CityRecId)).value(queryValue(_cityId));
        }

        lookup.parmQuery(query);
        LogisticsAddressCity selectedRecord = lookup.performFormLookup();
        return selectedRecord;
    }

]]></Source>
			</Method>
			<Method>
				<Name>propertiesOKTMO_RU</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays or sets OKTMO for address element
    /// </summary>
    /// <param name="_set">
    /// true when editing; false when viewing.
    /// </param>
    /// <param name="_oktmo">
    /// New value
    /// </param>
    /// <returns>
    /// Current (edited if _set is true) value
    /// </returns>
    [SysClientCacheDataMethodAttribute(true)]
    edit OKTMO_RU propertiesOKTMO_RU(
        boolean _set,
        OKATO_RU _oktmo)
    {
        if (!this.RecId && _set)
        {
            this.insert();
        }
        return LogisticsAddressFiasGuidCodes_RU::parmOKTMOByAddressTableId(this, _oktmo, _set);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>