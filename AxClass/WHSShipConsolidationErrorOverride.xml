<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSShipConsolidationErrorOverride</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSShipConsolidationErrorOverride</c> overrides shipment consolidation errors.
/// </summary>
internal abstract class WHSShipConsolidationErrorOverride
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    internal static void main(Args _args)
    {
        if (!_args || !_args.dataset() || !_args.parmEnumType())
        {
            throw error("@SYS30498");
        }

        if (_args.parmEnumType() != enumNum(NoYes))
        {
            throw error("@SYS30498");
        }

        Common                      selectedRecord;
        WHSShipConsolidationError   shipConsolidationError;
        NoYes                       errorOverride = _args.parmEnum();
        FormDataSource              selectedRecordDS = FormDataUtil::getFormDataSource(_args.record());

        if (!selectedRecordDS)
        {
            throw error("@SYS30498");
        }

        ttsbegin;

        for (selectedRecord = selectedRecordDS.getFirst(true) ? selectedRecordDS.getFirst(true) : selectedRecordDS.cursor();
            selectedRecord;
            selectedRecord = selectedRecordDS.getNext())
        {
            switch (selectedRecord.TableID)
            {
                case tableNum(WHSShipConsolidationError):
                    update_recordset shipConsolidationError
                        setting ErrorOverride = errorOverride
                        where shipConsolidationError.RecId         == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorOverride != errorOverride
                        &&   !shipConsolidationError.ErrorHardStop;

                    while select shipConsolidationError
                        where shipConsolidationError.RecId         == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorHardStop
                    {
                        warning(strFmt("@WAX:Error_ErrorCannotBeOverridden", shipConsolidationError.labelDescription()));
                    }
                    break;

                case tableNum(WHSShipConsolidationProposedShipment):
                    update_recordset shipConsolidationError
                        setting ErrorOverride = errorOverride
                        where shipConsolidationError.ProposedShipment == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorOverride    != errorOverride
                        &&   !shipConsolidationError.ErrorHardStop;

                    while select shipConsolidationError
                        where shipConsolidationError.ProposedShipment == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorHardStop
                    {
                        warning(strFmt("@WAX:Error_ErrorCannotBeOverridden", shipConsolidationError.labelDescription()));
                    }
                    break;

                case tableNum(WHSShipConsolidationProposedShipmentContent):
                    update_recordset shipConsolidationError
                        setting ErrorOverride = errorOverride
                        where shipConsolidationError.ProposedShipmentContent == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorOverride           != errorOverride
                        &&   !shipConsolidationError.ErrorHardStop;

                    while select shipConsolidationError
                        where shipConsolidationError.ProposedShipmentContent == selectedRecord.RecId
                        &&    shipConsolidationError.ErrorHardStop
                    {
                        warning(strFmt("@WAX:Error_ErrorCannotBeOverridden", shipConsolidationError.labelDescription()));
                    }
                    break;

                default:
                    throw error("@SYS30498");
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>