<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>tamVendRebatePurgeDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TAMVendRebatePurgeDialog</c> class serves to delete rebates.
/// </summary>
class tamVendRebatePurgeDialog extends RunBaseBatch implements BatchRetryable
{
    TAMVendRebatePurgeDialog    vendRebatePurgeDialog;
    date                        upToDate;
    DialogField                 dialogUpToDate;
    DialogRunbase               dialogRunbase;

    #define.CurrentVersion(4)
    #LOCALMACRO.CurrentList
    upToDate
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the rebate date range field to dialog box.
    /// </summary>
    /// <returns>
    /// The dialog box object.
    /// </returns>
    public Object dialog()
    {
        dialogRunbase = super();

        dialogUpToDate  = dialogRunbase.addFieldValue(
                            extendedTypeStr(ToDate),
                            upToDate,
                            "@PRO1118",
                            "@PRO1119");

        return dialogRunbase;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the rebate date value.
    /// </summary>
    /// <returns>
    /// true if the rebate date value is retrieved successfully;
    /// otherwise, false.
    /// </returns>
    public boolean getFromDialog()
    {
        upToDate  =  dialogUpToDate.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>module</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the application module value.
    /// </summary>
    /// <returns>
    /// The vendor application module value.
    /// </returns>
    public SysModule module()
    {
        return SysModule::Vend;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Substantiates the <c>TAMVendRebatePurgeDialog</c> object.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Stores the dialog object parameters.
    /// </summary>
    /// <returns>
    /// The list of stored parameters.
    /// </returns>
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches the rebate delete process.
    /// </summary>
    public void run()
    {
        if ((Box::yesNo(
                strFmt("@PRO1606", TAMVendRebateStatus::Approved, TAMVendRebateStatus::Processed, upToDate),
                DialogButton::No,
                TAMVendRebatePurgeDialog::description())) == DialogButton::Yes)
        {
            TAMVendRebateTable::purgeRebates(upToDate);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    /// <summary>
    /// Restores the last parameters saved.
    /// </summary>
    /// <param name="_packedClass">
    /// The list of stored parameters.
    /// </param>
    /// <returns>
    /// true if the restore succeded; otherwise, false.
    /// </returns>
    public boolean unpack(container _packedClass)
    {
        Integer     version     = conPeek(_packedClass,1);
        boolean     ret = true;

        switch (version)
        {
            case #CurrentVersion  :
                [version,#CurrentList] = _packedClass;
                break;
            default :
                ret = false;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns an instance of the <c>TAMVendRebatePurgeDialog</c> class.
    /// </summary>
    /// <returns>
    /// An instance of the <c>TAMVendRebatePurgeDialog</c> class.
    /// </returns>
    public static TAMVendRebatePurgeDialog construct()
    {
        return new TAMVendRebatePurgeDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns class description.
    /// </summary>
    /// <returns>class description.</returns>
    public static ClassDescription description()
    {
        return "@PRO1117";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        TAMVendRebatePurgeDialog tamVendRebatePurgeDialog = TAMVendRebatePurgeDialog::construct();

        try
        {
            if (tamVendRebatePurgeDialog.prompt())
            {
                tamVendRebatePurgeDialog.runOperation();

                if (    _args
                    &&  _args.caller()
                    &&  _args.caller() is FormRun
                    &&  formHasMethod(_args.caller(), identifierStr(refreshData)))
                {
                     _args.caller().refreshData();
                }
            }
        }
        catch (Exception::Error)
        {
            warning("@PRO1149");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>