<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSInventMov_QuarantineFrom</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSInventMov_QuarantineFrom</c> class is used as a data carrier when updating outbound quarantine transactions.
/// </summary>
[InventMovementFactoryAttribute(tableStr(WHSWorkQuarantine))]
public class WHSInventMov_QuarantineFrom extends WHSInventMov_Quarantine
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createOriginOwnerRelationship</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the relationship between the inventory transactions originator and the owner.
    /// </summary>
    public void createOriginOwnerRelationship()
    {
        InventTransOriginId originId = this.inventTransOriginId();

        if (originId && workQuarantine.RecId)
        {
            WHSInventTransOriginQuarantineFrom::writeOriginOwnerRelationship(workQuarantine.RecId, originId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteOriginOwnerRelationship</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Deletes the relationship between the inventory transactions originator and the owner that
    ///    corresponds.
    /// </summary>
    public void deleteOriginOwnerRelationship()
    {
        if (workQuarantine.RecId)
        {
            WHSInventTransOriginQuarantineFrom::deleteOwnerRelationship(workQuarantine.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventDimId</Name>
				<Source><![CDATA[
	/// <summary>
	/// Finds the id of the initial dimensions of the transaction.
	/// </summary>
	/// <returns>
	/// The id of the initial dimensions of the transaction.
	/// </returns>
    public InventDimId inventDimId()
    {
        return workQuarantine.InventDimIdFrom;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventTransOriginId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the ID of the inventory transaction originator that corresponds to the movement.
    /// </summary>
    /// <param name="_inventTransOriginId">
    ///    The new value of the ID of the inventory transaction originator.
    /// </param>
    /// <returns>
    ///    The ID of the inventory transaction originator that corresponds to the movement.
    /// </returns>
    public InventTransOriginId inventTransOriginId(InventTransOriginId _inventTransOriginId = inventTransOriginId)
    {
        if (_inventTransOriginId != inventTransOriginId)
        {
            inventTransOriginId = _inventTransOriginId;
        }
        else
        {
            if (!inventTransOriginId)
            {
                inventTransOriginId = WHSInventTransOriginQuarantineFrom::findInventTransOriginId(workQuarantine.RecId);
            }
        }

        return inventTransOriginId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newMovement_Orig</Name>
				<Source><![CDATA[
	/// <summary>
	/// Creates a new <c>WHSInventMov_QuarantineFrom</c> instance with original values of the current instance.
	/// </summary>
	/// <returns>
	/// New <c>WHSInventMov_QuarantineFrom</c> instance with current instance values.
	/// </returns>
    public InventMovement newMovement_Orig()
    {
        if (workQuarantine.RecId)
        {
            return new WHSInventMov_QuarantineFrom(workQuarantine.orig(), inventType);
        }
        else
        {
            return null;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postingBalanceSheet</Name>
				<Source><![CDATA[
	/// <summary>
	/// Finds balance sheet posting type.
	/// </summary>
	/// <returns>
	/// The balance sheet posting type.
	/// </returns>
    public LedgerPostingType postingBalanceSheet()
    {
        return LedgerPostingType::InventIssue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setInventDimId</Name>
				<Source><![CDATA[
	/// <summary>
	/// Sets initial dimensions of the work quarantine transaction.
	/// </summary>
	/// <param name="_dimId">
	/// Id of the initial dimensions.
	/// </param>
    public void setInventDimId(InventDimId _dimId)
    {
        workQuarantine.InventDimIdFrom = _dimId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferDirection</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the direction of the transfer movements.
    /// </summary>
    /// <returns>
    ///    The direction of the transfer movements.
    /// </returns>
    public InventDirection transferDirection()
    {
        return InventDirection::Issue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transferInventTransOriginId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves an ID of transfer counterpart inventory transaction originator that corresponds to the
    ///    movement.
    /// </summary>
    /// <returns>
    ///    The ID of a transfer counterpart inventory transaction originator that corresponds to the movement.
    /// </returns>
    public InventTransOriginId transferInventTransOriginId()
    {
        return InventTransOrigin::findByInventTransId(workQuarantine.InventTransIdTo).RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transId</Name>
				<Source><![CDATA[
	/// <summary>
	/// Finds the transaction id.
	/// </summary>
	/// <returns>
	/// The work quarantine transaction id.
	/// </returns>
    public InventTransId transId()
    {
        return workQuarantine.InventTransIdFrom;
    }

]]></Source>
			</Method>
			<Method>
				<Name>transSign</Name>
				<Source><![CDATA[
	/// <summary>
	/// Finds the transaction signature.
	/// </summary>
	/// <returns>
	/// The transaction signature.
	/// </returns>
    public Integer transSign()
    {
        return  -1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mustBePicked</Name>
				<Source><![CDATA[
    public boolean mustBePicked()
    {
        WHSWorkQuarantine whsWorkQuarantine = this.buffer() as WHSWorkQuarantine;

        // If the inventory status is changing for a WHSItem in quarantine, picking requirement should be removed.
        return super() && InventDim::find(whsWorkQuarantine.InventDimIdFrom).InventStatusId == InventDim::find(whsWorkQuarantine.InventDimIdTo).InventStatusId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>