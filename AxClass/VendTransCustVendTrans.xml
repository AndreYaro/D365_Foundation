<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendTransCustVendTrans</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>VendTransCustVendTrans</c> class contains <c>VendTrans</c> table specific functionality and mapped fields
/// related to the <c>CustVendTrans</c> table map.
/// </summary>
[ExportMetadata(classStr(CustVendTransInterface), tableStr(VendTrans)), ExportAttribute('Dynamics.AX.Application.CustVendTransInterface')]
public class VendTransCustVendTrans extends CustVendTransInterface
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmVendTrans</Name>
				<Source><![CDATA[
    protected CustVendTrans parmVendTrans()
    {
        return custVendTransMap;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkMultiTransByVoucherDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks whether multiple <c>VendTrans</c> records exist with the same voucher and date.
    /// </summary>
    /// <returns>
    ///  false if multiple transactions exist with the same voucher and date; otherwise, true.
    /// </returns>
    public boolean checkMultiTransByVoucherDate()
    {
        VendTrans vendTrans;

        select count(RecId) from vendTrans
            where vendTrans.Voucher == custVendTransMap.Voucher
                && vendTrans.TransDate == custVendTransMap.TransDate
                && vendTrans.TransType != LedgerTransType::Vend;

        return vendTrans.RecId <= 1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPaymentScheduleApplied</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if a payment schedule has been applied to the transaction.
    /// </summary>
    /// <returns>true if a payment schedule has been applied; otherwise, false.</returns>
    public boolean checkPaymentScheduleApplied()
    {
        VendTransOpen vendTransOpen;

        select count(RecId) from vendTransOpen
            where vendTransOpen.RefRecId == custVendTransMap.RecId;

        return vendTransOpen.RecId > 1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>exchRateCurToMst</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the exchange rate from the transaction currency to the monetary standard (MST) currency.
    /// </summary>
    /// <returns>
    ///    The exchange rate for the transaction.
    /// </returns>
    /// <remarks>
    ///    The method returns the <c>ExchRate</c> field if there is no triangulation. If there is
    ///    triangulation this returns the <c>ExchRateSecond</c> field divided by the <c>ExchRate</c> field.
    /// </remarks>
    public ExchRate exchRateCurToMst()
    {
        VendTrans vendTrans;
        return vendTrans.exchRateCurToMst();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getConsId_JP</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the consolidation ID.
    /// </summary>
    /// <returns>
    /// The consolidation ID.
    /// </returns>
    public CustVendConsId_JP getConsId_JP()
    {
        CustVendConsRefRecId_JP consRefRecId =
            VendInvoiceJour::findFromVendTransVoucher(custVendTransMap.Invoice, custVendTransMap.Voucher, custVendTransMap.TransDate, custVendTransMap.AccountNum).VendConsInvoice_JP;

        if (!consRefRecId && TaxConsInvoice_JP::isTaxDifferencePostingOnConsInvoiceEnabled())
        {
            VendTrans vendTrans = VendTrans::findVoucherDateType(custVendTransMap.Voucher, custVendTransMap.TransDate, custVendTransMap.TransType);
            VendTrans_W vendTrans_W = vendTrans.vendTrans_W();

            if (vendTrans_W.VendConsInvoice_JP)
            {
                VendConsInvoice_JP vendConsInvoice_JP = VendConsInvoice_JP::find(vendTrans_W.VendConsInvoice_JP);

                if (vendConsInvoice_JP.PostedVendTrans == vendTrans.RecId)
                {
                    return vendConsInvoice_JP.ConsId;
                }
            }
        }

        return VendConsInvoice_JP::find(consRefRecId).ConsId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendTrans_W</Name>
				<Source><![CDATA[
    /// <summary>
    /// Unpacks the child extension table from the parent table.
    /// </summary>
    /// <returns>
    /// The child map containing the country specific fields.
    /// </returns>
    public CustVendTrans_W getCustVendTrans_W()
    {
        VendTrans vendTrans = custVendTransMap;
        return vendTrans.vendTrans_W();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the retrieved table buffer from the query for the concrete table represented by the map.
    /// </summary>
    /// <param name="_queryRun">
    ///    The query that contains the concrete table.
    /// </param>
    /// <returns>
    ///    The retrieved table buffer from the query for the concrete table represented by the map.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    ///    The map has not been associated with one of the concrete tables represented by the map.
    /// </exception>
    public CustVendTrans getFromQuery(QueryRun _queryRun)
    {
        return _queryRun.get(tableNum(VendTrans));
    }

]]></Source>
			</Method>
			<Method>
				<Name>hasChangedInQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines if the concrete table represented by the map has changed in the query.
    /// </summary>
    /// <param name="_queryRun">
    ///    The query that contains the concrete table.
    /// </param>
    /// <returns>
    ///    true if the concrete table represented by the map has changed in the query; otherwise, false.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    ///    The map has not been associated with one of the concrete tables represented by the map.
    /// </exception>
    public boolean hasChangedInQuery(QueryRun _queryRun)
    {
        return _queryRun.changed(tableNum(VendTrans));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initConsInvoice_JP</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the value for field <c>ConsInvoice_JP</c> with the record Id of last confirmed consolidated invoice.
    /// </summary>
    /// <remarks>
    /// Field is set only when TransType is LedgerTransType::Payment or LedgerTransType::CashDiscount.
    /// </remarks>
    public void initConsInvoice_JP()
    {
        VendTrans vendTrans = custVendTransMap.data();

        if (vendTrans.isPayment_JP() && vendTrans.isTransForConsInvoice_JP())
        {
            CustVendConsRefRecId_JP refRecId = VendConsInvoice_JP::findLastConfirmedConsInvoice(custVendTransMap.AccountNum);
            CustVendTrans_W custVendTrans_W = custVendTransMap.CustVendTrans::getCustVendTrans_W();
            custVendTrans_W.ConsInvoice_JP = refRecId;
            custVendTransMap.CustVendTrans::packCustVendTrans_W(custVendTrans_W);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isMarkedForSettlement</Name>
				<Source><![CDATA[
    /// <summary>
    /// Detemines if the current record is marked.
    /// </summary>
    /// <returns>
    /// True if the current records is marked in the <c>SpecTrans</c> table.
    /// </returns>
    public boolean isMarkedForSettlement()
    {
        SpecTrans specTrans;
        VendTransOpen vendTransOpen;

        select firstonly crossCompany RecId from vendTransOpen
            exists join specTrans
                where specTrans.RefCompany == vendTransOpen.DataAreaId
                    && specTrans.RefTableId == vendTransOpen.TableId
                    && specTrans.RefRecId == vendTransOpen.RecId
                    && vendTransOpen.RefRecId == custVendTransMap.RecId;

        return vendTransOpen.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>packCustVendTrans_W</Name>
				<Source><![CDATA[
    /// <summary>
    /// Packs any change made to the child table to the parent table.
    /// </summary>
    /// <param name="_custVendTrans_W">
    /// The map containing the country specific fields.
    /// </param>
    public void packCustVendTrans_W(CustVendTrans_W _custVendTrans_W)
    {
        VendTrans vendTrans = custVendTransMap;
        vendTrans.packVendTrans_W(_custVendTrans_W);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustVendConsInvoice_JP</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>CustVendConsInvoice_JP</c> record from the <c>CustVendTrans_W</c> record.
    /// </summary>
    /// <param name = "_custVendTrans_W">The <c>CustVendTrans_W</c> record.</param>
    /// <param name = "_forUpdate">Select the reocrd for update.</param>
    /// <returns>The <c>CustVendConsInvoice_JP</c> record.</returns>
    protected CustVendConsInvoice_JP getCustVendConsInvoice_JP(CustVendTrans_W _custVendTrans_W, boolean _forUpdate)
    {
        return VendConsInvoice_JP::find(_custVendTrans_W.ConsInvoice_JP, _forUpdate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBankChequeRecipientType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>BankChequeRecipientType</c> enum value used to query the <c>BankChequePaymTrans</c>
    /// table.
    /// </summary>
    /// <returns>The <c>BankChequeRecipientType</c> enum value.</returns>
    protected BankChequeRecipientType getBankChequeRecipientType()
    {
        return BankChequeRecipientType::Vend;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>