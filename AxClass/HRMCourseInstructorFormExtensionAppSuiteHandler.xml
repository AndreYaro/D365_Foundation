<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HRMCourseInstructorFormExtensionAppSuiteHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles the Application Suite form extension events for the <c>HRMCourseInstructor</c> form.
/// </summary>
class HRMCourseInstructorFormExtensionAppSuiteHandler
{
    boolean initialized;
    FormRun element;

    // Extension controls
    FormReferenceGroupControl courseIntructorPartyControl;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of this form extension event handler class and registers it with the base form.
    /// </summary>
    /// <param name = "_formRunInstance">An instance of the form being extended.</param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void new(FormRun _formRunInstance)
    {
        initialized = false;
        element = _formRunInstance;

        // Register this extension with the main form
        element.registerExtensionInstance(classStr(HRMCourseInstructorFormExtensionAppSuiteHandler), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    private void init()
    {
        if (initialized)
        {
            return;
        }

        initialized = true;

        // Cache of the extension controls
        courseIntructorPartyControl = element.design().controlName(formControlStr(HRMCourseInstructor, HRMCourseInstructor_Party1)) as FormReferenceGroupControl;

        // Register overrides
        courseIntructorPartyControl.registerOverrideMethod(methodStr(FormReferenceControl, resolveReference) , methodStr(HRMCourseInstructorFormExtensionAppSuiteHandler, onCourseIntructorPartyResolveReference), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>HRMCourseInstructor_OnInitializing</Name>
				<Source><![CDATA[
    /// <summary>
    /// An OnInitializing event handler for the <c>HRMCourseIntructor</c> form
    /// </summary>
    /// <param name="_sender">The <c>HRMCourseIntructor</c> form.</param>
    /// <param name="_e">The args for the event.</param>
    [FormEventHandler(formStr(HRMCourseInstructor), FormEventType::Initializing)]
    public static void HRMCourseInstructor_OnInitializing(xFormRun _sender, FormEventArgs _e)
    {
        HRMCourseInstructorFormExtensionAppSuiteHandler extensionInstance = new HRMCourseInstructorFormExtensionAppSuiteHandler(_sender);
    }

]]></Source>
			</Method>
			<Method>
				<Name>HRMCourseInstructor_OnInitialized</Name>
				<Source><![CDATA[
    /// <summary>
    /// Form OnInitialized event handler for <c>HRMCourseIntructor</c> form.
    /// </summary>
    /// <param name="_sender">The <c>HRMCourseIntructor</c> form.</param>
    /// <param name="_e">Args for the event.</param>
    [FormEventHandler(formStr(HRMCourseInstructor), FormEventType::Initialized)]
    public static void HRMCourseInstructor_OnInitialized(xFormRun _sender, FormEventArgs _e)
    {
        // Initialize the instance of this form extension handler now that the controls exist
        FormRun hRMCourseInstructorForm = _sender as FormRun;
        HRMCourseInstructorFormExtensionAppSuiteHandler extensionInstance = hRMCourseInstructorForm.getExtensionInstance(classStr(HRMCourseInstructorFormExtensionAppSuiteHandler));
        extensionInstance.init();
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCourseIntructorPartyResolveReference</Name>
				<Source><![CDATA[
    /// <summary>
    /// Custom override method for handling the HRMCourseInstructor_Party1 control's resolveReference.
    /// </summary>
    /// <param name = "_sender">The control initiating the resolve reference.</param>
    private Common onCourseIntructorPartyResolveReference(FormReferenceGroupControl _sender)
    {
        DirPartyTable dirPartyTable;

        // super
        dirPartyTable = _sender.resolveReference();

        if (dirPartyTable != null && !HRMCourseInstructor_Extension::validateParty(dirPartyTable.RecId))
        {
            dirPartyTable = null;
        }

        return dirPartyTable;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>