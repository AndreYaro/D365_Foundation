<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjPayrollProj2ColDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The ProjListProjPayrollProj2ColDP class is the report data provider class for the associated
///    <c>ProjListProjPayRollProj2Column</c> reports.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjPayrollContract))
]

class ProjListProjPayrollProj2ColDP extends SRSReportDataProviderBase
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    TransDate               projFromDate;
    TransDate               projBreakDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerBreakDate;
    TransDate               ledgerToDate;
    projActualVsBudget      actualVsBudget;
    ProjForecastModelId     forecastModelId;
    ProjListStateProjType   stateProjType;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;

    ProjListProjTrans_PayRoll       listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjPayrollTmp          projListProjPayrollTmp;

    ProjTable               projTable;

    QueryRun                qrun;

    ProjId                  projIdFound, projId;
    boolean                 firstRun;
    NumberSequenceFormat    oldLevelFormat;

    ProjListProjHelpClass   actualProjHelpClass;
    ProjListProjHelpClass   budgetProjHelpClass;

    AmountMST               actualpayRollCost;
    AmountMST               budgetPayRollCost;
    ProjQty                 actualQtyHour;
    ProjQty                 budgetQtyHour;

    int                     reportType;
    int                     hourQtyDecimals, valueAddedDecimals, grossMarginDecimals, amountDecimals;
    int                     showPayrollCost, showQtyHour;
    str                     headerLabel, devianceLabel, hourQtyLabel, payrollCostLabel, periodLabel;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates the payroll cost amount and hour quantity.
    /// </summary>
    void calculate()
    {
        actualProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        actualProjHelpClass.setParmField();

        budgetProjHelpClass.parmProjStatement().parmProjId(projIdFound);
        budgetProjHelpClass.setParmField();

        if (amountQty == ProjAmountQty::Amount || amountQty == ProjAmountQty::AmountQty)
        {
            actualpayRollCost     += actualProjHelpClass.parmProjStatement().d100_PayrollAllocation();
            budgetPayRollCost     += budgetProjHelpClass.parmProjStatement().d100_PayrollAllocation();
        }

        if (amountQty == ProjAmountQty::Qty || amountQty == ProjAmountQty::AmountQty)
        {
            actualQtyHour         += actualProjHelpClass.parmProjStatement().d100_PayrollAllocation_Qty();
            budgetQtyHour         += budgetProjHelpClass.parmProjStatement().d100_PayrollAllocation_Qty();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the line should print.
    /// </summary>
    /// <returns>
    ///    true if the line should print; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!skipZero || this.lineValue()) && listProjTransLayout.canPrintId(actualProjHelpClass.id()))
        {
            return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should be printed and calls the insert to temporary table method.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record that contains information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        if (stateProjType == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= level) ||
                (projTableLocal.levelFormat(level)   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    actualProjHelpClass.setName();
                    this.insertProjListProjPayrollTmp();
                    this.setFieldZero();
                }
            }
        }
        else if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjPayrollTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjPayrollTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the data from the <c>ProjListProjPayrollTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>ProjListProjPayrollTmp</c> table.
    /// </returns>
    [SRSReportDataSetAttribute('ProjListProjPayrollTmp')]
    public ProjListProjPayrollTmp getProjListProjPayrollTmp()
    {
        select * from projListProjPayrollTmp;

        return projListProjPayrollTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>ProjListProjTrans</c> class.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans = ProjListProjTrans::construct(ProjListStateType::Payroll, ProjListStateId::Project);
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjToDate(projToDate);
        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmActualVsBudget(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);

        listProjTrans.parmShowDeviance(true);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjPayrollTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjPayrollTmp()
    {
        projListProjPayrollTmp.clear();

        projListProjPayrollTmp.Id            = actualProjHelpClass.id();
        projListProjPayrollTmp.Name          = actualProjHelpClass.name();

        projListProjPayrollTmp.QtyHour       = actualQtyHour;
        projListProjPayrollTmp.QtyHour1      = budgetQtyHour;
        projListProjPayrollTmp.QtyHour2      = ProjListProjHelpClass::calcActualVsBudget(projListProjPayrollTmp.QtyHour,
                                                projListProjPayrollTmp.QtyHour1, actualVsBudget, false);

        projListProjPayrollTmp.PayrollCost   = listProjTransLayout.showAmountReport(actualpayRollCost, showAmount);
        projListProjPayrollTmp.PayrollCost1  = listProjTransLayout.showAmountReport(budgetPayRollCost, showAmount);
        projListProjPayrollTmp.PayrollCost2  = ProjListProjHelpClass::calcActualVsBudget(projListProjPayrollTmp.PayrollCost,
                                                projListProjPayrollTmp.PayrollCost1, actualVsBudget, true);

        // set labels
        projListProjPayrollTmp.HeaderLabel   = headerLabel;
        projListProjPayrollTmp.QtyHourLabel  = hourQtyLabel;
        projListProjPayrollTmp.PayrollCostLabel = payrollCostLabel;
        projListProjPayrollTmp.DevianceLabel = devianceLabel;
        projListProjPayrollTmp.IdLabel       = strfmt('%1', stateProjType);
        projListProjPayrollTmp.PeriodLabel   = periodLabel;

        // set decimal values
        projListProjPayrollTmp.DecimalPayrollCost = amountDecimals;
        projListProjPayrollTmp.DecimalHourQty = hourQtyDecimals;

        // set field visibility
        projListProjPayrollTmp.ShowQtyHour = showQtyHour;
        projListProjPayrollTmp.ShowPayrollCost = showPayrollCost;

        projListProjPayrollTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the payroll cost amount and hour quantity are zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise, false.
    /// </returns>
    boolean lineValue()
    {
        return ((actualpayRollCost  != 0) ||
                (actualQtyHour      != 0) ||
                (budgetPayRollCost  != 0) ||
                (budgetQtyHour      != 0));
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = qrun.query().dataSourceTable(tablenum(ProjTable));
        qbdsProjTable.addSortField(fieldnum(ProjTable,ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldnum(ProjTable,Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = qrun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
        }
        else
        {
            if (timeMaterial == NoYes::Yes)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldnum(ProjTable, Type)).value(queryValue(ProjType::Internal));
            }

            if (!(timeMaterial || fixedPrice || investment || internalCost))
            {
                QueryBuildRange rangeEmptyString = qbdsProjTable.addRange(fieldnum(ProjTable, Type));
                rangeEmptyString.value(queryValue(SysQuery::valueEmptyString()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = qrun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,stateProjType,level);

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = qrun.get(tablenum(ProjFundingSource));
        }
        actualProjHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);
        budgetProjHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);

        if (! listProjTransLayout.canPrintId(actualProjHelpClass.idNew()))
        {
            actualProjHelpClass.setOld();

            return;
        }

        if (! firstRun && actualProjHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        actualProjHelpClass.setOld();
        budgetProjHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(level);
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the report business logic.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SSRS at
    ///    runtime. The method should compute data and populate the data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjPayrollContract contract = this.parmDataContract() as ProjListProjPayrollContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        projFromDate = contract.parmProjFromDate();
        projBreakDate = contract.parmProjBreakDate();
        projToDate = contract.parmProjToDate();
        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerBreakDate = contract.parmLedgerBreakDate();
        ledgerToDate = contract.parmLedgerToDate();

        actualVsBudget = contract.parmActualVsBudget2Col();
        forecastModelId = contract.parmForecastModelId();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();

        reportType = contract.parmReportType();

        firstRun = true;
        this.initProjListTrans();
        [headerLabel, hourQtyLabel, payrollCostLabel, devianceLabel, periodLabel] = listProjTrans.setDisplayLabel2Column(showAmount);

        [showQtyHour, showPayrollCost] = listProjTrans.setDisplayFlags(amountQty);

        [hourQtyDecimals, amountDecimals,
        grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        actualProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newProjListPayroll(
                                            listProjTrans,
                                            ProjActualBudget::Actual,
                                            amountQty),
                                    stateProjType);

        budgetProjHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                    projStatement::newProjListPayroll(
                                            listProjTrans,
                                            ProjActualBudget::Budget,
                                            amountQty,
                                            forecastModelId),
                                    stateProjType);

        qrun = new QueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (qrun.next())
        {
            this.printProject();
            firstRun = false;
        }

        if (this.checkPrintLine())
        {
            actualProjHelpClass.setName();
            this.insertProjListProjPayrollTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the fields to zero.
    /// </summary>
    void setFieldZero()
    {
        actualpayRollCost = 0;
        actualQtyHour     = 0;
        budgetPayRollCost = 0;
        budgetQtyHour     = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        qrun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>