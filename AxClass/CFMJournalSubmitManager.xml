<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMJournalSubmitManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>CFMJournalSubmitManager</c> is used to submit the payment schedule journal for approval to workflow.
/// </summary>
public class CFMJournalSubmitManager
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (!_args || _args.dataset() != tableNum(CFMJournalTable) || !_args.record())
        {
            throw error(strFmt("@SYS19306", funcName()));
        }

        CFMJournalTable journalTable = _args.record();
        CFMJournalName journalName = journalTable.journalName();

        if (journalName.RecId != 0 && journalName.checkWorkflowEnabled())
        {
            if (!CFMAnalyticDataEditUncommitted::isJournalExistUncommittedValues(journalTable.RecId))
            {
                WorkflowSubmitDialog workflowSubmitDialog = WorkflowSubmitDialog::construct(WorkflowVersionTable::findSequenceNumber(journalName.WorkflowConfiguration));
                workflowSubmitDialog.run();

                if (workflowSubmitDialog.parmIsClosedOK() && journalTable.WorkflowStatus == CFMJournalWorkflowStatus::NotSubmitted)
                {
                    WorkflowComment initialNote = workflowSubmitDialog.parmWorkflowComment();

                    try
                    {
                        ttsbegin;
                        Workflow::activateFromWorkflowSequenceNumber(journalName.WorkflowConfiguration, journalTable.RecId, initialNote, NoYes::No);
                        ttscommit;
                        info("@CashFlowManagement:SubmittedToWorkflow");
                        
                    }
                    catch(exception::Error)
                    {
                        error("@SYS303438");
                    }
                }
            }
            else
            {
                error("@CashFlowManagement:JourContainsUncommittedChanges");
            }
        }
        else
        {
            try
            {
                CFMJournalTable::setWorkflowStatus(journalTable.RecId, CFMJournalWorkflowStatus::None);

                info("@SYS111654");
            }
            catch(Exception::Error)
            {
                throw error(strFmt("@SYS111655", journalTable.JournalName, journalTable.JournalId));
            }
        }

        _args.caller().updateWorkflowControls();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>