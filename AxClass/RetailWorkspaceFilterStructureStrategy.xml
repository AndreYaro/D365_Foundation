<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailWorkspaceFilterStructureStrategy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>RetailWorkspaceFilterStructureStrategy</c> class applies a <c>SysIFilterStructureItem</c> class
/// to a <c>SysIQueryable</c> class. Org hierarchy filter is applied to the queries in the channel deployment and channel operations workspace.
/// </summary>
[
// Retail channel deployment workspace queries.
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailStoresInProgress)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailStoresInProgressCount)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailStores)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailRegisters)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailDevices)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailDevicesPendingActivation)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailDevicesFailingValidation)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailMCRChannelListPage)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailOnlineChannelTableListPage)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailB2BOperationSyncFailuresCountTileQuery)),

// Retail Prices and discount workspace queries.
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailTradeAgreementExpiringInNext7Days)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailCategoryPriceRule)),

// Retail store financials workspace quries.
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailCalculatedStatementsTileQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailTransactionsFailingValidationTileQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailAsyncCustomerOrderSyncFailuresCountTileQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailOnlineOrderSyncFailuresCountTileQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailShiftsFailingValidationTileQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailShifts)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailPostedStatements)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailSalesLineWorkflowItemListQuery)),
SysFilterExtension(SysQueryableTypeEnum::Query, SysFilterValueTypeEnum::Integer, queryStr(RetailSalesQuotationLineWorkflowItemListQuery))
]
public class RetailWorkspaceFilterStructureStrategy extends SysFilterStructureStrategyQuery implements SysIFilterStructureStrategy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>resolve</Name>
				<Source><![CDATA[
    protected boolean resolve(SysIFilterValueType _filterValueType)
    {
        boolean result = false;
        if (_filterValueType.parmType() == SysFilterValueTypeEnum::Integer)
        {
            // if EcoResCategoryHierarchy filter has Equal operator set then we enable the EcoResProductCategory datasource; otherwise we disable it and show all records without categories
            if (_filterValueType.parmTypeIdentifier() == extendedTypeNum(EcoResCategoryHierarchyId))
            {
                QueryBuildDataSource ecoResCategoryQdbs = this.getQueryBuildDataSource(tableNum(EcoResCategory));
                if (filterStructureItem.parmOperator() == SysFilterOperator::Equal)
                {
                    ecoResCategoryQdbs.enabled(true);
                    this.addLinkFromTableToFilter(ecoResCategoryQdbs, fieldNum(EcoResCategory, CategoryHierarchy), filterStructureItem.parmOperator());
                }
                else
                {
                    ecoResCategoryQdbs.enabled(false);
                }
                result = true;
            }
            else if (_filterValueType.parmTypeIdentifier() == extendedTypeNum(EcoResCategoryNestedSetLeft))
            {
                QueryBuildDataSource ecoResCategoryQdbs = this.getQueryBuildDataSource(tableNum(EcoResCategory));
                this.addLinkFromTableToFilter(ecoResCategoryQdbs, fieldNum(EcoResCategory, NestedSetLeft), filterStructureItem.parmOperator());
                result = true;
            }
            else if (_filterValueType.parmTypeIdentifier() == extendedTypeNum(EcoResCategoryNestedSetRight))
            {
                QueryBuildDataSource ecoResCategoryQdbs = this.getQueryBuildDataSource(tableNum(EcoResCategory));
                this.addLinkFromTableToFilter(ecoResCategoryQdbs, fieldNum(EcoResCategory, NestedSetRight), filterStructureItem.parmOperator());
                result = true;
            }
            else if (_filterValueType.parmTypeIdentifier() == extendedTypeNum(RecId))
            {
                QueryBuildDataSource explodedOrgSecurityGraphQdbs = this.getQueryBuildDataSource(tableNum(OMExplodedOrganizationSecurityGraph));
                QueryBuildDataSource omHierarchyRelationshipQdbs = this.getQueryBuildDataSource(tableNum(OMHierarchyRelationship));

                // if a filter is specified and is not blank then apply the filter. In the workspace filter implementation logic, a SysFilterOperator::Equal operator is used  if the filter value is not blank.
                if (filterStructureItem.parmOperator() == SysFilterOperator::Equal)
                {
                    explodedOrgSecurityGraphQdbs.enabled(true);
                    this.addLinkFromTableToFilter(omHierarchyRelationshipQdbs, fieldNum(OMHierarchyRelationship, RecId));
                }
                else //if filter is is blank, then disable the datasource that is used to filter records by org hierachy filter value.
                {
                    explodedOrgSecurityGraphQdbs.enabled(false);
                }
                result = true;
            }
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQueryBuildDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>QueryBuildDataSource</c> object from the query.
    /// </summary>
    /// <param name = "_tableId">The table id of the datasource.</param>
    /// <returns>The <c>QueryBuildDataSource</c> object if found; otherwise throw an error.</returns>
    public QueryBuildDataSource getQueryBuildDataSource(TableId _tableId)
    {
        Query _query = this.query;

        QueryBuildDataSource qdbs = _query.dataSourceTable(_tableId);
        if (!qdbs)
        {
            error(strFmt("@SYS89000", tableId2Name(_tableId), _query.name()));
        }
        return qdbs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addLinkFromTableToFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a link to the <c>SysFilterTable</c> table.
    /// </summary>
    /// <param name="_relatedTableDataSource">
    /// The <c>QueryBuildDataSource</c> class to which the link should be added.
    /// </param>
    /// <param name="_fieldId">
    /// The <c>FieldId</c> class for which the link should be added.
    /// </param>
    /// <param name="_filterOperator">
    /// The <c>SysFilterOperator</c> used in the range.
    /// </param>
    public void addLinkFromTableToFilter(
        QueryBuildDataSource _relatedTableDataSource,
        FieldId _fieldId,
        SysFilterOperator _filterOperator = SysFilterOperator::Equal)
    {
        QueryBuildRange qbr = SysQueryBase::findOrCreateRange(_relatedTableDataSource, _fieldId);
        qbr.status(RangeStatus::Hidden);
        // ((targetTable.TargetField <operator> sysFilterTable.ValueField) OR (sysFilterTable.ValueResolutionMethod != SysFilterValueResolutionMethod::None))
        qbr.value(strfmt('(((%1.%2 %3 %4.%5) || (%4.%6 != %7)))',
            _relatedTableDataSource.name(),
            fieldId2Name(_relatedTableDataSource.table(), _fieldId),
            SysFilterOperatorHelper::operator2SqlStr(_filterOperator),
            filterDatasource.name(),
            fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValue(filterStructureItem.parmValueType().parmType()), filterStructureItem.parmId()),
            fieldId2Name(filterMetadata.parmTypeId(), filterMetadata.parmValueResolutionMethod(), filterStructureItem.parmId()),
            int2Str(SysFilterValueResolutionMethod::None)));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>