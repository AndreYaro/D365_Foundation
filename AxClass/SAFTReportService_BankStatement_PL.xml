<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SAFTReportService_BankStatement_PL</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The service class that runs SAF-T Bank Statement (PL) report.
/// </summary>
public class SAFTReportService_BankStatement_PL extends SAFTReportService_Base_W
{
    private SAFTReportContract_BankStatement_PL dataContract;
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Starts file generation in GER.
    /// </summary>
    /// <param name = "_dataContract">A data contract containing user inputs.</param>
    public void run(SAFTReportContract_BankStatement_PL _dataContract)
    {
        dataContract = _dataContract;
        this.runReport(dataContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addQueriesToContext</Name>
				<Source><![CDATA[
    protected void addQueriesToContext(ERModelDefinitionDatabaseContext _databaseContext)
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(BankAccountTable));
        findOrCreateRange_W(qbds, fieldNum(BankAccountTable, AccountID), SysQuery::value(dataContract.parmbankAccountID()));

        Query userModifiedQuery = dataContract.parmQuery();
        Query bankAccountTransDataSource = this.createQueryWithCopiedDataSource(userModifiedQuery, tableNum(BankAccountTrans));

        super(_databaseContext);
        _databaseContext.addQuery(tableNum(BankAccountTable), query);
        _databaseContext.addQuery(tableNum(BankAccountTrans), bankAccountTransDataSource);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERFormatMappingRecId</Name>
				<Source><![CDATA[
    protected ERFormatMappingId getERFormatMappingRecId()
    {
        TaxParameters taxParameters;
        select firstonly SAFTBankStatementERFormatMapping_PL from taxParameters;
        return taxParameters.SAFTBankStatementERFormatMapping_PL;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addProcessedObjectParameters</Name>
				<Source><![CDATA[
    protected void addProcessedObjectParameters(ERIFormatMappingRun _erRun)
    {
        _erRun.withCreatingObjectParameter(this.getERModelName(), this.getERBankTransParametersName(), this.getBankTransParameters());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBankTransParameters</Name>
				<Source><![CDATA[
    private SAFTBankTransParameters_W getBankTransParameters()
    {
        BankAccountTrans bankAccountTrans;
        select sum(AmountCur) from bankAccountTrans
            where bankAccountTrans.AccountId == dataContract.parmbankAccountID()
                && bankAccountTrans.TransDate < dataContract.parmDateFrom();

        SAFTBankTransParameters_W params = new SAFTBankTransParameters_W();
        params.parmOpeningBalanceWithoutCorrection(bankAccountTrans.AmountCur);
        return params;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getERBankTransParametersName</Name>
				<Source><![CDATA[
    private str getERBankTransParametersName()
    {
        return SAFTConstants_W::ERBankTransParametersDatasourceName;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>