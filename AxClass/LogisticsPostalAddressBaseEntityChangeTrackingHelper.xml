<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LogisticsPostalAddressBaseEntityChangeTrackingHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>LogisticsPostalAddressBaseEntityChangeTrackingHelper</c> class contains helper methods for enabling change tracking
/// for data entities using LogisticsPostalAddressBaseEntity
/// </summary>
public static class LogisticsPostalAddressBaseEntityChangeTrackingHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addPostalAddressBasedOnAddressRecIdLink</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds change tracking tables to parent queries that link to <c>LogisticsPostalAddress</c> based on RecId
    /// </summary>
    /// <param name = "_parentDS">
    /// Parent datasource to which the tables should be added.
    /// </param>
    /// <param name = "_parentAddressFieldNum">
    /// FieldId of the field to which RecId of LogisticsPostalAddress should be linked.
    /// </param>
    public static void addPostalAddressBasedOnAddressRecIdLink(QueryBuildDataSource _parentDS, FieldId _parentAddressFieldNum)
    {
        var logisticsPostalAddressDS = _parentDS.addDataSource(tableNum(LogisticsPostalAddress));
        logisticsPostalAddressDS.joinMode(JoinMode::OuterJoin);
        logisticsPostalAddressDS.addLink(_parentAddressFieldNum, fieldNum(LogisticsPostalAddress, RecId));

        var logisticsLocationDS = logisticsPostalAddressDS.addDataSource(tableNum(LogisticsLocation));
        logisticsLocationDS.addLink(fieldNum(LogisticsPostalAddress, Location), fieldNum(LogisticsLocation, RecId));

        var dirDunsNumberDS = logisticsLocationDS.addDataSource(tableNum(DirDunsNumber));
        dirDunsNumberDS.joinMode(JoinMode::OuterJoin);
        dirDunsNumberDS.addLink(fieldNum(LogisticsLocation, DunsNumberRecId), fieldNum(DirDunsNumber, RecId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addPostalAddressBasedOnPrimaryAddressLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds change tracking tables to parent queries that link to <c>LogisticsPostalAddress</c> based on PrimaryAddressLocation.
    /// </summary>
    /// <param name = "_parentDS">
    /// Parent datasource to which the tables should be added.
    /// </param>
    /// <param name = "_parentAddressFieldNum">
    /// FieldId of the field to which RecId of LogisticsPostalAddress should be linked.
    /// </param>
    public static QueryBuildDataSource addPostalAddressBasedOnPrimaryAddressLocation(QueryBuildDataSource _parentDS, FieldId _parentAddressFieldNum)
    {
        QueryBuildDataSource  qbdLocation = _parentDS.addDataSource(tableNum(LogisticsLocation));
        qbdLocation.addLink(_parentAddressFieldNum, fieldNum(LogisticsLocation, RecId));

        QueryBuildDataSource  qbdPostal = qbdLocation.addDataSource(tableNum(LogisticsPostalAddress));
        qbdPostal.addLink(fieldNum(LogisticsLocation, RecId), fieldNum(LogisticsPostalAddress, Location));

        QueryBuildDataSource  qbdDunsNum = qbdLocation.addDataSource(tableNum(DirDunsNumber));
        qbdDunsNum.addLink(fieldNum(LogisticsLocation, DunsNumberRecId), fieldNum(DirDunsNumber, RecId));

        QueryBuildDataSource  qbdLocationExt = qbdLocation.addDataSource(tableNum(LogisticsLocationExt));
        qbdLocationExt.addLink(fieldNum(LogisticsLocation, RecId), fieldNum(LogisticsLocationExt, Location));

        return qbdLocation;

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>