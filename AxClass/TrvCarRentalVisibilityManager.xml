<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvCarRentalVisibilityManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class controls visibility for car rental expense type.
/// </summary>
class TrvCarRentalVisibilityManager extends TrvCommonExpenseTypeVisibilityManager
{
    Map                         transactionCarRental;
    const str                   transactionCarRentalGroupName = "CarRental";
    str                         dataSourceNameCarRental;
    TrvEnhancedCarRentalData    trvEnhancedCarRentalData;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmdataSourceNameCarRental</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets and sets the name of the datasource for car rental enhanced data to set field vibilities on for the class.
    /// </summary>
    /// <param name = "_dataSourceNameCarRental">
    /// The name of the car rental enhanced fields dataSource to set field visibilities on the form.
    /// </param>
    /// <returns>
    /// The name of the car rental enhanced fields dataSource to set field visibilities on the form.
    /// </returns>
    public str parmdataSourceNameCarRental(str _dataSourceNameCarRental = dataSourceNameCarRental)
    {
        dataSourceNameCarRental = _dataSourceNameCarRental;
        return dataSourceNameCarRental;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionCarRental</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for car rental field group.
    /// </summary>
    /// <returns>
    /// A map of car rental fields.
    /// </returns>
    Map getTransactionCarRental()
    {
        if (!transactionCarRental)
        {
            transactionCarRental = this.getFieldGroup(trvEnhancedCarRentalData, transactionCarRentalGroupName);
        }

        return transactionCarRental;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructor for an new instance of TrvCarRentalVisibilityManager.
    /// </summary>
    /// <param name = "_trvExpTrans">
    /// The current record instance of the <c>TrvExpensesEntity</c>.
    /// </param>
    /// <param name = "_trvExpensesForm">
    /// An instance of the calling form object.
    /// </param>
    /// <param name = "_dataSourceName">
    /// Name of the datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameCarRental">
    /// Name of the car rental enhanced datasource on the form for which we set the visibility.
    /// </param>
    /// <param name = "_dataSourceNameHeader">
    /// Name of the expense header datasource on the form for which we set the visibility.
    /// </param>
    void new(Common _trvExpTrans, FormRun _trvExpensesForm, str _dataSourceName, str _dataSourceNameCarRental, str _dataSourceNameHeader = "")
    {
        super(_trvExpTrans, _trvExpensesForm, _dataSourceName, _dataSourceNameHeader);
        this.parmdataSourceNameCarRental(_dataSourceNameCarRental);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldVisibilities</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets visibility for car rental fields.
    /// </summary>
    /// <param name = "_isVisible">
    /// Determines if we show or hide car rental fields.
    /// </param>
    void setFieldVisibilities(boolean _isVisible = false)
    {
        FormDataSource formDataSource = trvExpenseForm.dataSource(dataSourceName);
        FormDataSource formDataSourceCarRental = trvExpenseForm.dataSource(datasourceNameCarRental);

        if (!this.parmExpenseVisibilityManager())
        {
            this.setVisibilityForFieldGroup(this.getTransactionCarRental(), datasourceNameCarRental, _isVisible);
            if (_isVisible)
            {
                this.setVisibilityForAdminCustomFields(trvAdminCustomFieldsContainer, formDataSourceCarRental, trvExpTrans.isEditable());
            }
        }
        else
        {
            this.parmExpenseVisibilityManager().setVisibilityForCarRentalRelatedFields(formDataSource.table(), formDataSource, _isVisible);
            this.parmExpenseVisibilityManager().setVisibilityForCarRentalRelatedFields(formDataSourceCarRental.table(), formDataSourceCarRental, _isVisible);
        }

        super(_isVisible);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>