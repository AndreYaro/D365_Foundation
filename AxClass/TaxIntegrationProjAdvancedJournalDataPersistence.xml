<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxIntegrationProjAdvancedJournalDataPersistence</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TaxIntegrationProjAdvancedJournalDataPersistence</c> class persists data to <c>ProjAdvancedJournalLine</c>, etc.
/// </summary>
[TaxIntegrationDataPersistence(tableStr(ProjAdvancedJournal))]
public class TaxIntegrationProjAdvancedJournalDataPersistence extends TaxIntegrationAbstractDataPersistenceTemplate
{
    protected ProjAdvancedJournal projAdvancedJournal;
    protected ProjAdvancedJournalLine projAdvancedJournalLine;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the current line of the document.
    /// </summary>
    /// <returns>The current line of the document</returns>
    protected TaxIntegrationLineObject getLine()
    {
        TaxIntegrationLineObject line;

        TaxIntegrationLocalId localId = TaxIntegrationAbstractRecordObject::getLocalIdStatic(this.projAdvancedJournalLine.TableId, this.projAdvancedJournalLine.RecId);
        if (!document.checkLine(localId))
        {
            document.setException(new System.InvalidOperationException(
                strFmt("@TaxIntegrationException:LineOnDocumentNotFound", localId, document.getLocalId())));
        }
        else
        {
            line = document.getLine(localId);
        }

        return line;
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveDocument</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the document.
    /// </summary>
    /// <returns>Always true.</returns>
    protected boolean saveDocument()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves the current line of the document.
    /// </summary>
    /// <param name = "_line">The current line of the document.</param>
    /// <returns>true if the operation is successful; otherwise, false.</returns>
    protected boolean saveLine(TaxIntegrationLineObject _line)
    {
        TaxGroup taxGroup = _line.getTaxGroup();
        TaxItemGroup taxItemGroup = _line.getTaxItemGroup();

        if (projAdvancedJournalLine.TaxGroup != taxGroup || projAdvancedJournalLine.TaxItemGroup != taxItemGroup)
        {
            projAdvancedJournalLine.TaxGroup = taxGroup;
            projAdvancedJournalLine.TaxItemGroup = taxItemGroup;
            projAdvancedJournalLine.doUpdate();
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the document.
    /// </summary>
    /// <returns>The query for the document.</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentQueryObject()
    {
        if (document.getLocalRecord())
        {
            this.projAdvancedJournal = document.getLocalRecord();
            return null;
        }
        else
        {
            throw error(strFmt("@TaxIntegrationException:RecordNotFound",
                fieldStr(ProjAdvancedJournal, RecId),
                this.document.getLocalRecId(),
                tableStr(ProjAdvancedJournal)));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the lines of the document.
    /// </summary>
    /// <returns>The query for the lines of the document</returns>
    [Replaceable]
    protected SysDaQueryObject getLineQueryObject()
    {
        return SysDaQueryObjectBuilder::from(this.projAdvancedJournalLine)
            .forUpdate()
            .where(this.projAdvancedJournalLine, fieldStr(ProjAdvancedJournalLine, ProjAdvancedJournal))
                .isEqualToLiteral(this.projAdvancedJournal.RecId)
            .where(this.projAdvancedJournalLine, fieldStr(ProjAdvancedJournalLine, Voucher))
                .isEqualToLiteral(this.document.getVoucher())
            .toSysDaQueryObject();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of the document.
    /// </summary>
    /// <returns>The query for the charges of the document</returns>
    [Replaceable]
    protected SysDaQueryObject getDocumentChargeQueryObject()
    {
        throw Error(Error::wrongUseOfFunction(funcName()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLineChargeQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query for the charges of each line.
    /// </summary>
    /// <returns>The query for the charges of each line.</returns>
    [Replaceable]
    protected SysDaQueryObject getLineChargeQueryObject()
    {
        throw Error(Error::wrongUseOfFunction(funcName()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>