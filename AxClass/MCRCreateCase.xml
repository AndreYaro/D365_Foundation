<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRCreateCase</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>MCRCreateCase</c> creates cases.
/// </summary>
public class MCRCreateCase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createCaseDetail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new case with passed description.
    /// </summary>
    /// <param name="_description">
    /// The description for the new case.
    /// </param>
    /// <exception cref="Exception::Error">
    /// Throw error if new <c>CaseDetailBase</c> fails validation.
    /// </exception>
    /// <exception cref="Exception::Error">
    /// Throw error if new <c>smmActivities</c> fails validation.
    /// </exception>
    public void createCaseDetail(str _description)
    {
        CaseDetailBase                  caseDetailBase;
        CaseCategoryHierarchyDetail     caseCategoryHierarchyDetail;
        NumberSeq                       numberSeq;
        smmActivities                   smmActivities;

        select firstonly RecId from caseCategoryHierarchyDetail
            where caseCategoryHierarchyDetail.CategoryType == CaseCategoryType::General;

        ttsBegin;

        numberSeq = NumberSeq::newGetNum(NumberSeqReference::findReference(extendedTypeNum(CaseId)));
        caseDetailBase.CaseId = numberSeq.num();
        caseDetailBase.Description = _description;
        numberSeq.used();

        caseDetailBase.Status          = CaseStatus::Planned;
        caseDetailBase.CategoryRecId   = caseCategoryHierarchyDetail.RecId;
        caseDetailBase.modifiedField(fieldNum(CaseDetailBase, CategoryRecId));
        if (caseDetailBase.validateWrite())
        {
            caseDetailBase.insert();
        }
        else
        {
            throw error("@SYS104318");
        }

        // Create activity
        smmActivities.setActivityNum();
        smmActivities.initFromCommon(caseDetailBase);
        if (smmActivities.validateWrite())
        {
            smmActivities.insert();
        }
        else
        {
            throw error("@SYS104318");
        }

        ttsCommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>