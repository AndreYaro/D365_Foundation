<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustVendTransFormHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustVendTransFormHelper</c> class provides form utilities for functionality shared between the <c>CustTrans</c> and <c>VendTrans</c> forms.
/// </summary>
public class CustVendTransFormHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getClosedTransactionQueryValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the value of the open and closed transaction filter.
    /// </summary>
    /// <param name = "_showTransaction">The transaction type to show.</param>
    public static str getClosedTransactionQueryValue(
        CustVendShowTransaction _showTransaction,
        ModuleCustVend _module,
        TransDate _openAsOfDate = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone()))
    {
        str criteriaOpenValue;

        switch (_showTransaction)
        {
            case CustVendShowTransaction::All:
                criteriaOpenValue = SysQueryRangeUtil::valueUnlimited();
                break;

            case CustVendShowTransaction::Open:
                criteriaOpenValue = CustVendTransFormHelper::getOpenTransactionsQueryValue();
                break;

            case CustVendShowTransaction::OpenAsOf:
                criteriaOpenValue = CustVendTransFormHelper::getOpenTransactionsByClosedDateQueryValue(_module, _openAsOfDate);
                break;

            case CustVendShowTransaction::Closed:
                criteriaOpenValue = CustVendTransFormHelper::getClosedTransactionsQueryValue(_module);
                break;
        }

        return criteriaOpenValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOpenTransactionsQueryValue</Name>
				<Source><![CDATA[
    private static str getOpenTransactionsQueryValue()
    {
        return strfmt('%1..', dateNull());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getOpenTransactionsByClosedDateQueryValue</Name>
				<Source><![CDATA[
    private static str getOpenTransactionsByClosedDateQueryValue(ModuleCustVend _module, TransDate _closedByDate)
    {
        str tableString;
        str fieldString;

        switch (_module)
        {
            case ModuleCustVend::Cust:
                tableString = tableStr(CustTrans);
                fieldString = fieldStr(CustTrans, Closed);
                break;

            case ModuleCustVend::Vend:
                tableString = tableStr(VendTrans);
                fieldString = fieldStr(VendTrans, Closed);
                break;
        }

        return strfmt(
            '((%1.%2 == %3) || (%1.%2 > %4))',
            tableString,
            fieldString,
            date2StrXpp(dateNull()),
            date2StrXpp(_closedByDate));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClosedTransactionsQueryValue</Name>
				<Source><![CDATA[
    private static str getClosedTransactionsQueryValue(ModuleCustVend _module)
    {
        str tableString;
        str fieldString;

        switch (_module)
        {
            case ModuleCustVend::Cust:
                tableString = tableStr(CustTrans);
                fieldString = fieldStr(CustTrans, Closed);
                break;

            case ModuleCustVend::Vend:
                tableString = tableStr(VendTrans);
                fieldString = fieldStr(VendTrans, Closed);
                break;
        }

        return strfmt(
            '((%1.%2 != %3))',
            tableString,
            fieldString,
            date2StrXpp(dateNull()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getHideCurrencyRevaluationQueryValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the query value to hide currency revaluations by <c>LedgerTransType</c>.
    /// </summary>
    /// <returns>The query value to hide currency revaluations.</returns>
    public static str getHideCurrencyRevaluationQueryValue()
    {
        return strFmt('!%1', queryValue(LedgerTransType::ExchAdjustment));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>