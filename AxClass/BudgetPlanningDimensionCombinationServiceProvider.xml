<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanningDimensionCombinationServiceProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BudgetPlanningDimensionCombinationServiceProvider</c> class resolves and validates budget planning dimension combinations.
/// </summary>
[System.ComponentModel.Composition.ExportMetadataAttribute(enumstr(LedgerDimensionType), LedgerDimensionType::BudgetPlanning),
 System.ComponentModel.Composition.ExportAttribute(identifierstr(Dynamics.AX.Application.DimensionCombinationServiceProvider))]
public class BudgetPlanningDimensionCombinationServiceProvider extends DimensionCombinationServiceProvider
{
    DimensionHierarchyId accountStructureId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDimensionStorage</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets a <c>DimensionStorage</c> instance with the resolved values.
    /// </summary>
    /// <returns>
    ///    An instance of the <c>DimensionStorage</c> class.
    /// </returns>
    protected DimensionStorage getDimensionStorage()
    {
        LedgerDimensionType ledgerDimensionType = this.getCombinationContract().getLedgerDimensionType();

        LedgerDimensionDefaultingEngine defaultingEngine = LedgerDimensionDefaultingEngine::constructForAccountStructure(
            this.accountStructureId,
            LedgerDimensionDefaultingEngine::getValueSetStorageDimensionSpecifiers(this.getResolvedValues()),
            ledgerDimensionType);

        return defaultingEngine.getStorage(ledgerDimensionType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newForBudgetPlanningAccountContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Create a new instance of the <c>BudgetPlanningDimensionCombinationServiceProvider</c> class for the specified
    ///    budget planning account contract.
    /// </summary>
    /// <param name="_contract">
    ///    A <c>BudgetPlanningContract</c> instance.
    /// </param>
    /// <returns>
    ///    The <c>BudgetPlanningDimensionCombinationServiceProvider</c> instance.
    /// </returns>
    public static BudgetPlanningDimensionCombinationServiceProvider newForBudgetPlanningAccountContract(
        BudgetPlanningContract _contract)
    {
        BudgetPlanningDimensionCombinationServiceProvider serviceProvider =
            new BudgetPlanningDimensionCombinationServiceProvider();
        serviceProvider.initialize(_contract);
        return serviceProvider;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateNameValueListContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates the <c>DimensionNameValueListContract</c> instance and updates the dimension storage result with any errors.
    /// </summary>
    /// <returns>
    ///    true if the contract is valid; otherwise, false.
    /// </returns>
    protected boolean validateNameValueListContract()
    {
        BudgetPlanningContract budgetPlanningContract = this.getCombinationContract() as BudgetPlanningContract;

        if (budgetPlanningContract)
        {
            this.accountStructureId = DimensionHierarchy::findByTypeAndName(
                DimensionHierarchyType::AccountStructure,
                budgetPlanningContract.parmAccountStructure()).RecId;

            if (this.accountStructureId)
            {
                super();
            }
            else
            {
                this.getDimensionStorageResult().parmStatus(DimensionStorageResultStatus::InvalidPrimaryStructure);
                this.getDimensionStorageResult().parmInvalidPrimaryStructureName(budgetPlanningContract.parmAccountStructure());
            }
        }
        else
        {
            this.getDimensionStorageResult().parmStatus(DimensionStorageResultStatus::InvalidContract);
        }

        return this.getDimensionStorageResult().isValid();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>