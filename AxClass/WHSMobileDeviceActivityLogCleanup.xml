<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileDeviceActivityLogCleanup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>WHSMobileDeviceActivityLogCleanup</c> class provides an ability
///     to make a batch logs removal.
/// </summary>
public class WHSMobileDeviceActivityLogCleanup extends RunBaseBatch implements BatchRetryable, SysErrorMessageHelpScopeIBatchProvider
{
    DialogField     dialogDaysToKeep;
    PositiveDays    daysToKeep;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        daysToKeep
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatchJournal</Name>
				<Source><![CDATA[
    public boolean canGoBatchJournal()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunBase drb = super();

        dialogDaysToKeep = drb.addFieldValue(extendedTypeStr(PositiveDays), daysToKeep, "@WAX3411", "@WAX5676");
        drb.allowUpdateOnSelectCtrl(true);

        return drb;
    }

]]></Source>
			</Method>
			<Method>
				<Name>doCleanup</Name>
				<Source><![CDATA[
    private void doCleanup()
    {
        WHSMobileDeviceActivityLog  whsMobileDeviceActivityLog;

        ttsbegin;

        utcDateTime beforeDateTime = DateTimeUtil::addDays(DateTimeUtil::utcNow(), -daysToKeep);
        delete_from whsMobileDeviceActivityLog where whsMobileDeviceActivityLog.Timestamp <= beforeDateTime;

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        daysToKeep = dialogDaysToKeep.value();

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmDaysToKeep</Name>
				<Source><![CDATA[
    /// <summary>
	/// Setter method for daysToKeep field.
	/// </summary>
	/// <param name="_daysToKeep">
	/// Days to keep value to be set.
	/// </param>
    public void parmDaysToKeep(int _daysToKeep)
    {
        daysToKeep = _daysToKeep;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        using (SysErrorMessageHelpScope scope = SysErrorMessageHelpScope::newErrorContextBatchServer(this))
        {
            #OCCRetryCount

            try
            {
                this.doCleanup();
            }
            catch (Exception::Deadlock)
            {
                retry;
            }
            catch (Exception::TransientSqlConnectionError)
            {
                if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                {
                    retry;
                }
                else
                {
                    throw;
                }
            }
            catch (Exception::UpdateConflict)
            {
                if (appl.ttsLevel() == 0)
                {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        retry;
                    }
                }
                else
                {
                    throw;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        boolean ret;
        Integer version = RunBase::getVersion(_packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = _packedClass;
                ret = true;
                break;

            default:
                ret = false;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static WHSMobileDeviceActivityLogCleanup construct()
    {
        return new WHSMobileDeviceActivityLogCleanup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@WAX5670";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WHSMobileDeviceActivityLogCleanup whsMobileDeviceActivityLogCleanup = WHSMobileDeviceActivityLogCleanup::construct();

        using (SysErrorMessageHelpScope scope = SysErrorMessageHelpScope::newErrorContextBatchController(whsMobileDeviceActivityLogCleanup))
        {
            if (whsMobileDeviceActivityLogCleanup.prompt())
            {
                whsMobileDeviceActivityLogCleanup.runOperation();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>errorMessageHelpScopeFormName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the form name used for the error context using a <c>SysErrorMessageHelpScope</c> class.
    /// </summary>
    /// <returns>The form name used for the error context.</returns>
    [Hookable(false)]
    public FormName errorMessageHelpScopeFormName()
    {
        return strFmt('%1_%2', formStr(Dialog), classStr(WHSMobileDeviceActivityLogCleanup));
    }

]]></Source>
			</Method>
			<Method>
				<Name>errorMessageHelpScopeActionName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the action name used for the error context using a <c>SysErrorMessageHelpScope</c> class.
    /// </summary>
    /// <returns>The action name used for the error context.</returns>
    [Hookable(false)]
    public str errorMessageHelpScopeActionName()
    {
        return menuItemActionStr(WHSMobileDeviceActivityLogCleanup);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>