<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustBalanceListController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>CustBalanceListController</c> class starts the customer balance list report.
/// </summary>
class CustBalanceListController extends SrsReportRunController implements BatchRetryable
{
    TransDate transDate;

    #define.ReportName ('CustBalanceList.Report')
    #define.ParameterAsPerDate ('AsofDate')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Override this method to change the report contract before running the report.
    /// </summary>
    protected void preRunModifyContract()
    {
        transDate = this.parmReportContract().parmRdlContract().getParameter(#parameterAsPerDate).getValueTyped();
        this.setRanges(this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey()), transDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setRanges</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Modifies the report query and parameters based on the arguments.
    /// </summary>
    /// <param name="_query">
    ///    A <c>Query</c> object.
    /// </param>
    /// <param name="_transDate">
    ///    A <c>TransDate</c> object.
    /// </param>
    public void setRanges(Query _query, TransDate _transDate)
    {
        QueryBuildRange queryBuildRange;
        QueryBuildDataSource queryBuildDataSource;

        queryBuildDataSource = SysQuery::findOrCreateDataSource(_query,
                                                                tableNum(CustTrans));

        queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource,
                                                      fieldNum(CustTrans, TransDate));
        // Assign value to the TransDate field.
        queryBuildRange.value(strFmt('%1..%2', dateNull(), _transDate));

        // Set the range status to Hidden.
        queryBuildRange.status(RangeStatus::Hidden);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args _args)
    {
        CustBalanceListController controller = new CustBalanceListController();

        controller.parmReportName(#ReportName);
        controller.parmArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>