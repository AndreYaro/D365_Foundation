<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendCustTransportPointLineLogisticsAddressZipCodeApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(LogisticsAddressZipCodeLookup))]
internal final class VendCustTransportPointLineLogisticsAddressZipCodeApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    public void init()
    {
        // We need this extension for the ToZipCode on the Purchase transport points form (VendCustTransportPoint2Point)
        // since we can only have one logisticsPostalAddressMapInterface per table, and we have both To and From addresses.
        boolean isPurchaseTransportDaysToAddress = this.isPurchaseTransportDaysToAddress();
        if (isPurchaseTransportDaysToAddress)
        {
            VendCustTransportPointLine transportPointLine = this.args().record() as VendCustTransportPointLine;
            if (transportPointLine.ToCountryRegionId)
            {
                isCountryRegionSet = true;
            }
        }

        next init();

        if (isPurchaseTransportDaysToAddress)
        {
            VendCustTransportPointLine transportPointLine = this.args().record() as VendCustTransportPointLine;
            
            LogisticsPostalAddress logisticsPostalAddress;
            logisticsPostalAddress.CountryRegionId = transportPointLine.ToCountryRegionId;
            logisticsPostalAddress.State = transportPointLine.ToState;
            logisticsPostalAddress.County = transportPointLine.ToCounty;
            logisticsPostalAddress.City = transportPointLine.ToCity;

            callerLogisticsAddressMap = logisticsPostalAddress;
            callerHasLogisticsAddressMap = true;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>closeSelect</Name>
				<Source><![CDATA[
    public void closeSelect(str _selectString)
    {
        if (this.isPurchaseTransportDaysToAddress())
        {
            callerHasLogisticsAddressMap = false;
        }

        next closeSelect(_selectString);       
    }

]]></Source>
			</Method>
			<Method>
				<Name>isPurchaseTransportDaysToAddress</Name>
				<Source><![CDATA[
    private boolean isPurchaseTransportDaysToAddress()
    {
        xArgs args = this.args();

        if (args)
        {
            Object caller = args.caller();
            if (caller && caller is FormRun)
            {
                FormRun callerForm = caller;
                if (callerForm.name() == formStr(VendCustTransportPoint2Point)
                    && args.parm() == fieldId2Name(tableNum(VendCustTransportPointLine), fieldNum(VendCustTransportPointLine, ToCity)))
                {
                    return true;
                }
            }
        }

        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>