<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetConsumptionProposalDelete</Name>
	<SourceCode>
		<Declaration><![CDATA[
class AssetConsumptionProposalDelete extends RunBase
{
    Object                      callerObject;
    FormRun                     callerForm;
    FormDataSource              formDataSource;

    QueryRun                    queryRun;

    AssetBookType               assetBookType;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        ""
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    private void buildQuery()
    {
        Query query = new Query();
        ;

        query.addDataSource(  TableNum(TmpAssetConsumptionProposal));
        query.dataSourceTable(TableNum(TmpAssetConsumptionProposal)).addRange(FieldNum(TmpAssetConsumptionProposal,assetId));
        query.dataSourceTable(TableNum(TmpAssetConsumptionProposal)).addRange(FieldNum(TmpAssetConsumptionProposal,bookId));

        queryRun = new QueryRun(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(Args args)
    {
        ;

        super();

        callerObject    = args.caller();
        callerForm      = args.caller();
        formDataSource  = FormDataUtil::getFormDataSource(args.record());
        assetBookType   = args.parmEnum();

        this.buildQuery();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </summary>
    /// <returns>
    ///    The instance of the <c>QueryRun</c> class, which is used by the <c>queryPrompt</c> method.
    /// </returns>
    /// <remarks>
    ///    This method is used if a query prompt is in the dialog and if a select menu item is added to a
    ///    dialog. Do not create the instance of the <c>QueryRun</c> class when this method is called. Do it
    ///    in the <c>unpack</c> method and the <c>initParmDefault</c> method, or in the <c>init</c> method.
    /// </remarks>
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Contains the code that does the actual job of the class.
    /// </summary>
    public void run()
    {
        TmpAssetConsumptionProposal    tmpAssetConsumptionProposal;
        ;

        tmpAssetConsumptionProposal = callerForm.dataSource(1).cursor();

        queryRun.setCursor(formDataSource.cursor(),1);

        while (queryRun.next())
        {
            tmpAssetConsumptionProposal = queryRun.get(TableNum(TmpAssetConsumptionProposal));
            tmpAssetConsumptionProposal.delete();
        }

        callerObject.setAssetConsumptionProposal(tmpAssetConsumptionProposal);
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether to add a Select button to the dialog box.
    /// </summary>
    /// <returns>
    ///    Always returns true.
    /// </returns>
    /// <remarks>
    ///    If you click this button, it will show the query form. Therefore, the <c>queryRun</c> method has to
    ///    return a valid instance of the <c>QueryRun</c> class. If you change the return value to false, the
    ///    button will no longer be added.
    /// </remarks>
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer         version     = conpeek(packedClass,1);
        PackedQueryRun  packedQuery;

        switch (version)
        {
            case #CurrentVersion:
                [version,packedQuery] = packedClass;
                if (SysQuery::isPackedOk(packedQuery))
                {
                    queryRun = new QueryRun(packedQuery);
                }
                break;
            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    static public ClassDescription description()
    {
        return "@SYS71095";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static public void main(Args args)
    {
        AssetConsumptionProposalDelete proposalDelete;

        if (!args || args.dataset() != tablenum(TmpAssetConsumptionProposal))
            throw error(strfmt("@SYS25516",AssetConsumptionProposalDelete::description()));

        proposalDelete = new AssetConsumptionProposalDelete(args);

        if (proposalDelete.prompt())
            proposalDelete.runOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>