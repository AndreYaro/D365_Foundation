<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BomCalcLinePmfBurdenInitializer</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>BomCalcLinePmfBurdenInitializer</c> class is used to initialize <c>BomCalcLine</c> objects with burden related information.
/// </summary>
class BomCalcLinePmfBurdenInitializer
{
    BOMCalcLine   bomCalcLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getBurdenCostGroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the burden cost group id.
    /// </summary>
    /// <returns>
    /// The cost group id.
    /// </returns>
    /// <exception cref="Exception::Error">
    /// The cost group can not be found.
    /// </exception>
    protected CostGroupId getBurdenCostGroup()
    {
        ProdParameters      prodParameters      = ProdParameters::find();
        RouteCostCategory   routeCostCategory;

        routeCostCategory = RouteCostCategory::find(prodParameters.PmfBurdenCostCategory);

        if (!routeCostCategory.CostGroupId)
        {
            throw error("@SYS28398");
        }

        return routeCostCategory.CostGroupId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnitId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the unit of measure symbol for the burden consumption.
    /// </summary>
    /// <param name="_inventUnitId">
    /// Inventory unit of associated byproduct.
    /// </param>
    /// <param name="_burdenType">
    /// Burden type.
    /// </param>
    /// <returns>
    /// The unit of measure symbol.
    /// </returns>
    private InventUnitId getUnitId(InventUnitId   _inventUnitId, PmfBurdenType _burdenType)
    {
        switch (_burdenType)
        {
            case PmfBurdenType::Percent:
                return CompanyInfoHelper::standardCurrency();
            case PmfBurdenType::PerQuantity:
            case PmfBurdenType::PerSeries:
                return _inventUnitId;
            default:
                return '';
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeBomCalcLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the <c>BomCalcLine</c> with burden related information.
    /// </summary>
    /// <param name="_bomCalcLine">
    /// The <c>BomCalcLine</c> to be initialized.
    /// </param>
    public void initializeBomCalcLine(BOMCalcLine   _bomCalcLine)
    {
        bomCalcLine = _bomCalcLine;

        _bomCalcLine.parmCostGroupId(this.getBurdenCostGroup());
        _bomCalcLine.parmCalculationType(this.parmBurdenCalculationType());
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeBomCalcLineFromFormCoBy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the instance of the <c>BOMCalcLine</c> class
    /// using information from a <c>PmfFormulaCoBy</c> record.
    /// </summary>
    /// <param name="_bomCalcLine">
    /// The <c>BomCalcLine</c> class instance.
    /// </param>
    /// <param name="_pmfFormulaCoBy">
    /// The <c>PmfFormulaCoBy</c> record.
    /// </param>
    public void initializeBomCalcLineFromFormCoBy(BOMCalcLine    _bomCalcLine,
                                                  PmfFormulaCoBy _pmfFormulaCoBy)
    {
        _bomCalcLine.parmInventUnitId(this.getUnitId(_pmfFormulaCoBy.inventUnitId(), _pmfFormulaCoBy.BurdenType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeBomCalcLineFromProdCoBy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the instance of the <c>BOMCalcLine</c> class
    /// using information from a <c>PmfProdCoBy</c> record.
    /// </summary>
    /// <param name="_bomCalcLine">
    /// The <c>BomCalcLine</c> class instance.
    /// </param>
    /// <param name="_pmfProdCoBy">
    /// The <c>PmfProdCoBy</c> record.
    /// </param>
    public void initializeBomCalcLineFromProdCoBy(BOMCalcLine    _bomCalcLine,
                                                  PmfProdCoBy    _pmfProdCoBy)
    {
        _bomCalcLine.parmInventUnitId(this.getUnitId(_pmfProdCoBy.inventUnit(), _pmfProdCoBy.BurdenType));
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmBurdenCalculationType</Name>
				<Source><![CDATA[
    private ItemCalcType parmBurdenCalculationType()
    {
        return ItemCalcType::Burden;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static BomCalcLinePmfBurdenInitializer construct()
    {
        return new BomCalcLinePmfBurdenInitializer();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>