<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSPChannelProdAttrMetadataHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>RetailSPChannelProdAttrMetadataHandler</c> is used to generate XML based on
/// <c>RetailProductAttributeRelationType</c>. Also this class holds the metadata values for 
/// attribute definition regardless its relationship with other entities (e.g. Products, Channels,
/// Customers).
/// </summary>
class RetailSPChannelProdAttrMetadataHandler extends RetailChannelProdAttrMetadataHandler
{
    #define.IsSearchable('IsSearchable')
    #define.IsRetrievable('IsRetrievable')
    #define.IsQueryable('IsQueryable')
    #define.IsRefinable('IsRefinable')
    #define.IsSortable('IsSortable')
    #define.IsTokenNormalization('IsTokenNormalization')
    #define.IsCompleteMatch('IsCompleteMatch')
    #define.CurrentMetadataVersion(2)

    public const str IsMultiValueConst = 'IsMultiValue';
    public const str IsMandatoryConst = 'IsMandatory';
    public const str IsHiddenConst = 'IsHidden';

    boolean isSearchable;
    boolean isRetrievable;
    boolean isQueryable;
    boolean isRefinable;
    boolean isSortable;
    boolean isMultiValue;
    boolean isTokenNormalization;
    boolean isCompleteMatch;
    boolean isMandatory;
    boolean isHidden;

    FormCheckBoxControl isSearchableCheckBox;
    FormCheckBoxControl isRetrievableCheckBox;
    FormCheckBoxControl isQueryableCheckBox;
    FormCheckBoxControl isRefinableCheckBox;
    FormCheckBoxControl isSortableCheckBox;
    FormCheckBoxControl isMultiValueCheckBox;
    FormCheckBoxControl isTokenNormalizationCheckBox;
    FormCheckBoxControl isCompleteMatchCheckBox;
    FormCheckBoxControl isMandatoryCheckBox;
    FormCheckBoxControl isHiddenCheckBox;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>allowEdit</Name>
				<Source><![CDATA[
    public boolean allowEdit(boolean _value = allowEdit, int _version = Version1)
    {
        boolean ret;
        boolean changed;

        changed = !prmisdefault(_value);

        ret = super(_value, _version);

        if (changed)
        {
            this.allowEditChanged(_value, _version);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>allowEditChanged</Name>
				<Source><![CDATA[
    private void allowEditChanged(boolean _value, int _version)
    {
        switch (_version)
        {
            case Version1:
                this.allowEditChanged_v1(_value);
                break;
            case #CurrentMetadataVersion:
                this.allowEditChanged_v2(_value);
                break;
            default :
                // do nothing
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>allowEditChanged_v1</Name>
				<Source><![CDATA[
    private void allowEditChanged_v1(boolean _value)
    {
        if ((metadataId > 0) || (internalOrgId && tmpRetailProductAttributeMetadata.Attribute))
        {
            if (isSearchableCheckBox != null)
            {
                isSearchableCheckBox.allowEdit(_value);
            }

            if (isRetrievableCheckBox != null)
            {
                isRetrievableCheckBox.allowEdit(_value);
            }

            if (isQueryableCheckBox != null)
            {
                isQueryableCheckBox.allowEdit(_value);
            }

            if (isRefinableCheckBox != null)
            {
                isRefinableCheckBox.allowEdit(_value);
            }

            if (isSortableCheckBox != null)
            {
                isSortableCheckBox.allowEdit(_value);
            }

            if (isMultiValueCheckBox != null)
            {
                isMultiValueCheckBox.allowEdit(_value);
            }

            if (isTokenNormalizationCheckBox != null)
            {
                isTokenNormalizationCheckBox.allowEdit(_value);
            }

            if (isCompleteMatchCheckBox != null)
            {
                isCompleteMatchCheckBox.allowEdit(_value);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>allowEditChanged_v2</Name>
				<Source><![CDATA[
    private void allowEditChanged_v2(boolean _value)
    {
        this.allowEditChanged_v1(_value);

        if ((metadataId > 0) || (internalOrgId && tmpRetailProductAttributeMetadata.Attribute))
        {
            if (isMandatoryCheckBox != null)
            {
                isMandatoryCheckBox.allowEdit(_value);
            }

            if (isHiddenCheckBox != null)
            {
                isHiddenCheckBox.allowEdit(_value);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>currentMetadataVersion</Name>
				<Source><![CDATA[
    public int currentMetadataVersion()
    {
        return #CurrentMetadataVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultMetadata</Name>
				<Source><![CDATA[
    public str defaultMetadata(
        RetailProductAttributeRelationType _attributeRelationType,
        RetailStandardAttributeID _standardAttributeId = RetailStandardAttributeID::None,
        RetailChannelProductAttributeID _channelProductAttributeId = RetailChannelProductAttributeID::None,
        int _version = Version1)
    {
        str xml;

        switch (_attributeRelationType)
        {
            case RetailProductAttributeRelationType::StandardProduct:
                switch (_standardAttributeId)
                {
                    case RetailStandardAttributeID::ProductNumber:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, false, false, true, true, false, false);
                        break;

                    case RetailStandardAttributeID::ProductName:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, true, false, true, false);
                        break;

                    case RetailStandardAttributeID::SearchName:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, false, false, true, true);
                        break;

                    case RetailStandardAttributeID::Description:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, false);
                        break;

                    case RetailStandardAttributeID::Color:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);
                        break;

                    case RetailStandardAttributeID::Size:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);
                        break;

                    case RetailStandardAttributeID::Style:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);
                        break;

                    case RetailStandardAttributeID::Config:
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);
                        break;

                    case RetailStandardAttributeID::None:
                        // Enrichment attributes
                        xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, false, false, false, false, false, false);
                        break;

                    default:
                        EventHandlerResult result = new EventHandlerResult();
                        this.defaultMetadataDelegateBasedOnRetailStandardAttributeID(_attributeRelationType, _standardAttributeId, _channelProductAttributeId, _version, result);
                        if (result.hasResult())
                        {
                            xml = result.result();
                        }
                        else
                        {
                            // Enrichment attributes
                            xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, false, false, false, false, false, false);
                        }
                        break;
                }
                break;

            case RetailProductAttributeRelationType::Channel:
                xml = this.defaultMetadataFromChannelAttribute(_channelProductAttributeId, _version);
                break;

            case RetailProductAttributeRelationType::Category:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, true);
                break;

            default:
                // Set default case later
        }

        if (xml == '')
        {
            xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, false, false, false, false);
        }

        return xml;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultMetadataDelegateBasedOnRetailStandardAttributeID</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delegate to retrieve default metadata  based on the RetailStandardAttributeID.
    /// </summary>
    /// <param name = "_attributeRelationType">Type of attribute relation.</param>
    /// <param name = "_standardAttributeId">Identifier of the standard attribute.</param>
    /// <param name = "_channelProductAttributeId">Identifier of channel product attribute.</param>
    /// <param name = "_version">Version.</param>
    /// <param name = "_result">Subscriber should use this parameter to return the metadata for the specified RetailStandardAttributeID.</param>
    delegate void defaultMetadataDelegateBasedOnRetailStandardAttributeID(
        RetailProductAttributeRelationType _attributeRelationType,
        RetailStandardAttributeID _standardAttributeId,
        RetailChannelProductAttributeID _channelProductAttributeId,
        int _version,
        EventHandlerResult _result)
    {
        // Subscriber will provide proper implementation to this method
    }

]]></Source>
			</Method>
			<Method>
				<Name>deserialize</Name>
				<Source><![CDATA[
    public void deserialize(str _metadata, int _version)
    {
        switch (_version)
        {
            case Version1:
                this.parseXML_v1(_metadata);
                break;
            case #CurrentMetadataVersion:
                this.parseXML_v2(_metadata);
                break;
            default :
                // do nothing
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>draw</Name>
				<Source><![CDATA[
    public void draw(FormGroupControl _groupControl, int _version = Version1)
    {
        switch (_version)
        {
            case Version1:
                this.draw_v1(_groupControl);
                break;
            case #CurrentMetadataVersion:
                this.draw_v2(_groupControl);
                break;
            default :
                // do nothing
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>draw_v1</Name>
				<Source><![CDATA[
    private FormGroupControl draw_v1(FormGroupControl _groupControl)
    {
        FormGroupControl posAttributes = _groupControl.addControl(FormControlType::Group, 'POSAttributes');
        posAttributes.caption("@Retail:RetailAttributeMetadataForCommerceChannels");

        isRefinableCheckBox = posAttributes.addControl(FormControlType::CheckBox, 'IsRefinableCheckBox');
        isRefinableCheckBox.label("@RET490192");

        if (tmpRetailProductAttributeMetadata.Attribute)
        {
            retailAttributeMetadata = RetailAttributeMetadata::findByAttribute(tmpRetailProductAttributeMetadata.Attribute);
        }

        if (this.isAttributeDataTypeDateTimeOrRichType(retailAttributeMetadata))
        {
            isRefinableCheckBox.enabled(false);
        }
        else
        {
            isRefinableCheckBox.allowEdit(allowEdit);
        }

        isRefinableCheckBox.value(isRefinable);
        isRefinableCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        FormGroupControl thirdPartyAttributes = _groupControl.addControl(FormControlType::Group, 'ThirdPartyAttributes');
        thirdPartyAttributes.caption("@Retail:RetailProductAttributesMetadataForExternalSearch");

        isSearchableCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsSearchableCheckBox');
        isSearchableCheckBox.label("@RET261322");
        isSearchableCheckBox.allowEdit(allowEdit);
        isSearchableCheckBox.value(isSearchable);
        isSearchableCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isRetrievableCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsRetrievableCheckBox');
        isRetrievableCheckBox.label("@RET261321");
        isRetrievableCheckBox.allowEdit(allowEdit);
        isRetrievableCheckBox.value(isRetrievable);
        isRetrievableCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isQueryableCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsQueryableCheckBox');
        isQueryableCheckBox.label("@RET261319");
        isQueryableCheckBox.allowEdit(allowEdit);
        isQueryableCheckBox.value(isQueryable);
        isQueryableCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);        

        isSortableCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsSortableCheckBox');
        isSortableCheckBox.label("@RET261323");
        isSortableCheckBox.allowEdit(allowEdit);
        isSortableCheckBox.value(isSortable);
        isSortableCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isMultiValueCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsMultiValueCheckBox');
        isMultiValueCheckBox.label("@RET261328");
        isMultiValueCheckBox.allowEdit(allowEdit);
        isMultiValueCheckBox.value(isMultiValue);
        isMultiValueCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isTokenNormalizationCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsTokenNormalizationCheckBox');
        isTokenNormalizationCheckBox.label("@RET261336");
        isTokenNormalizationCheckBox.allowEdit(allowEdit);
        isTokenNormalizationCheckBox.value(isTokenNormalization);
        isTokenNormalizationCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isCompleteMatchCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsCompleteMatchCheckBox');
        isCompleteMatchCheckBox.label("@RET261343");
        isCompleteMatchCheckBox.allowEdit(allowEdit);
        isCompleteMatchCheckBox.value(isCompleteMatch);
        isCompleteMatchCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        return thirdPartyAttributes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isAttributeDataTypeDateTimeOrRichType</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Checks attribute data type and returns a boolean value.
    /// </summary>
    /// <param name = "_retailAttributeMetadata">
    ///     A global attribute metadata table buffer.
    /// </param>
    /// <returns>
    ///     true if <c>AttributeDataType</c> enum value is DateTime; Otherwise, false.
    /// </returns>
    protected boolean isAttributeDataTypeDateTimeOrRichType(RetailAttributeMetadata _retailAttributeMetadata)
    {
        return (_retailAttributeMetadata.RecId && 
            (AttributeDataTypeDiscriminator::checkAttributeDataTypeDateTime(EcoResAttributeType::findByAttribute(_retailAttributeMetadata.Attribute).DataType) ||
            RetailAttributeHelper::isRichAttributeType(EcoResAttribute::find(_retailAttributeMetadata.Attribute).AttributeType)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>draw_v2</Name>
				<Source><![CDATA[
    private void draw_v2(FormGroupControl _groupControl)
    {
        FormGroupControl thirdPartyAttributes = this.draw_v1(_groupControl);

        isMandatoryCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'IsMandatoryCheckBox');
        isMandatoryCheckBox.label("@Retail:MandatoryFlag");
        isMandatoryCheckBox.allowEdit(allowEdit);
        isMandatoryCheckBox.value(isMandatory);
        isMandatoryCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);

        isHiddenCheckBox = thirdPartyAttributes.addControl(FormControlType::CheckBox, 'isHiddenCheckBox');
        isHiddenCheckBox.label("@Retail:HiddenFlag");
        isHiddenCheckBox.allowEdit(allowEdit);
        isHiddenCheckBox.value(isHidden);
        isHiddenCheckBox.registerOverrideMethod(identifierStr(modified), identifierStr(modified), this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRefinable</Name>
				<Source><![CDATA[
    /// <summary>
    /// This function is used to expose value for isRefinable.
    /// </summary>
    /// <returns>
    /// true;if refinable is marked in metadata XML, false.
    /// </returns>
    public boolean isRefinable()
    {
        return isRefinable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>modified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Override <c>modified</c> method of a <c>FormCheckBoxControl</c> control
    /// </summary>
    /// <param name="_control">
    /// Form checkbox control
    /// </param>
    /// <returns>
    /// A boolean indicates the modified result
    /// </returns>
    public boolean modified(FormCheckBoxControl _control)
    {        
        boolean canModify = true;

        if (isSearchableCheckBox.equal(_control))
        {
            isSearchable = isSearchableCheckBox.checked();
        }
        else if (isRetrievableCheckBox.equal(_control))
        {
            isRetrievable = isRetrievableCheckBox.checked();
        }
        else if (isQueryableCheckBox.equal(_control))
        {
            isQueryable = isQueryableCheckBox.checked();
        }
        else if (isRefinableCheckBox.equal(_control))
        {
            if (((tmpRetailProductAttributeMetadata && !RetailAttributeFilter::findByAttribute(tmpRetailProductAttributeMetadata.Attribute)) 
                 || (retailAttributeMetadata && !RetailAttributeFilter::findByAttribute(retailAttributeMetadata.Attribute)))
                && isRefinableCheckBox.checked())
            {
                if (tmpRetailProductAttributeMetadata.AttributeName)
                {
               
                    // if the attribute does not have any filter but user want to enable isRefinable check box, display a warning message and disallow doing so
                    warning(strFmt("@Retail:MissingFilterSettingLabel", isRefinableCheckBox.label(), tmpRetailProductAttributeMetadata.AttributeName));
                }
                else
                {
                    EcoResAttribute ecoResAttribute = EcoResAttribute::find(retailAttributeMetadata.Attribute);

                    // if the attribute does not have any filter but user want to enable isRefinable check box, display a warning message and disallow doing so
                    warning(strFmt("@Retail:MissingFilterSettingLabel", isRefinableCheckBox.label(), EcoResAttribute.Name));
                }
                canModify = false;
            }
            else
            {
                isRefinable = isRefinableCheckBox.checked();
            }
        }
        else if (isSortableCheckBox.equal(_control))
        {
            isSortable = isSortableCheckBox.checked();
        }
        else if (isMultiValueCheckBox.equal(_control))
        {
            isMultiValue = isMultiValueCheckBox.checked();
        }
        else if (isTokenNormalizationCheckBox.equal(_control))
        {
            isTokenNormalization = isTokenNormalizationCheckBox.checked();
        }
        else if (isCompleteMatchCheckBox.equal(_control))
        {
            isCompleteMatch = isCompleteMatchCheckBox.checked();
        }
        else if (isMandatoryCheckBox.equal(_control))
        {
            isMandatory = isMandatoryCheckBox.checked();
        }
        else if (isHiddenCheckBox.equal(_control))
        {
            isHidden = isHiddenCheckBox.checked();
        }

        this.save();

        return _control.modified();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parseXML_v1</Name>
				<Source><![CDATA[
    private void parseXML_v1(str _metadata)
    {
        #define.True('true')

        XmlDocument xmlDoc = new XmlDocument();
        XmlElement propertyElmt;
        str attributeValue;

        if (!xmlDoc.loadXml(_metadata))
        {
            // Failed to load metadata
            // Use default
            xmlDoc.loadXml(this.defaultMetadata(RetailProductAttributeRelationType::Channel));
        }

        propertyElmt = xmlDoc.documentElement();

        attributeValue = propertyElmt.getAttribute(#IsSearchable);
        isSearchable = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsRetrievable);
        isRetrievable = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsQueryable);
        isQueryable = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsRefinable);
        isRefinable = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsSortable);
        isSortable = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(IsMultiValueConst);
        isMultiValue = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsTokenNormalization);
        isTokenNormalization = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(#IsCompleteMatch);
        isCompleteMatch = attributeValue == #True;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultMetadataFromChannelAttribute</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delegate to retrieve default metadata  based on the RetailChannelProductAttributeID.
    /// </summary>
    /// <param name = "_channelProductAttributeId">An enum specifying the type of the channel product attribute.</param>
    /// <param name = "_version">The version of the metadata to be retrieved.</param>
    /// <returns>
    /// Returns the metadata for the specified RetailChannelProductAttributeID.
    /// </returns>
    private str defaultMetadataFromChannelAttribute(RetailChannelProductAttributeID _channelProductAttributeId, int _version)
    {
        str xml;
        RetailProvisionedChannelProductAttribute retailProvisionedChannelProductAttribute;

        switch (_channelProductAttributeId)
        {
            case RetailChannelProductAttributeID::ProductName:
                //isSearchable, isRetrievable, isQueryable, isRefinable, isSortable, isMultiValue, isTokenNormalization, isCompleteMatch
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, true, false, true, false);
                break;

            case RetailChannelProductAttributeID::Description:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, false);
                break;

            case RetailChannelProductAttributeID::ItemNumber:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, false, false, false, true, true);
                break;

            case RetailChannelProductAttributeID::Features:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, false);
                break;

            case RetailChannelProductAttributeID::Specification:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, false);
                break;

            case RetailChannelProductAttributeID::Image:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, false, false, false, false, false, false);
                break;

            case RetailChannelProductAttributeID::Color:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::Color;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::MultiSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::Size:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::Size;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::Style:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::Style;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::MultiSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::Configuration:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::Configuration;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::ReviewRating:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, true, true, true, false, false, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::ReviewRating;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::Slider);
                break;

            case RetailChannelProductAttributeID::New:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, true, true, false, false, false, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::New;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::CustomerFavorites:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, true, true, false, false, false, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::CustomerFavorites;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::StaffFavorites:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, false, true, true, true, false, false, false, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::StaffFavorites;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::SingleSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

            case RetailChannelProductAttributeID::Brand:
                xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, true, true, true, false, true, false);

                select firstOnly retailProvisionedChannelProductAttribute
                            where retailProvisionedChannelProductAttribute.ChannelProductAttributeId == RetailChannelProductAttributeID::Brand;

                RetailAttributeFilter::createAttributeFilter(retailProvisionedChannelProductAttribute.Attribute, RetailAttributeFilterDisplayOption::MultiSelect, RetailAttributeFilterDisplayTemplate::List);
                break;

             case RetailChannelProductAttributeID::None:
                 xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, true);
                    break;
            default:
                EventHandlerResult result = new EventHandlerResult();
                this.defaultMetadataFromChannelAttributeDelegate(_channelProductAttributeId, _version, result);
                if (result.hasResult())
                {
                    xml = result.result();
                }
                else
                {
                    // this is the same metadata as when RetailChannelProductAttributeID::None is specified
                    xml = RetailSPChannelProdAttrMetadataHandler::generateXML(_version, true, true, false, false, false, false, true, true);
                }
        }

        return xml;

    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultMetadataFromChannelAttributeDelegate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delegate to retrieve the default metadata for the specified RetailChannelProductAttributeID.
    /// </summary>
    /// <param name = "_channelProductAttributeId">An enum specifying the type of the channel product attribute.</param>
    /// <param name = "_version">Version.</param>
    /// <param name = "_result">Subscriber should use this parameter to return the metadata for the specified RetailChannelProductAttributeID.</param>
    delegate void defaultMetadataFromChannelAttributeDelegate(
        RetailChannelProductAttributeID _channelProductAttributeId,
        int _version,
        EventHandlerResult _result)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>parseXML_v2</Name>
				<Source><![CDATA[
    private void parseXML_v2(str _metadata)
    {
        #define.True('true')

        this.parseXML_v1(_metadata);
        XmlDocument xmlDoc = new XmlDocument();
        XmlElement propertyElmt;
        str attributeValue;

        if (!xmlDoc.loadXml(_metadata))
        {
            // Failed to load metadata
            // Use default
            xmlDoc.loadXml(this.defaultMetadata(RetailProductAttributeRelationType::Channel));
        }

        propertyElmt = xmlDoc.documentElement();

        attributeValue = propertyElmt.getAttribute(IsMandatoryConst);
        isMandatory = attributeValue == #True;

        attributeValue = propertyElmt.getAttribute(IsHiddenConst);
        isHidden = attributeValue == #True;
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateCategoryProductAttributes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Populate category product attributes
    /// </summary>
    /// <param name="_channel">
    /// The channel to publish
    /// </param>
    private void populateCategoryProductAttributes(RefRecId _channel)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateChannelProductAttributes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Populate channel product attributes
    /// </summary>
    /// <param name="_channel">
    /// The channel to publish
    /// </param>
    private void populateChannelProductAttributes(RefRecId _channel)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateMetadata</Name>
				<Source><![CDATA[
    public void populateMetadata(RefRecId _channel)
    {
        RetailInternalOrgUpdateChannel updateChannel;

        updateChannel = RetailInternalOrgUpdateChannel::newFromChannel(_channel);

        ttsBegin;
        updateChannel.populateMetadata();
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>populateStandardProductAttributes</Name>
				<Source><![CDATA[
    /// <summary>
    /// Populate standard product attributes
    /// </summary>
    /// <param name="_channel">
    /// The channel to publish
    /// </param>
    private void populateStandardProductAttributes(RefRecId _channel)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>serialize</Name>
				<Source><![CDATA[
    protected str serialize(int _version)
    {
        return RetailSPChannelProdAttrMetadataHandler::generateXML(
                    _version, isSearchable, isRetrievable, isQueryable, isRefinable, isSortable, isMultiValue, isTokenNormalization, isCompleteMatch, isMandatory, isHidden);
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateXML</Name>
				<Source><![CDATA[
    private static str generateXML(int _version,
        boolean _isSearchable,
        boolean _isRetrievable,
        boolean _isQueryable,
        boolean _isRefinable,
        boolean _isSortable,
        boolean _isMultiValue,
        boolean _isTokenNormalization,
        boolean _isCompleteMatch,
        boolean _isMandatory = false,
        boolean _isHidden = false)
    {
        XmlDocument xmlDoc = XmlDocument::newBlank();
        XmlElement xmlRoot = xmlDoc.createElement('ProductProperty');

        xmlRoot.setAttribute(#IsSearchable, strFmt("%1", _isSearchable));
        xmlRoot.setAttribute(#IsRetrievable, strFmt("%1", _isRetrievable));
        xmlRoot.setAttribute(#IsQueryable, strFmt("%1", _isQueryable));
        xmlRoot.setAttribute(#IsRefinable, strFmt("%1", _isRefinable));
        xmlRoot.setAttribute(#IsSortable, strFmt("%1", _isSortable));
        xmlRoot.setAttribute(IsMultiValueConst, strFmt("%1", _isMultiValue));
        xmlRoot.setAttribute(#IsTokenNormalization, strFmt("%1", _isTokenNormalization));
        xmlRoot.setAttribute(#IsCompleteMatch, strFmt("%1", _isCompleteMatch));

        if (_version == #CurrentMetadataVersion)
        {
            xmlRoot.setAttribute(IsMandatoryConst, strFmt("%1", _isMandatory));
            xmlRoot.setAttribute(IsHiddenConst, strFmt("%1", _isHidden));
        }

        xmlDoc.appendChild(xmlRoot);

        return xmlRoot.toString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>