<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesTable_RU_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class <c>SalesTable_RU_Extension</c> holds the Russian specific business logic for the <c>SalesTable</c> table.
/// </summary>
[ExtensionOf(tableStr(SalesTable))]
public final class SalesTable_RU_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFinalDiscPriceDate</Name>
				<Source><![CDATA[
    public PriceDate getFinalDiscPriceDate()
    {
        PriceDate priceDate = next getFinalDiscPriceDate();

        #ISOCountryRegionCodes
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoRU]))
        {
            PriceAgreementDate_RU priceAgreementDate = this.salesPurchTableInterface().getPriceAgreementDate();
            if (!priceAgreementDate)
            {
                SalesLine salesLine;
                select minof(PriceAgreementDate_RU) from salesLine
                    where salesLine.SalesId == this.SalesId;

                priceAgreementDate = salesLine.PriceAgreementDate_RU;
            }

            if (priceAgreementDate)
            {
                return priceAgreementDate;
            }

            PriceDate priceDate_RU = this.salesPurchTableInterface().priceDate_RU();

            if (priceDate_RU != dateNull())
            {
                return priceDate_RU;
            }
        }
        
        return priceDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>