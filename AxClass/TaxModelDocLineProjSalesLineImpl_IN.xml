<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLineProjSalesLineImpl_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for Project <c>SalesLine</c>.
/// </summary>
[TaxModelDocumentLineAttribute(tableStr(ProjProposalItem))]
public class TaxModelDocLineProjSalesLineImpl_IN extends TaxModelDocLineSalesLineImpl_IN
{
    ProjProposalItem projProposalItem;
    ProjProposalJour projProposalJour;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssessableValueTransactionCurrency</Name>
				<Source><![CDATA[
    public AmountCur getAssessableValueTransactionCurrency()
    {
        real                                fator;
        AmountCur                           ret;

        if (salesLine.SalesQty != 0)
        {
            fator = this.getQuantity() / salesLine.SalesQty;

            ret = salesLine.SalesPurchJournalLine::getLineAmount() * fator
                + salesLine.SalesPurchJournalLine::getMiscChargesAmount(salesLine.TableId, salesLine.RecId);
        }
        else
        {
            ret = 0;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the quantity from the transaction line.
    /// </summary>
    /// <returns>
    ///    The quantity.
    /// </returns>
    /// <remarks>
    ///    If the transaction does not support quantities, 0 may be passed as the quantity. This will be used
    ///    to calculate taxes by unit.
    /// </remarks>
    public Qty getQuantity()
    {
        Qty ret;

        if (projProposalItem.ItemType == ProjItemType::SalesOrder)
        {
            ret = projProposalItem.DeliverNow;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransDate</Name>
				<Source><![CDATA[
    public TransDate getTransDate()
    {
        return projProposalJour ? projProposalJour.InvoiceDate : super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        projProposalItem = ProjProposalItem::findRecId(_documentLineMap.RecId);

        select firstonly salesLine
        where salesLine.InventTransId  == projProposalItem.InventTransId;

        select firstOnly projProposalJour
        where projProposalJour.ProposalId == projProposalItem.ProposalId;

        super(salesLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentInvoiceDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction invoice date
    /// </summary>
    /// <returns>
    /// The transaction invoice date
    /// </returns>
    public InvoiceDate getDocumentInvoiceDate()
    {
        return projProposalJour.InvoiceDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldPostTaxTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if tax transaction should be posted or not.
    /// </summary>
    /// <returns>
    /// True if the document line should post tax transaction; otherwise, false.
    /// </returns>
    public NoYes shouldPostTaxTrans()
    {
        if (salesTable.CustomsExportOrder_IN)
        {
            return false;
        }
        else
        {
            return super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>