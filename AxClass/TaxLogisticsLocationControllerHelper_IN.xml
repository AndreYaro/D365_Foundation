<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxLogisticsLocationControllerHelper_IN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Helper class for logistics location lookup controller
/// </summary>
class TaxLogisticsLocationControllerHelper_IN
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getDefaultLogisticsLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrives a default <c>LogisticsLocation</c> based on the parameters.
    /// </summary>
    /// <param name="_inventLocationId">
    /// The <c>InventLocationId</c> ID used to search for the default locations.
    /// </param>
    /// <param name="_inventSiteId">
    /// The <c>InventSiteId</c> ID used to search for the default locations.
    /// </param>
    /// <param name="_logisticsLocationRoleType">
    /// The <c>LogisticsLocationRoleType</c> role to use to search for the default locations.
    /// </param>
    /// <returns>
    /// A <c>LogisticsLocationRecId</c> ID
    /// </returns>
    /// <remarks>
    /// The order in which default locations are searched are: Warehouse, Site, Company.
    /// </remarks>
    static public LogisticsLocationRecId getDefaultLogisticsLocation(
        InventLocationId            _inventLocationId,
        InventSiteId                _inventSiteId,
        LogisticsLocationRoleType   _logisticsLocationRoleType = LogisticsLocationRoleType::Delivery)
    {
        LogisticsLocationRecId  logisticsLocationRecId;
        InventLocation          inventLocation;
        InventSite              inventSite;

        if (_inventLocationId)
        {
            inventLocation         = InventLocation::find(_inventLocationId);
            logisticsLocationRecId = LogisticsLocationDefault::findSimpleDefaultByRoleType(inventLocation, _logisticsLocationRoleType).RecId;
        }

        //warehouse didn't have a default Location so search for site Location.
        if (_inventSiteId && !logisticsLocationRecId)
        {
            inventSite             = InventSite::find(_inventSiteId);
            logisticsLocationRecId = LogisticsLocationDefault::findSimpleDefaultByRoleType(inventSite, _logisticsLocationRoleType).RecId;
        }

        //warehouse and site didn't have default Locations, so search for company Location.
        if (!logisticsLocationRecId)
        {
            logisticsLocationRecId = LogisticsLocationDefault::findSimpleDefaultByRoleType(CompanyInfo::find(), _logisticsLocationRoleType).RecId;
        }

        return logisticsLocationRecId;
    }
]]></Source>
			</Method>
			<Method>
				<Name>lookupAllLogisticsLocation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Opens the postal address lookup based on selected warehouse, site and company.
    /// </summary>
    /// <param name="_formReferenceControl">
    /// <c>FormReferenceControl</c> caller control.
    /// </param>
    /// <param name="_inventLoctionId">
    /// The <c>InventLocationId</c> ID used to search for the default locations.
    /// </param>
    /// <param name="_inventSiteId">
    /// The <c>InventSiteId</c> ID used to search for the default locations.
    /// </param>
    /// <param name="_returnLocation">
    /// true if the location record ID needs to be returned versus address record ID; otherwise, false.
    /// </param>
    /// <param name="_logisticsLocationRoleType">
    /// The <c>LogisticsLocationRoleType</c> role to use to search for the default locations.
    /// </param>
    /// <returns>
    /// <c>Common</c> table buffer to return. Either <c>LogisticsLocation</c> or <c>LogisticsPostalAddress</c>.
    /// </returns>
    /// <remarks>
    /// List all postal address based on selected warehouse, site and company.
    /// </remarks>
    static public Common lookupAllLogisticsLocation(
        FormReferenceControl _formReferenceControl,
        InventLocationId     _inventLoctionId,
        InventSiteId         _inventSiteId,
        boolean _returnLocation = false,
        LogisticsLocationRoleType   _logisticsLocationRoleType = LogisticsLocationRoleType::Delivery)
    {
        Common ret;
        LogisticsLocationSelectionLookup_Purch lookup;

        lookup = LogisticsLocationSelectionLookup_Purch::newParameters(_formReferenceControl);

        lookup.parmInventSiteId(_inventSiteId);
        lookup.parmInventLocationId(_inventLoctionId);
        lookup.addRoleByType(_logisticsLocationRoleType);
        lookup.parmReturnLocation(_returnLocation);

        ret = lookup.performFormLookup();
        return ret;
    }
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>