<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendOutPaymRecord_NACHA_CCD</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class VendOutPaymRecord_NACHA_CCD extends VendOutPaymRecord_NACHA
{
    //***** NACHA CCD Record Layout *****
    //
    //      Field                           Offset  Value
    //--    ------------------------------- ------- -----------
    //01    recordTypeCode                  01 - 01 6
    //02    transactionCode                 02 - 03
    //03    receivingDFI_Identification     04 - 11
    //04    checkDigit                      12 - 12
    //05    DFI_AccountNumber               13 - 29
    //06    amount                          30 - 39
    //07    identificationNumber            40 - 54
    //08    receivingCompanyName            55 - 76
    //09    discretionaryData               77 - 78
    //10    addendaRecordIndicator          79 - 79 0 (ZERO)
    //11    traceNumber                     80 - 94

    #define.interfaceName('Standard NACHA - CCD')
    #define.formatType('CCD')

    //Offsets:
    #define.receivingCompanyNameOffset(55)
    #define.traceNumberOffset(80)

    //Field Lengths:
    #define.receivingCompanyNameLength(22)

    container               nacha_ccd_Record;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addCCDFieldPositions</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds debug field information to the output file.
    /// </summary>
    private void addCCDFieldPositions()
    {
        #define.cCDRecordLayout         ('ABBCCCCCCCCDEEEEEEEEEEEEEEEEEFFFFFFFFFFGGGGGGGGGGGGGGGHHHHHHHHHHHHHHHHHHHHHHIIJKKKKKKKKKKKKKKK - CCD PAYABLES RECORD')
        #define.cCDTraceNumber          ('                                                                               KKKKKKKKKKKKKKK - Trace Number [Journal ID + Line Number]')
        #define.cCDAddendaIndicator     ('                                                                              J - Addenda Record Indicator = "0"')
        #define.cCDDiscretionayData     ('                                                                            II - Discretionary Data <BLANK>')
        #define.cCDReceivingCompanyName ('                                                      HHHHHHHHHHHHHHHHHHHHHH - Receiving Company Name/ID')
        #define.cCDIdentificationNumber ('                                       GGGGGGGGGGGGGGG - Identification Number')
        #define.cCDAmount               ('                             FFFFFFFFFF - Amount [$$$$$$$$cc]')
        #define.cCDDFIAccountNumber     ('            EEEEEEEEEEEEEEEEE - DFI Account Number')
        #define.cCDCheckDigit           ('           D - Check Digit')
        #define.cCDReceivingDFI_ID      ('   CCCCCCCC - Receiving DFI Identification')
        #define.cCDTransactionCode      (' BB - Transaction Code = "22" for AP Checking, "32" for AP Savings')
        #define.cCDRecordTypeCode       ('A - Record Type Code = "6"')

        file.write(#cCDRecordLayout);
        file.write(#cCDTraceNumber);
        file.write(#cCDAddendaIndicator);
        file.write(#cCDDiscretionayData);
        file.write(#cCDReceivingCompanyName);
        file.write(#cCDIdentificationNumber);
        file.write(#cCDAmount);
        file.write(#cCDDFIAccountNumber);
        file.write(#cCDCheckDigit);
        file.write(#cCDReceivingDFI_ID);
        file.write(#cCDTransactionCode);
        file.write(#cCDRecordTypeCode);
        file.write();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkValues</Name>
				<Source><![CDATA[
    public boolean checkValues(LedgerJournalTrans _ledgerJournalTrans = null,
        RunbaseProgress _progress = null)
    {
        boolean ok = true;

        ok = super(_ledgerJournalTrans, _progress);

        changecompany(custVendPaym.ledgerJournalTrans().Company)
        {
            if (!this.parmIsPrenote())
            {
                if (VendPaymMethodVal::exist(_ledgerJournalTrans.PaymMode, VendPaymentValidate::PaymReference)
                    && !_ledgerJournalTrans.PaymReference)
                {
                    ok = checkFailed("@SYS24523");
                }
            }
            if (ok)
            {
                // Only transactions with Debit Amounts can be used from Accounts Payable
                if (custVendPaym.paymAmountInSendersCurrency() <= 0)
                {
                    // Payment must be a debit amount greater than zero.
                    if (!this.parmIsPrenote())
                    {
                        ok = checkFailed(strfmt("@SYS98765", custVendPaym.ledgerJournalTrans().parmAccount()));
                    }
                }
                vendBankAccount = this.getVendBankAccount();
                if (!vendBankAccount)
                {
                    // A bank account has not been setup for the vendor '%1'.
                    ok = checkFailed(strfmt("@SYS98116", custVendPaym.ledgerJournalTrans().parmAccount()));
                }
                else
                {
                    if (!vendBankAccount.AccountNum)
                    {
                        ok = checkFailed(strfmt("@SYS82529", vendBankAccount.AccountID, custVendPaym.ledgerJournalTrans().parmAccount()));
                    }
                }

                if (vendBankAccount.bankAccountStatus(DateTimeUtil::utcNow()) == BankAccountStatus::Inactive)
                {
                    if (BankParameters::find().AllowInactiveBankPrenote == NoYes::Yes)
                    {
                        warning(strfmt("@SYS128746",vendBankAccount.AccountID,vendBankAccount.VendAccount));
                    }
                    else
                    {
                        ok = checkFailed(strfmt("@SYS127527",VendTable::find(vendBankAccount.VendAccount).Name(),vendBankAccount.AccountID));
                    }
                }
            }
        }

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField08</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the receiving company name/ID field in the NACHA CCD detail record. This field will contain
    ///    the receiver's company name.
    /// </summary>
    // Receiving Company Name/Id
    protected void fillField08()
    {
        this.strInsert(substr(custVendPaym.recieversCompanyName(), 1, #receivingCompanyNameLength), #receivingCompanyNameOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillField11</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the Trace Number in the NACHA CCD detail record.
    /// </summary>
    // Trace Number
    //
    // This is a number that we can use to attempt to uniquely identify the transaction.
    // Ideally, this would consist of an EFT Number, which would function like a Check Number.
    //
    // But because of time constraints, the Trace Number will be generated by concatinating
    // the Journal Numer & Line Number with the following limitations to create a number that
    // will be unique for most users:
    //
    //  The Line Number portion of the Trace Number will be at least four characters,
    //  allowing for a minimum of 10000 unique lines per journal.  If the Journal Number
    //  is shorter than 11 characters, the number of characters the line number uses will
    //  grow to fill the 15 characters allowed for the Trace Number.
    //  If the Journal Number is longer than 11 characters, this method will use the
    //  right-most 11 characters from the Journal Number when generating the Trace Number.
    //  If the Line Number is greater than 9999, the right-most three characters of the
    //  Line Number will be used.
    //  So in the situation where there are only three characters for the line number, the
    //  numbers will wrap-around to 0000 each 10000 lines and icrement from there, which will
    //  make this trace number non-unique and not suitable for electronic reconcile, but it
    //  should be unique enough for most manual needs.
    //
    //  EXAMPLES:
    //      Journal Number: 000097_003
    //      Line Number:    1
    //      Trace Number: "000097_00300001"
    //
    //      Journal Number: 1234567890_003
    //      Line Number:    11036
    //      Trace Number: "4567890_0031036"
    //

    protected void fillField11()
    {
        this.strInsert(this.generateTraceNumber(), #traceNumberOffset);
    }

]]></Source>
			</Method>
			<Method>
				<Name>fillRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calls each fillFieldXX method to populate the entire CCD payment record.
    /// </summary>
    protected void fillRecord()
    {
        this.fillField01();
        this.fillField02();
        this.fillField03();
        this.fillField04();
        this.fillField05();
        this.fillField06();
        this.fillField07();
        this.fillField08();
        this.fillField09();
        this.fillField10();
        this.fillField11();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initRecord</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the CCD payment output record.
    /// </summary>
    protected void initRecord()
    {
        #define.blankRoutingNumber('0')
        #define.blank(' ')

        nacha_ccd_Record = connull();
        bankRoutingNumber = strrep(#blankRoutingNumber, 8);
        outputRecord = strrep(#blank, 94);
        transactionAmount = 0;

        vendBankAccount = VendBankAccount::find(custVendPaym.ledgerJournalTrans().parmAccount(), VendTable::findByCompany(custVendPaym.ledgerJournalTrans().Company, custVendPaym.ledgerJournalTrans().parmAccount()).BankAccount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>interfaceName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Returns the name of the specification to be displayed in the user interface.
    /// </summary>
    /// <returns>
    ///    The name of the specification to be displayed in the user interface.
    /// </returns>
    public PaymInterfaceName interfaceName()
    {
        return #interfaceName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>output</Name>
				<Source><![CDATA[
    /// <summary>
    /// Writes the payment detail to the Electronic Funds Transfer (EFT) output file and increments the
    /// totals for the transaction.
    /// </summary>
    public void output()
    {
        this.initRecord();
        this.fillRecord();
        custVendOutPaym.addDetailRecord(#formatType, bankRoutingNumber, 0, transactionAmount);

        nacha_ccd_Record = conins(nacha_ccd_Record, 1, outputRecord);

        file.writeExp(nacha_ccd_Record);

        if (custVendOutPaym.isDebug())
        {
            this.addCCDFieldPositions();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>strInsert</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Writes the supplied data at the instructed position in the CCD payment record.
    /// </summary>
    /// <param name="_fieldValue">
    ///    The data to write.
    /// </param>
    /// <param name="_fieldPosition">
    ///    The position to write at.
    /// </param>
    protected void strInsert(str _fieldValue, int _fieldPosition)
    {
        //  Poke a supplied string into the "outputRecord" at a given position
        outputRecord = strpoke(outputRecord, _fieldValue, _fieldPosition);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>