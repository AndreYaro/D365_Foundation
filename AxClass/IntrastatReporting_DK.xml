<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntrastatReporting_DK</Name>
	<SourceCode>
		<Declaration><![CDATA[
#ISOCountryRegionCodes
/// <summary>
/// The <c>IntrastatReporting_DK</c> class is responsible for generating file and report for Intrastat Denmark.
/// </summary>
[IntrastatCountryRegionAttribute(#isoDK)]
class IntrastatReporting_DK extends IntrastatReporting
{
    IntrastatDirection vatDirection;

    DialogField dialogVatDirection;
    DialogField dialogCorrectionOnly;

    #DEFINE.CurrentVersion(1)
    #LOCALMACRO.CurrentList
        correctionOnly,
        vatDirection
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        dialog = super();

        dialogCorrectionOnly = dialog.addFieldValue(extendedTypeStr(NoYesId), correctionOnly, "@SYS3966", "@SYS83434");

        if (!TaxIntegrationUtils::isMultipleTaxIdEnabledForIntrastat())
        {
            CompanyInfo companyInfo = CompanyInfo::find();

            if (companyInfo.getPrimaryRegistrationNumber(TaxRegistrationTypesList::TAXID) != companyInfo.ImportVATNum)
            {
                dialogVatDirection = dialog.addFieldValue(enumStr(IntrastatDirection), vatDirection, "@SYS23152", "@SYS83439");
            }
        }

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret = super();

        if (dialogVatDirection)
        {
            vatDirection = dialogVatDirection.value();
        }

        correctionOnly = dialogCorrectionOnly.value();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion, #CurrentList, super()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>prepareQuery</Name>
				<Source><![CDATA[
    protected QueryBuildDataSource prepareQuery()
    {
        QueryBuildDataSource dataSource = super();

        if (dataSource)
        {
            if (correctionOnly)
            {
                dataSource.addRange(fieldNum(Intrastat, Correct)).value(queryValue(NoYes::Yes));
            }
        }

        return dataSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        Integer version = RunBase::getVersion(packedClass);
        container packedSuper;

        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList, packedSuper] = packedClass;
                if (packedSuper)
                {
                    return super(packedSuper);
                }
                break;
            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVatDirection</Name>
				<Source><![CDATA[
    public IntrastatDirection parmVatDirection(IntrastatDirection _vatDirection = vatDirection)
    {
        vatDirection = _vatDirection;
        return vatDirection;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRun</Name>
				<Source><![CDATA[
    protected void preRun()
    {
        super();

        if (!TaxIntegrationUtils::isMultipleTaxIdEnabledForIntrastat())
        {
            switch (vatDirection)
            {
                case IntrastatDirection::Import:
                    primaryVATNumber = this.companyImportVATNum();
                    break;
                case IntrastatDirection::Export:
                    primaryVATNumber = this.companyExportVATNum();
                    break;
            }
        }
        else
        {
            primaryVATNumber = TaxRegistration::find(taxRegistrationRecId).RegistrationNumber;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>