<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LeanDocumentReceiptAdviceUpdAggregator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LeanDocumentReceiptAdviceUpdAggregator</c> class is responsible for aggregation of kanban
///    jobs already assigned to a receipt advice.
/// </summary>
class LeanDocumentReceiptAdviceUpdAggregator extends LeanDocumentReceiptAdviceAggregator
{
    KanbanJob   filterKanbanJob;
    boolean     skipFilterJob;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addSourceRecordToLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds a source record from the current data provider to the document line.
    /// </summary>
    /// <param name="_line">
    ///    The document line to which the source record will be added.
    /// </param>
    /// <remarks>
    ///    As part of adding the source record, some data is also being aggregated.
    /// </remarks>
    protected void addSourceRecordToLine(LeanDocumentProcessingDataLine _line)
    {
        KanbanJobReceiptAdviceLine  kanbanJobReceiptAdviceLine = currentProvider.getCurrentRecord(tablenum(KanbanJobReceiptAdviceLine));
        Map                         additionalLineValues = _line.parmAdditionalValues();

        Debug::assert(kanbanJobReceiptAdviceLine.RecId != 0);

        // we should not add the filter job to the source records list if we need to filter it out
        if (!skipFilterJob
            || currentKanbanJob.RecId != filterKanbanJob.RecId)
        {
            // do the standard processing (date, qty, unit and source record handling)
            super(_line);
        }

        // store the receipt advice line for further usage
        if (kanbanJobReceiptAdviceLine.RecId != 0
            && !additionalLineValues.exists(tablenum(VendPackingSlipTrans)))
        {
            VendPackingSlipTrans tempVendPackingSlipTrans = VendPackingSlipTrans::findRecId(kanbanJobReceiptAdviceLine.ReceiptAdviceLine);
            additionalLineValues.insert(tablenum(VendPackingSlipTrans), tempVendPackingSlipTrans);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>equal</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines if the two objects are equal.
    /// </summary>
    /// <param name="otherObject">
    ///    The object to compare.
    /// </param>
    /// <returns>
    ///    true if the two objects are equal; otherwise, false.
    /// </returns>
    public boolean equal(Object otherObject)
    {
        LeanDocumentReceiptAdviceUpdAggregator otherAggregator = otherObject as LeanDocumentReceiptAdviceUpdAggregator;

        return  otherObject == this
                || (otherAggregator != null
                    && this.parmSkipFilterJob() == otherAggregator.parmSkipFilterJob()
                    && super(otherAggregator)) ;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmFilterKanbanJob</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public KanbanJob parmFilterKanbanJob(KanbanJob _filterKanbanJob = filterKanbanJob)
    {
        filterKanbanJob = _filterKanbanJob;

        return filterKanbanJob;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSkipFilterJob</Name>
				<Source><![CDATA[
        [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
        public boolean parmSkipFilterJob(boolean _skipFilterJob = skipFilterJob)
    {
        skipFilterJob = _skipFilterJob;

        return skipFilterJob;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes a new instance of the <c>LeanDocumentReceiptAdviceUpdAggregator</c> class.
    /// </summary>
    /// <param name="_aggregationMode">
    ///    The aggregation mode.
    /// </param>
    /// <param name="_filterKanbanJob">
    ///    The kanban job filter..
    /// </param>
    /// <param name="_skipFilterKanbanJob">
    ///    A Boolean value that indicates whether the filter job is to be skipped during aggregation of data.
    /// </param>
    /// <returns>
    ///    An instance of the <c>LeanDocumentPurchOrderUpdateAggregator</c> class.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static LeanDocumentReceiptAdviceUpdAggregator newStandard(
        LeanDocumentLinesAggregationMode    _aggregationMode,
        KanbanJob                           _filterKanbanJob,
        boolean                             _skipFilterKanbanJob = true)
    {
        LeanDocumentReceiptAdviceUpdAggregator result = new LeanDocumentReceiptAdviceUpdAggregator(_aggregationMode, Types::String);
        result.parmFilterKanbanJob(_filterKanbanJob);
        result.parmSkipFilterJob(_skipFilterKanbanJob);
        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
