<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMJournalLineSearch_CustDebt</Name>
	<SourceCode>
		<Declaration><![CDATA[
[CFMJournalSourceAttribute(CFMJournalTransSourceType::CustDebt)]
class CFMJournalLineSearch_CustDebt extends CFMJournalLineSearch_CustVendDebt
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addTransOpenFilters</Name>
				<Source><![CDATA[
    protected void addTransOpenFilters(QueryBuildDataSource qbdsTransOpen)
    {
        QueryBuildDataSource qbdsUncommittedValues;
       
        if (this.parmUseUncommittedValues())
        {
            qbdsUncommittedValues = qbdsTransOpen.addDataSource(tableNum(CFMAnalyticDataEditUncommitted));
            qbdsUncommittedValues.relations(true);
            qbdsUncommittedValues.fetchMode(QueryFetchMode::One2One);
            filter.setDateInterval(qbdsUncommittedValues, fieldNum(CFMAnalyticDataEditUncommitted, DueDate));

            qbdsUncommittedValues.addGroupByField(fieldNum(CFMAnalyticDataEditUncommitted, DueDate));
        }
        else
        {
            qbdsTransOpen.addGroupByField(fieldNum(CustTransOpen, DueDate));
            filter.setDateInterval(qbdsTransOpen, fieldNum(CustTransOpen, DueDate));
        }

        filter.setCashFlowDirectionFilterAmountSign(qbdsTransOpen, fieldNum(CustTransOpen, AmountCur), cashFlowDirection, true);

        QueryBuildDataSource qbdsTrans = qbdsTransOpen.addDataSource(tableNum(CustTrans));
        qbdsTrans.relations(true);
        qbdsTrans.fetchMode(QueryFetchMode::One2One);

        qbdsTrans.addGroupByField(fieldNum(CustTrans, DefaultDimension));

        if (this.parmUseUncommittedValues())
        {
            qbdsUncommittedValues.addGroupByField(fieldNum(CFMAnalyticDataEditUncommitted, PaymMode));
            qbdsUncommittedValues.addGroupByField(fieldNum(CFMAnalyticDataEditUncommitted, PaymentAccountNum));
        }
        else
        {
            qbdsTrans.addGroupByField(fieldNum(CustTrans, PaymMode));
        }

        qbdsTrans.addGroupByField(fieldNum(CustTrans, CurrencyCode));

        QueryBuildDataSource qbdsCustTable = qbdsTrans.addDataSource(tableNum(CustTable));
        qbdsCustTable.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustTable, AccountNum));
        qbdsCustTable.joinMode(JoinMode::InnerJoin);
        qbdsCustTable.addGroupByField(fieldNum(CustTable, Party));
        qbdsCustTable.fetchMode(QueryFetchMode::One2One);

        QueryBuildDataSource qbdsCFMDirParty = qbdsCustTable.addDataSource(tableNum(CFMDirPartyTable));
        qbdsCFMDirParty.addLink(fieldNum(CustTable, Party), fieldNum(CFMDirPartyTable, DirPartyTable));
        qbdsCFMDirParty.joinMode(JoinMode::OuterJoin);
        qbdsCFMDirParty.addGroupByField(fieldNum(CFMDirPartyTable, PaymentPriority));
        qbdsCFMDirParty.fetchMode(QueryFetchMode::One2One);

        if (this.parmSourceRefRecId())
        {
            qbdsTransOpen.addRange(fieldNum(CustTransOpen, RecId)).value(queryValue(this.parmSourceRefRecId()));
        }

        filter.setNotPaymentRequestFilter(qbdsTransOpen, fieldNum(CustTransOpen, RecId), this.sourceType());
    }

]]></Source>
			</Method>
			<Method>
				<Name>cashDiscQuery</Name>
				<Source><![CDATA[
    protected Query cashDiscQuery()
    {
        Query query = new Query();

        filter.setOrgUnitFilterCrossCompany(query);

        QueryBuildDataSource qbdsTransOpen = query.addDataSource(tableNum(CustTransOpen));

        findOrCreateRange_W(qbdsTransOpen, fieldNum(CustTransOpen, UseCashDisc), SysQuery::valueNot(UseCashDisc::Never));

        QueryBuildDataSource qbdsCashDisc = qbdsTransOpen.addDataSource(tableNum(CustTransCashDisc));
        qbdsCashDisc.relations(true);
        qbdsCashDisc.joinMode(JoinMode::ExistsJoin);
        qbdsCashDisc.fetchMode(QueryFetchMode::One2One);

        if (this.parmUseUncommittedValues())
        {
            QueryBuildDataSource qbdsUncommittedValues = qbdsTransOpen.addDataSource(tableNum(CFMAnalyticDataEditUncommitted));
            qbdsUncommittedValues.relations(true);
            qbdsUncommittedValues.fetchMode(QueryFetchMode::One2One);
            filter.setDateInterval(qbdsUncommittedValues, fieldNum(CFMAnalyticDataEditUncommitted, DueDate));
        }
        else
        {
            filter.setDateInterval(qbdsTransOpen, fieldNum(CustTransOpen, DueDate));
        }

        filter.setCashFlowDirectionFilterAmountSign(qbdsTransOpen, fieldNum(CustTransOpen, AmountCur), cashFlowDirection, true);

        QueryBuildDataSource qbdsTrans = qbdsTransOpen.addDataSource(tableNum(CustTrans));
        qbdsTrans.relations(true);
        qbdsTrans.fetchMode(QueryFetchMode::One2One);

        QueryBuildDataSource qbdsCustTable = qbdsTrans.addDataSource(tableNum(CustTable));
        qbdsCustTable.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustTable, AccountNum));
        qbdsCustTable.joinMode(JoinMode::InnerJoin);
        qbdsCustTable.fetchMode(QueryFetchMode::One2One);

        QueryBuildDataSource qbdsCFMDirParty = qbdsCustTable.addDataSource(tableNum(CFMDirPartyTable));
        qbdsCFMDirParty.addLink(fieldNum(CustTable, Party), fieldNum(CFMDirPartyTable, DirPartyTable));
        qbdsCFMDirParty.joinMode(JoinMode::OuterJoin);
        qbdsCFMDirParty.fetchMode(QueryFetchMode::One2One);

        if (this.parmSourceRefRecId())
        {
            qbdsTransOpen.addRange(fieldNum(CustTransOpen, RecId)).value(queryValue(this.parmSourceRefRecId()));
        }

        filter.setNotPaymentRequestFilter(qbdsTransOpen, fieldNum(CustTransOpen, RecId), this.sourceType());

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProtoTransParmContract</Name>
				<Source><![CDATA[
    protected CFMJournalProtoTransParmContract initProtoTransParmContract(QueryRun _queryRun)
    {
        CFMJournalProtoTransParmContractCustVend parmContract = new CFMJournalProtoTransParmContractCustVend();

        Query queryTransOpen = _queryRun.query();
        Counter fieldCountTransOpen;
        NoYes aggregatedRecords;

        if (queryTransOpen)
        {
            QueryBuildDataSource qbdsTransOpen = queryTransOpen.dataSourceTable(tableNum(CustTransOpen));
            fieldCountTransOpen = queryTransOpen.groupByFieldCount(qbdsTransOpen);
            aggregatedRecords = fieldCountTransOpen != 0;
        }

        parmContract.parmAggregatedRecords(aggregatedRecords);
        parmContract.parmCustVendTrans(_queryRun.get(tableNum(CustTrans)));
        parmContract.parmCustVendTransOpen(_queryRun.get(tableNum(CustTransOpen)));
        parmContract.parmDirPartyMap(_queryRun.get(tablenum(CustTable)));
        parmContract.parmCFMDirParty(_queryRun.get(tablenum(CFMDirPartyTable)));

        this.initProtoTransParmContractUncommitted(_queryRun, parmContract);

        return parmContract;
    }

]]></Source>
			</Method>
			<Method>
				<Name>neverCashDiscQuery</Name>
				<Source><![CDATA[
    protected Query neverCashDiscQuery()
    {
        Query query = new Query();

        filter.setOrgUnitFilterCrossCompany(query);

        QueryBuildDataSource qbdsTransOpen = query.addDataSource(tableNum(CustTransOpen));
        qbdsTransOpen.addGroupByField(fieldNum(CustTransOpen, dataAreaId));
        qbdsTransOpen.addSelectionField(fieldNum(CustTransOpen, AmountCur), SelectionField::Sum);

        findOrCreateRange_W(qbdsTransOpen, fieldNum(CustTransOpen, UseCashDisc), queryValue(UseCashDisc::Never));

        this.addTransOpenFilters(qbdsTransOpen);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>noCashDiscQuery</Name>
				<Source><![CDATA[
    protected Query noCashDiscQuery()
    {
        Query query = new Query();

        filter.setOrgUnitFilterCrossCompany(query);

        QueryBuildDataSource qbdsTransOpen = query.addDataSource(tableNum(CustTransOpen));
        qbdsTransOpen.addGroupByField(fieldNum(CustTransOpen, dataAreaId));
        qbdsTransOpen.addSelectionField(fieldNum(CustTransOpen, AmountCur), SelectionField::Sum);

        findOrCreateRange_W(qbdsTransOpen, fieldNum(CustTransOpen, UseCashDisc), SysQuery::valueNot(UseCashDisc::Never));

        QueryBuildDataSource qbdsCashDisc = qbdsTransOpen.addDataSource(tableNum(CustTransCashDisc));
        qbdsCashDisc.relations(true);
        qbdsCashDisc.joinMode(JoinMode::NoExistsJoin);
        qbdsCashDisc.fetchMode(QueryFetchMode::One2One);

        this.addTransOpenFilters(qbdsTransOpen);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceType</Name>
				<Source><![CDATA[
    protected CFMPaymentRequestSourceType sourceType()
    {
        return CFMPaymentRequestSourceType::CustInvoice;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>