<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'CustomerInvoice'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class CustInvoiceCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        CustInvoiceJour custInvoiceJour;
        select crosscompany OrderAccount from custInvoiceJour where custInvoiceJour.RecId == _caseAssociation.RefRecId;

        CustTable custTable = CustTable::find(custInvoiceJour.OrderAccount);

        if (custTable.RecId)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Customer, custTable.RecId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        CustInvoiceJour custInvoiceJour;
        select crosscompany DataAreaId from custInvoiceJour where custInvoiceJour.RecId == _parentRecId;

        return custInvoiceJour.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        CustInvoiceJour custInvoiceJour;
        select crosscompany InvoiceId from custInvoiceJour where custInvoiceJour.RecId == _parentRecId;

        return custInvoiceJour.InvoiceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        CustInvoiceJour custInvoiceJour;
        select RecId from custInvoiceJour where custInvoiceJour.InvoiceId == _naturalKey;

        return custInvoiceJour.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        return CustInvoiceJour::findRecId(_parentRecId).custTable_OrderAccount().name();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        CustInvoiceJour custInvoiceJour;

        //We need this code in order not to lose the localization data in the opened form.
        CustInvoiceJour custInvoiceJourCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from custInvoiceJourCrossCompany
            where custInvoiceJourCrossCompany.RecId == _association.RefRecId;

        if (custInvoiceJourCrossCompany.Recid)
        {
            changecompany (custInvoiceJourCrossCompany.DataAreaId)
            {
                select firstonly custInvoiceJour where custInvoiceJour.RecId == _association.RefRecId;
            }
        }

        _args.record(custInvoiceJour);
        return menuitemdisplaystr(CustInvoiceJournal);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(CustInvoiceJour), callerControl);

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(CustInvoiceJour));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceJour, InvoiceId));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceJour, InvoiceDate));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceJour, OrderAccount));
        sysTableLookup.addLookupMethod(tableMethodStr(CustInvoiceJour, invoiceName));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceJour, SalesId));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceJour, CurrencyCode));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(CustInvoiceJour))
        {
            _eventArgs.parmEntityType(CaseEntityType::CustomerInvoice);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>