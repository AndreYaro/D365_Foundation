<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustCollectionsListPageHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>CustCollectionsListPageHelper</c> class handles generalized work for the three Collections
///    list pages.
/// </summary>
class CustCollectionsListPageHelper implements CustCollectionsIListPageHelper
{
    CustCollectionsPoolId previousSelection;
    Map poolMap;
    List poolList;
    FormStringControl poolFilter;
    CustCollectionsAgent currentAgent;
    boolean enteredFromForm;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkAgingPeriodValid</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Counts the buckets of the aging period definition.
    /// </summary>
    /// <param name="_agingPeriodName">
    ///    The aging period definition at which to look.
    /// </param>
    /// <returns>
    ///    true if there is at least one bucket, but not more than CustVendBalanceList::numOfBalanceAge() - 1; otherwise, false.
    /// </returns>
    public boolean checkAgingPeriodValid(CustVendReportName _agingPeriodName)
    {
        StatRepIntervalLine intervalLines;
        select count(RecId) from intervalLines
            order by intervalLines.LineNum desc
            where intervalLines.Name == _agingPeriodName;

        //Valid only if there are CustVendBalanceList::numOfBalanceAge() - 1 or fewer buckets
        return intervalLines.RecId > 0 && intervalLines.RecId <= CustVendBalanceList::numOfBalanceAge() - 1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkForAging</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates the current aging period definition, and displays appropriate warnings.
    /// </summary>
    /// <returns>
    ///    true if the current aging period definition is valid; otherwise, false.
    /// </returns>
    public boolean checkForAging()
    {
        CustVendReportName agingPeriod;
        CustAging custAging;
        StatRepInterval statRepInterval;
        boolean validAgingPeriod;

        agingPeriod = this.getDefaultAgingPeriod();
        validAgingPeriod = CustVendBalanceList::validateAgingPeriodDefinition(agingPeriod, false, true, true);
        if (validAgingPeriod)
        {
            select firstonly custAging
                where custAging.StatRepInterval == agingPeriod;

            select firstonly statRepInterval;

            if (!statRepInterval)
            {
                warning("@SYS137274");
            }
            else if (!custAging)
            {
                warning(strfmt("@SYS136202", agingPeriod));
            }
        }

        return validAgingPeriod;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPoolExists</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks to see whether a given pool exists before trying to use it.
    /// </summary>
    /// <param name="_poolID">
    ///    The <c>CustCollectionsPoolId</c> object to check for.
    /// </param>
    /// <returns>
    ///    true if the pool exists in the current pool list; otherwise, false.
    /// </returns>
    public boolean checkPoolExists(CustCollectionsPoolId _poolID)
    {
        return poolMap.exists(_poolID);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createPoolLookup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Shows the lookup form for a custom <c>CustCollectionsPool</c> object.
    /// </summary>
    public void createPoolLookup()
    {
        Args args;
        Object formRun;

        args = new Args(poolFilter);
        args.name(formstr(CustCollectionsPoolsLookup));
        formRun = classfactory.formRunClass(args);
        formRun.custCollectionsPools(poolList);
        formRun.init();

        if (strlen(poolFilter.valueStr()) > 0)
        {
            formRun.selectRecord(poolFilter.valueStr());
        }
        else if (currentAgent.DefaultCustomerPool)
        {
            formRun.selectRecord(currentAgent.DefaultCustomerPool);
        }
        else
        {
            formRun.selectFirstRecord();
        }

        poolFilter.performFormLookup(formRun);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentAgent</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the current user as an instance of the <c>CustCollectionsAgent</c> class.
    /// </summary>
    /// <returns>
    ///    The current user as an instance of the <c>CustCollectionsAgent</c> class.
    /// </returns>
    public CustCollectionsAgent getCurrentAgent()
    {
        return currentAgent;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPoolQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Uses the <c>PoolFilter</c> value to get the current pool query from a <c>Map</c> object.
    /// </summary>
    /// <returns>
    ///    A <c>Query</c> object that contains the customer grouping of the current pool.
    /// </returns>
    public Query getCurrentPoolQuery()
    {
        Query poolQuery;
        str pool;

        //If there's no pool, show all customers
        if (poolMap.empty() || strlen(poolFilter.valueStr()) == 0)
        {
            poolQuery = CustCollectionsPool::defaultQuery(CustPoolType::Collection);
        }
        else
        {
            pool = poolFilter.text();
            poolQuery = new Query(poolMap.lookup(pool));
        }

        return poolQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDefaultAgingPeriod</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the <c>Name</c> value of the <c>StatRepInterval</c> object that the list page should filter by.
    /// </summary>
    /// <returns>
    ///    The <c>CustVendReportName</c> object of the correct <c>StatRepInterval</c> value.
    /// </returns>
    /// <remarks>
    ///    This method first tries to find the correct name by looking at the pool query. If it cannot be
    ///    found on the pool query, it looks for what default is set on the <c>CustParameters</c> form. If
    ///    there is not one set there, it tries to get the first available <c>StatRepInterval</c> value.
    /// </remarks>
    public CustVendReportName getDefaultAgingPeriod()
    {
        return CustCollectionsListPageHelper::getDefaultAgingPeriodByPool(poolFilter.valueStr());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstPoolId</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the <c>CustCollectionsPoolId</c> value of the first <c>CustCollectionsPool</c> object in the
    ///    map of the class.
    /// </summary>
    /// <returns>
    ///    The <c>CustCollectionsPoolId</c> value of the first <c>CustCollectionsPool</c> object in the map of
    ///    the class.
    /// </returns>
    public CustCollectionsPoolId getFirstPoolId()
    {
        MapEnumerator mapEnumerator;
        CustCollectionsPoolId firstPool;

        if (!poolMap.empty())
        {
            mapEnumerator = poolMap.getEnumerator();
            mapEnumerator.moveNext();
            firstPool = mapEnumerator.currentKey();
        }

        return firstPool;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInitialFilterValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the initial value for the pool filter for the collections list pages.
    /// </summary>
    /// <param name="_lastValue">
    ///    The <c>CustCollectionsPoolId</c> value that was selected when the form was last open.
    /// </param>
    /// <param name="_poolFormValue">
    ///    The <c>CustCollectionsPoolId</c> value from the collections pools form.
    /// </param>
    /// <returns>
    ///    The correct pool to use when a collections list page loads.
    /// </returns>
    public CustCollectionsPoolId getInitialFilterValue(CustCollectionsPoolId _lastValue, CustCollectionsPoolId _poolFormValue = '')
    {
        CustCollectionsAgent agent = this.getCurrentAgent();

        if (_poolFormValue && this.checkPoolExists(_poolFormValue))
        {
            return _poolFormValue;
        }
        else if (agent.DefaultCustomerPool && this.checkPoolExists(agent.DefaultCustomerPool))
        {
            return agent.DefaultCustomerPool;
        }
        else if (_lastValue && this.checkPoolExists(_lastValue))
        {
            return _lastValue;
        }
        else
        {
            return this.getFirstPoolId();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>loadPools</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the pool filter on the form with pools assigned to the current agent.
    /// </summary>
    public void loadPools()
    {
        CustCollectionsPool custCollectionsPool;
        CustCollectionsAgentPool custCollectionsAgentPool;
        CustCollectionsAgent custCollectionsAgent;

        poolMap = new Map(Types::String, Types::Container);
        poolList = new List(Types::String);

        while select Name, QueryPacked
            from custCollectionsPool
                where custCollectionsPool.Type == CustPoolType::Collection
            exists join custCollectionsAgentPool
                where custCollectionsPool.Name == custCollectionsAgentPool.CustomerPoolId
            exists join custCollectionsAgent
                where custCollectionsAgent.RecId == custCollectionsAgentPool.CustCollectionsAgent &&
                    custCollectionsAgent.RecId == currentAgent.RecId
        {
            poolMap.insert(custCollectionsPool.Name, custCollectionsPool.QueryPacked);
            poolList.addEnd(custCollectionsPool.Name);
        }

        if (currentAgent.AllowShowAllPools || enteredFromForm)
        {
            this.loadUnassignedPools();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>loadUnassignedPools</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Fills the pool filter on the form with pools not assigned to the current agent.
    /// </summary>
    /// <remarks>
    ///    This is run only when the <c>CustCollectionsAgent.ShowAll</c> value is checked or the agent has no
    ///    pools assigned.
    /// </remarks>
    public void loadUnassignedPools()
    {
        CustCollectionsPool custCollectionsPool;
        CustCollectionsAgentPool custCollectionsAgentPool;
        CustCollectionsAgent custCollectionsAgent;

        while select Name, QueryPacked
            from custCollectionsPool
                where custCollectionsPool.Type == CustPoolType::Collection
            notexists join custCollectionsAgentPool
                where custCollectionsPool.Name == custCollectionsAgentPool.CustomerPoolId
            exists join custCollectionsAgent
                where custCollectionsAgent.RecId == custCollectionsAgentPool.CustCollectionsAgent &&
                    custCollectionsAgent.RecId == currentAgent.RecId
        {
            poolMap.insert(custCollectionsPool.Name, custCollectionsPool.QueryPacked);
            poolList.addEnd(custCollectionsPool.Name);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>CustCollectionsListPageHelper</c> class.
    /// </summary>
    /// <param name="_poolFilter">
    /// A <c>FormStringControl</c> designed to contain a list of pools.
    /// </param>
    /// <param name="_fromForm">
    /// If the list page was given arguments or not.
    /// </param>
    public void new(FormStringControl _poolFilter, boolean _fromForm = false)
    {
        poolFilter = _poolFilter;

        currentAgent = CustCollectionsAgent::findCurrentAgent();

        enteredFromForm = _fromForm;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCompany</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the company that the listpage helper will query when performing cross company queries
    /// </summary>
    /// <param name = "_company">The company to perform queries with</param>
    /// <remarks>This does not do any thing in ths class since this is not cross company</remarks>
    public void updateCompany(SelectableDataArea _company = curExt())
    {
        //This does not do cross company so no code needed
    }

]]></Source>
			</Method>
			<Method>
				<Name>isCrossCompany</Name>
				<Source><![CDATA[
    public boolean isCrossCompany()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAgingPeriodDefFromPool</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the aging period definition range value that is used on the provided customer pool query.
    /// </summary>
    /// <param name="_customerPool">
    ///    The customer pool query to evaluate.
    /// </param>
    /// <returns>
    ///    The aging period definition range value that is used on the provided customer pool query.
    /// </returns>
    /// <remarks>
    ///    If no customer pool query has been provided, an empty string will be returned. If the customer pool
    ///    does not include an aging period definition range, an empty string will be returned. If the
    ///    customer pool contains an aging period definition, the aging period definiton will be returned as
    ///    long as it is in the system as a valid aging period definition.
    /// </remarks>
    public static CustVendReportName getAgingPeriodDefFromPool(CustCollectionsPoolId _customerPool)
    {
        CustVendReportName agingPeriodDefFromPool;
        CustCollectionsPool collectionPool;
        Query query;
        QueryBuildDataSource currentDs;
        int dsCount;
        int currentDsNumber;
        int currentTableId;
        QueryBuildRange currentRange;
        int rangeCount;
        int currentRangeNumber;
        str currentFieldName;

        if (_customerPool)
        {
            select firstonly QueryPacked from collectionPool where collectionPool.Name == _customerPool;

            // Nothing to do if the customer pool no longer exists
            if (collectionPool)
            {
                query = new Query(collectionPool.QueryPacked);

                dsCount = query.dataSourceCount();

                // Enumerate all datasources since the same table can exist multiple times
                for (currentDsNumber = 1; currentDsNumber <= dsCount; currentDsNumber++)
                {
                    currentDs = query.dataSourceNo(currentDsNumber);
                    currentTableId = currentDs.table();

                    if (currentTableId == tablenum(CustAging))
                    {
                        rangeCount = currentDs.rangeCount();

                        // Enumerate all ranges since multiple ranges of the same column can exist. Stop
                        // after the first match is found.
                        for (currentRangeNumber = 1; currentRangeNumber <= rangeCount; currentRangeNumber++)
                        {
                            currentRange = currentDs.range(currentRangeNumber);
                            currentFieldName = currentRange.fieldName();
                            if (currentFieldName == fieldstr(CustAging, StatRepInterval))
                            {
                                if (StatRepInterval::exist(currentRange.value()))
                                {
                                    agingPeriodDefFromPool = currentRange.value();
                                    return agingPeriodDefFromPool;
                                }
                            }
                        }
                    }
                }
            }
        }

        return agingPeriodDefFromPool;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDefaultAgingPeriodByPool</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the name of the default <c>StatRepInterval</c> record for a specified pool ID.
    /// </summary>
    /// <param name="_poolId">
    ///    The ID of a pool.
    /// </param>
    /// <returns>
    ///    The <c>Name</c> field of the specified <c>StatRepInterval</c> record.
    /// </returns>
    public static CustVendReportName getDefaultAgingPeriodByPool(CustCollectionsPoolId _poolId)
    {
        CustVendReportName agingPeriod;

        CustVendReportName validate(CustVendReportName _agingPeriod)
        {
            return CustVendBalanceList::validateAgingPeriodDefinition(_agingPeriod, false, false) ? _agingPeriod : '';
        }

        // Try to get it from the pool query
        if (_poolId)
        {
            agingPeriod = validate(CustCollectionsListPageHelper::getAgingPeriodDefFromPool(_poolId));
        }

        if (!agingPeriod)
        {
            // Try to get it from CustParameters
            agingPeriod = validate(CustParameters::find().CollectionsStatRepInterval);
        }

        if (!agingPeriod)
        {
            // Try to get the first one
            agingPeriod = CustCollectionsListPageHelper::getFirstValidAgingPeriod();
        }

        return agingPeriod;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstValidAgingPeriod</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds the name of the first valid aging period definition.
    /// </summary>
    /// <returns>
    ///    The <c>CustVendReportName</c> value of a <c>StatRepInterval</c> record; otherwise, an empty string.
    /// </returns>
    public static CustVendReportName getFirstValidAgingPeriod()
    {
        StatRepInterval statRepInterval;

        while select Name
            from statRepInterval
            order by statRepInterval.Name
        {
            if (CustVendBalanceList::validateAgingPeriodDefinition(statRepInterval.Name, false, false))
            {
                return statRepInterval.Name;
            }
        }
        return '';
    }

]]></Source>
			</Method>
			<Method>
				<Name>handleRange</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates or clears ranges on the data source.
    /// </summary>
    /// <param name="_qbds">
    ///    The value that should contain the ranges.
    /// </param>
    /// <param name="_qbr">
    ///    The value local to the form that may already exist.
    /// </param>
    /// <param name="_value">
    ///    The value for the <c>QueryBuildRange</c> object.
    /// </param>
    /// <param name="_fieldId">
    ///    The <c>fieldID</c> value that the <c>QueryBuildRange</c> object should be on.
    /// </param>
    /// <remarks>
    ///    The <c>handleRange</c> method attempts to find an existing range on the data source and if a
    ///    non-empty string value is provided, set the value on the range.If found, the range is cleared if an
    ///    empty string is given for the value.
    /// </remarks>
    public static void handleRange(QueryBuildDataSource _qbds, QueryBuildRange _qbr, str _value, fieldId _fieldId)
    {
        _qbr = _qbds.findRange(_fieldId);

        if (!_qbr && strlen(strltrim(_value)) > 0)
        {
            // Not there, but needed - create it
            _qbr = _qbds.addRange(_fieldId);
        }

        if (_qbr)
        {
            if (strlen(strltrim(_value)) > 0)
            {
                // Needed
                _qbr.value(_value);
            }
            else
            {
                // Not needed, remove it
                _qbds.clearRange(_fieldId);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDirPartyContactInfoView</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds the appropriate query ranges to the <c>DirPartyContactInfoView</c> data source provided in the
    ///    query.
    /// </summary>
    /// <param name="_query">
    ///    The form query to modify.
    /// </param>
    /// <remarks>
    ///    The ranges added ensure only the primary phone number is retrieved for the current date and time.
    /// </remarks>
    public static void updateDirPartyContactInfoView(Query _query)
    {
        QueryBuildDataSource dirPartyContactInfoViewDs;
        utcdatetime currentDateTime;
        QueryBuildRange dateRangeFrom, dateRangeTo;
        QueryBuildRange isPrimaryRange;
        QueryBuildRange methodTypeRange;

        dirPartyContactInfoViewDs = _query.dataSourceTable(tablenum(DirPartyContactInfoView));

        if (dirPartyContactInfoViewDs)
        {
            dirPartyContactInfoViewDs.clearRanges();

            currentDateTime = DateTimeUtil::utcNow();

            dateRangeFrom = dirPartyContactInfoViewDs.addRange(fieldnum(DirPartyContactInfoView,ValidFrom));
            dateRangeFrom.status(RangeStatus::Hidden);
            dateRangeFrom.value(queryRange(DateTimeUtil::minValue(), currentDateTime));

            dateRangeTo = dirPartyContactInfoViewDs.addRange(fieldnum(DirPartyContactInfoView,ValidTo));
            dateRangeTo.status(RangeStatus::Hidden);
            dateRangeTo.value(queryRange(currentDateTime, DateTimeUtil::maxValue()));

            isPrimaryRange = dirPartyContactInfoViewDs.addRange(fieldnum(DirPartyContactInfoView, IsPrimary));
            isPrimaryRange.status(RangeStatus::Hidden);
            isPrimaryRange.value(queryValue(NoYes::Yes));

            methodTypeRange = dirPartyContactInfoViewDs.addRange(fieldnum(DirPartyContactInfoView, Type));
            methodTypeRange.status(RangeStatus::Hidden);
            methodTypeRange.value(queryValue(LogisticsElectronicAddressMethodType::Phone));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDirPartyPostalAddressView</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds the appropriate query ranges to the <c>DirPartyPostalAddressView</c> data source that is
    ///    provided in the query.
    /// </summary>
    /// <param name="_query">
    ///    The form query to modify.
    /// </param>
    /// <remarks>
    ///    The added ranges make sure that only the primary address is retrieved for the current date and time.
    /// </remarks>
    public static void updateDirPartyPostalAddressView(Query _query)
    {
        QueryBuildDataSource dirPartyPostalAddressViewDs;
        utcdatetime currentDateTime;
        QueryBuildRange dateRangeFrom, dateRangeTo;
        QueryBuildRange isPrimaryRange;

        dirPartyPostalAddressViewDs = _query.dataSourceTable(tablenum(DirPartyPostalAddressView));

        if (dirPartyPostalAddressViewDs)
        {
            dirPartyPostalAddressViewDs.clearRanges();

            currentDateTime = DateTimeUtil::utcNow();

            dateRangeFrom = dirPartyPostalAddressViewDs.addRange(fieldnum(DirPartyPostalAddressView,ValidFrom));
            dateRangeFrom.status(RangeStatus::Hidden);
            dateRangeFrom.value(queryRange(DateTimeUtil::minValue(), currentDateTime));

            dateRangeTo = dirPartyPostalAddressViewDs.addRange(fieldnum(DirPartyPostalAddressView,ValidTo));
            dateRangeTo.status(RangeStatus::Hidden);
            dateRangeTo.value(queryRange(currentDateTime, DateTimeUtil::maxValue()));

            isPrimaryRange = dirPartyPostalAddressViewDs.addRange(fieldnum(DirPartyPostalAddressView, IsPrimary));
            isPrimaryRange.status(RangeStatus::Hidden);
            isPrimaryRange.value(queryValue(NoYes::Yes));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>