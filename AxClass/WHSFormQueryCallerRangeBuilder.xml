<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSFormQueryCallerRangeBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSFormQueryCallerRangeBuilder</c> class is the abstract base class for the warehouse form query caller range builders.
/// </summary>
internal abstract class WHSFormQueryCallerRangeBuilder
{
    private Common record;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmRecord</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal Common parmRecord(Common _record = record)
    {
        record = _record;

        return record;
    }

]]></Source>
			</Method>
			<Method>
				<Name>maxNumberOfSelectedRecords</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static NumberOfRecords maxNumberOfSelectedRecords()
    {
        return 500;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateWarehouseFormQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the warehouse form query.
    /// </summary>
    /// <param name = "_warehouseFormQuery">Warehouse form query to be updated.</param>
    internal abstract void updateWarehouseFormQuery(Query _warehouseFormQuery)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>sourceTableQueryRangeFieldId</Name>
				<Source><![CDATA[
    internal abstract FieldId sourceTableQueryRangeFieldId()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateQueryRangeFromSelectedRecords</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal str calculateQueryRangeFromSelectedRecords()
    {
        str calculatedQueryRange;

        NumberOfRecords maxNumberOfSelectedRecords = WHSFormQueryCallerRangeBuilder::maxNumberOfSelectedRecords();
        NumberOfRecords numberOfProcessedRecords;

        FieldId sourceTableQueryRangeFieldId = this.sourceTableQueryRangeFieldId();

        FormDataSource dataSource = FormDataUtil::getFormDataSource(this.parmRecord()) as FormDataSource;

        if (dataSource)
        {
            for (Common currentRecord = dataSource.getFirst(true) ? dataSource.getFirst(true) : dataSource.cursor(); currentRecord; currentRecord = dataSource.getNext())
            {
                if (numberOfProcessedRecords == maxNumberOfSelectedRecords)
                {
                    warning(strFmt("@WarehouseExecutionWorkloadBackoffice:WHSSourceRecordsFormQueryLimitWarning", maxNumberOfSelectedRecords));
                    break;
                }

                calculatedQueryRange = queryRangeConcat(calculatedQueryRange, currentRecord.(sourceTableQueryRangeFieldId));
                numberOfProcessedRecords++;
            }
        }
        else
        {
            calculatedQueryRange = queryValue(this.parmRecord().(sourceTableQueryRangeFieldId));
        }

        return calculatedQueryRange;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>