<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjListProjWIPProjDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>ProjListProjWIPProjDP</c> class provides data for the <c>ProjListProjWIPProj</c> report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(ProjStatementProject)),
    SRSReportParameterAttribute(classstr(ProjListProjWIPContract))
]

class ProjListProjWIPProjDP extends SRSReportDataProviderPreProcessTempDB
{
    #ProjReports

    ProjOptionTM            timeMaterial;
    ProjOptionFixedPrice    fixedPrice;
    ProjOptionInvestment    investment;
    ProjOptionInternal      internalCost;
    ProjOptionTime          time;
    NoYes                   timeMaterialBillingMethod;
    NoYes                   fixedPriceBillingMethod;
    ProjOptionHours         hour;
    ProjOptionCost          expenses;
    ProjOptionItem          item;
    ProjOptionRevenue       revenue;
    ProjOptionOnAcc         onAcc;
    TransDate               projFromDate;
    TransDate               projToDate;
    TransDate               ledgerFromDate;
    TransDate               ledgerToDate;
    ProjListColumnData      actualVsBudget;
    ForecastModelId         forecastModelId;
    ProjListStateProjType   stateProjType;
    ProjShowAmount          showAmount;
    ProjAmountQty           amountQty;
    Level                   level;
    boolean                 skipBlankId;
    boolean                 skipZero;
    boolean                 posWIP;
    boolean                 negWIP;

    ProjListProjWIPTmp              projListProjWIPTmp;
    ProjListProjTrans_WIP           listProjTrans;
    ProjListProjTransLayout         listProjTransLayout;
    ProjListProjHelpClass           projHelpClass;

    NumberSequenceFormat    oldLevelFormat;

    QueryRun    qrun;
    ProjId      projIdFound;
    ProjId      projId;

    ProjQty     qtyHour;
    AmountMST   costValue, profit, onAccount, salesValue, production, gross, net;
    Integer     showCostValue, showGross, showNet, showOnAccount,
                showQtyHour, showSalesValue;
    str         costValueLabel, salesValueLabel, periodLabel, selectionLabel;
    int         hourQtyDecimals, amountDecimals, grossMarginDecimals, valueAddedDecimals;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>calculate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Calculates the amounts that are needed for the report.
    /// </summary>
    void calculate()
    {
        projHelpClass.parmProjStatement().parmProjId(projIdFound);
        projHelpClass.setParmField();

        switch (amountQty)
        {
            case ProjAmountQty::Amount:
                break;

            case ProjAmountQty::Qty:
                qtyHour     += projHelpClass.parmProjStatement().b300_Gross_qty();
                break;

            case ProjAmountQty::AmountQty:
                qtyHour     += projHelpClass.parmProjStatement().b300_Gross_qty();
                break;
        }

        costValue   += projHelpClass.parmProjStatement().b200_TotalCostPrice();
        salesValue  += projHelpClass.parmProjStatement().b100_Salesvalue();
        production  += projHelpClass.parmProjStatement().b130_wip_Production();
        profit      += projHelpClass.parmProjStatement().b155_wip_Profit();
        gross       += projHelpClass.parmProjStatement().b300_Gross();
        onAccount   += projHelpClass.parmProjStatement().b400_wip_Invoiced_OnAccount();
        net         += projHelpClass.parmProjStatement().b500_Net();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPrintLine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks the parameters to determine whether the line should be printed.
    /// </summary>
    /// <returns>
    ///    true if the line should be printed; otherwise, false.
    /// </returns>
    boolean checkPrintLine()
    {
        if ((!listProjTransLayout.parmSkipZero() || this.lineValue()) && listProjTransLayout.canPrintId(projHelpClass.id()))
        {
            if ((listProjTransLayout.parmPosWIP() && net >= 0) ||
                (listProjTransLayout.parmNegWIP() && net  < 0))
            {
                return true;
            }
            if ((!listProjTransLayout.parmPosWIP()) &&
                (!listProjTransLayout.parmNegWIP()))
            {
                return true;
            }
        }

        this.setFieldZero();

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeLineOne</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Evaluates whether the line should be printed and inserts into the temporary table.
    /// </summary>
    /// <param name="projTableLocal">
    ///    A record buffer that contains the information about a project.
    /// </param>
    void executeLineOne(ProjTable projTableLocal)
    {
        boolean dataInserted = false;
        if (stateProjType == ProjListStateProjType::Project)
        {
            if ((projTableLocal.level() <= level) ||
                (projTableLocal.levelFormat(level)   != oldLevelFormat))
            {
                if (this.checkPrintLine())
                {
                    projHelpClass.setName();
                    this.insertProjListProjWIPTmp();
                    this.setFieldZero();
                    dataInserted = true;
                }
            }
        }
        if (this.checkPrintLine() && !dataInserted)
        {
            projHelpClass.setName();
            this.insertProjListProjWIPTmp();
            this.setFieldZero();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getProjListProjWIPTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the data from the <c>ProjListProjWIPProjSRSTmp</c> table.
    /// </summary>
    /// <returns>
    ///    The data from the <c>ProjListProjWIPProjSRSTmp</c> table.
    /// </returns>

    [SRSReportDataSetAttribute('ProjListProjWIPTmp')]
    public ProjListProjWIPTmp getProjListProjWIPTmp()
    {
        select * from projListProjWIPTmp;

        return projListProjWIPTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initProjListTrans</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the <c>ProjListProjTrans</c> class by using the parameters.
    /// </summary>
    private void initProjListTrans()
    {
        listProjTrans = ProjListProjTrans::construct(ProjListStateType::WIP, ProjListStateId::Project);
        listProjTransLayout = ProjListProjTransLayout::construct(listProjTrans);

        listProjTrans.parmTimeMaterial(timeMaterial);
        listProjTrans.parmFixedPrice(fixedPrice);
        listProjTrans.parmInvestment(investment);
        listProjTrans.parmInternalCost(internalCost);

        listProjTrans.parmHour(hour);
        listProjTrans.parmExpenses(expenses);
        listProjTrans.parmItem(item);
        listProjTrans.parmRevenue(revenue);
        listProjTrans.parmOnAcc(onAcc);

        listProjTrans.parmProjFromDate(projFromDate);
        listProjTrans.parmProjToDate(projToDate);
        listProjTrans.parmLedgerFromDate(ledgerFromDate);
        listProjTrans.parmLedgerToDate(ledgerToDate);

        listProjTrans.parmColumnData(actualVsBudget);
        listProjTrans.parmForecastModelId(forecastModelId);

        listProjTransLayout.parmStateProjType(stateProjType);
        listProjTransLayout.parmLevel(level);
        listProjTransLayout.parmSkipBlank(skipBlankId);
        listProjTransLayout.parmSkipZero(skipZero);

        listProjTransLayout.parmShowAmount(showAmount);
        listProjTransLayout.parmAmountQty(amountQty);
        listProjTransLayout.parmPosWIP(posWIP);
        listProjTransLayout.parmNegWIP(negWIP);

        if (listProjTransLayout.parmStateProjType() != ProjListStateProjType::Project)
        {
            listProjTransLayout.parmLevel(2);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertProjListProjWIPTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Retrieves the required data and inserts it into the temporary table.
    /// </summary>
    private void insertProjListProjWIPTmp()
    {
        projListProjWIPTmp.clear();

        projListProjWIPTmp.Id   = projHelpClass.id();
        projListProjWIPTmp.Name = projHelpClass.name();

        projListProjWIPTmp.CostValue  = listProjTransLayout.showAmountReport(costValue, showAmount);
        projListProjWIPTmp.SalesValue = listProjTransLayout.showAmountReport(salesValue, showAmount);
        projListProjWIPTmp.Gross      = listProjTransLayout.showAmountReport(gross, showAmount);
        projListProjWIPTmp.Net        = listProjTransLayout.showAmountReport(net, showAmount);
        projListProjWIPTmp.OnAccount  = listProjTransLayout.showAmountReport(onAccount, showAmount);
        projListProjWIPTmp.Production = listProjTransLayout.showAmountReport(production, showAmount);
        projListProjWIPTmp.Profit     = listProjTransLayout.showAmountReport(profit, showAmount);
        projListProjWIPTmp.QtyHour    = listProjTransLayout.showAmountReport(qtyHour, showAmount);

        // set labels
        projListProjWIPTmp.IdLabel         = strfmt('%1',stateProjType);
        projListProjWIPTmp.HeaderLabel     = selectionLabel;
        projListProjWIPTmp.PeriodLabel     = periodLabel;
        projListProjWIPTmp.CostValueLabel  = costValueLabel;
        projListProjWIPTmp.SalesValueLabel = salesValueLabel;

        // set field visibility
        projListProjWIPTmp.ShowCostValue  = showCostValue;
        projListProjWIPTmp.ShowGross      = showGross;
        projListProjWIPTmp.ShowNet        = showNet;
        projListProjWIPTmp.ShowOnAccount  = showOnAccount;
        projListProjWIPTmp.ShowQtyHour    = showQtyHour;
        projListProjWIPTmp.ShowSalesValue = showSalesValue;

        // set decimals
        projListProjWIPTmp.DecimalAmount  = amountDecimals;
        projListProjWIPTmp.DecimalHourQty = hourQtyDecimals;

        projListProjWIPTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lineValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks whether the amounts are equal to zero.
    /// </summary>
    /// <returns>
    ///    true if the values are not zero; otherwise, false.
    /// </returns>
    boolean lineValue()
    {
        return (qtyHour    != 0 || costValue != 0 ||
                profit     != 0 || onAccount != 0 ||
                salesValue != 0 || production);
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the query ranges.
    /// </summary>
    void modifyQuery()
    {
        QueryBuildDataSource qbdsProjTable = qrun.query().dataSourceTable(tableNum(ProjTable));
        qbdsProjTable.addSortField(fieldNum(ProjTable,ProjId));

        QueryBuildRange rangeHide = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
        rangeHide.status(RangeStatus::Hidden);

        if (ProjMultipleContractLinesForProjectFeatureHelper::isFeatureEnabled())
        {
            QueryBuildDataSource qbdsPSAContractLineItems = qrun.query().dataSourceTable(tableNum(PSAContractLineItems));
            qbdsPSAContractLineItems.enabled(true);
            PSAContractLineItemRangesHelper::addBillingMethodRangesToDS(
                qbdsPSAContractLineItems,
                timeMaterialBillingMethod,
                fixedPriceBillingMethod);
            PSAContractLineItemRangesHelper::addTransactionTypeRangesToDS(
                qbdsPSAContractLineItems,
                hour,
                expenses,
                revenue,
                item,
                false);
        }
        else
        {
            if (timeMaterial)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::TimeMaterial));
            }

            if (fixedPrice)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::FixedPrice));
            }

            if (investment)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Investment));
            }

            if (internalCost)
            {
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Cost));
                qbdsProjTable.addRange(fieldNum(ProjTable,Type)).value(queryValue(ProjType::Internal));
            }

            if (!(timeMaterial || fixedPrice || investment || internalCost))
            {
                QueryBuildRange rangeEmptyString = qbdsProjTable.addRange(fieldNum(ProjTable,Type));
                rangeEmptyString.value(queryValue(SysQuery::valueEmptyString()));
            }
        }

        if (skipZero)
        {
            QueryBuildDataSource qbdsProjectTrans;

            if (actualVsBudget == ProjListColumnData::Actual)
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransPosting)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransPosting, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, ProjTransDate)).value(queryValue(listProjTrans.parmProjFromDate()) + '..' + queryValue(listProjTrans.parmProjToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransPosting, LedgerTransDate)).value(queryValue(listProjTrans.parmLedgerFromDate()) + '..' + queryValue(listProjTrans.parmLedgerToDate()));
            }
            else
            {
                qbdsProjectTrans = qbdsProjTable.addDataSource((tableNum(ProjTransBudget)));
                qbdsProjectTrans.joinMode(JoinMode::ExistsJoin);
                qbdsProjectTrans.addLink(fieldNum(ProjTable, ProjId), fieldNum(ProjTransBudget, ProjId));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, ProjTransDate)).value(queryValue(listProjTrans.parmProjFromDate()) + '..' + queryValue(listProjTrans.parmProjToDate()));
                qbdsProjectTrans.addRange(fieldNum(ProjTransBudget, LedgerTransDate)).value(queryValue(listProjTrans.parmLedgerFromDate()) + '..' + queryValue(listProjTrans.parmLedgerToDate()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>printProject</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the project level and calls the <c>calculate</c> method.
    /// </summary>
    void printProject()
    {
        ProjTable           projTableLocal;
        ProjFundingSource   projFundingSourceLocal;

        projTableLocal  = qrun.get(tablenum(ProjTable));
        projIdFound     = projTableLocal.ProjId;
        projId          = ProjListProjHelpClass::setProjId(projTableLocal,listProjTransLayout.parmStateProjType(),listProjTransLayout.parmLevel());

        if (stateProjType == ProjListStateProjType::FundingSource)
        {
            projFundingSourceLocal = qrun.get(tablenum(ProjFundingSource));
        }
        projHelpClass.setId(projTableLocal, projId, projFundingSourceLocal);

        if (! listProjTransLayout.canPrintId(projHelpClass.idNew()))
        {
            projHelpClass.setOld();

            return;
        }

        if (projHelpClass.checkIdChanged())
        {
            this.executeLineOne(projTableLocal);
        }

        this.calculate();

        projHelpClass.setOld();
        oldLevelFormat  = projTableLocal.levelFormat(listProjTransLayout.parmLevel());
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Processes the business logic of the report.
    /// </summary>
    /// <remarks>
    ///    This method provides the ability to write the report business logic. It will be called by SQL
    ///    Server Reporting Services (SSRS) at run time. This method should compute the data and populate the
    ///    data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        ProjListProjWIPContract contract = this.parmDataContract() as ProjListProjWIPContract;

        timeMaterial = contract.parmTimeMaterial();
        fixedPrice = contract.parmFixedPrice();
        investment = contract.parmInvestment();
        internalCost = contract.parmInternalCost();

        timeMaterialBillingMethod = contract.parmTimeMaterialBillingMethod();
        fixedPriceBillingMethod = contract.parmFixedPriceBillingMethod();

        hour = contract.parmHour();
        expenses = contract.parmExpenses();
        item = contract.parmItem();
        revenue = contract.parmRevenue();
        onAcc = contract.parmOnAcc();

        projFromDate = contract.parmProjFromDate();
        projToDate = contract.parmProjToDate();
        ledgerFromDate = contract.parmLedgerFromDate();
        ledgerToDate = contract.parmLedgerToDate();

        actualVsBudget = contract.parmActualVsBudget();
        forecastModelId = contract.parmForecastModelIdV2();

        stateProjType = contract.parmStateProjType();
        level = contract.parmLevel();
        skipBlankId = contract.parmSkipBlankId();
        skipZero = contract.parmSkipZero();

        showAmount = contract.parmShowAmount();
        amountQty = contract.parmAmountQty();
        posWIP = contract.parmPosWIP();
        negWIP = contract.parmNegWIP();

        this.initProjListTrans();

        periodLabel = listProjTrans.formatPeriodLabel();
        selectionLabel = listProjTrans.formatSelection(showAmount);

        [costValueLabel, salesValueLabel] = listProjTrans.setDisplayLabel(hour, expenses, item);

        [showQtyHour, showCostValue, showSalesValue,
         showOnAccount, showGross, showNet] = listProjTrans.setDisplayFlags(hour, expenses, item,
                                                                            revenue, onAcc, amountQty);

        [hourQtyDecimals, amountDecimals,
         grossMarginDecimals, valueAddedDecimals] = listProjTrans.setNoOfDecimals(showAmount);

        projHelpClass = ProjListProjHelpClass::newListProjConsumption(
                                projStatement::newProjListWIP(listProjTrans,
                                projListProjTrans2Period::columnData2ActualBudget(actualVsBudget),
                                amountQty,
                                forecastModelId),
                                stateProjType);

        qrun = new QueryRun(this.parmQuery());

        this.modifyQuery();
        this.setSortField();

        while (qrun.next())
        {
            this.printProject();
        }
        if (this.checkPrintLine())
        {
            projHelpClass.setName();
            this.insertProjListProjWIPTmp();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldZero</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the variables to zero.
    /// </summary>
    void setFieldZero()
    {
        costValue   = 0;
        salesValue  = 0;
        production  = 0;
        profit      = 0;
        gross       = 0;
        onAccount   = 0;
        net         = 0;

        qtyHour     = 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSortField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the sort field for the query.
    /// </summary>
    void setSortField()
    {
        qrun.query().dataSourceTable(tablenum(ProjTable)).sortClear();

        switch (stateProjType)
        {
            case ProjListStateProjType::Project:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjId));
                break;

            case ProjListStateProjType::Customer:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,CustAccount));
                break;

            case ProjListStateProjType::ProjGroup:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjGroupId));
                break;

            case ProjListStateProjType::WIPProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,wipProject));
                break;

            case ProjListStateProjType::InvoiceProject:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));
                break;

            case ProjListStateProjType::ResponsibleSales:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleSales));
                break;

            case ProjListStateProjType::Responsible:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsible));
                break;

            case ProjListStateProjType::ResponsibleFinancial:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, WorkerResponsibleFinancial));
                break;

            case ProjListStateProjType::Type:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,Type));
                break;

            case ProjListStateProjType::Sorting1:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 1);
                break;

            case ProjListStateProjType::Sorting2:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 2);
                break;

            case ProjListStateProjType::Sorting3:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable, SortingId), SortOrder::Ascending, 3);
                break;

            case ProjListStateProjType::Asset:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,AssetId));
                break;

            case ProjListStateProjType::FundingSource:
                qrun.query().dataSourceTable(tablenum(ProjTable)).addSortField(fieldnum(ProjTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjTable)).addDataSource(tablenum(ProjInvoiceTable));
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addLink(fieldnum(ProjTable,ProjInvoiceProjId),fieldnum(ProjInvoiceTable,ProjInvoiceProjId));

                qrun.query().dataSourceTable(tablenum(ProjInvoiceTable)).addDataSource(tablenum(ProjFundingSource));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).joinMode(JoinMode::InnerJoin);
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addLink(fieldnum(ProjInvoiceTable,ProjInvoiceProjId), fieldnum(ProjFundingSource,ContractId));
                qrun.query().dataSourceTable(tablenum(ProjFundingSource)).addSortField(fieldnum(ProjFundingSource,FundingSourceId));
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>