<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmAbsenceCodeGroupEntityEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Handles events for <c>HcmAbsenceCodeGroupEntity</c> extension.
/// </summary>
public class HcmAbsenceCodeGroupEntityEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>HcmAbsenceCodeGroupEntity_onMappedEntityToDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a JobIdentification to record after mapping to the entity.
    /// </summary>
    /// <param name="_sender"><c>HcmAbsenceCodeGroupEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The args of this event.</param>
    [DataEventHandler(tableStr(HcmAbsenceCodeGroupEntity), DataEventType::MappedEntityToDataSource), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmAbsenceCodeGroupEntity_onMappedEntityToDataSource(Common _sender, DataEventArgs _eventArgs)
    {
        DataEntityContextEventArgs contextEventArgs = _eventArgs as DataEntityContextEventArgs;
        DataEntityRuntimeContext entityCtx = contextEventArgs.parmEntityContext();
        DataEntityDataSourceRuntimeContext dataSourceCtx = contextEventArgs.parmEntityDataSourceContext();

        if (entityCtx.getDatabaseOperation() == DataEntityDatabaseOperation::Insert
            && dataSourceCtx.name() == dataEntityDataSourceStr(HcmAbsenceCodeGroupEntity, HRMAbsenceCodeGroup))
        {
            HcmAbsenceCodeGroupEntity hcmAbsenceCodeGroupEntity = _sender as HcmAbsenceCodeGroupEntity;
            HRMAbsenceCodeGroup hrmAbsenceCodeGroup = dataSourceCtx.getBuffer();

            HcmAbsenceCodeGroupEntityEventHandler::defaultJobIdentificationId(hcmAbsenceCodeGroupEntity);
            hrmAbsenceCodeGroup.JmgJobId = hcmAbsenceCodeGroupEntity.JobIdentification;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmAbsenceCodeGroupEntity_onPersistedEntity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs after persisting entity to skip validation of JobIdentification.
    /// </summary>
    /// <param name="_sender"><c>HcmAbsenceCodeGroupEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The args of this event.</param>
    [DataEventHandler(tableStr(HcmAbsenceCodeGroupEntity), DataEventType::PersistedEntity), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmAbsenceCodeGroupEntity_onPersistedEntity(Common _sender, DataEventArgs _eventArgs)
    {
        HcmAbsenceCodeGroupEntity hcmAbsenceCodeGroupEntity = _sender as HcmAbsenceCodeGroupEntity;
        hcmAbsenceCodeGroupEntity.skipDataSourceValidateField(fieldNum(HcmAbsenceCodeGroupEntity, JobIdentification), true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>HcmAbsenceCodeGroupEntity_onDefaultedRow</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs after selecting defaultRow to get JobIdentification.
    /// </summary>
    /// <param name="_sender"><c>HcmAbsenceCodeGroupEntity</c> raising the event.</param>
    /// <param name="_eventArgs">The args of this event.</param>
    [DataEventHandler(tableStr(HcmAbsenceCodeGroupEntity), DataEventType::DefaultedRow), Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void HcmAbsenceCodeGroupEntity_onDefaultedRow(Common _sender, DataEventArgs _eventArgs)
    {
        HcmAbsenceCodeGroupEntity hcmAbsenceCodeGroupEntity = _sender as HcmAbsenceCodeGroupEntity;

        HcmAbsenceCodeGroupEntityEventHandler::defaultJobIdentificationId(hcmAbsenceCodeGroupEntity);
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultJobIdentificationId</Name>
				<Source><![CDATA[
    private static void defaultJobIdentificationId(HcmAbsenceCodeGroupEntity _hcmAbsenceCodeGroupEntity)
    {
        if (!_hcmAbsenceCodeGroupEntity.JobIdentification)
        {
            _hcmAbsenceCodeGroupEntity.JobIdentification = HRMAbsenceCode::getNextJobId();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>