<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WorkPeriodTemplateCalculateForward</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>WorkPeriodTemplateCalculate</c> class calculates periods for a period template.
/// </summary>
/// <remarks>
///    The storage of the periods must be handled by another class that implements the
///    <c>WorkPeriodTemplateCalculable</c> interface.The first period will have a <c>dateNull</c> from
///    date. The last period will have a <c>maxDate</c> to date.
/// </remarks>
public class WorkPeriodTemplateCalculateForward extends WorkPeriodTemplateCalculate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addHiddenDays</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds hidden days to a specified end date.
    /// </summary>
    /// <param name="_date">
    ///    An end date.
    /// </param>
    /// <param name="_workPeriodTemplateLine">
    ///    A <c>WorkPeriodTemplateLine</c> record.
    /// </param>
    /// <param name="_remainPeriods">
    ///    An integer that contains the number of remaining time periods the current table buffer
    ///    <c>WorkPeriodTemplateLine</c> record must create.
    /// </param>
    /// <returns>
    ///    An end date.
    /// </returns>
    protected TransDate addHiddenDays(
        TransDate                   _date,
        WorkPeriodTemplateLine      _workPeriodTemplateLine,
        int                         _remainPeriods)
    {
        TransDate               newEndDate;
        TransDate               startDateNextLine;
        WorkPeriodTemplateLine  nextLine;

        newEndDate = _date;

        if (   setWorkDays.elements() > 0
            && setWorkDays.elements() < #daysPerWeek)
        {
            if (_workPeriodTemplateLine.Period == DayWeekMonthQuarterYear::Day)
            {
                if (_remainPeriods == 0)
                {
                    nextLine = this.nextWorkPeriodTemplateLine(_workPeriodTemplateLine);

                    if (   nextLine.RecId
                        && nextLine.Period != DayWeekMonthQuarterYear::Day)
                    {
                        nextLine.NumberOfPeriods    = 0;
                        startDateNextLine           = this.calcEndDate(newEndDate,nextLine) + 1;
                    }
                }

                // subsequent hidden days are added as long we don't go into the next period
                while (!this.isWorkDay(newEndDate + 1 ) && (!startDateNextLine || startDateNextLine > (newEndDate + 1)))
                {
                    newEndDate++;
                }
            }
            else if (_remainPeriods && _workPeriodTemplateLine.Period == DayWeekMonthQuarterYear::Week)            
            {
                // Take the orders on weekends to display on the previous week period
                while (!this.isWorkDay(newEndDate + 1))
                {
                    newEndDate++;                    
                }
            }
        }

        return newEndDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcEndDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates an end date by adding zero or more time periods to a start date.
    /// </summary>
    /// <param name="_startDate">
    /// A start date.
    /// </param>
    /// <param name="_templateLine">
    /// A <c>WorkPeriodTemplateLine</c> record that contains the period length and number of periods that
    /// are added to the start date.
    /// </param>
    /// <returns>
    /// An end date.
    /// </returns>
    protected TransDate calcEndDate(
        TransDate                   _startDate,
        WorkPeriodTemplateLine      _templateLine)
    {
        int         numberOfPeriods;
        TransDate   endDate;
        int         firstWeekDay;
        int         remainPeriods;

        if (!_templateLine.RecId)
        {
            return _startDate;
        }

        if (   _templateLine.ExplodePeriods == NoYes::No
            && _templateLine.NumberOfPeriods >= 1)
        {
            numberOfPeriods =_templateLine.NumberOfPeriods - 1;
        }
        else
        {
            numberOfPeriods = 0;
        }

        PreferredLocale preferredLocale = (new xSession()).PreferredLocale();

        switch (_templateLine.Period)
        {
            case DayWeekMonthQuarterYear::Day:

                endDate = _startDate;

                remainPeriods = numberOfPeriods;

                if (   setWorkDays.elements() > 0
                    && setWorkDays.elements( )< #daysPerWeek)
                {
                    while (remainPeriods)
                    {
                        endDate++;
                        if (this.isWorkDay(endDate))
                        {
                            remainPeriods--;
                        }
                    }
                }
                else
                {
                    endDate += remainPeriods;
                }

                break;

            case DayWeekMonthQuarterYear::Week:
                firstWeekDay = DateTimeUtil::getFirstDayOfWeek(preferredLocale);

                if (dayofwk(_startDate) > firstWeekDay)
                {
                    endDate = _startDate + #daysPerWeek + firstWeekDay - dayofwk(_startDate);
                }
                else
                {
                    endDate = _startDate + firstWeekDay - dayofwk(_startDate);
                }

                endDate += (#daysPerWeek*numberOfPeriods);
                break;

            case DayWeekMonthQuarterYear::Month:
                endDate = dateEndMth(dateMthFwd(_startDate,numberOfPeriods));
                break;

            case DayWeekMonthQuarterYear::Quarter:
                endDate = dateEndQtr(dateMthFwd(dateEndQtr(_startDate),#monthsPerQuarter*numberOfPeriods));
                break;

            case DayWeekMonthQuarterYear::Year:
                endDate = DateTimeUtil::getEndOfYearDate(preferredLocale, dateMthFwd(_startDate,#monthsPerYear*numberOfPeriods));
                break;
        }

        return endDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcPeriods</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calculates periods from a specified period template.
    /// </summary>
    /// <param name="_caller">
    /// An object that implements the <c>WorkPeriodTemplateCalculable</c> interface.
    /// </param>
    public void calcPeriods(WorkPeriodTemplateCalculable _caller)
    {
        #define.maxNumberOfPeriods(1000)
        WorkPeriodTemplateLine      workPeriodTemplateLine;
        TransDate                   startDate;
        TransDate                   endDate;
        Counter                     createdPeriods;
        int                         remainPeriods;
        int                         extraPeriods;
        boolean                     considerFixedStartDate = workPeriodTemplate.FixedDayStart != WeekDays::None;
        PeriodTemplateDescription   periodTemplateDescription;

        if (!this.validate())
        {
            return;
        }

        //create backLog period
        startDate   = dateNull();
        endDate     = todayDate -1;
        _caller.addPeriod(startDate,endDate,"@SYS90879",0);

        //create periods
        while select workPeriodTemplateLine
            order by LineNumber
            where workPeriodTemplateLine.PeriodTemplate == workPeriodTemplate.RecId
        {
            if (createdPeriods > #maxNumberOfPeriods)
            {
                throw error(strfmt("@SYS132216",#maxNumberOfPeriods,workPeriodTemplate.Name));
            }

            remainPeriods   = workPeriodTemplateLine.ExplodePeriods ? workPeriodTemplateLine.NumberOfPeriods : 1;
            extraPeriods    = 0;
            do
            {
                startDate = endDate + 1;

                if (   considerFixedStartDate
                    && extraPeriods < #daysPerWeek
                    && workPeriodTemplateLine.ExplodePeriods
                    && workPeriodTemplateLine.Period        == DayWeekMonthQuarterYear::Day
                    && dayofwk(startDate)                   != workPeriodTemplate.FixedDayStart)
                {
                    extraPeriods++;
                }
                else
                {
                    remainPeriods--;
                    considerFixedStartDate = false;
                }

                endDate   = this.calcEndDate(startDate, workPeriodTemplateLine);
                endDate   = this.addHiddenDays(endDate,workPeriodTemplateLine,remainPeriods);

                if (!this.parmSkipPeriodDescriptions())
                {
                    periodTemplateDescription = workPeriodTemplateLine.periodDescription(startDate,endDate,startDate);
                }

                _caller.addPeriod(startDate,endDate,periodTemplateDescription,this.calcNumOfWorkDays(startDate,endDate));
                createdPeriods++;
            }
            while (    remainPeriods > 0
                    && endDate + 1   < dateMax());
        }

        // create outLook period
        startDate   = endDate+1;
        endDate     = dateMax(); // assigning a date make later selects faster as we will avoid using .... "|| ! endDate"
        _caller.addPeriod(startDate,endDate,"@SYS131154",0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        private static WorkPeriodTemplateCalculateForward construct()
    {
        return new WorkPeriodTemplateCalculateForward();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newStandard</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes a new instance of the <c>WorkPeriodCalculate</c> class.
    /// </summary>
    /// <param name="_refRecId">
    ///    An integer that contains a period template record ID.
    /// </param>
    /// <param name="_todayDate">
    ///    A date that contains today's date.
    /// </param>
    /// <returns>
    ///    A new instance of the <c>WorkPeriodCalculate</c> class.
    /// </returns>
    public static WorkPeriodTemplateCalculateForward newStandard(
        WorkPeriodTemplateRecId _refRecId,
        TransDate               _todayDate)
    {
        WorkPeriodTemplateCalculate workPeriodTemplateCalculate = WorkPeriodTemplateCalculateForward::construct();

        workPeriodTemplateCalculate.parmWorkPeriodTemplate(WorkPeriodTemplate::find(_refRecId));
        workPeriodTemplateCalculate.parmTodayDate(_todayDate);

        workPeriodTemplateCalculate.init();

        return workPeriodTemplateCalculate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>