<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResProductDimValueGeneration</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>EcoResProductDimValueGeneration</c> class creates the list of product dimension values that
///    exist.
/// </summary>
class EcoResProductDimValueGeneration
{
    EcoResTmpProductDimValue        ecoResTmpProductDimValue;
    EcoResProductVariantDimValue    ecoResProductVariantDimValue;
    RefRecId                        product;
    ItemId                          itemId;
    Set                             dimValuesSet;
    BOMId                           bomId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>fillTable</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Populates the temporary <c>EcoResTmpProductDimValue</c> table with the product dimension values.
    /// </summary>
    protected void fillTable()
    {
        List productDimValuesList;

        ttsbegin;

        if (dimValuesSet)
        {
            productDimValuesList = ecoResProductVariantDimValue.getDimValues(product, dimValuesSet);
        }
        else
        {
            productDimValuesList = ecoResProductVariantDimValue.getDimValues(product, null);
        }

        if (productDimValuesList)
        {
            ListEnumerator productDimValuesEnumerator = productDimValuesList.getEnumerator();

            while (productDimValuesEnumerator.moveNext())
            {
                ecoResTmpProductDimValue.clear();

                ecoResTmpProductDimValue.Name = any2str(productDimValuesEnumerator.current());

                if  (this.parmItemId())
                {
                    ecoResTmpProductDimValue.ItemId = this.parmItemId();
                }
        
                switch (ecoResProductVariantDimValue.parmDimFieldId())
                {
                    case fieldNum(InventDim, InventColorId):
                        ecoResTmpProductDimValue.RetailDisplayOrder = EcoResProductMasterColor::find(product, EcoResColor::findByName(ecoResTmpProductDimValue.Name).RecId).RetailDisplayOrder;
                        break;
                    case fieldNum(InventDim, InventSizeId):
                        ecoResTmpProductDimValue.RetailDisplayOrder = EcoResProductMasterSize::find(product, EcoResSize::findByName(ecoResTmpProductDimValue.Name).RecId).RetailDisplayOrder;
                        break;
                    case fieldNum(InventDim, InventStyleId):
                        ecoResTmpProductDimValue.RetailDisplayOrder = EcoResProductMasterStyle::find(product, EcoResStyle::findByName(ecoResTmpProductDimValue.Name).RecId).RetailDisplayOrder;
                        break;
                    case fieldNum(InventDim, ConfigId):
                        ecoResTmpProductDimValue.RetailDisplayOrder = EcoResProductMasterConfiguration::find(product, EcoResConfiguration::findByName(ecoResTmpProductDimValue.Name).RecId).RetailDisplayOrder;
                        break;
                    case fieldNum(InventDim, InventVersionId):
                        ecoResTmpProductDimValue.RetailDisplayOrder = EcoResProductMasterVersion::find(product, EcoResVersion::findByName(ecoResTmpProductDimValue.Name).RecId).RetailDisplayOrder;
                        break;
                }

                ecoResTmpProductDimValue.insert();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEcoResProductVariantDimValue</Name>
				<Source><![CDATA[
        public EcoResProductVariantDimValue parmEcoResProductVariantDimValue(EcoResProductVariantDimValue _ecoResProductVariantDimValue = ecoResProductVariantDimValue)
    {
        ecoResProductVariantDimValue = _ecoResProductVariantDimValue;

        return ecoResProductVariantDimValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmEcoResTmpProductDimValue</Name>
				<Source><![CDATA[
        public EcoResTmpProductDimValue parmEcoResTmpProductDimValue(EcoResTmpProductDimValue _ecoResTmpProductDimValue = ecoResTmpProductDimValue)
    {
        ecoResTmpProductDimValue = _ecoResTmpProductDimValue;

        return ecoResTmpProductDimValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmItemId</Name>
				<Source><![CDATA[
        public ItemId parmItemId(ItemId _itemId = itemId)
    {
        itemId = _itemId;

        return itemId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProduct</Name>
				<Source><![CDATA[
        public RefRecId parmProduct(RefRecId _product = product)
    {
        product = _product;

        return product;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSet</Name>
				<Source><![CDATA[
        public Set parmSet(Set _set = dimValuesSet)
    {
        dimValuesSet = _set;

        return dimValuesSet;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static EcoResProductDimValueGeneration construct()
    {
        return new EcoResProductDimValueGeneration();
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateFromForm</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Populates the <c>EcoResTmpProductDimValue</c> table with the product dimension value records.
    /// </summary>
    /// <param name="_ecoResTmpProductDimValue">
    ///     The ID of the <c>EcoResTmpProductDimValue</c> table.
    /// </param>
    /// <param name="_ecoResProductVariantDimValue">
    ///     The specifed instance of the <c>EcoResProductVariantDimValue</c> class.
    /// </param>
    /// <param name="_product">
    ///     The ID of the <c>EcoResProduct</c> record.
    /// </param>
    /// <param name="_itemId">
    ///     The <c>ItemId</c> record.
    /// </param>
    /// <param name="_dimValuesSet">
    ///     The <c>Set</c> contains the product variant dimension values.
    /// </param>
    public static void generateFromForm(
        EcoResTmpProductDimValue        _ecoResTmpProductDimValue,
        EcoResProductVariantDimValue    _ecoResProductVariantDimValue,
        RefRecId                        _product,
        ItemId                          _itemId         = '',
        Set                             _dimValuesSet   = null)
    {
        EcoResProductDimValueGeneration ecoResProductDimValueGeneration = EcoResProductDimValueGeneration::construct();

        ecoResProductDimValueGeneration.parmEcoResTmpProductDimValue(_ecoResTmpProductDimValue);
        ecoResProductDimValueGeneration.parmEcoResProductVariantDimValue(_ecoResProductVariantDimValue);
        ecoResProductDimValueGeneration.parmProduct(_product);
        ecoResProductDimValueGeneration.parmItemId(_itemId);
        ecoResProductDimValueGeneration.parmSet(_dimValuesSet);

        ecoResProductDimValueGeneration.fillTable();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>