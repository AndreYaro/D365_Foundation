<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AddFieldProviderEventHandler_AppSuite</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///  Event handler for class <c>AddFieldProvider</c>
/// </summary>
public final class AddFieldProviderEventHandler_AppSuite
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>AddFieldProvider_Post_setSelectedFields</Name>
				<Source><![CDATA[
    /// <summary>
    ///	  Show warning message when dimension fields are added during form personalization.
    /// </summary>
    /// <param name="args"> An object of <c>XppPrePostArgs</c>.</param>
    [PostHandlerFor(classStr(AddFieldProvider), methodStr(AddFieldProvider, setSelectedFields))]
    public static void AddFieldProvider_Post_setSelectedFields(XppPrePostArgs args)
    {
        const str addedFieldsArgName = '_addedFields';
        container addedFields = args.getArg(addedFieldsArgName);

        if (addedFields)
        {
            Set     dimFieldSet= InventDim::dimFieldList2Set(InventDim::dimFieldsList());
            TableId inventDimTableId = tableNum(InventDim);

            for (int i = 1; i <= conLen(addedFields); i++)
            {
                FormControlFieldSelector addedField;
                [addedField] = conPeek(addedFields, i);

                if (addedField.DataSourceTableId == inventDimTableId && dimFieldSet.in(fieldExt2Id(addedField.FieldId)))
                {
                    warning("@SCM:WarningOfAddDimensionFieldDuringFormPersonalization");
                    break;
                }

            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>