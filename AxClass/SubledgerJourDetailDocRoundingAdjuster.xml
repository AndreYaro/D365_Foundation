<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SubledgerJourDetailDocRoundingAdjuster</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SubledgerJourDetailDocRoundingAdjuster</c> class is responsible for asking all posting type rounding
/// adjusters to performing rounding adjustments for specific uptake documents.
/// </summary>
[
    SourceDocumentEnumExtensionAttribute(enumStr(SourceDocument_CustomerInvoice), SourceDocument_CustomerInvoice::CustomerFreeTextInvoice),
    SourceDocumentEnumExtensionAttribute(enumStr(SourceDocument_VendorInvoice), SourceDocument_VendorInvoice::VendorInvoice)
]
public class SubledgerJourDetailDocRoundingAdjuster extends SubledgerJourDetailRoundingAdjuster
{
    List postingTypeAdjusters;
    boolean postingTypeAdjustersListInitialized;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>adjustRoundingPerPostingTypeAdjuster</Name>
				<Source><![CDATA[
    private void adjustRoundingPerPostingTypeAdjuster(
        SubledgerJournalAccountEntryTmpDetail _tmpDetail,
        SubledgerJourIDetailRoundingAdjustEntry _roundingAdjustmentEntry)
    {
        ListEnumerator postingTypeAdjustersEnumerator;
        SubledgerJourDetailPostTypeRoundingAdj postingTypeRoundingAdjuster;

        postingTypeAdjustersEnumerator = postingTypeAdjusters.getEnumerator();

        while (postingTypeAdjustersEnumerator.moveNext())
        {
            postingTypeRoundingAdjuster = postingTypeAdjustersEnumerator.current();
            postingTypeRoundingAdjuster.performAdjustment(_tmpDetail, _roundingAdjustmentEntry);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializePostingTypeAdjustersIfNotSet</Name>
				<Source><![CDATA[
    private void initializePostingTypeAdjustersIfNotSet()
    {
        if (!postingTypeAdjustersListInitialized)
        {
            postingTypeAdjusters = SubledgerJourDetailPostTypeRoundingAdj::getAllPostingTypeRoundingAdjusters();
            postingTypeAdjustersListInitialized = true;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();

        postingTypeAdjusters = new List(Types::Class);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performAdjustment</Name>
				<Source><![CDATA[
    public void performAdjustment(
        SubledgerJournalAccountEntryTmpDetail _tmpDetail,
        SubledgerJourIDetailRoundingAdjustEntry _roundingAdjustmentEntry)
    {
        this.initializePostingTypeAdjustersIfNotSet();

        if (postingTypeAdjusters.elements() > 0)
        {
            this.adjustRoundingPerPostingTypeAdjuster(_tmpDetail, _roundingAdjustmentEntry);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>