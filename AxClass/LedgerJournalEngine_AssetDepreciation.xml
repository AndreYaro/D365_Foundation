<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalEngine_AssetDepreciation</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>LedgerJournalEngine_AssetDepreciation</c> class supports fixed assets depreciation journals.
/// </summary>
/// <remarks>
///    This class extends <see cref="T:LedgerJournalEngine" />.
/// </remarks>
// Fixed Assets
class LedgerJournalEngine_AssetDepreciation extends LedgerJournalEngine
    implements AssetIsPriorYearInterface
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>amountCurCreditModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adjusts a <c>LedgerJournalTrans</c> record and the <c>LedgerJournalTrans_Project</c> fields in
    /// response to a change in the <c>AmountCurCredit</c> field.
    /// </summary>
    /// <param name="_ledgerJournalTrans">
    /// The <c>LedgerJournalTrans</c> record that is modified.
    /// </param>
    /// <param name="_ledgerJournalTransProj">
    /// The <c>LedgerJournalTrans_Project</c> record that is modified; optional.
    /// </param>
    /// <param name="_ledgerJournalTrans_RCash">
    /// The <c>LedgerJournalTrans_RCash</c> record that is modified; optional.
    /// </param>
    public void amountCurCreditModified(LedgerJournalTrans         _ledgerJournalTrans,
                                        LedgerJournalTrans_Project _ledgerJournalTransProj   = null,
                                        LedgerJournalTrans_RCash   _ledgerJournalTrans_RCash = null)
    {
        super(_ledgerJournalTrans, _ledgerJournalTransProj, _ledgerJournalTrans_RCash);
    }

]]></Source>
			</Method>
			<Method>
				<Name>amountCurDebitModified</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adjusts a <c>LedgerJournalTrans</c> record and the <c>LedgerJournalTrans_Project</c> fields in
    /// response to a change in the <c>AmountCurDebit</c> value.
    /// </summary>
    /// <param name="_ledgerJournalTrans">
    /// The <c>LedgerJournalTrans</c> record that is modified.
    /// </param>
    /// <param name="_ledgerJournalTransProj">
    /// The <c>LedgerJournalTrans_Project</c> record that is modified; optional.
    /// </param>
    /// <param name="_ledgerJournalTrans_RCash">
    /// The <c>LedgerJournalTrans_RCash</c> record that is modified; optional.
    /// </param>
    public void amountCurDebitModified(LedgerJournalTrans         _ledgerJournalTrans,
                                       LedgerJournalTrans_Project _ledgerJournalTransProj   = null,
                                       LedgerJournalTrans_RCash   _ledgerJournalTrans_RCash = null)
    {
        super(_ledgerJournalTrans, _ledgerJournalTransProj, _ledgerJournalTrans_RCash);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValue</Name>
				<Source><![CDATA[
    public void initValue(LedgerJournalTrans _ledgerJournalTrans, LedgerJournalTrans_Asset _ledgerJournalTrans_Asset = null)
    {
        super(_ledgerJournalTrans);

        _ledgerJournalTrans.CurrencyCode      = Ledger::accountingCurrency(CompanyInfo::current());

        this.initOffsetFromLedgerJournalTable(_ledgerJournalTrans);

        _ledgerJournalTrans.AccountType       = LedgerJournalACType::FixedAssets;
        _ledgerJournalTrans.TransactionType   = LedgerTransType::FixedAssets;
        _ledgerJournalTrans.Approved          = NoYes::Yes;
        _ledgerJournalTrans.Approver          = HcmWorker::userId2Worker(curuserid());
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyIsPriorYear</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void applyIsPriorYear(LedgerJournalTrans _ledgerJournalTrans, LedgerJournalTrans_Asset _ledgerJournalTrans_Asset)
    {
        if (SysCountryRegionCode::isLegalEntityInCountryRegion([#isoJP])
            && (_ledgerJournalTrans_Asset.TransType == AssetTransTypeJournal::Depreciation
                || _ledgerJournalTrans_Asset.TransType == AssetTransTypeJournal::DepreciationAdj )
            && _ledgerJournalTrans_Asset.isSplitTransaction())
        {
            // Get current data from the database
            LedgerJournalTrans_Asset ledgerJournalTrans_Asset;
            select firstonly ledgerJournalTrans_Asset
                where ledgerJournalTrans_Asset.RecId == _ledgerJournalTrans_Asset.RecId;

            AssetDepreciationPeriod assetDepreciationPeriod = AssetDepreciationPeriod::constructFromLedgerJournalTransAsset(ledgerJournalTrans_Asset);
            NoYes isPriorYear = assetDepreciationPeriod.determineIsPriorPeriod(_ledgerJournalTrans.TransDate);

            // When the user changes the transaction date on a single journal line, the LedgerJournalEngine_Server
            // will automatically change the transaction date on the other journal lines in the same voucher.  Therefore,
            // we need to update the isPriorYear field on all journal lines in the same voucher that are related to the 
            // same period.  We are specifically not using the transaction date in the following query because they can be 
            // different dates at this point.
            if (isPriorYear != ledgerJournalTrans_Asset.IsPriorYear)
            {
                LedgerJournalTrans ledgerJournalTrans;

                update_recordset ledgerJournalTrans_Asset
                    setting IsPriorYear = isPriorYear
                    join ledgerJournalTrans
                    where ledgerJournalTrans_Asset.RefRecId == ledgerJournalTrans.RecId
                        && (ledgerJournalTrans_Asset.TransType == AssetTransTypeJournal::Depreciation
                        || ledgerJournalTrans_Asset.TransType == AssetTransTypeJournal::DepreciationAdj)
                        && ledgerJournalTrans_Asset.PeriodFromDate == _ledgerJournalTrans_Asset.PeriodFromDate
                        && ledgerJournalTrans_Asset.PeriodToDate == _ledgerJournalTrans_Asset.PeriodToDate
                        && ledgerJournalTrans.JournalNum == _ledgerJournalTrans.JournalNum
                        && ledgerJournalTrans.Voucher == _ledgerJournalTrans.Voucher;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>