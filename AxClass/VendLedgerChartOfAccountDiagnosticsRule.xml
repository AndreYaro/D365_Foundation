<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendLedgerChartOfAccountDiagnosticsRule</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>VendLedgerChartOfAccountDiagnosticsRule</c> class is used to check whether vendor posting profiles are set up correctly.
/// </summary>
[DiagnosticsRuleAttribute]
public final class VendLedgerChartOfAccountDiagnosticsRule implements IDiagnosticsRule
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates whether vendor posting profiles are set up correctly.
    /// </summary>
    /// <returns>
    /// An instance of <c>DiagnosticsRuleValidationResult</c> which contains the validation result.
    /// </returns>
    [DiagnosticsRuleSubscriptionAttribute(DiagnosticsArea::Financial, "@AccountsPayable:VendLedgerChartOfAccountRuleName", DiagnosticsRunFrequency::Monthly, "@AccountsPayable:VendLedgerChartOfAccountRuleDescription")]
    public DiagnosticsRuleValidationResult validate()
    {
        DiagnosticsRuleValidationResult validationResult = DiagnosticsRuleValidationResult::construct();
        DimensionAttributeValueCombination  davc;
        MainAccount             mainAccount;
        Ledger                  ledger;
        LedgerChartOfAccounts   coaLedger;
        VendLedgerAccounts      vendLedgerAccounts;
        CompanyInfo             companyInfo;
        Map mainAccounts = new Map(Types::Int64, Types::Container);
        DataArea                dataArea;

        while select id from dataArea
        {
            changecompany(dataArea.id)
            {
                vendLedgerAccounts.clear();
                davc.clear();
                mainAccount.clear();
                companyInfo.clear();
                ledger.clear();
                coaLedger.clear();

                // Clearling ledger dimension check
                if (isConfigurationkeyEnabled(configurationKeyNum(CashFlowForecasting)))
                {
                    while select ClearingLedgerDimension, DataAreaId
                        from vendLedgerAccounts
                        join MainAccount from davc
                            where davc.RecId == vendLedgerAccounts.ClearingLedgerDimension
                        join MainAccountId, LedgerChartOfAccounts from mainAccount
                            where mainAccount.RecId == davc.MainAccount
                        join DataArea from companyInfo
                            where companyInfo.DataArea == vendLedgerAccounts.DataAreaId
                        Join PrimaryForLegalEntity, ChartOfAccounts from ledger
                            where ledger.PrimaryForLegalEntity == companyInfo.RecId
                        join Name from coaLedger
                            where coaLedger.RecId == ledger.ChartOfAccounts
                            && mainAccount.LedgerChartOfAccounts != ledger.ChartOfAccounts
                    {
                        if (!mainAccounts.exists(mainAccount.RecId))
                        {
                            mainAccounts.insert(mainAccount.RecId, ["@SYS3286", coaLedger.Name, mainAccount.MainAccountId]);
                        }
                    }
                }

                // Purchase ledger dimension check
                while select PurchasingLedgerDimension, DataAreaId
                    from vendLedgerAccounts
                    join MainAccount from davc
                        where davc.RecId == vendLedgerAccounts.PurchasingLedgerDimension
                    join MainAccountId, LedgerChartOfAccounts from mainAccount
                        where mainAccount.RecId == davc.MainAccount
                    join DataArea from companyInfo
                        where companyInfo.DataArea == vendLedgerAccounts.DataAreaId
                    Join PrimaryForLegalEntity, ChartOfAccounts from ledger
                        where ledger.PrimaryForLegalEntity == companyInfo.RecId
                    join Name from coaLedger
                        where coaLedger.RecId == ledger.ChartOfAccounts
                        && mainAccount.LedgerChartOfAccounts != ledger.ChartOfAccounts
                {
                    if (!mainAccounts.exists(mainAccount.RecId))
                    {
                        mainAccounts.insert(mainAccount.RecId, ["@SYS67404", coaLedger.Name, mainAccount.MainAccountId]);
                    }
                }

                // Purchase offset ledger dimension check
                while select PurchasingOffsetLedgerDimension, DataAreaId
                    from vendLedgerAccounts
                    join MainAccount from davc
                        where davc.RecId == vendLedgerAccounts.PurchasingOffsetLedgerDimension
                    join MainAccountId, LedgerChartOfAccounts from mainAccount
                        where mainAccount.RecId == davc.MainAccount
                    join DataArea from companyInfo
                        where companyInfo.DataArea == vendLedgerAccounts.DataAreaId
                    Join PrimaryForLegalEntity, ChartOfAccounts from ledger
                        where ledger.PrimaryForLegalEntity == companyInfo.RecId
                    join Name from coaLedger
                        where coaLedger.RecId == ledger.ChartOfAccounts
                        && mainAccount.LedgerChartOfAccounts != ledger.ChartOfAccounts
                {
                    if (!mainAccounts.exists(mainAccount.RecId))
                    {
                        mainAccounts.insert(mainAccount.RecId, ["@SYS3348", coaLedger.Name, mainAccount.MainAccountId]);
                    }
                }

                // Summary account ledger dimension check
                while select SummaryLedgerDimension, DataAreaId
                    from vendLedgerAccounts
                    join MainAccount from davc
                        where davc.RecId == vendLedgerAccounts.SummaryLedgerDimension
                    join MainAccountId, LedgerChartOfAccounts from mainAccount
                        where mainAccount.RecId == davc.MainAccount
                    join DataArea from companyInfo
                        where companyInfo.DataArea == vendLedgerAccounts.DataAreaId
                    Join PrimaryForLegalEntity, ChartOfAccounts from ledger
                        where ledger.PrimaryForLegalEntity == companyInfo.RecId
                    join Name from coaLedger
                        where coaLedger.RecId == ledger.ChartOfAccounts
                        && mainAccount.LedgerChartOfAccounts != ledger.ChartOfAccounts
                {
                    if (!mainAccounts.exists(mainAccount.RecId))
                    {
                        mainAccounts.insert(mainAccount.RecId, ["@SYS11733", coaLedger.Name, mainAccount.MainAccountId]);
                    }
                }

                // Sales tax prepayments ledger dimension check
                while select VATPrepaymentsLedgerDimension, DataAreaId
                    from vendLedgerAccounts
                    join MainAccount from davc
                        where davc.RecId == vendLedgerAccounts.VATPrepaymentsLedgerDimension
                    join MainAccountId, LedgerChartOfAccounts from mainAccount
                        where mainAccount.RecId == davc.MainAccount
                    join DataArea from companyInfo
                        where companyInfo.DataArea == vendLedgerAccounts.DataAreaId
                    Join PrimaryForLegalEntity, ChartOfAccounts from ledger
                        where ledger.PrimaryForLegalEntity == companyInfo.RecId
                    join Name from coaLedger
                        where coaLedger.RecId == ledger.ChartOfAccounts
                        && mainAccount.LedgerChartOfAccounts != ledger.ChartOfAccounts
                {
                    if (!mainAccounts.exists(mainAccount.RecId))
                    {
                        mainAccounts.insert(mainAccount.RecId, ["@SYS68656", coaLedger.Name, mainAccount.MainAccountId]);
                    }
                }
            }
        }

        if (mainAccounts.elements())
        {
            validationResult.addValidationResult("@AccountsPayable:VendLedgerChartOfAccountRuleMessage", DiagnosticsValidationSeverity::High, "@AccountsPayable:VendLedgerChartOfAccountRuleRecommendation");
            MapEnumerator mainAccountEnumerator = mainAccounts.getEnumerator();

            while (mainAccountEnumerator.moveNext())
            {
                str coaName, mainAccountId, accountType;

                [accountType, coaName, mainAccountId] = mainAccountEnumerator.currentValue();
                validationResult.addValidationDetail("@SYS342312", accountType);
                validationResult.addValidationDetail("@SYS40787", coaName);
                validationResult.addValidationDetail("@SYS129344", mainAccountId);
                validationResult.addValidationDetail("@SYS3676", mainAccountEnumerator.currentKey());
            }
        }

        return validationResult;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>