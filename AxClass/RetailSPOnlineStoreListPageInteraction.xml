<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailSPOnlineStoreListPageInteraction</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class <c>RetailSPOnlineStoreListPageInteraction</c> is used to Update the User Control
/// in <c>RetailOnlineChannel</c> based on the selected record.
/// </summary>
class RetailSPOnlineStoreListPageInteraction extends ListPageInteraction
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>selectionChanged</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieve the currently selected record when a record is selected on the List Page
    /// </summary>
    public void selectionChanged()
    {
        RetailOnlineChannelTable onlineChannel;

        super();

        // Retrieve the currently selected record
        onlineChannel = this.listPage().activeRecord(
            queryDataSourceStr(RetailSPOnlineStoreListPage, RetailOnlineChannelTable));

        this.updateUI(onlineChannel);
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateUI</Name>
				<Source><![CDATA[
    private void updateUI(RetailOnlineChannelTable _onlineChannel)
    {
        boolean canPublish;
        RetailOnlineChannelPublishStatus publishStatus;

        publishStatus = RetailOnlineChannelPublishStatus::findByChannel(_onlineChannel.RecId);
        EcoResCategoryHierarchyId channelCategoryHierarchyId = RetailChannelTable::findCategoryHierarchyByChannel(_onlineChannel.RecId);

        canPublish = channelCategoryHierarchyId
                        && publishStatus.canTransiteTo(RetailOnlineChannelPublishStatusType::InProgress);

        this.listPage().actionPaneControlEnabled(
            formControlStr(RetailSPOnlineStoreDetailPage, MapAttributesButton),
            channelCategoryHierarchyId > 0);

        this.listPage().actionPaneControlEnabled(
            formControlStr(RetailSPOnlineStoreDetailPage, PublishButton),
            canPublish);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>