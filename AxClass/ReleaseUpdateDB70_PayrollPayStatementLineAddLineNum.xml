<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReleaseUpdateDB70_PayrollPayStatementLineAddLineNum</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Upgrade script for PayrollPayStatementLine schema changes.
/// </summary>
class ReleaseUpdateDB70_PayrollPayStatementLineAddLineNum extends ReleaseUpdateDB
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>moduleName</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the module name of an upgrade class.
    /// </summary>
    /// <returns>
    ///    The module name.
    /// </returns>
    /// <remarks>
    ///    This method must be implemented by sub classes.The module name is displayed on the cockpit form for
    ///    every script.
    /// </remarks>
    public ReleaseUpdateModule moduleName()
    {
        return "@Payroll:Payroll";
    }

]]></Source>
			</Method>
			<Method>
				<Name>myVersion</Name>
				<Source><![CDATA[
    ///<summary>
    ///The script runs in any system having version CTP8.
    ///</summary>
    ///<returns>Script version.</returns>
    public sysReleasedVersion myVersion()
    {
        return sysReleasedVersion::R1Update3;
    }

]]></Source>
			</Method>
			<Method>
				<Name>preSyncPayrollPayStatementLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Disables the unique index for the <c>PayrollPayStatementLine</c> table.
    /// </summary>
    [
        UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_PayrollPayStatementLineAddLineNum)),
        UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PreSyncUpdate),
        UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::StartScript),
        UpgradeScriptConfigKeyAttribute(configurationKeyStr(PayrollAdministration)),
        UpgradeScriptTableAttribute('PayrollPayStatementLine', false, false, false, false)
    , Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void preSyncPayrollPayStatementLine()
    {
        ReleaseUpdateDBSyncManager syncManager = ReleaseUpdateDBSyncManager::Construct();

        DictTable payStatementLine = new DictTable(tableNum(PayrollPayStatementLine));
        DictIndex lineNumIndex = new DictIndex(tableNum(PayrollPayStatementLine), indexNum(PayrollPayStatementLine, LineNumIdx));

        syncManager.IgnoreIndexSynchronization(payStatementLine, lineNumIndex);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postSyncUpdatePayrollPayStatementLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the values of the <c>LineNum</c> field in the <c>PayrollPayStatementLine</c> table.
    /// </summary>
    [
    UpgradeScriptDescriptionAttribute(classStr(ReleaseUpdateDB70_PayrollPayStatementLineAddLineNum)),
    UpgradeScriptStageAttribute(ReleaseUpdateScriptStage::PostSyncUpdate),
    UpgradeScriptTypeAttribute(ReleaseUpdateScriptType::SharedScript),
    UpgradeScriptConfigKeyAttribute(configurationKeyStr(PayrollAdministration)),
    UpgradeScriptTableAttribute(tableStr(PayrollPayStatementLine), false, true, true, false)
    , Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public void postSyncUpdatePayrollPayStatementLine()
    {
        SysDictTable payStatementLineTable = new SysDictTable(tableNum(PayrollPayStatementLine));

        str payStatementLineTableName = payStatementLineTable.name(DbBackend::Sql);
        str payStatementFieldName = payStatementLineTable.fieldName(fieldNum(PayrollPayStatementLine, PayStatement), DbBackend::Sql);
        str lineNumFieldName = payStatementLineTable.fieldName(fieldNum(PayrollPayStatementLine, LineNum), DbBackend::Sql);
        str updateLineNumSql = @"UPDATE [dbo].[%1]
                                SET [%2] = PAYLINETEMP.[ROWNUMBER]
                             FROM [dbo].[%1]
                             INNER JOIN
                                (SELECT RECID, ROW_NUMBER() OVER(PARTITION BY [%3]
                                    ORDER BY [%3]) AS ROWNUMBER
                                FROM [dbo].[%1] )
                             AS PAYLINETEMP
                                ON [dbo].[%1].[RECID] = PAYLINETEMP.[RECID]";        

        str sqlStatement = strFmt(updateLineNumSql, payStatementLineTableName, lineNumFieldName, payStatementFieldName);

        this.executeSqlStatement(sqlStatement);
    }

]]></Source>
			</Method>
			<Method>
				<Name>executeSqlStatement</Name>
				<Source><![CDATA[
    private void executeSqlStatement(str _sqlStatement)
    {
        Connection sessionConn = new Connection();

        Statement statement = sessionConn.createStatement();

        new SqlStatementExecutePermission(_sqlStatement).assert();
        statement.executeUpdate(_sqlStatement);

        CodeAccessPermission::revertAssert();

        statement.close();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>