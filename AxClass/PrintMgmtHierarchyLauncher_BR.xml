<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PrintMgmtHierarchyLauncher_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PrintMgmtHierarchyLauncher_BR</c> class centralize the logic necessary to launch
/// the Print management UI for the Bank hierarchy.
/// </summary>
/// <remarks>
/// To avoid logic in the forms and to allow Print management UI be opened by menu items
/// this class holds the logic to prepare and open the Print management UI
/// </remarks>
class PrintMgmtHierarchyLauncher_BR
{
    PrintMgmtSetupContext   setupContext;
    PrintMgmtNodeType       printMgmtNodeType;
    object                  caller;
    Common                  currentRecord;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of a given <c>Args</c> object.
    /// </summary>
    /// <param name="_args">
    /// <c>Args</c> object with proper <c>PrintMgmtNodeType</c> as enum value and an optinal
    /// record to have it's print management settings configured for.
    /// </param>
    void new(Args _args)
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR00016, funcName());

        printMgmtNodeType   = _args.parmEnum();
        caller              = _args.caller() ? _args.caller() : this;
        currentRecord       = _args.record();
    }

]]></Source>
			</Method>
			<Method>
				<Name>printMgmtLaunchSetup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Calls the method that launches the <c>PrintMgmtSetup</c> form.
    /// </summary>
    /// <param name="_setupContext">
    /// The information to display.
    /// </param>
    public void printMgmtLaunchSetup(PrintMgmtSetupContext _setupContext)
    {
        PrintMgmt::launchSetup(_setupContext);
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Launches print management setup form.
    /// </summary>
    public void run()
    {
        PrintMgmtHierarchyType hierarchyType;

        switch (printMgmtNodeType)
        {
            case PrintMgmtNodeType::Bank:
            case PrintMgmtNodeType::BankAccountTable:
                GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR00063, funcName());
                hierarchyType = PrintMgmtHierarchyType::Bank;
                break;

            case PrintMgmtNodeType::Ledger:
                hierarchyType = PrintMgmtHierarchyType::Ledger;
                break;
        }

        PrintMgmt::createDefaultData(hierarchyType);

        setupContext = new PrintMgmtSetupContext();
        setupContext.addHierarchyContext(hierarchyType, printMgmtNodeType);
        setupContext.parmCaller(caller);
        setupContext.parmReferencedTableBuffer(currentRecord);

        this.PrintMgmtLaunchSetup(setupContext);
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
        public static void main(Args args)
    {
        PrintMgmtHierarchyLauncher_BR   printMgmtHierarchyLauncher;
        ;

        if (!BrazilParameters::isEnabled())
        {
            throw error("@GLS50786");
        }

        printMgmtHierarchyLauncher = new PrintMgmtHierarchyLauncher_BR(args);

        printMgmtHierarchyLauncher.run();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>