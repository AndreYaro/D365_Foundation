<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableForm_INTax_EventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchTableForm_INTax_EventHandler</c> class is the event handler of form <c>PurchTable</c> for IN tax.
/// </summary>
internal class PurchTableForm_INTax_EventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>tcsGroup_OnValidating</Name>
				<Source><![CDATA[
    [FormDataFieldEventHandler(formDataFieldStr(PurchTable, PurchLine_IN, TCSGroup), FormDataFieldEventType::Validating)]
    public static void tcsGroup_OnValidating(FormDataObject _sender, FormDataFieldEventArgs _args)
    {
        if (TaxWithholdParameters_IN::isTaxWithholdingCreditNoteTaxGroupDefaultBlank())
        {
            PurchTableForm_INTax_EventHandler::validateCreditNoteTDSTCSGroup(
                _sender,
                _args,
                TaxWithholdType_IN::TCS);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>tdsGroup_OnValidating</Name>
				<Source><![CDATA[
    [FormDataFieldEventHandler(formDataFieldStr(PurchTable, PurchLine_IN, TDSGroup), FormDataFieldEventType::Validating)]
    public static void tdsGroup_OnValidating(FormDataObject _sender, FormDataFieldEventArgs _args)
    {
        if (TaxWithholdParameters_IN::isTaxWithholdingCreditNoteTaxGroupDefaultBlank())
        {
            PurchTableForm_INTax_EventHandler::validateCreditNoteTDSTCSGroup(
                _sender,
                _args,
                TaxWithholdType_IN::TDS);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCreditNoteTDSTCSGroup</Name>
				<Source><![CDATA[
    private static void validateCreditNoteTDSTCSGroup(FormDataObject _sender, FormDataFieldEventArgs _args, TaxWithholdType_IN _taxType)
    {
        var args = _args as FormDataFieldCancelEventArgs;
        FormRun formRun = _sender.datasource().formRun();
        PurchLine purchLine = formRun.dataSource(formDataSourceStr(PurchTable, PurchLine)).cursor();

        if (TaxWithholdParameters_IN::isTaxWithholdingCreditNoteTaxGroupDefaultBlank()
            && purchLine.isCreditNoteLineWithRefId())
        {
            PurchLine_IN purchLine_IN = formRun.dataSource(formDataSourceStr(PurchTable, PurchLine_IN)).cursor();
            PurchLine_IN originPurchLine_IN = PurchLine::findInventTransId(purchLine.InventRefTransId).purchLine_IN();

            if (originPurchLine_IN
                && ((purchLine_IN.TCSGroup
                        && _taxType == TaxWithholdType_IN::TCS
                        && purchLine_IN.TCSGroup != originPurchLine_IN.TCSGroup)
                    || (purchLine_IN.TDSGroup
                        && _taxType == TaxWithholdType_IN::TDS
                        && purchLine_IN.TDSGroup != originPurchLine_IN.TDSGroup)))
            {
                args.cancel(true);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>