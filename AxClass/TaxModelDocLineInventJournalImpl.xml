<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxModelDocLineInventJournalImpl</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The tax model class for <c>InventJournalTrans</c>.
/// </summary>
[TaxModelDocumentLineAttribute(tableStr(InventJournalTrans))]
public class TaxModelDocLineInventJournalImpl extends TaxModelDocLineBaseImpl
{
    InventJournalTrans      inventJournalTrans;
    InventJournalTable      inventJournalTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssessableValueTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the assessable value of the document line.
    /// </summary>
    /// <returns>
    /// The assessable value.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public AmountCur getAssessableValueTransactionCurrency()
    {
        return 0.0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getConsumptionStateId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the comsumption state id of the transaction line.
    /// </summary>
    /// <returns>
    /// The transaction line consumption state id.
    /// </returns>
    public LogisticsAddressStateId getConsumptionStateId()
    {
        InventDim               inventDim;
        LogisticsLocationRecId  logisticsLocationRecId;
        LogisticsPostalAddress  toAddress;
        LogisticsAddressStateId stateId;

        inventDim               = InventDim::find(inventJournalTrans.ToInventDimId);
        logisticsLocationRecId  = TaxLogisticsLocationControllerHelper_IN::getDefaultLogisticsLocation(inventDim.InventLocationId, inventDim.InventSiteId);
        toAddress = LogisticsPostalAddress::findByLocation(logisticsLocationRecId);

        if (toAddress)
        {
            stateId = toAddress.State;
        }
        return stateId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>CustTable</c> based on the cust account on the transaction line.
    /// </summary>
    /// <returns>
    /// The <c>CustTable</c>
    /// </returns>
    public CustTable getCustTable()
    {
        return CustTable::find(ProjTable::find(inventJournalTrans.ProjId).CustAccount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMaximumRetailPriceTransactionCurrency</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the maximum retail price of the document line.
    /// </summary>
    /// <returns>
    /// The maximum retail price.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public MaximumRetailPrice_IN getMaximumRetailPriceTransactionCurrency()
    {
        return 0.0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getQuantity</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the quantity from the transaction line.
    /// </summary>
    /// <returns>
    ///    The quantity.
    /// </returns>
    /// <remarks>
    ///    If the transaction does not support quantities, 0 may be passed as the quantity. This will be used
    ///    to calculate taxes by unit.
    /// </remarks>
    public Qty getQuantity()
    {
        return inventJournalTrans.Qty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxDirection</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax direction.
    /// </summary>
    /// <returns>
    /// The tax direction
    /// </returns>
    public TaxDirection getTaxDirection()
    {
        if (this.getQuantity() >= 0)
        {
            return TaxDirection::IncomingTax;
        }
        else
        {
            return TaxDirection::OutgoingTax;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the tax group for the transaction line.
    /// </summary>
    /// <returns>
    ///   The tax group.
    /// </returns>
    public TaxGroup getTaxGroup()
    {
        return "";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxItemGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the item tax group for the transaction line.
    /// </summary>
    /// <returns>
    ///   The item tax group.
    /// </returns>
    public TaxItemGroup getTaxItemGroup()
    {
        return "";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxRateBasingDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax rate basing date for the given tax type.
    /// </summary>
    /// <param name="_taxType">
    /// The given tax type.
    /// </param>
    /// <returns>
    /// The tax rate date.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public TransDate getTaxRateBasingDate(TaxType_IN _taxType)
    {
        return inventJournalTrans.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTransactionType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets <c>TaxModelTransactionType_IN</c> of the transaction
    /// </summary>
    /// <returns>
    /// The <c>TaxModelTransactionType_IN</c> of the transaction
    /// </returns>
    public TaxModelTransactionType getTransactionType()
    {
        if (this.getQuantity() >= 0)
        {
            return TaxModelTransactionType::Normal;
        }
        else
        {
            return TaxModelTransactionType::Return;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Init the domain object.
    /// The method should only be called by the <c>TaxModelDocLineFactoryable_IN</c> classes.
    /// </summary>
    /// <param name="_documentLineMap">
    /// The document line buffer.
    /// </param>
    public void init(SalesPurchJournalLine _documentLineMap)
    {
        inventJournalTrans = _documentLineMap;
        inventJournalTable = inventJournalTrans.inventJournalTable();

        super(_documentLineMap);

        helper = new TaxModelDocLineHelper();
        helper.setDocumentLine(_documentLineMap);
        helper.setTaxModelTaxable(this);
        helper.setTransTaxInformation(transTaxInformation);
    }

]]></Source>
			</Method>
			<Method>
				<Name>IsInterStateTransaction</Name>
				<Source><![CDATA[
    // True if it is not inter state transaction; otherwise, false.
    // </returns>
    public NoYes IsInterStateTransaction()
    {
        InventDim               fromInventDim;
        InventDim               toInventDim;
        LogisticsLocationRecId  fromLogisticsLocationRecId;
        LogisticsLocationRecId  toLogisticsLocationRecId;
        LogisticsPostalAddress  fromAddress;
        LogisticsPostalAddress  toAddress;
        NoYes                   isInterState;

        isInterState = NoYes::No;

        fromInventDim = InventDim::find(inventJournalTrans.InventDimId);
        toInventDim = InventDim::find(inventJournalTrans.ToInventDimId);

        fromLogisticsLocationRecId = TaxLogisticsLocationControllerHelper_IN::getDefaultLogisticsLocation(fromInventDim.InventLocationId, fromInventDim.InventSiteId);
        toLogisticsLocationRecId = TaxLogisticsLocationControllerHelper_IN::getDefaultLogisticsLocation(toInventDim.InventLocationId, toInventDim.InventSiteId);
        fromAddress = LogisticsPostalAddress::findByLocation(fromLogisticsLocationRecId);
        toAddress = LogisticsPostalAddress::findByLocation(toLogisticsLocationRecId);

        if (fromAddress && toAddress
            && ((fromAddress.CountryRegionId != toAddress.CountryRegionId)
                || (fromAddress.CountryRegionId == toAddress.CountryRegionId
                    && fromAddress.State != ''
                    && toAddress.State != ''
                    && fromAddress.State != toAddress.State))
        )
        {
            isInterState = NoYes::Yes;
        }

        return isInterState;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldPostQuantityRegister</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if excise quantity register should be posted or not.
    /// </summary>
    /// <returns>
    /// True if the document line should post excise quantity register; otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public NoYes shouldPostQuantityRegister()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldPostTaxTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if tax transaction should be posted or not.
    /// </summary>
    /// <returns>
    /// True if the document line should post tax transaction; otherwise, false.
    /// </returns>
    public NoYes shouldPostTaxTrans()
    {
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isScrap</Name>
				<Source><![CDATA[
    /// <summary>
    /// To determine if the document line is scrap or not.
    /// </summary>
    /// <returns>
    /// True if the document line is scrap;otherwise, false.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public NoYes isScrap()
    {
        return NoYes::No;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxModuleType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax module type
    /// </summary>
    /// <returns>
    /// The transaction tax module type
    /// </returns>
    public TaxModuleType getTaxModuleType()
    {
        return TaxModuleType::Inventory_IN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getTaxLogisticsPostalAddress</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the tax <c>LogisticsPostalAddress</c> of this transaction document line.
    /// </summary>
    /// <returns>
    /// The tax <c>LogisticsPostalAddress</c>
    /// </returns>
    /// This method is used to init TransTaxInformation.CompanyLocation, which should be defaulted from transaction line delivery address
    public LogisticsPostalAddress getTaxLogisticsPostalAddress(TransTaxInformationType_IN _type = TransTaxInformationType_IN::Normal)
    {
        InventDim               inventDim;
        LogisticsLocationRecId  logisticsLocationRecId;

        inventDim               = InventDim::find(this.getInventDimId());
        logisticsLocationRecId  = TaxLogisticsLocationControllerHelper_IN::getDefaultLogisticsLocation(inventDim.InventLocationId, inventDim.InventSiteId);
         
        return LogisticsPostalAddress::findByLocation(logisticsLocationRecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnit</Name>
				<Source><![CDATA[
    public UnitOfMeasureSymbol getUnit()
    {
        if (inventJournalTrans.ProjUnitID)
        {
            return inventJournalTrans.ProjUnitID;
        }
        else
        {
            return inventJournalTrans.unitId();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentInvoiceDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction invoice date
    /// </summary>
    /// <returns>
    /// The transaction invoice date
    /// </returns>
    public InvoiceDate getDocumentInvoiceDate()
    {
        return inventJournalTrans.TransDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the Transaction document date
    /// </summary>
    /// <returns>
    /// The transaction document date
    /// </returns>
    public DocumentDate getDocumentDate()
    {
        return inventJournalTrans.TransDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>