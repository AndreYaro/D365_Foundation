<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetPackingSlipDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>AssetPackingSlipDP</c> class is the data provider for the <c>AssetPackingSlip</c> report.
/// </summary>
[
    SRSReportParameterAttribute(classStr(AssetPackingSlipContract)),
    SRSReportQueryAttribute(queryStr(AssetPackingSlip))
]
public class AssetPackingSlipDP extends SRSReportDataProviderPreProcessTempDB
{
    AssetPackingSlipTmp assetPackingSlipTmp;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAssetPackingSlipTmp</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Returns the <c>AssetPackingSlipTmp</c> table.
    /// </summary>
    /// <returns>
    ///     The <c>AssetPackingSlipTmp</c> table.
    /// </returns>
    [
        SRSReportDataSetAttribute(tableStr(AssetPackingSlipTmp))
    ]
    public AssetPackingSlipTmp getAssetPackingSlipTmp()
    {
        select assetPackingSlipTmp;
        return assetPackingSlipTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Populates the data required for the report.
    /// </summary>
    public void processReport()
    {
        AssetPackingSlipContract contract = this.parmDataContract() as AssetPackingSlipContract;
        QueryRun queryRun = new QueryRun(this.parmQuery());

        AssetTable assetTable;
        LtAssetPackingSlipLine packingSlipLine;
        RecordInsertList recordInsertList = new RecordInsertList(
            tableNum(AssetPackingSlipTmp),
            true,
            true,
            true,
            false,
            true,
            assetPackingSlipTmp);

        while (queryRun.next())
        {
            assetTable = queryRun.get(tableNum(AssetTable)) as AssetTable;
            packingSlipLine = queryRun.get(tableNum(LtAssetPackingSlipLine)) as LtAssetPackingSlipLine;

            assetPackingSlipTmp.clear();
            assetPackingSlipTmp.AssetId = packingSlipLine.AssetId;
            assetPackingSlipTmp.Description = packingSlipLine.Description;
            assetPackingSlipTmp.DepartmentFrom = OMOperatingUnit::find(packingSlipLine.DepartmentFromId, OMOperatingUnitType::OMDepartment).Name;
            assetPackingSlipTmp.Quantity = assetTable.Quantity;
            assetPackingSlipTmp.UnitOfMeasure = assetTable.UnitOfMeasure;
            recordInsertList.add(assetPackingSlipTmp);
        }

        recordInsertList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>