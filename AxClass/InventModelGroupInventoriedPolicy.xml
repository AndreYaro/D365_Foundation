<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventModelGroupInventoriedPolicy</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventModelGroupInventoriedPolicy</c> class checks modifications to the inventoried policy
///    on the <c>InventModelGroup</c> table and specified by the <c>StockedProduct</c> field.
/// </summary>
public class InventModelGroupInventoriedPolicy extends InventInventoriedPolicy
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateFieldValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates that the <c>StockedItem</c> and <c>AlwaysExpensed</c> fields are not modified if the
    ///    <c>InventModelGroup</c> buffer is being used.
    /// </summary>
    /// <param name="_fieldName">
    ///    The field to validate.
    /// </param>
    /// <param name="_arrayIndex">
    ///    The array index of the field to validate; optional.
    /// </param>
    /// <returns>
    ///    true if the policy can be changed for the <c>InventModelGroup</c> buffer; otherwise, false.
    /// </returns>
    public boolean validateFieldValue(FieldName _fieldName, int _arrayIndex = 1)
    {
        boolean ret = true;

        switch (_fieldName)
        {
            case fieldStr(InventModelGroup, StockedProduct):
                if (inventModelGroup.StockedProduct != inventModelGroup.orig().StockedProduct)
                {
                    ret = InventModelGroupInventoriedPolicy::checkCanStockedProductBeModified(inventModelGroup);
                }
                break;
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkCanStockedProductBeModified</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Checks that the <c>StockedProduct</c> field can be modified for the <paramref
    ///    name="_inventModelGroup" /> parameter.
    /// </summary>
    /// <param name="_inventModelGroup">
    ///    The <c>InventModelGroup</c> buffer to check modification for.
    /// </param>
    /// <returns>
    ///    true if the <c>StockedProduct</c> field can be modified; otherwise, false.
    /// </returns>
    protected static boolean checkCanStockedProductBeModified(InventModelGroup _inventModelGroup)
    {
        InventTable             inventTable;
        InventModelGroupItem    inventModelGroupItem;
        boolean                 ret = true;

        select firstonly RecId from inventTable
            exists join inventModelGroupItem
                where inventModelGroupItem.ItemId               == inventTable.ItemId
                   && inventModelGroupItem.ItemDataAreaId       == inventTable.DataAreaId
                   && inventModelGroupItem.ModelGroupDataAreaId == _inventModelGroup.DataAreaId
                   && inventModelGroupItem.ModelGroupId         == _inventModelGroup.ModelGroupId;

        if (inventTable.RecId != 0)
        {
            ret = checkFailed("@SYS152677");
        }
        if (InventModelGroup::isModelGroupUsedInStdCostConv(_inventModelGroup.ModelGroupId))
        {
            ret = checkFailed("@SYS152678");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    static public InventModelGroupInventoriedPolicy construct()
    {
        return new InventModelGroupInventoriedPolicy();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newInventModelGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes a new instance of the <c>InventModelGroupInventoriedPolicy</c> class from an
    ///    <c>InventModelGroup</c> buffer.
    /// </summary>
    /// <param name="_inventModelGroup">
    ///    The <c>InventModelGroup</c> buffer on which the policy resides.
    /// </param>
    /// <returns>
    ///    A new instance of the <c>InventModelGroupInventoriedPolicy</c> class.
    /// </returns>
    public static InventModelGroupInventoriedPolicy newInventModelGroup(InventModelGroup _inventModelGroup)
    {
        InventModelGroupInventoriedPolicy inventModelGroupInventoriedPolicy = InventModelGroupInventoriedPolicy::construct();
        inventModelGroupInventoriedPolicy.parmInventModelGroup(_inventModelGroup);
        return inventModelGroupInventoriedPolicy;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
