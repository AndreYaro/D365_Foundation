<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CreditCardProviderVoidResult</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CreditCardProviderCaptureResult</c> stores result from a credit card void operation.
/// </summary>
class CreditCardProviderVoidResult extends CreditCardProviderResult
{
    CreditCardVoidResult creditCardVoidResult;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the object from an array of properties.
    /// </summary>
    /// <param name="_properties">
    /// An array of properties.
    /// </param>
    protected void init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty[] _properties)
    {
        str ns = Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.GenericNamespace::get_VoidResponse();
        CreditCardPaymentProperty property;

        super(_properties);

        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_UniqueCardId());
        this.parmCreditCardUniqueCardId(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_Last4Digits());
        this.parmCreditCardNumber(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_CardType());
        this.parmCreditCardTypeName(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_CloseAmount());
        this.parmTransactionAmount(property ? property.parmDecimalValue() : 0);
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_ProviderTransactionId());
        this.parmAuthorization(property ? property.parmStringValue() : '');
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_VoidResult());
        this.parmVoidResult(property ? CreditCardProviderVoidResult::getVoidResult(property.parmStringValue()) : CreditCardVoidResult::None);
        this.parmAvsResult(CreditCardAddressVerificationResult::None);
        this.parmAvsDetail(CreditCardMSAVSDetailCode::None);
        this.parmCvcResult(CreditCardMSCVCResultCode::Unknown);
        property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_PaymentTrackingId());
        this.parmMCRPaymentTrackingId(property ? property.parmStringValue() : '');

        if (EnhancedWalletSupportAndPaymentsImprovementFeature::isEnabled())
        {
            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_PaymentReference());
            this.parmProcessorReference(property ? property.parmStringValue() : '');

            property = properties.find(ns, Microsoft.Dynamics.Retail.PaymentSDK.Portable.Constants.VoidResponseProperties::get_PaymentMethodName());
            this.parmProcessorPayment(property ? property.parmStringValue() : '');
        }

        this.parmResultOK(this.parmVoidResult() == CreditCardVoidResult::Success);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmVoidResult</Name>
				<Source><![CDATA[
    public CreditCardVoidResult parmVoidResult(CreditCardVoidResult _value = creditCardVoidResult)
    {
        creditCardVoidResult = _value;
        return creditCardVoidResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromResponse</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a response object.
    /// </summary>
    /// <param name="_response">
    /// A response.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderVoidResult constructFromResponse(Microsoft.Dynamics.Retail.PaymentSDK.Portable.Response _response)
    {
        CreditCardProviderVoidResult result = new CreditCardProviderVoidResult();
        result.init(_response.get_Properties());
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>constructFromXmlString</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a result from a XML string.
    /// </summary>
    /// <param name="_xmlString">
    /// A XML string.
    /// </param>
    /// <returns>
    /// A result.
    /// </returns>
    public static CreditCardProviderVoidResult constructFromXmlString(str _xmlString)
    {
        CreditCardProviderVoidResult result = new CreditCardProviderVoidResult();
        result.init(Microsoft.Dynamics.Retail.PaymentSDK.Portable.PaymentProperty::ConvertXMLToPropertyArray(_xmlString));
        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVoidResult</Name>
				<Source><![CDATA[
    private static CreditCardVoidResult getVoidResult(str _code)
    {
        str toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.VoidResult _value)
        {
            str s = System.Convert::ToString(_value);

            return s;
        }

        if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.VoidResult::None))
            return CreditCardVoidResult::None;
        else if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.VoidResult::Success))
            return CreditCardVoidResult::Success;
        else if (_code == toStr(Microsoft.Dynamics.Retail.PaymentSDK.Portable.VoidResult::Failure))
            return CreditCardVoidResult::Failure;

        throw error(Error::wrongUseOfFunction(funcName()));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>