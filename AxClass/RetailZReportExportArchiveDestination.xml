<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailZReportExportArchiveDestination</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The <c>RetailZReportExportArchiveDestination</c> class represents the destination for the Z-report export archive.
/// </summary>
final internal class RetailZReportExportArchiveDestination implements ERIFileDestination, RetailIDigitalSignatureManagerHost
{
    private ERIFileDestination originDestination;
    private RetailZReportExportArchiveContract zReportContract;
    private RetailIDigitalSignatureManager digitalSignatureManager;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>saveFile</Name>
				<Source><![CDATA[
    /// <summary>
    /// Saves file in destination.
    /// </summary>
    /// <param name = "_stream">A stream to save.</param>
    /// <param name = "_filePath">A file path.</param>
    /// <returns>Saved stream.</returns>
    public System.IO.Stream saveFile(System.IO.Stream _stream, System.String _filePath)
    {
        System.IO.Stream ret;

        using (System.IO.Stream signature = this.getSignature(_stream))
        {
            ERFileDestinationArchive dataAndSignatureArchive = new ERFileDestinationArchive();
            dataAndSignatureArchive.saveFile(_stream, _filePath);
            dataAndSignatureArchive.saveFile(signature, this.getSignatureFilePath(_filePath));

            ret = originDestination.saveFile(dataAndSignatureArchive.GetStream(), this.getDataAndSignatureArchiveFilePath(_filePath));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFileStream</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a stream for new file.
    /// </summary>
    /// <param name = "_filePath">Name of new file.</param>
    /// <returns>Stream for new file.</returns>
    public System.IO.Stream newFileStream(System.String _filePath)
    {
        return originDestination.newFileStream(_filePath);
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs the z-report archive destination.
    /// </summary>
    /// <param name = "_originDestination">The original destination.</param>
    /// <param name = "_zReportContract">The z-report contract.</param>
    /// <returns>The archive destination.</returns>
    public static ERIFileDestination construct(ERIFileDestination _originDestination, RetailZReportExportArchiveContract _zReportContract)
    {
        RetailZReportExportArchiveDestination archiveDestination = new RetailZReportExportArchiveDestination();
        archiveDestination.originDestination = _originDestination;
        archiveDestination.zReportContract = _zReportContract;

        return archiveDestination;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSignatureFilePath</Name>
				<Source><![CDATA[
    private System.String getSignatureFilePath(System.String _dataPath)
    {
        return System.IO.Path::GetFileNameWithoutExtension(_dataPath) + '.sign';
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDataAndSignatureArchiveFilePath</Name>
				<Source><![CDATA[
    private System.String getDataAndSignatureArchiveFilePath(System.String _dataPath)
    {
        return System.IO.Path::GetFileNameWithoutExtension(_dataPath) + '.zip';
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSignature</Name>
				<Source><![CDATA[
    private System.IO.Stream getSignature(System.IO.Stream _stream)
    {
        if (!_stream)
        {
            throw new System.NullReferenceException("_stream");
        }

        if (_stream.CanSeek)
        {
            _stream.Seek(0, System.IO.SeekOrigin::Begin);
        }

        System.Byte[] dataToSign;
        using (System.IO.MemoryStream memoryStream = new System.IO.MemoryStream())
        {
            _stream.CopyTo(memoryStream);
            dataToSign  = memoryStream.ToArray();
        }

        System.String signatureAsString = this.getDigitalSignatureManager().hashAndSignBytes(dataToSign);

        System.IO.Stream signatureAsStream = new System.IO.MemoryStream(
            System.Text.Encoding::GetEncoding(this.getDigitalSignatureManager().getEncoding()).GetBytes(signatureAsString));

        return signatureAsStream;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDigitalSignatureManager</Name>
				<Source><![CDATA[
    public RetailIDigitalSignatureManager getDigitalSignatureManager()
    {
        if (!digitalSignatureManager)
        {
            digitalSignatureManager = RetailDigitalSignatureManager::constructFromParms();
        }

        return digitalSignatureManager;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDigitalSignatureManager</Name>
				<Source><![CDATA[
    public void setDigitalSignatureManager(RetailIDigitalSignatureManager _digitalSignatureManager)
    {
        digitalSignatureManager = _digitalSignatureManager;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>