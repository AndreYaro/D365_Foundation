<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMixedLPCompleteDispositionCodeDialog</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal final class WHSMixedLPCompleteDispositionCodeDialog
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static WHSMixedLPCompleteDispositionCodeDialog construct()
    {
        return new WHSMixedLPCompleteDispositionCodeDialog();
    }

]]></Source>
			</Method>
			<Method>
				<Name>captureDispositionCode</Name>
				<Source><![CDATA[
    internal container captureDispositionCode(WHSMixedLicensePlateReceipt _mixedLPHeader)
    {
        Dialog dialog = new Dialog("@WAX1841");
        dialog.windowType(FormWindowType::PopUp);
        
        DialogField dialogField = dialog.addField(extendedTypeStr(WHSDispositionCode));
        this.setupDialogField(dialogField, _mixedLPHeader);
        dialog.run();

        return [dialog.closedOk(), dialogField.value()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>setupDialogField</Name>
				<Source><![CDATA[
    [Wrappable(true)]
    internal void setupDialogField(DialogField _dialogField, WHSMixedLicensePlateReceipt _mixedLPHeader)
    {
        if (_mixedLPHeader.ReceiptStatus == WHSMixedLicensePlateReceiptStatus::BuildingLP
            && _mixedLPHeader.WorkTransType == WHSWorkTransType::ReturnOrder
            && WHSMixedLPCompleteReturnDispositionCodeFlight::instance().isEnabled())
        {
            _dialogField.registerOverrideMethod(
                methodStr(FormStringControl, lookup),
                methodstr(WHSMixedLPCompleteDispositionCodeDialog, lookupReturnDispositionCodes),
                this);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupReturnDispositionCodes</Name>
				<Source><![CDATA[
    private void lookupReturnDispositionCodes(FormStringControl _ctrl)
    {
        SysTableLookup tableLookup = SysTableLookup::newParameters(tableNum(WHSDispositionTable), _ctrl);
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(WHSDispositionTable));

        QueryBuildRange range = qbds.addRange(fieldNum(WHSDispositionTable, ReturnDispositionCodeId));
        range.value(SysQuery::valueNotEmptyString());
        
        tableLookup.parmQuery(query);
        tableLookup.addLookupfield(fieldNum(WHSDispositionTable, DispositionCode));

        tableLookup.performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>