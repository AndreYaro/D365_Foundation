<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CFMPRViewQueryFilter_FTI</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class sets the payment request data source filters based upon the filter record CustInvoiceTable.
/// </summary>
[CFMPaymentRequestSourceAttribute(tableStr(CustInvoiceTable))]
public class CFMPRViewQueryFilter_FTI extends CFMPRViewQueryFilter
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>filter</Name>
				<Source><![CDATA[
    protected void filter()
    {
        CustInvoiceTable invoiceTable = filterRecord;
        QueryBuildDataSource paymentRequestDataSource = dataSourceQuery.dataSourceTable(tableNum(CFMPaymentRequestTable));

        if (paymentRequestDataSource)
        {
            paymentRequestDataSource.firstOnly(false);
            paymentRequestDataSource.clearDynalinks();
            paymentRequestDataSource.clearRanges();

            QueryBuildRange qbr = paymentRequestDataSource.addRange(fieldNum(CFMPaymentRequestTable, SourceType));
            qbr.value(queryValue(CFMPaymentRequestSourceType::CustInvoice));

            QueryBuildDataSource qbds = paymentRequestDataSource.addDataSource(tableNum(CustTrans));
            qbds.addLink(fieldNum(CFMPaymentRequestTable, HeaderRefRecId), fieldNum(CustTrans, RecId));

            qbds = qbds.addDataSource(tableNum(CustInvoiceJour));
            qbds.addLink(fieldNum(CustTrans, Invoice), fieldNum(CustInvoiceJour, InvoiceId));
            qbds.addLink(fieldNum(CustTrans, AccountNum), fieldNum(CustInvoiceJour, InvoiceAccount));
            qbds.addLink(fieldNum(CustTrans, TransDate), fieldNum(CustInvoiceJour, InvoiceDate));
            qbds.addLink(fieldNum(CustTrans, Voucher), fieldNum(CustInvoiceJour, LedgerVoucher));
            qbds.joinMode(JoinMode::ExistsJoin);

            qbr = qbds.addRange(fieldNum(CustInvoiceJour, InvoiceId));
            qbr.value(queryValue(invoiceTable.InvoiceId));

            qbr = qbds.addRange(fieldNum(CustInvoiceJour, InvoiceDate));
            qbr.value(queryValue(invoiceTable.InvoiceDate));

            qbr = qbds.addRange(fieldNum(CustInvoiceJour, NumberSequenceGroup));
            qbr.value(queryValue(invoiceTable.NumberSequenceGroup));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>