<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvReturnCashVisibilityManager</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class controls visibility for cash advance return expense type.
/// </summary>
class TrvReturnCashVisibilityManager extends TrvExpensesVisibilityManager
{
    Map transactionReturnCash;
    const str transactionReturnCashGroupName = 'Transaction_Advance';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getTransactionReturnCash</Name>
				<Source><![CDATA[
    /// <summary>
    /// Getter method for cash advance return field group.
    /// </summary>
    /// <returns>
    /// A map of cash advance return fields.
    /// </returns>
    Map getTransactionReturnCash()
    {
        if (!transactionReturnCash)
        {
            transactionReturnCash = this.getFieldGroup(trvExpTrans, transactionReturnCashGroupName);
        }

        return transactionReturnCash;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFieldVisibilities</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets visibility for cash advance return fields.
    /// </summary>
    /// <param name = "_isVisible">
    /// Determines if we show or hide cash advance return fields.
    /// </param>
    void setFieldVisibilities(boolean _isVisible = false)
    {
        if (!this.parmExpenseVisibilityManager())
        {
            this.setVisibilityForFieldGroup(this.getTransactionReturnCash(), datasourceName, _isVisible);
        }
        else
        {
            this.setReturnCashSpecificFieldsVisibility(_isVisible);
        }

        if (_isVisible)
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReturnCashSpecificFieldsVisibility</Name>
				<Source><![CDATA[
    private void setReturnCashSpecificFieldsVisibility(boolean _isVisible)
    {
        FormDataSource formDataSource = trvExpenseForm.dataSource(dataSourceName);

        this.setFormDataObjectVisibility(formDataSource.object(fieldnum(TrvExpTrans, ExchangeCode)), _isVisible);
        this.setFormDataObjectVisibility(formDataSource.object(fieldnum(TrvExpTrans, AmountCurr)), _isVisible);

        this.setVisibilityForFormControl(this.parmFormInstance().design().controlName(tablemethodstr(TrvExpTrans, editExchRate)), _isVisible, _isVisible);
        this.setVisibilityForFormControl(this.parmFormInstance().design().controlName(tablemethodstr(TrvExpTrans, amountMSTWithCurrencyCode)), _isVisible, _isVisible);
        this.setVisibilityForFormControl(this.parmFormInstance().design().controlName(tablemethodstr(TrvExpTrans, cashAdvanceNumber)), _isVisible, _isVisible);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>