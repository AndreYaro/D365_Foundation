<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalFormTransAsset</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class contains the specific asset journal behavior.
/// </summary>
public class LedgerJournalFormTransAsset extends LedgerJournalFormTrans
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>formInitPost</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes form controls depending on the journal context.
    /// The form triggers this logic in the end of the init method.
    /// </summary>
    public void formInitPost()
    {
        super();

        LedgerJournalTable ledgerJournalTable;

        if (journalTableData)
        {
            ledgerJournalTable = journalTableData.journalTable();
        }

        if (ledgerJournalTable && ledgerJournalTable.CurrentOperationsTax == CurrentOperationsTax::None)
        {
            // Hide all fields related to offset account when posting layer is None
            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetAccountType)).visible(false);
            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetLedgerDimension)).visible(false);

            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetDefaultDimension)).visible(false);
            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetTxt)).visible(false);

            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetAccountAgreementId_RU)).visible(false);
            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetAgreementCompany_RU)).visible(false);
            journalTrans_ds.object(fieldNum(LedgerJournalTrans, OffsetPostingProfile_RU)).visible(false);

            // Hide balances totals
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, BalanceHeader))).visible(false);
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, VoucherTotal))).visible(false);
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, JournalBalance))).visible(false);

            // Hide financial dimension buttons
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, AccountDimension))).visible(false);
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, OffsetAccountDimension))).visible(false);

            // Disable Balance and Budget check errors or warnings buttons
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, Movements))).enabled(false);
            formRun.control(formRun.controlId(formControlStr(LedgerJournalTransAsset, BudgetCheckResultsInquiry))).enabled(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setBalanceControlButtonEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Balance Control button does not need to be enabled for None posting layer as those transactions are not posted to GL.
    /// </summary>
    protected void setBalanceControlButtonEnabled()
    {
        super();

        if (ctrlBalanceControlButton.enabled())
        {
            ctrlBalanceControlButton.enabled(!this.isJournalPostingLayerNone());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setVoucherTransButtonEnabled</Name>
				<Source><![CDATA[
    /// <summary>
    /// Voucher button does not need to be enabled for None posting layer as those transactions are not posted to GL.
    /// </summary>
    protected void setVoucherTransButtonEnabled()
    {
        super();

        if (ctrlVoucherTransButton.enabled())
        {
            ctrlVoucherTransButton.enabled(!this.isJournalPostingLayerNone());
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isJournalPostingLayerNone</Name>
				<Source><![CDATA[
    private boolean isJournalPostingLayerNone()
    {
        return ledgerJournalEngine.parmLedgerJournalTable().CurrentOperationsTax == CurrentOperationsTax::None;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>