<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTransListUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustTransListUIBuilder</c> class builds the UI for the <c>CustTransList</c> SSRS report.
/// </summary>
class CustTransListUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField billingClassificationField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>billingClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a multi-select billing classification lookup dialog box field.
    /// </summary>
    public void billingClassificationLookup()
    {
        Query query = new Query(queryStr(CustBillingClassificationLookup));
        TableId multiSelectTableNum = tableNum(CustBillingClassification);
        container selectedFields = [multiSelectTableNum, fieldName2id(multiSelectTableNum, fieldStr(CustBillingClassification, BillingClassification))];

        SysLookupMultiSelectCtrl::constructWithQuery(this.dialog().dialogForm().formRun(), billingClassificationField.control(), query, false, selectedFields);
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the <c>CustTransList</c> report parameter form based on billing classification.
    /// </summary>
    public void build()
    {
        boolean isBillingClassEnabled;
        DialogField inclTransWithNoBillingClassField;
        CustTransListContract custTransListContract;

        super();

        custTransListContract = this.dataContractObject() as CustTransListContract;

        billingClassificationField = this.bindInfo().getDialogField(custTransListContract, methodStr(CustTransListContract, parmBillingClassification));
        inclTransWithNoBillingClassField = this.bindInfo().getDialogField(custTransListContract, methodStr(CustTransListContract, parmInclTransWithNoBillingClass));

        // controlling visibility
        if (billingClassificationField && inclTransWithNoBillingClassField)
        {
            isBillingClassEnabled = CustParameters::isBillingClassificationEnabled();
            billingClassificationField.visible(isBillingClassEnabled);
            inclTransWithNoBillingClassField.visible(isBillingClassEnabled);
            billingClassificationField.lookupButton(FormLookupButton::Always);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Implements the logic that is supposed to execute after run.
    /// </summary>
    public void postRun()
    {
        if (CustParameters::isBillingClassificationEnabled())
        {
            this.billingClassificationLookup();
        }
        else
        {
            super();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>