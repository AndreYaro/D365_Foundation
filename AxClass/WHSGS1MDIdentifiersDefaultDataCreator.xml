<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSGS1MDIdentifiersDefaultDataCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal static class WHSGS1MDIdentifiersDefaultDataCreator
{  
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        WHSGS1MDIdentifiersDefaultDataCreator::createDefaultMDIdentifiers();
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkMDIdentifiersListSetup</Name>
				<Source><![CDATA[
    public static void checkMDIdentifiersListSetup()
    {
        WHSGS1MDIdentifiersList mDIdentifiersList;

        select firstfast mDIdentifiersList;

        if (!mDIdentifiersList)
        {
            WHSGS1MDIdentifiersDefaultDataCreator::createDefaultMDIdentifiers();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createDefaultMDIdentifiers</Name>
				<Source><![CDATA[
    public static void createDefaultMDIdentifiers()
    {
        RecordInsertList mdIdentifiersListToInsert = new RecordInsertList(tableNum(WHSGS1MDIdentifiersList));
        WHSGS1MDIdentifiersList mdIdentifiersList;

        ttsbegin;

        WHSGS1MDIdentifiersDefaultDataCreator::cleanMDIdentifiersList();

        ListEnumerator listEnumerator = WHSGS1MDIdentifiersDefaultDataCreator::getMDIdentifiersEnum();
        while (listEnumerator.moveNext())
        {
            mdIdentifiersList.initValue();
            mdIdentifiersList.MDIdentifier = listEnumerator.current();
            mdIdentifiersListToInsert.add(mdIdentifiersList);
        }

        mdIdentifiersListToInsert.insertDatabase();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>cleanMDIdentifiersList</Name>
				<Source><![CDATA[
    private static void cleanMDIdentifiersList()
    {
        WHSGS1MDIdentifiersList mdIdentifiersList;

        delete_from mdIdentifiersList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMDIdentifiersEnum</Name>
				<Source><![CDATA[
    private static ListEnumerator getMDIdentifiersEnum()
    {
        List mdIdentifiers = new List(Types::String);

        SysDictClass    mobileAppStepDictClass = new SysDictClass(className2Id(classStr(WHSMobileAppStep)));
        List            mobileAppStepChildsList = mobileAppStepDictClass.extendedBy();
        ListEnumerator  li = mobileAppStepChildsList.getEnumerator();
        SysDictClass    mobileAppStepChild;
        Array           attributes;

        while (li.moveNext())
        {
            mobileAppStepChild = new SysDictClass(li.current());
            attributes = mobileAppStepChild.getAllAttributes();
                    
            for (int i = 1; i <= attributes.lastIndex(); i++)
            {
                var attribute = attributes.value(i);

                if (attribute is WHSMobileAppStepIdAttribute)
                {
                    WHSMobileAppStepIdAttribute mobileAppStepIdAttribute = attribute;

                    mdIdentifiers.addEnd(mobileAppStepIdAttribute.parmStepId());
                }
            }
        }

        return mdIdentifiers.getEnumerator();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>