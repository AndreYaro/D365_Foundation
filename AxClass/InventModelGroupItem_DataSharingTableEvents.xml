<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventModelGroupItem_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>InventModelGroupItem_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class InventModelGroupItem_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static InventModelGroupItem_DataSharingTableEvents construct()
    {
        return new InventModelGroupItem_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = InventModelGroupItem_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Inserted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventModelGroupItem origInventModelGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;
                InventModelGroupItem inventModelGroupItem;
                buf2Buf(origInventModelGroupItem, inventModelGroupItem);
                inventModelGroupItem.ItemDataAreaId = _dataSharingCompany;
                inventModelGroupItem.ModelGroupDataAreaId = _dataSharingCompany;
                if (!inventModelGroupItem.validateWrite())
                {
                    throw error(strFmt("@SCM:SysDataSharingCannotInsertRecordInTable", tablePName(InventModelGroupItem), _dataSharingCompany));
                }
                inventModelGroupItem.insert();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Deleted);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventModelGroupItem origInventModelGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                InventModelGroupItem inventModelGroupItem = InventModelGroupItem::findByItemIdLegalEntity(origInventModelGroupItem.ItemId, _dataSharingCompany, true);
                if (inventModelGroupItem.RecId)
                {
                    if (!inventModelGroupItem.validateDelete())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotDeleteRecordInTable", tablePName(InventModelGroupItem), _dataSharingCompany));
                    }
                    inventModelGroupItem.delete();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);

                InventModelGroupItem origInventModelGroupItem = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                InventModelGroupItem inventModelGroupItem = InventModelGroupItem::findByItemIdLegalEntity(origInventModelGroupItem.ItemId, _dataSharingCompany, true);
                if (inventModelGroupItem.RecId)
                {
                    buf2Buf(origInventModelGroupItem, inventModelGroupItem);
                    inventModelGroupItem.ItemDataAreaId = _dataSharingCompany;
                    inventModelGroupItem.ModelGroupDataAreaId = _dataSharingCompany;        
                    if (!inventModelGroupItem.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotUpdateRecordInTable", tablePName(InventModelGroupItem), _dataSharingCompany));
                    }
                      
                    inventModelGroupItem.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventModelGroup), tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventModelGroupItem_onInserted</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of InventModelGroupItem table
    [DataEventHandler(tableStr(InventModelGroupItem), DataEventType::Inserted)]
    public static void InventModelGroupItem_onInserted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Inserted);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventModelGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventModelGroupItem = InventModelGroupItem_DataSharingTableEvents::construct();
                tableEventsProcessorInventModelGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventModelGroupItem);
                using (var contextInsertCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventModelGroupItem), DataEventType::Inserted))
                {
                    processor.insertCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventModelGroupItem_onDeleted</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(InventModelGroupItem), DataEventType::Deleted)]
    public static void InventModelGroupItem_onDeleted(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Deleted);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventModelGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventModelGroupItem = new InventModelGroupItem_DataSharingTableEvents();
                tableEventsProcessorInventModelGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventModelGroupItem);
                using (var contextDeleteCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventModelGroupItem), DataEventType::Deleted))
                {
                    processor.deleteCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventModelGroupItem_onUpdated</Name>
				<Source><![CDATA[
    [DataEventHandler(tableStr(InventModelGroupItem), DataEventType::Updated)]
    public static void InventModelGroupItem_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventModelGroupItem), DataEventType::Updated);
            if (!context 
                && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventModelGroup))
                && (SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable))))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventModelGroupItem = new InventModelGroupItem_DataSharingTableEvents();
                tableEventsProcessorInventModelGroupItem.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventModelGroupItem);
                using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventModelGroupItem), DataEventType::Updated))
                {
                    processor.updateCrossCompany(false);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>