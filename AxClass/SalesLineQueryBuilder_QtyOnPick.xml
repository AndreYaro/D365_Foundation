<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesLineQueryBuilder_QtyOnPick</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesLineQueryBuilder_QtyOnPick</c> class creates queries related to the <c>InventTrans</c> table.
/// </summary>
public final class SalesLineQueryBuilder_QtyOnPick extends SalesLineQueryBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>newFromParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes the <c>SalesLineQueryBuilder_QtyOnPick</c> class object.
    /// </summary>
    /// <param name = "_inventTransId">
    /// The inventory transaction ID.
    /// </param>
    /// <param name = "_dataAreaId">
    /// The dataarea ID.
    /// </param>
    /// <param name = "_inventTrans">
    /// The <c>InventTrans</c> table buffer.
    /// </param>
    /// <returns>
    /// Initialized <c>SalesLineQueryBuilder_QtyOnPick</c> class object.
    /// </returns>
    public static SalesLineQueryBuilder_QtyOnPick newFromParameters(TradeInventTransId _inventTransId,
                                                                    DataAreaId         _dataAreaId,
                                                                    InventTrans        _inventTrans)
    {
        SalesLineQueryBuilder salesLineQueryBuilder_QtyOnPick = new SalesLineQueryBuilder_QtyOnPick();
        salesLineQueryBuilder_QtyOnPick.inventTransId         = _inventTransId;
        salesLineQueryBuilder_QtyOnPick.dataAreaId            = _dataAreaId;
        salesLineQueryBuilder_QtyOnPick.inventTrans           = _inventTrans;

        return salesLineQueryBuilder_QtyOnPick;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildQueryObject</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a query related to the <c>InventTrans</c> table.
    /// </summary>
    /// <returns>
    /// The query object.
    /// </returns>
    public SysDaQueryObject buildQueryObject()
    {
        queryObject = this.getInventTransQuery();

        this.joinInventTransOriginSalesLine();

        return queryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventTransQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates the <c>InventTrans</c> table query object with where clause.
    /// </summary>
    /// <returns>
    /// The <c>InventTrans</c> table query object.
    /// </returns>
    [Wrappable(true)]
    protected final SysDaQueryObject getInventTransQuery()
    {
        queryObject = new SysDaQueryObject(inventTrans);
        queryObject.projection().addSum(fieldStr(InventTrans, Qty));

        queryObject.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, TransChildType)),
                new SysDaValueExpression(InventTransChildType::WMSOrder))
            .and(new SysDaEqualsExpression(
                new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, StatusReceipt)),
                new SysDaValueExpression(StatusReceipt::None)))
            .and((new SysDaEqualsExpression(
                new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, StatusIssue)),
                new SysDaValueExpression(StatusIssue::OnOrder))
                .or(new SysDaEqualsExpression(
                    new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, StatusIssue)),
                    new SysDaValueExpression(StatusIssue::ReservOrdered)))
                .or(new SysDaEqualsExpression(
                    new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, StatusIssue)),
                    new SysDaValueExpression(StatusIssue::ReservPhysical))))));

        return queryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>joinInventTransOriginSalesLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Joins the query object to <c>InventTransOriginSalesLine</c> table.
    /// </summary>
    /// <returns>
    /// The query object.
    /// </returns>
    [Wrappable(true)]
    protected final SysDaQueryObject joinInventTransOriginSalesLine()
    {
        InventTransOriginSalesLine inventTransOriginSalesLine;

        SysDaQueryObject inventTransOriginSalesLineQuery = new SysDaQueryObject(inventTransOriginSalesLine);

        inventTransOriginSalesLineQuery = this.addWhereClauseToInventTransOriginSalesLine(inventTransOriginSalesLineQuery, inventTransOriginSalesLine);

        queryObject.joinClause(SysDaJoinKind::ExistsJoin, inventTransOriginSalesLineQuery);

        return queryObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addWhereClauseToInventTransOriginSalesLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds where clause to the <c>InventTransOriginSalesLine</c> table of query object.
    /// </summary>
    /// <param name = "_inventTransOriginSalesLineQuery">
    /// The query object.
    /// </param>
    /// <param name = "_inventTransOriginSalesLine">
    /// The <c>InventTransOriginSalesLine</c> buffer.
    /// </param>
    /// <returns>
    /// The query object.
    /// </returns>
    [Wrappable(true)]
    protected final SysDaQueryObject addWhereClauseToInventTransOriginSalesLine(SysDaQueryObject _inventTransOriginSalesLineQuery, InventTransOriginSalesLine _inventTransOriginSalesLine)
    {
        _inventTransOriginSalesLineQuery.whereClause(
            new SysDaEqualsExpression(
                new SysDaFieldExpression(_inventTransOriginSalesLine, fieldStr(InventTransOriginSalesLine, InventTransOrigin)),
                new SysDaFieldExpression(inventTrans, fieldStr(InventTrans, InventTransOrigin)))
            .and(new SysDaEqualsExpression(
                new SysDaFieldExpression(_inventTransOriginSalesLine, fieldStr(InventTransOriginSalesLine, SalesLineInventTransId)),
                new SysDaValueExpression(inventTransId)))
            .and(new SysDaEqualsExpression(
                new SysDaFieldExpression(_inventTransOriginSalesLine, fieldStr(InventTransOriginSalesLine, SalesLineDataAreaId)),
                new SysDaValueExpression(dataAreaId))));

        return _inventTransOriginSalesLineQuery;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>