<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EmplCloseSettle_Close_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class EmplCloseSettle_Close_RU extends RunBaseBatch implements BatchRetryable
{
    DialogField dialogTransDate;
    DialogField dialogProfileSettlement;

    boolean     profileSettlement;
    TransDate   transDate;

    QueryRun    queryRun;

    protected const Version CurrentVersion_EmplCloseSettle_Close_RU = 2;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildInternalQueryRun</Name>
				<Source><![CDATA[
    protected QueryRun buildInternalQueryRun(QueryRun _queryRun)
    {
        Query                query;
        QueryBuildDataSource qbds, qbdsNew;
        QueryBuildRange      qbr;
        int                  i;

        query = new Query();

        qbds = _queryRun.query().dataSourceTable(tableNum(EmployeeTable_RU));

        qbdsNew = query.addDataSource(qbds.table());

        for (i = 1; i <= qbds.rangeCount(); i++)
        {
            qbr = qbds.range(i);
            qbdsNew.addRange(qbr.field()).value(qbr.value());
        }

        qbdsNew = qbdsNew.addDataSource(tableNum(EmplTrans_RU));
        qbdsNew.addSelectionField(fieldNum(EmplTrans_RU, EmplAccount));
        qbdsNew.orderMode(OrderMode::GroupBy);
        qbdsNew.joinMode(JoinMode::ExistsJoin);
        qbdsNew.relations(true);

        return new QueryRun(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase   dialog = super();

        dialogTransDate = dialog.addFieldValue(extendedTypeStr(TransDate),
                                               transDate ?
                                               transDate :
                                               DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone()),
                                               "@SYS67515",
                                               "@GLS109128");

        dialogProfileSettlement = dialog.addFieldValue(extendedTypeStr(NoYesId),
                                                       profileSettlement ?
                                                       profileSettlement :
                                                       EmplParameters_RU::find().ProfileSettlement,
                                                       "@GLS106881",
                                                       "@GLS106882");
        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret;

        ret = super();

        if (ret)
        {
            transDate         = dialogTransDate.value();
            if (! transDate)
            {
                ret = checkFailed("@SYS56505");
            }

            profileSettlement = dialogProfileSettlement.value();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new()
    {
        Query query;

        super();

        query = new Query("@GLS105107");

        query.addDataSource(tableNum(EmployeeTable_RU));

        queryRun = new QueryRun(query);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [ CurrentVersion_EmplCloseSettle_Close_RU, transDate, profileSettlement, queryRun.pack()];
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryRun</Name>
				<Source><![CDATA[
    public QueryRun queryRun()
    {
        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        EmplSettlePeriodic_RU   settle;
        QueryRun                queryRunInternal;
        EmployeeTable_RU        emplTable;

        queryRunInternal = this.buildInternalQueryRun(queryRun);

        this.progressInit("@GLS105107", SysQuery::countLoops(queryRunInternal), #AviUpdate);

        try
        {
            while (queryRunInternal.next())
            {
                emplTable              = queryRunInternal.get(tableNum(EmployeeTable_RU));

                settle              = new EmplSettlePeriodic_RU(transDate);
                settle.parmProfileSettlement(profileSettlement);

                progress.incCount();
                progress.setText("@GLS100699" + "@GLS116883" + emplTable.EmployeeId);

                settle.settleNow(emplTable.EmployeeId);
            }
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>showQueryValues</Name>
				<Source><![CDATA[
    public boolean showQueryValues()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container _packedClass)
    {
        Integer     version = RunBase::getVersion(_packedClass);
        container   conQueryRun;

        switch (version)
        {
            case CurrentVersion_EmplCloseSettle_Close_RU:

                [version, transDate, profileSettlement, conQueryRun] = _packedClass;
                queryRun = new QueryRun(conQueryRun);
                break;

            default:
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@GLS105107";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        EmplCloseSettle_Close_RU closeSettlement = new EmplCloseSettle_Close_RU();

        if (closeSettlement.prompt())
        {
            GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceRU00062, funcName());
            closeSettlement.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		<Method><Name>isRetryable</Name><Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }
]]></Source></Method></Methods>
	</SourceCode>
</AxClass>