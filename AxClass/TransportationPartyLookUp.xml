<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TransportationPartyLookUp</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TransportationPartyLookUp</c> class is the lookup class for the transportation party
/// selection.
/// </summary>
class TransportationPartyLookUp
{
    TransportationParty transportationParty;
    Query               query;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>convertLookupRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds or creates a record in the <c>TransportationDeliveryParty</c> table.
    /// </summary>
    /// <param name="_record">
    /// The <c>TransportationDeliveryParty</c> record in which to find.
    /// </param>
    /// <returns>
    /// A record in the <c>TransportationDeliveryParty</c> table.
    /// </returns>
    public Common convertLookupRecord(Common _record)
    {
        TransportationDeliveryParty transportationDeliveryPartyLocal;
        transportationDeliveryPartyLocal = TransportationDeliveryParty::findByView(_record);

        if (transportationDeliveryPartyLocal)
        {
            return transportationDeliveryPartyLocal;
        }
        else
        {
            return TransportationDeliveryParty::insertFromView(_record);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupTransportationParty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs a lookup with the custom lookup form for transportation party.
    /// </summary>
    /// <param name="_control">
    /// The form control to use to perform the reference lookup.
    /// </param>
    /// <returns>
    /// The record that is selected.
    /// </returns>
    public Common lookupTransportationParty(FormStringControl _control)
    {
        Common selectedRecord;
        Args args;
        FormRun formRun;

        args = new Args();
        args.name(formstr(TransportationPartyLookUp));
        args.caller(_control);

        args.parmObject(this);

        formRun = classfactory.formRunClass(args);

        formRun.init();

        formRun.selectMode(_control);

        _control.performFormLookup(formRun);

         formRun.wait();

        selectedRecord = formRun.selectRecordModeSelectedRecord();

        return selectedRecord;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
        query = new Query(querystr(TransportationPartyLookUp));
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTransPartyType</Name>
				<Source><![CDATA[
        public TransportationParty parmTransPartyType(TransportationParty _transportationParty = transportationParty)
    {
        transportationParty = _transportationParty;
        return transportationParty;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the query based on the user selection in the filter.
    /// </summary>
    /// <returns>
    /// The updated query.
    /// </returns>
    public Query updateQuery()
    {
        QueryBuildRange validFromRange;
        QueryBuildRange validToRange;
        QueryBuildRange transportationPartyType;
        QueryBuildDataSource transportationPartyDS;

        utcdatetime     now = DateTimeUtil::utcNow();

        transportationPartyDS = query.dataSourceTable(tablenum(TransportationAllParty));

        validFromRange = transportationPartyDS.findRange(fieldnum(TransportationAllParty, ValidFrom));
        validToRange = transportationPartyDS.findRange(fieldnum(TransportationAllParty, ValidTo));
        transportationPartyType = transportationPartyDS.findRange(fieldnum(TransportationAllParty, TransportationPartyType));

        if (transportationParty)
        {
            transportationPartyType.value(queryValue(transportationParty));
        }

        query.validTimeStateDateTimeRange(now, now);
        validFromRange.value(SysQuery::valueUnlimited());
        validToRange.value(SysQuery::valueUnlimited());

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static TransportationPartyLookUp construct()
    {
        return new TransportationPartyLookUp();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>