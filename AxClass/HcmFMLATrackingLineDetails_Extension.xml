<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HcmFMLATrackingLineDetails_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(HcmFMLATrackingLineDetails))]
public final class HcmFMLATrackingLineDetails_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createFMLATrackingLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates <c>HcmFMLATrackingLineDetails</c> record based on the
    /// <c>HcmFMLACaseDetail</c> record and <c>PayrollEarningStatementLine</c> record.
    /// </summary>
    /// <param name="_earningStatementLineRecId">
    /// The <c>PayrollEarningStatementLine</c> recId.
    /// </param>
    /// <returns>
    /// true if the record is created; otherwise, false.
    /// </returns>
    public static boolean createFMLATrackingLine(PayrollEarningStatementLineRecId  _earningStatementLineRecId)
    {
        HcmFMLATrackingLineDetails  trackingLineDetails;
        HcmFMLATrackingHeader       trackingHeader;
        PayrollEarningStatement     earningStatement;
        PayrollEarningStatementLine earningStatementLine;
        HcmFMLACaseDetail           caseDetail;
        HcmWorker                   worker;
        HcmFMLATrackingLineDetails  trackingLineDetailsLocal;
        Hours                       totalHours;

        if (isConfigurationkeyEnabled(configurationKeyNum(CRSEUnitedStates)))
        {
            select firstOnly RecId, EarningStatement, EarningDate, EarningCode, Quantity from earningStatementLine
                where earningStatementLine.RecId == _earningStatementLineRecId
                join RecId, Worker from earningStatement
                    where earningStatement.RecId == earningStatementLine.EarningStatement
                join RecId from worker
                    where worker.RecId == earningStatement.Worker
                join ApprovedHours, CaseId, LeaveReason, LeaveSchedule, RecId from caseDetail
                    order by LeaveStartDate asc, CreatedDateTime asc
                    where caseDetail.LeaveStartDate <= earningStatementLine.EarningDate
                    && caseDetail.EstimatedLeaveEndDate >= earningStatementLine.EarningDate
                    && caseDetail.Approved == NoYes::Yes
                    && caseDetail.Party == worker.Person;

            if (caseDetail.RecId)
            {
                select sum(HoursTaken) from trackingLineDetailsLocal
                    where trackingLineDetailsLocal.CaseDetailBase == caseDetail.RecId;

                totalHours = trackingLineDetailsLocal.HoursTaken + earningStatementLine.Quantity;

                if (totalHours <= caseDetail.ApprovedHours)
                {
                    select firstonly RecId from trackingHeader
                        where trackingHeader.Worker == earningStatement.Worker;

                    ttsbegin;
                    if (!trackingHeader.RecId)
                    {
                        trackingHeader.Worker = earningStatement.Worker;
                        trackingHeader.insert();
                    }

                    trackingLineDetails.CaseDetailBase       = caseDetail.RecId;
                    trackingLineDetails.HoursTaken           = earningStatementLine.Quantity;
                    trackingLineDetails.EarningsDate         = earningStatementLine.EarningDate;
                    trackingLineDetails.TrackingHeader       = trackingHeader.RecId;
                    trackingLineDetails.EarningStatementLine = earningStatementLine.RecId;
                    trackingLineDetails.insert();
                    ttscommit;
                }
            }
        }

        return trackingLineDetails.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteByPayrollEarningLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes records from the <c>HcmFMLATrackingLineDetails</c> table based on the specified values.
    /// </summary>
    /// <param name="_earningStatementLine">
    /// The <c>PayrollEarningStatementLine</c> record.
    /// </param>
    public static void deleteByPayrollEarningLine(PayrollEarningStatementLine _earningStatementLine)
    {
        HcmFMLATrackingLineDetails trackingLineDetails;

        if (isConfigurationkeyEnabled(configurationKeyNum(CRSEUnitedStates)))
        {
            delete_from trackingLineDetails
                where trackingLineDetails.EarningStatementLine == _earningStatementLine.RecId;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>