<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BomPartOfUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BOMPartOfUIBuilder</c> class is the UI builder class for the <c>BomPartOf</c> report.
/// </summary>
class BomPartOfUIBuilder extends SrsReportDataContractUIBuilder
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addDialogField</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Adds fields to the dialog that is being built.
    /// </summary>
    /// <param name="methodName">
    ///    The name of the property to add.
    /// </param>
    /// <param name="_dataContract">
    ///    The object on which the property to be bound exists.
    /// </param>
    /// <returns>
    ///    If the property type is supported for the UI, the <c>DialogField</c> object that represents the UI
    ///    control that was added; otherwise, null.
    /// </returns>
    /// <remarks>
    ///    This is a derived class and uses this method to add fields to the dialog that is being built. The
    ///    method automatically derives labels and control types from the data contract.
    /// </remarks>
    protected DialogField addDialogField(identifierName methodName, Object _dataContract = this.dataContractObject())
    {
        DialogField ret;

        ret = super(methodName, _dataContract);
        if (ret)
        {
            ret.visible(this.isFieldVisible(methodName));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Modifies a dialog to use two columns.
    /// </summary>
    public void build()
    {
        FormBuildGroupControl formBuildGroupControl;
        super();
        formBuildGroupControl = this.dialog().curFormGroup();
        formBuildGroupControl.columns(2);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGroup</Name>
				<Source><![CDATA[
        protected void buildGroup(SysOperationGroupInfo groupInfo, FormBuildGroupControl groupControl)
    {
        BOMPartOfContract contract = this.dataContractObject();

        if (groupInfo.groupName() == 'Date')
        {
            groupControl.frameOptionButton(FormFrameOptionButton::Check);
            groupControl.optionValue(contract.parmSearchInterval());
        }

        super(groupInfo, groupControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
        public void getFromDialog()
    {
        super();
        this.getSearchIntervalFromDialog(this.dialog());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSearchIntervalFromDialog</Name>
				<Source><![CDATA[
        protected void getSearchIntervalFromDialog(Dialog  _dialog)
    {
        BOMPartOfContract contract = this.dataContractObject();
        FormGroupControl  dialogSearchIntervalGroup = _dialog.formRun().control(_dialog.formRun().controlId('Date'));

        contract.parmSearchInterval(dialogSearchIntervalGroup.optionValue());
    }

]]></Source>
			</Method>
			<Method>
				<Name>isFieldVisible</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the field should be visible for a certain method of the contract.
    /// </summary>
    /// <param name="_methodName">
    ///    The name of a contract method.
    /// </param>
    /// <returns>
    ///    true if the field should be visible; otherwise, false.
    /// </returns>
    public boolean isFieldVisible(identifierName _methodName)
    {
        if (_methodName == methodstr(BOMPartOfContract, parmDisplayPrinciple))
        {
            return isConfigurationkeyEnabled(configurationkeynum(BOMVersion));
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>