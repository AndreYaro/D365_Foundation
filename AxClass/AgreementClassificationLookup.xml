<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AgreementClassificationLookup</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>AgreementClassificationLookup</c> class contains methods allowing to perform a lookup on the agreement classification.
/// </summary>
/// <remarks>
/// This class is called whenever the user presses the Lookup button on the agreement classification
/// field.
/// </remarks>
public class AgreementClassificationLookup
{
    TableId agreementTableId;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmAgreementTableId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets the table id of the <c>AgreementHeader</c> object to query.
    /// </summary>
    /// <remarks>
    /// The table id set must extend the AgreementHeader table type.
    /// </remarks>
    /// <param name = "_relationType">The table id of the <c>AgreementHeader</c> object to query.</param>
    /// <returns>
    /// The table id of the <c>AgreementHeader</c> object to query.
    /// </returns>
    public TableId parmAgreementTableId(TableId _agreementTableId = agreementTableId)
    {
        agreementTableId = _agreementTableId;
        return agreementTableId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupAgreementClassification</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays a filtered version of the <c>AgreementClassification</c> lookup form to the user based on
    /// a specified <c>AgreementHeader</c> object.
    /// </summary>
    /// <param name="_formReferenceControl">The form control on which the lookup will be performed.</param>
    /// <returns>
    /// The record of the <c>AgreementClassification</c> table that is selected by the user.
    /// </returns>
    /// <remarks>
    /// This method is called whenever the user presses the Lookup button on the agreement classification
    /// field.
    /// </remarks>
    public AgreementClassification lookupAgreementClassification(FormReferenceControl _formReferenceControl)
    {
        SysReferenceTableLookup sysLookupReference = SysReferenceTableLookup::newParameters(tablenum(AgreementClassification), _formReferenceControl, true);

        sysLookupReference.addLookupfield(fieldnum(AgreementClassification, Name));

        if (isConfigurationkeyEnabled(configurationKeyNum(PublicSector)))
        {
            sysLookupReference.addLookupfield(fieldNum(AgreementClassification, Description_PSN));
        }

        sysLookupReference.parmQuery(this.buildLookupAgreementClassificationQuery());

        return sysLookupReference.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildLookupAgreementClassificationQuery</Name>
				<Source><![CDATA[
    private Query buildLookupAgreementClassificationQuery()
    {
        Query                   query = new Query();
        QueryBuildDataSource    dataSource;
        QueryBuildRange         range;

        dataSource = SysQuery::findOrCreateDataSource(query, tablenum(AgreementClassification));
        range      = SysQuery::findOrCreateRange(dataSource, fieldnum(AgreementClassification, AgreementRelationType));
        range.value(queryValue(agreementTableId));
        range.status(RangeStatus::Hidden);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static AgreementClassificationLookup construct()
    {
        AgreementClassificationLookup agreementClassificationLookup = new AgreementClassificationLookup();

        return agreementClassificationLookup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newAgreementClassificationLookup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of <see cref="AgreementClassificationLookup Class"/> class.
    /// </summary>
    /// <param name = "_agreementTableId">Gets or set the table id of the <c>AgreementHeader</c> object to query. <see cref="parmAgreementTableId Method"/></param>
    /// <returns>
    /// The new instance of <c>AgreementClassificationLookup</c> class.
    /// </returns>
    public static AgreementClassificationLookup newAgreementClassificationLookup(TableId _agreementTableId)
    {
        AgreementClassificationLookup agreementClassificationLookup = new AgreementClassificationLookup();
        agreementClassificationLookup.parmAgreementTableId(_agreementTableId);

        return agreementClassificationLookup;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>