<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FBIncomingBookController_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FBIncomingBookController_BR</c> class shows the <c>FBIncomingBook_BR</c> SSRS report.
/// </summary>
class FBIncomingBookController_BR extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    protected void preRunModifyContract()
    {
        FBIncomingOutgoingBookContract_BR   contract;
        FBBookingPeriod_BR                  bookingPeriod;
        FiscalEstablishment_BR              fiscalEstablishment;

        contract = this.parmReportContract().parmRdpContract() as FBIncomingOutgoingBookContract_BR;

        select firstOnly crossCompany FiscalEstablishmentId, DataAreaId, CNPJCPFNum_BR, IENum_BR, Name from fiscalEstablishment
            where fiscalEstablishment.FiscalEstablishmentId == contract.parmFiscalEstablishmentId()
                && fiscalEstablishment.dataAreaId == contract.parmFiscalEstablishmentDataArea();

        if (this.parmArgs().record())
        {
            contract.parmFBBookingPeriodRecId(this.parmArgs().record().RecId);
        }
        else
        {
            select firstOnly RecId from bookingPeriod
                where bookingPeriod.FiscalEstablishmentId == fiscalEstablishment.FiscalEstablishmentId
                    && bookingPeriod.FiscalEstablishmentDataArea == fiscalEstablishment.dataAreaId
                    && bookingPeriod.PeriodMonth == mthOfYr(contract.parmFromDate())
                    && bookingPeriod.PeriodYear == year(contract.parmFromDate());

            contract.parmFBBookingPeriodRecId(bookingPeriod.RecId);
        }

        contract.parmCnpj(fiscalEstablishment.CNPJCPFNum_BR);
        contract.parmIeNum(fiscalEstablishment.IENum_BR);
        contract.parmFiscalEstablishmentName(fiscalEstablishment.Name);
    }

]]></Source>
			</Method>
			<Method>
				<Name>startReportOperation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Run the <c>FBIncomingBook_BR</c> report
    /// </summary>
    /// <param name="_args">
    /// An <c>Args</c> object
    /// </param>
    protected void startReportOperation(Args _args)
    {
        this.parmReportName(ssrsReportStr(FBIncomingBook_BR, Report));
        this.parmArgs(_args);
        this.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    static void main(Args _args)
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceBR10017, funcName());

        FBIncomingBookController_BR controller = new FBIncomingBookController_BR();
        controller.startReportOperation(_args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Controls whether the batch task should be retried in case of transient errors.
    /// </summary>
    /// <returns>
    /// Always returns true.
    /// </returns>
    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>