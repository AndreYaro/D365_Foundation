<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventItemSalesSetup_DataSharingTableEvents</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>InventItemSalesSetup_DataSharingTableEvents</c> class processes data sharing table event operations cross company.
/// </summary>
internal class InventItemSalesSetup_DataSharingTableEvents implements SysDataSharingApplicationSuiteITableEventsProcessor
{
    private SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingTableEventParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static InventItemSalesSetup_DataSharingTableEvents construct()
    {
        return new InventItemSalesSetup_DataSharingTableEvents();
    }

]]></Source>
			</Method>
			<Method>
				<Name>dataSharingParameters</Name>
				<Source><![CDATA[
    public SysDataSharingApplicationSuiteTableEventsProcessorParameters dataSharingParameters()
    {
        return this.dataSharingTableEventParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeProcessor</Name>
				<Source><![CDATA[
    public void initializeProcessor(Common _dataSharingRecord)
    {
        this.dataSharingTableEventParameters = InventItemSalesSetup_DataSharingTableEvents::createParameters(_dataSharingRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void insertDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        // inserting of blank inventdim record is done when insert inventtable record and other dimension creation should happen in each company.
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void deleteDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        // deleting is handled in each company, if item is deleted cleanup is done from InventTable delete method.
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateDataSharingCompany</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final void updateDataSharingCompany(DataAreaId _dataSharingCompany)
    {
        var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemSalesSetup), DataEventType::Updated);
        
        if (context && this.dataSharingTableEventParameters && this.dataSharingTableEventParameters.OrigDataSharingRecord.RecId)
        {
            Set companyList = context.parmProcessedCompanies();
            if (!companyList.in(_dataSharingCompany))
            {
                companyList.add(_dataSharingCompany);
                
                InventItemSalesSetup origInventItemSalesSetup = this.dataSharingTableEventParameters.OrigDataSharingRecord;

                // Only update the default blank inventdim record, other updates should be handle in each company.
                InventItemSalesSetup inventItemSalesSetup = InventItemSalesSetup::findDefault(origInventItemSalesSetup.ItemId,true);
                if (inventItemSalesSetup.RecId)
                {
                    buf2Buf(origInventItemSalesSetup, inventItemSalesSetup);
                    if (!inventItemSalesSetup.validateWrite())
                    {
                        throw error(strFmt("@SCM:SysDataSharingCannotUpdateRecordInTable", tablePName(InventItemSalesSetup), _dataSharingCompany));
                    }

                    inventItemSalesSetup.update();
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createParameters</Name>
				<Source><![CDATA[
    private static SysDataSharingApplicationSuiteTableEventsProcessorParameters createParameters(Common _dataSharingRecord)
    {
        SysDataSharingApplicationSuiteTableEventsProcessorParameters tableEventsProcessorParameters = SysDataSharingApplicationSuiteTableEventsProcessorParameters::construct();
        tableEventsProcessorParameters.CurrentCompany = curExt();
        tableEventsProcessorParameters.DataSharingPolicyTableNames = [tableStr(InventTable)];
        tableEventsProcessorParameters.OrigDataSharingRecord = _dataSharingRecord;
        return tableEventsProcessorParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>InventItemSalesSetup_onUpdated</Name>
				<Source><![CDATA[
    // Post Events for Data sharing of InventItemSalesSetup table 
    [DataEventHandler(tableStr(InventItemSalesSetup), DataEventType::Updated)]
    public static void InventItemSalesSetup_onUpdated(Common _dataSharingRecord, DataEventArgs e)
    {
        if (InventTableDataSharingFeature::isEnabled())
        {
            var context = SysDataSharingApplicationSuiteTableDataEventContext::current(tableStr(InventItemSalesSetup), DataEventType::Updated);
            if (!context && SysDataSharingPolicy::isSharingPolicyEnabledByCompanyAndTable(curExt(), tableStr(InventTable)))
            {
                SysDataSharingApplicationSuiteITableEventsProcessor tableEventsProcessorInventItemSalesSetup = new InventItemSalesSetup_DataSharingTableEvents();
                tableEventsProcessorInventItemSalesSetup.initializeProcessor(_dataSharingRecord);

                SysDataSharingApplicationSuiteTableEventsProcessor processor = SysDataSharingApplicationSuiteTableEventsProcessor::newTableEventProcessor(tableEventsProcessorInventItemSalesSetup);
                using (var contextUpdateCompany = SysDataSharingApplicationSuiteTableDataEventContext::newTableEvent(tableStr(InventItemSalesSetup), DataEventType::Updated))
                {
                    processor.updateCrossCompany();
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>