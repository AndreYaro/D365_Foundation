<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BarcodeCode39</Name>
	<SourceCode>
		<Declaration><![CDATA[
[BarcodeTypeFactory(BarcodeType::Code39)]
public class BarcodeCode39 extends Barcode
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>isValidAssetLabelBarcodeType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes if the bar code type is valid for fixed asset labels.
    /// </summary>
    /// <returns>true, if is valid; otherwise, false.</returns>
    public boolean isValidAssetLabelBarcodeType()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidLocationLabelBarcodeType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Describes if the bar code type is valid for location labels.
    /// </summary>
    /// <returns>true, if is valid; otherwise, false.</returns>
    public boolean isValidLocationLabelBarcodeType()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addExtraLookupFields</Name>
				<Source><![CDATA[
    public void addExtraLookupFields(SysTableLookup sysTableLookup)
    {
        sysTableLookup.addLookupfield(fieldNum(TmpFontName, Description));
    }

]]></Source>
			</Method>
			<Method>
				<Name>barcodeType</Name>
				<Source><![CDATA[
    public BarcodeType barcodeType()
    {
        return BarcodeType::Code39;
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultFont</Name>
				<Source><![CDATA[
    public FontName defaultFont()
    {
        return 'BC C39 3 to 1 HD Wide';
    }

]]></Source>
			</Method>
			<Method>
				<Name>encode</Name>
				<Source><![CDATA[
    public void encode()
    {
        if (strLen(string) > MaxStringLength - 2) // adds the start and stop character automatically
        {
            throw(error("@SYS90717"));
        }

        barcodeStr = this.encodeString(strUpr(string));

        barcodeStrHR = string;
        encoded = true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>encodeString</Name>
				<Source><![CDATA[
    BarCodeString encodeString(BarCodeString _stringIn)
    {
        BarCodeString   resultStr;
        int             len     = strLen(_stringIn);
        int             x;
        int             idx;

        if (len < 1)
        {
            return _stringIn;
        }

        // add the start character
        resultStr = num2char(42);

        for (x = 1; x <= len; x++)
        {
            idx = char2num(_stringIn, x);
            if (this.isValidChar(idx))
            {
                resultStr += (idx == 32) ? num2char(44) : subStr(_stringIn, x, 1);
            }
        }
        // add the stop code
        resultStr += num2char(42);
        return resultStr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isValidChar</Name>
				<Source><![CDATA[
    boolean isValidChar(int _charValue)
    {
        if (_charValue >= 43 && _charValue <= 57)
        {
            return true;
        }

        if (_charValue >= 65 && _charValue <= 90)
        {
            return true;
        }

        switch (_charValue)
        {
            case 32:
            case 36:
            case 37:
            case 38:    return true;
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validFontInfo</Name>
				<Source><![CDATA[
    protected container validFontInfo()
    {
        container   res;

        res += [['BC C39 3 to 1 Narrow'     ,  33, "@SYS63144"]];
        res += [['BC C39 3 to 1 Medium'     ,  65, "@SYS63144"]];
        res += [['BC C39 3 to 1 Wide'       , 100, "@SYS63144"]];
        res += [['BC C39 3 to 1 HD Narrow'  ,   8, "@SYS63144"]];
        res += [['BC C39 3 to 1 HD Medium'  ,  15, "@SYS63144"]];
        res += [['BC C39 3 to 1 HD Wide'    ,  25, "@SYS63144"]];

        res += [['BC C39 2 to 1 Narrow'     ,  33, "@SYS63145"]];
        res += [['BC C39 2 to 1 Medium'     ,  65, "@SYS63145"]];
        res += [['BC C39 2 to 1 Wide'       , 100, "@SYS63145"]];
        res += [['BC C39 2 to 1 HD Narrow'  ,   8, "@SYS63145"]];
        res += [['BC C39 2 to 1 HD Medium'  ,  15, "@SYS63145"]];
        res += [['BC C39 2 to 1 HD Wide'    ,  25, "@SYS63145"]];

        return res;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static BarcodeCode39 construct()
    {
        return new BarcodeCode39();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>