<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjFormLetterJournalPost_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(FormLetterJournalPost))]
/// <summary>
/// Extends the <c>FormLetterJournalPost</c> class to add a funding limit
/// check for project based scenarios.
/// </summary>
final class ProjFormLetterJournalPost_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>postLines</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks for over the funding limit error before posting lines.
    /// </summary>
    protected void postLines()
    {   
        if (this.sourceTable() is SalesTable && isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            SalesTable salesTable = this.sourceTable() as SalesTable;
            if (salesTable.ProjId != '' && salesTable.SalesType != SalesType::ItemReq)
            {
                try
				{
                    if (salesTable.DocumentStatus != DocumentStatus::Confirmation && salesTable.DocumentStatus != DocumentStatus::PackingSlip)
                    {
                        ttsbegin;

                        SalesLine salesLine;
                        while select salesLine
                        where salesLine.SalesId == salesTable.SalesId
                        {
                            ProjTransSalesLine projTrans = new ProjTransSalesLine(salesLine);
                            if (!ProjFundingEngine::isAmountWithinFundingLimits(projTrans, projTrans.fundingSource(), projTrans.transTurnover()))
                            {
                                throw error("@SYS344241");
                            }
                            ProjFundingLimitTrackingManager::updateUsingProjTrans(projTrans, ProjFundingLimitTrackingAction::Allocate);
                        }

						ttscommit;
                    }

				}
                catch
                {
                    ttsabort;
                }
            }
        }

        next postLines();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>