<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>OMLegalEntityEventHandler_CA</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Add event handler for OMLegalEntity form to support Canada foundation.
/// </summary>
internal class OMLegalEntityEventHandler_CA
{
    #ISOCountryRegionCodes
    private const SoftwareIdentificationCode_CA SoftwareIdentificationCode_CA = 'AX030003';
    private const LogisticsAddressCountryRegionId CountryRegion = 'CAN';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dirPartyLocation_Post_update</Name>
				<Source><![CDATA[
    /// <summary>
    /// When create or modify address, if legal entity's primary address's SoftwareIdentificationCode_CA is empty value, 
    /// set default value 'AX030003' provided by The Canada Revenue Agency.
    /// </summary>
    /// <param name="args">Data event arguments.</param>
    [Hookable(false), PostHandlerFor(tableStr(DirPartyLocation), tableMethodStr(DirPartyLocation, update))]
    public static void dirPartyLocation_Post_update(XppPrePostArgs _args)
    {
        CompanyInfo legalEntity;
        DirPartyLocation partyLocation;
        LogisticsPostalAddress postalAddress;
        DirPartyTypeView partyType;

        partyLocation = _args.getThis();
        postalAddress = DirParty::primaryPostalAddress(partyLocation.Party);

        if (postalAddress.CountryRegionId == CountryRegion)
        {
            select firstonly legalEntity
                where legalEntity.RecId == partyLocation.Party;

            OMLegalEntityEventHandler_CA::updateCompanyInfoSoftwareIdentificationCode(legalEntity);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCompanyInfoSoftwareIdentificationCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updaet company's softwareIdentificationCode_CA value to 'AX030003' provided by The Canada Revenue Agency.
    /// </summary>
    /// <param name = "_legalEntity">A <c>CompanyInfo</c> instance.</param>
    private static void updateCompanyInfoSoftwareIdentificationCode(CompanyInfo _legalEntity)
    {
        DirPartyTypeView partyType;

        select firstonly Type from partyType
            where partyType.Party == _legalEntity.RecId;

        if (partyType.Type == DirPartyType::LegalEntity)
        {
            ttsbegin;

            if (_legalEntity.SoftwareIdentificationCode_CA == '')
            {
                _legalEntity.selectForUpdate(true);
                _legalEntity.SoftwareIdentificationCode_CA = SoftwareIdentificationCode_CA;
                _legalEntity.update();
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>