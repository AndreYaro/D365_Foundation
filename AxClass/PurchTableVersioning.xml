<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchTableVersioning</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchTableVersioning</c> class provides versioning functionality for purchase orders.
/// </summary>
final class PurchTableVersioning
{

    private Common purchTableVersion;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initializePurchTableVersion</Name>
				<Source><![CDATA[
    private void initializePurchTableVersion(Common _purchTableVersion)
    {
        purchTableVersion = _purchTableVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPurchTableVersion</Name>
				<Source><![CDATA[
    [Hookable(false), Wrappable(false)]
    public Common parmPurchTableVersion()
    {
        return purchTableVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isChangeConfirmationRequired</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines whether a change to a record will require a purchase order confirmation.
    /// </summary>
    /// <param name = "_archivedPurchTableVersion">
    /// An archived <c>PurchTableVersioning</c> object to compare the current instance against.
    /// </param>
    /// <returns>
    /// true if the change requires confirmation; otherwise, false.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public boolean isChangeConfirmationRequired(PurchTableVersioning _archivedPurchTableVersion)
    {
        boolean ret = false;

        Enumerator enumerator = PurchTableDictVersioning::createInstance().versioningFieldSetsEnumerator();

        while (enumerator.moveNext() && !ret)
        {
            PurchTableIVersioningFieldSet versionFieldSet = enumerator.current() as PurchTableIVersioningFieldSet;

            ret = versionfieldset.isChangeConfirmationRequired(this, _archivedPurchTableVersion);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyVersion</Name>
				<Source><![CDATA[
    /// <summary>
    /// Copies versioned fields from a source version to the current instance.
    /// </summary>
    /// <param name = "_sourcePurchTableVersion">
    /// A <c>PurchTableVersioning</c> object containing the source version.
    /// </param>
    [Hookable(false), Wrappable(false)]
    public void copyVersion(PurchTableVersioning _sourcePurchTableVersion)
    {
        Enumerator enumerator = PurchTableDictVersioning::createInstance().versioningFieldSetsEnumerator();

        while (enumerator.moveNext())
        {
            PurchTableIVersioningFieldSet versionFieldSet = enumerator.current() as PurchTableIVersioningFieldSet;

            versionfieldset.copyVersion(this, _sourcePurchTableVersion);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>createInstance</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a <c>PurchTableVersioning</c> object based on the provided <c>PurchTableMap</c> record.
    /// </summary>
    /// <param name = "_purchTableMap">
    /// A <c>PurchTableMap</c> record to create a <c>PurchTableVersioning</c> object for.
    /// </param>
    /// <returns>
    /// The instantiated <c>PurchTableVersioning</c> object.
    /// </returns>
    [Hookable(false), Wrappable(false)]
    public static PurchTableVersioning createInstance(PurchTableMap _purchTableMap)
    {
        PurchTableVersioning instance = new PurchTableVersioning();

        instance.initializePurchTableVersion(_purchTableMap);

        return instance;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>