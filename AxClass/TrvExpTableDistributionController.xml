<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
  <Name>TrvExpTableDistributionController</Name>
  <SourceCode>
    <Declaration><![CDATA[
/// <summary>
/// The <c>TrvExpTableDistributionController</c> class is the controller class for the <c>TrvExpTableDistribution</c> report.
/// </summary>
public class TrvExpTableDistributionController extends SrsReportRunController implements BatchRetryable
{
    TrvExpTable trvExpTable;
    private static const str parmExpNumber = "ExpenseReport_ExpNumber";

}
]]></Declaration>
    <Methods>
      <Method>
        <Name>prePromptModifyContract</Name>
        <Source><![CDATA[
    /// <summary>
    /// Modifies the report query and parameters by using the caller settings.
    /// </summary>
    protected void prePromptModifyContract()
    {
        QueryBuildRange      qbrRecId;
        QueryBuildDataSource qbdsTrvExpTable;

        if (this.parmArgs().record())
        {
            this.setRecord();

            if (this.getFirstQuery())
            {
                qbdsTrvExpTable = this.getFirstQuery().dataSourceTable(tablenum(TrvExpTable));

                qbdsTrvExpTable.clearRanges();

                qbrRecId = SysQuery::findOrCreateRange(qbdsTrvExpTable, fieldnum(TrvExpTable, RecId));
                qbrRecId.value(queryValue(trvExpTable.RecId));
                qbrRecId.status(RangeStatus::Hidden);
            }

            if (this.parmReportContract() && this.parmReportContract().parmRdlContract())
            {
                this.parmReportContract().parmRdlContract().setValue(parmExpNumber, trvExpTable.ExpNumber);
            }
        }
    }

]]></Source>
      </Method>
      <Method>
        <Name>setRecord</Name>
        <Source><![CDATA[
    private void setRecord()
    {
        trvExpTable = this.parmArgs().record();
    }

]]></Source>
      </Method>
      <Method>
        <Name>main</Name>
        <Source><![CDATA[
    public static void main(Args _args)
    {
        SrsReportRunController controller = new TrvExpTableDistributionController();
        controller.parmArgs(_args);
        controller.parmReportName(ssrsReportStr(TrvExpTableDistribution, Report));
        controller.parmShowDialog(false);
        controller.startOperation();
    }

]]></Source>
      </Method>
      <Method>
        <Name>isRetryable</Name>
        <Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source>
      </Method>
    </Methods>
  </SourceCode>
</AxClass>