<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustDueAmountAnalysisService_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics365.LocalizationFramework;

/// <summary>
/// The service of Customer due amount analysis (CN) report generation
/// </summary>
class CustDueAmountAnalysisService_CN extends SysOperationServiceBase
{
    public const str ERFormatModelName = 'model';
    public const str ERModelDataSourceName = 'Contract';

    public const str  ReportName = "@AccountsReceivable:DueAmountAnalysisReportChina";

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateReportByGER</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates report by generic electronic reporting
    /// </summary>
    /// <param name = "_contract">
    /// Instance of <c>CustDueAmountAnalysisContract_CN</c>.
    /// </param>
    public void generateReportByGER(CustDueAmountAnalysisContract_CN _contract)
    {
        ERFormatMappingId					erBinding;
        CustDueAmountAnalysisDP_CN			dataProvider;
        AgingAndDueAmountTmp_CN 			custDueAmountAnalysisTmp_CN;

        dataProvider = new CustDueAmountAnalysisDP_CN();

        erBinding = _contract.parmERBinding();

        custDueAmountAnalysisTmp_CN = dataProvider.prepareData(_contract);

        AgingAndDueAmountContract_CN _reportParameter = new AgingAndDueAmountContract_CN();
        _reportParameter.parmAsOfDate(_contract.parmAsOn());

        if (custDueAmountAnalysisTmp_CN)
        {
            try
            {
                ERIModelDefinitionParamsAction parameters = new ERModelDefinitionParamsUIActionComposite()
                    .add(
                        new ERModelDefinitionDatabaseContext()
                            .addTemporaryTable(custDueAmountAnalysisTmp_CN))
                    .add(
                        new ERModelDefinitionDatabaseContext()
                            .addValue(tableNum(CompanyInfo), fieldNum(CompanyInfo, DataArea), curext()))
                    .add(
                        new ERModelDefinitionObjectParameterAction(ERFormatModelName, ERModelDataSourceName, _reportParameter))
                    .add(
                        new ERModelDefinitionInputParametersAction()
                            .addParameter('ParameterCustRange', dataProvider.parmCustRange()));

                // Call ER to generate the excel.
                ERObjectsFactory::createFormatMappingRunByFormatMappingId(erBinding, ReportName)
                    .withParameter(parameters)
                    .withFileDestination(_contract.getFileDestination())
                    .run();
            }
            catch
            {
                // An error occurred while exporting data.
                error("@SYP4861341");
            }
        }
        else
        {
            // There is no data available.
            checkFailed("@SYS300117");
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>