<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AssetDepreciationProfilesValidationBooks</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class contains the common logic for the depreciation profiles and related fields
/// validation in <c>AssetBookTable</c>, <c>AssetGroupBookSetup</c> and <c>AssetBook</c> tables
/// </summary>
public class AssetDepreciationProfilesValidationBooks extends AssetDepreciationProfilesValidation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateDepreciationProfile</Name>
				<Source><![CDATA[
    public boolean validateDepreciationProfile()
    {
        boolean ret = true;

        if (this.assetBookTable().postToGeneralLedger() == NoYes::No)
        {
            ret = ret && this.checkDepreciationProfileIsNotPolish_PL();
            ret = ret && this.checkDepreciationProfileIsNotRBSL_FR();

            ret = ret && this.checkDepreciationProfileAccrualIsNotDaily();
        }

        if (depreciationProfile && depreciationAltProfile &&
            (AssetDepreciationProfile::isProfilePolishDepreciationMethod(depreciationProfile.Method) ||
             AssetDepreciationProfile::isProfilePolishDepreciationMethod(depreciationAltProfile.Method)))
        {
            ret = ret && this.checkDepreciationProfilesMethodsForAlternativeProfile_PL();
        }
        else
        {
            ret = ret && this.checkDepreciationProfilesMethodsForAlternativeProfile();
            ret = ret && this.checkDepreciationProfileAndAlternativeProfilePropertiesMatch();
        }

        ret = ret && this.checkDepreciationProfileNotAcceleratedMethod_JP();
        ret = ret && this.checkDepreciationProfileNotSpecialDepreciationMethod_JP();
        ret = ret && this.checkDepreciationExtProfileAdditionalMethodIsAllowedByDepreciationProfile_JP();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDepreciationAltProfile</Name>
				<Source><![CDATA[
    public boolean validateDepreciationAltProfile()
    {
        boolean ret = true;

        if (this.assetBookTable().postToGeneralLedger() == NoYes::No)
        {
            ret = ret && this.checkDepreciationAltProfileIsNotPolish_PL();
            ret = ret && this.checkDepreciationAltProfileIsNotRBSL_FR();

            ret = ret && this.checkDepreciationAltProfileAccrualIsNotDaily();
        }

        if (this.assetBookTable().postToGeneralLedger() == NoYes::Yes)
        {
            if (depreciationProfile && depreciationAltProfile)
            {
                if (AssetParameters::isDepreciationCompaniesActEnable_IN())
                {
                    ret = ret && this.checkDepreciationProfileAndDepreciationAltProfileCompatibility_IN();
                }
                else if (AssetDepreciationProfile::isProfilePolishDepreciationMethod(depreciationProfile.Method) ||
                         AssetDepreciationProfile::isProfilePolishDepreciationMethod(depreciationAltProfile.Method))
                {
                    ret = ret && this.checkDepreciationProfilesMethodsForAlternativeProfile_PL();
                }
                else
                {
                    ret = ret && this.checkDepreciationProfilesMethodsForAlternativeProfile();
                }
            }
        }
        else
        {
            ret = ret && this.checkDepreciationProfilesMethodsForAlternativeProfile();
        }

        ret = ret && this.checkDepreciationProfileAndAlternativeProfilePropertiesMatch();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDepreciationExtProfile</Name>
				<Source><![CDATA[
    public boolean validateDepreciationExtProfile()
    {
        boolean ret = true;

        if (isISOJP)
        {
            ret = ret && this.checkDepreciationExtProfileMethodIsNotManualOrSpecial_JP();
            ret = ret && this.checkDepreciationExtProfileAdditionalMethodIsAllowedByDepreciationProfile_JP();
        }
        else
        {
            ret = ret && this.checkDepreciationExtProfileMethodIsNotManual();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDepreciationAcceleratedProfile_JP</Name>
				<Source><![CDATA[
    public boolean validateDepreciationAcceleratedProfile_JP()
    {
        boolean ret = true;

        ret = ret && this.checkDepreciationAcceleratedProfileMethodIsAccelerated_JP();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDepreciationConvention</Name>
				<Source><![CDATA[
    public boolean validateDepreciationConvention()
    {
        boolean ret = true;

        ret = ret && this.checkDepreciationConventionNoneForAllowedMethods();
        ret = ret && this.checkDepreciationConventionNoneForDailyAccrual();

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validatePostingLayer</Name>
				<Source><![CDATA[
    public boolean validatePostingLayer()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>assetBookTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns an <c>AssetBookTable</c> record by the value model specified.
    /// </summary>
    /// <returns>
    /// An <c>AssetBookTable</c> found.
    /// </returns>
    protected AssetBookTable assetBookTable()
    {
        AssetBookTable assetBookTable;

        if (assetDepreciationProfilesValidationMap.TableId == tableNum(AssetBookTable))
        {
            assetBookTable = assetDepreciationProfilesValidationMap;
        }

        return AssetBookTable::find(assetDepreciationProfilesValidationMap.BookId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkDepreciationProfileAndDepreciationAltProfileCompatibility_IN</Name>
				<Source><![CDATA[
    private boolean checkDepreciationProfileAndDepreciationAltProfileCompatibility_IN()
    {
        boolean ret = true;
        boolean isCompanyActEnabled = AssetParameters::isDepreciationCompaniesActEnable_IN();

        if (isCompanyActEnabled)
        {
            if ((depreciationAltProfile.Method != AssetDepreciationMethod::StraightLine              &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance           &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance125        &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance150        &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance175        &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance200        &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::StraightLinePercentage_IN &&
                 depreciationAltProfile.Method != AssetDepreciationMethod::StraightLineLifeRemaining)  ||
                (depreciationProfile.Method != AssetDepreciationMethod::StraightLine              &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance           &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance125        &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance150        &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance175        &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance200        &&
                 depreciationProfile.Method != AssetDepreciationMethod::StraightLineLifeRemaining &&
                 depreciationProfile.Method != AssetDepreciationMethod::StraightLine_HU    &&
                 depreciationProfile.Method != AssetDepreciationMethod::ReducingBalance_HU &&
                 depreciationProfile.Method != AssetDepreciationMethod::StraightLinePercentage_IN))
            {
                if (this.assetBookTable().CurrentOperationsTax == CurrentOperationsTax::Current &&
                    (depreciationProfile.Method == AssetDepreciationMethod::ReducingBalance125        ||
                     depreciationProfile.Method == AssetDepreciationMethod::ReducingBalance150        ||
                     depreciationProfile.Method == AssetDepreciationMethod::ReducingBalance175        ||
                     depreciationProfile.Method == AssetDepreciationMethod::ReducingBalance200        ||
                     depreciationProfile.Method == AssetDepreciationMethod::StraightLineLifeRemaining ||
                     depreciationProfile.Method == AssetDepreciationMethod::StraightLine)       &&
                    (depreciationAltProfile.Method != AssetDepreciationMethod::ReducingBalance  &&
                     depreciationAltProfile.Method != AssetDepreciationMethod::StraightLinePercentage_IN))
                {
                    ret = checkFailed(
                        strFmt("@GLS5959",
                            AssetDepreciationMethod::StraightLine,
                            AssetDepreciationMethod::StraightLinePercentage_IN,
                            AssetDepreciationMethod::ReducingBalance,
                            AssetDepreciationMethod::StraightLine,
                            AssetDepreciationMethod::ReducingBalance));
                }
                else
                {
                    ret = checkFailed(
                        strFmt("@GLS63376",
                            AssetDepreciationMethod::StraightLine,
                            AssetDepreciationMethod::StraightLineLifeRemaining,
                            AssetDepreciationMethod::ReducingBalance));
                }
            }

            if (this.assetBookTable().CurrentOperationsTax == CurrentOperationsTax::Current)
            {
                if (depreciationProfile.Method == AssetDepreciationMethod::ReducingBalance ||
                    depreciationProfile.Method == AssetDepreciationMethod::StraightLinePercentage_IN)
                {
                    ret = checkFailed(strFmt("@SYS71087", depreciationProfile.Method));
                }
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>