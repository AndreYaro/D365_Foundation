<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAResAssignAnalysis_ResCategory</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This derived class is used for handling worker wise assignment analysis.
/// </summary>
class PSAResAssignAnalysis_ResCategory extends PSAResAssignAnalysisBase
{
    #TimeConstants

    Query rootQuery;
    QueryBuildDataSource analysisDS;

    utcdatetime startDateTime;
    utcdatetime endDateTime;

    boolean projBase;
    private const real pecent100 = 100.0;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Builds the query for getting worker wise resource assignment analysis records.
    /// </summary>
    void buildQuery()
    {
        startDateTime = DateTimeUtil::minValue();
        endDateTime = DateTimeUtil::maxValue();

        if (m_dStartDate)
        {
            startDateTime = DateTimeUtil::newDateTime(m_dStartDate, 0);
        }

        if (m_dEndDate)
        {
            endDateTime = DateTimeUtil::newDateTime(m_dEndDate, 0);
        }

        rootQuery = new Query();

        if (m_tCommon && (m_tCommon.TableId == tableNum(ProjTable)))
        {
            projBase = true;
            ProjTable projTable = m_tCommon;
            ActivityRecId activity = ProjWBS::getRootActivityRecId(projTable.ProjId);

            analysisDS = rootQuery.addDataSource(tableNum(ResResourceAnalysisPerProjectDayView));

            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Activity))
                .value(SysQuery::value(activity));
            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, RecipientCompany))
                .value(SysQuery::value(curExt()));
            analysisDS.addRange(fieldNum(ResResourceAnalysisPerProjectDayView, Timeframe))
                .value(queryRange(startDateTime, endDateTime));
            
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategoryId));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategory));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, Capacity), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, HardLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisPerProjectDayView, SoftLocked), SelectionField::Sum);
            
            analysisDS.orderMode(OrderMode::GroupBy);
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategoryId));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, ResourceCategory));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisPerProjectDayView, Activity));
        }
        else
        {
            analysisDS = rootQuery.addDataSource(tableNum(ResResourceAnalysisDayView));
            
            analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, RecipientCompany))
                .value(SysQuery::value(curExt()));
            analysisDS.addRange(fieldNum(ResResourceAnalysisDayView, Timeframe))
                .value(queryRange(startDateTime, endDateTime));
            
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, ResourceCategoryId));
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, ResourceCategory));

            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, Capacity), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, HardLocked), SelectionField::Sum);
            analysisDS.addSelectionField(fieldnum(ResResourceAnalysisDayView, SoftLocked), SelectionField::Sum);
            
            analysisDS.orderMode(OrderMode::GroupBy);
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, ResourceCategoryId));
            analysisDS.addGroupByField(fieldnum(ResResourceAnalysisDayView, ResourceCategory));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initQuery</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes the query based on value provided for department wise resource assignment analysis.
    /// </summary>
    void initQuery()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertAssignmentAnalysis</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Calculates and populates the temporary table for worker wise assignment analysis.
    /// </summary>
    void insertAssignmentAnalysis()
    {
        m_oSummaryQueryRun = new QueryRun(rootQuery);

        ttsbegin;

        if (projBase)
        {
            this.insertRecordByActivityProcess();
        }
        else
        {
            this.insertRecordByResourceProcess();
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertRecordByActivityProcess</Name>
				<Source><![CDATA[
    private void insertRecordByActivityProcess()
    {
        real hardUtilizationPercentage, softUtilizationPercentage;
        real totalUtilizationPercentage, unscheduledHoursPercentage;
        real capacityHours, hardReservedHours, softReservedHours, unscheduledHours;

        while (m_oSummaryQueryRun.next())
        {
            ResResourceAnalysisPerProjectDayView projectAnalysis = m_oSummaryQueryRun.get(tableNum(ResResourceAnalysisPerProjectDayView));
                
            capacityHours = projectAnalysis.Capacity / #SecondsPerHour;
            hardReservedHours = projectAnalysis.HardLocked / #SecondsPerHour;
            softReservedHours = projectAnalysis.SoftLocked / #SecondsPerHour;
            unscheduledHours = capacityHours - hardReservedHours;
            if (projectAnalysis.Capacity == 0)
            {
                hardUtilizationPercentage = 0;
                softUtilizationPercentage = 0;
                totalUtilizationPercentage = 0;
                unscheduledHoursPercentage = 0;
            }
            else
            {
                hardUtilizationPercentage = hardReservedHours / capacityHours * pecent100;
                softUtilizationPercentage = softReservedHours / capacityHours * pecent100;
                totalUtilizationPercentage = hardUtilizationPercentage + softUtilizationPercentage;
                unscheduledHoursPercentage = unscheduledHours / capacityHours * pecent100;
            }

            m_tTmpResourceAssignmentAnalysis.Description = PSASchedRole::findById(projectAnalysis.ResourceCategoryId).Description;
            m_tTmpResourceAssignmentAnalysis.ResourceCategoryId = projectAnalysis.ResourceCategoryId;
            m_tTmpResourceAssignmentAnalysis.Capacity = capacityHours;
            m_tTmpResourceAssignmentAnalysis.HardCommit = hardReservedHours;
            m_tTmpResourceAssignmentAnalysis.SoftCommit = softReservedHours;
            m_tTmpResourceAssignmentAnalysis.Availability = unscheduledHours;
            m_tTmpResourceAssignmentAnalysis.PercentHard = hardUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentSoft = softUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentTotal = totalUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentAvailability = unscheduledHoursPercentage;

            m_tTmpResourceAssignmentAnalysis.insert();
        }
        
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertRecordByResourceProcess</Name>
				<Source><![CDATA[
    private void insertRecordByResourceProcess()
    {
        real hardUtilizationPercentage, softUtilizationPercentage;
        real totalUtilizationPercentage, unscheduledHoursPercentage;        
        real capacityHours, hardReservedHours, softReservedHours, unscheduledHours;
            
        while (m_oSummaryQueryRun.next())
        {
            ResResourceAnalysisDayView companyAnalysis = m_oSummaryQueryRun.get(tableNum(ResResourceAnalysisDayView));
               
            capacityHours = companyAnalysis.Capacity / #SecondsPerHour;
            hardReservedHours = companyAnalysis.HardLocked / #SecondsPerHour;
            softReservedHours = companyAnalysis.SoftLocked / #SecondsPerHour;
            unscheduledHours = capacityHours - hardReservedHours;
            if (companyAnalysis.Capacity == 0)
            {
                hardUtilizationPercentage = 0;
                softUtilizationPercentage = 0;
                totalUtilizationPercentage = 0;
                unscheduledHoursPercentage = 0;
            }
            else
            {
                hardUtilizationPercentage = hardReservedHours / capacityHours * pecent100;
                softUtilizationPercentage = softReservedHours / capacityHours * pecent100;
                totalUtilizationPercentage = hardUtilizationPercentage + softUtilizationPercentage;
                unscheduledHoursPercentage = unscheduledHours / capacityHours * pecent100;
            }

            m_tTmpResourceAssignmentAnalysis.Description = PSASchedRole::findById(companyAnalysis.ResourceCategoryId).Description;
            m_tTmpResourceAssignmentAnalysis.ResourceCategoryId = companyAnalysis.ResourceCategoryId;
            m_tTmpResourceAssignmentAnalysis.Capacity = capacityHours;
            m_tTmpResourceAssignmentAnalysis.HardCommit = hardReservedHours;
            m_tTmpResourceAssignmentAnalysis.SoftCommit = softReservedHours;
            m_tTmpResourceAssignmentAnalysis.Availability = unscheduledHours;
            m_tTmpResourceAssignmentAnalysis.PercentHard = hardUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentSoft = softUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentTotal = totalUtilizationPercentage;
            m_tTmpResourceAssignmentAnalysis.PercentAvailability = unscheduledHoursPercentage;

            m_tTmpResourceAssignmentAnalysis.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>