<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustExchRateAdjSimulationDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CustExchRateAdjSimulationDP</c> class fetches records for the
/// <c>CustExchRateAdjSimulation</c> SRSS report.
/// </summary>
[
    SRSReportQueryAttribute(querystr(CustExchAdj)),
    SRSReportParameterAttribute(classstr(CustVendExchRateAdjSimulationContract))
]
public class CustExchRateAdjSimulationDP extends CustVendExchAdjSimulationDP
{
    CustExchRateAdjSimulationTmp custExchRateAdjSimulationTmp;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addQueryRange</Name>
				<Source><![CDATA[
    protected void addQueryRange(QueryRun _queryRun)
    {
        QueryBuildRange queryBuildRange;

        queryBuildRange = _queryRun.query().dataSourceTable(tablenum(CustTrans)).addRange(fieldnum(CustTrans, TransDate));
        queryBuildRange.value(queryRange(dateNull(), this.getConsideredDate()));
        queryBuildRange = _queryRun.query().dataSourceTable(tablenum(CustTrans)).addRange(fieldnum(CustTrans, LastExchAdj));
        queryBuildRange.value(queryRange(dateNull(), this.getConsideredDate()));
        queryBuildRange.status(RangeStatus::Hidden);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOpenTransQuery</Name>
				<Source><![CDATA[
    protected Query createOpenTransQuery(Query _baseQuery)
    {
        container originalQueryContainer;
        Query openTransQuery;
        QueryBuildDataSource custTransDs;
        QueryBuildDataSource custTransOpenDs;
        
        // Pack/unpack query to get a fresh instance
        originalQueryContainer = _baseQuery.pack();
        openTransQuery = new query(originalQueryContainer);
        
        // Create a query to get open transactions based on the user-defined query
        custTransDs = openTransQuery.dataSourceTable(tableNum(CustTrans));
        
        custTransOpenDs = custTransDs.addDataSource(tableNum(CustTransOpen));
        custTransOpenDs.addLink(fieldNum(CustTrans, RecId), fieldNum(CustTransOpen, RefRecId));
        custTransDs.joinMode(JoinMode::InnerJoin);
        custTransOpenDs.fetchMode(QueryFetchMode::One2One);
        custTransOpenDs.relations(false);
        
        return openTransQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createSettledTransQuery</Name>
				<Source><![CDATA[
    protected Query createSettledTransQuery(Query _baseQuery)
    {
        container originalQueryContainer;
        Query settledTransQuery;
        QueryBuildDataSource custTransDs;
        QueryBuildDataSource custSettlementDs;
        
        // Pack/unpack query to get a fresh instance
        originalQueryContainer = _baseQuery.pack();
        settledTransQuery = new Query(originalQueryContainer);
        
        // Create a query to get settled transactions based on the user-defined query
        custTransDs = settledTransQuery.dataSourceTable(tablenum(CustTrans));
        
        custSettlementDs = custTransDs.addDataSource(tableNum(CustSettlement));
        custSettlementDs.addLink(fieldnum(CustTrans, RecId), fieldnum(CustSettlement, TransRecId));
        custSettlementDs.joinMode(JoinMode::ExistsJoin);
        custSettlementDs.addRange(fieldnum(CustSettlement, TransDate)).value(strfmt('> %1', queryValue(this.getConsideredDate())));
        custSettlementDs.addRange(fieldnum(CustSettlement, CanBeReversed)).value(queryValue(NoYes::Yes));
        custSettlementDs.addRange(fieldnum(CustSettlement, SettleAmountCur)).value(SysQuery::valueNot(0));
        custSettlementDs.addSortField(fieldnum(CustSettlement, TransDate), SortOrder::Ascending);
        custSettlementDs.fetchMode(QueryFetchMode::One2One);
        custSettlementDs.relations(false);
        
        return settledTransQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createReversedTransQuery</Name>
				<Source><![CDATA[
    protected Query createReversedTransQuery(Query _baseQuery)
    {    
        // Pack/unpack query to get a fresh instance
        container originalQueryContainer = _baseQuery.pack();
        Query reversedTransQuery = new Query(originalQueryContainer);
        
        // Create a query to get settled transactions based on the user-defined query
        QueryBuildDataSource custTransDs = reversedTransQuery.dataSourceTable(tablenum(CustTrans));

        QueryBuildDataSource custSettlementDs = custTransDs.addDataSource(tableNum(CustSettlement));
        custSettlementDs.addLink(fieldnum(CustTrans, RecId), fieldnum(CustSettlement, TransRecId));
        custSettlementDs.joinMode(JoinMode::ExistsJoin);
        custSettlementDs.addRange(fieldnum(CustSettlement, TransDate)).value(strFmt('> %1', queryValue(this.getConsideredDate())));
        custSettlementDs.addRange(fieldnum(CustSettlement, SettleAmountCur)).value(SysQuery::valueNot(0));
        custSettlementDs.addRange(fieldnum(CustSettlement, CanBeReversed)).value(queryValue(NoYes::No));
        custSettlementDs.fetchMode(QueryFetchMode::One2One);
        custSettlementDs.relations(false);

        QueryBuildDataSource subledgerVoucherGeneralJournalEntryDs = custTransDs.addDataSource(tableNum(SubledgerVoucherGeneralJournalEntry));
        subledgerVoucherGeneralJournalEntryDs.addLink(fieldnum(CustTrans, DataAreaId), fieldnum(SubledgerVoucherGeneralJournalEntry, VoucherDataAreaId));
        subledgerVoucherGeneralJournalEntryDs.addLink(fieldnum(CustTrans, Voucher), fieldnum(SubledgerVoucherGeneralJournalEntry, Voucher));
        subledgerVoucherGeneralJournalEntryDs.joinMode(JoinMode::ExistsJoin);
        subledgerVoucherGeneralJournalEntryDs.fetchMode(QueryFetchMode::One2One);

        QueryBuildDataSource generalJournalAccountEntryDs = subledgerVoucherGeneralJournalEntryDs.addDataSource(tablenum(GeneralJournalAccountEntry));
        generalJournalAccountEntryDs.addLink(fieldnum(SubledgerVoucherGeneralJournalEntry, GeneralJournalEntry), fieldNum(GeneralJournalAccountEntry, GeneralJournalEntry));
        generalJournalAccountEntryDs.joinMode(JoinMode::ExistsJoin);
        generalJournalAccountEntryDs.fetchMode(QueryFetchMode::One2One);

        QueryBuildDataSource transactionReversalDs = generalJournalAccountEntryDs.addDataSource(tableNum(TransactionReversalTrans));
        transactionReversalDs.addLink(fieldnum(GeneralJournalAccountEntry, TableId), fieldNum(TransactionReversalTrans, RefTableId));
        transactionReversalDs.addLink(fieldnum(GeneralJournalAccountEntry, RecId), fieldNum(TransactionReversalTrans, RefRecId));
        transactionReversalDs.joinMode(JoinMode::ExistsJoin);
        transactionReversalDs.fetchMode(QueryFetchMode::One2One);

        return reversedTransQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createBankChequeCancelTransQuery</Name>
				<Source><![CDATA[
    protected Query createBankChequeCancelTransQuery(Query _baseQuery)
    {
        // Pack/unpack query to get a fresh instance
        container originalQueryContainer = _baseQuery.pack();
        Query reversedTransQuery = new Query(originalQueryContainer);

        QueryBuildDataSource ledgerJournalTransDs = this.setupQueryForBankPaymentCancel(reversedTransQuery);

        QueryBuildDataSource chequeDs = ledgerJournalTransDs.addDataSource(tableNum(BankChequeTable));
        chequeDs.addLink(fieldNum(LedgerJournalTrans, TableId), fieldNum(BankChequeTable, SourceTableId));
        chequeDs.addLink(fieldNum(LedgerJournalTrans, RecId), fieldNum(BankChequeTable, SourceRecId));
        chequeDs.joinMode(JoinMode::ExistsJoin);
        chequeDs.fetchMode(QueryFetchMode::One2One);
        chequeDs.addRange(fieldNum(BankChequeTable, ChequeStatus)).value(queryValue(ChequeStatus::Cancelled));

        return reversedTransQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createBankDepositCancelTransQuery</Name>
				<Source><![CDATA[
    protected Query createBankDepositCancelTransQuery(Query _baseQuery)
    {
        // Pack/unpack query to get a fresh instance
        container originalQueryContainer = _baseQuery.pack();
        Query reversedTransQuery = new Query(originalQueryContainer);

        QueryBuildDataSource ledgerJournalTransDs = this.setupQueryForBankPaymentCancel(reversedTransQuery);

        QueryBuildDataSource depositDs = ledgerJournalTransDs.addDataSource(tableNum(BankDeposit));
        depositDs.addLink(fieldNum(LedgerJournalTrans, TableId), fieldNum(BankDeposit, SourceTableId));
        depositDs.addLink(fieldNum(LedgerJournalTrans, RecId), fieldNum(BankDeposit, SourceRecId));
        depositDs.joinMode(JoinMode::ExistsJoin);
        depositDs.fetchMode(QueryFetchMode::One2One);
        depositDs.addRange(fieldNum(BankDeposit, Canceled)).value(queryValue(NoYes::Yes));

        return reversedTransQuery;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setupQueryForBankPaymentCancel</Name>
				<Source><![CDATA[
    protected QueryBuildDataSource setupQueryForBankPaymentCancel(Query _baseQuery)
    {        
        // Create a query to get settled transactions based on the user-defined query
        QueryBuildDataSource custTransDs = _baseQuery.dataSourceTable(tablenum(CustTrans));

        QueryBuildDataSource custSettlementDs = custTransDs.addDataSource(tableNum(CustSettlement));
        custSettlementDs.addLink(fieldnum(CustTrans, RecId), fieldnum(CustSettlement, TransRecId));
        custSettlementDs.joinMode(JoinMode::ExistsJoin);
        custSettlementDs.addRange(fieldnum(CustSettlement, TransDate)).value(strFmt('> %1', queryValue(this.getConsideredDate())));
        custSettlementDs.addRange(fieldnum(CustSettlement, SettleAmountCur)).value(SysQuery::valueNot(0));
        custSettlementDs.addRange(fieldnum(CustSettlement, CanBeReversed)).value(queryValue(NoYes::No));
        custSettlementDs.fetchMode(QueryFetchMode::One2One);
        custSettlementDs.relations(false);

        QueryBuildDataSource ledgerJournalTransDs = custTransDs.addDataSource(tablenum(LedgerJournalTrans));
        ledgerJournalTransDs.addLink(fieldNum(CustTrans, RecId), fieldNum(LedgerJournalTrans, CustTransId));
        ledgerJournalTransDs.joinMode(JoinMode::InnerJoin);
        ledgerJournalTransDs.fetchMode(QueryFetchMode::One2One);

        return ledgerJournalTransDs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getConsideredDate</Name>
				<Source><![CDATA[
    protected TransDate getConsideredDate()
    {
        CustVendExchRateAdjSimulationContract contract = this.parmDataContract() as CustVendExchRateAdjSimulationContract;

        return contract.parmConsideredDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustExchRateAdjSimulationTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the data from the <c>CustExchRateAdjSimulationTmp</c> temporary table for SRS report.
    /// </summary>
    /// <returns>
    /// The <c>CustExchRateAdjSimulationTmp</c> temporary table.
    /// </returns>
    [
        SRSReportDataSetAttribute(tablestr(CustExchRateAdjSimulationTmp))
    , Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public CustExchRateAdjSimulationTmp getCustExchRateAdjSimulationTmp()
    {
        select custExchRateAdjSimulationTmp;
        return custExchRateAdjSimulationTmp;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExchangeRateDate</Name>
				<Source><![CDATA[
    protected TransDate getExchangeRateDate()
    {
        CustVendExchRateAdjSimulationContract contract = this.parmDataContract() as CustVendExchRateAdjSimulationContract;

        return contract.parmDateOfRate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getModule</Name>
				<Source><![CDATA[
    protected ModuleCustVend getModule()
    {
        return ModuleCustVend::Cust;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRevaluationMethod</Name>
				<Source><![CDATA[
    protected CustVendExchPrinciples getRevaluationMethod()
    {
        CustVendExchRateAdjSimulationContract contract = this.parmDataContract() as CustVendExchRateAdjSimulationContract;

        return contract.parmExchPrinciple();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertRecord</Name>
				<Source><![CDATA[
    protected void insertRecord(CustVendTrans _custVendTrans, AmountCur _transactionCurrencyAmount, AmountMST _currentBalance, AmountMST _newBalance, CurrencyExchangeRate _accountingCurrencyExchangeRateUsed, AmountMSTSecondary _currentBalanceReporting, AmountMSTSecondary _newBalanceReporting, CurrencyExchangeRate _reportingCurrencyExchangeRateUsed)
    {
        custExchRateAdjSimulationTmp.clear();
        custExchRateAdjSimulationTmp.AccountNum = _custVendTrans.AccountNum;
        custExchRateAdjSimulationTmp.AccountName = CustTable::find(_custVendTrans.AccountNum).name();
        custExchRateAdjSimulationTmp.CurrencyCode = _custVendTrans.CurrencyCode;
        custExchRateAdjSimulationTmp.TransDate = _custVendTrans.TransDate;
        custExchRateAdjSimulationTmp.AmountCur = _transactionCurrencyAmount;

        custExchRateAdjSimulationTmp.CurBalance = _currentBalance;
        custExchRateAdjSimulationTmp.NewBalance = _newBalance;
        custExchRateAdjSimulationTmp.ExchangeAdj = _newBalance - _currentBalance;
        custExchRateAdjSimulationTmp.ExchangeRate = _accountingCurrencyExchangeRateUsed;

        custExchRateAdjSimulationTmp.CurrentBalanceReporting = _currentBalanceReporting;
        custExchRateAdjSimulationTmp.NewBalanceReporting = _newBalanceReporting;
        custExchRateAdjSimulationTmp.ExchangeAdjReporting = _newBalanceReporting - _currentBalanceReporting;
        custExchRateAdjSimulationTmp.ExchangeRateReporting = _reportingCurrencyExchangeRateUsed;

        custExchRateAdjSimulationTmp.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves records based on the parameters entered.
    /// </summary>
    public void processReport()
    {
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>