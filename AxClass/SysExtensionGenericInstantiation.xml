<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysExtensionGenericInstantiation</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SysExtensionGenericInstantiation</c> class is a generic initialization class for subclasses without parameterless constructors.
/// </summary>
public class SysExtensionGenericInstantiation implements SysExtensionIInstantiationStrategy
{
    anytype arg1;
    anytype arg2;
    anytype arg3;
    anytype arg4;
    anytype arg5;

    int numOfArguments;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>instantiate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of the subclass.
    /// </summary>
    /// <param name="_element">
    /// The element that represents the subclass to create.
    /// </param>
    /// <returns>
    /// An instance of the subclass.
    /// </returns>
    public anytype instantiate(SysExtModelElement _element)
    {
        SysExtModelElementApp appElement = _element as SysExtModelElementApp;

        if (appElement)
        {
            SysDictClass dictClass = SysDictClass::newName(appElement.parmAppName());
            if (dictClass)
            {
                switch (numOfArguments)
                {
                    case 0:
                        return dictClass.makeObject();
                    case 1:
                        return dictClass.makeObject(arg1);
                    case 2:
                        return dictClass.makeObject(arg1, arg2);
                    case 3:
                        return dictClass.makeObject(arg1, arg2, arg3);
                    case 4:
                        return dictClass.makeObject(arg1, arg2, arg3, arg4);
                    case 5:
                        return dictClass.makeObject(arg1, arg2, arg3, arg4, arg5);
                }
            }
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(
        anytype _arg1 = 0,
        anytype _arg2 = 0,
        anytype _arg3 = 0,
        anytype _arg4 = 0,
        anytype _arg5 = 0
        )
    {
        if (!prmIsDefault(_arg1))
        {
            this.arg1 = _arg1;
            this.numOfArguments++;
        }

        if (!prmIsDefault(_arg2))
        {
            this.arg2 = _arg2;
            this.numOfArguments++;
        }

        if (!prmIsDefault(_arg3))
        {
            this.arg3 = _arg3;
            this.numOfArguments++;
        }

        if (!prmIsDefault(_arg4))
        {
            this.arg4 = _arg4;
            this.numOfArguments++;
        }

        if (!prmIsDefault(_arg5))
        {
            this.arg5 = _arg5;
            this.numOfArguments++;
        }
        
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>