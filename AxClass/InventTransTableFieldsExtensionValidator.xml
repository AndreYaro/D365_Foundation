<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventTransTableFieldsExtensionValidator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventTransTableFieldsExtensionValidator</c> class validates whether any extension fields exists for <c>InventTrans</c> table.
/// </summary>
final public class InventTransTableFieldsExtensionValidator
{    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    [Hookable(false)]
    static public InventTransTableFieldsExtensionValidator construct()
    {
        return new InventTransTableFieldsExtensionValidator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>canEnableFeature</Name>
				<Source><![CDATA[
    [Hookable(false)]
    final public boolean canEnableFeature()
    {
        boolean canEnable = true;
        Set fieldsSetFromTransArchiveGroup = InventTransArchiveSqlStatementHelper::contruct(false).getInventTransFieldsSet();
        SysDictTable sysDictTable = new SysDictTable(tableNum(InventTrans));
        int fieldCnt = sysDictTable.fieldCntWithoutSys();
        int i;
        FieldId fieldId;
        setPrefix("@SCM:InventTransTableFieldsExtensionExists");
        for (i = 1; i <= fieldCnt; i++)
        {
            fieldId = sysDictTable.fieldCnt2Id(i);
            SysDictField dictField = new SysDictField(tableNum(InventTrans), fieldId);
            if (!dictField.saveContents())
            {
                continue;
            }
            else if (dictField.isSqlRowVersion())
            {
                continue;
            }

            FieldName inventTransFieldName = fieldId2Name(tableNum(InventTrans), fieldId);
            if (!fieldsSetFromTransArchiveGroup.in(fieldId))
            {
                canEnable = checkFailed(strFmt("@SCM:ArchivingFieldsMissedInArchiveFieldsGroup",
                    inventTransFieldName, tableFieldGroupStr(InventTrans, InventoryArchiveFields)));
            }

            // Check if the fields has been merged into InventTransArchive
            FieldId inventTransArchiveFieldId = fieldName2Id(tableNum(InventTransArchive), inventTransFieldName);
            if (!inventTransArchiveFieldId)
            {
                canEnable = checkFailed(strFmt("@SCM:ArchivingFieldsMissedInInventTransArchiveTable", inventTransFieldName));
            }
                
        }

        var inventTransTableExtensionsEnumerator = Microsoft.Dynamics.Ax.Xpp.MetadataSupport::GetTableExtensionsForTable(tableStr(InventTrans)).getEnumerator();
        while (inventTransTableExtensionsEnumerator.moveNext())
        {
            Microsoft.Dynamics.AX.Metadata.MetaModel.AxTableExtension inventTransTableExtension = inventTransTableExtensionsEnumerator.get_Current();

            if (inventTransTableExtension.PropertyModifications.Count)
            {
                warning("@SCM:PropertyOfInventTransTableExtendedWarning");
                break;
            }
        }
  
        return canEnable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>