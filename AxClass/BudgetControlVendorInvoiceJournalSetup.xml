<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetControlVendorInvoiceJournalSetup</Name>
	<SourceCode>
		<Declaration><![CDATA[
[SysSetupTableAttribute(tableStr(BudgetControlSourceIntegratorEnabled))]
[SysSetupConfigKey(configurationKeyStr(LedgerAdv2BudgetCtrl))]
public final class BudgetControlVendorInvoiceJournalSetup extends SysSetupAsync implements SysSetup
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>loadData</Name>
				<Source><![CDATA[
    public void loadData()
    {
        this.insertVendorInvoiceJournalSourceIntegrator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertVendorInvoiceJournalSourceIntegrator</Name>
				<Source><![CDATA[
    private void insertVendorInvoiceJournalSourceIntegrator()
    {
        if (BudgetControlConfiguration::isControlConfigured())
        {
            // Insert the new integrator into all active and draft configurations that already have vendor invoices enabled.
            BudgetControlSourceIntegratorEnabled sourceIntegratorEnabled;
            BudgetControlSourceIntegratorEnabled sourceIntegratorEnabledNotExists;
            BudgetControlSourceIntegratorEnabled sourceIntegratorEnabledToInsert;

            ttsbegin;

            BudgetControlSourceIntegrator vendorInvoiceJournal = BudgetControlSourceIntegrator::VendorInvoiceJournal;

            insert_recordset sourceIntegratorEnabledToInsert (BudgetControlSourceIntegrator, DoBudgetCheckOnEntry, BudgetControlConfiguration)
                select vendorInvoiceJournal, DoBudgetCheckOnEntry, BudgetControlConfiguration
                from sourceIntegratorEnabled
                    where sourceIntegratorEnabled.BudgetControlSourceIntegrator == BudgetControlSourceIntegrator::VendorInvoice
                notexists join sourceIntegratorEnabledNotExists
                    where sourceIntegratorEnabledNotExists.BudgetControlSourceIntegrator == vendorInvoiceJournal
                        && sourceIntegratorEnabledNotExists.BudgetControlConfiguration == sourceIntegratorEnabled.BudgetControlConfiguration;

            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>