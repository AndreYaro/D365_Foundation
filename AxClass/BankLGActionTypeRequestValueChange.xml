<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankLGActionTypeRequestValueChange</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BankLGActionTypeRequestValueChange</c> class requests the increase value and decrease value
///    actions for the letter of guarantee.
/// </summary>
class BankLGActionTypeRequestValueChange extends BankLGActionType
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>doAction</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Does the request action "Increase value" / "Decrease value" based on the parameters.
    /// </summary>
    protected void doAction()
    {
        ttsbegin;

        // Create request action
        bankLGAction.ActionNum                          = NumberSeq::newGetNum(BankParameters::numRefBankLGTransNum()).num();
        bankLGAction.RequestAction                      = BankLGType::actionToRequestAction(actionParms.getActionType());
        bankLGAction.ExecutionAction                    = BankLGExecutionActionType::None;
        bankLGAction.RequestDateTime                    = DateTimeUtil::getSystemDateTime();
        bankLGAction.Guarantee                          = bankLGGuarantee.RecId;
        bankLGAction.BankAccountCurrencyGuaranteeAmount = actionParms.parmValueCur();
        bankLGAction.insert();

        // Update LG
        bankLGGuarantee.CurrentAction = bankLGAction.RecId;
        bankLGGuarantee.updateStatus(actionParms.getActionType());
        bankLGGuarantee.update();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateActionParms</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Validates the action parameters.
    /// </summary>
    /// <returns>
    ///     True if all parameters are valid; otherwise, false.
    /// </returns>
    protected boolean validateActionParms()
    {
        boolean ret = super();
        BankDocumentFacilityAgreementLineType bankDocumentFacilityAgreementLineType;

        if (ret)
        {
            switch (actionParms.getActionType())
            {
                case BankLGAllActionType::RequestIncreaseValue:
                    if (actionParms.parmValueCur() > bankLGGuarantee.BankAccountCurrencyGuaranteeAmount)
                    {
                        bankDocumentFacilityAgreementLineType = BankDocumentFacilityAgreementLineType::construct(
                            bankLGGuarantee.FacilityAgreementLine,
                            true,
                            false);
                        if (!bankDocumentFacilityAgreementLineType.anyRemainingBalance(actionParms.parmValueCur() - bankLGGuarantee.BankAccountCurrencyGuaranteeAmount))
                        {
                            // Facility balance is less than the requested Letter of Guarantee value amount.
                            ret = checkFailed("@SYS321801");
                        }
                    }
                    else
                    {
                        ret = false;
                    }
                    break;

                case BankLGAllActionType::RequestDecreaseValue:
                    if (actionParms.parmValueCur()      >= bankLGGuarantee.BankAccountCurrencyGuaranteeAmount
                        || actionParms.parmValueCur()   <= 0.0)
                    {
                        ret = false;
                    }
                    break;

                default:
                    ret = false;
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>