<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesTableCashFlow</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// The <c>SalesTableCashFlow</c> class is the implementation between open sales orders and cash flow.
/// </summary>
[ExportMetadata(classStr(LedgerCovIDocument), 'SalesTable'), ExportAttribute('Dynamics.AX.Application.LedgerCovIDocument')]
public class SalesTableCashFlow implements LedgerCovIDocument, LedgerCovIDocumentSingleTransactionScope
{
    private LedgerCovContext ledgerCovContext;
    private SysInstrumentationProgressLogger progressLogger;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>generateCashFlow</Name>
				<Source><![CDATA[
    /// <summary>
    /// Generates cash flow.
    /// </summary>
    /// <param name = "_allNew">
    /// Identifies when cash flow is generated for only new documents or all documents.
    /// </param>
    public void generateCashFlow(AllNew _allNew)
    {
        this.init();

        if (LedgerCovDocumentSingleTransactionScopeKillSwitchFlight::isEnabled())
        {
            this.generateCashFlowSetBased(_allNew);
        }
        else
        {
            this.generateCashFlowForProcessSet(_allNew);
            this.deleteCompletedRows();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>init</Name>
				<Source><![CDATA[
    private void init()
    {
        progressLogger = this.createProgressLogger();
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateCashFlowForProcessSet</Name>
				<Source><![CDATA[
    private void generateCashFlowForProcessSet(AllNew _allNew)
    {
        const int SetSize = 1000;

        SalesId lastSalesId;
        SalesId fromSalesId;
        SalesId toSalesId;

        Query salesIdQuery = this.buildSalesIdQuery(_allNew);
        QueryRun salesIdQueryRun = new QueryRun(salesIdQuery);
        int i = 0;

        while (salesIdQueryRun.next())
        {
            i++;

            SalesTable salesTable = salesIdQueryRun.get(tableNum(SalesTable));

            SalesId salesId = salesTable.SalesId;

            if (i == 1)
            {
                fromSalesId = salesId;
            }

            if (i == SetSize)
            {
                toSalesId = salesId;

                this.processCashFlowForProcessSet(_allNew, fromSalesId, toSalesId);

                i = 0;
                fromSalesId = '';
                toSalesId = '';
            }

            lastSalesId = salesId;

        }

        if (i > 0)
        {
            toSalesId = lastSalesId;

            this.processCashFlowForProcessSet(_allNew, fromSalesId, toSalesId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>processCashFlowForProcessSet</Name>
				<Source><![CDATA[
    private void processCashFlowForProcessSet(AllNew _allNew, SalesId _fromSalesId, SalesId _toSalesId)
    {
        #OCCRetryCount
        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextForStage(CustVendInstrumentationConstants::ActivityNameCashFlowSalesOrder))
        {
            System.Exception exception;
            RecId salesTableRecId;
            try
            {
                SalesTable salesTable;

                ttsbegin;

                LedgerCoverage ledgerCoverage = LedgerCoverage::construct(salesTable, NoYes::Yes);

                Query q = this.buildGenerateCashFlowQueryForSet(_allNew, _fromSalesId, _toSalesId);
                QueryRun qr = new QueryRun(q);
                while (qr.next())
                {
                    salesTable = qr.get(tableNum(SalesTable));
                    salesTableRecId = salesTable.RecId;
                    salesTable.updateLedgerCov(ledgerCoverage);
                }

                LedgerCov::deleteForTransTableIdAndFilter(tableNum(SalesTable), ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                ledgerCoverage.insertDatabase();

                LedgerCovDocument::deleteForTransTableIdAndFilter(tableNum(SalesTable), ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                this.insertLedgerCovDocumentRows(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                this.updateSalesTableCovStatus(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::Deadlock;
                }
                else
                {
                    int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                    sleep(xSession::currentRetryCount() * backOffWait);
                    retry;
                }
            }
            catch (Exception::TransientSqlConnectionError)
            {
                if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                {
                    retry;
                }
                else
                {
                    throw;
                }
            }

            #StandardRetryCatchBlock

            catch (exception)
            {
                this.logProcessExecutionMessage(Exception::Error, strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedError", this.getDocumentName(), this.getDocumentNum(tableNum(SalesTable), salesTableRecId), exception.Message));
                SysInstrumentationLoggerFactory::CreateLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting).logError(strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedErrorLocked", identifierStr(SalesTable), salesTableRecId, exception.Message));

                exceptionTextFallThrough();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGenerateCashFlowQueryForSet</Name>
				<Source><![CDATA[
    private Query buildGenerateCashFlowQueryForSet(AllNew _allNew, SalesId _fromSalesId, SalesId _toSalesId)
    {
        Query query = this.buildGenerateCashFlowQuery(_allNew);
        QueryBuildDataSource qbds = query.dataSourceTable(tableNum(SalesTable));

        QueryBuildRange qbr = SysQuery::findOrCreateRange(qbds, fieldNum(SalesTable, SalesId));
        qbr.value(_fromSalesId + '..' + _toSalesId);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildSalesIdQuery</Name>
				<Source><![CDATA[
    private Query buildSalesIdQuery(AllNew _allNew)
    {
        Query query = this.buildGenerateCashFlowQuery(_allNew);
        QueryBuildDataSource qbds = query.dataSourceTable(tableNum(SalesTable));

        qbds.addSelectionField(fieldNum(SalesTable, SalesId));
        qbds.addOrderByField(fieldNum(SalesTable, SalesId), SortOrder::Ascending);

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initLedgerCovContext</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public void initLedgerCovContext(LedgerCovContext _ledgerCovContext)
    {
        this.ledgerCovContext = _ledgerCovContext;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createProgressLogger</Name>
				<Source><![CDATA[
    private SysInstrumentationProgressLogger createProgressLogger()
    {
        return SysInstrumentationProgressLogger::createLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting, CustVendInstrumentationConstants::InstrumentationNamespace);
    }

]]></Source>
			</Method>
			<Method>
				<Name>logProcessExecutionMessage</Name>
				<Source><![CDATA[
    private void logProcessExecutionMessage(Exception _exceptionType, SysInfoLogStr _message, URL _helpUrl = '', SysInfoAction _sysInfoAction = null)
    {
        if (ledgerCovContext != null && ledgerCovContext.parmProcessExecutionSourceLinkRecId() != 0)
        {
            ProcessExecutionMessageLogItem messageLogItem = ProcessExecutionMessageLogItem::construct();
            messageLogItem.parmException(_exceptionType);
            messageLogItem.parmProcessExecutionSourceLink(ledgerCovContext.parmProcessExecutionSourceLinkRecId());
            messageLogItem.parmMessage(_message);

            ProcessExecutionMessageLog::insertMessage(messageLogItem);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildGenerateCashFlowQuery</Name>
				<Source><![CDATA[
    private Query buildGenerateCashFlowQuery(AllNew _allNew)
    {
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tableNum(SalesTable));

        qbds.addRange(fieldNum(SalesTable, SalesStatus)).value(SysQuery::valueNot(SalesStatus::Invoiced));

        if (_allNew == AllNew::New)
        {
            qbds.addRange(fieldNum(SalesTable, CovStatus)).value(queryValue(0));
        }

        return query;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedRows</Name>
				<Source><![CDATA[
    private void deleteCompletedRows()
    {
        #OCCRetryCount
        try
        {
            ttsbegin;

            this.deleteCompletedLedgerCovRows();
            this.deleteCompletedLedgerCovDocumentRows();
            this.deleteInvoicedRecordsFromPayPred();

            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            if (xSession::currentRetryCount() >= #RetryNum)
            {
                throw Exception::Deadlock;
            }
            else
            {
                int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                sleep(xSession::currentRetryCount() * backOffWait);
                retry;
            }
        }
        catch (Exception::TransientSqlConnectionError)
        {
            if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
            {
                retry;
            }
            else
            {
                throw;
            }
        }

        #StandardRetryCatchBlock
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedLedgerCovRows</Name>
				<Source><![CDATA[
    private void deleteCompletedLedgerCovRows()
    {
        SalesTable salesTable;
        LedgerCov ledgerCov = LedgerCov::initializeForSetBasedDelete();

        delete_from ledgerCov
                where ledgerCov.TransTableId == tableNum(SalesTable)
                    exists join salesTable
                        where (salesTable.SalesStatus == SalesStatus::Invoiced || salesTable.SalesStatus == SalesStatus::Canceled)
                            && ledgerCov.TransRecId == salesTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteCompletedLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void deleteCompletedLedgerCovDocumentRows()
    {
        SalesTable salesTable;
        LedgerCovDocument ledgerCovDocument = LedgerCovDocument::initializeForSetBasedDelete();

        delete_from ledgerCovDocument
                where ledgerCovDocument.TransTableId == tableNum(SalesTable)
                    exists join salesTable
                        where (salesTable.SalesStatus == SalesStatus::Invoiced || salesTable.SalesStatus == SalesStatus::Canceled)
                            && ledgerCovDocument.TransRecId == salesTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generateCashFlowSetBased</Name>
				<Source><![CDATA[
    private void generateCashFlowSetBased(AllNew _allNew)
    {
        #OCCRetryCount
        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextForStage(CustVendInstrumentationConstants::ActivityNameCashFlowSalesOrder))
        {
            System.Exception exception;
            RecId salesTableRecId;
            try
            {
                SalesTable salesTable;

                ttsbegin;

                LedgerCoverage ledgerCoverage = LedgerCoverage::construct(salesTable, NoYes::Yes);

                Query q = this.buildGenerateCashFlowQuery(_allNew);
                QueryRun qr = new QueryRun(q);
                while (qr.next())
                {
                    salesTable = qr.get(tableNum(SalesTable));
                    salesTableRecId = salesTable.RecId;
                    salesTable.updateLedgerCov(ledgerCoverage);
                }

                this.deleteLedgerCovRows(_allNew, ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                ledgerCoverage.insertDatabase();

                this.deleteLedgerCovDocumentRows(_allNew, ledgerCoverage.parmUpdatedCashFlowDocumentRows());
                this.insertLedgerCovDocumentRows(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                this.deleteInvoicedRecordsFromPayPred();

                this.updateSalesTableCovStatus(ledgerCoverage.parmUpdatedCashFlowDocumentRows());

                ttscommit;
            }
            catch (Exception::Deadlock)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::Deadlock;
                }
                else
                {
                    int backOffWait = RandomGenerate::construct().randomInt(50, 1000);
                    sleep(xSession::currentRetryCount() * backOffWait);
                    retry;
                }
            }
            catch (Exception::TransientSqlConnectionError)
            {
                if (ReqTransientSqlConnectionErrorHandlingHelper::retryTransientSqlConnectionError())
                {
                    retry;
                }
                else
                {
                    throw;
                }
            }

            #StandardRetryCatchBlock

            catch (exception)
            {
                this.logProcessExecutionMessage(Exception::Error, strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedError", this.getDocumentName(), this.getDocumentNum(tableNum(SalesTable), salesTableRecId), exception.Message));
                SysInstrumentationLoggerFactory::CreateLogger(CustVendInstrumentationConstants::SourceNameCashFlowForecasting).logError(strFmt("@CashFlowManagement:CashFlowGenerateUnexpectedErrorLocked", identifierStr(SalesTable), salesTableRecId, exception.Message));

                exceptionTextFallThrough();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateSalesTableCovStatus</Name>
				<Source><![CDATA[
    private void updateSalesTableCovStatus(TmpDataAreaRecIdFilter _updatedSalesTableRows)
    {
        SalesTable salesTable;

        salesTable.skipDataMethods(true);
        salesTable.skipDatabaseLog(true);
        salesTable.skipEvents(true);

        update_recordset salesTable
            setting CovStatus = 1
            exists join _updatedSalesTableRows
                where salesTable.RecId == _updatedSalesTableRows.RefRecId
                    && _updatedSalesTableRows.RefTableId == tableNum(SalesTable)
                    && _updatedSalesTableRows.RefDataAreaId == curExt();
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteLedgerCovRows</Name>
				<Source><![CDATA[
    private void deleteLedgerCovRows(AllNew _allNew, TmpDataAreaRecIdFilter _filter)
    {
        if (_allNew == AllNew::Total)
        {
            LedgerCov::deleteForTransTableId(tableNum(SalesTable));
        }
        else
        {
            LedgerCov::deleteForTransTableIdAndFilter(tableNum(SalesTable), _filter);

            SalesTable salesTable;
            LedgerCov ledgerCov = LedgerCov::initializeForSetBasedDelete();

            delete_from ledgerCov
                where ledgerCov.TransTableId == tableNum(SalesTable)
                    exists join salesTable
                        where (salesTable.SalesStatus == SalesStatus::Invoiced || salesTable.SalesStatus == SalesStatus::Canceled)
                            && ledgerCov.TransRecId == salesTable.RecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void deleteLedgerCovDocumentRows(AllNew _allNew, TmpDataAreaRecIdFilter _filter)
    {
        if (_allNew == AllNew::Total)
        {
            LedgerCovDocument::deleteForTransTableId(tableNum(SalesTable));
        }
        else
        {
            LedgerCovDocument::deleteForTransTableIdAndFilter(tableNum(SalesTable), _filter);

            SalesTable salesTable;
            LedgerCovDocument ledgerCovDocument = LedgerCovDocument::initializeForSetBasedDelete();

            delete_from ledgerCovDocument
                where ledgerCovDocument.TransTableId == tableNum(SalesTable)
                    exists join salesTable
                        where (salesTable.SalesStatus == SalesStatus::Invoiced || salesTable.SalesStatus == SalesStatus::Canceled)
                            && ledgerCovDocument.TransRecId == salesTable.RecId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteInvoicedRecordsFromPayPred</Name>
				<Source><![CDATA[
    private void deleteInvoicedRecordsFromPayPred()
    {
        if (CashflowInsightsActivation::isPayPredIntegrationActive())
        {
            Query::delete_from(this.buildDeleteRecordsRelatedToInvoiedOrdersQuery(tableNum(PayPredProjectedInvoice),
                fieldNum(PayPredProjectedInvoice, SourceTableId),
                fieldNum(PayPredProjectedInvoice, SourceRecId)), true, true, true);

            Query::delete_from(this.buildDeleteRecordsRelatedToInvoiedOrdersQuery(tableNum(PayPredPredictionResult),
                fieldNum(PayPredPredictionResult, SourceTableId),
                fieldNum(PayPredPredictionResult, SourceRecId)), true, true, true);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildDeleteRecordsRelatedToInvoiedOrdersQuery</Name>
				<Source><![CDATA[
    private Query buildDeleteRecordsRelatedToInvoiedOrdersQuery(TableId _deleteTable, FieldId _sourceTablId, FieldId _sourceRecid)
    {
        Query q = new Query();
        QueryBuildDataSource qbdsProjectedInvoice = q.addDataSource(_deleteTable);
        qbdsProjectedInvoice.addRange(_sourceTablId).value(queryValue(tableNum(SalesTable)));

        QueryBuildDataSource qbdsSalesTable = qbdsProjectedInvoice.addDataSource(tableNum(SalesTable));
        qbdsSalesTable.addLink(_sourceRecid, fieldNum(SalesTable, RecId));
        qbdsSalesTable.joinMode(JoinMode::ExistsJoin);

        qbdsSalesTable.addRange(fieldNum(SalesTable, SalesStatus)).value(queryValue(SalesStatus::Invoiced));

        return q;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertLedgerCovDocumentRows</Name>
				<Source><![CDATA[
    private void insertLedgerCovDocumentRows(TmpDataAreaRecIdFilter _filter)
    {
        LedgerCovDocument ledgerCovDocument;
        SalesTable salesTable;
        CustTable custTable;
        DirPartyTable dirPartyTable;

        TableId salesTableTableId = tableNum(SalesTable);
        TransDate transDate;
        LedgerCovDocumentType type = LedgerCovDocumentType::SalesTable;
        Voucher voucher;
        DueDate dueDate;
        CurrencyCode accountingCurrency = Ledger::accountingCurrency();

        insert_recordset ledgerCovDocument
            (TransTableId,
            TransRecId,
            TransDate,
            Type,
            AccountingCurrency,
            Number,
            Voucher,
            AccountNumber,
            DueDate,
            AccountName)
            select salesTableTableId,
                RecId,
                transDate,
                type,
                accountingCurrency,
                SalesId,
                voucher,
                CustAccount,
                dueDate
                from salesTable
            join _filter
                where salesTable.TableId == _filter.RefTableId
                    && salesTable.RecId == _filter.RefRecId
            outer join custTable
                where custTable.AccountNum == salesTable.CustAccount
            outer join Name
                from dirPartyTable
                    where dirPartyTable.RecId == custTable.Party;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentName</Name>
				<Source><![CDATA[
    /// <summary>
    /// The document name.
    /// </summary>
    /// <returns>The document name.</returns>
    public str getDocumentName()
    {
        return "@SYS58747";
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentNum</Name>
				<Source><![CDATA[
    /// <summary>
    /// The number of the document.
    /// </summary>
    /// <param name = "_tableId">Identifies the document table.</param>
    /// <param name = "_recId">Identifies the row of the document.</param>
    /// <returns>The number of the document.</returns>
    public Name getDocumentNum(TableId _tableId, RecId _recId)
    {
        Name documentNum;

        if (_tableId == tableNum(SalesTable))
        {
            SalesTable salesTable = SalesTable::findRecId(_recId);

            documentNum = salesTable.SalesId;
        }

        return documentNum;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>