<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesPurchSummaryModel_Auto</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SalesPurchSummaryModel_Auto extends SalesPurchSummaryModel_Account
{
    Num prevorderId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addRangesQuery</Name>
				<Source><![CDATA[
    QueryRun addRangesQuery(QueryRun _queryRun, Common _orderTable)
    {
        QueryRun queryRun = super(_queryRun, _orderTable);

        AutoSummary autoSummary = orderSummary.orderSummaryFields().autoSummary(_orderTable);

        queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).addRange(orderSummary.fieldNumJourAutoSummary_AutoSummary()).value(queryValue(autoSummary));

        if (!autoSummary)
            queryRun.query().dataSourceTable(orderSummary.tableNumParmTable()).addRange(orderSummary.fieldNumParmTable_OrderId()).value(queryValue(orderSummary.orderId(_orderTable)));

        return queryRun;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createNewJournal</Name>
				<Source><![CDATA[
    boolean createNewJournal(Common _orderTable)
    {
        boolean     createNewJournal;

        Common summaryOrderTable = orderSummary.summaryOrderTable();

        if (orderSummary.orderId(summaryOrderTable))
            createNewJournal = prevorderId != orderSummary.orderId(_orderTable) &&(
                                (!orderSummary.orderSummaryFields().autoSummary(_orderTable)  ||
                                (orderSummary.orderSummaryFields().autoSummary(summaryOrderTable) !=
                                 orderSummary.orderSummaryFields().autoSummary(_orderTable))));
        else
            createNewJournal = true;

        if (!createNewJournal && prevorderId != orderSummary.orderId(_orderTable))
            createNewJournal = super(_orderTable);

        if (createNewJournal)
            prevorderId = orderSummary.orderId(_orderTable);

        return createNewJournal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryBuild</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query for finding records to post.
    /// </summary>
    /// <param name="_isInBatch">
    /// A Boolean value that determines whether the <c>queryBuild</c> method is called from batch; optional.
    /// </param>
    /// <returns>
    /// The <c>QueryRun</c> object for the build query.
    /// </returns>
    QueryRun queryBuild(boolean _isInBatch = false)
    {
        container selectedFieldIdList = orderSummary.orderSummaryFields().selectedFieldIdList();

        QueryRun queryRun = super(_isInBatch);

        QueryBuildDataSource qbds = queryRun.query().dataSourceTable(orderSummary.tableNumOrderTable()).addDataSource(orderSummary.tableNumJourAutoSummary(),
                                                                             tableid2name(orderSummary.tableNumJourAutoSummary()));
        qbds.joinMode(JoinMode::OuterJoin);

        queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).fetchMode(0);

        queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).addLink(orderSummary.fieldNumOrderTable_AutoSummaryModuleType(),
                                                                                         orderSummary.fieldNumJourAutoSummary_ModuleType());

        str queryExpression =
            '(((' +
            tableid2name(orderSummary.tableNumOrderTable()) + '.' + fieldid2name(orderSummary.tableNumOrderTable(), orderSummary.fieldNumOrderTable_AutoSummaryModuleType()) +
            ' == ' +
            strfmt('%1', orderSummary.moduleAccount() + 0) +
            ') && (' +
            tableid2name(orderSummary.tableNumJourAutoSummary()) + '.' + fieldid2name(orderSummary.tableNumJourAutoSummary(),
                                                                                      orderSummary.fieldNumJourAutoSummary_CustVendAccount()) +
            ' == ' +
            tableid2name(orderSummary.tableNumOrderTable()) + '.' + fieldid2name(orderSummary.tableNumOrderTable(),
                                                                                 orderSummary.orderSummaryFields().autoSummaryFieldId()) +
            ')) || ((' +
            tableid2name(orderSummary.tableNumOrderTable()) + '.' + fieldid2name(orderSummary.tableNumOrderTable(),
                                                                                 orderSummary.fieldNumOrderTable_AutoSummaryModuleType()) +
            ' == ' +
            strfmt('%1', orderSummary.moduleOrder() + 0) +
            ') && (' +
            tableid2name(orderSummary.tableNumJourAutoSummary()) + '.' + fieldid2name(orderSummary.tableNumJourAutoSummary(),
                                                                                      orderSummary.fieldNumJourAutoSummary_OrderId()) +
            ' == ' +
            tableid2name(orderSummary.tableNumOrderTable()) + '.' + fieldid2name(orderSummary.tableNumOrderTable(),
                                                                                 orderSummary.fieldNumOrderTable_OrderId()) +
            ')))';

        QueryBuildRange qrModuleType = queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).addRange(orderSummary.fieldNumJourAutoSummary_ModuleType());
        qrModuleType.value(queryExpression);
        qrModuleType.status(RangeStatus::Hidden);
        queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).addRange(orderSummary.fieldNumJourAutoSummary_DocumentStatus()).value(
                                                                                          queryValue(orderSummary.orderSummaryFields().documentStatus()));
        queryRun.query().dataSourceTable(orderSummary.tableNumJourAutoSummary()).addSortField(orderSummary.fieldNumJourAutoSummary_AutoSummary());

        return queryRun;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>