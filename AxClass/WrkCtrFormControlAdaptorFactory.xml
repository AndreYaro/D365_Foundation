<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WrkCtrFormControlAdaptorFactory</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Factory class for constructing form control adaptors.
/// </summary>
class WrkCtrFormControlAdaptorFactory
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>constructAdaptor</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a form control adaptor based on its type.
    /// </summary>
    /// <param name="_formControl">
    /// A form control instance.
    /// </param>
    /// <returns>
    /// A form control adaptor.
    /// </returns>
    public WrkCtrFormControlAdaptor constructAdaptor(FormControl _formControl)
    {
        WrkCtrFormControlAdaptor formControlAdaptor;

        Debug::assert(_formControl != null);

        switch (this.getClassId(_formControl))
        {
            case classNum(FormRealControl) :
                formControlAdaptor = WrkCtrFormControlAdaptorReal::construct(_formControl);
                break;
            case classNum(FormIntControl) :
                formControlAdaptor = WrkCtrFormControlAdaptorInteger::construct(_formControl);
                break;
            case classNum(FormStringControl) :
                formControlAdaptor = WrkCtrFormControlAdaptorString::construct(_formControl);
                break;
            case classNum(FormComboBoxControl) :
                formControlAdaptor = WrkCtrFormControlAdaptorComboBox::construct(_formControl);
                break;
            case classNum(FormCheckBoxControl) :
                formControlAdaptor = WrkCtrFormControlAdaptorCheckBox::construct(_formControl);
                break;
            default:
                throw error(Error::wrongUseOfFunction(funcName()));
        }

        Debug::assert(formControlAdaptor != null);

        return formControlAdaptor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClassId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the class id of a form control instance.
    /// </summary>
    /// <param name="_formControl">
    /// A form control instance.
    /// </param>
    /// <returns>
    /// A class id.
    /// </returns>
    protected ClassId getClassId(FormControl _formControl)
    {
        return classIdGet(_formControl);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
        protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
        public static WrkCtrFormControlAdaptorFactory construct()
    {
        return new WrkCtrFormControlAdaptorFactory();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>