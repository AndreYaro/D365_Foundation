<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TransactionTextContextPurch</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TransactionTextContextCust</c> class enables additional parameter value functionality for
/// <c>PurchCreditNoteLedger</c>, <c>PurchCreditNoteVend</c>, <c>PurchInvoiceLedger</c>, and
/// <c>PurchInvoiceVend</c> documents.
/// </summary>
[
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, PurchCreditNoteLedger)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, PurchCreditNoteVend)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, PurchInvoiceLedger)),
SourceDocumentEnumLiteralExtension(enumStr(LedgerTransTxt), enumLiteralStr(LedgerTransTxt, PurchInvoiceVend))
]
class TransactionTextContextPurch extends TransactionTextContext
{
    PurchTable          purchTable;
    VendInvoiceJour     vendInvoiceJour;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSupportedTables</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides a list of tables supported for use with the specified transaction type.
    /// </summary>
    /// <returns>
    /// A container that contains the <c>TableId</c> collection of tables supported by specified
    /// transaction type.
    /// </returns>
    /// <remarks>
    /// A container contains only those tables that are associated with the specified transaction type.
    /// </remarks>
    public container getSupportedTables()
    {
        container   tableList;

        tableList += tableNum(PurchTable);
        tableList += tableNum(VendInvoiceJour);

        return tableList;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the parameter variable of the object to contain the table buffers used with the specified
    /// transaction type.
    /// </summary>
    /// <remarks>
    /// This must be called before you attempt to map the table fields and values for the transaction text
    /// setup parameters.
    /// </remarks>
    public void setParameters()
    {
        parameters = [purchTable, vendInvoiceJour];
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTableBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Set a particular table buffer for the specified transaction type.
    /// </summary>
    /// <param name="_common">
    /// The <c>Common</c> record buffer that is passed in and then set based on the <c>TableId</c> of the
    /// buffer.
    /// </param>
    /// <returns>
    /// true if the table is supported and buffer is saved; otherwise, false.
    /// </returns>
    /// <remarks>
    /// The common record buffer is cast to the appropriate table through switch logic. This method can be
    /// called repeatedly to assign multiple buffers.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    /// The table is not supported by this transaction type.
    /// </exception>
    public boolean setTableBuffer(Common _common)
    {
        boolean setTableBuffer;

        switch (_common.TableId)
        {
            case tableNum(PurchTable):
                purchTable = _common as PurchTable;
                setTableBuffer = true;
                break;
            case tableNum(VendInvoiceJour):
                vendInvoiceJour = _common as VendInvoiceJour;
                setTableBuffer = true;
                break;
        }

        return setTableBuffer;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>