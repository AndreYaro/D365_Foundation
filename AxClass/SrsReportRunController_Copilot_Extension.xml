<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SrsReportRunController_Copilot_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
using Microsoft.Dynamics.ApplicationPlatform.SSRSReportRuntime.Instrumentation;
using Newtonsoft.Json.Linq;

/// <summary>
/// Extension of SrsReportRunController class for Copliot integration.
/// </summary>
[ExtensionOf(classStr(SrsReportRunController))]
internal final class SrsReportRunController_Copilot_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Ensure the dialog prompt and syslast value are suppressed for Copilot scenario.
    /// </summary>
    protected void prePromptModifyContract()
    {
        next prePromptModifyContract();

        if (SrsFinanceCopilotContractUtil::useReportCopilotV2())
        {
            // Only suppress the dialog and SysLast value for Copilot scenario.
            if (this.parmArgs() && this.parmArgs().caller())
            {
                if (SrsFinanceCopilotContractUtil::isCopilotExecution(this.parmArgs().caller()))
                {
                    // Suppress dialog promp since copilot does not have an interactive UI interface.
                    // Suppress SysLast value because it will save the RDP data contract into the cache
                    // and cause the confusion from interactive UI in the same user session.
                    SSRSReportSharedEventSource::EventWriteSharedInformation("Suppress dialog prompt and syslast value for Copilot.");
                    this.parmShowDialog(false);
                    this.parmLoadFromSysLastValue(false);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates report data contract (RDP contract) value for copilot scenarios.
    /// It will ensure the RDP data contract values from copilot are used for data
    /// pre-processing stage.
    /// </summary>
    protected void preRunModifyContract()
    {
        next preRunModifyContract();

        if (SrsFinanceCopilotContractUtil::useReportCopilotV2())
        {
            if (this.parmArgs() && this.parmArgs().caller())
            {
                // Only apply the changes for Copilot scenario.
                if (SrsFinanceCopilotContractUtil::isCopilotExecution(this.parmArgs().caller()))
                {
                    var controllerContract = SrsFinanceCopilotContractUtil::setRdpDataContractFromJson(this.parmArgs().caller(), this.parmReportContract().parmRdpContract());
                    this.parmReportContract().parmRdpContract(controllerContract);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>runReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// There are two major things we need to enforce before running report for Copilot:
    ///     1. The print destination is set to File (byte arrary will be used later with Foundation support.
    ///     2. The RDP contract is overwritten if the contract value is provided from Copilot.
    /// </summary>
    public void runReport()
    {
        if (SrsFinanceCopilotContractUtil::useReportCopilotV2())
        {
            // Only apply the changes for Copilot scenario.
            if (this.parmArgs() && this.parmArgs().caller())
            {
                if (SrsFinanceCopilotContractUtil::isCopilotExecution(this.parmArgs().caller()))
                {
                    // Update print destination to file.
                    SSRSReportSharedEventSource::EventWriteSharedInformation("Update print destination to file for Copilot.");
                    this.parmReportContract().parmPrintSettings().printMediumType(SRSPrintMediumType::File);
                    this.parmReportContract().parmPrintSettings().fileFormat(SRSReportFileFormat::PDF);

                    // Set report run id for File output.
                    // Note, this is workaround for Copilot scenario because we don't have native API
                    // available in foundation layer to render the report to byte array.
                    // The report run id is used as identifier to download report from blob storage
                    // and convery to byte array in memeor.
                    // This logic can be simplified after foundation support is available.
                    this.parmReportContract().parmReportExecutionInfo().parmReportRunId(SrsFinanceCopilotContractUtil::getReportRunId(this.parmArgs().caller()));

                    // Only apply the changes for Copilot scenario.
                    if (SrsFinanceCopilotContractUtil::isCopilotExecution(this.parmArgs().caller()))
                    {
                        var controllerContract = SrsFinanceCopilotContractUtil::setRdpDataContractFromJson(this.parmArgs().caller(), this.parmReportContract().parmRdpContract());
                        this.parmReportContract().parmRdpContract(controllerContract);
                    }
                }
            }
        }

        next runReport();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>