<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>LedgerJournalCheckPostBankTransSummarization_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(LedgerJournalCheckPost))]
public final class LedgerJournalCheckPostBankTransSummarization_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>publishPostedParentJournal</Name>
				<Source><![CDATA[
    public static void publishPostedParentJournal(LedgerJournalId _postedParentJournalNum)
    {
        next publishPostedParentJournal(_postedParentJournalNum);

        BankTransSummarizationLogger logger = new BankTransSummarizationLogger();
        System.Exception ex;
        try
        {
            logger.logSummarizationStart();

            List journalNums = LedgerJournalCheckPost::getSplitPostedJournalNums(_postedParentJournalNum);
            BankTransSummarizationFacade::doSummarize(journalNums, logger);

            logger.logSummarizationSucceed();
        }
        catch (ex)
        {
            logger.logException(ex);
        }
        finally
        {
            logger.logSummarizationFinish();
            logger.finishActivity();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSplitPostedJournalNums</Name>
				<Source><![CDATA[
    internal static List getSplitPostedJournalNums(LedgerJournalId _postedParentJournalNum)
    {
        List journalNums = new List(Types::String);

        LedgerJournalTable journalHeader;
        while select JournalNum from journalHeader
            where journalHeader.ParentJournalNum == _postedParentJournalNum
                && journalHeader.Posted == NoYes::Yes
        {
            journalNums.addEnd(journalHeader.JournalNum);
        }

        return journalNums;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>