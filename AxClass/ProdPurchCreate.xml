<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdPurchCreate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class creates purchase order table record based on production purchase order parameter class.
/// </summary>
class ProdPurchCreate
{
    PurchAgreementHeader        purchAgreementHeader;
    PurchTable                  purchTable;
    ProdPurchCreateParameters   prodPurchCreateParameters;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of the <c>ProdPurchCreate</c> class.
    /// </summary>
    /// <param name = "_prodPurchCreateParameters">
    /// The instance of the <c>ProdPurchCreateParameters</c> class
    /// </param>
    /// <returns>
    /// An instance of the <c>ProdPurchCreate</c> class.
    /// </returns>
    public static ProdPurchCreate construct(ProdPurchCreateParameters _prodPurchCreateParameters)
    {
        ProdPurchCreate prodPurchCreate = new ProdPurchCreate();
        prodPurchCreate.initializeParameters(_prodPurchCreateParameters);
        return prodPurchCreate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializeParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the instance of the <c>ProdPurchCreateParameters</c> class.
    /// </summary>
    /// <param name = "_prodPurchCreateParameters">
    /// The instance of the <c>ProdPurchCreateParameters</c> class
    /// </param>
    protected void initializeParameters(ProdPurchCreateParameters _prodPurchCreateParameters)
    {
        prodPurchCreateParameters = _prodPurchCreateParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createPurchTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates purchase order table record based on <c>ProdPurchCreateParameters</c> class.
    /// </summary>
    /// <returns>
    /// The ID of the newly created purchase order record.
    /// </returns>
    public PurchId createPurchTable()
    {
        if (prodPurchCreateParameters.parmAgreementId())
        {
            purchAgreementHeader = PurchAgreementHeader::findAgreementId(prodPurchCreateParameters.parmAgreementId());
        }

        this.initializePurchTable();

        purchTable.insert(prodPurchCreateParameters.parmInterCompanySilent());

        if (purchAgreementHeader)
        {
            purchTable.associateWithPurchAgreement(purchAgreementHeader);
        }

        return purchTable.PurchId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initializePurchTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the purchase order table buffer based on the <c>ProdPurchCreateParameters</c> class.
    /// </summary>
    protected void initializePurchTable()
    {
        purchTable = ProdPurch::initPurchTable(prodPurchCreateParameters.parmVendAccount(),  
                                            prodPurchCreateParameters.parmPurchaseType(), 
                                            prodPurchCreateParameters.parmDeliveryDate(),
                                            purchAgreementHeader, 
                                            prodPurchCreateParameters.parmProjId(),
                                            prodPurchCreateParameters.parmSalesId());
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>