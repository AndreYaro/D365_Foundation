<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>IntentLetterCancelController_IT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>IntentLetterCancelController_IT</c> controller for start intent letter canceling.
/// </summary>
public class IntentLetterCancelController_IT extends SysOperationServiceController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (_args && _args.caller() is FormRun)
        {
            Common record = _args.record();

            if (record.tableId == tablenum(IntentLetter_IT))
            {
                IntentLetterCancelController_IT controller = IntentLetterCancelController_IT::construct();
                controller.parmDialogCaption("@ExtendedItallianLocalization:CancelLetter");

                IntentLetterCancelDataContract_IT contract = controller.getDataContractObject();

                List intentLetterList = new List(Types::Record);
                MultiSelectionHelper multiselectionHelper = MultiSelectionHelper::createFromCaller(_args.caller());

                IntentLetter_IT intentLetter = multiselectionHelper.getFirst();

                while (intentLetter)
                {
                    if (intentLetter.Status == IntentLetterStatus_IT::Open
                        || intentLetter.Status == IntentLetterStatus_IT::Closed)
                    {
                        intentLetterList.addEnd(intentLetter);
                    }
                    else
                    {
                        warning(strFmt("@ExtendedItallianLocalization:LeterCannotBeCanceled", intentLetter.IntentLetterId));
                    }

                    intentLetter = multiselectionHelper.getNext();
                }

                contract.parmIntentLetterList(intentLetterList);
                controller.startOperation();

                _args.record().dataSource().reSearch(true);

            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static IntentLetterCancelController_IT construct()
    {
        IdentifierName className = classStr(IntentLetterCancelService_IT);
        IdentifierName methodName = methodStr(IntentLetterCancelService_IT, runProcessing);
        SysOperationExecutionMode mode = SysOperationExecutionMode::Synchronous;

        IntentLetterCancelController_IT controller = new IntentLetterCancelController_IT(className, methodName, mode);
        return controller;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>