<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSReturnLineDetailReturnUntilDateCalculator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WHSReturnLineDetailReturnUntilDateCalculator</c> class is used to calculate the return until date for an item given a ship date.
/// </summary>
internal final class WHSReturnLineDetailReturnUntilDateCalculator
{
    private ItemId itemId;
    private WHSReturnDetailShipUTCDateTime shipDateTime;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    private void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromItemIdAndShipDate</Name>
				<Source><![CDATA[
    internal static WHSReturnLineDetailReturnUntilDateCalculator newFromItemIdAndShipDate(ItemId _itemId, WHSReturnDetailShipUTCDateTime _shipDateTime)
    {
        WHSReturnLineDetailReturnUntilDateCalculator calculator = new WHSReturnLineDetailReturnUntilDateCalculator();
        calculator.itemId = _itemId;
        calculator.shipDateTime = _shipDateTime;

        return calculator;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateReturnUntilDate</Name>
				<Source><![CDATA[
    internal WHSReturnUntilUTCDateTime calculateReturnUntilDate()
    {
        WHSReturnItemPolicyRetriever policyRetriever = WHSReturnItemPolicyRetriever::newFromItemId(itemId);
        WHSReturnItemPolicy returnItemPolicy = policyRetriever.retrieveMostSpecificPolicy();

        if (!returnItemPolicy)
        {
            return DateTimeUtil::maxValue();
        }

        return this.calculateReturnUntilDateFromReturnItemPolicy(returnItemPolicy);
    }

]]></Source>
			</Method>
			<Method>
				<Name>calculateReturnUntilDateFromReturnItemPolicy</Name>
				<Source><![CDATA[
    protected WHSReturnUntilUTCDateTime calculateReturnUntilDateFromReturnItemPolicy(WHSReturnItemPolicy _returnItemPolicy)
    {
        switch (_returnItemPolicy.ReturnAcceptance)
        {
            case WHSReturnItemPolicyReturnAcceptance::AlwaysAllowed:
                return DateTimeUtil::maxValue();
            case WHSReturnItemPolicyReturnAcceptance::NeverAllowed:
                return DateTimeUtil::minValue();
            case WHSReturnItemPolicyReturnAcceptance::AllowedDaysAfterShipment:
                return DateTimeUtil::addDays(shipDateTime, _returnItemPolicy.AllowedDaysForReturn);
            default:
                return DateTimeUtil::maxValue();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>