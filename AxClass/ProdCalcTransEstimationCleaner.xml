<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdCalcTransEstimationCleaner</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProdCalcTransEstimationCleaner</c> class is responsible for clearing production estimated values.
/// </summary>
class ProdCalcTransEstimationCleaner
{
    ProdId  prodId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmProdId</Name>
				<Source><![CDATA[
    public ProdId parmProdId(ProdId _prodId = prodId)
    {
        prodId = _prodId;

        return prodId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Clears estimated values.
    /// </summary>
    public void run()
    {
        ProdCalcTrans                   prodCalcTrans;
        PmfCoByProdCalcTrans            pmfProdCalcTrans;

        delete_from pmfProdCalcTrans
        where pmfProdCalcTrans.TransRefType == ProdTransRefType::Production
		   && pmfProdCalcTrans.TransRefId == prodId
           && pmfProdCalcTrans.RealConsump           == 0
           && pmfProdCalcTrans.RealCostAdjustment    == 0
           && pmfProdCalcTrans.RealCostAmount        == 0
           && pmfProdCalcTrans.RealQty               == 0;

        delete_from prodCalcTrans
        where prodCalcTrans.TransRefType == ProdTransRefType::Production
		   && prodCalcTrans.TransRefId == prodId
           && prodCalcTrans.RealConsump           == 0
           && prodCalcTrans.RealCostAdjustment    == 0
           && prodCalcTrans.RealCostAmount        == 0
           && prodCalcTrans.RealQty               == 0;

        update_recordset prodCalcTrans
        setting     ConsumpVariable   = 0,
                    ConsumpConstant   = 0,
                    CostAmount        = 0,
                    CostMarkup        = 0,
                    SalesAmount       = 0,
                    SalesMarkup       = 0
        where       prodCalcTrans.TransRefType == ProdTransRefType::Production
		   &&       prodCalcTrans.TransRefId == prodId;

        update_recordset pmfProdCalcTrans
        setting     ConsumpVariable   = 0,
                    ConsumpConstant   = 0,
                    CostAmount        = 0,
                    CostMarkup        = 0,
                    SalesAmount       = 0,
                    SalesMarkup       = 0
        where       pmfProdCalcTrans.TransRefType == ProdTransRefType::Production
		   &&       pmfProdCalcTrans.TransRefId == prodId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static ProdCalcTransEstimationCleaner construct()
    {
        return new ProdCalcTransEstimationCleaner();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>