<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PSAContractLineItemRangesHelper</Name>
	<SourceCode>
		<Declaration><![CDATA[
internal class PSAContractLineItemRangesHelper
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addBillingMethodRangesToDS</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method adds ranges for BillingMethod field.
    /// </summary>
    /// <param name = "_qbds">
    /// PSAContractLineItems data source
    /// </param>
    /// <param name = "_timeMaterialBillingMethod">
    /// Time and material billing method
    /// </param>
    /// <param name = "_fixedPriceBillingMethod">
    /// Fixed price billing method
    /// </param>
    internal static void addBillingMethodRangesToDS(
        QueryBuildDataSource _qbds,
        boolean _timeMaterialBillingMethod,
        boolean _fixedPriceBillingMethod)
    {
        if (_timeMaterialBillingMethod)
        {
            _qbds.addRange(fieldNum(PSAContractLineItems, BillingMethod)).value(queryValue(ProjContractBillingMethod::TimeMaterial));
        }

        if (_fixedPriceBillingMethod)
        {
            _qbds.addRange(fieldNum(PSAContractLineItems, BillingMethod)).value(queryValue(ProjContractBillingMethod::FixedPrice));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addTransactionTypeRangesToDS</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method adds ranges for transaction types fieds.
    /// </summary>
    /// <param name = "_qbds">
    /// PSAContractLineItems data source
    /// </param>
    /// <param name = "_hour">
    /// Include hour transactions
    /// </param>
    /// <param name = "_expenses">
    /// Include expense transactions
    /// </param>
    /// <param name = "_fee">
    /// Include fee transactions
    /// </param>
    /// <param name = "_item">
    /// Include item transactions
    /// </param>
    /// <param name = "_indirectCost">
    /// Include inderect costs transactions
    /// </param>
    internal static void addTransactionTypeRangesToDS(
        QueryBuildDataSource _qbds,
        boolean _hour,
        boolean _expenses,
        boolean _fee,
        boolean _item,
        boolean _indirectCost)
    {
        container transTypes;
        int conPos;

        if (_hour)
        {
            conPos++;
            transTypes = PSAContractLineItems::addRangeToCon(
                    _qbds.name(),
                    fieldStr(PSAContractLineItems, IncludeHourTrans),
                    queryValue(1),
                    transTypes,
                    conPos);
        }

        if (_expenses)
        {
            conPos++;
            transTypes = PSAContractLineItems::addRangeToCon(
                    _qbds.name(),
                    fieldStr(PSAContractLineItems, IncludeExpenseTrans),
                    queryValue(1),
                    transTypes,
                    conPos);
        }

        if (_fee)
        {
            conPos++;
            transTypes = PSAContractLineItems::addRangeToCon(
                    _qbds.name(),
                    fieldStr(PSAContractLineItems, IncludeFeeTrans),
                    queryValue(1),
                    transTypes,
                    conPos);
        }

        if (_item)
        {
            conPos++;
            transTypes = PSAContractLineItems::addRangeToCon(
                    _qbds.name(),
                    fieldStr(PSAContractLineItems, IncludeItemTrans),
                    queryValue(1),
                    transTypes,
                    conPos);
        }

        if (_indirectCost)
        {
            conPos++;
            transTypes = PSAContractLineItems::addRangeToCon(
                _qbds.name(),
                fieldStr(PSAContractLineItems, IncludeIndirectCost),
                queryValue(1),
                transTypes,
                conPos);
        }

        if (conLen(transTypes) > 0)
        {
            _qbds.addRange(fieldNum(PSAContractLineItems, IncludeHourTrans)).value(
                    strFmt('(%1)', con2Str(transTypes, ' || ')));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>