<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ReqSetupDimBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// A builder class for constructing a <c>ReqSetupDim</c> instance.
/// </summary>
class ReqSetupDimBuilder
{
    private ReqSetup            reqSetup;
    private ReqCovInventDimId   covInventDimId;
    private InventDim           covInventDim;
    private InventTable         inventTable;
    private InventDimId         inventDimId;
    private InventDim           inventDim;

    public List distinctInventDimFlagsList;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>reqSetup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets a <c>ReqSetup</c> instance.
    /// </summary>
    /// <param name = "_reqSetup">A <c>ReqSetup</c> instance.</param>
    /// <returns>A <c>ReqSetup</c> instance.</returns>
    public ReqSetup reqSetup(ReqSetup _reqSetup = reqSetup)
    {
        reqSetup = _reqSetup;

        if (!reqSetup)
        {
            reqSetup = ReqSetup::newItemId(inventTable ? inventTable.ItemId : '', inventTable);
        }
        
        return reqSetup;
    }

]]></Source>
			</Method>
			<Method>
				<Name>covInventDimId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets an <c>InventDimId</c> for the coverage dimension.
    /// </summary>
    /// <param name = "_covInventDimId">An <c>InventDimId</c>.</param>
    /// <returns>An <c>InventDimId</c>.</returns>
    public InventDimId covInventDimId(InventDimId _covInventDimId = covInventDimId)
    {
        covInventDimId = _covInventDimId;

        if (!covInventDimId)
        {
            covInventDimId = this.covInventDim().inventDimId;
        }

        return covInventDimId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>covInventDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the coverage dimension.
    /// </summary>
    /// <returns>An <c>InventDim</c> record.</returns>
    public InventDim covInventDim()
    {
        if (!covInventDim)
        {
            if (covInventDimId)
            {
                covInventDim = InventDim::find(covInventDimId);
            }
            else 
            {
                if (!inventDim && inventDimId)
                {
                    covInventDim = InventDim::find(inventDimId).data();
                }
                else
                {
                    covInventDim = inventDim.data();
                }

                covInventDim.clearNotCovPrDim(this.reqSetup().inventDimGroupSetup());
                covInventDim = InventDim::findOrCreate(covInventDim);
            }
        }

        return covInventDim;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventTable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets an <c>inventTable</c> record.
    /// </summary>
    /// <param name = "_inventTable">An <c>inventTable</c> record.</param>
    /// <returns>An <c>inventTable</c> record.</returns>
    public InventTable inventTable(InventTable _inventTable = inventTable)
    {
        inventTable = _inventTable;
        return inventTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventDimId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets an <c>InventDimId</c>.
    /// </summary>
    /// <param name = "_inventDimId">An <c>InventDimId</c>.</param>
    /// <returns>An <c>InventDimId</c>.</returns>
    public InventDimId inventDimId(InventDimId _inventDimId = inventDimId)
    {
        inventDimId = _inventDimId;
        return inventDimId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>inventDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets or sets an <c>InventDim</c> record.
    /// </summary>
    /// <param name = "_inventDim">An <c>InventDim</c> record.</param>
    /// <returns>An <c>InventDim</c> record.</returns>
    public InventDim inventDim(InventDim _inventDim = inventDim)
    {
        inventDim = _inventDim;
        return inventDim;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>