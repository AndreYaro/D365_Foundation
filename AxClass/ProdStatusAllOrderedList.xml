<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProdStatusAllOrderedList</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProdStatusAllOrderedList</c> class represents the ordered list of enumeration values for <c>ProdStatusAll</c> enum.
/// Allows to add values to the list to support extension.
/// </summary>
public class ProdStatusAllOrderedList implements Enumerable
{
    private List prodStatusAllOrderedList;
	  
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getEnumerator</Name>
				<Source><![CDATA[
	/// <summary>
    /// Get the enumerator of the ordered list.
    /// </summary>
    /// <returns>An instance of <c>ListEnumerator</c> class.</returns>
    public Enumerator getEnumerator()
    {
        return prodStatusAllOrderedList.getEnumerator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>addAfter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>ProdStatusAll</c> enum to the ordered list after the specified value.
    /// </summary>
    /// <param name = "_newProdStatusAllValue">Value to be added.</param>
    /// <param name = "_addAfterValue">Value to add after.</param>
    /// <exception>Could not find _addAfterValue in the ordered list.</exception>
    public void addAfter(ProdStatusAll _newProdStatusAllValue, ProdStatusAll _addAfterValue)
    {
        ListIterator listIterator = new ListIterator(prodStatusAllOrderedList);
        while (listIterator.more())
        {
            if (listIterator.value() == _addAfterValue)
            {
                listIterator.next();
                listIterator.insert(_newProdStatusAllValue);
                return;
            }
            listIterator.next();
        }
        throw error(strFmt("@SCM:CouldNotAddValueXAfterValueY_YnotFound", _newProdStatusAllValue, _addAfterValue));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addStart</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>ProdStatusAll</c> enum to the start of the ordered list.
    /// </summary>
    /// <param name = "_prodStatusAll">Value to be added.</param>
    public void addStart(ProdStatusAll _prodStatusAll)
    {
        prodStatusAllOrderedList.addStart(_prodStatusAll);
    }

]]></Source>
			</Method>
			<Method>
				<Name>addEnd</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add a new value of <c>ProdStatusAll</c> enum to the end of the ordered list.
    /// </summary>
    /// <param name = "_prodStatusAll">Value to be added.</param>
    public void addEnd(ProdStatusAll _prodStatusAll)
    {
        prodStatusAllOrderedList.addEnd(_prodStatusAll);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        prodStatusAllOrderedList = new List(Types::Enum);
        this.initialize();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static ProdStatusAllOrderedList construct()
    {
        return new ProdStatusAllOrderedList();        
    }

]]></Source>
			</Method>
			<Method>
				<Name>initialize</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize the list with all the applicable values of <c>ProdStatusAll</c> enumeration in ascending order.
    /// </summary>    
    protected void initialize()
    {
        this.addEnd(ProdStatusAll::Created);
        this.addEnd(ProdStatusAll::CostEstimated);
        this.addEnd(ProdStatusAll::OperationScheduled);
        this.addEnd(ProdStatusAll::JobScheduled);
        this.addEnd(ProdStatusAll::Released);
        this.addEnd(ProdStatusAll::StartUp);
        this.addEnd(ProdStatusAll::ReportedFinished);
        this.addEnd(ProdStatusAll::Completed);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>