<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ComplementaryInvoiceCreator</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ComplementaryInvoiceCreator</c> class creates a new complementary invoice for a fiscal
/// document.
/// </summary>
abstract class ComplementaryInvoiceCreator
{
    FiscalDocument_BR fiscalDocToBeComplemented;
    InvoiceComplementaryType_BR complementaryType;
    FiscalDocumentLine_BR fiscalDocumentLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createComplementaryInvoiceHeader</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new complementary invoice.
    /// </summary>
    /// <returns>
    /// New complementary invoice record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected abstract Common createComplementaryInvoiceHeader()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>createComplementaryInvoiceLine</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a complementary invoice line.
    /// </summary>
    /// <returns>
    /// A new complementary invoice line record.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected abstract Common createComplementaryInvoiceLine()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFormName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the form name used to present the created complementary invoice to the user.
    /// </summary>
    /// <returns>
    /// Form name used to present the complementary invoice.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public abstract FormName getFormName()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the <c>ComplementaryInvoiceCreator</c> class.
    /// </summary>
    /// <param name="_fiscalDocToBeComplemented">
    /// The <c>FiscalDocument_BR</c> table buffer of the fiscal document to be complemented.
    /// </param>
    /// <param name="_complementaryType">
    /// An enum value representing the type of the complemntary invoice.
    /// </param>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    protected void new(FiscalDocument_BR _fiscalDocToBeComplemented, InvoiceComplementaryType_BR _complementaryType)
    {
        fiscalDocToBeComplemented = _fiscalDocToBeComplemented;
        complementaryType = _complementaryType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmComplementaryType</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public InvoiceComplementaryType_BR parmComplementaryType()
    {
        return complementaryType;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmComplementedFiscalDocLine</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public FiscalDocumentLine_BR parmComplementedFiscalDocLine()
    {
        return fiscalDocumentLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmComplementedFiscalDocument</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public FiscalDocument_BR parmComplementedFiscalDocument()
    {
        return fiscalDocToBeComplemented;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new complementary invoice.
    /// </summary>
    /// <returns>
    /// The record that corresponds to the header of the new invoice.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public Common run()
    {
        Common complementaryInvoice;

        ttsBegin;

        if (!fiscalDocToBeComplemented.canBeComplemented())
        {
            throw error("@GLS222020");
        }

        complementaryInvoice = this.createComplementaryInvoiceHeader();

        while select fiscalDocumentLine
            where fiscalDocumentLine.FiscalDocument == fiscalDocToBeComplemented.RecId
        {
            this.createComplementaryInvoiceLine();
        }

        ttsCommit;

        return complementaryInvoice;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Constructs a new instance of a complementary invoice creator.
    /// </summary>
    /// <param name="_fiscalDocToBeComplemented">
    /// The <c>FiscalDocument_BR</c> table buffer of the fiscal document to be complemented.
    /// </param>
    /// <param name="_complementaryType">
    /// An enum value representing the type of the complemntary invoice.
    /// </param>
    /// <returns>
    /// An instance of a complementary invoice creator.
    /// </returns>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static ComplementaryInvoiceCreator construct(FiscalDocument_BR _fiscalDocToBeComplemented, InvoiceComplementaryType_BR _complementaryType)
    {
        ComplementaryInvoiceCreator creator;

        switch (_fiscalDocToBeComplemented.FiscalDocumentAccountType)
        {
            case CustVendType_BR::Customer:
                creator = new SalesComplementaryInvoiceCreator(_fiscalDocToBeComplemented, _complementaryType);
                break;

            case CustVendType_BR::Vendor:
                creator = new PurchComplementaryInvoiceCreator(_fiscalDocToBeComplemented, _complementaryType);
                break;
        }
        return creator;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>