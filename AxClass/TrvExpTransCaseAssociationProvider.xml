<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TrvExpTransCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'TrvExpTrans'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class TrvExpTransCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
        public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
        TrvExpTrans trvExpTrans;
        select crosscompany CreatingWorker from trvExpTrans where trvExpTrans.RecId == _caseAssociation.RefRecId;

        if (trvExpTrans)
        {
            _caseAssociation.insertCaseAssociation(CaseEntityType::Employee, trvExpTrans.CreatingWorker);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        TrvExpTrans trvExpTrans;
        select crosscompany DataAreaId from trvExpTrans where trvExpTrans.RecId == _parentRecId;

        return trvExpTrans.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        TrvExpTrans trvExpTrans;
        select crosscompany ExpNumber from trvExpTrans where trvExpTrans.RecId == _parentRecId;

        return trvExpTrans.ExpNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        TrvExpTrans trvExpTrans;
        select RecId from trvExpTrans where trvExpTrans.ExpNumber == _naturalKey;

        if (!trvExpTrans)
        {
            // Since this table doesn't have a single field natural key
            // RecId might be sent instead.  See performLookup().
            select RecId from trvExpTrans where trvExpTrans.RecId == str2Int64(_naturalKey);
        }

        return trvExpTrans.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        TrvExpTrans trvExpTrans;
        select crosscompany ExpNumber from trvExpTrans where trvExpTrans.RecId == _parentRecId;

        return HcmWorker::worker2Name(trvExpTrans.CreatingWorker);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        TrvExpTrans trvExpTrans;
        
        //We need this code in order not to lose the localization data in the opened form.
        TrvExpTrans trvExpTransCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from trvExpTransCrossCompany
            where trvExpTransCrossCompany.RecId == _association.RefRecId;

        if (trvExpTransCrossCompany.Recid)
        {
            changecompany (trvExpTransCrossCompany.DataAreaId)
            {
                select firstonly trvExpTrans where trvExpTrans.RecId == _association.RefRecId;
            }
        }

        _args.record(trvExpTrans);

        return menuitemdisplaystr(TrvExpenses);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(TrvExpTrans), callerControl);

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(TrvExpTrans));
        qbds.addRange(fieldNum(TrvExpTrans, TrvExpTable)).value("!0");
        sysTableLookup.addLookupfield(fieldNum(TrvExpTrans, RecId), true);
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, ExpNumber));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, CreatingWorker));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, CostType));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, ProjId));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, PayMethod));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, TransDate));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, ExchangeCode));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, AmountCurr));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, AmountMST));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, TrvLocations));
        sysTableLookup.addLookupfield(fieldnum(TrvExpTrans, OriginalReceiptsRecvd));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(TrvExpTrans))
        {
            _eventArgs.parmEntityType(CaseEntityType::TrvExpTrans);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>