<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'Customer'),ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class CustTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        CustTable custTable;
        select crosscompany DataAreaId from custTable where custTable.RecId == _parentRecId;

        return custTable.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        CustTable custTable;
        select crosscompany AccountNum from custTable where custTable.RecId == _parentRecId;

        return custTable.AccountNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        CustTable custTable;
        select RecId from custTable where custTable.AccountNum == _naturalKey;

        return custTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        CustTable custTable;
        select crosscompany * from custTable where custTable.RecId == _parentRecId;

        return custTable.name();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        CustTable custTable;
        
        //We need this code in order not to lose the localization data in the opened form.
        CustTable custTableCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from custTableCrossCompany
            where custTableCrossCompany.RecId == _association.RefRecId;

        if (custTableCrossCompany.Recid)
        {
            changecompany (custTableCrossCompany.DataAreaId)
            {
                select firstonly custTable where custTable.RecId == _association.RefRecId;
            }
        }

        _args.record(custTable);
        return menuitemdisplaystr(CustTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(CustTable), callerControl, true,
                        CustomerDataInteractorFactory::useExistingOrCreateResolver(callerControl));

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(CustTable));

        sysTableLookup.addLookupfield(fieldnum(CustTable, AccountNum));
        sysTableLookup.addLookupfield(fieldnum(CustTable, Party));
        sysTableLookup.addLookupMethod('nameAlias');
        sysTableLookup.addLookupfield(fieldnum(CustTable, OurAccountNum));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(CustTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::Customer);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>