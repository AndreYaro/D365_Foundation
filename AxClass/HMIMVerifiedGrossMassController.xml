<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>HMIMVerifiedGrossMassController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>HMIMVerifiedGrossMassController</c> class is used to run controller of the <c>HMIMVerifiedGrossMass</c> report.
/// </summary>
public class HMIMVerifiedGrossMassController extends SrsReportRunController implements BatchRetryable
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>preRunModifyContract</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the <c>SRSReportParameters</c> values to the report contract.
    /// </summary>
    protected void preRunModifyContract()
    {
        super();

        Query query = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = query.dataSourceTable(tableNum(WHSShipmentTable));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(WHSShipmentTable, ShipmentId));

        if (this.parmArgs().parm())
        {
            queryBuildRange.value(queryValue(this.parmArgs().parm()));
        }
        else if (this.parmArgs().record() && this.parmArgs().dataset() == tableNum(WHSShipmentTable))
        {
            WHSShipmentTable whsShipmentTable = this.parmArgs().record();
            queryBuildRange.value(whsShipmentTable.ShipmentId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initailzes the controller, assigns parameter values and runs the SRS report.
    /// </summary>
    /// <param name="_args">
    /// The <c>Args</c> object.
    /// </param>
    public static void main(Args _args)
    {
        if (_args.record() && _args.dataset() == tableNum(WHSShipmentTable))
        {
            HMIMVerifiedGrossMassController verifiedGrossMassController = new HMIMVerifiedGrossMassController();
            verifiedGrossMassController.parmArgs(_args);
            verifiedGrossMassController.parmReportName(ssrsReportStr(HMIMVerifiedGrossMass, Report));
            verifiedGrossMassController.parmShowDialog(false);
            verifiedGrossMassController.startOperation();
        }
        else if (_args.record() 
                && _args.dataset() == tableNum(WHSLoadTable))
        {
            WHSLoadTable loadTable = _args.record();
            WHSShipmentTable shipmentTable;
            Args newArgs = Args::createFromArgs(_args);
            
            while select shipmentTable
                where shipmentTable.LoadId  ==  loadTable.LoadId
            {
                HMIMVerifiedGrossMassController verifiedGrossMassController = new HMIMVerifiedGrossMassController();
                verifiedGrossMassController.parmArgs(newArgs).record(shipmentTable);
                verifiedGrossMassController.parmReportName(ssrsReportStr(HMIMVerifiedGrossMass, Report));
                verifiedGrossMassController.parmShowDialog(false);
                verifiedGrossMassController.startOperation();
            }
        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>