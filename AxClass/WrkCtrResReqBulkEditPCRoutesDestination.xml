<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WrkCtrResReqBulkEditPCRoutesDestination</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>WrkCtrResReqBulkEditPCRoutesDestination</c> class is the product configurator routes
/// destination.
/// </summary>
class WrkCtrResReqBulkEditPCRoutesDestination extends WrkCtrResReqEntityTypeDestination
{
    str oprId;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves the destination description.
    /// </summary>
    /// <returns>
    /// The destination description.
    /// </returns>
    public str description()
    {
        return "@SYS335074";
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes an instance of the <c>WrkCtrResReqBulkEditPCRoutesDestination</c> class.
    /// </summary>
    /// <param name="_oprId">
    /// The operation ID.
    /// </param>
    public void new(str _oprId)
    {
        oprId = _oprId;
        super(WrkCtrActivityType::PCRouteOperation);
    }

]]></Source>
			</Method>
			<Method>
				<Name>onFilteringActivity</Name>
				<Source><![CDATA[
    /// <summary>
    /// Modifies a query to find the appropriate destination.
    /// </summary>
    /// <param name="_query">
    /// The query in which to modify.
    /// </param>
    public void onFilteringActivity(Query _query)
    {
        QueryBuildRange         queryBuildRange;
        QueryBuildDataSource    queryBuildDataSource;
        QueryBuildDataSource    ecoResTextValueDatasource;

        if (oprId)
        {
            queryBuildDataSource    = SysQuery::findOrCreateDataSource(_query, tablenum(WrkCtrActivity));
            queryBuildDataSource    = queryBuildDataSource.addDataSource(tablenum(WrkCtrPCRouteOperationActivity));
            queryBuildDataSource.joinMode(JoinMode::ExistsJoin);
            queryBuildDataSource.relations(true);

            queryBuildDataSource    = queryBuildDataSource.addDataSource(tablenum(PCTemplateComponent));
            queryBuildDataSource.joinMode(JoinMode::ExistsJoin);
            queryBuildDataSource.relations(true);
            
            if (WrkCtrResReqBulkEditPCRoutesDestinationFilterLegalEntityFlight::instance().isEnabled())
            {
                queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldnum(PCTemplateComponent, LegalEntity));
                queryBuildRange.value(queryValue(CompanyInfo::current()));
            }

            queryBuildDataSource    = queryBuildDataSource.addDataSource(tablenum(PCTemplateConstant));
            queryBuildDataSource.joinMode(JoinMode::ExistsJoin);
            queryBuildDataSource.relations(true);

            ecoResTextValueDatasource = queryBuildDataSource.addDataSource(tablenum(EcoResTextValue));
            ecoResTextValueDatasource.joinMode(JoinMode::ExistsJoin);
            ecoResTextValueDatasource.relations(true);

            queryBuildRange         = SysQuery::findOrCreateRange(ecoResTextValueDatasource, fieldnum(EcoResTextValue, TextValue));
            queryBuildRange.value(oprId);

            queryBuildDataSource    = queryBuildDataSource.addDataSource(tablenum(PCTemplateAttribute));
            queryBuildDataSource.joinMode(JoinMode::ExistsJoin);
            queryBuildDataSource.relations(true);

            queryBuildRange         = SysQuery::findOrCreateRange(queryBuildDataSource, fieldnum(PCTemplateAttribute, ReferencedFieldId));
            queryBuildRange.value(queryValue(fieldnum(RouteOpr, OprId)));

            queryBuildDataSource    = queryBuildDataSource.addDataSource(tablenum(PCTemplate));
            queryBuildDataSource.joinMode(JoinMode::ExistsJoin);
            queryBuildDataSource.relations(true);

            queryBuildRange         = SysQuery::findOrCreateRange(queryBuildDataSource, fieldnum(PCTemplate, ReferencedTableId));
            queryBuildRange.value(queryValue(tableNum(RouteOpr)));
        }

        super(_query);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>