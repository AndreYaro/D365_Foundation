<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MainAccountLegalEntityTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension to the <c>MainAccountLegalEntity</c> table.
/// </summary>
public static class MainAccountLegalEntityTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkTaxCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if a tax code is valid for a legal entity.
    /// </summary>
    /// <param name = "_mainAccountLegalEntityId">The <c>MainAccountLegalEntity</c> record.</param>
    /// <param name = "_taxCode">The TaxCode value.</param>
    /// <returns></returns>
    public static boolean checkTaxCode(recId _mainAccountLegalEntityId, TaxCode _taxCode)
    {
        boolean                 result = true;
        MainAccountLegalEntity  mainAccountLegalEntity;

        if (_mainAccountLegalEntityId)
        {
            mainAccountLegalEntity = MainAccountLegalEntity::find(_mainAccountLegalEntityId);

            result = MainAccountLegalEntityTable_Extension::checkTaxCodeValue(mainAccountLegalEntity, _taxCode);
        }

        return result;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkTaxCodeValue</Name>
				<Source><![CDATA[
    private static boolean checkTaxCodeValue(MainAccountLegalEntity _mainAccountLegalEntity, TaxCode _taxCode)
    {
        switch (_mainAccountLegalEntity.ValidateTaxCode)
        {
            case FieldControl::FillIn:
                if (!_taxCode)
                {
                    return checkFailed(strFmt("@SYS138949", MainAccount::find(_mainAccountLegalEntity.MainAccount).MainAccountId));
                }
                break;
            case FieldControl::Table:
                if (_taxCode != _mainAccountLegalEntity.TaxCode)
                {
                    return checkFailed(strFmt("@SYS138950", _mainAccountLegalEntity.TaxCode, MainAccount::find(_mainAccountLegalEntity.MainAccount).MainAccountId));
                }
                break;
            case FieldControl::List:
                if (_taxCode != _mainAccountLegalEntity.TaxCode && !MainAccountControlTaxCode::exist(_mainAccountLegalEntity.MainAccount, _taxCode))
                {
                    return checkFailed(strFmt("@SYS138951", MainAccount::find(_mainAccountLegalEntity.MainAccount).MainAccountId));
                }
                break;
            default:
        }

        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>