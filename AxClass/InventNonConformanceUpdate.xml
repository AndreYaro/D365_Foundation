<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventNonConformanceUpdate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>InventNonConformanceUpdate</c> class is the base class for several nonconformance update
///    classes.
/// </summary>
abstract class InventNonConformanceUpdate extends RunBase
{
    InventNonConformanceID      inventNonConformanceID;
    HcmWorkerRecId              worker;

    #define.CurrentVersion(2)
    #LOCALMACRO.CurrentList
        inventNonConformanceID,
        worker
    #ENDMACRO
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the class based on the calling object.
    /// </summary>
    /// <param name="_args">
    ///    A standard <c>Args</c> value from the calling element.
    /// </param>
    /// <exception cref="M:Exception::Error">
    ///    The caller does not provide a <c>InventNonConformanceTable</c> table buffer.
    /// </exception>
    /// <exception cref="M:Exception::Error">
    ///    The current user is not mapped to a worker.
    /// </exception>
    protected void initFromArgs(Args _args)
    {
        if (!_args || !_args.record() || !_args.record().RecId || _args.dataset() != tableNum(InventNonConformanceTable))
            throw error(strFmt("@SYS19306", funcName()));

        InventNonConformanceTable inventNonConformanceTable = _args.record();

        this.getLast();
        this.parmInventNonConformanceId(inventNonConformanceTable.InventNonConformanceID);

        worker = DirPersonUserEx::findUserWorkerReference();
        if (!worker)
            throw error("@SYS80637");
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventNonConformanceId</Name>
				<Source><![CDATA[
    public InventNonConformanceID parmInventNonConformanceId(InventNonConformanceID _inventNonConformanceID = inventNonConformanceID)
    {
        inventNonConformanceID = _inventNonConformanceID;

        return inventNonConformanceID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    boolean unpack(container packedClass)
    {
        Integer     version      = RunBase::getVersion(packedClass);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Validates that the parameters provided are valid for the update.
    /// </summary>
    /// <param name="calledFrom">
    ///    This parameter is not used in this class. See the base class, <c>RunBase</c>, for a description.
    /// </param>
    /// <returns>
    ///    true if the validation succeeded; otherwise, false.
    /// </returns>
    public boolean validate(Object calledFrom = null)
    {
        boolean ret = super(calledFrom);

        if (!inventNonConformanceID)
            ret = checkFailed(strFmt("@SYS19306", funcName()));

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>refreshCaller</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Refreshes the calling form so updates are shown.
    /// </summary>
    /// <param name="_caller">
    ///    The calling record.
    /// </param>
    protected static void refreshCaller(Common _caller)
    {
        if (FormDataUtil::isFormDataSource(_caller))
        {
            FormDataSource formDataSource = FormDataUtil::getFormDataSource(_caller);
            formDataSource.reread();
            formDataSource.active();
            formDataSource.refresh();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
