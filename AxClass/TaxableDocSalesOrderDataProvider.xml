<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxableDocSalesOrderDataProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider for sales order.
/// </summary>
public class TaxableDocSalesOrderDataProvider extends TaxableDocumentDataProvider
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Builds the query to load detail data for taxable document. And setup the row definitions.
    /// </summary>
    /// <returns>
    /// The query to load detail data for taxable document.
    /// </returns>
    /// <remarks>
    /// The row definitions must be setup here. It defines the mapping between row and data source,
    /// the child/parent relations between rows, and the row data provider class.
    /// </remarks>
    public Query buildQuery()
    {
        Query                   query;
        QueryBuildDataSource    salesTableDataSource;
        QueryBuildDataSource    salesLineDataSource;
        QueryBuildDataSource    markupTransLineDataSource;

        query = new Query();
        salesTableDataSource = query.addDataSource(tableNum(SalesTable));
        salesTableDataSource.addRange(fieldNum(SalesTable, RecId)).value(queryValue(rootTable.RecId));

        salesLineDataSource  = salesTableDataSource.addDataSource(tableNum(SalesLine));
        salesLineDataSource.addRange(fieldNum(SalesLine, ReturnStatus)).value(strFmt('((ReturnStatus != %1) || ((ExpectedRetQty < %2) && (ReturnStatus == %1)))',
            enum2int(ReturnStatusLine::Awaiting),
            0));
        salesLineDataSource.joinMode(JoinMode::OuterJoin);
        salesLineDataSource.relations(true);

        markupTransLineDataSource = salesLineDataSource.addDataSource(tableNum(MarkupTrans));
        markupTransLineDataSource.joinMode(JoinMode::OuterJoin);
        markupTransLineDataSource.relations(true);

        rowDefinitions.empty();
        this.registerRow(salesTableDataSource.id(), 0, TaxEngineIntegrationConstants::Header, classStr(TaxableDocSalesTableRowDataProvider));
        this.registerRow(salesLineDataSource.id(), salesTableDataSource.id(), TaxEngineModelFieldsConstants::Lines, classStr(TaxableDocSalesLineRowDataProvider));
        this.registerRow(markupTransLineDataSource.id(), salesLineDataSource.id(), TaxEngineModelFieldsConstants::SubLines, classStr(TaxableDocMarkupTransRowDataProvider));

        return query;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>