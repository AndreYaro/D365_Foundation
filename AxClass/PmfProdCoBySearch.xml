<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PmfProdCoBySearch</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Searches for matching Batch order Co/By records.
/// </summary>
public class PmfProdCoBySearch extends PmfCoBySearchBase
{
    ProdId                  prodId;

    PmfProdCoBy             prodCoBy;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>coBy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the current PmfCoByMap record found by this search object.
    /// </summary>
    /// <returns>
    /// Current PmfCoByMap retrieved by this search object. Empty record if nothing has been found or no more
    /// matching records left.
    /// </returns>
    public PmfCoByMap coBy()
    {
        return prodCoBy;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Standard protected new.
    /// </summary>
    /// <param name="_prodId">
    /// Batch order id.
    /// </param>
    /// <param name="_forUpdate">
    /// Optional. Specifiy if the records retrieved should be automatically locked for update.
    /// Default is false.
    /// </param>
    protected void new(
        ProdId              _prodId,
        boolean             _forUpdate  = false)
    {
        prodId      = _prodId;

        super(_forUpdate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>next</Name>
				<Source><![CDATA[
    /// <summary>
    /// Advance to the next record.
    /// </summary>
    /// <returns>
    /// True if the next record is valid; false if there are no more records.
    /// </returns>
    public boolean next()
    {
        next prodCoBy;

        if (prodCoBy)
        {
            return true;
        }
        else
        {
            return false;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>prodId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Get the batch order id that was used when the object was created.
    /// </summary>
    /// <returns>
    /// Batch order id defined when this object was constructed.
    /// </returns>
    public ProdId prodId()
    {
        return prodId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>search</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initialize selection of the matching Batch order Co/By records from the database.
    /// </summary>
    public void search()
    {
        prodCoBy.selectForUpdate(update);

        select nofetch prodCoBy
            where prodCoBy.ProdId == prodId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Static constructor to create an instance of this class.
    /// </summary>
    /// <param name="_prodId">
    /// Batch order id.
    /// </param>
    /// <param name="_forUpdate">
    /// Optional. Specifiy if the records retrieved should be automatically locked for update.
    /// Default is false.
    /// </param>
    /// <returns>
    /// Instance of Batch order Co/By search.
    /// </returns>
    public static PmfProdCoBySearch construct(
        ProdId _prodId,
        boolean _forUpdate = false)
    {
        return new PmfProdCoBySearch(_prodId,_forUpdate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newBOMRouteCopy</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create and initialize a new instance of Batch order Co/By search from copy job.
    /// </summary>
    /// <param name="_copy">
    /// Instance of the BOMRouteCopyJob
    /// </param>
    /// <returns>
    /// Instance of Batch order Co/By search.
    /// </returns>
    /// <remarks>
    /// The new search object will be initialized automatically.
    /// </remarks>
    public static PmfProdCoBySearch newBOMRouteCopy(BOMRouteCopyJob _copy)
    {
        PmfProdCoBySearch   search;

        search = PmfProdCoBySearch::construct(_copy.parmFromProdId());
        search.init();

        return search;
    }

]]></Source>
			</Method>
			<Method>
				<Name>newProdId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create and initialize a new instance of Batch order Co/By search from a Batch order id.
    /// </summary>
    /// <param name="_prodId">
    /// Batch order id.
    /// </param>
    /// <param name="_forupdate">
    /// Optional. Specifiy if the records retrieved should be automatically locked for update.
    /// Default is false.
    /// </param>
    /// <returns>
    /// Instance of Batch order Co/By search.
    /// </returns>
    /// <remarks>
    /// The new search object will be initialized automatically.
    /// </remarks>
    public static PmfProdCoBySearch newProdId(
        ProdId      _prodId,
        boolean     _forupdate)
    {
        PmfProdCoBySearch   search;

        search = PmfProdCoBySearch::construct(_prodId,_forupdate);
        search.init();

        return search;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>