<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventProductDimensionConfig</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>InventProductDimensionConfig</c> class defines the behavior of the configuration inventory dimension.
/// </summary>
class InventProductDimensionConfig extends InventProductDimension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmDefaultValueForItem</Name>
				<Source><![CDATA[
    public anytype parmDefaultValueForItem(InventTable _inventTable, anytype _value = _inventTable.StandardConfigId)
    {
        _inventTable.StandardConfigId = _value;
        return _inventTable.StandardConfigId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmAlternativeValueForItem</Name>
				<Source><![CDATA[
    public anytype parmAlternativeValueForItem(InventTable _inventTable, anytype _value = _inventTable.AltConfigId)
    {
        _inventTable.AltConfigId = _value;
        return _inventTable.AltConfigId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findDimensionByNaturalKey</Name>
				<Source><![CDATA[
    public Common findDimensionByNaturalKey(anytype _value)
    {
        return EcoResConfiguration::findByName(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>createProductDimensionValue</Name>
				<Source><![CDATA[
    public void createProductDimensionValue(
        EcoResDistinctProductVariant    _ecoResDistinctProductVariant,
        EcoResProductDimensionAttribute _ecoResProductDimensionAttribute,
        RefRecId                        _dimensionValue)
    {
        EcoResProductVariantConfiguration  ecoResProductVariantConfiguration;
        ecoResProductVariantConfiguration.initFromDistinctProductVariant(_ecoResDistinctProductVariant);
        ecoResProductVariantConfiguration.ProductDimensionAttribute     = _ecoResProductDimensionAttribute.recId;
        ecoResProductVariantConfiguration.Configuration                 = _dimensionValue;
        ecoResProductVariantConfiguration.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>findTranslationByProduct</Name>
				<Source><![CDATA[
    public EcoResProductMasterDimValueTranslation findTranslationByProduct(EcoResProductRecId _productMasterRecId, anytype _dimensionValue, SystemLanguageId _languageId)
    {
        return EcoResProductMasterConfiguration::findTranslationByProduct(_productMasterRecId, _dimensionValue, _languageId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveDimensionNaturalKey</Name>
				<Source><![CDATA[
    public anytype retrieveDimensionNaturalKey(RecId _id)
    {
        return EcoResConfiguration::find(_id).Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>retrieveDimensionNaturalKeyByProductId</Name>
				<Source><![CDATA[
    public anytype retrieveDimensionNaturalKeyByProductId(RecId _productRecId)
    {
        EcoResConfiguration ecoResConfiguration;
        EcoResProductVariantConfiguration ecoResProductVariantConfiguration;

        RefRecId productDimensionAttribute   = EcoResProductDimensionAttribute::inventDimFieldId2DimensionAttributeRecId(this.parmInventDimFieldId());

        select firstonly Name
            from         ecoResConfiguration
            exists join  ecoResProductVariantConfiguration
                where    ecoResProductVariantConfiguration.Configuration                 == ecoResConfiguration.RecId
                    &&   ecoResProductVariantConfiguration.ProductDimensionAttribute     == productDimensionAttribute
                    &&   ecoResProductVariantConfiguration.DistinctProductVariant        == _productRecId;

        return ecoResConfiguration.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInstance</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the singleton instance of the <c>InventProductDimensionConfig</c> class.
    /// </summary>
    /// <returns>An instance of the <c>InventProductDimensionConfig</c> class.</returns>
    public static InventProductDimensionConfig getInstance()
    {
        return InventDimension::singletonFromInventDimFieldId(fieldNum(InventDim, configId)) as InventProductDimensionConfig;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>