<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustInvoiceTransCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'CustomerInvoiceLine'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class CustInvoiceTransCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        CustInvoiceTrans custInvoiceTrans;
        select crosscompany DataAreaId from custInvoiceTrans where custInvoiceTrans.RecId == _parentRecId;

        return custInvoiceTrans.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        CustInvoiceTrans custInvoiceTrans;
        select crosscompany InvoiceId from custInvoiceTrans where custInvoiceTrans.RecId == _parentRecId;

        return custInvoiceTrans.InvoiceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        CustInvoiceTrans custInvoiceTrans;
        select RecId from custInvoiceTrans where custInvoiceTrans.InvoiceId == _naturalKey;

        if (!custInvoiceTrans.RecId)
        {
            custInvoiceTrans = CustInvoiceTrans::findRecId(str2Int64(_naturalKey));
        }

        return custInvoiceTrans.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        CustInvoiceTrans custInvoiceTrans;
        select crosscompany Name from custInvoiceTrans where custInvoiceTrans.RecId == _parentRecId;

        return custInvoiceTrans.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        CustInvoiceTrans custInvoiceTrans;
        
        //We need this code in order not to lose the localization data in the opened form.
        CustInvoiceTrans custInvoiceTransCrossCompany;

        select firstonly crosscompany DataAreaId, RecId from custInvoiceTransCrossCompany
            where custInvoiceTransCrossCompany.RecId == _association.RefRecId;

        if (custInvoiceTransCrossCompany.Recid)
        {
            changecompany (custInvoiceTransCrossCompany.DataAreaId)
            {
                select firstonly custInvoiceTrans where custInvoiceTrans.RecId == _association.RefRecId;
            }
        }

        _args.record(custInvoiceTrans);
        return menuitemdisplaystr(CustInvoiceJournal);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(CustInvoiceTrans), callerControl);

        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(CustInvoiceTrans));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, RecId), true);
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, InvoiceId));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, SalesId));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, ItemId));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, LineAmount));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, SalesUnit));
        sysTableLookup.addLookupfield(fieldnum(CustInvoiceTrans, Qty));
        sysTableLookup.addLookupMethod(tableMethodStr(CustInvoiceTrans, deliveryDate));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(CustInvoiceTrans))
        {
            _eventArgs.parmEntityType(CaseEntityType::CustomerInvoiceLine);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>