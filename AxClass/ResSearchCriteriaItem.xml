<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ResSearchCriteriaItem</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Search criteria item base abstract class.
/// </summary>
public abstract class ResSearchCriteriaItem implements ResISearchCriteriaItem
{
    private Set values;
    private const str delimiter = ",";

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>delimiter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the delimiter characters.
    /// </summary>
    /// <returns>Delimiter characters.</returns>
    protected str delimiter()
    {
        return delimiter;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCriteriaItemValuesEnumerator</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the enumerator of the values collection.
    /// </summary>
    /// <returns>Collection enumerator.</returns>
    public Enumerator getCriteriaItemValuesEnumerator()
    {
        return values.getEnumerator();
    }

]]></Source>
			</Method>
			<Method>
				<Name>applySearch</Name>
				<Source><![CDATA[
    /// <summary>
    /// Applies search criteria in the specified query.
    /// </summary>
    /// <param name = "_query">Base query of collection items.</param>
    /// <returns>Updated query applied with search criteria.</returns>
    public abstract Query applySearch(Query _query)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the criteria value string.
    /// </summary>
    /// <param name = "_value">Criteria value.</param>
    public void setValues(str _value)
    {
        if (_value)
        {
            ListEnumerator enumerator = strSplit(_value, this.delimiter()).getEnumerator();

            while (enumerator.moveNext())
            {
                this.addValue(enumerator.current());
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getValues</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the criteria value string.
    /// </summary>
    /// <returns>Criteria value.</returns>
    public str getValues()
    {
        Enumerator enumerator = this.getCriteriaItemValuesEnumerator();
        str criteriaValues;
        str format = "%1";

        while (enumerator.moveNext())
        {
            anytype currentValue = enumerator.current();

            if (currentValue)
            {
                criteriaValues = strFmt(format, currentValue, criteriaValues, this.delimiter());
                format = "%2%3%1";
            }
        }

        return criteriaValues;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the criteria value to the criteria item.
    /// </summary>
    /// <param name = "_value">Criteria values.</param>
    public void addValue(anytype _value)
    {
        values.add(_value);
    }

]]></Source>
			</Method>
			<Method>
				<Name>removeValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Removes the criteria value from the criteria item.
    /// </summary>
    /// <param name = "_value">Criteria values.</param>
    public void removeValue(anytype _value)
    {
        if (values.in(_value))
        {
            values.remove(_value);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of resource search criteria.
    /// </summary>
    protected void new ()
    {
        values = new Set(Types::AnyType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>linkDataSource</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delegate to link data sources.
    /// </summary>
    /// <param name = "_dataSource">Data source to link.</param>
    delegate void linkDataSource(QueryBuildDataSource _dataSource)
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
