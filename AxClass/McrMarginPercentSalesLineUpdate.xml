<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>McrMarginPercentSalesLineUpdate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Class for updating margin percentages on sales lines.
/// </summary>
public class McrMarginPercentSalesLineUpdate
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>updateSalesLinesInSandbox</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the margin percentage on sales lines using SandBox.
    /// </summary>
    /// <param name = "_salesId">The sales order id to update for.</param>
    public static void updateSalesLinesInSandbox(SalesId _salesId)
    {
        SysOperationSandbox::callStaticMethod(classnum(McrMarginPercentSalesLineUpdate), staticMethodStr(McrMarginPercentSalesLineUpdate, updateLines), [_salesId], strfmt("@SCM:RecalculateLineMarginsProgress", _salesId));
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLines</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the margin percentage on sales lines.
    /// </summary>
    /// <param name = "_callerParams">A container with the parameters, that should be in the form of [The salesId to update for].</param>
    public static void updateLines(container _callerParams)
    {
        if (conlen(_callerParams) != 1)
        {
            throw error(Error::wrongUseOfFunction(staticMethodStr(McrMarginPercentSalesLineUpdate, updateLines)));
        }
        
        if (!SalesParameters::find().mcrEnableMarginAlert)
        {
            return;
        }

        SalesId salesId = conPeek(_callerParams, 1);                        
        SalesLine salesLine;
        
        ttsbegin;

        while select forupdate salesLine
            where salesLine.SalesId == salesId
            && salesLine.SalesStatus != SalesStatus::Invoiced
        {
            if (salesLine.setMcrMarginPercent(true))
            {
                salesLine.update();
            }
        }

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(xArgs _args)
    {
        SalesTable salesTable;

        if (_args.dataset() == tableNum(SalesTable) && _args.record())
        {
            salesTable = _args.record();
        }

        if (!salesTable)
        {
            throw error(Error::missingRecord(staticMethodStr(McrMarginPercentSalesLineUpdate, updateLines)));
        }

        McrMarginPercentSalesLineUpdate::updateSalesLinesInSandbox(salesTable.SalesId);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>