<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FiscalCalendarEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>FiscalCalendarEventHandler</c> class implements event handlers for the <c>FiscalCalendar</c> table.
/// </summary>
public class FiscalCalendarEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canDeleteCalendarPostHandler</Name>
				<Source><![CDATA[
    /// <summary>
    /// Post event handler for the canDeleteCalendar method on the <c>FiscalCalendar</c> table.
    /// </summary>
    /// <param name = "_args">
    /// The <c>XppPrePostArgs</c> instance.
    /// </param>
    [PostHandlerFor(tableStr(FiscalCalendar), tableMethodStr(FiscalCalendar, canDeleteCalendar))]
    public static void canDeleteCalendarPostHandler(XppPrePostArgs _args)
    {
        FiscalCalendar fiscalCalendar = _args.getThis() as FiscalCalendar;
        boolean returnValue = _args.getReturnValue();

        if (!Ledger::existLedgerForFiscalCalendar(fiscalCalendar.RecId))
        {
            GeneralJournalEntry generalJournalEntry;
            FiscalCalendarYear fiscalCalendarYear;

            select firstonly RecId from generalJournalEntry
                exists join fiscalCalendarYear
                    where fiscalCalendarYear.RecId == generalJournalEntry.FiscalCalendarYear
                        && fiscalCalendarYear.FiscalCalendar == fiscalCalendar.RecId;

            if (generalJournalEntry != null)
            {
                returnValue = checkFailed(strFmt("@SYP4881041", fiscalCalendar.CalendarId));
            }
        }

        if (returnValue)
        {
            AssetBookTable assetBookTable;

            select firstonly crossCompany RecId, BookId, DataAreaId from assetBookTable
                where assetBookTable.FiscalCalendar == fiscalCalendar.RecId;

            if (assetBookTable != null)
            {
                CompanyInfo companyInfo = assetBookTable.companyInfo();
                returnValue = checkFailed(strFmt("@FixedAssets:FA_MessageError_CannotDeleteFiscalCalendarAssociatedWithBook", fiscalCalendar.CalendarId, assetBookTable.BookId, strFmt("@SYS191501", companyInfo.DataArea, companyInfo.Name)));
            }
        }

        if (returnValue)
        {
            BudgetCycleTimeSpan budgetCycleTimeSpan;

            select firstonly crossCompany RecId, Name from budgetCycleTimeSpan
                where budgetCycleTimeSpan.FiscalCalendar == fiscalCalendar.RecId;

            if (budgetCycleTimeSpan != null)
            {
                returnValue = checkFailed(strFmt("@SYS330013", fiscalCalendar.CalendarId, budgetCycleTimeSpan.Name));
            }
        }

        _args.setReturnValue(returnValue);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>