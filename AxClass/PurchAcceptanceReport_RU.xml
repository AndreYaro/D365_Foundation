<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchAcceptanceReport_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class PurchAcceptanceReport_RU extends SalesPurchAcceptanceReport_RU
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initContragentData</Name>
				<Source><![CDATA[
    protected void initContragentData()
    {
        super();

        consigneeData = new SalesPurchReportCounteragentData_RU();

        consigneeData.parmName(companyInfo.name());
        consigneeData.parmAddress(companyInfo.postalAddress().Address);
        consigneeData.parmPhone(companyInfo.phone());
        consigneeData.parmTelefax(companyInfo.teleFax());
        consigneeData.parmBankAccountTable(BankAccountTable::find(companyInfo.Bank));
        consigneeData.parmOKDP(CompanyInfoHelper::partyOKDPasOfDate_RU(companyInfo, custVendInvoiceJour.InvoiceDate));
        consigneeData.parmOKPO(CompanyInfoHelper::partyOKPOasOfDate_RU(companyInfo, custVendInvoiceJour.InvoiceDate));
        consigneeData.parmINN(companyInfo.partyINNasOfDate_RU(custVendInvoiceJour.InvoiceDate));

        consignorData = new SalesPurchReportCounteragentData_RU();
        VendTable vendTable = VendTable::find(custVendInvoiceJour.InvoiceAccount);

        consignorData.parmName(vendTable.name());
        consignorData.parmAddress(vendTable.postalAddress().Address);

        consignorData.parmPhone(
            LogisticsLocationEntity::constructFromLocationRecId(
                DirPartyLocation::findPrimaryPartyLocation(vendTable.Party, false, LogisticsElectronicAddressMethodType::Phone).Location,
                false,
                DateTimeUtil::newDateTime(custVendInvoiceJour.InvoiceDate, 0)).getPhone());

        consignorData.parmTelefax(
            LogisticsLocationEntity::constructFromLocationRecId(
                DirPartyLocation::findPrimaryPartyLocation(
                    vendTable.Party,
                    false,
                    LogisticsElectronicAddressMethodType::Phone).Location,
                false,
                DateTimeUtil::newDateTime(custVendInvoiceJour.InvoiceDate, 0)).getTeleFax());

        consignorData.parmBankAccountTable(VendBankAccount::find(vendTable.AccountNum, vendTable.BankAccount));
        consignorData.parmOKDP(vendTable.partyOKDPasOfDate_RU(custVendInvoiceJour.InvoiceDate));
        consignorData.parmOKPO(vendTable.partyOKPOasOfDate_RU(custVendInvoiceJour.InvoiceDate));
        consignorData.parmINN(vendTable.partyINNasOfDate_RU(custVendInvoiceJour.InvoiceDate));
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        if (_args)
        {
            PurchAcceptanceReport_RU report;

            boolean reportRanSuccessfully = false;

            if (_args.record())
            {
                report = new PurchAcceptanceReport_RU();
                report.parmInvoiceJour(_args.record());
                report.run();

                reportRanSuccessfully = true;
            }

            if (_args.object())
            {
                VendInvoiceJour vendInvoiceJour;
                RecordSortedList rst = _args.object();

                boolean existsNext = rst.first(vendInvoiceJour);

                if (!reportRanSuccessfully)
                {
                    reportRanSuccessfully = existsNext;
                }

                while (existsNext)
                {
                    report = new PurchAcceptanceReport_RU();
                    report.parmInvoiceJour(vendInvoiceJour);
                    report.run();

                    existsNext = rst.next(vendInvoiceJour);
                }
            }

            if (reportRanSuccessfully)
            {
                GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceRU00056, funcName());
            }

        }
        else
        {
            throw error(Error::wrongUseOfFunction(funcname()));
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>