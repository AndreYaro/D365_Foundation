<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>MCRSalesTableForm_Campaign</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>MCRSalesTableForm_Campaign</c> class supports viewing and creating sales orders from a campaign.
/// </summary>
class MCRSalesTableForm_Campaign extends SalesTableForm
{
    smmCampaignSelection    smmCampaignSelection;
    smmCampaignTable        smmCampaignTable;
    CustTable               custTable;
    smmBusRelTable          smmBusRelTable_Campaign;
    smmCampaignId           smmCampaignId_Campaign;
    MCRSourceID             mcrSourceID_Campaign;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initValues</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Initializes values for a new sales order.
    /// </summary>
    public void initValues()
    {
        MCRSalesTable       mcrSalesTable = salesTable.mcrSalesTable();

        super();

        if (custAccount)
        {
            salesTable.initFromCustTable();
        }

        if (ContactPersonId)
        {
            salesTable.ContactPersonId = ContactPersonId;
            salesTable.initFromContactInfo();
        }

        mcrSalesTable.SourceId = mcrSourceID_Campaign;
        salesTable.SmmCampaignId = smmCampaignId;
        mcrSalesTable.SmmTMCallListID = smmCampaignTable.TMCallListID;
        salesTable.mcrPackMCRSalesTable(mcrSalesTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>MCRSalesTableForm_Campaign</c> class.
    /// </summary>
    /// <param name="_source">
    /// The passed buffer that dictates which buffers to create.
    /// </param>
    public void new(Common _source)
    {
        if (_source.TableId == tableNum(smmCampaignSelection))
        {
            smmCampaignSelection = _source;

            smmBusRelTable_Campaign    = smmBusRelTable::findByParty(smmCampaignSelection.Party);
            custTable      = CustTable::findByPartyRecId(smmBusRelTable_Campaign.Party);
            smmCampaignTable  = smmCampaignTable::find(smmCampaignSelection.CampaignId);

            custAccount     = custTable.AccountNum;
            invoiceAccount  = custTable.InvoiceAccount ? custTable.InvoiceAccount: custTable.AccountNum;
            smmCampaignId      = smmCampaignSelection.CampaignId;
            ContactPersonId = smmCampaignSelection.ContactPersonId;
            mcrSourceID_Campaign      = smmCampaignTable.MCRSourceID;
        }
        else if (_source.TableId == tableNum(smmCampaignTable))
        {
            smmCampaignTable  = _source;
            smmCampaignId      = smmCampaignTable.CampaignId;
            mcrSourceID_Campaign      = smmCampaignTable.MCRSourceID;
        }

        [advancedStartup] = [true];

        super(_source);
    }

]]></Source>
			</Method>
			<Method>
				<Name>querySalesTable</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Queries sales orders related to passed campaign if one was passed.
    /// </summary>
    /// <param name="querySalesTable">
    ///     The sales order query to be used as a base for sales order initialization.
    /// </param>
    /// <returns>
    ///    An updated query initialized to a campaign and customer account.
    /// </returns>
    public Query querySalesTable(Query querySalesTable)
    {
        Query           ret;
        ret = super(querySalesTable);

        querySalesTable.dataSourceTable(tableNum(SalesTable)).clearRange(fieldNum(SalesTable, CustAccount));
        querySalesTable.dataSourceTable(tableNum(SalesTable)).clearRange(fieldNum(SalesTable, ContactPersonId));
        querySalesTable.dataSourceTable(tableNum(SalesTable)).clearRange(fieldNum(SalesTable, SmmCampaignId));

        if (smmCampaignId)
        {
            querySalesTable.dataSourceTable(tableNum(SalesTable)).addRange(fieldNum(SalesTable, SmmCampaignId)).value(smmCampaignId);

            if (smmCampaignSelection)
            {
                querySalesTable.dataSourceTable(tableNum(SalesTable)).addRange(fieldNum(SalesTable, CustAccount)).value(custAccount);
            }
        }
        else
        {
            querySalesTable.dataSourceTable(tableNum(SalesTable)).addRange(fieldNum(SalesTable, SmmCampaignId)).value(SysQuery::valueNotEmptyString());
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>