<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgTransferDate</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>JmgTransferDate</c> class is used to transfer registrations
/// for a day and a set of workers.
/// </summary>
public class JmgTransferDate extends RunBaseBatch implements BatchRetryable
{
    JmgGroupApproveId   jmgGroupApproveId;
    JmgDate             profileFromDate;
    JmgDate             profileToDate;

    private DialogField dfJmgGroupApproveId;
    private DialogField dfProfileFromDate;
    DialogField         dfProfileToDate;

    #DEFINE.CurrentVersion(3)
    #LOCALMACRO.CurrentList
        profileFromDate,
        profileToDate,
        jmgGroupApproveId
    #ENDMACRO

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>canGoBatch</Name>
				<Source><![CDATA[
    public boolean canGoBatch()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLast</Name>
				<Source><![CDATA[
    void getLast()
    {
        JmgDate tmpFromDate = profileFromDate;
        JmgDate tmpToDate = profileToDate;
        JmgGroupApproveId tmpApproveId = jmgGroupApproveId;

        super();

        if (tmpFromDate)
        {
            profileFromDate = tmpFromDate;
        }
        if (tmpToDate)
        {
            profileToDate = tmpToDate;
        }
        if (tmpApproveId)
        {
            jmgGroupApproveId = tmpApproveId;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pack</Name>
				<Source><![CDATA[
    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contains the code that does the actual job of the class.
    /// </summary>
    void run()
    {
        #OCCRetryCount
        JmgStampJournalTable    jmgStampJournalTable;
        Counter                 counter;

        #AviFiles

        using(var activityContext = JmgInstrumentationLogger::createLogger(classStr(JmgTransferDate)).calcApprove().transferAll())
        {
            if(this.isInBatch())
            {
                this.setProfileDateInBatchRun();
            }

            try
            {
                this.progressInit(JmgTransferDate::description(),(select count(WorkSeconds) from jmgStampJournalTable
                                                                  where jmgStampJournalTable.ProfileDate >= profileFromDate
                                                                     && jmgStampJournalTable.ProfileDate <= profileToDate).WorkSeconds,#AviUpdate);

                ttsbegin;

                while select forupdate jmgStampJournalTable
                where jmgStampJournalTable.ProfileDate      >= profileFromDate      &&
                      jmgStampJournalTable.ProfileDate      <= profileToDate        &&
                      jmgStampJournalTable.ApproveGroupId   == jmgGroupApproveId    &&
                      jmgStampJournalTable.Approved                                 &&
                      jmgStampJournalTable.Transferred      == NoYes::No
                {
                    progress.setText(HcmWorker::worker2Name(jmgStampJournalTable.Worker));
                    JmgStampJournalTransfer::newFromJournalTable(jmgStampJournalTable).run();
                    counter++;
                    progress.setCount(counter);
                }

                ttscommit;
            }

            catch (Exception::Deadlock)
            {
                retry;
            }
            catch (Exception::UpdateConflict)
            {
                if (appl.ttsLevel() == 0)
                {
                    if (xSession::currentRetryCount() >= #RetryNum)
                    {
                        throw Exception::UpdateConflictNotRecovered;
                    }
                    else
                    {
                        retry;
                    }
                }
                else
                {
                    throw Exception::UpdateConflict;
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setProfileDateInBatchRun</Name>
				<Source><![CDATA[
    private void setProfileDateInBatchRun()
    {
        #TimeConstants

        date today = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone());
        if (DateTimeUtil::getDifference(today, profileToDate) >= #secondsPerDay)
        {
            profileToDate = today;
        }

        // Do not allow to run Transfer All for more than 1 month
        date potentialFromDate = DateTimeUtil::date(DateTimeUtil::addMonths(profileToDate, -1));
        profileFromDate = max(profileFromDate, potentialFromDate);
    }

]]></Source>
			</Method>
			<Method>
				<Name>runsImpersonated</Name>
				<Source><![CDATA[
    public boolean runsImpersonated()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setapprovegroup</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the approval group for which transfer is run.
    /// </summary>
    /// <param name="jmgGroupApproveIdIn">
    /// The ID of the approval group to set.
    /// </param>
    private void setapprovegroup(JmgGroupApproveId jmgGroupApproveIdIn)
    {
        jmgGroupApproveId = jmgGroupApproveIdIn;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setProfileDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the profile date which transfer is run.
    /// </summary>
    /// <param name="_profileDate">
    /// The profile date to set.
    /// </param>
    private void setProfileDate(JmgDate _profileDate)
    {
        profileFromDate = _profileDate;
        profileToDate = _profileDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unpack</Name>
				<Source><![CDATA[
    public boolean unpack(container packedClass)
    {
        #LOCALMACRO.List
            profileFromDate
        #ENDMACRO

        Integer     version      = conpeek(packedClass,1);

        switch (version)
        {
            case #CurrentVersion:
                [version,#CurrentList] = packedClass;
                break;
            case 1  :
                [version,#List] = packedClass;
                break;
            default :
                return false;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates whether the current instance is valid.
    /// </summary>
    /// <param name="calledFrom">
    /// A reference to the calling object.
    /// </param>
    /// <returns>
    /// True, if the instance is valid; otherwise, false.
    /// </returns>
    boolean validate(Object calledFrom = null)
    {
        boolean    ok = true;

        return ok;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes a new instance of the <c>JmgTransferDate</c> class.
    /// </summary>
    /// <returns>
    ///    A new instance of the <c>JmgTransferDate</c> class.
    /// </returns>
    public static JmgTransferDate construct()
    {
        return new JmgTransferDate();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public static ClassDescription description()
    {
        return "@SYS38741";
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        JmgTransferDate         jmgTransferDate = JmgTransferDate::construct();
        JmgStampJournalTable    jmgStampJournalTable = args.record();
        jmgTransferDate.setProfileDate(jmgStampJournalTable.ProfileDate);
        jmgTransferDate.setapprovegroup(jmgStampJournalTable.ApproveGroupId);

        if (jmgTransferDate.prompt())
        {
            jmgTransferDate.runOperation();
        }

        if (FormDataUtil::getFormDataSource(jmgStampJournalTable))
        {
            JmgStampJournalTable    jmgStampJournalTableCurRec;
            select firstonly jmgStampJournalTableCurRec
                where jmgStampJournalTableCurRec.RecId == jmgStampJournalTable.RecId;

            FormDataUtil::getFormDataSource(jmgStampJournalTable).research();
            FormDataUtil::getFormDataSource(jmgStampJournalTable).findRecord(jmgStampJournalTableCurRec);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// This method adds the FromDate, ToDate and Approval Group fields to the dialog.
    /// </summary>
    /// <returns>
    /// An object of the <c>Dialog</c> Class.
    /// </returns>
    public Object dialog()
    {
        Dialog ret = super();

        dfProfileFromDate   = ret.addField(extendedTypeStr(JmgDate), "@SYS80661", "@SCMManufacturingExecution:JmgTransferAllDateDialogHint");
        dfProfileToDate     = ret.addField(extendedTypeStr(JmgDate), "@SYS14656", "@SCMManufacturingExecution:JmgTransferAllDateDialogHint");
        dfJmgGroupApproveId = ret.addField(extendedtypestr(JmgGroupApproveId));

        dfJmgGroupApproveId.value(jmgGroupApproveId);
        dfProfileFromDate.value(profileFromDate);
        dfProfileToDate.value(profileToDate);

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    /// Assigns the dialog values to the internal variables after the dialog is closed.
    /// </summary>
    /// <returns>
    /// True if user input is successfully validated; otherwise, false.
    /// </returns>
    /// <remarks>
    /// It checked that the profileToDate is greater than the profileFromDate.
    /// </remarks>
    public boolean getFromDialog()
    {
        boolean ret = super();

        jmgGroupApproveId = dfJmgGroupApproveId.value();
        profileFromDate   = dfProfileFromDate.value();
        profileToDate     = dfProfileToDate.value();

        if (profileToDate < profileFromDate)
        {
            ret = checkFailed("@SYP4881865");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isRetryable</Name>
				<Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>