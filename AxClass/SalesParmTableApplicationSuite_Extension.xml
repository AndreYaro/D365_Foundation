<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesParmTableApplicationSuite_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesParmTableApplicationSuite_Extension</c> class is an extension class which extends the <c>SalesParmTable</c> table.
/// </summary>
[ExtensionOf(tableStr(SalesParmTable))]
public final class SalesParmTableApplicationSuite_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromSalesTable</Name>
				<Source><![CDATA[
    public void initFromSalesTable(SalesTable salesTable)
    {
        next initFromSalesTable(salesTable);

        if (FeatureStateProvider::isFeatureEnabled(CompanyTaxRegistrationFeature::instance()))
        {
            this.TaxId = salesTable.TaxId;
        }

        if (TaxParameters::isCustomsEnable_IN())
        {
            this.EXIMPorts_IN = salesTable.EXIMPorts_IN;
        }

        if (CFDISharedLogic_MX::mustInitCFDIEInvoicePeriod(salesTable.CustAccount) || 
            CFDISharedLogic_MX::mustInitCFDIEInvoicePeriod(salesTable.InvoiceAccount))
        {
            this.CFDIEInvoicePeriod_MX = EInvoiceCFDIParameters_MX::find().EInvoiceGlobalPeriod;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedField</Name>
				<Source><![CDATA[
    public void modifiedField(FieldId _fieldId)
    {
        next modifiedField(_fieldId);

        switch (_fieldId)
        {
            case fieldNum(SalesParmTable, EXIMPorts_IN):
                if (TaxParameters::isCustomsEnable_IN())
                {
                    SalesParmLine salesParmLine;
                    EximSalesParmLine_IN eximSalesParmLine_IN;

                    update_recordset eximSalesParmLine_IN
                        setting EximPorts = this.EXIMPorts_IN
                        exists join salesParmLine
                            where salesParmLine.RecId == eximSalesParmLine_IN.SalesParmLineRecId
                                && salesParmLine.ParmId == this.ParmId
                                && salesParmLine.TableRefId == this.TableRefId;
                }
                break;

            default:
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>