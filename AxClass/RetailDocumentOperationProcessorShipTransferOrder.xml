<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailDocumentOperationProcessorShipTransferOrder</Name>
	<SourceCode>
		<Declaration><![CDATA[
using CommerceRunTimeDataModel = Microsoft.Dynamics.Commerce.Runtime.DataModel;

/// <summary>
/// The <c>RetailDocumentOperationProcessorShipTransferOrder</c> class is the processor for ShipTransferOrder operation.
/// </summary>
internal class RetailDocumentOperationProcessorShipTransferOrder extends RetailDocumentOperationProcessorTransferOrderBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates an instance of a class of the <c>RetailDocumentOperationProcessorShipTransferOrder</c> class.
    /// </summary>
    /// <param name="_parameters">
    /// The <c>RetailDocumentOperationProcessorParameters</c> instance.
    /// </param>
    /// <returns>
    /// The new instance of an <c>RetailDocumentOperationProcessorShipTransferOrder</c> subclass.
    /// </returns>
    public static RetailDocumentOperationProcessorShipTransferOrder construct(RetailDocumentOperationProcessorParameters _parameters)
    {
        var processor = new RetailDocumentOperationProcessorShipTransferOrder(_parameters);
        processor.isRetriable = false;
        return processor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processQuantityToUpdate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process quantity to update.
    /// </summary>
    /// <param name = "_quantityToUpdate">For ship transfer order, the quantity to update will be mapped to QtyShipNow in <c>InventTransferLine</c>.</param>
    /// <param name = "_inventTransferLine">The invent transfer line xml element.</param>
    /// <param name = "_numberOfCharacters">The number of characters when convert _quantityToUpdate value to string.</param>
    /// <param name = "_numberOfDecimals">The number of decimals when convert _quantityToUpdate value to string.</param>
    protected void processQuantityToUpdate(real _quantityToUpdate, XmlElement _inventTransferLine, int _numberOfCharacters, int _numberOfDecimals)
    {
        _inventTransferLine.setAttribute(fieldStr(InventTransferLine, QtyShipNow), num2Str(_quantityToUpdate, _numberOfCharacters, _numberOfDecimals, DecimalSeparator::Dot, ThousandSeparator::None));
    }

]]></Source>
			</Method>
			<Method>
				<Name>processLicensePlateId</Name>
				<Source><![CDATA[
    /// <summary>
    /// Process license plate id.
    /// </summary>
    /// <param name = "_originalInventTransferLine">The original invent transfer line.</param>
    /// <param name = "_inventoryDocumentLine"> The inventory document line.</param>
    /// <param name = "_inventTransferLine">The invent transfer line xml element.</param>
    protected void processLicensePlateId(InventTransferLine _originalInventTransferLine, CommerceRunTimeDataModel.InventoryInboundOutboundDocumentLine _inventoryDocumentLine, XmlElement _inventTransferLine)
    {
        InventDimId inventDimId = _originalInventTransferLine.InventDimId;
        InventDim inventDimLine = InventDim::find(inventDimId);

        if (inventDimLine)
        {
            _inventTransferLine.setAttribute(fieldStr(InventDim, LicensePlateId), inventDimLine.LicensePlateId);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateTransferOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Create or update transfer order, should be implemented in subclasses.
    /// </summary>
    /// <param name = "_transferXmlStr">An XML string containing details of a transfer order.</param>
    /// <returns>A container object to indicate result documents.</returns>
    protected container createOrUpdateTransferOrder(str _transferXmlStr)
    {
        return RetailTransactionServiceInventory::updateTransferOrder(_transferXmlStr);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>