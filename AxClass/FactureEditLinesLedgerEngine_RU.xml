<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>FactureEditLinesLedgerEngine_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
final class FactureEditLinesLedgerEngine_RU
{
    LedgerJournalTable  jour;
    LedgerJournalTrans  trans;

    boolean             jourIsSet,
                        transIsSet,
                        transExistsIsSet;

    Map                 jourMap,
                        transMap;

    LedgerJournalTrans  transExists;

    FactureJourCreateTaxCorrection_RU factureCreate;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>copy</Name>
				<Source><![CDATA[
    private void copy(Common _src,
                      Common _dst,
                      Common _dstExists,
                      Map    _mapping,
                      Map    _cache     = null)
    {
        if (! _cache || ! _cache.exists(_src.RecId))
        {
            _dst.data(_src);
            _dst.doInsert();
            if (! _cache)
            {
                _dstExists.data(_src);
                _dstExists.doInsert();
            }
            _mapping.insert(_dst.RecId, _src.RecId);
            _mapping.insert(_src.RecId, _src.RecId);
            if (_cache)
            {
                _cache.insert(_src.RecId, null);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>decodeJour</Name>
				<Source><![CDATA[
    public RecId decodeJour(RecId _recId)
    {
        RecId ret;

        if (jourMap.exists(_recId))
        {
            ret = jourMap.lookup(_recId);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>decodeTrans</Name>
				<Source><![CDATA[
    public RecId decodeTrans(RecId _recId)
    {
        RecId ret;

        if (transMap.exists(_recId))
        {
            ret = transMap.lookup(_recId);
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    public void new(FactureJourCreateTaxCorrection_RU _factureCreate)
    {
        factureCreate = _factureCreate;

        jourMap  = new Map(typeName2Type(extendedTypeStr(recId)), typeName2Type(extendedTypeStr(recId)));
        transMap = new Map(typeName2Type(extendedTypeStr(recId)), typeName2Type(extendedTypeStr(recId)));
    }

]]></Source>
			</Method>
			<Method>
				<Name>process</Name>
				<Source><![CDATA[
    public void process()
    {
        LedgerJournalTable    tmpJour;
        LedgerJournalTrans    tmpTrans;
        TaxTable              taxTable;
        TaxLedgerAccountGroup taxGroup;

        Map jourCache;

        if (jourIsSet && transIsSet && transExistsIsSet)
        {
            jourCache = new Map(typeName2Type(extendedTypeStr(recId)), Types::void);

            ttsbegin;

            while select tmpTrans
            join tmpJour
                where tmpTrans.JournalNum      == tmpJour.JournalNum            &&
                      tmpTrans.AccountType     == LedgerJournalACType::Ledger   &&
                      tmpJour.JournalType      == LedgerJournalType::Daily      &&
                      tmpJour.Posted
            join RecId
                from  taxTable
                where taxTable.TaxCode         == tmpTrans.TaxCode
                join TaxOutgoingLedgerDimension,
                 TaxIncomingPaymentLedgerDimension_RU,
                 TaxIncomingLedgerDimension
                from  taxGroup
                where taxGroup.TaxAccountGroup == taxTable.TaxAccountGroup
            {
                if (!tmpTrans.isTaxCorrection_RU(false) || tmpTrans.fullyFactured_RU(true))
                    continue;

                tmpTrans.Qty = FactureEditLinesEngineTaxCorrection_RU::taxCorrectionSign(tmpTrans, taxGroup);

                this.copy(tmpTrans, trans, transExists, transMap);
                this.copy(tmpJour,  jour,  null,        jourMap,  jourCache);
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setJour</Name>
				<Source><![CDATA[
    public void setJour(LedgerJournalTable _jour)
    {
        jour      = _jour;
        jourIsSet = true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTrans</Name>
				<Source><![CDATA[
    public void setTrans(LedgerJournalTrans _trans)
    {
        trans      = _trans;
        transIsSet = true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setTransExists</Name>
				<Source><![CDATA[
    public void setTransExists(LedgerJournalTrans _trans)
    {
        transExists      = _trans;
        transExistsIsSet = true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>