<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>KanbanUpd</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>KanbanUpd</c> class hierarchy performs status updates for kanban s.
/// </summary>
public abstract class KanbanUpd extends UpdateBase
{
    Kanban                  kanban;
    Kanban                  beforeUpdKanban;
    KanbanStatusUpdate      kanbanStatusUpdate;
    boolean                 updateJobs;

    ProdJournalVoucherId    voucherId;
    NumberSeq               numberSeq;
    LedgerVoucher           ledgerVoucher;

    LeanConversionCostUpd   costUpd;
    KanbanStatus            kanbanStatus;

    boolean                 notifyChangeStatus;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>beginLedger</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the data that is required to update the ledger voucher.
    /// </summary>
    /// <remarks>
    ///    The data is not committed before the <c>commitLedger</c> method is called.
    /// </remarks>
    protected void beginLedger()
    {
        TransactionTxt          transactionTxt = TransactionTxt::construct();
        KanbanType              kanbanType     = kanban.type();
        TransDate               transDate      = DateTimeUtil::date(DateTimeUtil::applyTimeZoneOffset(kanbanStatusUpdate.TransactionDateTime,DateTimeUtil::getCompanyTimeZone()));
        LeanCostingFacadeBase   leanCostingFacade;

        numberSeq      = NumberSeq::newGetNum(ProdParameters::numRefProdJournalVoucherId());
        voucherId      = numberSeq.num();

        ledgerVoucher  = LedgerVoucher::newLedgerPost(DetailSummary::Summary, kanbanType.sysModule(), numberSeq.parmNumberSequenceCode());

        transactionTxt.setType(LedgerTransTxt::Kanban);
        transactionTxt.setDate(transDate);
        transactionTxt.setVoucher(voucherId);
        transactionTxt.setKey1(kanban.KanbanId);

        LedgerVoucherObject ledgerVoucherObject = LedgerVoucherObject::newVoucher(voucherId,
                                                                                  transDate,
                                                                                  kanbanType.sysModule(),
                                                                                  kanbanType.ledgerTransType());
        ledgerVoucherObject.lastTransTxt(transactionTxt.txt());
        ledgerVoucher.addVoucher(ledgerVoucherObject);

        if (kanbanStatus)
        {
            leanCostingFacade = kanbanStatus.parmLeanCostingFacade();
        }

        costUpd = LeanConversionCostUpd::newFromParameters(ledgerVoucher, kanban.leanProductionFlowReference(), leanCostingFacade, kanban.currentJob());
    }

]]></Source>
			</Method>
			<Method>
				<Name>commitLedger</Name>
				<Source><![CDATA[
    /// <summary>
    /// Commits the voucher.
    /// </summary>
    protected void commitLedger()
    {
        if (ledgerVoucher)
        {
            costUpd.updateNow();
            ledgerVoucher.end();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>initKanban</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the <c>Kanban</c> table buffer from the <c>ParmBuffer</c> buffer.
    /// </summary>
    /// <param name="_forUpdate">
    ///    A Boolean value that indicates whether to read the <c>Kanban</c> class variable for update;
    ///    optional.
    /// </param>
    /// <exception cref="M:Exception::Error">
    ///    The record is not valid; the update was canceled.
    /// </exception>
    protected void initKanban(boolean _forUpdate = false)
    {
        ParmBuffer           parmBuffer  = this.parmParmBuffer();
        KanbanStatusUpdate   statusUpdate;

        statusUpdate.data(parmBuffer);

        kanban = Kanban::find(statusUpdate.Kanban,_forUpdate);

        if (!kanban.checkValid(statusUpdate.RecId))
        {
            throw error("@SYS18447");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>kanban</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Kanban</c> table buffer.
    /// </summary>
    /// <returns>
    /// The <c>Kanban</c> table buffer.
    /// </returns>
    public Kanban kanban()
    {
        return kanban;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes a new instance of the <c>KanbanUpd</c> class.
    /// </summary>
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyChangeStatus</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a notification that kanban status has changed.
    /// </summary>
    protected void notifyChangeStatus()
    {
        if (notifyChangeStatus &&
            kanban)
        {
            switch (kanban.Status)
            {
                case LeanKanbanStatus::Emptied:
                    info(strfmt("@SYS303808", kanban.KanbanId,kanban.kanbanCard().CardId,kanban.ItemId,kanban.QuantityReceived));
                    break;

                case LeanKanbanStatus::NotAssigned:
                    info(strfmt("@SYS303809", kanban.KanbanId,kanban.Status));
                    break;
                case LeanKanbanStatus::Assigned:
                case LeanKanbanStatus::Received:
                    info(strfmt("@SYS303810", kanban.KanbanId,kanban.kanbanCard().CardId,kanban.Status));
                    break;

                default:
                    throw error(Error::wrongUseOfFunction(funcname()));
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>notifyRulesEngine</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Notifies the rules engine of the status change.
    /// </summary>
    /// <remarks>
    ///    The rules engine must be notified after all status updates are completed.
    /// </remarks>
    protected void notifyRulesEngine()
    {
        if (beforeUpdKanban.Status != kanban.Status)
        {
            //notify rules engine
            LeanRulesEngine::onKanbanStatusChange(kanban);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmKanbanStatus</Name>
				<Source><![CDATA[
    public KanbanStatus parmKanbanStatus(KanbanStatus _kanbanStatus = kanbanStatus)
    {
        kanbanStatus = _kanbanStatus;
        return kanbanStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmNotifyChangeStatus</Name>
				<Source><![CDATA[
    public boolean parmNotifyChangeStatus(boolean _notifyChangeStatus = notifyChangeStatus)
    {
        notifyChangeStatus = _notifyChangeStatus;
        return notifyChangeStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmParmBuffer</Name>
				<Source><![CDATA[
    public ParmBuffer parmParmBuffer(ParmBuffer _kanbanStatusUpdate = kanbanStatusUpdate)
    {
        if (!_kanbanStatusUpdate.RecId)
        {
            throw error(strfmt("@SYS134373",tableid2name(tablenum(Kanban))));
        }

        kanbanStatusUpdate = _kanbanStatusUpdate;

        return kanbanStatusUpdate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmUpdateJobs</Name>
				<Source><![CDATA[
    public boolean parmUpdateJobs(boolean _updateJobs = updateJobs)
    {
        updateJobs = _updateJobs;
        return updateJobs;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs final actions that can be performed in an independent transaction scope.
    /// </summary>
    /// <param name="_mainRunSucceeded">
    /// A Boolean value that indicates whether the main process completed successfully; optional.
    /// </param>
    protected void postRun(boolean _mainRunSucceeded = true)
    {
        if (_mainRunSucceeded)
        {
            if (kanban.Status >= LeanKanbanStatus::Assigned)
            {
                LeanRuleEvent::updatePeggingRequirements(kanban);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postRunStatus</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Notifies the rules engine of the status change.
    /// </summary>
    /// <remarks>
    ///    Any subclass that overwrites this method must call the <c>super</c> method and insert all logic
    ///    before the call.
    /// </remarks>
    protected void postRunStatus()
    {
        this.notifyRulesEngine();
        this.notifyChangeStatus();
        beforeUpdKanban.clear();
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Performs initial actions that can be performed in an independent transaction scope.
    /// </summary>
    protected void preRun()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>preRunStatus</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Saves the unchanged <c>KanbanJob</c> object to a local variable for later comparison.
    /// </summary>
    /// <remarks>
    ///    Any subclass that overwrites this method must call the <c>super</c> method and insert all logic
    ///    after the call.
    /// </remarks>
    protected void preRunStatus()
    {
        beforeUpdKanban.data(kanban);
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Runs the status update.
    /// </summary>
    /// <exception cref="M:Exception::Error">
    ///    The validation failed.
    /// </exception>
    /// <exception cref="M:Exception::UpdateConflictNotRecovered">
    ///    An update conflict occurred and was not recovered.
    /// </exception>
    /// <exception cref="M:Exception::UpdateConflict">
    ///    An update conflict occurred.
    /// </exception>
    public void run()
    {
        #OCCRetryCount

        setprefix(KanbanMultiKanban::description());
        setprefix(#PreFixField(KanbanStatusUpdate,KanbanId));

        this.preRun();
        try
        {
            ttsbegin;
            if (!this.validate())
            {
                throw Exception::Error;
            }

            this.setParameters();
            this.preRunStatus();
            this.runStatus();
            this.postRunStatus();

            this.updateJobJournal(ParmJobStatus::Executed);
            ttscommit;
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
        catch (Exception::Error)
        {
            this.updateJobJournal(ParmJobStatus::ContainErrors);
            this.postRun(false);
            throw error("@SYS18447");
        }
        this.postRun();
    }

]]></Source>
			</Method>
			<Method>
				<Name>runStatus</Name>
				<Source><![CDATA[
    /// <summary>
    /// Performs actions for a specific status update.
    /// </summary>
    abstract protected void runStatus()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>setParameters</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets parameters for the status update.
    /// </summary>
    protected void setParameters()
    {
        this.initKanban(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setStatus</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets the <c>status</c> field of the <c>Kanban</c> table buffer.
    /// </summary>
    /// <param name="_status">
    ///    The new status.
    /// </param>
    /// <remarks>
    ///    The <c>Kanban</c> object must already be selected for update.
    /// </remarks>
    /// <exception cref="M:Exception::Error">
    ///    The record validation failed.
    /// </exception>
    protected void setStatus(LeanKanbanStatus _status)
    {
        kanban.Status = _status;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateKanban</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Updates the <c>Kanban</c> record.
    /// </summary>
    /// <remarks>
    ///    The record must already be selected for update.
    /// </remarks>
    protected void updateKanban()
    {
        kanban.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newKanban</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the class.
    /// </summary>
    /// <param name="_kanban">
    /// The <c>Kanban</c> table buffer used to instantiate the class.
    /// </param>
    /// <param name="_leanKanbanStatus">
    /// The target kanban  status.
    /// </param>
    /// <param name="_updateJobs">
    /// A Boolean value that specifies if the kanban jobs must be updated.
    /// </param>
    /// <param name="_kanbanStatus">
    /// The <c>KanbanStatus</c> record to retrieve the <c>LeanCostingFacadeBase</c> instance.
    /// </param>
    /// <returns>
    /// An instance of the <c>KanbanUpd_End</c> class.
    /// </returns>
    public static KanbanUpd newKanban(Kanban _kanban,LeanKanbanStatus _leanKanbanStatus,boolean _updateJobs,KanbanStatus _kanbanStatus)
    {
        return KanbanUpd::newParmBuffer(KanbanStatusUpdate::makeParameters(_kanban.status(),_leanKanbanStatus),_updateJobs,_kanbanStatus);
    }

]]></Source>
			</Method>
			<Method>
				<Name>newParmBuffer</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the <c>KanbanUpd</c> class.
    /// </summary>
    /// <param name="_kanbanStatusUpdate">
    /// The <c>KanbanStatusUpdate</c> table buffer that is used to initialize the class.
    /// </param>
    /// <param name="_updateJobs">
    /// A Boolean value that indicates whether the kanban jobs must be updated.
    /// </param>
    /// <param name="_kanbanStatus">
    /// The <c>KanbanStatus</c> record to retrieve the <c>LeanCostingFacadeBase</c> instance.
    /// </param>
    /// <param name="_notifyStatus">
    /// A Boolean value that specifies whether messages should be printed for a change of status.
    /// </param>
    /// <returns>
    /// An instance of the <c>KanbanUpd</c> class.
    /// </returns>
    public static KanbanUpd newParmBuffer(
        KanbanStatusUpdate  _kanbanStatusUpdate,
        boolean             _updateJobs,
        KanbanStatus        _kanbanStatus,
        boolean             _notifyStatus = false)
    {
        KanbanUpd    kanbanUpd;

        if (!_kanbanStatusUpdate.ParmId)
        {
            _kanbanStatusUpdate.skipTTSCheck(true);
            _kanbanStatusUpdate.ParmBuffer::initParmId();
            _kanbanStatusUpdate.write();
        }

        switch (_kanbanStatusUpdate.Type)
        {
            case LeanKanbanStatus::Assigned:
                kanbanUpd = KanbanUpdAssigned::construct();
                break;
            case LeanKanbanStatus::Received:
                kanbanUpd = KanbanUpdReceived::construct();
                break;
            case LeanKanbanStatus::Emptied:
                kanbanUpd = KanbanUpdEmptied::construct();
                break;
            default:
                throw error(Error::wrongUseOfFunction(funcname()));
        }

        kanbanUpd.parmParmBuffer(_kanbanStatusUpdate);
        kanbanUpd.parmUpdateJobs(_updateJobs);
        kanbanUpd.parmKanbanStatus(_kanbanStatus);
        kanbanUpd.parmNotifyChangeStatus(_notifyStatus);

        return kanbanUpd;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>
