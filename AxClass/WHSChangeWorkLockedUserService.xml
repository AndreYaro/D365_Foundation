<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSChangeWorkLockedUserService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The service class for executing the 'Change Work Locked user' business operation.
/// </summary>
public class WHSChangeWorkLockedUserService extends WHSChangeWorkDetailsServiceBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>startOperation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Entry point for the business operation.
    /// </summary>
    /// <param name="_dataContract">The data contract object reference.</param>
    public void startOperation(WHSChangeWorkLockedUserDataContract _dataContract)
    {
        this.loopWorkLinesAndDoOperation(_dataContract);
    }

]]></Source>
			</Method>
			<Method>
				<Name>doOperation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the actual business operation. Overridden to do the actual implementation.
    /// </summary>
    /// <param name="_workId">Work Id to modify.</param>
    /// <param name="_dataContract">The data contract object reference.</param>
    protected void doOperation(WHSWorkId _workId, WHSChangeWorkDetailsDataContract _dataContract)
    {
        var dataContract = _dataContract as WHSChangeWorkLockedUserDataContract;
        this.updateWorkLockedUser(_workId, dataContract.parmNewWorkLockedUser());
    }

]]></Source>
			</Method>
			<Method>
				<Name>operationFailedErrorMsg</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns the message for errors in the operation.
    /// </summary>
    /// <returns>The message to use for formatting the user error message.</returns>
    protected str operationFailedErrorMsg()
    {
        return "@WAX:WorkLockedUserUpdateFailed";
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateWorkLockedUser</Name>
				<Source><![CDATA[
    private void updateWorkLockedUser(WHSWorkId _workId, WHSUserId _newWorkUserId)
    {
        ttsbegin;

        var workTable = WHSWorkTable::find(_workId, true);
        if (workTable.LockedUser != _newWorkUserId)
        {
            if (!workTable.allowChangeWorkLockedUser())
            {
                throw error("@WAX3756");
            }

            workTable.LockedUser = _newWorkUserId;
            if (!workTable.validateField(fieldNum(WHSWorkTable, LockedUser)))
            {
                throw Exception::Error; // Just throw. The error messages will come from validateField and catch block.
            }
            workTable.UserManuallyAssigned = _newWorkUserId ? NoYes::Yes : NoYes::No;
            workTable.update();
        }
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>