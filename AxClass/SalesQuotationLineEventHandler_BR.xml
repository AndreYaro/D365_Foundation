<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesQuotationLineEventHandler_BR</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SalesQuotationLineEventHandler_BR</c> class is used by event handlers from SalesQuotationLine table methods.
/// </summary>
class SalesQuotationLineEventHandler_BR
{
    SalesQuotationLine salesQuotationLine;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes a new instance of the SalesQuotationLineEventHandler_BR class.
    /// </summary>
    /// <param name="_salesQuotationLine">The current instance of SalesQuotationLine table.</param>
    public void new(SalesQuotationLine _salesQuotationLine)
    {
        salesQuotationLine = _salesQuotationLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>onCFOPChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes after the CFOP field is changed.
    /// </summary>
    protected void onCFOPChange()
    {
        salesQuotationLine.onCFOPChange_BR();
    }

]]></Source>
			</Method>
			<Method>
				<Name>onDeliveryPostalAddressChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes after the delivery postal address field is changed.
    /// </summary>
    protected void onDeliveryPostalAddressChange()
    {
        salesQuotationLine.onDeliveryPostalAddressChange_BR();
    }

]]></Source>
			</Method>
			<Method>
				<Name>onTaxChange</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes after the tax field is changed.
    /// </summary>
    /// <param name="_fieldId">
    /// A table field id.
    /// </param>
    protected void onTaxChange(FieldId _fieldId)
    {
        salesQuotationLine.onTaxChange_BR(_fieldId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postModifiedField_BR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes after a modification in a Brazilian field.
    /// </summary>
    /// <param name="_fieldId">The modified field ID.</param>
    public void postModifiedField_BR(FieldId _fieldId)
    {
        switch (_fieldId)
        {
            case fieldNum(SalesQuotationLine, DeliveryPostalAddress):
                this.onDeliveryPostalAddressChange();
                break;

            case fieldNum(SalesQuotationLine, cfopTable_BR):
                this.onCFOPChange();
                break;

            case fieldNum(SalesQuotationLine, TaxItemGroup):
            case fieldNum(SalesQuotationLine, TaxGroup):
                this.onTaxChange(_fieldId);
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>postModifiedFieldHandler_BR</Name>
				<Source><![CDATA[
        [PostHandlerFor(tableStr(SalesQuotationLine), tableMethodStr(SalesQuotationLine, modifiedField))]
    /// <summary>
    /// Executes after a modification in a Brazilian field.
    /// </summary>
    /// <param name="_args">The arguments from the original method.</param>
    /// <remarks>This method is called from an event handler.</remarks>
    public static void postModifiedFieldHandler_BR(XppPrePostArgs _args)
    {
        if (!BrazilParameters::isEnabled())
        {
            return;
        }

        SalesQuotationLineEventHandler_BR::postModifiedFieldHandlerServer_BR(_args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>postModifiedFieldHandlerServer_BR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes after a modification in a Brazilian field on the server.
    /// </summary>
    /// <param name="_args">The arguments from the original method.</param>
    /// <remarks>
    /// This method explicilty runs on the server since the SalesQuotationLineEventHandler_BR
    /// class is marked as run on server and would otherwise cause multiple RPCs.
    /// </remarks>
    public static void postModifiedFieldHandlerServer_BR(XppPrePostArgs _args)
    {
        SalesQuotationLineEventHandler_BR salesQuotationLineEventHandler = new SalesQuotationLineEventHandler_BR(_args.getThis());

        salesQuotationLineEventHandler.postModifiedField_BR(_args.getArg('_fieldId'));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>