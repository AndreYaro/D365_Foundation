<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>NettingMarkTransMgrMarkWithBundleSize</Name>
	<SourceCode>
		<Declaration><![CDATA[
[NettingRuleSelectionPolicyTypeAttribute(NettingRuleSelectionPolicyType::MarkWithBundleSize)]
internal final class NettingMarkTransMgrMarkWithBundleSize extends AbsNettingMarkTransMgr
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    internal void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>markTransWithPolicy</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public INettingMarked markTransWithPolicy(Query _debitQuery, TableId _debitTableId, CompanyId _debitCompany, Query _creditQuery, TableId _creditTableId, CompanyId _creditCompany)
    {
        this.init();
        QueryRun debitQueryRun = new QueryRun(_debitQuery);
        QueryRun creditQueryRun = new QueryRun(_creditQuery);
        AmountCur debitAmountCounted = 0;
        AmountCur creditAmountCounted = 0;
        int currentPacked = 0;
        int bundleSizeConfigured = this.getBundleSize();
        while (currentPacked < bundleSizeConfigured)
        {
            if (debitAmountCounted <= creditAmountCounted)
            {
                changecompany(_debitCompany)
                {
                    if (debitQueryRun.next())
                    {
                        Common debitRecord = debitQueryRun.get(_debitTableId);
                        markedResult.addDebitMarked(NettingMarkedTrans::construct(
                            this.getTableId(_debitTableId, debitRecord),
                            this.getRecId(_debitTableId, debitRecord),
                            debitRecord.getFieldValue(AmountCurFieldName)));
                        debitAmountCounted += abs(debitRecord.getFieldValue(AmountCurFieldName));
                        currentPacked ++;
                    }
                    else
                    {
                        break;
                    }
                }
            }
            else
            {
                changecompany(_creditCompany)
                {
                    if (creditQueryRun.next())
                    {
                        Common creditRecord = creditQueryRun.get(_creditTableId);
                        markedResult.addCreditMarked(NettingMarkedTrans::construct(
                            this.getTableId(_creditTableId, creditRecord),
                            this.getRecId(_creditTableId, creditRecord),
                            creditRecord.getFieldValue(AmountCurFieldName)));
                        creditAmountCounted += abs(creditRecord.getFieldValue(AmountCurFieldName));
                        currentPacked ++;
                    }
                    else
                    {
                        break;
                    }
                }
            }
        }
        return markedResult;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getBundleSize</Name>
				<Source><![CDATA[
    private int getBundleSize()
    {
        return BundleSize;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>