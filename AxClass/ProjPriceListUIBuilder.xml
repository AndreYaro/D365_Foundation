<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjPriceListUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>ProjPriceListUIBuilder</c> class builds and handles the user interface dialog for the
/// <c>ProjPriceList</c> reports.
/// </summary>
class ProjPriceListUIBuilder extends SrsReportDataContractUIBuilder
{
    DialogField                     projResource;
    FormBuildReferenceGroupControl  resource;
    FormReferenceGroupControl       selectedResource;
    FormBuildGroupControl           viewGrp;
    ResourceRecId                   projResourceRecId;
    ProjPriceListContract           rdlContract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
/// <summary>
/// Transfers data from the dialog into the data contract object.
/// </summary>
public void getFromDialog()
{
    super();

    selectedResource  = this.dialog().dialogForm().control('ResourceRecId');
    projResourceRecId = selectedResource.value();
    rdlContract.parmResource(projResourceRecId);
}
]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
public void postBuild()
{
    FormBuildStringControl categoryCtrl;

    rdlContract = this.dataContractObject() as ProjPriceListContract;
    super();

    //Hide the In64 resource control
    projResource = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(ProjPriceListContract, parmResource));
    if (projResource)
    {
        projResource.visible(false);
    }

    //Add ReferenceGroup control for resource lookup
    viewGrp = this.dialog().dialogForm().control('View');
    resource  = viewGrp.addControl(FormControlType::ReferenceGroup, identifierStr(ResourceRecId));
    resource.extendedDataType(extendedTypeNum(ResourceRecId));

    //Adjust the position of resource field. Should be after Category Id.
    categoryCtrl = this.bindInfo().getDialogField(this.dataContractObject(), methodStr(ProjPriceListContract, parmCategoryId)).control();
    viewGrp.moveControl(resource.id(), categoryCtrl.id());
}
]]></Source>
			</Method>
			<Method>
				<Name>postRun</Name>
				<Source><![CDATA[
/// <summary>
/// Runs the post run actions
/// </summary>
public void postRun()
{
    super();

    projResourceRecId = rdlContract.parmResource();
    selectedResource  = this.dialog().dialogForm().control('ResourceRecId');
    selectedResource.value(projResourceRecId);
}
]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>