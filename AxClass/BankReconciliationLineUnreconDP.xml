<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BankReconciliationLineUnreconDP</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Data provider class of unreconciled line for Bank reconciliation ER report.
/// </summary>
public final class BankReconciliationLineUnreconDP
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getMatchedDocumentTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the matched company transaction data stored in temporary table for report printing
    /// </summary>
    /// <param name = "_reconciliationHeaderRecId">The RecId of the bank reconciliation header for print</param>
    /// <returns>The temporary table storing the company transactions information for report printing.</returns>
    [SRSReportDataSet(tableStr(BankReconciliationElectronicReportDocumentTmp)), Hookable(false)]
    public BankReconciliationElectronicReportDocumentTmp getMatchedDocumentTmp(RecId _reconciliationHeaderRecId)
    {
        BankReconciliationElectronicReportDocumentTmp tmpDocument;
        BankReconciliationHeader reconciliationHeader = BankReconciliationHeader::find(_reconciliationHeaderRecId);
        BankDocumentTableViewResult bankDocumentTableViewResult;
        BankReconciliationMatchingItemView bankReconciliationMatchingItemView;
        NoYes matched = NoYes::Yes;

        tmpDocument.skipDatabaseLog(true);
        tmpDocument.skipAosValidation(true);
        tmpDocument.skipDataMethods(true);
        tmpDocument.skipEvents(true);

        // Company data unreconciled and matched
        insert_recordset tmpDocument (BankDocumentTableView, BankCurrencyAmount, CorrectionBankCurrencyAmount,
            BankTransType, Reference, TransactionDate,
            DocumentNum, DocumentType, Cancelled, Posted, IsMatched)
            select BankDocumentTableRecId, BankCurrencyAmount, CorrectionBankCurrencyAmount,
                BankTransType, Reference, TransactionDate,
                DocumentNum, DocumentType, Cancelled, Posted, matched
                from bankDocumentTableViewResult
                where bankDocumentTableViewResult.IsReconciled == NoYes::No
                    && bankDocumentTableViewResult.AccountId == reconciliationHeader.AccountId
                    && bankDocumentTableViewResult.DocumentDate <= reconciliationHeader.CutOffDate
                join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankDocumentTable == bankDocumentTableViewResult.BankDocumentTableRecId;

        // Company data reconciled and cancelled and matched
        BankDocumentBankChequeTableView bankDocumentBankChequeTableView;
        BankAccountTrans bankAccountTrans;

        insert_recordset tmpDocument (BankDocumentTableView, BankCurrencyAmount, CorrectionBankCurrencyAmount,
            BankTransType, Reference, TransactionDate,
            DocumentNum, DocumentType, Cancelled, Posted, IsMatched)
            select BankDocumentTableRecId, BankCurrencyAmount, CorrectionBankCurrencyAmount,
                BankTransType, Reference, TransactionDate,
                DocumentNum, DocumentType, Cancelled, Posted, matched
                from bankDocumentBankChequeTableView
                where bankDocumentBankChequeTableView.IsReconciled == NoYes::Yes
                    && bankDocumentBankChequeTableView.DocumentType == BankDocumentBookType::Cheque
                    && bankDocumentBankChequeTableView.AccountId   == reconciliationHeader.AccountId
                    && bankDocumentBankChequeTableView.DocumentDate <= reconciliationHeader.CutoffDate
                    && bankDocumentBankChequeTableView.Cancelled   == NoYes::Yes
                    && bankDocumentBankChequeTableView.SourceRelationType == tableNum(BankChequeTable)
                join bankAccountTrans
                    where bankAccountTrans.SourceTableId == bankDocumentBankChequeTableView.SourceRelationType
                        && bankAccountTrans.SourceRecId == bankDocumentBankChequeTableView.SourceRecId
                        && bankAccountTrans.Cancel == NoYes::Yes
                        && bankAccountTrans.TransDate > reconciliationHeader.CutoffDate
                join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankDocumentTable == bankDocumentBankChequeTableView.BankDocumentTableRecId;

        // Include generate voucher document lines
        BankDocumentStatementLineView bankDocumentStatementLineView;
        BankStmtISOReportEntry bankStmtISOReportEntry;
        insert_recordset tmpDocument
            (BankDocumentTableView, BankCurrencyAmount, CorrectionBankCurrencyAmount, BankTransType, Reference,
                DocumentNum, DocumentType, Cancelled, Posted, IsMatched, TransactionDate)
            select RecId, BankCurrencyAmount, CorrectionBankCurrencyAmount, BankTransType, Reference,
                DocumentNum, DocumentType, Cancelled, Posted, matched
                from bankDocumentStatementLineView
                where bankDocumentStatementLineView.IsReconciled == NoYes::Yes
                    && bankDocumentStatementLineView.DocumentType == BankDocumentBookType::BankStatement
                    && bankDocumentStatementLineView.AccountId == reconciliationHeader.AccountId
                    && bankDocumentStatementLineView.SourceRelationType == tableNum(BankStmtISOReportEntry)
                join AccountingDate from bankStmtISOReportEntry
                    where bankStmtISOReportEntry.RecId == bankDocumentStatementLineView.SourceRecId
                        && bankStmtISOReportEntry.Reconciled == NoYes::No
                        && bankStmtISOReportEntry.AccountingDate <= reconciliationHeader.CutoffDate;

        // For bank clearing, BankAccountTrans is generated when its status is cleared.
        BankReconciliationReportGERUtils::updateBankClearingInfo(tmpDocument);

        // Update transaction date for PDC
        BankReconciliationReportGERUtils::updateTransDateForPDC(tmpDocument, reconciliationHeader);

        // Update transaction date for generate voucher
        BankReconciliationReportGERUtils::updateTransDateForGenerateVoucher(tmpDocument);

        return tmpDocument;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnmatchedDocumentTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the matched company transaction data stored in temporary table for report printing
    /// </summary>
    /// <param name = "_reconciliationHeaderRecId">The RecId of the bank reconciliation header for print</param>
    /// <returns>The temporary table storing the company transactions information for report printing.</returns>
    [SRSReportDataSet(tableStr(BankReconciliationElectronicReportDocumentTmp)), Hookable(false)]
    public BankReconciliationElectronicReportDocumentTmp getUnmatchedDocumentTmp(RecId _reconciliationHeaderRecId)
    {
        BankReconciliationElectronicReportDocumentTmp tmpDocument;
        BankReconciliationHeader reconciliationHeader = BankReconciliationHeader::find(_reconciliationHeaderRecId);
        BankDocumentTableViewResult bankDocumentTableViewResult;
        BankReconciliationMatchingItemView bankReconciliationMatchingItemView;
        NoYes matched = NoYes::No;

        tmpDocument.skipDatabaseLog(true);
        tmpDocument.skipAosValidation(true);
        tmpDocument.skipDataMethods(true);
        tmpDocument.skipEvents(true);

        // Company data unreconciled and matched
        insert_recordset tmpDocument (BankDocumentTableView, BankCurrencyAmount, CorrectionBankCurrencyAmount,
            BankTransType, Reference, TransactionDate,
            DocumentNum, DocumentType, Cancelled, Posted, IsMatched)
            select BankDocumentTableRecId, BankCurrencyAmount, CorrectionBankCurrencyAmount,
                BankTransType, Reference, TransactionDate,
                DocumentNum, DocumentType, Cancelled, Posted, matched
                from bankDocumentTableViewResult
                where bankDocumentTableViewResult.IsReconciled == NoYes::No
                    && bankDocumentTableViewResult.AccountId == reconciliationHeader.AccountId
                    && bankDocumentTableViewResult.DocumentDate <= reconciliationHeader.CutOffDate
                notexists join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankDocumentTable == bankDocumentTableViewResult.BankDocumentTableRecId;

        // Company data reconciled and cancelled and matched
        BankDocumentBankChequeTableView bankDocumentBankChequeTableView;
        BankAccountTrans bankAccountTrans;

        insert_recordset tmpDocument (BankDocumentTableView, BankCurrencyAmount, CorrectionBankCurrencyAmount,
            BankTransType, Reference, TransactionDate,
            DocumentNum, DocumentType, Cancelled, Posted, IsMatched)
            select BankDocumentTableRecId, BankCurrencyAmount, CorrectionBankCurrencyAmount,
                BankTransType, Reference, TransactionDate,
                DocumentNum, DocumentType, Cancelled, Posted, matched
                from bankDocumentBankChequeTableView
                where bankDocumentBankChequeTableView.IsReconciled == NoYes::Yes
                    && bankDocumentBankChequeTableView.DocumentType == BankDocumentBookType::Cheque
                    && bankDocumentBankChequeTableView.AccountId   == reconciliationHeader.AccountId
                    && bankDocumentBankChequeTableView.DocumentDate <= reconciliationHeader.CutoffDate
                    && bankDocumentBankChequeTableView.Cancelled   == NoYes::Yes
                    && bankDocumentBankChequeTableView.SourceRelationType == tableNum(BankChequeTable)
                join bankAccountTrans
                    where bankAccountTrans.SourceTableId == bankDocumentBankChequeTableView.SourceRelationType
                        && bankAccountTrans.SourceRecId == bankDocumentBankChequeTableView.SourceRecId
                        && bankAccountTrans.Cancel == NoYes::Yes
                        && bankAccountTrans.TransDate > reconciliationHeader.CutoffDate
                notexists join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankDocumentTable == bankDocumentBankChequeTableView.BankDocumentTableRecId;

        // For bank clearing, BankAccountTrans is generated when its status is cleared.
        BankReconciliationReportGERUtils::updateBankClearingInfo(tmpDocument);

        // Update transaction date for PDC
        BankReconciliationReportGERUtils::updateTransDateForPDC(tmpDocument, reconciliationHeader);

        return tmpDocument;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getMatchedStatementTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the matched bank statement transaction data stored in temporary table for report printing
    /// </summary>
    /// <param name = "_reconciliationHeaderRecId">The RecId of the bank reconciliation header for print</param>
    /// <returns>The temporary table storing the bank statement information for report printing.</returns>
    [SRSReportDataSet(tableStr(BankReconciliationElectronicReportStatementTmp)), Hookable(false)]
    public BankReconciliationElectronicReportStatementTmp getMatchedStatementTmp(RecId _reconciliationHeaderRecId)
    {
        BankReconciliationElectronicReportStatementTmp tmpStatement;
        BankReconciliationHeader reconciliationHeader = BankReconciliationHeader::find(_reconciliationHeaderRecId);
        BankReconciliationMatchingItemView bankReconciliationMatchingItemView;
        BankStmtISOReportEntry reportEntry;
        NoYes matched = NoYes::Yes;

        tmpStatement.skipDatabaseLog(true);
        tmpStatement.skipAosValidation(true);
        tmpStatement.skipDataMethods(true);
        tmpStatement.skipEvents(true);

        BankStmtISOAccountStatement bankStmtISOAccountStatement;
        BankReconciliationStatementOpenView statementView;
        TransDateTime cutOffDateTime = reconciliationHeader.getCutOffDateTime();

        insert_recordset tmpStatement (BankStmtISOReportEntry, BankCurrencyAmount, ProprietaryBankTransactionCode, ReferenceNumber, EntryReference,
            BankDocumentNumber, IsNewBankDocument, BankStmtISOAccountStatement, AdditionalEntryInformation, IsMatched)
            select BankISOReportEntryRecId, BankCurrencyAmount, ProprietaryBankTransactionCode, ReferenceNumber, EntryReference,
                BankDocumentNumber, IsNewBankDocument, BankISOAccountStatementRecId, AdditionalEntryInformation, matched
                from statementView
                    where statementView.Reconciled == NoYes::No
                        && statementView.BookingDateTime <= cutOffDateTime
                        && statementView.Status != BankStatementStatus::Open
                        && statementView.BankAccountTable == reconciliationHeader.AccountId
                join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankStmtISOReportEntry == statementView.BankISOReportEntryRecId;

        // update booking date
        BankReconciliationReportGERUtils::updateBookingDate(tmpStatement);

        return tmpStatement;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getUnmatchedStatementTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the unmatched bank statement transaction data stored in temporary table for report printing
    /// </summary>
    /// <param name = "_reconciliationHeaderRecId">The RecId of the bank reconciliation header for print</param>
    /// <returns>The temporary table storing the bank statement information for report printing.</returns>
    [SRSReportDataSet(tableStr(BankReconciliationElectronicReportStatementTmp)), Hookable(false)]
    public BankReconciliationElectronicReportStatementTmp getUnmatchedStatementTmp(RecId _reconciliationHeaderRecId)
    {
        BankReconciliationElectronicReportStatementTmp tmpStatement;
        BankReconciliationHeader reconciliationHeader = BankReconciliationHeader::find(_reconciliationHeaderRecId);
        BankReconciliationMatchingItemView bankReconciliationMatchingItemView;
        BankStmtISOReportEntry reportEntry;
        NoYes unMatched = NoYes::No;

        tmpStatement.skipDatabaseLog(true);
        tmpStatement.skipAosValidation(true);
        tmpStatement.skipDataMethods(true);
        tmpStatement.skipEvents(true);

        BankStmtISOAccountStatement bankStmtISOAccountStatement;
        BankReconciliationStatementOpenView statementView;
        TransDateTime cutOffDateTime = reconciliationHeader.getCutOffDateTime();

        insert_recordset tmpStatement (BankStmtISOReportEntry, BankCurrencyAmount, ProprietaryBankTransactionCode, ReferenceNumber, EntryReference,
            BankDocumentNumber, IsNewBankDocument, BankStmtISOAccountStatement, AdditionalEntryInformation, IsMatched)
            select BankISOReportEntryRecId, BankCurrencyAmount, ProprietaryBankTransactionCode, ReferenceNumber, EntryReference,
                BankDocumentNumber, IsNewBankDocument, BankISOAccountStatementRecId, AdditionalEntryInformation, unMatched
                from statementView
                    where statementView.Reconciled == NoYes::No
                        && statementView.BookingDateTime <= cutOffDateTime
                        && statementView.Status != BankStatementStatus::Open
                        && statementView.BankAccountTable == reconciliationHeader.AccountId
                notexists join bankReconciliationMatchingItemView
                    where bankReconciliationMatchingItemView.BankReconciliationHeader == _reconciliationHeaderRecId
                        && bankReconciliationMatchingItemView.BankStmtISOReportEntry == statementView.BankISOReportEntryRecId;

        // update booking date
        BankReconciliationReportGERUtils::updateBookingDate(tmpStatement);

        return tmpStatement;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>