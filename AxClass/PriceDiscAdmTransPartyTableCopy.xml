<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PriceDiscAdmTransPartyTableCopy</Name>
	<SourceCode>
		<Declaration><![CDATA[
[PriceDiscPartyCodeTypeFactoryAttribute(PriceDiscPartyCodeType::Table)]
class PriceDiscAdmTransPartyTableCopy extends PriceDiscAdmTransPartyCopy
{ 
    PriceDiscAccountRelation    accountRelation;
    boolean                     searchAll;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>parmAccountRelation</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final PriceDiscAccountRelation parmAccountRelation(PriceDiscAccountRelation _accountRelation = accountRelation)
    {
        accountRelation = _accountRelation;
        return accountRelation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmSearchAll</Name>
				<Source><![CDATA[
    [Hookable(false)]
    public final boolean parmSearchAll(boolean _searchAll = searchAll)
    {
        searchAll = _searchAll;
        return searchAll;
    }

]]></Source>
			</Method>
			<Method>
				<Name>applyFilters</Name>
				<Source><![CDATA[
    protected void applyFilters()
    {
        super();
        
        qbdsPriceDiscTable.addRange(fieldNum(PriceDiscTable, AccountCode)).value(queryValue(PriceDiscPartyCodeType::Table));

        if (accountRelation)
        {
            qbdsPriceDiscTable.addRange(fieldNum(PriceDiscTable, AccountRelation)).value(queryValue(accountRelation));
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>addDataSources</Name>
				<Source><![CDATA[
    /// <summary>
    /// Joins <c>CustTable</c> to <c>PriceDiscTable</c>.
    /// </summary>
    /// <param name = "_qbdsPriceDiscTable">The <c>PriceDiscTable</c> datasource to modify.</param>
    protected void addDataSources()
    {
        if (!searchAll)
        {
            super();
            return;
        }

        query = new Query();
        switch (partyRoleModule)
        {
            case ModuleInventCustVend::Cust:
                QueryBuildDataSource qbdsCustTable = query.addDataSource(tableNum(CustTable));
                qbdsPriceDiscTable = qbdsCustTable.addDataSource(tableNum(PriceDiscTable));
                qbdsPriceDiscTable.relations(false);
                qbdsPriceDiscTable.fetchMode(QueryFetchMode::One2One);
                qbdsPriceDiscTable.addLink(fieldNum(CustTable, AccountNum), fieldNum(PriceDiscTable, AccountRelation), qbdsCustTable.name());
                break;
            case ModuleInventCustVend::Vend:
                QueryBuildDataSource qbdsVendTable = query.addDataSource(tableNum(VendTable));
                qbdsPriceDiscTable = qbdsVendTable.addDataSource(tableNum(PriceDiscTable));
                qbdsPriceDiscTable.relations(false);
                qbdsPriceDiscTable.fetchMode(QueryFetchMode::One2One);
                qbdsPriceDiscTable.addLink(fieldNum(VendTable, AccountNum), fieldNum(PriceDiscTable, AccountRelation), qbdsVendTable.name());
                break;
        }
        super();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>