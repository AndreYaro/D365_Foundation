<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SplitPaymentMandatoryRuleEngine_VendInvoiceinfoTable</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>SplitPaymentMandatoryRuleEngine_VendInvoiceInfoTable</c> class is extended class for updating method of payment and
/// payment specification in Vendor invoice journal line.
/// </summary>
public class SplitPaymentMandatoryRuleEngine_VendInvoiceInfoTable extends SplitPaymentMandatoryRuleEngine
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getVendInvoiceInfoTable</Name>
				<Source><![CDATA[
    private VendInvoiceInfoTable getVendInvoiceInfoTable()
    {
        return this.parmCommonTable() as VendInvoiceInfoTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>findRuleForOrder</Name>
				<Source><![CDATA[
    protected SplitPaymentMandatoryRule_PL findRuleForOrder(Amount _totalAmount, PaymMode _paymMode = '', PaymSpec _paymSpec = '')
    {
        VendInvoiceInfoLine vendInvoiceInfoLine;
        SplitPaymentMandatoryRule_PL rule;
        SplitPaymentMandatoryRule_PL voluntaryRule;
        VendInvoiceInfoTable vendInvoiceInfoTable = this.getVendInvoiceInfoTable();
        IntrastatCountryRegionParameters intrastatCountryRegionParameters = IntrastatCountryRegionParameters::find(vendInvoiceInfoTable.vendTable_InvoiceAccount().postalAddress().CountryRegionId);
        boolean isDomesticPartner = intrastatCountryRegionParameters && intrastatCountryRegionParameters.CountryRegionType == CountryRegionType::Domestic;

        while select vendInvoiceInfoLine
            group by ItemId
            where vendInvoiceInfoLine.ParmId == vendInvoiceInfoTable.ParmId
                && vendInvoiceInfoLine.TableRefId == vendInvoiceInfoTable.TableRefId
        {
            SplitPaymentMandatoryItemGroupId_PL itemGroupId = SplitPaymentMandatoryItemGroup_PL::findGroupByItem(
                this.getDocumentType(),
                vendInvoiceInfoLine.ItemId);

            rule = null;

            if (isDomesticPartner)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::Domestic)
                    .withCurrencyCode(vendInvoiceInfoTable.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }

            if (!rule)
            {
                rule = SplitPaymentMandatoryRule_PL::findByContract(SplitPaymentMandatoryRuleContract::construct()
                    .withDocumentType(this.getDocumentType())
                    .withAmount(_totalAmount)
                    .withItemGroupId(itemGroupId)
                    .withCountryType(SplitPaymentMandatoryCountryRegionType_PL::All)
                    .withCurrencyCode(vendInvoiceInfoTable.CurrencyCode)
                    .withSearchByTaxItemGroup(false));
            }

            if (!voluntaryRule && rule && rule.VoluntarySplitPayment == NoYes::Yes)
            {
                voluntaryRule = rule;
            }

            if (rule && rule.VoluntarySplitPayment == NoYes::No)
            {
                break;
            }
        }

        if (!rule && voluntaryRule)
        {
            rule = voluntaryRule;
        }

        return rule;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAmount</Name>
				<Source><![CDATA[
    protected Amount getAmount()
    {
        PurchTotals purchTotals = PurchTotals::newParmTable(
            this.getVendInvoiceInfoTable(),
            PurchParmUpdate::find(this.getVendInvoiceInfoTable().ParmId).SpecQty,
            this.getVendInvoiceInfoTable().ParmId,
            '',
            this.getVendInvoiceInfoTable().Ordering);

        purchTotals.calc();

        return purchTotals.purchTotalAmount();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCommonTable</Name>
				<Source><![CDATA[
    protected void updateCommonTable(PaymMode _paymMode, PaymSpec _paymSpec)
    {
        VendInvoiceInfoTable vendInvoiceInfoTableUpd;

        ttsbegin;

        vendInvoiceInfoTableUpd = VendInvoiceInfoTable::findRecId(this.getVendInvoiceInfoTable().RecId, true);

        if (vendInvoiceInfoTableUpd)
        {
            vendInvoiceInfoTableUpd.PaymMode = _paymMode;
            vendInvoiceInfoTableUpd.PaymSpec = _paymSpec;

            if (vendInvoiceInfoTableUpd.validateWrite())
            {
                vendInvoiceInfoTableUpd.update();
            }
        }

        ttscommit;

        this.getVendInvoiceInfoTable().reread();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymMode</Name>
				<Source><![CDATA[
    protected PaymMode getCurrentPaymMode()
    {
        return this.getVendInvoiceInfoTable().PaymMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDocumentType</Name>
				<Source><![CDATA[
    protected SplitPaymentDocumentType_PL getDocumentType()
    {
        return SplitPaymentDocumentType_PL::Vendor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isSplitPayment</Name>
				<Source><![CDATA[
    protected boolean isSplitPayment()
    {
        return this.getCurrentSplitPayment() == NoYes::Yes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SplitPaymentMandatoryRuleEngine_VendInvoiceInfoTable construct()
    {
        return new SplitPaymentMandatoryRuleEngine_VendInvoiceInfoTable();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrentPaymModeSpec</Name>
				<Source><![CDATA[
    protected PaymSpec getCurrentPaymModeSpec()
    {
        return this.getVendInvoiceInfoTable().PaymSpec;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>