<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TaxServiceFeatureVersionRestorationController_Tax_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
using DesigntimeRestore = Microsoft.Dynamics365.GlobalizationFeature.Feature.DesigntimeRestore;

/// <summary>
/// The <c>TaxServiceFeatureVersionRestorationController_Extension</c> is the extension of <c>TaxServiceFeatureVersionRestorationController</c>.
/// </summary>
[ExtensionOf(classStr(TaxServiceFeatureVersionRestorationController))]
internal final class TaxServiceFeatureVersionRestorationController_Tax_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initForMandantoryFeatureVersions</Name>
				<Source><![CDATA[
    protected DesigntimeRestore.FeatureVersionIdCollection initForMandantoryFeatureVersions(DesigntimeRestore.FeatureVersionIdCollection _featureVersionIdCollection)
    {
        var featureVersionIdCollection = next initForMandantoryFeatureVersions(_featureVersionIdCollection);

        TaxIntegrationTaxServiceParameters integrationParameter;
        while select crosscompany FeatureSetupId, ISVFeatureSetupId from integrationParameter
            where integrationParameter.FeatureSetupId != ""
                || integrationParameter.ISVFeatureSetupId != ""
        {
            this.addFeatureIdToFeatureVersionCollection(featureVersionIdCollection, integrationParameter.FeatureSetupId);
            this.addFeatureIdToFeatureVersionCollection(featureVersionIdCollection, integrationParameter.ISVFeatureSetupId);
        }

        return featureVersionIdCollection;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addFeatureIdToFeatureVersionCollection</Name>
				<Source><![CDATA[
    private void addFeatureIdToFeatureVersionCollection(DesigntimeRestore.FeatureVersionIdCollection _featureVersionCollection, str _integrationFeatureId)
    {
        if (_integrationFeatureId)
        {
            str featureGuidStr;
            ERFeatureVersionNumber versionNumber;
            [featureGuidStr, versionNumber] = str2con(_integrationFeatureId, '|');
            var featureGuid = str2Guid(featureGuidStr);

            if (!_featureVersionCollection.Contains(featureGuid, versionNumber))
            {
                TaxServiceDesignTimeView designTime;
                select Name from designTime
                    where designTime.FeatureGUID == featureGuid
                        && designTime.VersionNumber == versionNumber;

                if (!designTime.Name)
                {
                    _featureVersionCollection.Add(featureGuid, versionNumber);
                }
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>