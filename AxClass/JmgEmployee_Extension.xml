<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>JmgEmployee_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
public static class JmgEmployee_Extension
{
    const str CustomExportTimeRegistrationWorkerToExcelOptionId = 'JmgEmployeeCustom';

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>ExportToExcel_DataEntityCustom_OnInitializing</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add <c>OfficeMenuEventArgs</c> so we can customize and add menu item for time registration worker.
    /// </summary>
    /// <param name = "sender">The form sender object.</param>
    /// <param name = "e">The form event args.</param>
    [FormEventHandler(formStr(HCMWorker), FormEventType::Initializing)]
    public static void ExportToExcel_DataEntityCustom_OnInitializing(xFormRun sender, FormEventArgs e)
    {
        FormRun formRun = sender as FormRun;
        if (formRun)
        {
            OfficeFormRunHelper officeHelper = formRun.officeHelper();
            if (officeHelper)
            {
                officeHelper.OfficeMenuInitializing += eventhandler(JmgEmployee_Extension::officeMenuInitializingHandler);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>ExportToExcel_WorkerV2_DataEntityCustom_OnInitializing</Name>
				<Source><![CDATA[
    /// <summary>
    /// Add <c>OfficeMenuEventArgs</c> so we can customize and add menu item for time registration worker.
    /// </summary>
    /// <param name = "sender">The form sender object.</param>
    /// <param name = "e">The form event args.</param>
    [FormEventHandler(formStr(HCMWorkerV2), FormEventType::Initializing)]
    public static void ExportToExcel_WorkerV2_DataEntityCustom_OnInitializing(xFormRun sender, FormEventArgs e)
    {
        FormRun formRun = sender as FormRun;
        if (formRun)
        {
            OfficeFormRunHelper officeHelper = formRun.officeHelper();
            if (officeHelper)
            {
                officeHelper.OfficeMenuInitializing += eventhandler(JmgEmployee_Extension::officeMenuInitializingHandler);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>officeMenuInitializingHandler</Name>
				<Source><![CDATA[
    private static void officeMenuInitializingHandler(FormRun _formRun, OfficeMenuEventArgs _eventArgs)
    {
        OfficeGeneratedExportMenuItem jmgEmployeeMenuItem = OfficeGeneratedExportMenuItem::construct(
            tableStr(JmgTimeAndAttendanceTimeRegistrationWorkerEntity),
            CustomExportTimeRegistrationWorkerToExcelOptionId);
        jmgEmployeeMenuItem.displayName(strfmt("@SYS76681", "@SYS319111", '', curExt()));
                
        jmgEmployeeMenuItem.getDataEntityContext += eventhandler(JmgEmployee_Extension::getDataEntityContextHandler);

        List customMenuItems = _eventArgs.menuOptions().customMenuItems();
        customMenuItems.addEnd(jmgEmployeeMenuItem);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDataEntityContextHandler</Name>
				<Source><![CDATA[
    private static void getDataEntityContextHandler(OfficeGeneratedExportMenuItem _menuItem, FormRun _formRun, ExportToExcelDataEntityContext _dataEntityContext)    
    {
        if (_menuItem.id() == CustomExportTimeRegistrationWorkerToExcelOptionId)
        {
            _dataEntityContext.addEntityDefault(tablestr(JmgTimeAndAttendanceTimeRegistrationWorkerEntity));        
        }
        
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>