<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CBDDestinationsFormLetterContext_QuotationBase</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>CBDDestinationsFormLetterContext_QuotationBase</c> class is an abstraction
/// for Quotation based documents to support
/// runtime Electronic reporting destinations.
/// </summary>
internal abstract class CBDDestinationsFormLetterContext_QuotationBase
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getCountReferences</Name>
				<Source><![CDATA[
    public NumberOfRecords getCountReferences(Object _formLetter)
    {
        SalesQuotationEditLinesForm salesQuotationEditLinesForm = _formLetter as SalesQuotationEditLinesForm;

        if (!salesQuotationEditLinesForm)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        SalesQuotationParmUpdate salesQuotationParmUpdate = salesQuotationEditLinesForm.salesQuotationParmUpdate();
        SalesQuotationParmTable salesQuotationParmTable;
        SalesQuotationTable salesQuotationTable;

        select count(RecId) from salesQuotationTable
        exists join salesQuotationParmTable
            where salesQuotationParmTable.QuotationId == salesQuotationTable.QuotationId
                && salesQuotationParmTable.ParmId == salesQuotationParmUpdate.ParmId;

        return salesQuotationTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReferenceBuffer</Name>
				<Source><![CDATA[
    public Common getReferenceBuffer(Object _formLetter)
    {
        SalesQuotationEditLinesForm salesQuotationEditLinesForm = _formLetter as SalesQuotationEditLinesForm;

        if (!salesQuotationEditLinesForm)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        SalesQuotationParmUpdate salesQuotationParmUpdate = salesQuotationEditLinesForm.salesQuotationParmUpdate();
        SalesQuotationParmTable salesQuotationParmTable;
        SalesQuotationTable salesQuotationTable;

        select salesQuotationTable
        exists join salesQuotationParmTable
            where salesQuotationParmTable.QuotationId == salesQuotationTable.QuotationId
                && salesQuotationParmTable.ParmId == salesQuotationParmUpdate.ParmId;

        return salesQuotationTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrintContext</Name>
				<Source><![CDATA[
    public PrintMgmtPrintContext getPrintContext(Common _printMgmtReferencedTable, Common _queryTableBuffer)
    {
        FormLetterReport formLetterReport = FormLetterReport::construct(this.getPrintMgmtDocumentType());

        return formLetterReport.getDefaultPrintMgmtContext(_queryTableBuffer, _printMgmtReferencedTable.data());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPrintMgmtDocumentType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets print management document type.
    /// </summary>
    /// <returns>The print management document type.</returns>
    abstract protected PrintMgmtDocumentType getPrintMgmtDocumentType()
    {
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>