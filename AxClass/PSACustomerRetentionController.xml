<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
  <Name>PSACustomerRetentionController</Name>
  <SourceCode>
    <Declaration><![CDATA[
/// <summary>
/// The <c>PSACustomerRetentionController</c> is controller class for the <c>PSACustomerRetention</c>
/// report.
/// </summary>
public class PSACustomerRetentionController extends SrsReportRunController implements BatchRetryable
{
    CustAccount                     custAccount;
    ProjInvoiceProjId               contract;
    ProjId                          projId;
    PSAContractLineNum              billingId;
    PSACustomerRetentionContract    rdpcontract;

    #define.ReportName('PSACustomerRetention.Report')
}
]]></Declaration>
    <Methods>
      <Method>
        <Name>preRunModifyContract</Name>
        <Source><![CDATA[
        protected void preRunModifyContract()
    {
        Query reportQuery;

        reportQuery = this.parmReportContract().parmQueryContracts().lookup(this.getFirstQueryContractKey());
        rdpcontract = this.parmReportContract().parmRdpContract() as PSACustomerRetentionContract;

        custAccount = rdpcontract.parmCustAccount();
        contract = rdpcontract.parmProjInvoiceProjId();
        projId = rdpcontract.parmProjId();
        billingId = rdpcontract.parmPSAContractLineNum();

        this.setRanges(reportQuery,custAccount,contract,projId,billingId);
    }

]]></Source>
      </Method>
      <Method>
        <Name>setRanges</Name>
        <Source><![CDATA[
    /// <summary>
    /// Initializes the report parameters.
    /// </summary>
    /// <param name="_queryContract">
    /// The report query.
    /// </param>
    /// <param name="_custAccount">
    /// A customer account value.
    /// </param>
    /// <param name="_contract">
    /// A contract value.
    /// </param>
    /// <param name="_projId">
    /// A project ID value.
    /// </param>
    /// <param name="_billingId">
    /// A contract line number value.
    /// </param>
    public void setRanges(Query               _queryContract,
                          CustAccount         _custAccount,
                          ProjInvoiceProjId   _contract,
                          ProjId              _projId,
                          PSAContractLineNum  _billingId)
    {
        _queryContract.dataSourceTable(tableNum(ProjInvoiceJour)).clearRange(fieldNum(ProjInvoiceJour,InvoiceAccount));
        _queryContract.dataSourceTable(tableNum(ProjInvoiceRevenue)).clearRange(fieldNum(ProjInvoiceRevenue,ProjId));
        _queryContract.dataSourceTable(tableNum(ProjInvoiceJour)).clearRange(fieldNum(ProjInvoiceJour,ProjInvoiceProjId));
        _queryContract.dataSourceTable(tableNum(ProjInvoiceRevenue)).clearRange(fieldNum(ProjInvoiceRevenue,PSAContractLineNum));

        SRSReportHelper::addParameterValueRangeToQuery(
                _queryContract,
                tableNum(ProjInvoiceJour),
                fieldNum(ProjInvoiceJour,InvoiceAccount),
                _custAccount);

        SRSReportHelper::addParameterValueRangeToQuery(
                _queryContract,
                tableNum(ProjInvoiceRevenue),
                fieldNum(ProjInvoiceRevenue,ProjId),
                _projId);

        SRSReportHelper::addParameterValueRangeToQuery(
                _queryContract,
                tableNum(ProjInvoiceJour),
                fieldNum(ProjInvoiceJour,ProjInvoiceProjId),
                _contract);

         SRSReportHelper::addParameterValueRangeToQuery(
                _queryContract,
                tableNum(ProjInvoiceRevenue),
                fieldNum(ProjInvoiceRevenue,PSAContractLineNum),
                _billingId);
    }

]]></Source>
      </Method>
      <Method>
        <Name>main</Name>
        <Source><![CDATA[
        public static void main(Args _args)
    {
        SrsReportRunController  controller = new PSACustomerRetentionController();
        controller.parmReportName(#ReportName);
        controller.parmArgs(_args);
        controller.startOperation();
    }

]]></Source>
      </Method>
      <Method>
        <Name>isRetryable</Name>
        <Source><![CDATA[
    /// <summary>
    /// Specifies if the batch task is retryable for transient exceptions or not.
    /// </summary>
    /// <returns>
    /// If true is returned, the batch task is retryable, otherwise it is not.
    /// </returns>
    [Hookable(false)]
    final boolean isRetryable() 
    {
        return true;
    }
]]></Source>
      </Method>
    </Methods>
  </SourceCode>
</AxClass>