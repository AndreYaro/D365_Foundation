<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetProposalGenerateSourceFromBudget</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// This class does all the work necessary to send budgeting data from historical budgets to the AI processor.
/// </summary>
internal final class BudgetProposalGenerateSourceFromBudget
{
    private BudgetProposalConfiguration proposalConfig;
    private BudgetTransactionHeader budgetHeader;
    private BudgetTransactionLine budgetLine;
    private BudgetProposalLedgerDimensionCache ledgerDimensionCache;
    private FiscalPeriodStart firstDayInFiscalPeriod;
    private FiscalPeriodEnd lastDayInFiscalPeriod;
    private SysInstrumentationProgressLogger progressLogger;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    internal static BudgetProposalGenerateSourceFromBudget construct()
    {
        BudgetProposalGenerateSourceFromBudget populateSource = new BudgetProposalGenerateSourceFromBudget();

        return populateSource;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();

        progressLogger = SysInstrumentationProgressLogger::createLogger(BudgetProposalConstants::SourceNameBudgetProposalGenerateFromBudget, BudgetProposalConstants::InstrumentationNamespace);
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmProposalConfiguration</Name>
				<Source><![CDATA[
    internal BudgetProposalConfiguration parmProposalConfiguration(BudgetProposalConfiguration _proposalConfig = proposalConfig)
    {
        this.proposalConfig = _proposalConfig;
        return this.proposalConfig;
    }

]]></Source>
			</Method>
			<Method>
				<Name>generate</Name>
				<Source><![CDATA[
    internal void generate()
    {
        using (SysInstrumentationActivityContext activityContext = progressLogger.activityContextForStage(BudgetProposalConstants::ActivityNameGenerateFromActuals))
        {
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameStarDate, date2StrXpp(proposalConfig.StartDate));
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameEndDate, date2StrXpp(proposalConfig.EndDate));
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameBudgetAsOfDate, date2StrXpp(proposalConfig.BudgetAsOfDate));
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameLedger, int642Str(proposalConfig.Ledger));
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameProposalConfigRecId, int642Str(proposalConfig.RecId));
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameTimeSeriesId, proposalConfig.TimeSeriesId);
            activityContext.addCustomProperty(BudgetProposalConstants::PropertyNameSourceTransactions, enum2Symbol(enumNum(BudgetSourceTransactions), proposalConfig.SourceTransactions));

            this.updateLedgerDimensionCache();

            FiscalCalendarRecId fiscalCalendarRecId = CompanyInfoHelper::fiscalCalendarRecId();

            FiscalPeriodStart fiscalPeriodStartDate = FiscalCalendars::findFirstDayOfMonth(fiscalCalendarRecId, proposalConfig.StartDate);
            FiscalPeriodEnd fiscalPeriodEndDate = FiscalCalendars::findLastDayOfMonth(fiscalCalendarRecId, proposalConfig.EndDate);

            FiscalCalendarPeriod fiscalCalendarPeriod;

            while select StartDate, EndDate
            from fiscalCalendarPeriod
            group by fiscalCalendarPeriod.StartDate, fiscalCalendarPeriod.EndDate
            order by fiscalCalendarPeriod.StartDate
            where fiscalCalendarPeriod.FiscalCalendar == fiscalCalendarRecId &&
                fiscalCalendarPeriod.StartDate >= fiscalPeriodStartDate &&
                fiscalCalendarPeriod.EndDate <= fiscalPeriodEndDate &&
                fiscalCalendarPeriod.Type == FiscalPeriodType::Operating
            {
                firstDayInFiscalPeriod = fiscalCalendarPeriod.StartDate;
                lastDayInFiscalPeriod = fiscalCalendarPeriod.EndDate;

                SysDaQueryObject sourceQuery = this.createSourceQueryFromBudgetRegister(true);
                SysDaInsertObject insertObject = this.buildTimeSeriesSourceInsertObject();
                insertObject.query(sourceQuery);
                SysDaInsertStatement insertStatement = new SysDaInsertStatement();
                ttsbegin;
                insertStatement.insert(insertObject);
                ttscommit;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createSourceQueryFromBudgetRegister</Name>
				<Source><![CDATA[
    private SysDaQueryObject createSourceQueryFromBudgetRegister(boolean _joinBudgetLedgerDimension = false)
    {
        // Creates the query used to create data for predictive analysis
        SysDaQueryObject sdqoBudgetLine = this.buildBudgetTransactionLineObject();
        SysDaQueryObject sdqoBudgetHeader = this.buildBudgetTransactionHeaderObject();

        if (_joinBudgetLedgerDimension)
        {
            SysDaQueryObject sdqoLedgerDimensionCache = this.buildBudgetProposalLedgerDimensionCacheObject();
        
            sdqoBudgetLine
                .joinClause(SysDaJoinKind::InnerJoin, sdqoLedgerDimensionCache)
                .joinClause(SysDaJoinKind::ExistsJoin, sdqoBudgetHeader);
        }
        else
        {
            sdqoBudgetLine.joinClause(SysDaJoinKind::InnerJoin, sdqoBudgetHeader);
        }

        return sdqoBudgetLine;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildBudgetProposalLedgerDimensionCacheObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildBudgetProposalLedgerDimensionCacheObject()
    {
        ledgerDimensionCache.skipDataMethods(true);

        SysDaQueryObject sdqo = new SysDaQueryObject(ledgerDimensionCache);
        sdqo.projection().add(fieldStr(BudgetProposalLedgerDimensionCache, BudgetLedgerDimension));
        sdqo.whereClause(new SysDaEqualsExpression(
            new SysDaFieldExpression(ledgerDimensionCache, fieldStr(BudgetProposalLedgerDimensionCache, LedgerDimension)),
            new SysDaFieldExpression(budgetLine, fieldStr(BudgetTransactionLine, LedgerDimension))));
        sdqo.joinClauseKind(SysDaJoinKind::InnerJoin);

        sdqo.groupByClause().add(fieldStr(BudgetProposalLedgerDimensionCache, BudgetLedgerDimension));

        return sdqo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildBudgetTransactionLineObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildBudgetTransactionLineObject()
    {
        // The line object aggregates data from BudgetTransactionHeader by LedgerDimension for a date range. It is then
        // reused throughout the process to gather data for each fiscal period in the date range defined in the configuration header
        budgetLine.skipDataMethods(true);
        SysDaQueryObject sdqo = new SysDaQueryObject(budgetLine);

        this.addFieldsToQueryObject(sdqo.projection());
        this.addLiteralsToQueryObject(sdqo.projection());

        TransDate expressionFirstDayInFiscalPeriod = this.getFirstDayForCriteria(firstDayInFiscalPeriod);

        SysDaGreaterThanOrEqualsExpression whereExpr = new SysDaGreaterThanOrEqualsExpression(
            new SysDaFieldExpression(budgetLine, fieldStr(BudgetTransactionLine, date)),
            new SysDaValueExpression(expressionFirstDayInFiscalPeriod));
        sdqo.whereClause(new SysDaAndExpression(whereExpr, new SysDaLessThanOrEqualsExpression(
            new SysDaFieldExpression(budgetLine, fieldStr(BudgetTransactionLine, date)),
            new SysDaValueExpression(lastDayInFiscalPeriod))));

        // Add groupby for sum
        sdqo.groupByClause().add(fieldStr(BudgetTransactionLine, BudgetType));

        return(sdqo);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFirstDayForCriteria</Name>
				<Source><![CDATA[
    private FiscalPeriodStart getFirstDayForCriteria(FiscalPeriodStart _expressionFirstDayInFiscalPeriod)
    {
        FiscalPeriodStart expressionDate = _expressionFirstDayInFiscalPeriod;

        if (proposalConfig.SourceTransactions == BudgetSourceTransactions::Both && firstDayInFiscalPeriod < proposalConfig.BudgetAsOfDate)
        {
            expressionDate = proposalConfig.BudgetAsOfDate;
        }

        return expressionDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>addFieldsToQueryObject</Name>
				<Source><![CDATA[
    private void addFieldsToQueryObject(SysDaSelection _projectionList)
    {
        _projectionList
            .add(fieldStr(BudgetTransactionLine, BudgetType))
            .addSum(fieldStr(BudgetTransactionLine, AccountingCurrencyAmount));
    }

]]></Source>
			</Method>
			<Method>
				<Name>addLiteralsToQueryObject</Name>
				<Source><![CDATA[
    private void addLiteralsToQueryObject(SysDaSelection _projectionList)
    {
        _projectionList
            .addValue(this.firstDayInFiscalPeriod)
            .addValue(proposalConfig.Ledger)
            .addValue(proposalConfig.TimeSeriesId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildBudgetTransactionHeaderObject</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildBudgetTransactionHeaderObject()
    {
        budgetHeader.skipDataMethods(true);
        SysDaQueryObject sdqoBudgetHeader = new SysDaQueryObject(budgetHeader);

        SysDaQueryObject sdqoProposalSource = this.buildProposalSourceCriteria();
        sdqoBudgetHeader.whereClause(
            new SysDaAndExpression(
                new SysDaEqualsExpression(
                    new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, RecId)),
                    new SysDaFieldExpression(budgetLine, fieldStr(BudgetTransactionLine, BudgetTransactionHeader))),
                new SysDaEqualsExpression(
                    new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, PrimaryLedger)), 
                    new SysDaValueExpression(proposalConfig.Ledger))));

        sdqoBudgetHeader.joinClause(SysDaJoinKind::ExistsJoin, sdqoProposalSource);

        return sdqoBudgetHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildProposalSourceCriteria</Name>
				<Source><![CDATA[
    private SysDaQueryObject buildProposalSourceCriteria()
    {
        BudgetProposalSource sourceModel;
        sourceModel.skipDataMethods(true);

        SysDaQueryObject sdqo = new SysDaQueryObject(sourceModel);
        SysDaEqualsExpression eqls = new SysDaEqualsExpression(
            new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetProposalConfiguration)), 
            new SysDaValueExpression(proposalConfig.RecId));
        SysDaAndExpression andEqls = new SysDaAndExpression(
            eqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetModelDataAreaId)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetModelDataAreaId))));
        andEqls = new SysDaAndExpression(
            andEqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetModelId)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetModelId))));
        andEqls = new SysDaAndExpression(
            andEqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetModelType)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetModelType))));
        andEqls = new SysDaAndExpression(
            andEqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetSubModelId)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetSubModelId))));
        andEqls = new SysDaAndExpression(
            andEqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetTransactionCode)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetTransactionCode))));
        andEqls = new SysDaAndExpression(
            andEqls, 
            new SysDaEqualsExpression(
                new SysDaFieldExpression(sourceModel, fieldStr(BudgetProposalSource, BudgetModelDataAreaId)), 
                new SysDaFieldExpression(budgetHeader, fieldStr(BudgetTransactionHeader, BudgetModelDataAreaId))));
        sdqo.whereClause(andEqls);

        return sdqo;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildTimeSeriesSourceInsertObject</Name>
				<Source><![CDATA[
    private SysDaInsertObject buildTimeSeriesSourceInsertObject()
    {
        BudgetTimeSeriesDetail tsDetail;
        tsDetail.skipDataMethods(true);

        SysDaInsertObject insertObject = new SysDaInsertObject(tsDetail);

        insertObject.fields()
            .add(fieldStr(BudgetTimeSeriesDetail, BudgetType))
            .add(fieldStr(BudgetTimeSeriesDetail, TimeSeriesValue))
            .add(fieldStr(BudgetTimeSeriesDetail, TimeSeriesDate))
            .add(fieldStr(BudgetTimeSeriesDetail, Ledger))
            .add(fieldStr(BudgetTimeSeriesDetail, TimeSeriesId))
            .add(fieldStr(BudgetTimeSeriesDetail, TimeSeriesDimension));
        return insertObject;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateLedgerDimensionCache</Name>
				<Source><![CDATA[
    private void updateLedgerDimensionCache()
    {
        // To improve performance, the table BudgetProposalLedgerDimensionCache creates a LedgerDimension to LedgerDimensionBudget
        // mapping table to facilitate aggregation of budgets by LedgerDimension
        SysDaQueryObject sdqo = new SysDaQueryObject(budgetLine);
        sdqo.projection().add(fieldStr(BudgetTransactionLine, LedgerDimension));
        
        // Set the date range to the configuration range to process all LedgerDimension
        this.firstDayInFiscalPeriod = proposalConfig.StartDate;
        this.lastDayInFiscalPeriod = proposalConfig.EndDate;
        SysDaQueryObject queryObject = this.createSourceQueryFromBudgetRegister();
        
        // The group by insures we return each distinct LedgerDimension only once
        queryObject.groupByClause(new SysDaGroupBys().add(fieldStr(BudgetTransactionLine, LedgerDimension)));

        // Only create mappings that do not already exist in the table
        SysDaQueryObject qoDimensionMatch = new SysDaQueryObject(ledgerDimensionCache);
        qoDimensionMatch.projection().add(fieldStr(BudgetProposalLedgerDimensionCache, RecId));
        qoDimensionMatch.whereClause(new SysDaEqualsExpression(
            new SysDaFieldExpression(ledgerDimensionCache, fieldStr(BudgetProposalLedgerDimensionCache, LedgerDimension)),
            new SysDaFieldExpression(budgetLine, fieldStr(BudgetTransactionLine, LedgerDimension))));
        qoDimensionMatch.joinClauseKind(SysDaJoinKind::NotExistsJoin);

        // Add the join definitions to the primary table in the query
        sdqo.joinClause(SysDaJoinKind::InnerJoin, queryObject)
            .joinClause(SysDaJoinKind::NotExistsJoin, qoDimensionMatch);

        SysDaSearchObject searchObj = new SysDaSearchObject(sdqo);
        SysDaSearchStatement searchStatement = new SysDaSearchStatement();
        RecordInsertList insertList = new RecordInsertList(tableNum(BudgetProposalLedgerDimensionCache));

        while (searchStatement.next(searchObj))
        {
            ledgerDimensionCache.LedgerDimension = budgetLine.LedgerDimension;
            ledgerDimensionCache.BudgetLedgerDimension = BudgetTransactionManager::saveBudgetLedgerDimension(budgetLine.LedgerDimension);

            if (ledgerDimensionCache.BudgetLedgerDimension)
            {
                insertList.add(ledgerDimensionCache);
            }
        }

        insertList.insertDatabase();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>