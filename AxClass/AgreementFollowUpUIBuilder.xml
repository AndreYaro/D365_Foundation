<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>AgreementFollowUpUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>BOMConsistOfUIBuilder</c> class is the user interface builder class for the associated SQL
///    Server Reporting Services (SSRS) report.
/// </summary>
public class AgreementFollowUpUIBuilder extends SrsReportDataContractUIBuilder
{
    AgreementFollowUpRDPContract    contract;
    Map                             dimControlsMap;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>addInventDimFields</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates the dialog box fields for the inventory dimensions.
    /// </summary>
    protected void addInventDimFields()
    {
        DialogField     dialogField;
        DialogGroup     viewGroup;
        FieldId         fieldId;
        SysDictField    dictField;
        List            inventDimFieldList  = InventDim::dimAgreementFieldList();
        ListEnumerator  fieldListEnumerator = inventDimFieldList.getEnumerator();
        InventDimParm   inventDimParm;
        InventDimFixed  inventDimFixed;

        if (!contract)
        {
            contract = this.dataContractObject() as AgreementFollowUpRDPContract;
        }
        inventDimFixed = contract.parmInventoryDimensions();

        InventDimFixedClass::inventDimFixed2InventDimParm(inventDimFixed, inventDimParm);

        dimControlsMap = new Map(Types::Integer, Types::Class);

        viewGroup = dialog.addGroup("@SYS53394");

        while (fieldListEnumerator.moveNext())
        {
            fieldId = InventDim::dim2dimParm(fieldListEnumerator.current());
            dictField = new SysDictField(tablenum(InventDimParm), fieldId);
            dialogField = dialog.addFieldValue(extendedtypestr(NoYesId), inventDimParm.(fieldId),
                                               dictField.label(), dictField.help());
            dimControlsMap.insert(fieldId, dialogField);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>build</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes the dialog box fields.
    /// </summary>
    public void build()
    {
        super();

        this.addInventDimFields();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the dialog box field values.
    /// </summary>
    public void getFromDialog()
    {
        super();

        this.getFromInventDimFields();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromInventDimFields</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the values from the dialog box fields and puts the value into the contract object.
    /// </summary>
    protected void getFromInventDimFields()
    {
        MapEnumerator enumerator = dimControlsMap.getEnumerator();
        fieldId       fieldId;
        DialogField   dialogField;
        InventDimParm inventDimParm;

        while (enumerator.moveNext())
        {
            fieldId = enumerator.currentKey();
            dialogField = enumerator.currentValue();
            inventDimParm.(fieldId) = dialogField.value();
        }

        contract.parmInventoryDimensions(InventDimFixedClass::inventDimParm2InventDimFixed(inventDimParm));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>