<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>InventPurchShipmentAnalysisDP_CN</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PurchaseShipmentAnalysisReportDP</c> class is the Report Data Provider class for the<c>PurchaseShipmentReport</c> Overview Report.
/// </summary>
/// <remarks>
/// This is the RDP class for the Inventory <c>PurchaseShipmentReport</c> SRS Report.
/// </remarks>
[
    SRSReportParameterAttribute(classStr(InventPurchShipmentAnalysisContract_CN))
]
class InventPurchShipmentAnalysisDP_CN extends SrsReportDataProviderPreProcessTempDB
{
    //TMP table and contract class
    InventPurchShipmentAnalysisTmp_CN           purchShipmentAnalysisTmp_CN;
    InventPurchShipmentAnalysisContract_CN      contract;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>executeQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Executes the related query to get data
    /// </summary>
    private void executeQuery()
    {
        //Initial Contract class
        contract = this.parmDataContract() as InventPurchShipmentAnalysisContract_CN;

        //Get parameters from Contract
        ItemId itemId = contract.parmItemId();
        VendAccount vendAccount = contract.parmVendAccount();
        PackingSlipId packingSlipId = contract.parmPackingSlipId();
        TransDate fromDate = contract.parmFromDate();
        TransDate toDate = contract.parmToDate();
        TMSCarrierCode carrierCode = contract.parmCarrierCode();

        Query queryOrder = this.buildQueryForOrder();
        SysQuery::findOrCreateRange(queryOrder.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, ItemId)).value(itemId);
        SysQuery::findOrCreateRange(queryOrder.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, VendAccount)).value(vendAccount);
        SysQuery::findOrCreateRange(queryOrder.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, PackingSlipId)).value(packingSlipId);
        SysQuery::findOrCreateRange(queryOrder.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, DatePhysical)).value(queryRange(fromDate, toDate));
        SysQuery::findOrCreateRange(queryOrder.dataSourceTable(tableNum(TMSCarrier)), fieldNum(TMSCarrier, CarrierCode)).value(carrierCode);
        QueryRun queryRunOrder = new QueryRun(queryOrder);

        while (queryRunOrder.next())
        {
            PurchLine purchLineOrder = queryRunOrder.get(tableNum(PurchLine));
            DirPartyTable dirPartyTable = queryRunOrder.get(tableNum(DirPartyTable));
            TMSCarrier tMSCarrier = queryRunOrder.get(tableNum(TMSCarrier));

            //insert Purch line and carrier info to tmp table.
            this.insertIntoShipmentAnalysisTmp(purchLineOrder, dirPartyTable, tMSCarrier);
        }

        //Execute the Query and insert the InventTrans quantity and amount(Received, On-time, Delayed) to tmp table
        Query queryTrans = this.buildQueryForInventTrans();
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, ItemId)).value(itemId);
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, VendAccount)).value(vendAccount);
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, PackingSlipId)).value(packingSlipId);
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, DatePhysical)).value(queryRange(fromDate, toDate));
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, Qty)).value(' > 0');
        SysQuery::findOrCreateRange(queryTrans.dataSourceTable(tableNum(TMSCarrier)), fieldNum(TMSCarrier, CarrierCode)).value(carrierCode);
        QueryRun queryRunTrans = new QueryRun(queryTrans);

        while (queryRunTrans.next())
        {
            PurchLine purchLineTrans = queryRunTrans.get(tableNum(PurchLine));
            InventTrans inventTrans = queryRunTrans.get(tableNum(InventTrans));
            TMSCarrier tMSCarrier = queryRunTrans.get(tableNum(TMSCarrier));

            //insert InventTrans quantity and amount(Received, On-time, Dealyed) to tmp table.
            this.insertInventTrans(purchLineTrans, inventTrans, tMSCarrier);
        }

        //Execute the Query and insert the InventTrans quantity and amount(Returned) to tmp table
        Query queryReturn = this.buildQueryForInventTrans();
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, ItemId)).value(itemId);
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(PurchLine)), fieldNum(PurchLine, VendAccount)).value(vendAccount);
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, PackingSlipId)).value(packingSlipId);
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, DatePhysical)).value(queryRange(fromDate, toDate));
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(InventTrans)), fieldNum(InventTrans, Qty)).value(' < 0');
        SysQuery::findOrCreateRange(queryReturn.dataSourceTable(tableNum(TMSCarrier)), fieldNum(TMSCarrier, CarrierCode)).value(carrierCode);
        QueryRun queryRunReturn = new QueryRun(queryReturn);

        while (queryRunReturn.next())
        {
            PurchLine purchLineReturn = queryRunReturn.get(tableNum(PurchLine));
            InventTrans inventTransReturn = queryRunReturn.get(tableNum(InventTrans));
            TMSCarrier tMSCarrier = queryRunReturn.get(tableNum(TMSCarrier));

            //insert InventTrans quantity and amount(Returned) to tmp table.
            this.insertInventReturn(purchLineReturn, inventTransReturn, tMSCarrier);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getPurchShipmentAnalysisTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Retrieves data from the <c>PurchaseShipmentAnalysisReport</c> table.
    /// </summary>
    /// <returns>
    /// The data from the <c>PurchaseShipmentAnalysisReport</c> table.
    /// </returns>
    [
        SRSReportDataSetAttribute('InventPurchShipmentAnalysisDP_CN')
    ]
    public InventPurchShipmentAnalysisTmp_CN getPurchShipmentAnalysisTmp()
    {
        select purchShipmentAnalysisTmp_CN;
        return purchShipmentAnalysisTmp_CN;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertInventReturn</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts InventTrans quantity and amount(Returned) to tmp table.
    /// </summary>
    /// <param name = "_purchLine">
    /// SalesLine
    /// </param>
    /// <param name = "_inventTrans">
    /// InventTrans
    /// </param>
    /// <param name="_tMSCarrier">
    /// TMSCarrier
    /// </param>
    public void insertInventReturn(PurchLine _purchLine, InventTrans _inventTrans, TMSCarrier _tMSCarrier)
    {
        select firstOnly forUpdate purchShipmentAnalysisTmp_CN
            where purchShipmentAnalysisTmp_CN.ItemId == _purchLine.ItemId
                && purchShipmentAnalysisTmp_CN.VendAccount == _purchLine.VendAccount
                && purchShipmentAnalysisTmp_CN.CarrierCode == _tMSCarrier.CarrierCode;

        Debug::assert(purchShipmentAnalysisTmp_CN.RecId);
        purchShipmentAnalysisTmp_CN.ReturnedQty     += - this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _inventTrans.Qty);
        purchShipmentAnalysisTmp_CN.ReturnedAmount  += - _inventTrans.CostAmountPhysical;
        purchShipmentAnalysisTmp_CN.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>convertQuantityToInventoryUnit</Name>
				<Source><![CDATA[
    /// <summary>
    /// Convert the specified item quantity to the inventory unit
    /// </summary>
    /// <param name = "_itemId">Item number</param>
    /// <param name = "_inventDimId">Dimension ID for conversions.</param>
    /// <param name = "_unitSymbolFrom">Unit to convert from</param>
    /// <param name = "_qtyToConvert">Quantity to convert</param>
    /// <returns>Quantity in inventory unit</returns>
    protected Qty convertQuantityToInventoryUnit(
        ItemId              _itemId, 
        InventDimId         _inventDimId,
        UnitOfMeasureSymbol _unitSymbolFrom, 
        Qty                 _qtyToConvert)
    {
        return InventTableModule::unitConvert(_itemId, ModuleInventPurchSales::Invent, _unitSymbolFrom, _qtyToConvert, _inventDimId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertInventTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts InventTrans quantity and amount(Received, On-time, Dealyed) to tmp table.
    /// </summary>
    /// <param name = "_purchLine">
    /// SalesLine
    /// </param>
    /// <param name = "_inventTrans">
    /// InventTrans
    /// </param>
    /// <param name="_tMSCarrier">
    /// TMSCarrier
    /// </param>
    public void insertInventTrans(PurchLine _purchLine, InventTrans _inventTrans, TMSCarrier _tMSCarrier)
    {
        select firstOnly forUpdate purchShipmentAnalysisTmp_CN
            where purchShipmentAnalysisTmp_CN.ItemId == _purchLine.ItemId
                && purchShipmentAnalysisTmp_CN.VendAccount == _purchLine.VendAccount
                && purchShipmentAnalysisTmp_CN.CarrierCode == _tMSCarrier.CarrierCode;

        Debug::assert(purchShipmentAnalysisTmp_CN.RecId);
        purchShipmentAnalysisTmp_CN.ReceivedQty     += this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _inventTrans.Qty);
        purchShipmentAnalysisTmp_CN.ReceivedAmount  += _inventTrans.CostAmountPhysical;
        if (_inventTrans.DatePhysical <= _inventTrans.DateExpected)
        {
            //If DateExpected is later than DatePhysical, set the On-time quantity and amount.
            purchShipmentAnalysisTmp_CN.OnTimeReceivedQty     += this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _inventTrans.Qty);
            purchShipmentAnalysisTmp_CN.OnTimeReceivedAmount  += _inventTrans.CostAmountPhysical;
        }
        else
        {
            //If DateExpected is earlier than DatePhysical, set the Delayed quantity and amount.
            purchShipmentAnalysisTmp_CN.DelayedQty     += this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _inventTrans.Qty);
            purchShipmentAnalysisTmp_CN.DelayedAmount  += _inventTrans.CostAmountPhysical;
        }
        purchShipmentAnalysisTmp_CN.update();
    }

]]></Source>
			</Method>
			<Method>
				<Name>insertIntoShipmentAnalysisTmp</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts the purchase line and carrier info to the tmp table.
    /// </summary>
    /// <param name="_purchLine">
    /// SalesLine
    /// </param>
    /// <param name="_dirPartyTable">
    /// DirPartyTable
    /// </param>
    /// <param name="_tMSCarrier">
    /// TMSCarrier
    /// </param>
    public void insertIntoShipmentAnalysisTmp(PurchLine _purchLine, DirPartyTable _dirPartyTable, TMSCarrier _tMSCarrier)
    {
        select firstOnly forUpdate purchShipmentAnalysisTmp_CN
            where purchShipmentAnalysisTmp_CN.ItemId == _purchLine.ItemId
                && purchShipmentAnalysisTmp_CN.VendAccount == _purchLine.VendAccount
                && purchShipmentAnalysisTmp_CN.CarrierCode == _tMSCarrier.CarrierCode;

        if (purchShipmentAnalysisTmp_CN.RecId == 0)
        {
            InventTable item = InventTable::find(_purchLine.ItemId);
            purchShipmentAnalysisTmp_CN.VendAccount     = _purchLine.VendAccount;
            purchShipmentAnalysisTmp_CN.VendName        = _dirPartyTable.Name;
            purchShipmentAnalysisTmp_CN.ItemId          = _purchLine.ItemId;
            purchShipmentAnalysisTmp_CN.Name            = item.itemName();
            purchShipmentAnalysisTmp_CN.PurchUnit       = item.inventUnitId();;
            purchShipmentAnalysisTmp_CN.OrderedQty      = this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _purchLine.PurchQty);
            purchShipmentAnalysisTmp_CN.OrderedAmount   = _purchLine.LineAmount;
            purchShipmentAnalysisTmp_CN.CarrierCode     = _tMSCarrier.CarrierCode;
            purchShipmentAnalysisTmp_CN.CarrierName     = _tMSCarrier.Name;
            purchShipmentAnalysisTmp_CN.insert();
        }
        else
        {
            purchShipmentAnalysisTmp_CN.OrderedQty     += this.convertQuantityToInventoryUnit(_purchLine.ItemId, _purchLine.InventDimId, _purchLine.PurchUnit, _purchLine.PurchQty);
            purchShipmentAnalysisTmp_CN.OrderedAmount  += _purchLine.LineAmount;
            purchShipmentAnalysisTmp_CN.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildQueryForOrder</Name>
				<Source><![CDATA[
    /// <summary>
    /// Build query from <C>InventPurchaseShipmentAnalysisOrder_CN</C>
    /// When shipping carrier has value, filter data to show specified shipping carrier code.
    /// When shipping carrier is empty, show all the data with or without shipping carrier info.
    /// </summary>
    /// <returns>The built query</returns>
    private Query buildQueryForOrder()
    {
        Query queryLocal = new Query(queryStr(InventPurchaseShipmentAnalysisOrder_CN));

        if (contract.parmCarrierCode())
        {
            queryLocal.dataSourceTable(tableNum(TMSPurchTable)).joinMode(JoinMode::InnerJoin);
            queryLocal.dataSourceTable(tableNum(TMSCarrier)).joinMode(JoinMode::InnerJoin);
        }

        return queryLocal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>buildQueryForInventTrans</Name>
				<Source><![CDATA[
    /// <summary>
    /// Build query from <C>InventPurchaseShipmentAnalysisTrans_CN</C>
    /// Narrow down the data when shipping carrier has value.
    /// </summary>
    /// <returns>The built query</returns>
    private Query buildQueryForInventTrans()
    {
        Query queryLocal = new Query(queryStr(InventPurchaseShipmentAnalysisTrans_CN));

        if (contract.parmCarrierCode())
        {
            queryLocal.dataSourceTable(tableNum(TMSPurchTable)).joinMode(JoinMode::InnerJoin);
            queryLocal.dataSourceTable(tableNum(TMSCarrier)).joinMode(JoinMode::InnerJoin);
        }

        return queryLocal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processReport</Name>
				<Source><![CDATA[
    /// <summary>
    /// Processes the report business logic.
    /// </summary>
    /// <remarks>
    /// Provides the ability to write the report business logic. This method will be called by SSRS at
    /// runtime. The method should compute data and populate the data tables that will be returned to SSRS.
    /// </remarks>
    public void processReport()
    {
        GlobalizationInstrumentationHelper::featureRun(GlobalizationConstants::FeatureReferenceCN00132, funcName());

        this.executeQuery();

        select firstOnly forUpdate RecId from purchShipmentAnalysisTmp_CN;
        if (purchShipmentAnalysisTmp_CN.RecId == 0)
        {
            purchShipmentAnalysisTmp_CN.clear();
            purchShipmentAnalysisTmp_CN.insert();
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>