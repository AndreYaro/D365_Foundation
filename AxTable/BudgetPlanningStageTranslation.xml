<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanningStageTranslation</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class BudgetPlanningStageTranslation extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>delete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes a <c>BudgetPlanningStageTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to delete the record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void delete(boolean _allowSystemLanguageChange = false)
    {
        if (_allowSystemLanguageChange ||
            this.Name != BudgetPlanningStage::find(this.BudgetPlanningStage).Name)
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    /// Inserts a <c>BudgetPlanningStageTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to insert the record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void insert(boolean _allowSystemLanguageChange = false)
    {
        if (this.validateWrite(_allowSystemLanguageChange))
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets a default <c>LanguageId</c> value.
    /// </summary>
    public void setDefaultLanguage()
    {
        this.LanguageId = SystemParameters::getSystemLanguageId();
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates a <c>BudgetPlanningStageTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to update the record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void update(boolean _allowSystemLanguageChange = false)
    {
        if (this.validateWrite(_allowSystemLanguageChange))
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
        public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean isValid = super(_fieldIdToCheck);

        if (isValid)
        {
            if (this.LanguageId == SystemParameters::getSystemLanguageId())
            {
                isValid = checkFailed("@GLS150186");
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the name of a <c>BudgetPlanningStage</c> record is not being used.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to write the record when the language ID is equal to the
    /// system language ID.
    /// </param>
    /// <returns>
    /// true if validation passes; otherwise, false.
    /// </returns>
    public boolean validateWrite(boolean _allowSystemLanguageChange = false)
    {
        BudgetPlanningStage budgetPlanningStage;
        boolean             isValid;

        isValid = super();

        if (isValid)
        {
            if (!_allowSystemLanguageChange &&
                this.LanguageId == SystemParameters::getSystemLanguageId())
            {
                isValid = checkFailed("@GLS150186");
            }
            else
            {
                // Ensure the budget planning stage name isn't already in use
                select firstonly RecId from budgetPlanningStage
                    where budgetPlanningStage.Name  == this.Name &&
                        budgetPlanningStage.RecId != this.BudgetPlanningStage;

                if (budgetPlanningStage)
                {
                    return checkFailed("@GLS150059");
                }
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkExist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if a record ID exists in <c>BudgetPlanningStageTranslation</c> table and shows an infolog message if record does not exist.
    /// </summary>
    /// <param name="_budgetPlanningStageTranslationRecId">
    /// The value of the <c>BudgetPlanningStageTranslation</c> record ID to check.
    /// </param>
    /// <returns>
    /// true if the record ID exists; otherwise, false.
    /// </returns>
    public static boolean checkExist(BudgetPlanningStageTranslationRecId _budgetPlanningStageTranslationRecId)
    {
        boolean isExisting = true;

        if (!BudgetPlanningStageTranslation::exist(_budgetPlanningStageTranslationRecId))
        {
            isExisting = checkFailed(strfmt(BudgetPlanningStageTranslation::txtNotExist(), tableId2Name(tablenum(BudgetPlanningStageTranslation))));
        }

        return isExisting;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateTranslation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates a budget planning stage translation record.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The record ID of the <c>BudgetPlanningStage</c> record.
    /// </param>
    /// <param name="_name">
    /// The specified budget planning stage name value.
    /// </param>
    /// <param name="_description">
    /// The specified budget planning stage description value.
    /// </param>
    /// <param name="_languageId">
    /// The specified <c>LanguageId</c> value.
    /// </param>
    public static void createOrUpdateTranslation(
        BudgetPlanningStageRecId       _budgetPlanningStageRecId,
        Name                           _name,
        BudgetPlanningStageDescription _description = '',
        LanguageId                     _languageId = '')
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;
        LanguageId                     languageId = _languageId;

        if (!_languageId)
        {
            languageId = SystemParameters::systemLanguageId();
        }

        // If system language found then create or update BudgetPlanningStageTranslation
        if (languageId)
        {
            ttsbegin;

            budgetPlanningStageTranslation = BudgetPlanningStageTranslation::
                findByBudgetPlanningStageLanguage(_budgetPlanningStageRecId, languageId, true);

            if (budgetPlanningStageTranslation.RecId)
            {
                budgetPlanningStageTranslation.Name        = _name;
                budgetPlanningStageTranslation.Description = _description;
                budgetPlanningStageTranslation.update(true);
            }
            else
            {
                budgetPlanningStageTranslation.BudgetPlanningStage = _budgetPlanningStageRecId;
                budgetPlanningStageTranslation.Name                = _name;
                budgetPlanningStageTranslation.Description         = _description;
                budgetPlanningStageTranslation.LanguageId          = languageId;
                budgetPlanningStageTranslation.insert(true);
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteForBudgetPlanningStage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes all the <c>BudgetPlanningStageTranslation</c> records that are associated with a
    /// <c>BudgetPlanningStage</c> record.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The record ID of the <c>BudgetPlanningStage</c> record.
    /// </param>
    public static void deleteForBudgetPlanningStage(BudgetPlanningStageRecId _budgetPlanningStageRecId)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;

        budgetPlanningStageTranslation.skipDeleteMethod(true);
        budgetPlanningStageTranslation.skipDeleteActions(true);

        delete_from budgetPlanningStageTranslation
            where budgetPlanningStageTranslation.BudgetPlanningStage == _budgetPlanningStageRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>exist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether a record ID exists in the <c>BudgetPlanningStageTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanningStageTranslationRecId">
    /// The value of the <c>BudgetPlanningStageTranslation</c> record ID to check.
    /// </param>
    /// <returns>
    /// true if the record ID exists; otherwise, false.
    /// </returns>
    public static boolean exist(BudgetPlanningStageTranslationRecId _budgetPlanningStageTranslationRecId)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;

        return _budgetPlanningStageTranslationRecId && (select firstonly RecId from budgetPlanningStageTranslation
                    where budgetPlanningStageTranslation.RecId == _budgetPlanningStageTranslationRecId).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanningStageTranslation</c> table that is based on a specified
    /// record ID.
    /// </summary>
    /// <param name="_budgetPlanningStageTranslationRecId">
    /// The value of the record ID to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <param name="_concurrencyModel">
    /// An enumeration value that indicates the concurrency settings to use on the table; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanningStageTranslation</c> record.
    /// </returns>
    public static BudgetPlanningStageTranslation find(
        BudgetPlanningStageTranslationRecId _budgetPlanningStageTranslationRecId,
        boolean                             _forUpdate = false,
        ConcurrencyModel                    _concurrencyModel = ConcurrencyModel::Auto)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;

        if (_budgetPlanningStageTranslationRecId)
        {
            budgetPlanningStageTranslation.selectForUpdate(_forUpdate);

            if (_forUpdate && _concurrencyModel != ConcurrencyModel::Auto)
            {
                budgetPlanningStageTranslation.concurrencyModel(_concurrencyModel);
            }

            select firstonly budgetPlanningStageTranslation
                where budgetPlanningStageTranslation.RecId == _budgetPlanningStageTranslationRecId;
        }

        return budgetPlanningStageTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByBudgetPlanningStageLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanningStageTranslation</c> table that is based on the specified
    /// <c>BudgetPlanningStage</c> and <c>LanguageId</c> values.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The value of the <c>BudgetPlanningStage</c> field to find.
    /// </param>
    /// <param name="_languageId">
    /// The value of the <c>LanguageId</c> field to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <param name="_concurrencyModel">
    /// An enumeration value that indicates the concurrency settings to use on the table; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanningStageTranslation</c> record.
    /// </returns>
    public static BudgetPlanningStageTranslation findByBudgetPlanningStageLanguage(
        BudgetPlanningStageRecId _budgetPlanningStageRecId,
        LanguageIdAll            _languageId,
        boolean                  _forUpdate = false,
        ConcurrencyModel         _concurrencyModel = ConcurrencyModel::Auto)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;

        if (_budgetPlanningStageRecId && _languageId)
        {
            budgetPlanningStageTranslation.selectForUpdate(_forUpdate );

            if (_forUpdate && _concurrencyModel != ConcurrencyModel::Auto)
            {
                budgetPlanningStageTranslation.concurrencyModel(_concurrencyModel);
            }

            select firstonly budgetPlanningStageTranslation
                where BudgetPlanningStageTranslation.BudgetPlanningStage == _budgetPlanningStageRecId &&
                        BudgetPlanningStageTranslation.LanguageId        == _languageId;
        }

        return budgetPlanningStageTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findStageTranslationByLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanningStageTranslation</c> table that is based on the specified
    /// <c>BudgetPlanningStage</c> and <c>LanguageId</c> values.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The ID of a budget planning stage that the name is retrieved.
    /// </param>
    /// <param name="_languageId">
    /// The ID of a language that the name is retrieved.
    /// </param>
    /// <param name="_translationFieldId">
    /// The field ID of the localized field that is retrieved.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanningStageTranslation</c> record.
    /// </returns>
    public static BudgetPlanningStageTranslation findStageTranslationByLanguage(
        BudgetPlanningStageRecId _budgetPlanningStageRecId,
        LanguageId               _languageId,
        fieldId                  _translationFieldId)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;
        LanguageId                     systemLanguageId;

        if (_budgetPlanningStageRecId)
        {
            budgetPlanningStageTranslation = BudgetPlanningStageTranslation::findByBudgetPlanningStageLanguage(
                _budgetPlanningStageRecId,
                _languageId);

            if (!budgetPlanningStageTranslation.RecId || !budgetPlanningStageTranslation.(_translationFieldId))
            {
                // no translation record or no value provided - fallback to system language
                systemLanguageId = SystemParameters::getSystemLanguageId();

                if (systemLanguageId != _languageId)
                {
                    budgetPlanningStageTranslation = BudgetPlanningStageTranslation::findByBudgetPlanningStageLanguage(
                        _budgetPlanningStageRecId,
                        systemLanguageId);
                }
            }
        }

        return budgetPlanningStageTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLocalizedName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the localized <c>Name</c> field of the <c>BudgetPlanningStageTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The ID of a budget planning stage that the name is retrieved.
    /// </param>
    /// <returns>
    /// The localized name of a budget planning stage.
    /// </returns>
    /// <remarks>
    /// If a name in the user current language is not found, a name in the system language is returned.
    /// </remarks>
    public static BudgetPlanningStageName getLocalizedName(BudgetPlanningStageRecId _budgetPlanningStageRecId)
    {
        BudgetPlanningStageName budgetPlanningStageName;

        budgetPlanningStageName = BudgetPlanningStageTranslation::findByBudgetPlanningStageLanguage(
                                                _budgetPlanningStageRecId,
                                                currentUserLanguage()
                                                ).Name;
        if (!budgetPlanningStageName)
        {
            budgetPlanningStageName = BudgetPlanningStageTranslation::findByBudgetPlanningStageLanguage(
                                                    _budgetPlanningStageRecId,
                                                    SystemParameters::getSystemLanguageId()
                                                    ).Name;
        }

        // Fall back to Name if no localized name found
        if (!budgetPlanningStageName)
        {
            budgetPlanningStageName = BudgetPlanningStage::find(_budgetPlanningStageRecId).Name;
        }

        return budgetPlanningStageName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Name</c> field of the <c>BudgetPlanningStageTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanningStageRecId">
    /// The ID of a budget planning stage that the name is retrieved.
    /// </param>
    /// <param name="_languageId">
    /// The ID of a language that the name is retrieved.
    /// </param>
    /// <returns>
    /// The name of a budget planning stage.
    /// </returns>
    /// <remarks>
    /// If a name in the language that is specified by the <c>_languageId</c> parameter is not found, a
    /// name in the system language is returned.
    /// </remarks>
    public static Name getName(
        BudgetPlanningStageRecId _budgetPlanningStageRecId,
        LanguageId               _languageId)
    {
        BudgetPlanningStageTranslation budgetPlanningStageTranslation;

        budgetPlanningStageTranslation = BudgetPlanningStageTranslation::findStageTranslationByLanguage(
            _budgetPlanningStageRecId,
            _languageId,
            fieldNum(BudgetPlanningStageTranslation, Name)
            );

        return budgetPlanningStageTranslation.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryAddDefaultLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a default language ID filter for a specified query.
    /// </summary>
    /// <param name="_query">
    /// The query that is extended with the default language ID filter.
    /// </param>
    /// <param name="_dataSourceName">
    /// The name of the specified data source for which the range is added.
    /// </param>
    /// <exception cref="M:Exception::Error">
    /// The query does not contain the specified data source or the <c>BudgetPlanningStage</c> data source.
    /// </exception>
    public static void queryAddDefaultLanguage(Query _query, str _dataSourceName = '')
    {
        QueryBuildDataSource qbdsBudgetPlanningStageTranslation;

        if (_dataSourceName)
        {
            qbdsBudgetPlanningStageTranslation = _query.dataSourceName(_dataSourceName);
        }
        else
        {
            qbdsBudgetPlanningStageTranslation = _query.dataSourceTable(tableNum(BudgetPlanningStageTranslation));
        }

        if (!qbdsBudgetPlanningStageTranslation)
        {
            throw error(Error::wrongUseOfFunction(tableStaticMethodStr(BudgetPlanningStageTranslation, queryAddDefaultLanguage)));
        }

        qbdsBudgetPlanningStageTranslation.addRange(fieldNum(BudgetPlanningStageTranslation, LanguageId))
            .value(queryValue(SystemParameters::getSystemLanguageId()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryAddUserLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the user language ID filter for a specified query.
    /// </summary>
    /// <param name="_query">
    /// The query that is extended with the company language ID filter.
    /// </param>
    /// <param name="_dataSourceName">
    /// The name of the specified data source for which the range is added.
    /// </param>
    /// <exception cref="M:Exception::Error">
    /// The query does not contain the specified data source or the <c>BudgetPlanningStage</c> data source.
    /// </exception>
    public static void queryAddUserLanguage(Query _query, str _dataSourceName = '')
    {
        QueryBuildDataSource qbdsBudgetPlanningStageTranslation;

        if (_dataSourceName)
        {
            qbdsBudgetPlanningStageTranslation = _query.dataSourceName(_dataSourceName);
        }
        else
        {
            qbdsBudgetPlanningStageTranslation = _query.dataSourceTable(tableNum(BudgetPlanningStageTranslation));
        }

        if (!qbdsBudgetPlanningStageTranslation)
        {
            throw error(Error::wrongUseOfFunction(tableStaticMethodStr(BudgetPlanningStageTranslation, queryAddDefaultLanguage)));
        }

        qbdsBudgetPlanningStageTranslation.addRange(fieldNum(BudgetPlanningStageTranslation, LanguageId))
            .value(queryValue(currentUserLanguage()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtNotExist</Name>
				<Source><![CDATA[
    public static TxtNotExist txtNotExist()
    {
        return "@SYS321009";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LedgerAdv2BudgetPlanning</ConfigurationKey>
	<DeveloperDocumentation>@GLS150118</DeveloperDocumentation>
	<Label>@GLS150119</Label>
	<SingularLabel>@GLS150130</SingularLabel>
	<TableGroup>Group</TableGroup>
	<TitleField1>BudgetPlanningStage</TitleField1>
	<TitleField2>LanguageId</TitleField2>
	<CacheLookup>FoundAndEmpty</CacheLookup>
	<ClusteredIndex>BudgetPlanningStageTranslationIdx</ClusteredIndex>
	<Modules>Budget</Modules>
	<ReplacementKey>BudgetPlanningStageTranslationIdx</ReplacementKey>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanningStage</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanningStage</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanningStage</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>General</Name>
			<Label>@SYS312922</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Identification</Name>
			<Label>@SYS5711</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanningStage</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt64">
			<Name>BudgetPlanningStage</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanningStageRecId</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Description</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanningStageDescription</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>LanguageId</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>LanguageIdAll</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Name</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanningStageName</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>BudgetPlanningStageTranslationIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>BudgetPlanningStage</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>LanguageId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
		<AxTableIndex>
			<Name>NameLanguageIdIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>Name</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>LanguageId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>BudgetPlanningStage</Name>
			<Cardinality>OneMore</Cardinality>
			<OnDelete>Cascade</OnDelete>
			<RelatedTable>BudgetPlanningStage</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Aggregation</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>BudgetPlanningStage</Name>
					<Field>BudgetPlanningStage</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>LanguageTable</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>LanguageTable</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Association</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>LanguageId</Name>
					<SourceEDT>LanguageIdAll</SourceEDT>
					<Field>LanguageId</Field>
					<RelatedField>LanguageId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>Key</Index>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>