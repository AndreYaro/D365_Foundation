<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SalesBookVATProcessParameters_RU</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class SalesBookVATProcessParameters_RU extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkDefault</Name>
				<Source><![CDATA[
    public boolean checkDefault(boolean _checkDefaultExists = false)
    {
        boolean                             ret = true;
        SalesBookVATProcessParameters_RU    salesBookVATProcessParameters;

        select firstonly salesBookVATProcessParameters
            where salesBookVATProcessParameters.ProcessingType == this.ProcessingType &&
                  salesBookVATProcessParameters.RestoringType  == this.RestoringType  &&
                  salesBookVATProcessParameters.RecId          != this.RecId          &&
                  salesBookVATProcessParameters.IsDefault;

        if (! this.IsDefault && ! salesBookVATProcessParameters)
        {
            ret = checkFailed("@GLS112939");
        }

        if (_checkDefaultExists
         && this.IsDefault
         && salesBookVATProcessParameters)
        {
            if (Box::yesNo("@GLS221420", DialogButton::No) == DialogButton::No)
            {
                ret = false;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Inserts the record in the <c>SalesBookVATProcessParameters_RU</c> table.
    /// </summary>
    public void insert()
    {
        ttsbegin;

        this.setDefault();

        super();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefault</Name>
				<Source><![CDATA[
    private void setDefault()
    {
        SalesBookVATProcessParameters_RU    salesBookVATProcessParametersDef;

        if (this.IsDefault
         &&(!this.RecId
         || !this.orig().IsDefault))
        {
            update_recordset salesBookVATProcessParametersDef
                setting IsDefault = NoYes::No
                where salesBookVATProcessParametersDef.ProcessingType == this.ProcessingType &&
                      salesBookVATProcessParametersDef.RestoringType  == this.RestoringType  &&
                      salesBookVATProcessParametersDef.IsDefault      == NoYes::Yes;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Updates the record in the <c>SalesBookVATProcessParameters_RU</c> table.
    /// </summary>
    public void update()
    {
        ttsbegin;

        this.setDefault();

        super();

        ttscommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDelete</Name>
				<Source><![CDATA[
    public boolean validateDelete()
    {
        boolean                          ret;
        SalesBookVATProcessParameters_RU vatProcessParameters;

        ret = super();

        if (ret)
        {
            select count(RecId) from vatProcessParameters
                where vatProcessParameters.ProcessingType == this.ProcessingType &&
                      vatProcessParameters.RestoringType  == this.RestoringType;

            if (this.IsDefault && vatProcessParameters.RecId > 1)
            {
                ret = checkFailed("@GLS113067");
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean ret;

        ret = super(_fieldIdToCheck);

        switch (_fieldIdToCheck)
        {
            case fieldNum(SalesBookVATProcessParameters_RU, IsDefault) :
                ret = this.checkDefault(true);
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    public boolean validateWrite()
    {
        boolean ret;

        ret = super();

        if (ret)
        {
            ret = this.checkDefault();
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcLedgerDimensions</Name>
				<Source><![CDATA[
    /// <summary>
    /// Returns ledger dimension for transaction type and given parameters
    /// </summary>
    /// <param name="_taxCode">
    /// Tax code parameter
    /// </param>
    /// <param name="_operationCode">
    /// <c>SalesBookVATProcessParameters_RU</c> RecId
    /// </param>
    /// <param name="_transType">
    /// Sales or purchase book transaction type
    /// </param>
    /// <param name="_transRecId">
    /// CustTrans record id
    /// </param>
    /// <param name="_offsetRecId">
    /// Offset record id
    /// </param>
    /// <param name="_transCompany">
    /// Transaction company
    /// </param>
    /// <param name="_offsetCompany">
    /// Offset transaction company
    /// </param>
    /// <returns>
    /// Ledger dimension value
    /// </returns>
    public static container calcLedgerDimensions(TaxCode                    _taxCode,
                                                        RefRecId                   _operationCode,
                                                        SalesPurchBookTransType_RU _transType,
                                                        RecId                      _transRecId,
                                                        RecId                      _offsetRecId,
                                                        CompanyId                  _transCompany,
                                                        CompanyId                  _offsetCompany)
    {
        LedgerDimensionDefaultAccount       defaultAccount, offsetDefaultAccount;
        CustVendTransPostingLog_RU          postingLog;
        TaxLedgerAccountGroup               taxLedgerAccountGroup = TaxLedgerAccountGroup::find(TaxTable::findAccountGroup(_taxCode));
        RefTableId                          refTableId            = tableNum(CustTrans);

        offsetDefaultAccount = taxLedgerAccountGroup.TaxOutgoingLedgerDimension;

        switch (_transType)
        {
            case SalesPurchBookTransType_RU::PrepaymentStorno:
                defaultAccount = taxLedgerAccountGroup.TaxIncomingLedgerDimension;
                break;

            case SalesPurchBookTransType_RU::VATRestoring :
                defaultAccount = TaxParameters::find().IncomingVATPayment_RU                ?
                                 taxLedgerAccountGroup.TaxIncomingPaymentLedgerDimension_RU :
                                 taxLedgerAccountGroup.TaxIncomingLedgerDimension;
                break;

            default :
                if (_offsetRecId)
                {
                    select firstonly LedgerDimension from postingLog
                        where postingLog.TaxCode        == _taxCode                                                      &&
                              ((postingLog.TransRecId   == _transRecId    && postingLog.OffSetRecId   == _offsetRecId    &&
                                postingLog.TransCompany == _transCompany  && postingLog.OffsetCompany == _offsetCompany) ||
                               (postingLog.TransRecId   == _offsetRecId   && postingLog.OffSetRecId   == _transRecId     &&
                                postingLog.TransCompany == _offsetCompany && postingLog.OffsetCompany == _transCompany)) &&
                              postingLog.PostingLogType == CustVendTransPostingLogType_RU::VATTransit                    &&
                              postingLog.RefTableId     == refTableId;

                    offsetDefaultAccount = postingLog.LedgerDimension ? LedgerDefaultAccountHelper::getDefaultAccountFromLedgerDimension(postingLog.LedgerDimension) : offsetDefaultAccount;
                }
        }

        return [defaultAccount, offsetDefaultAccount];
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcProcessLedgerAccounts</Name>
				<Source><![CDATA[
    public static container calcProcessLedgerAccounts(TaxCode                      _taxCode,
                                                             DimensionDefault             _defaultDimension,
                                                             InventTransId                _inventTransId,
                                                             FactureId_RU                 _factureId,
                                                             RecId                        _transRecId,
                                                             SalesBookProcessingType_RU   _processingType,
                                                             SalesBookVATRestoringType_RU _restoringType,
                                                             TransDate                    _fromDate = dateNull())
    {
        SalesBookVATProcessLogTable_RU      processLogTable;
        SalesBookVATProcessLogTrans_RU      processLogTrans;
        SalesBookVATProcessLogTransOper_RU  processLogTransOper;
        LedgerDimensionDefaultAccount       defaultAccount, offsetDefaultAccount;
        SalesBookVATProcessParameters_RU    vatProcessParameters = SalesBookVATProcessParameters_RU::defaultParms(_processingType, _restoringType);
        SalesBookVATLedgerPosting_RU        ledgerPosting        = SalesBookVATLedgerPosting_RU::ledgerPosting(_taxCode, vatProcessParameters.RecId);

        select firstonly LedgerDimension, OffsetLedgerDimension from processLogTransOper
            where processLogTransOper.DefaultDimension == _defaultDimension &&
                  processLogTransOper.TaxCode          == _taxCode          &&
                  processLogTransOper.InventTransId    == _inventTransId    &&
                  processLogTransOper.RestoringType    == _restoringType    &&
                  processLogTransOper.Blocked          == NoYes::No         &&
                  processLogTransOper.BlockedAuto      == NoYes::No         &&
                  processLogTransOper.CanceledRefRecId == 0                 &&
                  processLogTransOper.TransDate        >= _fromDate
        exists join processLogTrans
            where processLogTrans.RecId           == processLogTransOper.RefRecId &&
                  processLogTrans.FactureId       == _factureId                   &&
                  processLogTrans.InvoiceRecIdRef == _transRecId                  &&
                  processLogTrans.ProcessingType  == _processingType
        exists join processLogTable
                where processLogTable.RecId     == processLogTransOper.LogTableRefRecId &&
                      processLogTable.TransDate >= _fromDate;

        defaultAccount       = processLogTransOper.LedgerDimension       ? processLogTransOper.LedgerDimension       : ledgerPosting.LedgerDimension;
        offsetDefaultAccount = processLogTransOper.OffsetLedgerDimension ? processLogTransOper.OffsetLedgerDimension : ledgerPosting.OffsetLedgerDimension;

        if (! offsetDefaultAccount)
        {
            offsetDefaultAccount = TaxLedgerAccountGroup::find(TaxTable::findAccountGroup(_taxCode)).TaxOutgoingLedgerDimension;
        }

        return [defaultAccount, offsetDefaultAccount];
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultParms</Name>
				<Source><![CDATA[
    public static SalesBookVATProcessParameters_RU defaultParms(SalesBookProcessingType_RU   _processingType,
                                                                SalesBookVATRestoringType_RU _restoringType = SalesBookVATRestoringType_RU::None)
    {
        SalesBookVATProcessParameters_RU salesBookVATProcessParameters;

        select firstonly salesBookVATProcessParameters
            where salesBookVATProcessParameters.ProcessingType == _processingType &&
                  salesBookVATProcessParameters.RestoringType  == _restoringType  &&
                  salesBookVATProcessParameters.IsDefault      == NoYes::Yes;

        if (! salesBookVATProcessParameters)
        {
            throw error(strFmt("@GLS112942", _processingType));
        }

        return salesBookVATProcessParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>existsIncludeInBookRecord</Name>
				<Source><![CDATA[
    static public boolean existsIncludeInBookRecord(SalesBookProcessingType_RU   _processingType,
                                                           SalesBookVATRestoringType_RU _restoringType = SalesBookVATRestoringType_RU::None)
    {
        SalesBookVATProcessParameters_RU salesBookVATProcessParameters;

        select firstonly RecId from salesBookVATProcessParameters
            where salesBookVATProcessParameters.ProcessingType == _processingType &&
                  salesBookVATProcessParameters.RestoringType  == _restoringType  &&
                  salesBookVATProcessParameters.IncludeInBook  == NoYes::Yes;

        return salesBookVATProcessParameters.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds the specified record in the <c>SalesBookVATProcessParameters_RU</c> table.
    /// </summary>
    /// <param name="_recId">
    /// The RecId of the SalesBookVATProcessParameters_RU record to find.
    /// </param>
    /// <param name="_forupdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <param name="_concurrencyModel">
    /// The concurrency model option to use when reading the record; optional.
    /// </param>
    /// <returns>
    /// A record in the <c>SalesBookVATProcessParameters_RU</c> table; otherwise, an empty record.
    /// </returns>
    public static SalesBookVATProcessParameters_RU find(
        RecId              _recId,
        boolean            _forupdate = false,
        ConcurrencyModel   _concurrencyModel = ConcurrencyModel::Auto)
    {
        SalesBookVATProcessParameters_RU salesBookVATProcessParameters;

        salesBookVATProcessParameters.selectForUpdate(_forupdate);
        if (_forupdate && _concurrencyModel != ConcurrencyModel::Auto)
        {
            salesBookVATProcessParameters.concurrencyModel(_concurrencyModel);
        }

        select firstonly salesBookVATProcessParameters
            where salesBookVATProcessParameters.RecId == _recId;

        return salesBookVATProcessParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByCode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Searches for <c>SalesBookVATProcessParameters_RU</c> record by operation code
    /// </summary>
    /// <param name="_operationCode">
    /// Operation code to look for
    /// </param>
    /// <param name="_forUpdate">
    /// Determines if record should be selected for update
    /// </param>
    /// <returns>
    /// Found <c>SalesBookVATProcessParameters_RU</c> record
    /// </returns>
    static public SalesBookVATProcessParameters_RU findByCode(SalesBookOperationCode_RU _operationCode,
                                                                     boolean                   _forUpdate = false)
    {
        SalesBookVATProcessParameters_RU    salesBookVATProcessParameters;

        if (_operationCode)
        {
            salesBookVATProcessParameters.selectForUpdate(_forUpdate);

            select firstonly salesBookVATProcessParameters
                where salesBookVATProcessParameters.OperationCode  == _operationCode;
        }

        return salesBookVATProcessParameters;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldThrowExceptionOnZeroDelete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if concurrent deletes should throw exception.
    /// </summary>
    /// <returns>true if exception should be thrown; otherwise false.</returns>
    public boolean shouldThrowExceptionOnZeroDelete()
    {
        return super() || SalesEnforceConcurrentDeletesFlight::instance().isEnabled();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LedgerBasicSalesTax</ConfigurationKey>
	<CountryRegionCodes>RU</CountryRegionCodes>
	<DeveloperDocumentation>@GLS104553</DeveloperDocumentation>
	<FormRef>SalesBookVATProcessParameters_RU</FormRef>
	<Label>@GLS104553</Label>
	<TableGroup>Group</TableGroup>
	<TitleField1>OperationCode</TitleField1>
	<TitleField2>ProcessingType</TitleField2>
	<AllowRowVersionChangeTracking>Yes</AllowRowVersionChangeTracking>
	<CacheLookup>EntireTable</CacheLookup>
	<ClusteredIndex>OperationCodeIdx</ClusteredIndex>
	<Modules>RSalesPurchBooks</Modules>
	<PrimaryIndex>RecId</PrimaryIndex>
	<ReplacementKey>OperationCodeIdx</ReplacementKey>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>OperationCode</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>ProcessingType</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>OperationCode</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>ProcessingType</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>OperationCode</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Description</Name>
			<Label>@SYS6303</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Dimension</Name>
			<Label>@SYS14926</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>DefaultDimension</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Identification</Name>
			<Label>@SYS5711</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>OperationCode</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>ProcessingType</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>ProcessVAT</Name>
			<Label>@GLS101421</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>IsDefault</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>IncludeInBook</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>DeriveDim</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>FactureOperationType</Name>
			<Label>@GLS112932</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>FactureOperationTypeCode</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt64">
			<Name>DefaultDimension</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>LedgerDefaultDimensionValueSet</ExtendedDataType>
			<SysSharingType>Never</SysSharingType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>DeriveDim</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BookDeriveDimension_RU</ExtendedDataType>
			<EnumType>NoYes</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Description</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>Description</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>IncludeInBook</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>SalesBookIncludeOperation_RU</ExtendedDataType>
			<EnumType>NoYes</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>IsDefault</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<Label>@GLS101757</Label>
			<EnumType>NoYes</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>OperationCode</Name>
			<AllowEdit>No</AllowEdit>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>SalesBookOperationCode_RU</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>ProcessingType</Name>
			<AllowEdit>No</AllowEdit>
			<AssetClassification>Customer Content</AssetClassification>
			<EnumType>SalesBookProcessingType_RU</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>RestoringType</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<Visible>No</Visible>
			<EnumType>SalesBookVATRestoringType_RU</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>FactureOperationTypeCode</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>OperationTypeListCode_RU</ExtendedDataType>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>OperationCodeIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>OperationCode</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
		<AxTableIndex>
			<Name>DefaultDimensionIdx</Name>
			<AllowDuplicates>Yes</AllowDuplicates>
			<Fields>
				<AxTableIndexField>
					<DataField>DefaultDimension</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>DefaultDimension</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>DimensionAttributeValueSet</RelatedTable>
			<RelatedTableCardinality>ZeroOne</RelatedTableCardinality>
			<RelatedTableRole>DefaultDimension</RelatedTableRole>
			<RelationshipType>Association</RelationshipType>
			<Role>SalesBookVATProcessParameters_RU</Role>
			<UseDefaultRoleNames>No</UseDefaultRoleNames>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>DefaultDimension</Name>
					<Field>DefaultDimension</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>OperationTypeList_RU</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>OperationTypeList_RU</RelatedTable>
			<RelatedTableCardinality>ZeroOne</RelatedTableCardinality>
			<RelationshipType>Association</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>FactureOperationTypeCode</Name>
					<Field>FactureOperationTypeCode</Field>
					<RelatedField>Code</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>