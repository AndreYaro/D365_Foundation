<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResColor</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class EcoResColor extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>exist</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the specified record in the <c>EcoResColor</c> table exists.
    /// </summary>
    /// <param name="_color">
    ///    The ID of the <c>EcoResColor</c> record to find.
    /// </param>
    /// <returns>
    ///    true if the specified record exists; otherwise, false.
    /// </returns>
    static boolean exist(RefRecId _color)
    {
        if (!_color)
        {
            return false;
        }

        return (select firstonly RecId from ecoResColor
                    where ecoResColor.RecId == _color).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds the specified record in the <c>EcoResColor</c> table.
    /// </summary>
    /// <param name="_color">
    ///    The ID of the <c>EcoResColor</c> record to find.
    /// </param>
    /// <param name="_forUpdate">
    ///    A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <returns>
    ///    A record in the <c>EcoResColor</c> table; otherwise, an empty record.
    /// </returns>
    static EcoResColor find(RefRecId _color, boolean _forUpdate = false)
    {
        EcoResColor ecoResColor;

        ecoResColor.selectForUpdate(_forUpdate);

        if (0 != _color)
        {
            select firstonly ecoResColor
                where ecoResColor.RecId == _color;
        }
        return ecoResColor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds the specified record in the <c>EcoResColor</c> table.
    /// </summary>
    /// <param name="_name">
    ///    A value of the <c>Name</c> field of the <c>EcoResColor</c> table record.
    /// </param>
    /// <param name="_forUpdate">
    ///    A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <returns>
    ///    A record in the <c>EcoResColor</c> table; otherwise, an empty record.
    /// </returns>
    static EcoResColor findByName(EcoResColorName _name, boolean _forUpdate = false)
    {
        EcoResColor  ecoResColor;

        ecoResColor.selectForUpdate(_forUpdate);

        if (_name)
        {
            select firstonly ecoResColor
                where ecoResColor.Name == _name;
        }

        return ecoResColor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds the Go to Main Table form option to the name lookup.
    /// </summary>
    /// <param name="_name">
    /// A <c>EcoResColorName</c> value to look up.
    /// </param>
    /// <remarks>
    /// This method opens the relevant color form.
    /// </remarks>
    public static void jumpRef(EcoResColorName _name)
    {
        Args args = new Args();

        args.lookupField(fieldNum(EcoResColor, Name));
        args.lookupValue(_name);

        new MenuFunction(menuitemDisplayStr(RetailEcoResColor), MenuItemType::Display).run(args);
    }

]]></Source>
			</Method>
			<Method>
				<Name>label</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets a string with the label of the <c>EcoResColorName</c> extended data type.
    /// </summary>
    /// <returns>
    /// A string that contains the label.
    /// </returns>
    static FieldLabel label()
    {
        return new SysDictType(extendedTypeNum(EcoResColorName)).label();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays a lookup form to decide a color for a product.
    /// </summary>
    /// <param name="_formControl">
    /// A control for which a lookup is performed.
    /// </param>
    /// <param name="_useLookupValue">
    /// True if filter should be used in the lookup; otherwise false.
    /// </param>
    /// <remarks>
    /// The colors that are displayed are chosen from the colors that are assigned to the product master.
    /// </remarks>
    public static void lookupName(FormControl _formControl, boolean _useLookupValue = false)
    {
        Query                   query = new Query();
        QueryBuildDataSource    queryBuildDataSource  = query.addDataSource(tableNum(EcoResColor));
        SysTableLookup          tableLookup = SysTableLookup::newParameters(tablenum(EcoResColor), _formControl);
        tableLookup.addLookupfield(fieldnum(EcoResColor, Name));
        tableLookup.parmQuery(query);
        tableLookup.parmUseLookupValue(_useLookupValue);
        tableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupProductVariantColor</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays a lookup form to use to decide a color for a product variant.
    /// </summary>
    /// <param name="_formControl">
    /// A control for which a lookup is performed.
    /// </param>
    /// <param name="_productMaster">
    /// A value that specifies a product master to display in the lookup form.
    /// </param>
    /// <param name="_productDimensionAttribute">
    /// The ID of a record in the <c>EcoResProductDimensionAttribute</c> table that specifies the dimension
    /// attribute for which colors are displayed in the lookup form.
    /// </param>
    /// <returns>
    /// A selected <c>EcoResColor</c> buffer.
    /// </returns>
    /// <remarks>
    /// The colors that are displayed are chosen from the colors that are assigned to the product master.
    /// </remarks>
    public static EcoResColor lookupProductVariantColor(
        FormControl             _formControl,
        RefRecId                _productMaster,
        RefRecId                _productDimensionAttribute)
    {
        SysReferenceTableLookup sysReferenceTableLookup;
        Query                   query;

        sysReferenceTableLookup = SysReferenceTableLookup::newParameters(tableNum(EcoResColor), _formControl);
        sysReferenceTableLookup.addLookupfield(fieldNum(EcoResColor, Name));

        query = new Query(queryStr(EcoResProductVariantColorLookup));
        query.dataSourceTable(tableNum(EcoResProductMasterColor)).rangeField(fieldNum(EcoResProductMasterColor, ColorProductMaster)).value(queryValue(_productMaster));
        query.dataSourceTable(tableNum(EcoResProductMasterColor)).rangeField(fieldNum(EcoResProductMasterColor, ColorProductDimensionAttribute)).value(queryValue(_productDimensionAttribute));

        sysReferenceTableLookup.parmQuery(query);
        return sysReferenceTableLookup.performFormLookup() as EcoResColor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mcrInsertTmpStr</Name>
				<Source><![CDATA[
    private static TmpMCRStr mcrInsertTmpStr()
    {
        TmpMCRStr                               tmpMCRStr;
        EcoResColor                             ecoResColor;
        EcoResProductMasterColor                ecoResProductMasterColor;
        EcoResProductMasterDimValueTranslation  productMasterDimValueTranslation;

        insert_recordset tmpMCRStr (String15, Str60, Str1260)
            select firstOnly Name from  ecoResColor
                join ecoResProductMasterColor
                    where ecoResProductMasterColor.Color == ecoResColor.RecId
                join Name, Description from productMasterDimValueTranslation
                    where productMasterDimValueTranslation.ProductMasterDimensionValue == ecoResProductMasterColor.RecId
                    && productMasterDimValueTranslation.LanguageId == SystemParameters::getSystemLanguageId();

        select firstonly tmpMCRStr; //needed due to the insert_recordset
        return tmpMCRStr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mcrInsertTmpStrByProduct</Name>
				<Source><![CDATA[
    private static TmpMCRStr mcrInsertTmpStrByProduct(RefRecId _productId)
    {
        TmpMCRStr                               tmpMCRStr;
        EcoResColor                             ecoResColor;
        EcoResProductMasterColor                ecoResProductMasterColor;
        EcoResProductMasterDimValueTranslation  productMasterDimValueTranslation;

        if (_productId)
        {
            insert_recordset tmpMCRStr (String15, Str60, Str1260)
                select firstOnly Name from  ecoResColor
                    join ecoResProductMasterColor
                        where ecoResProductMasterColor.ColorProductMaster == _productId
                        && ecoResColor.RecId == ecoResProductMasterColor.Color
                    outer join Name, Description from productMasterDimValueTranslation
                        where productMasterDimValueTranslation.ProductMasterDimensionValue == ecoResProductMasterColor.RecId
                        && productMasterDimValueTranslation.LanguageId == SystemParameters::getSystemLanguageId();
        }

        select firstonly tmpMCRStr; //needed due to the insert_recordset
        return tmpMCRStr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>mcrlookupAll</Name>
				<Source><![CDATA[
    /// <summary>
    /// Loads a lookup form that can be used to choose a color.
    /// </summary>
    /// <param name="_formControl">
    /// A control for which a lookup is performed.
    /// </param>
    /// <remarks>
    /// The colors that are displayed are chosen from the colors that are assigned to the product master.
    /// </remarks>
    public static void mcrlookupAll(FormControl    _formControl)
    {
        SysTableLookup                      sysTableLookup = SysTableLookup::newParameters(tableNum(TmpMCRStr), _formControl);
        TmpMCRStr                           tmpMCRStr;

        tmpMCRStr = EcoResColor::mcrInsertTmpStr();

        sysTableLookup.parmTmpBuffer(tmpMCRStr);
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, String15));
        sysTableLookup.setLabel("@SYS30436");
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, Str60));
        sysTableLookup.setLabel("@SYS25589");
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, Str1260));
        sysTableLookup.setLabel("@SYS127587");

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>mcrlookupByGroup</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Loads a lookup form that is used to choose a color.
    /// </summary>
    /// <param name="_formControl">
    ///    A control for which a lookup is performed.
    /// </param>
    /// <param name="_itemGroupId">
    ///    A value that specifies item group id of colors to add to lookup form.
    /// </param>
    /// <remarks>
    ///    The colors that are displayed are chosen from the colors that are assigned to the product master.
    /// </remarks>
    public static void mcrlookupByGroup(
        FormControl         _formControl,
        ItemGroupId         _itemGroupId)
    {
        SysTableLookup          sysTableLookup = SysTableLookup::newParameters(tableNum(EcoResColor), _formControl);
        Query                   query = new Query();
        QueryBuildDataSource    qbdsColor;
        QueryBuildDataSource    qbdsColorMaster;
        QueryBuildDataSource    qbdsProductMaster;
        QueryBuildDataSource    qbdsInventTable;
        QueryBuildRange         queryBuildRange;
        QueryBuildDataSource    qbdsInventItemGroupItem;

        qbdsColor          =  query.addDataSource(tableNum(EcoResColor));
        qbdsColorMaster    =  qbdsColor.addDataSource(tableNum(EcoResProductMasterColor));
        qbdsProductMaster  =  qbdsColorMaster.addDataSource(tableNum(EcoResProductMaster));
        qbdsInventTable    =  qbdsColorMaster.addDataSource(tableNum(InventTable));
        qbdsInventTable.addLink(fieldNum(EcoResProductMaster, RecId), fieldNum(InventTable, Product));

        qbdsInventItemGroupItem    = qbdsInventTable.addDataSource(tableNum(InventItemGroupItem));
        queryBuildRange            = qbdsInventItemGroupItem.addRange(fieldNum(InventItemGroupItem, ItemGroupId));
        queryBuildRange.value(_itemGroupId);

        sysTableLookup.addLookupfield(fieldNum(EcoResColor, Name));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>mcrlookupByItemOrProduct</Name>
				<Source><![CDATA[
    /// <summary>
    /// Looks up color by item or product (no need for item if product is supplied).
    /// </summary>
    /// <param name="_formControl">
    /// The control on which the look up will be performed.
    /// </param>
    /// <param name="_itemId">
    /// The item for which the products need to be looked up.
    /// </param>
    /// <param name="_product">
    /// The product for which the colors have to be looked up.
    /// </param>
    /// <remarks>
    /// If the product <c>RefRecId</c> is supplied, the item will get disregarded.
    /// </remarks>
    public static void mcrlookupByItemOrProduct(
        FormControl             _formControl,
        ItemId                  _itemId,
        RefRecId                _product = 0
        )
    {
        SysTableLookup                      sysTableLookup = SysTableLookup::newParameters(tableNum(TmpMCRStr), _formControl);
        RefRecId                            productID = _product;
        TmpMCRStr                           tmpMCRStr;

        if (_product == 0 && _itemId)
        {
            productID = EcoResProductMaster::find(InventTable::find(_itemId).Product).RecId;
        }

        tmpMCRStr = EcoResColor::mcrInsertTmpStrByProduct(productID);

        sysTableLookup.parmTmpBuffer(tmpMCRStr);
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, String15));
        sysTableLookup.setLabel("@SYS30436");
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, Str60));
        sysTableLookup.setLabel("@SYS25589");
        sysTableLookup.addLookupfield(fieldNum(TmpMCRStr, Str1260));
        sysTableLookup.setLabel("@SYS127587");

        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>resolveProductVariantColor</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds an <c>EcoResColor</c> record.
    /// </summary>
    /// <param name="_colorName">
    ///    The name of a color to find.
    /// </param>
    /// <param name="_productMaster">
    ///    The ID of an <c>EcoResGenericProductMaster</c> record that identifies a product master for which a
    ///    color is assigned.
    /// </param>
    /// <param name="_productDimensionAttribute">
    ///    The ID of an <c>EcoResProductDimensionAttribute</c> record.
    /// </param>
    /// <returns>
    ///    A <c>EcoResColor</c> buffer.
    /// </returns>
    /// <remarks>
    ///    This function is used to resolve a color name to the ID of an <c>EcoResColor</c> record.It is
    ///    resolved based on the colors that are assigned to a specified product master.
    /// </remarks>
    static EcoResColor resolveProductVariantColor(
        EcoResColorName     _colorName,
        RefRecId            _productMaster,
        RefRecId            _productDimensionAttribute)
    {
        EcoResColor              ecoResColor;
        EcoResProductMasterColor ecoResProductMasterColor;

        select firstonly ecoResColor
            where ecoResColor.Name == _colorName
        exists join ecoResProductMasterColor
            where ecoResProductMasterColor.Color                            == ecoResColor.RecId
               && ecoResProductMasterColor.ColorProductMaster               == _productMaster
               && ecoResProductMasterColor.ColorProductDimensionAttribute   == _productDimensionAttribute;

        if (!ecoResColor.RecId)
        {
            // Assigning null to selectedRecord does not cause the kernel to receive a true NULL value, but rather
            // just an empty record. Return NULL directly so the kernel knows that the intent is to not clear the
            // selected value.
            return null;
        }

        return ecoResColor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldThrowExceptionOnZeroDelete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if concurrent deletes should throw exception.
    /// </summary>
    /// <returns>true if exception should be thrown; otherwise false.</returns>
    public boolean shouldThrowExceptionOnZeroDelete()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean ret;
        System.Text.RegularExpressions.Match regExMatch;
        str expectedFormat = '^#[a-fA-F0-9]{6}$';
        
        ret = super(_fieldIdToCheck);

    
        if (this.HexCode)
        {
            regExMatch = System.Text.RegularExpressions.Regex::Match(this.HexCode, expectedFormat);

            ret = regExMatch.get_Success();

            if (!ret)
            {
                ret = checkFailed("@Retail:RetailHexCodeValidationError");
            }

        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>EcoResProductColor</ConfigurationKey>
	<DeveloperDocumentation>@SYS133209</DeveloperDocumentation>
	<FormRef>EcoResProductMasterDimensionPerCompany</FormRef>
	<Label>@SYS73744</Label>
	<TableGroup>Main</TableGroup>
	<TitleField1>Name</TitleField1>
	<AllowRowVersionChangeTracking>Yes</AllowRowVersionChangeTracking>
	<CacheLookup>Found</CacheLookup>
	<Modules>ProductInformationManagement</Modules>
	<ReplacementKey>NameIdx</ReplacementKey>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<DeleteActions>
		<AxTableDeleteAction>
			<Name>EcoResProductMasterColor</Name>
			<DeleteAction>Restricted</DeleteAction>
			<Relation></Relation>
			<Table>EcoResProductMasterColor</Table>
		</AxTableDeleteAction>
		<AxTableDeleteAction>
			<Name>EcoResProductVariantColor</Name>
			<DeleteAction>Restricted</DeleteAction>
			<Relation></Relation>
			<Table>EcoResProductVariantColor</Table>
		</AxTableDeleteAction>
	</DeleteActions>
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Identification</Name>
			<Label>@SYS5711</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>DisplayOrder</Name>
			<Label>@Retail:RetailGlobalDisplayOrder</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>DisplayOrder</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Name</Name>
			<AllowEdit>No</AllowEdit>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>EcoResColorName</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldReal">
			<Name>DisplayOrder</Name>
			<ExtendedDataType>RetailGlobalDisplayOrder</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Url</Name>
			<ExtendedDataType>MediaUrl</ExtendedDataType>
			<FeatureClass>RetailColorSwatchFeature</FeatureClass>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>HexCode</Name>
			<ExtendedDataType>RetailHexCode</ExtendedDataType>
			<FeatureClass>RetailColorSwatchFeature</FeatureClass>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>RefinerGroup</Name>
			<ExtendedDataType>RetailRefinerGroup</ExtendedDataType>
			<FeatureClass>RetailColorSwatchFeature</FeatureClass>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>NameIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>Name</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations />
	<StateMachines />
</AxTable>