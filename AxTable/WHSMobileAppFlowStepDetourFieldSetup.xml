<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSMobileAppFlowStepDetourFieldSetup</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class WHSMobileAppFlowStepDetourFieldSetup extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>existFieldSetup</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static boolean existFieldSetup(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup)
    {
        WHSMobileAppFlowStepDetourFieldSetup otherFieldSetup;

        select firstonly RecId from otherFieldSetup
            where otherFieldSetup.MobileAppFlowStepDetour == _fieldSetup.MobileAppFlowStepDetour
               && otherFieldSetup.Direction               == _fieldSetup.Direction
               && otherFieldSetup.MainField               == _fieldSetup.MainField
               && otherFieldSetup.DetourField             == _fieldSetup.DetourField;

        return otherFieldSetup.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateOtherFieldForRefreshPageRecord</Name>
				<Source><![CDATA[
    private static ExtendedTypeId updateOtherFieldForRefreshPageRecord(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup, boolean _selectingMainField)
    {
        // One of the following is happening: If one field is set to Refresh, set the other to Refresh. If one field is set to no longer be Refresh, set the other to Empty.
        boolean mainFieldIsRefresh = (_fieldSetup.MainField == extendedTypeNum(ProcessGuideRefreshPageType));
        boolean selectingRefresh = (mainFieldIsRefresh == _selectingMainField); // We are modifying/selecting exactly one field, and exactly one field is Refresh.
        ExtendedTypeId otherFieldNewValue = (selectingRefresh ? extendedTypeNum(ProcessGuideRefreshPageType) : 0);

        if (_selectingMainField)
        {
            _fieldSetup.DetourField = otherFieldNewValue;
        }
        else
        {
            _fieldSetup.MainField = otherFieldNewValue;
        }

        return otherFieldNewValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkAndUpdateRefreshPageRecord</Name>
				<Source><![CDATA[
    private static boolean checkAndUpdateRefreshPageRecord(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup, boolean _isWrite, boolean _selectingMainField)
    {
        if (_fieldSetup.Direction != WHSMobileAppFlowStepDetourFieldDirection::FromDetourToMain)
        {
            return checkFailed("@WAX:Error_RefreshPageOnlyAllowedFromDetourToMain");
        }

        ExtendedTypeId otherFieldNewValue = -1; // Unchanged
        if (_fieldSetup.MainField != _fieldSetup.DetourField)
        {
            if (_isWrite)
            {
                return checkFailed("@WAX:Error_BothFieldsMustBeRefreshPage");
            }

            // If one field is set to Refresh, set the other to Refresh. If one field is set to no longer be Refresh, set the other to Empty.
            otherFieldNewValue = WHSMobileAppFlowStepDetourFieldSetup::updateOtherFieldForRefreshPageRecord(_fieldSetup, _selectingMainField);
        }

        if (otherFieldNewValue != 0) // Unless we no longer have any Refresh values selected
        {
            WHSMobileAppFlowStepDetourFieldSetup otherFieldSetup;
            select firstonly RecId from otherFieldSetup
                    where otherFieldSetup.MobileAppFlowStepDetour == _fieldSetup.MobileAppFlowStepDetour
                       && otherFieldSetup.Direction               == _fieldSetup.Direction
                       && otherFieldSetup.RecId                   != _fieldSetup.RecId
                       && otherFieldSetup.AutoSubmit              == NoYes::Yes;

            if (otherFieldSetup.RecId != 0)
            {
                return checkFailed("@WAX:Error_RefreshPageCannotBeUsedWithOtherAutoSubmit");
            }

            _fieldSetup.AutoSubmit = NoYes::Yes;
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkAndUpdateAutoSubmitRecord</Name>
				<Source><![CDATA[
    private static boolean checkAndUpdateAutoSubmitRecord(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup, boolean _isWrite)
    {
        WHSMobileAppFlowStepDetourFieldSetup otherFieldSetup;
        select firstonly RecId from otherFieldSetup
                where otherFieldSetup.MobileAppFlowStepDetour == _fieldSetup.MobileAppFlowStepDetour
                   && otherFieldSetup.Direction               == _fieldSetup.Direction
                   && otherFieldSetup.MainField               == extendedTypeNum(ProcessGuideRefreshPageType);

        if (otherFieldSetup.RecId != 0)
        {
            if (!_isWrite)
            {
                _fieldSetup.AutoSubmit = NoYes::No;
            }

            return checkFailed("@WAX:Error_RefreshPageCannotBeUsedWithOtherAutoSubmit");
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkAndUpdateFieldsForRefreshPage</Name>
				<Source><![CDATA[
    [Hookable(false)]
    internal static boolean checkAndUpdateFieldsForRefreshPage(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup, boolean _isWrite, boolean _selectingMainField)
    {
        // WHSMobileAppDetourFlowAvailableFieldsProviderGenericDataInquiry initializes fields map with fields numbers and it's not correct to compare this value with EDT number.
        // There are no tables which use ProcessGuideRefreshPageType as a field type and it should not be used as a field type on a table.
        if (WHSMobileAppFlowStepDetourFieldSetup::isFieldRefreshPageType(_fieldSetup)
            && !WHSMobileAppFlowStepDetourFieldSetup::isGenericDataInquiry(_fieldSetup))
        {
            return WHSMobileAppFlowStepDetourFieldSetup::checkAndUpdateRefreshPageRecord(_fieldSetup, _isWrite, _selectingMainField);
        }
        else if (_fieldSetup.AutoSubmit == NoYes::Yes)
        {
            return WHSMobileAppFlowStepDetourFieldSetup::checkAndUpdateAutoSubmitRecord(_fieldSetup, _isWrite);
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>isFieldRefreshPageType</Name>
				<Source><![CDATA[
    private static boolean isFieldRefreshPageType(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup)
    {
        return _fieldSetup.MainField   == extendedTypeNum(ProcessGuideRefreshPageType) || 
            _fieldSetup.DetourField == extendedTypeNum(ProcessGuideRefreshPageType);
    }

]]></Source>
			</Method>
			<Method>
				<Name>isGenericDataInquiry</Name>
				<Source><![CDATA[
    private static boolean isGenericDataInquiry(WHSMobileAppFlowStepDetourFieldSetup _fieldSetup)
    {
        if (WHSMobAppFlowStepDetFieldSetupRefreshPageGenerDataInqFlight::instance().isEnabled())
        {
            WHSRFMenuItemTable menuItem;
            WHSMobileAppFlowStepDetour stepDetour;

            select firstonly RecId from menuItem
                where menuItem.WorkActivity == WHSWorkActivity::GenericDataInquiry
                exists join stepDetour
                    where stepDetour.MenuItemName == menuItem.MenuItemName
                        && stepDetour.RecId == _fieldSetup.MobileAppFlowStepDetour;

            return menuItem.RecId != 0;
        }
        
        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    public boolean validateWrite()
    {
        boolean ret = super();

        ret = ret && WHSMobileAppFlowStepDetourFieldSetup::checkAndUpdateFieldsForRefreshPage(this, true, false);
    
        if (ret && this.RecId == 0 && WHSMobileAppFlowStepDetourFieldSetup::existFieldSetup(this))
        {
            return checkFailed("@WAX:Error_DuplicatedDetourFieldSetupIsNotAllowed");
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>WHSandTMS</ConfigurationKey>
	<SubscriberAccessLevel>
		<Read>Allow</Read>
	</SubscriberAccessLevel>
	<TableGroup>Main</TableGroup>
	<CacheLookup>FoundAndEmpty</CacheLookup>
	<ClusteredIndex>FlowStepDetourDirectionMainDetourFieldIdx</ClusteredIndex>
	<PrimaryIndex>FlowStepDetourDirectionMainDetourFieldIdx</PrimaryIndex>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt64">
			<Name>MobileAppFlowStepDetour</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ConfigurationKey>WHSandTMS</ConfigurationKey>
			<ExtendedDataType>WHSMobileAppFlowStepDetourRefRecId</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>Direction</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ConfigurationKey>WHSandTMS</ConfigurationKey>
			<Mandatory>Yes</Mandatory>
			<EnumType>WHSMobileAppFlowStepDetourFieldDirection</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt">
			<Name>MainField</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ConfigurationKey>WHSandTMS</ConfigurationKey>
			<ExtendedDataType>ExtendedTypeId</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt">
			<Name>DetourField</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ConfigurationKey>WHSandTMS</ConfigurationKey>
			<ExtendedDataType>ExtendedTypeId</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>AutoSubmit</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ConfigurationKey>WHSandTMS</ConfigurationKey>
			<Label>@WAX:WHSMobileAppFlowStepDetourAutoSubmit</Label>
			<EnumType>NoYes</EnumType>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>FlowStepDetourDirectionMainDetourFieldIdx</Name>
			<Fields>
				<AxTableIndexField>
					<DataField>MobileAppFlowStepDetour</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>Direction</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>MainField</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>DetourField</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation>
			<Name>WHSMobileAppFlowStepDetour</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>WHSMobileAppFlowStepDetour</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Composition</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>MobileAppFlowStepDetour</Name>
					<Field>MobileAppFlowStepDetour</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>