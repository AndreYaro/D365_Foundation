<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>VendUserRequest</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class VendUserRequest extends UserRequest
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>accountNumber</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the account number for the request.
    /// </summary>
    /// <returns>
    /// The account number for the request.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table will provide the account number that is specific to the
    /// sub type.
    /// </remarks>
    public display UserRequestAccountNumber accountNumber()
    {
        return this.VendorAccountNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkPerson</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates the person for the request.
    /// </summary>
    /// <returns>
    /// true if the person is valid; otherwise, false.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to validate the person.
    /// </remarks>
    public boolean checkPerson()
    {
        DirPartyRelationship    contactRelationship;
        DirRelationshipTypeId   relationShipType;
        boolean                 ret = true;

        relationShipType = DirRelationshipTypeTable::findBySystemType(DirSystemRelationshipType::ContactPerson).RelationshipTypeId;

        select RecId from contactRelationship
            where contactRelationship.ParentParty == this.vendorParty()
            && contactRelationship.ChildParty == this.Person
            && contactRelationship.RelationshipTypeId == relationShipType;

        if (contactRelationship.RecId == 0)
        {
            ret = checkFailed("@SYS334522");
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkRequestedRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the requested role is available.
    /// </summary>
    /// <returns>
    /// true if the role is valid; otherwise, false.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to validate the role.
    /// </remarks>
    public boolean checkRequestedRole()
    {
        DirExternalRole externalRole;
        boolean         ret = true;

        if (this.RequestedRole != 0)
        {
            select RecId from externalRole
                where externalRole.Role == this.RequestedRole
                && externalRole.EntityType == UserExternalPartyEntityType::Vendor;

            if (externalRole.RecId == 0)
            {
                ret = checkFailed("@SYS324475");
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFrom</Name>
				<Source><![CDATA[
    /// <summary>
    /// Initializes the request from a related record.
    /// </summary>
    /// <param name="_relatedRecord">
    /// The related record from which to initialize the request.
    /// </param>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this this method to initialize the request.
    /// </remarks>
    public void initFrom(Common _relatedRecord)
    {
        ContactPerson contact;

        if (_relatedRecord is ContactPerson)
        {
            contact = _relatedRecord as ContactPerson;

            this.VendorDataAreaId = contact.DataAreaId;
            this.VendorAccountNumber = VendTable::findByPartyRecId(contact.ContactForParty).AccountNum;
        }

        super(_relatedRecord);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initValue</Name>
				<Source><![CDATA[
        public void initValue()
    {
        super();

        this.VendorDataAreaId = curext();
        this.AuthenticationMode = UserRequestParameters::find().VendorAuthenticationMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>jobTitle</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays the job title.
    /// </summary>
    /// <returns>
    /// The job title.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to provide the job tile.
    /// </remarks>
    public display UserRequestJobTitle jobTitle()
    {
        UserRequestJobTitle jobTitle;

        if (this.VendorDataAreaId != '')
        {
            changecompany(this.VendorDataAreaId)
            {
                jobTitle = ContactPerson::findByPartyRecId(this.Person).Title;
            }
        }

        return jobTitle;
    }

]]></Source>
			</Method>
			<Method>
				<Name>organizationName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays the organization name.
    /// </summary>
    /// <returns>
    /// The organization name.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to provide the organization name.
    /// </remarks>
    public display UserRequestOrganizationName organizationName()
    {
        UserRequestOrganizationName ret;

        if (this.VendorDataAreaId != '')
        {
            changecompany(this.VendorDataAreaId)
            {
                ret = VendTable::find(this.VendorAccountNumber).name();
            }
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>personFilter</Name>
				<Source><![CDATA[
    /// <summary>
    /// Provides the filter for the lookup of the person for the request.
    /// </summary>
    /// <returns>
    /// The filter for the person lookup.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to provide the filter.
    /// </remarks>
    public dirPartyLookupEntitiesFilter personFilter()
    {
        DirRelationshipTypeId           relationShipType;
        dirPartyLookupEntitiesFilter    filter = dirPartyLookupEntitiesFilter::generateDirPartyLookupEntitiesFilter(NoYes::No);

        relationShipType = DirRelationshipTypeTable::findBySystemType(DirSystemRelationshipType::ContactPerson).RelationshipTypeId;
        filter.parmFilterRelationshipType(relationShipType);

        filter.parmFilterParentParty(this.vendorParty());

        return filter;
    }

]]></Source>
			</Method>
			<Method>
				<Name>requestType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Displays the type of the request.
    /// </summary>
    /// <returns>
    /// The request type.
    /// </returns>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table should override this method to provide the request type.
    /// </remarks>
    public display UserRequestEntityType requestType()
    {
        return UserRequestEntityType::Vendor;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultRole</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the default security role for the person.
    /// </summary>
    public void setDefaultRole()
    {
        DirExternalRole     externalRole;
        SecurityUserRole    userRole;

        if (this.RequestAction == UserRequestAction::AddUser)
        {
            select firstonly externalRole
                where externalRole.EntityType == UserExternalPartyEntityType::Vendor;

            this.RequestedRole = externalRole.Role;
        }
        else
        {
            select firstonly SecurityRole from userRole
                exists join externalRole
                where userRole.User == this.User
                && externalRole.EntityType == UserExternalPartyEntityType::Vendor
                && externalRole.Role == userRole.SecurityRole;

            this.RequestedRole = userRole.SecurityRole;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultUser</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the default user for the request.
    /// </summary>
    public void setDefaultUser()
    {
        UserExternalParty   userExternalParty;
        DirPersonUser       personUser;

        select firstonly User from personUser
            exists join userExternalParty
            where personUser.User == userExternalParty.User
            && personUser.PersonParty == this.Person
            && userExternalParty.ExternalEntityType == UserExternalPartyEntityType::Vendor
            && userExternalParty.LegalEntity == CompanyInfo::findDataArea(this.VendorDataAreaId).RecId
            && userExternalParty.ExternalParty == this.vendorParty();

        this.User = personUser.User;
        this.userModified();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateRoleLookupQuery</Name>
				<Source><![CDATA[
    /// <summary>
    /// Updates the query that will be used as the security role lookup.
    /// </summary>
    /// <param name="_securityRoleLookupQuery">
    /// The query to update.
    /// </param>
    /// <remarks>
    /// Sub types of the <c>UserRequest</c> table can override this method to update the default query.
    /// </remarks>
    public void updateRoleLookupQuery(Query _securityRoleLookupQuery)
    {
        QueryBuildDataSource externalRoleDataSource = _securityRoleLookupQuery.dataSourceTable(tableNum(SecurityRole)).addDataSource(tableNum(DirExternalRole));

        externalRoleDataSource.addLink(fieldNum(SecurityRole, RecId), fieldNum(DirExternalRole, Role));
        externalRoleDataSource.joinMode(JoinMode::ExistsJoin);
        SysQuery::findOrCreateRange(externalRoleDataSource, fieldNum(DirExternalRole, EntityType)).value(queryValue(UserExternalPartyEntityType::Vendor));
    }

]]></Source>
			</Method>
			<Method>
				<Name>vendorParty</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the vendor party for the request.
    /// </summary>
    /// <returns>
    /// The vendor party for the request.
    /// </returns>
    public DirPartyRecId vendorParty()
    {
        DirPartyRecId vendorParty;

        if (this.VendorDataAreaId != '')
        {
            changecompany(this.VendorDataAreaId)
            {
                vendorParty = VendTable::find(this.VendorAccountNumber).Party;
            }
        }

        return vendorParty;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>ProcEssentials</ConfigurationKey>
	<DeveloperDocumentation>@SYS334528</DeveloperDocumentation>
	<Label>@SYS130187</Label>
	<SingularLabel>@SYS334480</SingularLabel>
	<TableGroup>Transaction</TableGroup>
	<CacheLookup>Found</CacheLookup>
	<Extends>UserRequest</Extends>
	<Modules>Vendor</Modules>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<SupportInheritance>Yes</SupportInheritance>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>VendorAccountNumber</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>VendorDataAreaId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Identification</Name>
			<Label>@SYS35583</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>VendorAccountNumber</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>VendorDataAreaId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>VendorAccountNumber</Name>
			<AllowEdit>No</AllowEdit>
			<AssetClassification>Customer Content </AssetClassification>
			<ExtendedDataType>VendAccount</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>VendorDataAreaId</Name>
			<AllowEdit>No</AllowEdit>
			<AssetClassification>Customer Content </AssetClassification>
			<ExtendedDataType>SelectableDataArea</ExtendedDataType>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>VendTableIdx</Name>
			<AllowDuplicates>Yes</AllowDuplicates>
			<Fields>
				<AxTableIndexField>
					<DataField>VendorAccountNumber</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>VendorDataAreaId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>VendTable</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>VendTable</RelatedTable>
			<RelatedTableCardinality>ZeroOne</RelatedTableCardinality>
			<RelationshipType>Association</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>VendorDataAreaId</Name>
					<Field>VendorDataAreaId</Field>
					<RelatedField>dataAreaId</RelatedField>
				</AxTableRelationConstraint>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>VendorAccountNumber</Name>
					<SourceEDT>VendAccount</SourceEDT>
					<Field>VendorAccountNumber</Field>
					<RelatedField>AccountNum</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>AccountIdx</Index>
		</AxTableRelation>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>VendUserRequest</Name>
			<Cardinality>ZeroOne</Cardinality>
			<RelatedTable>UserRequest</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Specialization</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>RecId</Name>
					<Field>RecId</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>