<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WHSImplementationTask</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class WHSImplementationTask extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>taskLinkLabelValue</Name>
				<Source><![CDATA[
    internal edit WHSImplementationAction taskLinkLabelValue(boolean _set, WHSImplementationTask _currentTemplateTask, WHSImplementationAction _action)
    {
        var actionToPerform = _currentTemplateTask.MenuItem;
        WHSImplementationAction actionLabel;

        if (strLen(actionToPerform) > 0)
        {
            var menuItemType = _currentTemplateTask.MenuItemType;

            if (menuItemType == WHSImplementationTask::menuItemTypeURL())
            {
                actionLabel = actionToPerform;
            }
            else
            {
                MenuFunction menuFunction = new MenuFunction(_currentTemplateTask.MenuItem, _currentTemplateTask.MenuItemType);
                actionLabel = menuFunction.label();
            }
        }

        return actionLabel;
    }

]]></Source>
			</Method>
			<Method>
				<Name>displayStatusImage</Name>
				<Source><![CDATA[
    internal display container displayStatusImage(WHSImplementationTask _task)
    {
        str symbolString = '';

        switch (_task.Status)
        {
            case WHSImplementationTaskStatus::Complete:
                symbolString = ImageReferenceSymbol::Completed;
                break;
        }

        return ImageReference::constructForSymbol(symbolString).pack();
    }

]]></Source>
			</Method>
			<Method>
				<Name>editIsComplete</Name>
				<Source><![CDATA[
    internal edit boolean editIsComplete(boolean _isSet, WHSImplementationTask _task, boolean _isComplete)
    {
        boolean isComplete = false;

        if (_isSet)
        {
            if (_isComplete)
            {
                _task.Status = WHSImplementationTaskStatus::Complete;
                _task.CompletedDateTime = DateTimeUtil::utcNow();
                _task.CompletedByWorker = HcmWorkerLookup::currentWorker();
            }
            else
            {
                _task.Status = WHSImplementationTaskStatus::NotStarted;
                _task.CompletedDateTime = DateTimeUtil::minValue();
                _task.CompletedByWorker = 0;
            }
        }

        if (_task.Status == WHSImplementationTaskStatus::Complete)
        {
            isComplete = true;
        }

        return isComplete;
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRef</Name>
				<Source><![CDATA[
    internal static void jumpRef(WHSImplementationAction _menuItem, MenuItemType _menuItemType)
    {
        if (_menuItem)
        {
            if (_menuItemType == WHSImplementationTask::menuItemTypeURL())
            {
                new Browser().navigate(_menuItem, false, false);
            }
            else
            {
                MenuFunction menuFunction = new MenuFunction(_menuItem, _menuItemType);
                appl.setDefaultCompany(curExt());
                menuFunction.run();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findPrevTaskAtSameLevel</Name>
				<Source><![CDATA[
    internal static WHSImplementationTask findPrevTaskAtSameLevel(WHSSeqNum _sequenceNumber, WHSSeqNum _parentSequenceNumber)
    {
        WHSImplementationTask prevTask;
        
        select firstonly prevTask
            order by SequenceNumber desc
                where prevTask.ParentSequenceNumber == _parentSequenceNumber
                && prevTask.SequenceNumber < _sequenceNumber;

        return prevTask;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findNextTaskAtSameLevel</Name>
				<Source><![CDATA[
    internal static WHSImplementationTask findNextTaskAtSameLevel(WHSSeqNum _sequenceNumber, WHSSeqNum _parentSequenceNumber)
    {
        WHSImplementationTask nextTask;
        
        select firstonly nextTask
            order by SequenceNumber asc
            where  nextTask.ParentSequenceNumber == _parentSequenceNumber
                && nextTask.SequenceNumber > _sequenceNumber;

        return nextTask;
    }

]]></Source>
			</Method>
			<Method>
				<Name>sequenceNumberStr</Name>
				<Source><![CDATA[
    internal display str sequenceNumberStr(WHSImplementationTask _task)
    {
        if (!_task.ParentSequenceNumber)
        {
            int countOfChildTasksForTasksAtCurrentLevel = WHSImplementationTask::findCountOfChildTasksForTasksAtCurrentLevel(_task.SequenceNumber, _task.ParentSequenceNumber);
            return int2Str(_task.SequenceNumber - countOfChildTasksForTasksAtCurrentLevel);
        }

        WHSImplementationTask parentTask = WHSImplementationTask::findBySequenceNumber(_task.ParentSequenceNumber);
        str retVal = this.sequenceNumberStr(parentTask);

        int countOfChildTasksForTasksAtCurrentLevel2 = WHSImplementationTask::findCountOfChildTasksForTasksAtCurrentLevel(_task.SequenceNumber, _task.ParentSequenceNumber);
        retVal += "." + int2Str(_task.SequenceNumber - _task.ParentSequenceNumber - countOfChildTasksForTasksAtCurrentLevel2);

        return retVal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findCountOfChildTasksForTasksAtCurrentLevel</Name>
				<Source><![CDATA[
    private static int findCountOfChildTasksForTasksAtCurrentLevel(WHSSeqNum _currentSequenceNumber, WHSSeqNum _currentTasksParentSequenceNumber)
    {
        WHSImplementationTask task;

        select count(SequenceNumber) from task
            where  task.SequenceNumber < _currentSequenceNumber
                && task.ParentSequenceNumber > _currentTasksParentSequenceNumber;

        return task.SequenceNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    public static WHSImplementationTask find(WHSImplementationTaskName _taskName, boolean _forupdate = false)
    {
        WHSImplementationTask task;

        if (_taskName)
        {
            task.selectForUpdate(_forupdate);

            select firstonly task
                where task.Name == _taskName;
        }

        return task;
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldThrowExceptionOnZeroDelete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if concurrent deletes should throw exception.
    /// </summary>
    /// <returns>true if exception should be thrown; otherwise false.</returns>
    [Hookable(false)]
    public boolean shouldThrowExceptionOnZeroDelete()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findNextSequenceNumber</Name>
				<Source><![CDATA[
    internal static WHSSeqNum findNextSequenceNumber()
    {
        WHSImplementationTask task;

        select maxof(SequenceNumber) from task;

        return task.SequenceNumber + 1;
    }

]]></Source>
			</Method>
			<Method>
				<Name>moveRows</Name>
				<Source><![CDATA[
    internal WHSImplementationTask moveRows(boolean _up)
    {
        WHSImplementationTask taskCursor = this;
        WHSImplementationTask taskNew;

        if (this.checkRecordsCanBeMoved(_up))
        {
            ttsbegin;

            Map recordIdSequenceNumberMap = new Map(Types::Int64, Types::Container);

            select forupdate taskNew where taskNew.RecId == taskCursor.RecId;

            if (taskNew)
            {
                WHSSeqNum oldLineNum = taskNew.SequenceNumber;

                WHSSeqNum toTaskSeqNum;
                int totalChildOfToTask, totalChildOfFromTask;

                if (_up)
                {
                    WHSImplementationTask taskTo = WHSImplementationTask::findPrevTaskAtSameLevel(taskNew.SequenceNumber, taskNew.ParentSequenceNumber);

                    toTaskSeqNum = taskTo.SequenceNumber;
                    totalChildOfToTask = oldLineNum - toTaskSeqNum;

                    // How many children does the "From" task have.
                    WHSImplementationTask taskFrom = WHSImplementationTask::findNextTaskAtSameLevel(taskNew.SequenceNumber, taskNew.ParentSequenceNumber);

                    if (!taskFrom)
                    {
                        totalChildOfFromTask = 1;
                    }
                    else
                    {
                        totalChildOfFromTask = taskFrom.SequenceNumber - oldLineNum;
                    }

                    taskNew.SequenceNumber = toTaskSeqNum;

                    // If there are child task they should be updated with new parent id
                    this.updateChildTasks(oldLineNum, -totalChildOfToTask, recordIdSequenceNumberMap, false);
                    this.updateChildTasks(toTaskSeqNum, totalChildOfFromTask, null, true);

                    oldLineNum = taskNew.SequenceNumber + totalChildOfFromTask;
                }
                else
                {
                    WHSImplementationTask taskDown = WHSImplementationTask::findNextTaskAtSameLevel(taskNew.SequenceNumber, taskNew.ParentSequenceNumber);

                    toTaskSeqNum = taskDown.SequenceNumber;
                    totalChildOfFromTask = taskDown.SequenceNumber - oldLineNum;

                    // How many children does the "To" task have.
                    WHSImplementationTask taskUp = WHSImplementationTask::findNextTaskAtSameLevel(toTaskSeqNum, taskNew.ParentSequenceNumber);

                    if (!taskUp)
                    {
                        totalChildOfToTask = 1;
                    }
                    else
                    {
                        totalChildOfToTask = taskUp.SequenceNumber - toTaskSeqNum;
                    }

                    taskNew.SequenceNumber += totalChildOfToTask;

                    // If there are child task they should be updated with new parent id
                    this.updateChildTasks(oldLineNum, totalChildOfToTask, recordIdSequenceNumberMap, false);
                    this.updateChildTasks(toTaskSeqNum, -totalChildOfFromTask, null, true);
                }

                WHSImplementationTask taskOld;
                select firstonly taskOld
                    where  taskOld.SequenceNumber == toTaskSeqNum
                        && taskOld.ParentSequenceNumber == taskNew.ParentSequenceNumber
                        && taskOld.RecId != taskNew.RecId;

                recordIdSequenceNumberMap.insert(taskOld.RecId, [oldLineNum, taskNew.ParentSequenceNumber]);

                taskNew.update();

                this.updateTaskRecords(recordIdSequenceNumberMap);
            }

            ttscommit;
        }

        return taskNew;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateTaskRecords</Name>
				<Source><![CDATA[
    private void updateTaskRecords(Map _recordIdSequenceNumberMap)
    {
        WHSImplementationTask taskUpdate;
        MapEnumerator enumertor = _recordIdSequenceNumberMap.getEnumerator();

        while (enumertor.moveNext())
        {
            RecId taskRecId = enumertor.currentKey();
            WHSSeqNum sequenceNumber, parentSequenceNumber;
            [sequenceNumber, parentSequenceNumber] = enumertor.currentValue();

            select forupdate taskUpdate where taskUpdate.RecId == taskRecId;

            taskUpdate.SequenceNumber = sequenceNumber;
            taskUpdate.ParentSequenceNumber = parentSequenceNumber;
            taskUpdate.update();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkRecordsCanBeMoved</Name>
				<Source><![CDATA[
    private boolean checkRecordsCanBeMoved(boolean _up)
    {
        if (_up)
        {
            if (this.SequenceNumber > 1)
            {
                return true;
            }
        }
        else
        {
            WHSSeqNum nextSequenceNumber = WHSImplementationTask::findNextSequenceNumber();

            if (this.SequenceNumber < nextSequenceNumber - 1
                    && this.SequenceNumber != 0)
            {
                return true;
            }
        }

        return false;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateChildTasks</Name>
				<Source><![CDATA[
    private void updateChildTasks(WHSSeqNum _existingSequenceNumber, 
        WHSSeqNum _change, 
        Map _recordIdSequenceNumberMap,
        boolean _update)
    {
        WHSImplementationTask task;

        while select forupdate task
            order by SequenceNumber asc
            where task.ParentSequenceNumber == _existingSequenceNumber
        {
            this.updateChildTasks(task.SequenceNumber, _change, _recordIdSequenceNumberMap, _update);

            task.SequenceNumber += _change;
            task.ParentSequenceNumber += _change;

            if (_update) 
            {
                task.update();
            }
            else
            {
                _recordIdSequenceNumberMap.insert(task.RecId, [task.SequenceNumber, task.ParentSequenceNumber]);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>findBySequenceNumber</Name>
				<Source><![CDATA[
    internal static WHSImplementationTask findBySequenceNumber(WHSSeqNum _sequenceNumber)
    {
        WHSImplementationTask task;

        select firstonly task
            where task.SequenceNumber == _sequenceNumber;

        return task;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findBySeqNumAndParentSeqNum</Name>
				<Source><![CDATA[
    internal static WHSImplementationTask findBySeqNumAndParentSeqNum(WHSSeqNum _sequenceNumber, WHSSeqNum _parentSequenceNumber)
    {
        WHSImplementationTask task;

        select firstonly task
            where task.SequenceNumber == _sequenceNumber
                && task.ParentSequenceNumber == _parentSequenceNumber;

        return task;
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    public void insert()
    {
        this.Name = this.convertLabelId2String(this.Name);
        this.Description = this.convertLabelId2String(this.Description);
        this.initSequenceNumbers();

        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>initSequenceNumbers</Name>
				<Source><![CDATA[
    private void initSequenceNumbers()
    {
        if (!this.SequenceNumber)
        {
            this.SequenceNumber = WHSImplementationTask::findNextSequenceNumber();
        }
        else
        {
            WHSSeqNum nextAvailableSequenceNumber = WHSImplementationTask::findNextSequenceNumber();

            if (this.SequenceNumber != nextAvailableSequenceNumber)
            {
                WHSSeqNum oldSequenceNumber = this.SequenceNumber;
                this.SequenceNumber = nextAvailableSequenceNumber;

                if (this.ParentSequenceNumber > 0)
                {
                    this.ParentSequenceNumber = nextAvailableSequenceNumber - (oldSequenceNumber - this.ParentSequenceNumber);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>convertLabelId2String</Name>
				<Source><![CDATA[
    private str convertLabelId2String(str _labelId)
    {
        str labelIdStr = _labelId;

        if (SysLabel::isLabelId(_labelId))
        {
            labelIdStr = SysLabel::labelId2String(_labelId, SystemParameters::getSystemLanguageId());
        }

        return labelIdStr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDelete</Name>
				<Source><![CDATA[
    public boolean validateDelete()
    {
        boolean ret = super();

        if (ret && WHSImplementationTask::existsTaskWithParentSequenceNumber(this.SequenceNumber))
        {
            ret = checkFailed(strFmt("@WAX:WarehouseImplementationTaskDeleteTaskValidation", this.Name));
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>existsTaskWithParentSequenceNumber</Name>
				<Source><![CDATA[
    private static boolean existsTaskWithParentSequenceNumber(WHSSeqNum _sequenceNumber)
    {
        return (select firstonly RecId from whsImplementationTask
                    where whsImplementationTask.ParentSequenceNumber == _sequenceNumber).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>menuItemTypeURL</Name>
				<Source><![CDATA[
    private static int menuItemTypeURL()
    {
        return 255;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>WHSandTMS</ConfigurationKey>
	<Label>@WAX:WarehouseImplementationTasks</Label>
	<SubscriberAccessLevel>
		<Read>Allow</Read>
	</SubscriberAccessLevel>
	<TableGroup>Main</TableGroup>
	<TitleField1>Name</TitleField1>
	<TitleField2>Description</TitleField2>
	<CacheLookup>FoundAndEmpty</CacheLookup>
	<Modules>InventoryAndAdvancedWarehouse</Modules>
	<PrimaryIndex>NameIdx</PrimaryIndex>
	<TableContents>DefaultData</TableContents>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Name</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskName</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Description</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskDescription</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>Status</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<EnumType>WHSImplementationTaskStatus</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldUtcDateTime">
			<Name>CompletedDateTime</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskCompletedDateTime</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt64">
			<Name>CompletedByWorker</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskCompletedByWorker</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>MenuItem</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationAction</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldEnum">
			<Name>MenuItemType</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<EnumType>MenuItemType</EnumType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt">
			<Name>SequenceNumber</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskSeqNumber</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt">
			<Name>ParentSequenceNumber</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>WHSImplementationTaskSeqNumber</ExtendedDataType>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>NameIdx</Name>
			<Fields>
				<AxTableIndexField>
					<DataField>Name</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
		<AxTableIndex>
			<Name>NameSequenceNumberIdx</Name>
			<AllowDuplicates>Yes</AllowDuplicates>
			<Fields>
				<AxTableIndexField>
					<DataField>Name</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>SequenceNumber</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
		<AxTableIndex>
			<Name>SeqNumParentSeqNumIdx</Name>
			<AllowDuplicates>Yes</AllowDuplicates>
			<Fields>
				<AxTableIndexField>
					<DataField>SequenceNumber</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>ParentSequenceNumber</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>HCMWorker</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>HcmWorker</RelatedTable>
			<RelatedTableCardinality>ZeroOne</RelatedTableCardinality>
			<RelatedTableRole>CompletedByWorker</RelatedTableRole>
			<RelationshipType>Association</RelationshipType>
			<Role>CompletedByWorker</Role>
			<UseDefaultRoleNames>No</UseDefaultRoleNames>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>CompletedByWorker</Name>
					<Field>CompletedByWorker</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>