<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>EcoResDateTimeValue</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class EcoResDateTimeValue extends EcoResValue
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>compareTo</Name>
				<Source><![CDATA[
    /// <summary>
    /// Compares this record against the specified record value.
    /// </summary>
    /// <param name="_value">
    /// The value definition that contains the date and time value to which to compare this record.
    /// </param>
    /// <returns>
    /// -1 if the <c>DateTime</c> value in this record is anterior to the value in the given record; 0 if
    /// the <c>DateTime</c> values are identical; 1 if the <c>DateTime</c> value in this record is
    /// posterior than the value in the specified record.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    /// This record and the given record are not comparable as they are not of the same type.
    /// </exception>
    public int compareTo(EcoResValue _value)
    {
        int                 ret;
        EcoResDateTimeValue argValue;
        int64               compareVal;
        int64               zero = 0;

        //validates the type and throws an error if they are not the same
        super(_value);

        argValue = _value as EcoResDateTimeValue;
        argValue = _value as EcoResDateTimeValue;

        compareVal = DateTimeUtil::getDifference(this.DateTimeValue, argValue.DateTimeValue);

        if (compareVal < zero)
        {
            ret = -1;
        }
        else if (compareVal > zero)
        {
            ret = 1;
        }
        else
        {
            ret = 0;
        }
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getStringValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets a string representation of this record that is compatible with the procurement category
    ///    configuration.
    /// </summary>
    /// <param name="_localTimeZone">
    ///    The value definition that contains the preferred time zone.
    /// </param>
    /// <returns>
    ///    The string representation of this record.
    /// </returns>
    public str getStringValue(boolean _localTimeZone = true)
    {
        utcdatetime utcDateTimeVal;

        utcDateTimeVal = this.DateTimeValue;
        if (_localTimeZone == true)
        utcDateTimeVal = DateTimeUtil::applyTimeZoneOffset (this.DateTimeValue, DateTimeUtil::getUserPreferredTimeZone());
        return  datetime2str(utcDateTimeVal,DateFlags::None);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getValue</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets the value of this record.
    /// </summary>
    /// <returns>
    ///    The <c>datetime</c> enumeration value representation of this record.
    /// </returns>
    public anytype getValue()
    {
        return this.DateTimeValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pcDuplicate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Duplicates the record.
    /// </summary>
    /// <returns>
    /// A reference to the duplicated record.
    /// </returns>
    public RefRecId pcDuplicate()
    {
        EcoResDateTimeValue duplicate;

        duplicate.data(this);
        duplicate.Origin = this.RecId;
        duplicate.insert();

        return duplicate.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pcGetStringValue</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the string representation of this record that is compatible with the product configuration.
    /// </summary>
    /// <returns>
    /// The string representation of this record.
    /// </returns>
    public str pcGetStringValue()
    {
        #define.DateSequence(123)
        #define.DayCode(2)
        #define.MonthCode(2)
        #define.YearCode(2)
        #define.Separator1Code(2)
        #define.Separator2Code(2)
        date    dateConverted = DateTimeUtil::date(this.DateTimeValue);
        super();
        return date2str(dateConverted, #DateSequence, #DayCode, #Separator1Code, #MonthCode, #Separator2Code, #YearCode, DateFlags::None );
    }

]]></Source>
			</Method>
			<Method>
				<Name>exist</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Indicates whether the specified record of the <c>EcoResDateTimeValue</c> table exists.
    /// </summary>
    /// <param name="_recId">
    ///    The record ID of the record to check for existence.
    /// </param>
    /// <returns>
    ///    true if the specified record exists; otherwise, false.
    /// </returns>
    public static boolean exist(RefRecId _recId)
    {
        return (EcoResDateTimeValue::find(_recId).RecId != 0);
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Finds the specified record in the <c>EcoResDateTimeValue</c> table.
    /// </summary>
    /// <param name="_recId">
    ///    The record ID of the record to find.
    /// </param>
    /// <param name="_forupdate">
    ///    A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <returns>
    ///    A record in the <c>EcoResDateTimeValue</c> table; otherwise, an empty buffer.
    /// </returns>
    public static EcoResDateTimeValue find(RefRecId _recId, boolean _forupdate = false)
    {
        EcoResDateTimeValue record;

        if (!_recId)
        {
            return record;
        }
        record.selectForUpdate(_forupdate);
        select firstonly record where record.RecId == _recId;
        return record;
    }

]]></Source>
			</Method>
			<Method>
				<Name>readFromHashAndValue</Name>
				<Source><![CDATA[
    public EcoResValue readFromHashAndValue(boolean _forupdate = false)
    {
        EcoResDateTimeValue ecoResDateTimeValue;

        ecoResDateTimeValue.selectForUpdate(_forupdate);
        select firstonly ecoResDateTimeValue 
            where ecoResDateTimeValue.SHA1HashHex       == this.hashValue()
                && ecoResDateTimeValue.DateTimeValue    == this.DateTimeValue;
        
        return ecoResDateTimeValue;
    }

]]></Source>
			</Method>
			<Method>
				<Name>hashKey</Name>
				<Source><![CDATA[
    protected str hashKey()
    {
        return strFmt('%1~', this.DateTimeValue);
    }

]]></Source>
			</Method>
			<Method>
				<Name>shouldThrowExceptionOnZeroDelete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Determines if concurrent deletes should throw exception.
    /// </summary>
    /// <returns>true if exception should be thrown; otherwise false.</returns>
    public boolean shouldThrowExceptionOnZeroDelete()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>valueFieldId</Name>
				<Source><![CDATA[
    public FieldId valueFieldId()
    {
        return fieldNum(EcoResDateTimeValue, DateTimeValue);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LogisticsBasic</ConfigurationKey>
	<DeveloperDocumentation>@SYS341226</DeveloperDocumentation>
	<Label>@SYS131188</Label>
	<TableGroup>Main</TableGroup>
	<TitleField1>DateTimeValue</TitleField1>
	<AllowRowVersionChangeTracking>Yes</AllowRowVersionChangeTracking>
	<CacheLookup>Found</CacheLookup>
	<Extends>EcoResValue</Extends>
	<ModifiedDateTime>Yes</ModifiedDateTime>
	<Modules>ProductInformationManagement</Modules>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<SupportInheritance>Yes</SupportInheritance>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>DateTimeValue</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Default</Name>
			<Label>@SYS26467</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>DateTimeValue</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldUtcDateTime">
			<Name>DateTimeValue</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>AttributeValueDateTime</ExtendedDataType>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes />
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>EcoResValue_DateTime</Name>
			<Cardinality>ZeroOne</Cardinality>
			<RelatedTable>EcoResValue</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Specialization</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>RecId</Name>
					<Field>RecId</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>