<?xml version="1.0" encoding="utf-8"?>
<AxTable xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>BudgetPlanScenarioTranslation</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class BudgetPlanScenarioTranslation extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>delete</Name>
				<Source><![CDATA[
    /// <summary>
    /// Deletes a <c>BudgetPlanScenarioTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to delete a record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void delete(boolean _allowSystemLanguageChange = false)
    {
        if (_allowSystemLanguageChange ||
            this.Name != BudgetPlanScenario::find(this.BudgetPlanScenario).Name)
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>insert</Name>
				<Source><![CDATA[
    /// <summary>
    /// Insert a <c>BudgetPlanScenarioTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to insert a record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void insert(boolean _allowSystemLanguageChange = false)
    {
        if (this.validateWrite(_allowSystemLanguageChange))
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setDefaultLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets a default <c>LanguageId</c> value.
    /// </summary>
    public void setDefaultLanguage()
    {
        this.LanguageId = SystemParameters::getSystemLanguageId();
    }

]]></Source>
			</Method>
			<Method>
				<Name>update</Name>
				<Source><![CDATA[
    /// <summary>
    /// Update a <c>BudgetPlanScenarioTranslation</c> record.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to update a record when the language ID is equal to the
    /// system language ID.
    /// </param>
    public void update(boolean _allowSystemLanguageChange = false)
    {
        if (this.validateWrite(_allowSystemLanguageChange))
        {
            super();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
        public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean isValid = super(_fieldIdToCheck);

        if (isValid)
        {
            if (this.LanguageId == SystemParameters::getSystemLanguageId())
            {
                isValid = checkFailed("@GLS150186");
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    /// <summary>
    /// Validates that the name of a <c>BudgetPlanScenario</c> record is not being used.
    /// </summary>
    /// <param name="_allowSystemLanguageChange">
    /// A Boolean value that indicates whether to write a record when the language ID is equal to the
    /// system language ID.
    /// </param>
    /// <returns>
    /// true if validation passes; Otherwise, false .
    /// </returns>
    public boolean validateWrite(boolean _allowSystemLanguageChange = false)
    {
        BudgetPlanScenario budgetPlanScenario;
        boolean            isValid;

        isValid = super();

        if (isValid)
        {
            if (!_allowSystemLanguageChange &&
                this.LanguageId == SystemParameters::getSystemLanguageId())
            {
                isValid = checkFailed("@GLS150186");
            }
            else
            {
                // Ensure the budget plan scenario name isn't already in use
                select firstonly RecId from budgetPlanScenario
                    where budgetPlanScenario.Name == this.Name &&
                        budgetPlanScenario.RecId != this.BudgetPlanScenario;

                if (budgetPlanScenario)
                {
                    return checkFailed("@GLS150059");
                }
            }
        }

        return isValid;
    }

]]></Source>
			</Method>
			<Method>
				<Name>checkExist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Checks if a record ID exists in <c>BudgetPlanScenarioTranslation</c> table and shows an infolog message if record does not exist.
    /// </summary>
    /// <param name="_budgetPlanScenarioTranslationRecId">
    /// The value of the <c>BudgetPlanScenarioTranslation</c> record ID to check.
    /// </param>
    /// <returns>
    /// true if the record ID exists; otherwise, false.
    /// </returns>
    public static boolean checkExist(BudgetPlanScenarioTranslationRecId _budgetPlanScenarioTranslationRecId)
    {
        boolean isExisting = true;

        if (!BudgetPlanScenarioTranslation::exist(_budgetPlanScenarioTranslationRecId))
        {
            isExisting = checkFailed(strfmt(BudgetPlanScenarioTranslation::txtNotExist(), tableId2Name(tablenum(BudgetPlanScenarioTranslation))));
        }

        return isExisting;
    }

]]></Source>
			</Method>
			<Method>
				<Name>createOrUpdateTranslation</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates or updates a budget plan scenario translation record.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The record ID of a <c>BudgetPlanScenario</c> record.
    /// </param>
    /// <param name="_name">
    /// A specified budget plan scenario name value.
    /// </param>
    /// <param name="_description">
    /// A specified budget plan scenario description value.
    /// </param>
    /// <param name="_languageId">
    /// A specified <c>LanguageId</c> value.
    /// </param>
    public static void createOrUpdateTranslation(
        BudgetPlanScenarioRecId       _budgetPlanScenarioRecId,
        Name                          _name,
        BudgetPlanScenarioDescription _description = '',
        LanguageId                    _languageId = '')
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;
        LanguageId                    languageId = _languageId;

        if (!_languageId)
        {
            languageId = SystemParameters::systemLanguageId();
        }

        // If system language found then create or update BudgetPlanScenarioTranslation
        if (languageId)
        {
            ttsbegin;

            budgetPlanScenarioTranslation = BudgetPlanScenarioTranslation::
                findByBudgetPlanScenarioLanguage(_budgetPlanScenarioRecId, languageId, true);

            if (budgetPlanScenarioTranslation)
            {
                budgetPlanScenarioTranslation.Name        = _name;
                budgetPlanScenarioTranslation.Description = _description;
                budgetPlanScenarioTranslation.update(true);
            }
            else
            {
                budgetPlanScenarioTranslation.BudgetPlanScenario = _budgetPlanScenarioRecId;
                budgetPlanScenarioTranslation.Name               = _name;
                budgetPlanScenarioTranslation.Description        = _description;
                budgetPlanScenarioTranslation.LanguageId         = languageId;
                budgetPlanScenarioTranslation.insert(true);
            }

            ttscommit;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteForBudgetPlanScenario</Name>
				<Source><![CDATA[
    /// <summary>
    /// Delete all the <c>BudgetPlanScenarioTranslation</c> records that are associated with a
    /// <c>BudgetPlanScenario</c> record.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The record ID of <c>BudgetPlanScenario</c> record.
    /// </param>
    public static void deleteForBudgetPlanScenario(BudgetPlanScenarioRecId _budgetPlanScenarioRecId)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        budgetPlanScenarioTranslation.skipDeleteMethod(true);
        budgetPlanScenarioTranslation.skipDeleteActions(true);

        delete_from budgetPlanScenarioTranslation
            where budgetPlanScenarioTranslation.BudgetPlanScenario == _budgetPlanScenarioRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>exist</Name>
				<Source><![CDATA[
    /// <summary>
    /// Indicates whether a record ID exists in the <c>BudgetPlanScenarioTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanScenarioTranslationRecId">
    /// The value of a <c>BudgetPlanScenarioTranslation</c> record ID to check.
    /// </param>
    /// <returns>
    /// true if the record ID exists; otherwise, false .
    /// </returns>
    public static boolean exist(BudgetPlanScenarioTranslationRecId _budgetPlanScenarioTranslationRecId)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        return _budgetPlanScenarioTranslationRecId && (select firstonly RecId from budgetPlanScenarioTranslation
                    where budgetPlanScenarioTranslation.RecId == _budgetPlanScenarioTranslationRecId).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>find</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanScenarioTranslation</c> table that is based on a record ID value.
    /// </summary>
    /// <param name="_budgetPlanScenarioTranslationRecId">
    /// The value of the record ID to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that specifies whether to read the record for update; optional.
    /// </param>
    /// <param name="_concurrencyModel">
    /// An enumeration value that indicates the concurrency settings to use on the table; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanScenarioTranslation</c> record.
    /// </returns>
    public static BudgetPlanScenarioTranslation find(
        BudgetPlanScenarioTranslationRecId _budgetPlanScenarioTranslationRecId,
        boolean                            _forUpdate = false,
        ConcurrencyModel                   _concurrencyModel = ConcurrencyModel::Auto)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        if (_budgetPlanScenarioTranslationRecId)
        {
            budgetPlanScenarioTranslation.selectForUpdate(_forUpdate );

            if (_forUpdate && _concurrencyModel != ConcurrencyModel::Auto)
            {
                budgetPlanScenarioTranslation.concurrencyModel(_concurrencyModel);
            }

            select firstonly budgetPlanScenarioTranslation
                where budgetPlanScenarioTranslation.RecId == _budgetPlanScenarioTranslationRecId;
        }

        return budgetPlanScenarioTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByBudgetPlanScenarioLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanScenarioTranslation</c> table that is based on the specified
    /// <c>BudgetPlanScenario</c> and <c>LanguageId</c> values.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The value of the <c>BudgetPlanScenario</c> field to find.
    /// </param>
    /// <param name="_languageId">
    /// The value of the <c>LanguageId</c> field to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that specifies whether to read the record for update; optional.
    /// </param>
    /// <param name="_concurrencyModel">
    /// An enumeration value that indicates the concurrency settings to use on the table; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanScenarioTranslation</c> record.
    /// </returns>
    public static BudgetPlanScenarioTranslation findByBudgetPlanScenarioLanguage(
        BudgetPlanScenarioRecId _budgetPlanScenarioRecId,
        LanguageIdAll           _languageId,
        boolean                 _forUpdate = false,
        ConcurrencyModel        _concurrencyModel = ConcurrencyModel::Auto)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        if (_budgetPlanScenarioRecId && _languageId)
        {
            budgetPlanScenarioTranslation.selectForUpdate(_forUpdate);

            if (_forUpdate && _concurrencyModel != ConcurrencyModel::Auto)
            {
                budgetPlanScenarioTranslation.concurrencyModel(_concurrencyModel);
            }

            select firstonly budgetPlanScenarioTranslation
                where BudgetPlanScenarioTranslation.BudgetPlanScenario == _budgetPlanScenarioRecId &&
                        BudgetPlanScenarioTranslation.LanguageId == _languageId;
        }

        return budgetPlanScenarioTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByLocalizedName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a <c>BudgetPlanScenarioTranslation</c> record for a specified budget plan name.
    /// </summary>
    /// <param name="_budgetPlanScenarioName">
    /// The value of the <c>Name</c> field to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanScenarioTranslation</c> record.
    /// </returns>
    public static BudgetPlanScenarioTranslation findByLocalizedName(
        BudgetPlanScenarioName _budgetPlanScenarioName,
        boolean                _forUpdate = false)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;
        BudgetPlanScenario            budgetPlanScenario;

        budgetPlanScenarioTranslation = BudgetPlanScenarioTranslation::findByNameLanguage(_budgetPlanScenarioName, _forUpdate);

        if (!budgetPlanScenarioTranslation)
        {
            budgetPlanScenarioTranslation = BudgetPlanScenarioTranslation::findByNameLanguage(
                                                        _budgetPlanScenarioName,
                                                        _forUpdate,
                                                        SystemParameters::getSystemLanguageId());
        }

        // Fall back to the original name.
        if (!budgetPlanScenarioTranslation)
        {
            budgetPlanScenario = BudgetPlanScenario::findByName(_budgetPlanScenarioName);

            budgetPlanScenarioTranslation.Name = budgetPlanScenario.Name;
            budgetPlanScenarioTranslation.BudgetPlanScenario = budgetPlanScenario.RecId;
            budgetPlanScenarioTranslation.selectForUpdate(_forUpdate);
        }

        return budgetPlanScenarioTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findByNameLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds the <c>BudgetPlanScenarioTranslation</c> record for a specified budget plan name and language.
    /// </summary>
    /// <param name="_budgetPlanScenarioName">
    /// The value of the <c>Name</c> field to find.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to read the record for update; optional.
    /// </param>
    /// <param name="_languageId">
    /// The language of the <c>BudgetPlanScenarioTranslation</c> record; optional.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanScenarioTranslation</c> record.
    /// </returns>
    public static BudgetPlanScenarioTranslation findByNameLanguage(
            BudgetPlanScenarioName _budgetPlanScenarioName,
            boolean                _forUpdate = false,
            LanguageId             _languageId = currentUserLanguage())
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        if (_budgetPlanScenarioName)
        {
            BudgetPlanScenarioTranslation.selectForUpdate(_forUpdate);

            select firstOnly Name, BudgetPlanScenario from budgetPlanScenarioTranslation
                where budgetPlanScenarioTranslation.Name == _budgetPlanScenarioName
                &&    budgetPlanScenarioTranslation.LanguageId == _languageId;
        }

        return budgetPlanScenarioTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>findScenarioTranslationByLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a record in the <c>BudgetPlanScenarioTranslation</c> table that is based on the specified
    /// <c>BudgetPlanScenario</c> and <c>LanguageId</c> values.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The ID of a budget plan scenario for which a name is retrieved.
    /// </param>
    /// <param name="_languageId">
    /// The ID of a language in which a name is retrieved.
    /// </param>
    /// <param name="_translationFieldId">
    /// The field ID of localized field in which retrieved.
    /// </param>
    /// <returns>
    /// A <c>BudgetPlanScenarioTranslation</c> record.
    /// </returns>
    public static BudgetPlanScenarioTranslation findScenarioTranslationByLanguage(
        BudgetPlanScenarioRecId _budgetPlanScenarioRecId,
        LanguageId              _languageId,
        fieldId                 _translationFieldId)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;
        LanguageId                    systemLanguageId;

        if (_budgetPlanScenarioRecId)
        {
            budgetPlanscenarioTranslation = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                _budgetPlanScenarioRecId,
                _languageId);

            if (!budgetPlanscenarioTranslation.RecId || !budgetPlanscenarioTranslation.(_translationFieldId))
            {
                // no translation record or no value provided - fallback to system language
                systemLanguageId = SystemParameters::getSystemLanguageId();

                if (systemLanguageId != _languageId)
                {
                    budgetPlanscenarioTranslation = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                        _budgetPlanScenarioRecId,
                        systemLanguageId);
                }
            }
        }

        return budgetPlanscenarioTranslation;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLocalizedDescription</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Description</c> field of the <c>BudgetPlanScenarioTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The ID of a <c>BudgetPlanScenario</c> record for which a description is retrieved.
    /// </param>
    /// <returns>
    /// The localized description of a budget plan scenario.
    /// </returns>
    public static BudgetPlanScenarioDescription getLocalizedDescription(BudgetPlanScenarioRecId _budgetPlanScenarioRecId)
    {
        BudgetPlanScenarioDescription budgetPlanScenarioDescription;

        budgetPlanScenarioDescription = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                                                _budgetPlanScenarioRecId,
                                                currentUserLanguage()
                                                ).Description;
        if (!budgetPlanScenarioDescription)
        {
            budgetPlanScenarioDescription = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                                                    _budgetPlanScenarioRecId,
                                                    SystemParameters::getSystemLanguageId()
                                                    ).Description;
        }

        // Fall back to Description if no localized description found
        if (!budgetPlanScenarioDescription)
        {
            budgetPlanScenarioDescription = BudgetPlanScenario::find(_budgetPlanScenarioRecId).Description;
        }

        return budgetPlanScenarioDescription;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLocalizedName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Name</c> field of the <c>BudgetPlanScenarioTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The ID of a budget plan scenario for which a name is retrieved.
    /// </param>
    /// <returns>
    /// The localized name of a budget plan scenario.
    /// </returns>
    public static BudgetPlanScenarioName getLocalizedName(BudgetPlanScenarioRecId _budgetPlanScenarioRecId)
    {
        BudgetPlanScenarioName budgetPlanScenarioName;

        budgetPlanScenarioName = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                                                _budgetPlanScenarioRecId,
                                                currentUserLanguage()
                                                ).Name;
        if (!budgetPlanScenarioName)
        {
            budgetPlanScenarioName = BudgetPlanScenarioTranslation::findByBudgetPlanScenarioLanguage(
                                                    _budgetPlanScenarioRecId,
                                                    SystemParameters::getSystemLanguageId()
                                                    ).Name;
        }

        // Fall back to Name if no localized name found
        if (!budgetPlanScenarioName)
        {
            budgetPlanScenarioName = BudgetPlanScenario::find(_budgetPlanScenarioRecId).Name;
        }

        return budgetPlanScenarioName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getName</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the <c>Name</c> field of the <c>BudgetPlanScenarioTranslation</c> table.
    /// </summary>
    /// <param name="_budgetPlanScenarioRecId">
    /// The ID of a budget plan scenario for which a name is retrieved.
    /// </param>
    /// <param name="_languageId">
    /// The ID of a language in which a name is retrieved.
    /// </param>
    /// <returns>
    /// The name of a budget plan scenario.
    /// </returns>
    public static Name getName(
        BudgetPlanScenarioRecId _budgetPlanScenarioRecId,
        LanguageId              _languageId)
    {
        BudgetPlanScenarioTranslation budgetPlanScenarioTranslation;

        budgetPlanScenarioTranslation = BudgetPlanScenarioTranslation::findScenarioTranslationByLanguage(
            _budgetPlanScenarioRecId,
            _languageId,
            fieldNum(BudgetPlanScenarioTranslation, Name)
            );

        return budgetPlanScenarioTranslation.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryAddDefaultLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a default language ID filter for a specified query.
    /// </summary>
    /// <param name="_query">
    /// The query that is extended to include the default language ID filter.
    /// </param>
    /// <param name="_dataSourceName">
    /// The name of the specified data source for which to add the range.
    /// </param>
    /// <exception cref="M:Exception::Error">
    /// The query does not contain the specified data source or <c>BudgetPlanScenario</c> data source.
    /// </exception>
    public static void queryAddDefaultLanguage(Query _query, str _dataSourceName = '')
    {
        QueryBuildDataSource qbdsBudgetPlanScenarioTranslation;

        if (_dataSourceName)
        {
            qbdsBudgetPlanScenarioTranslation = _query.dataSourceName(_dataSourceName);
        }
        else
        {
            qbdsBudgetPlanScenarioTranslation = _query.dataSourceTable(tableNum(BudgetPlanScenarioTranslation));
        }

        if (!qbdsBudgetPlanScenarioTranslation)
        {
            throw error(Error::wrongUseOfFunction(tableStaticMethodStr(BudgetPlanScenarioTranslation, queryAddDefaultLanguage)));
        }

        qbdsBudgetPlanScenarioTranslation.addRange(fieldNum(BudgetPlanScenarioTranslation, LanguageId))
            .value(queryValue(SystemParameters::getSystemLanguageId()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>queryAddUserLanguage</Name>
				<Source><![CDATA[
    /// <summary>
    /// Adds a user language ID filter for a specified query.
    /// </summary>
    /// <param name="_query">
    /// The query that is extended to include the company language ID filter.
    /// </param>
    /// <param name="_dataSourceName">
    /// The name of the specified data source for which to add the range.
    /// </param>
    /// <exception cref="M:Exception::Error">
    /// The query does not contain the specified data source or <c>BudgetPlanScenario</c> data source.
    /// </exception>
    public static void queryAddUserLanguage(Query _query, str _dataSourceName = '')
    {
        QueryBuildDataSource qbdsBudgetPlanScenarioTranslation;

        if (_dataSourceName)
        {
            qbdsBudgetPlanScenarioTranslation = _query.dataSourceName(_dataSourceName);
        }
        else
        {
            qbdsBudgetPlanScenarioTranslation = _query.dataSourceTable(tableNum(BudgetPlanScenarioTranslation));
        }

        if (!qbdsBudgetPlanScenarioTranslation)
        {
            throw error(Error::wrongUseOfFunction(tableStaticMethodStr(BudgetPlanScenarioTranslation, queryAddDefaultLanguage)));
        }

        qbdsBudgetPlanScenarioTranslation.addRange(fieldNum(BudgetPlanScenarioTranslation, LanguageId))
            .value(queryValue(currentUserLanguage()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtNotExist</Name>
				<Source><![CDATA[
    public static TxtNotExist txtNotExist()
    {
        return "@SYS321009";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LedgerAdv2BudgetPlanning</ConfigurationKey>
	<DeveloperDocumentation>@GLS150042</DeveloperDocumentation>
	<Label>@GLS150040</Label>
	<SingularLabel>@GLS150041</SingularLabel>
	<TableGroup>Group</TableGroup>
	<TitleField1>BudgetPlanScenario</TitleField1>
	<TitleField2>LanguageId</TitleField2>
	<CacheLookup>FoundAndEmpty</CacheLookup>
	<ClusteredIndex>BudgetPlanScenarioTranslationIdx</ClusteredIndex>
	<Modules>Budget</Modules>
	<ReplacementKey>BudgetPlanScenarioTranslationIdx</ReplacementKey>
	<SaveDataPerCompany>No</SaveDataPerCompany>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanScenario</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanScenario</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanScenario</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>General</Name>
			<Label>@SYS312922</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>Name</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>Description</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>Identification</Name>
			<Label>@SYS5711</Label>
			<Fields>
				<AxTableFieldGroupField>
					<DataField>BudgetPlanScenario</DataField>
				</AxTableFieldGroupField>
				<AxTableFieldGroupField>
					<DataField>LanguageId</DataField>
				</AxTableFieldGroupField>
			</Fields>
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxTableField xmlns=""
			i:type="AxTableFieldInt64">
			<Name>BudgetPlanScenario</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanScenarioRecId</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Description</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanScenarioDescription</ExtendedDataType>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>LanguageId</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>LanguageIdAll</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
		<AxTableField xmlns=""
			i:type="AxTableFieldString">
			<Name>Name</Name>
			<AssetClassification>Customer Content</AssetClassification>
			<ExtendedDataType>BudgetPlanScenarioName</ExtendedDataType>
			<Mandatory>Yes</Mandatory>
		</AxTableField>
	</Fields>
	<FullTextIndexes />
	<Indexes>
		<AxTableIndex>
			<Name>BudgetPlanScenarioTranslationIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>BudgetPlanScenario</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>LanguageId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
		<AxTableIndex>
			<Name>NameLanguageIdIdx</Name>
			<AlternateKey>Yes</AlternateKey>
			<Fields>
				<AxTableIndexField>
					<DataField>Name</DataField>
				</AxTableIndexField>
				<AxTableIndexField>
					<DataField>LanguageId</DataField>
				</AxTableIndexField>
			</Fields>
		</AxTableIndex>
	</Indexes>
	<Mappings />
	<Relations>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>BudgetPlanScenario</Name>
			<Cardinality>OneMore</Cardinality>
			<OnDelete>Cascade</OnDelete>
			<RelatedTable>BudgetPlanScenario</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Aggregation</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>BudgetPlanScenario</Name>
					<Field>BudgetPlanScenario</Field>
					<RelatedField>RecId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>RecId</Index>
		</AxTableRelation>
		<AxTableRelation xmlns=""
			i:type="AxTableRelationForeignKey">
			<Name>LanguageTable</Name>
			<Cardinality>ZeroMore</Cardinality>
			<RelatedTable>LanguageTable</RelatedTable>
			<RelatedTableCardinality>ExactlyOne</RelatedTableCardinality>
			<RelationshipType>Association</RelationshipType>
			<Constraints>
				<AxTableRelationConstraint xmlns=""
					i:type="AxTableRelationConstraintField">
					<Name>LanguageId</Name>
					<SourceEDT>LanguageIdAll</SourceEDT>
					<Field>LanguageId</Field>
					<RelatedField>LanguageId</RelatedField>
				</AxTableRelationConstraint>
			</Constraints>
			<Index>Key</Index>
		</AxTableRelation>
	</Relations>
	<StateMachines />
</AxTable>