<?xml version="1.0" encoding="utf-8"?>
<AxDataEntityView xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProcurementDocumentSortingOrderEntity</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class ProcurementDocumentSortingOrderEntity extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>documentNameComputedColumnDefinition</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static str documentNameComputedColumnDefinition()
    {
        Map comparisonExpressions = SysComputedColumn::comparisionExpressionMap();
        comparisonExpressions.insert(
                SysComputedColumn::comparisonLiteral(tableNum(VendPurchOrderJour)),
                SysComputedColumn::returnLiteral(tableStr(VendPurchOrderJour)));
        comparisonExpressions.insert(
                SysComputedColumn::comparisonLiteral(tableNum(VendReceiptsListJour)),
                SysComputedColumn::returnLiteral(tableStr(VendReceiptsListJour)));
        comparisonExpressions.insert(
                SysComputedColumn::comparisonLiteral(tableNum(VendPackingSlipJour)),
                SysComputedColumn::returnLiteral(tableStr(VendPackingSlipJour)));
        comparisonExpressions.insert(
                SysComputedColumn::comparisonLiteral(tableNum(VendInvoiceJour)),
                SysComputedColumn::returnLiteral(tableStr(VendInvoiceJour)));

        return SysComputedColumn::switch(
                SysComputedColumn::comparisonField(tableStr(ProcurementDocumentSortingOrderEntity), dataEntityDataSourceStr(ProcurementDocumentSortingOrderEntity, FormLetterSortingParameters), fieldStr(FormLetterSortingParameters, SortingTableId)),
                comparisonExpressions,
                SysComputedColumn::returnLiteral(''));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldNameComputedColumnDefinition</Name>
				<Source><![CDATA[
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static str fieldNameComputedColumnDefinition()
    {
        Map fieldsVendPackingSlipJour =  FormLetterSortingParametersEntityHelper::generateComparisonMapForFieldIds(tablenum(VendPackingSlipJour));
        Map fieldsVendPurchOrderJour = FormLetterSortingParametersEntityHelper::generateComparisonMapForFieldIds(tablenum(VendPurchOrderJour));
        Map fieldsVendReceiptsListJour = FormLetterSortingParametersEntityHelper::generateComparisonMapForFieldIds(tablenum(VendReceiptsListJour));
        Map fieldsVendInvoiceJour = FormLetterSortingParametersEntityHelper::generateComparisonMapForFieldIds(tablenum(VendInvoiceJour));

        str sortingTableId_FormLetterSortingParametersEntity = SysComputedColumn::comparisonField(tableStr(ProcurementDocumentSortingOrderEntity), dataEntityDataSourceStr(ProcurementDocumentSortingOrderEntity, FormLetterSortingParameters), fieldStr(FormLetterSortingParameters, SortingTableId));
        str fieldId_FormLetterSortingParametersEntity = SysComputedColumn::comparisonField(tableStr(ProcurementDocumentSortingOrderEntity), dataEntityDataSourceStr(ProcurementDocumentSortingOrderEntity, FormLetterSortingParameters), fieldStr(FormLetterSortingParameters, fieldId));

        return
            SysComputedColumn::if(
                SysComputedColumn::equalExpression(
                    sortingTableId_FormLetterSortingParametersEntity,
                    SysComputedColumn::comparisonLiteral(tableNum(VendPackingSlipJour))
                ),
                SysComputedColumn::switch(
                    fieldId_FormLetterSortingParametersEntity,
                    fieldsVendPackingSlipJour,
                    SysComputedColumn::returnLiteral('')
                ),
                SysComputedColumn::if(
                    SysComputedColumn::equalExpression(
                        sortingTableId_FormLetterSortingParametersEntity,
                        SysComputedColumn::comparisonLiteral(tableNum(VendPurchOrderJour))
                    ),
                    SysComputedColumn::switch(
                        fieldId_FormLetterSortingParametersEntity,
                        fieldsVendPurchOrderJour,
                        SysComputedColumn::returnLiteral('')
                    ),
                    SysComputedColumn::if(
                        SysComputedColumn::equalExpression(
                            sortingTableId_FormLetterSortingParametersEntity,
                            SysComputedColumn::comparisonLiteral(tableNum(VendReceiptsListJour))
                        ),
                        SysComputedColumn::switch(
                            fieldId_FormLetterSortingParametersEntity,
                            fieldsVendReceiptsListJour,
                            SysComputedColumn::returnLiteral('')
                        ),
                        SysComputedColumn::if(
                            SysComputedColumn::equalExpression(
                                sortingTableId_FormLetterSortingParametersEntity,
                                SysComputedColumn::comparisonLiteral(tableNum(VendInvoiceJour))
                            ),
                            SysComputedColumn::switch(
                                fieldId_FormLetterSortingParametersEntity,
                                fieldsVendInvoiceJour,
                                SysComputedColumn::returnLiteral('')
                            ),
                            SysComputedColumn::returnLiteral('')
                        )
                    )
                )
            );
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExtension</Name>
				<Source><![CDATA[
    public TableExtension getExtension()
    {
        return SysTableExtension::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDefaultingDependencies</Name>
				<Source><![CDATA[
    public container getDefaultingDependencies()
    {
        return [
                [fieldNum(ProcurementDocumentSortingOrderEntity, SortingTableId),
                    [fieldNum(ProcurementDocumentSortingOrderEntity, DocumentName)]],
                [fieldNum(ProcurementDocumentSortingOrderEntity, FieldId),
                    [fieldNum(ProcurementDocumentSortingOrderEntity, SortingTableId), fieldNum(ProcurementDocumentSortingOrderEntity, FieldName)]]
        ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultField</Name>
				<Source><![CDATA[
    public void defaultField(FieldId _fieldId)
    {
        super (_fieldId);

        switch (_fieldId)
        {
            case fieldNum(ProcurementDocumentSortingOrderEntity, SortingTableId):
                this.defaultTableId();
                break;

            case fieldNum(ProcurementDocumentSortingOrderEntity, FieldId):
                this.defaultFieldId();
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultTableId</Name>
				<Source><![CDATA[
    private void defaultTableId()
    {
        if (this.DocumentName)
        {
            if (this.isFieldSet(fieldNum(ProcurementDocumentSortingOrderEntity, DocumentName)))
            {
                this.SortingTableId = tableName2Id(this.DocumentName);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultFieldId</Name>
				<Source><![CDATA[
    private void defaultFieldId()
    {
        if (this.SortingTableId && this.FieldName)
        {
            if (this.isFieldSet(fieldNum(ProcurementDocumentSortingOrderEntity, SortingTableId))
                    && this.isFieldSet(fieldNum(ProcurementDocumentSortingOrderEntity, FieldName)))
            {
                // the logic in the table for this entity expects the fieldId to be array-based
                const int firstArrayIdx = 1;
                FieldId resolvedFieldId = fieldName2Id(this.SortingTableId, this.FieldName);
                this.FieldId = new SysDictField(this.SortingTableId, resolvedFieldId, firstArrayIdx).id();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean ret = super(_fieldIdToCheck);

        switch (_fieldIdToCheck)
        {
            case fieldNum(ProcurementDocumentSortingOrderEntity, DocumentName):
                ret = this.validateDocumentName() && ret;
                break;
            case fieldNum(ProcurementDocumentSortingOrderEntity, FieldName):
                ret = this.validateFieldName() && ret;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDocumentName</Name>
				<Source><![CDATA[
    private boolean validateDocumentName()
    {
        switch (tableName2Id(this.DocumentName))
        {
            case tableNum(VendPackingSlipJour):
            case tableNum(VendPurchOrderJour):
            case tableNum(VendReceiptsListJour):
            case tableNum(VendInvoiceJour):
                return true;
            default:
                return checkFailed("@SYS16078");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateFieldName</Name>
				<Source><![CDATA[
    private boolean validateFieldName()
    {
        TableId tableId = tableName2Id(this.DocumentName);
        FieldId fieldId = fieldName2Id(tableId,this.FieldName);

        if (!fieldId)
        {
            return checkFailed("@SCM:InvalidFieldTableName");
        }

        if (!FormLetterSortingParameters::isFieldVisible(tableId,fieldId))
        {
            return checkFailed("@SCM:FieldNotVisible");
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>jumpRefMethod</Name>
				<Source><![CDATA[
    /// <summary>
    /// Jump ref method for target record
    /// </summary>
    [Microsoft.Dynamics.BusinessPlatform.SharedTypes.InternalUseOnlyAttribute]
    public static void jumpRefMethod(str _menuItemName, Common _stagingBuffer, Object _callerObj, DataAreaId _dataAreaId)
    {
        changeCompany(_dataAreaId)
        {
            ProcurementDocumentSortingOrderStaging stagingBuffer = _stagingBuffer;

            Args args = new Args();
            if (stagingBuffer.DocumentName == tableStr(VendInvoiceJour))
            {
                MenuFunction::runCalled(menuItemDisplayStr(VendFormLetterSortingParameters), MenuItemType::Display, false, args);
            }
            else
            {
                MenuFunction::runCalled(menuItemDisplayStr(PurchFormLetterSortingParameters), MenuItemType::Display, false, args);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LedgerBasic</ConfigurationKey>
	<DeveloperDocumentation>@SCM:ProcurementDocumentSortingOrderEntityDevDoc</DeveloperDocumentation>
	<FormRef>PurchFormLetterSortingParameters</FormRef>
	<Label>@SCM:ProcurementFormSorting</Label>
	<Tags>Setup</Tags>
	<DataManagementEnabled>Yes</DataManagementEnabled>
	<DataManagementStagingTable>ProcurementDocumentSortingOrderStaging</DataManagementStagingTable>
	<Modules>ProcurementAndSourcing</Modules>
	<PrimaryCompanyContext>DataAreaId</PrimaryCompanyContext>
	<PrimaryKey>EntityKey</PrimaryKey>
	<SupportsSetBasedSqlOperations>Yes</SupportsSetBasedSqlOperations>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewMappedField">
			<Name>FieldId</Name>
			<AccessModifier>Private</AccessModifier>
			<DataField>FieldId</DataField>
			<DataSource>FormLetterSortingParameters</DataSource>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewMappedField">
			<Name>SortingTableId</Name>
			<AccessModifier>Private</AccessModifier>
			<DataField>SortingTableId</DataField>
			<DataSource>FormLetterSortingParameters</DataSource>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewUnmappedFieldString">
			<Name>FieldName</Name>
			<AllowEdit>Yes</AllowEdit>
			<AllowEditOnCreate>Yes</AllowEditOnCreate>
			<Mandatory>No</Mandatory>
			<ComputedFieldMethod>fieldNameComputedColumnDefinition</ComputedFieldMethod>
			<ExtendedDataType>FieldName</ExtendedDataType>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewUnmappedFieldString">
			<Name>DocumentName</Name>
			<AllowEdit>Yes</AllowEdit>
			<AllowEditOnCreate>Yes</AllowEditOnCreate>
			<Mandatory>No</Mandatory>
			<ComputedFieldMethod>documentNameComputedColumnDefinition</ComputedFieldMethod>
			<ExtendedDataType>TableName</ExtendedDataType>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewMappedField">
			<Name>Position</Name>
			<AllowEdit>Yes</AllowEdit>
			<AllowEditOnCreate>Yes</AllowEditOnCreate>
			<DataField>Position</DataField>
			<DataSource>FormLetterSortingParameters</DataSource>
		</AxDataEntityViewField>
	</Fields>
	<Keys>
		<AxDataEntityViewKey>
			<Name>EntityKey</Name>
			<Fields>
				<AxDataEntityViewKeyField>
					<DataField>FieldName</DataField>
				</AxDataEntityViewKeyField>
				<AxDataEntityViewKeyField>
					<DataField>DocumentName</DataField>
				</AxDataEntityViewKeyField>
			</Fields>
		</AxDataEntityViewKey>
	</Keys>
	<Mappings />
	<Ranges />
	<Relations />
	<StateMachines />
	<ViewMetadata>
		<Name>Metadata</Name>
		<SourceCode>
			<Methods>
				<Method>
					<Name>classDeclaration</Name>
					<Source><![CDATA[
[Query]
public class Metadata extends QueryRun
{
}
]]></Source>
				</Method>
			</Methods>
		</SourceCode>
		<DataSources>
			<AxQuerySimpleRootDataSource>
				<Name>FormLetterSortingParameters</Name>
				<DynamicFields>Yes</DynamicFields>
				<Table>FormLetterSortingParameters</Table>
				<DataSources />
				<DerivedDataSources />
				<Fields />
				<Ranges>
					<AxQuerySimpleDataSourceRange>
						<Name>SortingTableId</Name>
						<Field>SortingTableId</Field>
						<Value>(FormLetterSortingParametersEntityHelper::procurementTableRangeValues())</Value>
					</AxQuerySimpleDataSourceRange>
				</Ranges>
				<GroupBy />
				<Having />
				<OrderBy />
			</AxQuerySimpleRootDataSource>
		</DataSources>
	</ViewMetadata>
</AxDataEntityView>