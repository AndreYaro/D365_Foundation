<?xml version="1.0" encoding="utf-8"?>
<AxDataEntityView xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PurchaseOrderSummaryUpdateCriteriaEntity</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class PurchaseOrderSummaryUpdateCriteriaEntity extends common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>tableNameComputedColumnDefinition</Name>
				<Source><![CDATA[
    private static str tableNameComputedColumnDefinition()
    {
        str internalTableIdExpression = SysComputedColumn::logicalShiftRight(
            tableStr(PurchaseOrderSummaryUpdateCriteriaEntity),
            dataEntityDataSourceStr(PurchaseOrderSummaryUpdateCriteriaEntity, PurchSummaryParameters),
            fieldStr(PurchSummaryParameters, FieldId),
            24);

        Map comparisonExpressionMap = SysComputedColumn::comparisionExpressionMap();

        comparisonExpressionMap.insert(
            SysComputedColumn::comparisonLiteral(PurchTable::tableIdToInternalTableId_W(tableNum(PurchTable))),
            SysComputedColumn::returnLiteral(tableStr(PurchTable)));
        comparisonExpressionMap.insert(
            SysComputedColumn::comparisonLiteral(PurchTable::tableIdToInternalTableId_W(tableNum(PurchTable_BR))),
            SysComputedColumn::returnLiteral(tableStr(PurchTable_BR)));
        comparisonExpressionMap.insert(
            SysComputedColumn::comparisonLiteral(PurchTable::tableIdToInternalTableId_W(tableNum(PurchTable_RU))),
            SysComputedColumn::returnLiteral(tableStr(PurchTable_RU)));
        comparisonExpressionMap.insert(
            SysComputedColumn::comparisonLiteral(PurchTable::tableIdToInternalTableId_W(tableNum(PurchTable_W))),
            SysComputedColumn::returnLiteral(tableStr(PurchTable_W)));

        return SysComputedColumn::switch(
            internalTableIdExpression,
            comparisonExpressionMap,
            SysComputedColumn::returnLiteral(''));
    }

]]></Source>
			</Method>
			<Method>
				<Name>fieldNameComputedColumnDefinition</Name>
				<Source><![CDATA[
    private static str fieldNameComputedColumnDefinition()
    {
        str fieldIdExpression = SysComputedColumn::applyBitmask(
            tableStr(PurchaseOrderSummaryUpdateCriteriaEntity),
            dataEntityDataSourceStr(PurchaseOrderSummaryUpdateCriteriaEntity, PurchSummaryParameters),
            fieldStr(PurchSummaryParameters, FieldId),
            24);

        Map comparisonExpressionMap = SysComputedColumn::comparisionExpressionMap();

        SalesPurchaseOrderSummaryUpdateCriteriaEntityHelper::AddGroupFieldsToComparisonMap(
            comparisonExpressionMap, tableNum(PurchTable), tablefieldgroupstr(PurchTable, SummaryUpdate));
        SalesPurchaseOrderSummaryUpdateCriteriaEntityHelper::AddGroupFieldsToComparisonMap(
            comparisonExpressionMap, tableNum(PurchTable_BR), tablefieldgroupstr(PurchTable_BR, SummaryUpdate));
        SalesPurchaseOrderSummaryUpdateCriteriaEntityHelper::AddGroupFieldsToComparisonMap(
            comparisonExpressionMap, tableNum(PurchTable_RU), tablefieldgroupstr(PurchTable_RU, SummaryUpdate));
        SalesPurchaseOrderSummaryUpdateCriteriaEntityHelper::AddGroupFieldsToComparisonMap(
            comparisonExpressionMap, tableNum(PurchTable_W), tablefieldgroupstr(PurchTable_W, SummaryUpdate));

        return SysComputedColumn::switch(
            fieldIdExpression,
            comparisonExpressionMap,
            SysComputedColumn::returnLiteral(''));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getExtension</Name>
				<Source><![CDATA[
    public TableExtension getExtension()
    {
        return SysTableExtension::construct();
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultinternalFieldId</Name>
				<Source><![CDATA[
    private void defaultinternalFieldId()
    {
        if (this.TableName && this.FieldName)
        {
            if (this.isFieldSet(fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, TableName))
                && this.isFieldSet(fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, FieldName)))
            {
                TableId normalTableId = tableName2Id(this.TableName);
                FieldId normalFieldId = fieldName2Id(normalTableId, this.FieldName);

                this.InternalFieldId = PurchTable::fieldIdToInternalFieldId_W(normalFieldId, normalTableId);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getDefaultingDependencies</Name>
				<Source><![CDATA[
    public container getDefaultingDependencies()
    {
        return [
            [fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, InternalFieldId),
                [fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, TableName), fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, FieldName)]]
        ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>defaultField</Name>
				<Source><![CDATA[
    public void defaultField(FieldId _fieldId)
    {
        super (_fieldId);

        switch (_fieldId)
        {
            case fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, InternalFieldId):
                this.defaultInternalFieldId();
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    public boolean validateField(FieldId _fieldIdToCheck)
    {
        boolean ret = super(_fieldIdToCheck);

        switch (_fieldIdToCheck)
        {
            case fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, DocumentStatus):
                ret = this.validateDocumentStatus() && ret;
                break;
            case fieldNum(PurchaseOrderSummaryUpdateCriteriaEntity, TableName):
                ret = this.validateTableName() && ret;
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateDocumentStatus</Name>
				<Source><![CDATA[
    private boolean validateDocumentStatus()
    {
        // Only a subset of DocumentStatus is valid for use in this entity.
        switch (this.DocumentStatus)
        {
            // <GEERU>
            case DocumentStatus::Invoice4Paym_RU:
            // </GEERU>
            case DocumentStatus::PurchaseOrder:
            case DocumentStatus::ReceiptsList:
            case DocumentStatus::PackingSlip:
            case DocumentStatus::Invoice:
                return true;
            default:
                return checkFailed('@SCM:InvalidPurchaseOrderSummaryCriteriaDocumentStatus');
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateTableName</Name>
				<Source><![CDATA[
    private boolean validateTableName()
    {
        if (!tableName2Id(this.TableName))
        {
            return checkFailed('@SYS16078');
        }

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWrite</Name>
				<Source><![CDATA[
    public boolean validateWrite()
    {
        boolean ret = super();

        if (!this.InternalFieldId)
        {
            ret = checkFailed('@SCM:InvalidFieldTableName');
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
	<ConfigurationKey>LogisticsBasic</ConfigurationKey>
	<DeveloperDocumentation>@SCM:PurchaseOrderSummaryUpdateCriteriaEntityDevDoc</DeveloperDocumentation>
	<FormRef>PurchSummaryParameters</FormRef>
	<Label>@SYS57802</Label>
	<Tags>Summary parameters</Tags>
	<DataManagementEnabled>Yes</DataManagementEnabled>
	<DataManagementStagingTable>PurchaseOrderSummaryUpdateCriteriaStaging</DataManagementStagingTable>
	<EntityCategory>Parameters</EntityCategory>
	<Modules>ProcurementAndSourcing</Modules>
	<PrimaryCompanyContext>DataAreaId</PrimaryCompanyContext>
	<PrimaryKey>EntityKey</PrimaryKey>
	<SupportsSetBasedSqlOperations>Yes</SupportsSetBasedSqlOperations>
	<DeleteActions />
	<FieldGroups>
		<AxTableFieldGroup>
			<Name>AutoReport</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoLookup</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoIdentification</Name>
			<AutoPopulate>Yes</AutoPopulate>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoSummary</Name>
			<Fields />
		</AxTableFieldGroup>
		<AxTableFieldGroup>
			<Name>AutoBrowse</Name>
			<Fields />
		</AxTableFieldGroup>
	</FieldGroups>
	<Fields>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewMappedField">
			<Name>DocumentStatus</Name>
			<DataField>DocumentStatus</DataField>
			<DataSource>PurchSummaryParameters</DataSource>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewUnmappedFieldString">
			<Name>TableName</Name>
			<AllowEdit>No</AllowEdit>
			<AllowEditOnCreate>Yes</AllowEditOnCreate>
			<Mandatory>Yes</Mandatory>
			<ComputedFieldMethod>tableNameComputedColumnDefinition</ComputedFieldMethod>
			<ExtendedDataType>TableName</ExtendedDataType>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewUnmappedFieldString">
			<Name>FieldName</Name>
			<AllowEdit>No</AllowEdit>
			<AllowEditOnCreate>Yes</AllowEditOnCreate>
			<Mandatory>Yes</Mandatory>
			<ComputedFieldMethod>fieldNameComputedColumnDefinition</ComputedFieldMethod>
			<ExtendedDataType>FieldName</ExtendedDataType>
		</AxDataEntityViewField>
		<AxDataEntityViewField xmlns=""
			i:type="AxDataEntityViewMappedField">
			<Name>InternalFieldId</Name>
			<AccessModifier>Private</AccessModifier>
			<DataField>FieldId</DataField>
			<DataSource>PurchSummaryParameters</DataSource>
		</AxDataEntityViewField>
	</Fields>
	<Keys>
		<AxDataEntityViewKey>
			<Name>EntityKey</Name>
			<Fields>
				<AxDataEntityViewKeyField>
					<DataField>DocumentStatus</DataField>
				</AxDataEntityViewKeyField>
				<AxDataEntityViewKeyField>
					<DataField>TableName</DataField>
				</AxDataEntityViewKeyField>
				<AxDataEntityViewKeyField>
					<DataField>FieldName</DataField>
				</AxDataEntityViewKeyField>
			</Fields>
		</AxDataEntityViewKey>
	</Keys>
	<Mappings />
	<Ranges />
	<Relations />
	<StateMachines />
	<ViewMetadata>
		<Name>Metadata</Name>
		<SourceCode>
			<Methods>
				<Method>
					<Name>classDeclaration</Name>
					<Source><![CDATA[
[Query]
public class Metadata extends QueryRun
{
}
]]></Source>
				</Method>
			</Methods>
		</SourceCode>
		<DataSources>
			<AxQuerySimpleRootDataSource>
				<Name>PurchSummaryParameters</Name>
				<DynamicFields>Yes</DynamicFields>
				<Table>PurchSummaryParameters</Table>
				<DataSources />
				<DerivedDataSources />
				<Fields />
				<Ranges />
				<GroupBy />
				<Having />
				<OrderBy />
			</AxQuerySimpleRootDataSource>
		</DataSources>
	</ViewMetadata>
</AxDataEntityView>